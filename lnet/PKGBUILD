pkgname=lnet
pkgver=0.6.5
pkgrel=2
pkgdesc="lNet or Lightweight Networking Library is a collection of classes and components to enable event-driven TCP or UDP networking."
url="http://wiki.freepascal.org/lNet"
license=("LGPL")
arch=(i686 x86_64)
makedepends=(lazarus dos2unix)
depends=(fpc)
source=("http://downloads.sourceforge.net/lazarus-ccr/lnet-$pkgver.zip")
sha1sums=('829aef9af514aa30aa1b25c94a04fed5ec5722d6')
_unittgt=`fpc -iSP`-`fpc -iSO`
_fpcver=`fpc -iV`

prepare() {
	cd "${srcdir}/lnet/lib"
	rm lws2tcpip.pp
	rm openssl.pas
	sed -i "s,OPENSSLaddallalgorithms,OpenSSL_add_all_algorithms,g" lnetssl.pp
	find . -type f -exec dos2unix {} \;
}

build() {
  cd "${srcdir}/lnet/lib"
  for file in `ls *.pp`
  do
    fpc -O3 -Xs -XX -Isys -Sm $file
  done
}

package() {
  cd "${srcdir}/lnet/lib"
  find . -name '*.o' -o -name '*.ppu' -o -name '*.rst' -o -name '*.a'|
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/fpc/$_fpcver/units/$_unittgt/lnet/"{}
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/lnet/LICENSE"
  install -m644 LICENSE.ADDON "$pkgdir/usr/share/licenses/lnet/LICENSE.ADDON"
}
