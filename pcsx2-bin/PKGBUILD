# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=pcsx2-bin
pkgver=1.0.0
pkgrel=2
_pkgrev=r5325
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == i686 ]; then
  depends=('alsa-lib' 'bzip2' 'glew1.7' 'libjpeg-turbo'
           'nvidia-cg-toolkit' 'portaudio' 'sdl'
           'zlib' 'wxgtk' 'libgl' 'libpng' 'libtiff'
           'gtk-engines')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-bzip2' 'lib32-glew1.7' 'lib32-libjpeg-turbo'
           'lib32-nvidia-cg-toolkit' 'lib32-portaudio'
           'lib32-sdl' 'lib32-wxgtk' 'lib32-soundtouch'
           'lib32-libgl' 'lib32-libpng' 'lib32-libtiff'
           'lib32-gtk-engines')
fi
conflicts=('pcsx2-bin' 'pcsx2-svn') # Same destination folders
provides=('pcsx2')
options=('emptydirs')
install="pcsx2.install"
source=("http://pcsx2.googlecode.com/files/pcsx2-${pkgver}_${_pkgrev}.tar.gz"
        'pcsx2.sh' 'pcsx2.xpm' 'pcsx2.desktop')
md5sums=('d3e690f3c835304551df91e122d2af8f'
         'ebf35616e3b965984159d8c86d9df70b'
         '18cb3c5a243a27363553d3e1008210c0'
         'b055ff7c0f236f33702f4813ae4b980d')

package() {
  _plugindir=usr/lib/pcsx2
  [ $CARCH == x86_64 ] && _plugindir=usr/lib32/pcsx2
  
  cd pcsx2-${pkgver}
  
  install -dm755 "${pkgdir}"/opt/pcsx2 \
                 "${pkgdir}"/usr/bin \
                 "${pkgdir}"/${_plugindir} \
                 "${pkgdir}"/usr/share/applications \
                 "${pkgdir}"/usr/share/doc/pcsx2 \
                 "${pkgdir}"/usr/share/locale \
                 "${pkgdir}"/usr/share/man/man1 \
                 "${pkgdir}"/usr/share/pcsx2 \
                 "${pkgdir}"/usr/share/pcsx2/bios \
                 "${pkgdir}"/usr/share/pcsx2/cheats \
                 "${pkgdir}"/usr/share/pcsx2/help \
                 "${pkgdir}"/usr/share/pixmaps
  
  # install data files
  install GameIndex.dbf "${pkgdir}"/usr/share/pcsx2
  install cheats/*.pnach "${pkgdir}"/usr/share/pcsx2/cheats
  install plugins/*.glsl "${pkgdir}"/usr/share/pcsx2
  
  # install plugins
  install plugins/*.so "${pkgdir}"/${_plugindir}/
  rm "${pkgdir}"/${_plugindir}/{libPADnull,libSPU2null,libGSnull}.so
  install plugins/ps2hw.dat "${pkgdir}"/${_plugindir}/
  cd plugins
  for i in $(ls *.glsl); do
    ln -s /usr/share/pcsx2/$i "${pkgdir}"/${_plugindir}/
  done
  cd ..
  
  # install locales (translations)
  cd Langs
  find . -type f -exec install -Dm644 '{}' "${pkgdir}"/usr/share/locale/'{}' \;
  rm "${pkgdir}"/usr/share/locale/*/*.mo
  cd ..
  
  # install man page
  install -m644 docs/pcsx2.1 "${pkgdir}"/usr/share/man/man1/
  
  # install docs
  install -m644 docs/PCSX2_FAQ_1.0.0.pdf "${pkgdir}"/usr/share/doc/pcsx2
  install -m644 docs/PCSX2_Readme_1.0.0.pdf "${pkgdir}"/usr/share/doc/pcsx2
  
  # install other stuff
  install -m755 "${srcdir}"/pcsx2.sh  "${pkgdir}"/usr/bin/pcsx2
  install -m644 "${srcdir}"/pcsx2.xpm "${pkgdir}"/usr/share/pixmaps/
  install -m644 "${srcdir}"/pcsx2.desktop "${pkgdir}"/usr/share/applications/
  
  # And finally, install pcsx2's opt folder
  install -m755 pcsx2 "${pkgdir}"/opt/pcsx2/
  install -m755 launch_pcsx2_linux.sh "${pkgdir}"/opt/pcsx2/
  ln -s /usr/share/pcsx2/GameIndex.dbf "${pkgdir}"/opt/pcsx2/
  ln -s /usr/share/doc/pcsx2 "${pkgdir}"/opt/pcsx2/docs
  
  # Allow to pass arguments to pcsx2 binary
  sed -i 's_./pcsx2_./pcsx2 $@_' "${pkgdir}"/opt/pcsx2/launch_pcsx2_linux.sh
}
