# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=portaudio-svn
pkgver=1828
pkgrel=1
pkgdesc="PortAudio is a free, cross platform, open-source, audio I/O library."
arch=('i686' 'x86_64')
url="http://www.portaudio.com"
license="custom"
depends=('jack')
makedepends=('subversion')
conflicts=('portaudio')
provides=('portaudio')
options=('!libtool')

_svntrunk=https://subversion.assembla.com/svn/portaudio/portaudio/trunk
_svnmod=portaudio

build() {
  msg "Connecting to SVN server...."

  if [[ -d "${_svnmod}/.svn" ]]; then
    (cd "${_svnmod}" && svn up -r "${pkgver}")
  else
    svn co "${_svntrunk}" --config-dir ./ -r "${pkgver}" "${_svnmod}"
  fi
  
  msg "SVN checkout done or server timeout"  
  msg "Starting cmake and build..."

  rm -rf "${srcdir}/${_svnmod}-build"
  svn export --quiet "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"
    
  ./configure --prefix=/usr --with-alsa --with-jack
  make
}

package() {
  cd "${_svnmod}-build"
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
