# Contributor: Paul Ezvan <paul@ezvan.fr>
# Maintainer: Alexsandr Pavlov <kidoz at mail dot ru>

pkgname=pnp4nagios
pkgver=0.6.16
pkgrel=1
pkgdesc="Addon to Nagios which analyzes performance data provided by plugins and stores them automatically into RRD-databases"
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.pnp4nagios.org/"
depends=('nagios' 'perl' 'rrdtool' 'php' 'php-gd' 'ttf-liberation' 'libtool')
source=("http://downloads.sourceforge.net/pnp4nagios/$pkgname-$pkgver.tar.gz")
backup=('etc/httpd/conf/extra/pnp4nagios.conf' 'etc/pnp/config.php')
md5sums=('53afa0e62eb35575c16a09081be382fa')

	_nagios_user="nagios"
	_nagios_group="nagios"
	_instdir="usr/share/pnp"
	_bindir="usr/bin"
	_vardir="var/nagios"
	_confdir="etc/pnp"
	_httpdconfdir="etc/httpd/conf/extra"
	_perfdatadir="var/nagios/spool/perfdata/pnp"
	_libexecdir="usr/lib/pnp"

build() {
	cd "$srcdir/$pkgname-$pkgver"

	getent group $_nagios_group || _nagios_group=30
	getent passwd $_nagios_user || _nagios_user=30

	./configure \
		--with-nagios-user=$_nagios_user \
		--with-nagios-group=$_nagios_group \
		--prefix="/$_instdir" \
		--bindir="/$_bindir" \
		--localstatedir="/$_vardir" \
		--sysconfdir="/$_confdir" \
		--with-httpd-conf="/$_httpdconfdir" \
		--with-perfdata-dir=/$_perfdatadir \
		--libexecdir=/$_libexecdir

	make all
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	
	make \
		prefix=$pkgdir/$_instdir \
		BINDIR=$pkgdir/$_bindir \
		LOGDIR=$pkgdir/$_vardir \
		CFGDIR=$pkgdir/$_confdir \
		SYSCONFDIR=$pkgdir/$_confdir \
		HTTPD_CONF=$pkgdir/$_httpdconfdir \
		CHECKRESULTDIR=$pkgdir/$_checkresultdir \
		PERFDATADIR=$pkgdir/$_perfdatadir \
		PERFDATA_DIR=$pkgdir/$_perfdatadir \
		LIBEXECDIR=$pkgdir/$_libexecdir \
		install install-config

install -D -m644 ${srcdir}/$pkgname-$pkgver/sample-config/httpd.conf ${pkgdir}/etc/httpd/conf/extra/pnp4nagios.conf
}

