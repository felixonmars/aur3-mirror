# Maintainer:  Elder Marco <eldermarco at gmail dot com>

pkgname=gpaste
pkgver=2.9.1
pkgrel=1
pkgdesc="Clipboard management system with a gnome-shell extension"

license=('GPL3')
url="https://github.com/Keruspe/GPaste"
source=("https://github.com/downloads/Keruspe/GPaste/$pkgname-$pkgver.tar.xz" "gpaste-gir.patch")

makedepends=('dbus-core' 'libxml2' 'libxcb' 'xcb-util-keysyms' 'gtk3' 'vala' 'intltool')
options=(!libtool)

depends=('dbus-core' 'libxml2' 'libxcb' 'xcb-util-keysyms' 'gtk3' 'gnome-shell>=3.4.0')
install=$pkgname.install

arch=('i686' 'x86_64')
md5sums=('57450a9298cb410d1dcb471c8aa73538' '3459fe6d78bd1b1e709c1574043263df')

build () {
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i ../gpaste-gir.patch

    ./configure --prefix=/usr           \
                --sysconfdir=/etc       \
                --libexecdir=/usr/lib   \
                --enable-vala           \
                --disable-static        \
                --disable-applet        \
                --disable-schemas-compile
    make
}

package () {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    # bash/zsh completion support
    install -Dp -m0644 data/completions/$pkgname     \
        "$pkgdir/etc/bash_completion.d/$pkgname"
    install -Dp -m0644 data/completions/_$pkgname    \
        "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
# expandtab:tabstop=4:shiftwidth=4
