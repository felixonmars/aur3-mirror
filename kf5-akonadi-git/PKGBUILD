# Contributor: ant32 <antreimer@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kf5-akonadi-git
pkgver=v1.11.0.r79.g48f56b9
pkgrel=1
arch=('i686' 'x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
license=('LGPL')
depends=('qt5-base' 'shared-mime-info' 'boost-libs' 'mariadb' 'libxslt')
makedepends=('cmake' 'boost' 'postgresql' 'git')
optdepends=('postgresql: PostgreSQL backend')
install=akonadi.install
conflicts=('kf5-akonadi')
provides=('kf5-akonadi')
source=("git://anongit.kde.org/akonadi.git")
md5sums=('SKIP')

pkgver() {
  cd akonadi
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../akonadi \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DAKONADI_BUILD_TESTS=OFF \
    -DQT5_BUILD=ON \
    -DWITH_SOPRANO=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
