# $Id$
# Maintainer: Jan Cholasta <grubber grubber cz>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: scj <scj archlinux us>

pkgname=sfml1.6
pkgver=1.6
pkgrel=1
pkgdesc="A simple, fast, cross-platform, and object-oriented multimedia API (version 1.6)"
arch=('i686' 'x86_64')
url="http://www.sfml-dev.org/"
license=('zlib')
depends=('libxrandr' 'libsndfile' 'openal' 'glew' 'libjpeg' 'libpng14' 'freetype2')
source=(http://downloads.sourceforge.net/sfml/SFML-${pkgver}-sdk-linux-64.tar.gz
        compilation-fix.patch
        use-system-libs.patch)
md5sums=('7a9b3a1ef6d14cd25090e440ccdbb3a8'
         '209b757927a297f7a7405b3bc7a03281'
         'e8a37b1c53ab084cfac69faa2fc02455')

build() {
  cd "${srcdir}/SFML-${pkgver}"

  patch -Np1 < ../compilation-fix.patch

  # apply patch to use system libs in favor of included ones (fixes many problems)
  patch -Np1 < ../use-system-libs.patch

  # compile sfml
  make

  # prepare samples
  sed -e '/export LDFLAGS/d' -i samples/Makefile

  #check optional dependencies
  if [ ! -e "/usr/bin/wx-config" ]; then
    sed -e 's/wxwidgets-sample //' -i samples/Makefile
  fi 
  if [ ! -e "/usr/include/QtGui" ]; then
    sed -e 's/qt-sample //' -i samples/Makefile
  fi

  # fix some samples
  sed -e 's|qt4/||g' -i samples/qt/Makefile
  sed -e '/#include <iostream>/a\#include <stdlib.h>' -i \
    samples/sockets/Sockets.cpp \
    samples/voip/VoIP.cpp

  # fix the library softlinks for samples
  cd lib
  for lib in *; do
      ln -sf $lib ${lib/.${pkgver}/}
  done
  cd ..
}

package() {
  cd "${srcdir}/SFML-${pkgver}"

  # prepare some dirs
  mkdir -p ${pkgdir}/usr/lib \
           ${pkgdir}/usr/include/sfml1.6 \
           ${pkgdir}/usr/share/sfml1.6 \
           ${pkgdir}/usr/share/doc

  # install it
  sed '/export DESTDIR/d' -i src/SFML/Makefile
  make DESTDIR="${pkgdir}/usr" DESTINCDIR="${pkgdir}/usr/include/sfml1.6" install

  # fix the library softlinks - again
  cd ${pkgdir}/usr/lib
  rm *.so
  for lib in *; do
      ln -s $lib ${lib/.${pkgver}/}
  done
  cd "${srcdir}/SFML-${pkgver}"

  # install docs
  cp -r doc ${pkgdir}/usr/share/doc/sfml1.6

  # handy symlinks
  cd ${pkgdir}/usr/share/sfml1.6
  ln -s ../doc/sfml1.6 docs

  # install license
  install -Dm 644 ${srcdir}/SFML-${pkgver}/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
