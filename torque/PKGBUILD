# Maintainer: flub <flub123@gmx.de>

pkgname=torque
pkgver=4.0.0
pkgrel=1
pkgdesc='An open source resource manager providing control over batch jobs and distributed compute nodes.'
url='http://www.clusterresources.com/products/torque-resource-manager.php'
arch=('i686' 'x86_64')
license=('GPL')
depends=('openssh')
makedepends=('make' 'gcc')
optdepends=()
install=torque.install
options=(!libtool)
backup=(var/spool/torque/server_name var/spool/torque/mom_priv/config var/spool/torque/serv_priv/{nodes,serverdb})
source=("http://www.adaptivecomputing.com/resources/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz" "torque-server" "torque-node" "torque-scheduler")
md5sums=('f714d325221cd493508dedaaa86711e5'
         '41581ea0fba02960ec7c128432f8f6ec'
         'dfe8aa183dd9ec5dfcc2ba6a5f234053'
         'e17b16f4898f1bfbbc4a6c208973889e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --with-default-server=localhost \
              --with-server-home=/var/spool/torque \
  				    --with-rcp=scp \
  				    --prefix=/usr \
  				    --disable-gcc-warnings \
  				    --mandir=/usr/share/man || return 1
  mv src/resmom/Makefile src/resmom/Makefile.old
  cat src/resmom/Makefile.old | sed 's/$(MOMLIBS) $(PBS_LIBS)/$(PBS_LIBS) $(MOMLIBS)/g' > src/resmom/Makefile
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install || return 1
  mkdir -p ${pkgdir}/etc/rc.d/
  cp ${srcdir}/torque-server ${pkgdir}/etc/rc.d/
  cp ${srcdir}/torque-node ${pkgdir}/etc/rc.d/ 
  cp ${srcdir}/torque-scheduler ${pkgdir}/etc/rc.d/
}

# vim:set ts=2 sw=2 et:
