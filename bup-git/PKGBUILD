# Maintainer: Michael Witten <mfwitten>

# The various phases of the build can be controlled
# with the following variables (use an empty value
# to mean false; if you're hacking on the source,
# I RECOMMEND skipping the _clean phase):

_source=yes
_clean=yes
_config=yes
_compile=yes
_test=yes
_install=yes
#_install=          # do not install

######################################################################

pkgname=bup-git
pkgver=20120617
pkgrel=1
pkgdesc="Backup tool (built from git repo); it handles many very large files"

arch=(i686 x86_64)
url=https://github.com/apenwarr/bup
license=(GPL)

provides=(bup)
conflicts=(bup)

depends=(

  git
  python2
  python-fuse
  python2-pyxattr
  pylibacl

  par2cmdline

)

makedepends=(

  #pandoc Currrently the pre-generated docs are used.

)

options=('!strip')

_gitroot=https://github.com/apenwarr/bup.git
_gitname=repo

build()
{
  cd "$srcdir"

  #### Source ####

    if [[ ! $_source ]]; then

      cd "$_gitname"

    else

      msg "Connecting to server...."

      if [[ -d $_gitname ]]; then

        cd "$_gitname"
        git pull origin
        msg "The local files are updated."

      else

        git clone "$_gitroot" "$_gitname"
        cd "$_gitname"

      fi

      msg "Checkout done or server timeout"

    fi

  # At this point, the current working directory
  # should be "$srcdir/$_gitname"

  #### Clean ####

    if [[ $_clean ]]; then

      msg "Cleaning ..."
      git reset --hard HEAD
      git clean -fdx

    fi

  #### Configure ####

    if [[ $_config ]]; then

      msg "Configuring ..."

      find . -type f -exec sed -i -e '1s/env python\b/env python2/' {} +

      sed -i -e 's/acLookFor python\b/acLookFor python2/' \
             -e 's/MF_PATH_INCLUDE PYTHON python\b/MF_PATH_INCLUDE PYTHON python2/' \
             config/configure

      sed -i -e 's/PYTHON=python\b/PYTHON=python2/' -e '/docs-available/d' Makefile

      sed -i -e 's/python -c/python2 -c/' t/test-meta.sh

    fi

  #### Compile ####

    if [[ $_compile ]]; then
    
      msg "Compiling ..."
      make all import-docs

    fi

  #### Test ####

    if [[ $_test ]]; then
    
      msg "Testing ..."
      make test

    fi
}

package()
{
  cd "$srcdir/$_gitname"

  #### Install  ####

    if [[ $_install ]]; then
    
      msg "Installing ..."
      make DESTDIR="$pkgdir" install

    fi
}
