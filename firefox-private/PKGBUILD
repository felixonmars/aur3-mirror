# Maintainer: Firef0x < Firefgx { at } gmail { dot } com>
# Contributor: Firef0x < Firefgx { at } gmail { dot } com>
# Contributor: Kyro <chris-bos-lee@hotmail.com>

pkgname=firefox-private
pkgver=25.0
pkgrel=1
pkgdesc='Standalone web browser from mozilla.org, built with gcc -O4 SSE2, betterpgo, support for media-libs/gstreamer, and added a MOZILLA_DISABLE_PLUGINS environment variable to firefox.'
url='https://code.google.com/p/cppinfo/downloads/list'
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2'
         'dbus-glib'
         'desktop-file-utils'
         'libxt'
         'mime-types'
         'nss'
         'shared-mime-info')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'gstreamer0.10-base-plugins: vorbis decoding, ogg demuxing'
            'gstreamer0.10-bad-plugins: aac, vp8 and opus decoding'
            'gstreamer0.10-good-plugins: webm and mp4 demuxing'
            'gstreamer0.10-ugly-plugins: h.264 decoding'
            'libpulse: PulseAudio audio driver')
provides=('firefox=25.0' 'firefox-i18n-zh-cn=25.0')
conflicts=('firefox' 'firefox-private-zh-cn')
source=("https://cppinfo.googlecode.com/files/firefox_${pkgver}.zh_CN.linux_x86_64.tar.gz"
        "vendor.js"
        "16.png" "22.png" "24.png" "32.png" "48.png" "64.png" "128.png" "192.png" "256.png" "384.png"
        )
sha1sums=('dad442c11461724f1a32b7a326d3844d12db3ac3'
          'eb5d1640f5312ad01661c4406b196fdc40cfefc3'
          '077c7737036af790eb9ee80102d082facf299606'
          'b74f9a7b72cf6295ebf7dd08734c6797500290aa'
          '345f4b9a9618ff3221f5c725073da1467057ea41'
          '97b320865dbe365139b1ece3c2c01b673ecacff6'
          '6209703d3cd18aede02bd945695095b2a5699c89'
          'a293ac493192e4a93d5360086785b0402f09c385'
          '2270a28c1d8f4aaf865cdd9bfa029a3db8f2ce51'
          '3e314c006819e0553841e0ebffb097e1632d278f'
          'e387bc1179f0551ca9ce308778c51e6632dd7dec'
          'acc9dd95a755d0ecf0f4854b708d6ac92f4f1ceb')

install=${pkgname}.install

changelog=readme.txt

_keep_searchplugins=0

package(){
    cd "${srcdir}"/firefox"${pkgver}"_setup/

    ar x firefox-private-"${pkgver}".zh-CN.linux-"$CARCH".deb

    tar xzf data.tar.gz -C "${pkgdir}"/

    # Clean up Debian files and unwanted plugins
    rm -rf "${pkgdir}/etc"
    rm -rf "${pkgdir}/usr/share/doc"
    rm -rf "${pkgdir}/usr/share/lintian"
    rm -rf "${pkgdir}/usr/share/pixmaps"
    rm -rf "${pkgdir}/usr/lib/firefox-addons/plugins/libaliedit64.so"
    rm -rf "${pkgdir}/usr/lib/firefox-addons/plugins/libflashplayer.so"

    # Install missing resource files
    install -d -m755 "${pkgdir}/usr/lib/firefox/browser/defaults/preferences"
    install -Dm644 "${srcdir}/vendor.js" "${pkgdir}/usr/lib/firefox/browser/defaults/preferences/vendor.js"

    install -d -m755 ${pkgdir}/usr/share/icons/hicolor/{16x16,22x22,24x24,32x32,48x48,64x64,128x128,192x192,256x256,384x384}/apps
    for i in 16 22 24 32 48 64 128 192 256 384; do
        install -Dm644 "${srcdir}/$i.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
    done

    # Use system-provided dictionaries
    ln -s /usr/share/hunspell "${pkgdir}/usr/lib/firefox/dictionaries"
    ln -s /usr/share/hyphen "${pkgdir}/usr/lib/firefox/hyphenation"

    # Move firefox-addons back into firefox/browser
    rm -rf "${pkgdir}/usr/lib/firefox/browser/extensions"
    rm -rf "${pkgdir}/usr/lib/firefox/browser/plugins"
    rm -rf "${pkgdir}/usr/lib/firefox/browser/searchplugins"
    mv "${pkgdir}"/usr/lib/firefox-addons/{extensions,searchplugins} "${pkgdir}/usr/lib/firefox/browser/"
    rm -rf "${pkgdir}/usr/lib/firefox-addons"
    if [ ${_keep_searchplugins} -eq 0 ]; then
        rm -f "${pkgdir}"/usr/lib/firefox/browser/searchplugins/*
    fi

    # Workaround for bug #658850
    # https://bugzilla.mozilla.org/show_bug.cgi?id=658850
    ln -sf /usr/lib/firefox/firefox "${pkgdir}"/usr/lib/firefox/firefox-bin
}

# vim:set ts=4 sw=4 et:
