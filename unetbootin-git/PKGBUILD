# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>
# Current i686 Maintainer: ifaigios <ifaigios_at_gmail_dot_com>

pkgname=unetbootin-git
_realname=unetbootin
pkgver=20130304
pkgrel=1
pkgdesc="Lets you create bootable Live USB drives"
url="http://unetbootin.sourceforge.net/"
license=('GPL')
arch=('i686' 'x86_64')
provides=('unetbootin')
conflicts=('unetbootin')
depends=('syslinux' 'p7zip' 'qt4' 'mtools' 'gtk-update-icon-cache')
makedepends=('git')
optdepends=('gksu: for graphically requesting the root password'
            'kdebase-runtime: for graphically requesting the root password')
install="${_realname}.install"

_gitroot=git://${_realname}.git.sourceforge.net/gitroot/${_realname}/${_realname}
_gitname=${_realname}

build() {
  # GIT stuff
  cd ${srcdir}

  if [[ -d ${_gitname} ]]; then
    cd ${_gitname} && git pull origin 
    cd ..
  else
    git clone --depth 1 ${_gitroot}
  fi

  rm -rf ${_gitname}build
  cp -a ${_gitname}/src/${_realname} ${_gitname}-build
  cd ${_gitname}-build

  # Actual build
  sed -i '/^RESOURCES/d' ${_realname}.pro
  lupdate ${_realname}.pro
  lrelease ${_realname}.pro
  qmake-qt4 "DEFINES += NOSTATIC" "RESOURCES -= ${_realname}.qrc"
  make
}

package() {
  cd ${srcdir}/${_gitname}-build

  install -Dm755 ${_realname} ${pkgdir}/usr/bin/${_realname}
  install -dm755 ${pkgdir}/usr/share/${_realname}
  install -m644 ${_realname}_*.qm ${pkgdir}/usr/share/${_realname}/
  install -Dm644 ${_realname}.desktop ${pkgdir}/usr/share/applications/${_realname}.desktop

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -D ${_realname}_${i/x*/}.png ${pkgdir}/usr/share/icons/hicolor/${i}/apps/${_realname}.png
  done
}
