pkgname=telepathy-kde-mission-control
_pkgname=telepathy-mission-control
pkgver=5.12.1
pkgrel=1
pkgdesc="A Telepathy component providing abstraction of some of the details of connection managers. for KDE (without Gnome Dependencies)"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/Mission Control"
license=('LGPL2.1')
depends=('telepathy-glib' 'dconf')
makedepends=('libxslt' 'python2')
conflicts=('telepathy-mission-control')
replaces=('telepathy-mission-control')
provides=('telepathy-mission-control')
install=telepathy-kde-mission-control.install
options=('!libtool')
source=(http://telepathy.freedesktop.org/releases/$_pkgname/$_pkgname-$pkgver.tar.gz)
md5sums=('79449fd2e1b02fc6cb5c6adc447e2bb6')

build() {
    rm -rf "${srcdir}"/python
    mkdir "${srcdir}"/python
    ln -s /usr/bin/python2 "${srcdir}"/python/python
    export PATH="${srcdir}"/python:$PATH
    cd "$_pkgname-$pkgver"
    ./configure --prefix=/usr --libexecdir=/usr/lib/telepathy --disable-schemas-compile --disable-gnome-keyring
    make
}

package() {
    cd "$_pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}