# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=auralquiz
pkgver=0.8.1
pkgrel=1
pkgdesc="a simple music quiz game, using your own music files."
arch=(i686 x86_64)
url="http://jancoding.wordpress.com/auralquiz/"
license=('GPL')
depends=('phonon' 'taglib')
install="$pkgname.install"
source=("http://qt-apps.org/CONTENT/content-files/139127-$pkgname-v$pkgver-src.tar.gz")
md5sums=('0eee47ac30728dd616fb9991f52996d6')

build() {
  cd "$srcdir/$pkgname-v$pkgver-src"

  # locale path fix
  sed -i 's|:/translations|/usr/share/locale|' src/main.cpp

  [ -d bld ] || mkdir bld && cd bld
  qmake ..
  make
}

package() {
  cd "$srcdir/$pkgname-v$pkgver-src"

  # bin
  install -Dm755 bld/$pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # man
  install -Dm644 manual/$pkgname.6 \
    "$pkgdir/usr/share/man/man6/$pkgname.6"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 icon/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # locales
  install -d "$pkgdir/usr/share/locale"
  install -Dm644 translations/*.qm \
    "$pkgdir/usr/share/locale"
}

# vim:set ts=2 sw=2 et:
