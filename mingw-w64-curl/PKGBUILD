pkgname=mingw-w64-curl
pkgver=7.33.0
pkgrel=2
pkgdesc="An URL retrival utility and library. (mingw-w64)"
arch=(any)
url="http://curl.haxx.se"
license=("MIT")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-gnutls
mingw-w64-libidn
mingw-w64-libssh2
mingw-w64-openssl
mingw-w64-zlib)
options=(staticlibs !strip !buildflags)
source=("$url/download/curl-$pkgver.tar.bz2")
md5sums=('57409d6bf0bd97053b8378dbe0cadcef')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --without-ca-bundle \
      --without-random
    make
  done
}

package() {
	mkdir -p "$pkgdir/usr/bin"
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    ln -s "/usr/${_arch}/bin/curl-config" "$pkgdir/usr/bin/${_arch}-curl-config"
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
