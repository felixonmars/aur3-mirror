# Maintainer: Ivan Puntiy <ivan.puntiy-at-gmail.com>
pkgname=unix-runescape-client
pkgver=3.9.2
pkgrel=2
pkgdesc="Runescape Client for Linux and Unix"
arch=(any)
url="http://hkprojects.weebly.com/runescape-client-for-linux-and-unix.html"
license=(GPL2)
depends=('java-runtime' 'perl' 'xterm' 'p7zip')
optdepends=('wine: for compatibility mode' 'perl-wx: for rsu-settings')
conflicts=('runescape-client-bin')
install=$pkgname.install
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/HikariKnight/rsu-client/tarball/v$pkgver"
  'runescape-beta.desktop'
)
md5sums=(
  '57a97e89543c7a72023b2e8cadf1c1de'
  '82fa354ae0e2e1d505453ed5bf3422f5'
)

package() {
  local _instdir="$pkgdir"/opt/runescape
  
  cd "$srcdir/HikariKnight-rsu-client-61f9aad/runescape"

  mkdir -p "$_instdir"/{share,modules,win32} "$pkgdir"/usr/{bin,share/applications}

  # copy the stuff
  cp -t "$_instdir" *.txt runescape update-runescape-client rsu-settings
  cp -R share "$_instdir"/
  cp -R modules "$_instdir"/
  cp -R templates/packaging/usr/share "$pkgdir"/usr/
  cp -R win32/jawt "$_instdir"/win32/
  cp "$srcdir"/runescape-beta.desktop "$pkgdir"/usr/share/applications/
 
  # symlink executables
  ln -s ../../opt/runescape/runescape "$pkgdir"/usr/bin/runescape
  ln -s ../../opt/runescape/update-runescape-client "$pkgdir"/usr/bin/update-runescape-client
  ln -s ../../opt/runescape/rsu-settings "$pkgdir"/usr/bin/rsu-settings
}

# vim:set ts=2 sw=2 et:

