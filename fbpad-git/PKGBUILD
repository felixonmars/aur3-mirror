# Maintainer: Sara <sara at archlinux dot us>

pkgname=fbpad-git
pkgver=20120502
pkgrel=1
pkgdesc="A small linux framebuffer virtual terminal, with 256 colors"
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/fbpad.git"
license=('GPL')
optdepends=('ft2tf: TTF to tiny font converter')
makedepends=('git' 'ncurses')
provides=('fbpad')
conflicts=('fbpad')
install=${pkgname}.install
source=('config.h'
        'fbpad-256.info')
md5sums=('af2409e009be1359f67ca80294362174'
         '185b9d6ec1c539213226a3e2509c7ccd')

_gitroot=git://repo.or.cz/fbpad.git
_gitname=fbpad

build(){
  cd "${srcdir}"
  msg "Connecting to GIT server...."
    
  if [[ -d ${_gitname} ]]; then
    cd "${_gitname}"
    git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" -b bold
    cd "${_gitname}"
    git pull --no-edit "${_gitroot}" font:origin
  fi
  msg "Checkout done or server timeout"
  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  cp "${srcdir}"/config.h config.h

  msg "Starting make..."
  make
}

package(){
  install -Dm755 "${srcdir}/${_gitname}-build/${_gitname}" "${pkgdir}/usr/bin/${_gitname}"
  install -Dm644 "${srcdir}/${_gitname}-build/README" "${pkgdir}/usr/share/doc/${_gitname}/README"
  # Compile terminfo
  install -dm755 "${pkgdir}/usr/share/terminfo"
  tic -x -o "${pkgdir}/usr/share/terminfo" "${_gitname}-256.info" 
}
 
# vim:set ts=2 sw=2 et:
