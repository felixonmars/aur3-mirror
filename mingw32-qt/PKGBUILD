# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Alexander 'hatred' Drozdov <adrozdoff AT gmail.com>

pkgname=mingw32-qt
pkgver=4.7.1
pkgrel=1
pkgdesc="The Qt gui toolkit (mingw32)."
arch=('any')
url="http://qt.nokia.com/"
license=('LGPL')
groups=()
depends=('mingw32-gcc' 'qt')
makedepends=('p7zip' 'perl' 'sed')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=

source=(http://get.qt.nokia.com/qt/source/qt-win-opensource-$pkgver-mingw.exe
        qmake.conf
        i486-mingw32-qmake)

build()
{
  cd "${srcdir}"

  msg "Extracting data from windows installer..."
  7z x -y "${srcdir}/qt-win-opensource-${pkgver}-mingw.exe" '$OUTDIR'

  msg "Copying the contents..."
  install -d "${pkgdir}/usr/i486-mingw32/bin"
  mv '$OUTDIR/bin'/{include,lib,src} "${pkgdir}/usr/i486-mingw32/"
  mv '$OUTDIR/bin/bin'/{Qt*.dll,phonon*.dll,syncqt,elf2e32_qtwrapper} "${pkgdir}/usr/i486-mingw32/bin/"
  install -d "${pkgdir}/usr/i486-mingw32/bin/qt-plugins"
  mv '$OUTDIR/bin/plugins'/* "${pkgdir}/usr/i486-mingw32/bin/qt-plugins/"
  install -d "${pkgdir}/usr/i486-mingw32/share/mkspecs"
  mv '$OUTDIR/bin/mkspecs'/* "${pkgdir}/usr/i486-mingw32/share/mkspecs/"

  # copy *.dll from mingw in to bin/qt-bin
  install -d "${pkgdir}/usr/i486-mingw32/bin/qt-bin"
  mv '$OUTDIR/bin/bin'/{libgcc_s_dw2-1.dll,mingwm10.dll} "${pkgdir}/usr/i486-mingw32/bin/qt-bin/"

  msg "Generating the headers..."
  export QTDIR="${pkgdir}/usr/i486-mingw32"
  perl "${QTDIR}/bin/syncqt"

  msg "Removing the debug libraries..."
  find "${pkgdir}/usr/i486-mingw32/bin" -name '*.dll' | while read lib; do
    debug_lib="`echo -n "${lib}" | sed -r 's/(4?)\.dll$/d\1.dll/'`"
    test -f "${debug_lib}" && rm -fv "${debug_lib}" || true
  done
  find "${pkgdir}/usr/i486-mingw32/lib" -name '*.a' | while read lib; do
    debug_lib="`echo -n "${lib}" | sed -r 's/(4?)\.a$/d\1.a/'`"
    test -f "${debug_lib}" && rm -fv "${debug_lib}" || true
  done
  find "${pkgdir}/usr/i486-mingw32/lib" -name '*.prl' | while read lib; do
    debug_prl="`echo -n "${lib}" | sed -r 's/(4?)\.prl$/d\1.prl/'`"
    test -f "${debug_prl}" && rm -fv "${debug_prl}" || true
  done

  find "${pkgdir}/usr/i486-mingw32" -type f -exec chmod 644 {} \;
  find "${pkgdir}/usr/i486-mingw32" -type d -exec chmod 755 {} \;

  install -D -m644 ${srcdir}/qmake.conf ${pkgdir}/usr/share/qt/mkspecs/win32-x-g++/qmake.conf
  install -D -m755 ${srcdir}/i486-mingw32-qmake ${pkgdir}/usr/bin/i486-mingw32-qmake
}
md5sums=('2fdcb64bbfe78370c3b0a9e4fc3de026'
         '7e2477edf89b22d05130c6d34076ce3f'
         '5686d930bfc73c5d996389b520f27a15')
