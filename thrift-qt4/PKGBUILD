# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Byron Clark <byron@theclarkfamily.name>
pkgname=thrift-qt4
pkgver=0.9.0
pkgrel=2
pkgdesc="C++ bindings (with support for Qt) for Thrift, a scalable cross-language services framework for IPC/RPC"
arch=(i686 x86_64)
url="http://thrift.apache.org/"
license=(APACHE)
conflicts=(thrift-cpp)
provides=(thrift-cpp)
depends=(boost-libs qt4 zlib libevent openssl)
makedepends=(boost automake autoconf libtool)
optdepends=(
  'thrift-base: for the Thrift compiler'
  'vim-thrift: for syntax highlighting in vim')
options=(!emptydirs !makeflags)
source=(http://www.apache.org/dist/thrift/$pkgver/thrift-$pkgver.tar.gz)
md5sums=('beb2c8290e97c93e3b2844f558cc5c7d')

build() {
  cd $srcdir/thrift-$pkgver

  ./configure --prefix=/usr \
    --with-cpp \
    --with-qt4 \
    --without-c_glib \
    --without-csharp \
    --without-java \
    --without-erlang \
    --without-perl \
    --without-php \
    --without-php_extension \
    --without-python \
    --without-ruby \
    --without-haskell \
    --without-go \
    --without-d

  make
}

package() {
  cd $srcdir/thrift-$pkgver
  make DESTDIR=$pkgdir install
  rm -r $pkgdir/usr/bin
}

# vim:set ts=2 sw=2 et:
