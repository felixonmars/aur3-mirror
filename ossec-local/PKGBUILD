# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ossec-local
pkgver=2.7
pkgrel=1
pkgdesc="Open Source Host-based Intrusion Detection System"
arch=('i686' 'x86_64')
url="http://www.ossec.net/"
license=('GPL3')
depends=('openssl')
makedepends=('dnsutils')
backup=('var/ossec/etc/ossec.conf')
install=ossec.install
options=('emptydirs')
source=(http://www.ossec.net/files/ossec-hids-$pkgver.tar.gz \
        ossec.rc config)
md5sums=('71cd21a20f22b8eafffa3b57250f0a70'
         'c5d59932a40c8782867627f29f99ace4'
         '510463fc82537b59a45fe55993e6d712')

_instdir=/var/ossec

build() {
  cd "$srcdir/ossec-hids-$pkgver"

  export USER_NO_STOP=yes
  export USER_LANGUAGE=en
  export USER_INSTALL_TYPE=local
  export USER_DIR=$_instdir

  . "$srcdir/config" # load configuration

  # fix placement of ossec-init.conf
  mkdir -p $pkgdir/etc
  sed -i "s|^OSSEC_INIT.*|OSSEC_INIT=\"$pkgdir/etc/ossec-init.conf\"|" src/init/shared.sh
  # change the install location
  sed -i "s|^DIR=.*|DIR=$pkgdir/$_instdir|" src/InstallServer.sh

  # change user names to existing users to make sure the installation works
  # the users are later changed to id's for which appropriate users are created by the ossec.install
  sed -i -e 's|^USER=.*|USER=nobody|' -e 's|^USER_MAIL=.*|USER_MAIL=mail|' \
    -e 's|^USER_REM=.*|USER_REM=daemon|' src/InstallServer.sh
  # change group name to nobody, group is created by ossec.install
  # this ensures install will work perfectly, the we will change the group later
  sed -i 's|^GROUP=.*|GROUP=nobody|' src/InstallServer.sh

  ./install.sh

  # install rc initscript
  install -D -m755 "$srcdir"/ossec.rc "$pkgdir"/etc/rc.d/ossec

  # change the users
  find "$pkgdir" -user nobody -exec chown 524 '{}' ';'
  find "$pkgdir" -user mail -exec chown 525 '{}' ';'
  find "$pkgdir" -user daemon -exec chown 526 '{}' ';'
  # change the groups
  find "$pkgdir" -group nobody -exec chgrp 525 '{}' ';'
}
