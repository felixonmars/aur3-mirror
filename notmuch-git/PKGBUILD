# Maintainer: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Olivier Ramonat <olivier at ramonat dot fr>
# Contributor: Richard Murri <admin@richardmurri.com>

pkgname=notmuch-git
pkgver=20111207
pkgrel=1
pkgdesc="Notmuch is not much of an email program"
arch=('i686' 'x86_64')
url="http://notmuchmail.org/"
license=('GPL3')
depends=('xapian-core' 'gmime')
makedepends=('git' 'talloc' 'python2')
provides=('notmuch')
conflicts=('notmuch')
optdepends=('emacs: for using the emacs interface'
            'vim: for using the vim interface'
            'python2: for using the python bindings'
            'gnupg: for email encryption')

_gitroot="git://notmuchmail.org/git/notmuch"
_gitname="notmuch"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./configure --prefix=/usr --sysconfdir=/etc --zshcompletiondir=/usr/share/zsh/site-functions
  make
}

package(){
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" LIBDIR_IN_LDCONFIG=0 install

  install -D notmuch $pkgdir/usr/sbin/notmuch

  mkdir -p $pkgdir/usr/share/vim/vimfiles/{plugin,syntax}

  cd vim && make PREFIX="$pkgdir/usr/share/vim/vimfiles" install

  # Install python bindings

  cd $srcdir/$_gitname-build/bindings/python

  find "." -name '*.py' -print0 |xargs -0 \
      sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
      -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
  ln -s ../../lib/libnotmuch.so.1 libnotmuch.so.1
  env LD_LIBRARY_PATH="." python2 setup.py build
  env LD_LIBRARY_PATH="." python2 setup.py install --prefix=/usr --root=$pkgdir
}
