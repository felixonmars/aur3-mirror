pkgname=mingw-w64-icu
pkgver=50.1.2
pkgrel=3
pkgdesc="International Components for Unicode library (mingw-w64)"
arch=('any')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!buildflags' 'staticlibs' '!strip')
source=("http://download.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz"
        'icu4c-4_6_1-crossbuild.patch'
icu4c-4_6_1-use-correct-genccode-assembly-on-win32-and-win64.patch
icu4c-4_6_1-mingw-w64-mkdir-compatibility.patch)


md5sums=('beb98aa972219c9fcd9c8a71314943c9'
         '6583c1cfbee96469f4b77120ed0f9278'
         'c480877485438a1f677ca23f7d915692'
         'c1194c6ac4bc02bd46f127a457ab4e93')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/icu"
  # http://pkgs.fedoraproject.org/cgit/mingw-icu.git
  patch -p1 -i "$srcdir"/icu4c-4_6_1-crossbuild.patch
  patch -p1 -i "$srcdir"/icu4c-4_6_1-use-correct-genccode-assembly-on-win32-and-win64.patch
  patch -p1 -i "$srcdir"/icu4c-4_6_1-mingw-w64-mkdir-compatibility.patch
  cd source
  autoreconf -vfi
}

build() {
  cd "$srcdir/icu/source"
  mkdir -p nativebuild && pushd nativebuild
  CFLAGS=-fno-stack-protector
  ../configure --enable-static --disable-shared
  make
  popd
  for _arch in ${_architectures}; do
    unset CFLAGS LDFLAGS
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --with-cross-build=$PWD/../nativebuild \
      --with-data-packaging=library \
      --enable-static
    make
    popd
  done
}

package() {
	mkdir -p "$pkgdir/usr/bin"
  for _arch in ${_architectures}; do
    cd "$srcdir/icu/source/build-${_arch}"
    make install DESTDIR="$pkgdir"
    rm -rf "$pkgdir"/usr/${_arch}/share
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
    mv "$pkgdir"/usr/${_arch}/lib/*.dll "$pkgdir"/usr/${_arch}/bin
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    ln -s "/usr/${_arch}/bin/icu-config" "$pkgdir/usr/bin/${_arch}-icu-config"
  done
}
