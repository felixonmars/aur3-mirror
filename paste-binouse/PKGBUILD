# Maintainer: Alexandre Bique <bique.alexandre@gmail.com>

pkgname=paste-binouse
pkgver=1.8
pkgrel=1
pkgdesc="paste-binouse is a standalone C++ web pastebin"
arch=('i686' 'x86_64')
url="http://github.com/abique/paste-binouse"
license=('MIT')
makedepends=('cmake>=2.8')
depends=('gnutls')
options=()
source=("https://github.com/downloads/abique/${pkgname}/${pkgname}-${pkgver}.tar.xz"
  "paste-binoused"
  "paste-binoused.confd"
  "paste-binouse.install"
  "paste-binoused.service")
md5sums=('2bc07646de313325dab8ce67729e3136'
         '5c2b7483f612e8f432cfb0fab33268ef'
         '5c3fe9f7f8c01666284c260fc0d49ff9'
         '16c1e7de70723d7ffd2de803d1bffaf3'
         '19ee3f1c9e19e293c4369a98945c8f18')
backup=('etc/conf.d/paste-binoused')
install='paste-binouse.install'

build() {
  cd ${srcdir}/${pkgname}-${pkgver} &&
  rm -rf build &&
  mkdir build &&
  cd build &&
  cmake -DCMAKE_INSTALL_PREFIX=/usr .. &&
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/build &&
  DESTDIR=${pkgdir} make install
  install -Dm755 "${srcdir}"/paste-binoused "${pkgdir}"/etc/rc.d/paste-binoused
  install -Dm644 "${srcdir}"/paste-binoused.confd "${pkgdir}"/etc/conf.d/paste-binoused
  install -Dm644 "${srcdir}"/paste-binoused.service "${pkgdir}"/usr/lib/systemd/system/paste-binoused.service
}
