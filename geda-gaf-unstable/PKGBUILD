# Contributor: Jared Casper <jaredcasper@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=geda-gaf-unstable
_pkgname=geda-gaf
pkgver=1.7.2
pkgrel=2
pkgdesc="gEDA/gaf suite - Contains gschem, gnetlist, gsymcheck, gattrib, utilities and documentation from the gEDA project"
arch=('i686' 'x86_64')
url="http://www.gpleda.org"
license=('GPL')
depends=('gtk2' 'guile' 'libstroke' 'hicolor-icon-theme' 'desktop-file-utils' 'gawk' 'python2')
conflicts=('geda-gaf')
provides=('geda-gaf')
install=geda-gaf.install
source=(http://geda.seul.org/devel/v1.7/$pkgver/${_pkgname}-$pkgver.tar.gz)
md5sums=('ccfe334e333d6ed14ace22a43c2cdc7c')

build ()
{
  cd $srcdir/${_pkgname}-$pkgver/
  CFLAGS="-DGLIB_COMPILATION" ./configure --prefix=/usr \
    --disable-update-xdg-database 
  make 
}
package() {
  cd $srcdir/${_pkgname}-$pkgver/
  make DESTDIR=$pkgdir/ install 
  sed -i 's+/usr/bin/env python+/usr/bin/env python2+' \
    $pkgdir/usr/bin/garchive
  sed -i 's+/usr/bin/python+/usr/bin/python2+' \
    $pkgdir/usr/bin/tragesym
  sed -i 's+/usr/bin/python+/usr/bin/python2+' \
    $pkgdir/usr/share/doc/${_pkgname}/examples/lightning_detector/bom
}

