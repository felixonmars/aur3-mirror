# $Id$
# Maintainer: Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious-git
pkgver=20120924
pkgrel=1
pkgdesc="Lightweight, GTK-based advanced audio player focused on audio quality"
url="http://audacious-media-player.org/"
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('gtk3' 'dbus-glib' 'libguess' 'libsm' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('pkg-config' 'git')
optdepends=('unzip: zipped skins support')
provides=('audacious')
conflicts=('audacious' 'audacious-player-hg')
install=$pkgname.install

_gitroot="git://github.com/audacious-media-player/audacious.git"
_gitname="audacious"
_branch="master" # Choose between 'master' (devel), '3.3.x' and '3.2.x' (legacy)

build() {
    msg "Connecting to GIT server..."

    if [[ -d ${_gitname} ]]; then
        ( cd ${_gitname} && git pull origin )
        msg "The local files are updated."
    else
        git clone -b ${_branch} ${_gitroot}
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf ${_gitname}-build
    git clone ${_gitname}{,-build}
    cd ${_gitname}-build

    ./autogen.sh
    ./configure --prefix=/usr --with-buildstamp="Arch Linux"
    make
}

package() {
    cd "${srcdir}/${_gitname}-build"
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
