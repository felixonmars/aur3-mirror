# Maintainer: György Balló <ballogy@freestart.hu>
_pkgname=indicator-application
pkgname=indicator-application-gtk2
pkgver=0.4.0
pkgrel=2
pkgdesc="A library and indicator to take menus from applications and place them in the panel (GTK+ 2 library for Xfce/LXDE)"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-application"
license=('GPL')
depends=('indicator-application' 'libappindicator>=0.2.9')
options=('!libtool')
source=(http://launchpad.net/$_pkgname/0.4/$pkgver/+download/$_pkgname-$pkgver.tar.gz)
md5sums=('c1a8cec847d92dc011a3a56d5ff7141f')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$_pkgname \
              --disable-static --with-gtk=2
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make -C src DESTDIR="$pkgdir/" install-applicationlibLTLIBRARIES
}
