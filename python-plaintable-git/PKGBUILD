# Maintainer: Stefan Tatschner <stefan@sevenbyte.org>

pkgname=python-plaintable-git
pkgver=r8.8d4d026
pkgrel=1
pkgdesc="A simple library to build plain text tables"
arch=('any')
url="https://github.com/rumpelsepp/plaintable"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
conflicts=('python-plaintable')
provides=('python-plaintable')
source=("${pkgname}::git+https://github.com/rumpelsepp/plaintable.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  #git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir/$pkgname"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}
