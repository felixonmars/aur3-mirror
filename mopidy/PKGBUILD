# Maintainer: Alexandre Petitjean <alpetitjean at gmail dot com>

pkgname=mopidy
_pypiname=Mopidy
pkgver=0.19.2
pkgrel=1
pkgdesc="Mopidy is a music server which can play music both from multiple sources, like your local hard drive, radio streams, and from Spotify and SoundCloud."
arch=('any')
url="http://www.mopidy.com"
license=('APACHE')
depends=('python2'
         'python2-pykka'
         'python2-setuptools'
         'gstreamer0.10-python'
         'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins'
         'gstreamer0.10-ugly-plugins'
         'python2-tornado')
makedepends=('python2')
optdepends=('mopidy-spotify: Spotify backend'
            'mopidy-scrobbler: Last.FM scrobbler'
            'mopidy-MPRIS: MPRIS frontend')
conflicts=('mopidy-git')
provides=('mopidy')
source=("https://pypi.python.org/packages/source/M/${_pypiname}/${_pypiname}-${pkgver}.tar.gz"
        'mopidy.service'
        'mopidy.conf')
md5sums=('9f57b5219e30c9cc01461a4587aa1d40'
         '41580d0e18e0358bfaa48b6a37be4a13'
         '048eabb8928508f216cac419f5f3fa36')

backup=('etc/xdg/mopidy/mopidy.conf')
install=install

package() {
  cd "${srcdir}/${_pypiname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm644 "${srcdir}/mopidy.service" "${pkgdir}/usr/lib/systemd/system/mopidy.service"
  install -Dm644 "${srcdir}/mopidy.conf" "${pkgdir}/etc/xdg/mopidy/mopidy.conf"
}

# vim:set ts=2 sw=2 et:
