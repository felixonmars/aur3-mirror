# Maintainer: Alexandre Petitjean <alpetitjean at gmail dot com>

pkgname=mopidy
_pkgpyname=Mopidy
pkgver=0.15.0
pkgrel=1
pkgdesc="A music server which can play music from Spotify"
arch=('any')
url="http://www.mopidy.com"
license=('APACHE')
depends=('python2'
         'pyspotify'
         'python2-pykka'
         'python2-distribute'
         'gawk'
         'gstreamer0.10-python'
         'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins'
         'gstreamer0.10-ugly-plugins')
makedepends=('python2')
optdepends=('python2-cherrypy: HTTP frontend'
            'python2-ws4py: HTTP frontend'
            'python2-pylast: Last.fm frontend'
            'python2-dbus: MPRIS frontend')
conflicts=('mopidy-git')
provides=('mopidy')
source=("https://pypi.python.org/packages/source/M/${_pkgpyname}/${_pkgpyname}-${pkgver}.tar.gz"
        'mopidy@.service')
md5sums=('ec9c1630fa8beedbdd0df1045ceff358'
         'eee7b58b4eb5ca7de27f67e174ab7f9a')

package() {
  cd "$srcdir/$_pkgpyname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 "$srcdir/mopidy@.service" "$pkgdir/usr/lib/systemd/system/mopidy@.service"
}

# vim:set ts=2 sw=2 et:
