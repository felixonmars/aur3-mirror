# Maintainer: Alexandre Petitjean <alpetitjean at gmail dot com>

pkgname=mopidy
_pypiname=Mopidy
pkgver=0.19.5
pkgrel=1
pkgdesc="Mopidy is a music server which can play music both from multiple sources, like your local hard drive, radio streams, and from Spotify and SoundCloud."
arch=('any')
url="http://www.mopidy.com"
license=('APACHE')
depends=('python2'
         'python2-pykka'
         'python2-setuptools'
         'gstreamer0.10-python'
         'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins'
         'gstreamer0.10-ugly-plugins'
         'python2-tornado')
optdepends=('mopidy-spotify: Spotify backend'
            'mopidy-scrobbler: Last.FM scrobbler'
            'mopidy-MPRIS: MPRIS frontend'
            'mopidy-alsamixer: ALSA volume control')
conflicts=('mopidy-git')
backup=('etc/mopidy/mopidy.conf')
install=$pkgname.install
source=("https://pypi.python.org/packages/source/M/${_pypiname}/${_pypiname}-${pkgver}.tar.gz"
        'man-mopidy.1.gz'
        'mopidy.conf'
        'mopidy.desktop'
        'mopidy.install'
        'mopidy.service')
md5sums=('60bbbdc3cda9ab723d7da1495f558d44'
         '55630327372a2136c7603b9b38beb690'
         'f198485317cd1f481b9b9a369e122833'
         'f6f94b7fb57d40dadbbfa93997d1b7ab'
         '8f682806f179970c5829089e387598e6'
         'f7f94cafab8b8ed381f06afb5a74533c')

package() {
  cd "${srcdir}/${_pypiname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm640 "${srcdir}/mopidy.conf" "${pkgdir}/etc/mopidy/mopidy.conf"
  install -Dm644 "${srcdir}/mopidy.service" "${pkgdir}/usr/lib/systemd/system/mopidy.service"
  install -Dm644 "${srcdir}/man-mopidy.1.gz" "${pkgdir}/usr/share/man/man1/mopidy.1.gz"
  install -Dm644 "${srcdir}/mopidy.desktop" "${pkgdir}/usr/share/applications/mopidy.desktop"

  install -dm755 "${pkgdir}/etc/mopidy/extensions.d"
  install -dm755 "${pkgdir}/var/cache/mopidy"
  install -dm755 "${pkgdir}/var/log/mopidy"
  install -dm755 "${pkgdir}/var/lib/mopidy/local"
  install -dm755 "${pkgdir}/var/lib/mopidy/media"
  install -dm755 "${pkgdir}/var/lib/mopidy/playlists"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
