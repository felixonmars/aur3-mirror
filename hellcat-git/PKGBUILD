# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=hellcat
pkgname=${_pkgname}-git
pkgver=r37.92d1def
pkgrel=1
pkgdesc="netcat that takes unfair advantage of traffic shaping systems that don't initially ratelimit"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://github.com/matildah/hellcat"
license=('MIT')
depends=('glibc')
makedepends=('clang' 'git')
provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("$_pkgname")

source=("git://github.com/matildah/${_pkgname}.git#branch=master")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd $_pkgname
    make
}

package() {
    cd $_pkgname
    install -Dm755 hellrecv "${pkgdir}/usr/bin/hellrecv"
    install -Dm755 hellsend "${pkgdir}/usr/bin/hellsend"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
