# Maintainer: Daniel Ehlers <danielehlers@mindeye.net>
pkgname=coin-cgl
pkgver=0.56.2
pkgrel=2
pkgdesc="The COIN-OR Cut Generation Library (Cgl) is a collection of cut
generators"
arch=('i686')
url="https://projects.coin-or.org/Cgl"
license=('EPL')
groups=('coin-or')
depends=('coin-utils>=2.7.1' 'coin-osi>=0.104.2' 'coin-clp>=1.13.3' )
makedepends=('coin-utils>=2.7.1' 'coin-osi>=0.104.2' 'coin-clp>=1.13.3' )
source=(http://www.coin-or.org/download/source/Cgl/Cgl-$pkgver.tgz)
md5sums=('9664c15ea3b90a37a7a8da7d3e8fcda7')

build() {
  cd "$srcdir/Cgl-$pkgver"
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="`pkg-config --libs osi`" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-clp-lib="`pkg-config --libs clp`" \
              --with-clp-incdir="/usr/include/coin/" \
              --with-coinutils-lib="`pkg-config --libs coinutils`" \
              --with-coinutils-incdir="/usr/include/coin/" -C
          
  make
}

check() {
  cd "$srcdir/Cgl-$pkgver"
  make test
}

package() {
  cd "$srcdir/Cgl-$pkgver"
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir/" install
}
