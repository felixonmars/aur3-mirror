# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

pkgname=thunderbird-beta-bin
pkgver=16.0
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge binary version"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="http://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite' 'startup-notification')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
conflicts=('thunderbird-beta')
install=$pkgname.install
# In case releases.mozilla.org is not responding or the file(s) aren't yet uploaded there:
# source=(http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/en-US/thunderbird-$pkgver.tar.bz2
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/en-US/thunderbird-$pkgver.tar.bz2
        thunderbird-beta.desktop
        vendor.js)
[ "$CARCH" = 'x86_64' ] && source[0]="ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-x86_64/en-US/thunderbird-$pkgver.tar.bz2"
md5sums=(`curl -s ${source/li*}/MD5SUMS | grep en-US | grep "$CARCH" | sed -n 3p | cut -d " " -f1`
         'f8a9b91202e8c85ad7b8f23fb82a4d7b'
         '5a53179d14ae9631b7afe5e4d0fc0b25')

package() {
  install -d "$pkgdir"/{usr/bin,opt}
  cp -a thunderbird "$pkgdir"/opt/$pkgname-$pkgver
  cp vendor.js "$pkgdir"/opt/$pkgname-$pkgver/defaults/pref/
  ln -s /opt/$pkgname-$pkgver/thunderbird "$pkgdir"/usr/bin/${pkgname/-bin}
  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 thunderbird/chrome/icons/default/default${i/x*/}.png "$pkgdir"/usr/share/icons/hicolor/$i/apps/${pkgname/-bin}.png
  done
  install -Dm644 ${pkgname/-bin}.desktop "$pkgdir"/usr/share/applications/${pkgname/-bin}.desktop
  rm -rf "$pkgdir"/opt/$pkgname-$pkgver/dictionaries/
  ln -sf /usr/share/hunspell/ "$pkgdir"/opt/$pkgname-$pkgver/dictionaries
}
