# Maintainer: Techlive Zheng <techlivezheng at gmail dot com>
# Contributor: Maxim Chervonny <fiskus@chervonny.ru>

pkgname=dotploy
pkgdesc="A bash only script to deploy $HOME dot files acrossing different hosts"
pkgver=0.3.2
pkgrel=1
url="https://github.com/techlivezheng/dotploy"
arch=('any')
license=('GPL')
source=(https://github.com/techlivezheng/dotploy/archive/"${pkgname}"-v"${pkgver}".tar.gz)
depends=('bash')
makedepends=('git' 'perl')

build() {
  cd "${srcdir}"/"${pkgname}"-"${pkgname}"-v"${pkgver}"

  make standalone

  sed -i 's/dotploy\.sh/dotploy/g' dotploy
}

check() {
  cd "${srcdir}"/"${pkgname}"-"${pkgname}"-v"${pkgver}"

  sed -i 's/dotploy\.sh/dotploy/g' tests/test-dotploy.sh

  make test
}

package() {
  cd "${srcdir}"/"${pkgname}"-"${pkgname}"-v"${pkgver}"

  install -D -m 0755 dotploy "${pkgdir}"/usr/bin/dotploy
}

md5sums=('b041880a28692f00232f4636904e180f')
