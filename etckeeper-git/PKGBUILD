# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mathieu.clabaut <mathieu.clabaut@gmail.com>

pkgname=etckeeper-git
pkgver=1.11.r0.g214332b
pkgrel=1
pkgdesc="collection of tools to let /etc be stored in a git, hg or bzr repository - git checkout"
arch=('any')
url="http://kitenet.net/~joey/code/etckeeper/"
license=('GPL2')
provides=('etckeeper')
conflicts=('etckeeper')
depends=('git')
makedepends=('mercurial' 'bzr')
optdepends=('mercurial: use mercurial for version control'
	'bzr: use bazaar for version control')
source=('git://git.kitenet.net/etckeeper')
backup=('etc/etckeeper/etckeeper.conf')

pkgver() {
	cd etckeeper/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd etckeeper/

	# This does not exist, but we do not want the apt stuff installed.
	sed -i -e "/^LOWLEVEL_PACKAGE_MANAGER/c LOWLEVEL_PACKAGE_MANAGER=pacman" \
		-e "/^HIGHLEVEL_PACKAGE_MANAGER/c HIGHLEVEL_PACKAGE_MANAGER=pacman" etckeeper.conf
}

package() {
	cd etckeeper/

	make DESTDIR=${pkgdir} install
	install -D -m0755 debian/cron.daily ${pkgdir}/etc/cron.daily/etckeeper
}

sha256sums=('SKIP')
