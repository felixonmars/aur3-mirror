# Maintainer: jtts
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org> 

_pkgbasename=colord
pkgname=lib32-$_pkgbasename
pkgver=1.2.0
pkgrel=2
pkgdesc="System daemon for managing color devices (32-bit)"
arch=(x86_64)
url="http://www.freedesktop.org/software/colord"
license=(GPL2)
depends=(lib32-lcms2 lib32-libgusb lib32-polkit lib32-sqlite lib32-systemd lib32-dconf lib32-dbus lib32-glib2 $_pkgbasename)
makedepends=(gcc-multilib intltool gobject-introspection vala) # docbook2x sane bash-completion
#optdepends=('sane: scanner support')
conflicts=('shared-color-profiles')
#install=colord.install
source=($url/releases/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('decbad07f7dbdebabe552f3280582008511f46201b47e0a91ea266d54072155e')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $_pkgbasename-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib32/$_pkgbasename --libdir=/usr/lib32 --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-vala --disable-volume-search \
    --disable-nls --disable-bash-completion \
    --with-daemon-user=colord
    #--enable-sane 
    # Not recognized by configure
    #--disable-manpages
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/{etc,lib,var,usr/{bin,include,lib,share}}
}
