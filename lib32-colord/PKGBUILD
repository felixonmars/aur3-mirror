# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=colord
pkgname=lib32-${_pkgbase}
pkgver=0.1.21
pkgrel=2
pkgdesc="Color daemon (32 bit)"
arch=('x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lib32-glib2' 'lib32-libgusb' 'lib32-lcms2' 'lib32-sqlite3' 'lib32-polkit' "${_pkgbase}") #  'lib32-udev' 'lib32-sane'
makedepends=('intltool' 'docbook2x' 'lib32-systemd-tools' 'lib32-sane') # 'gobject-introspection'
options=('!libtool' '!emptydirs')
source=(${url}/releases/${_pkgbase}-${pkgver}.tar.xz)
sha1sums=('dbf981beec70e81c45cf46b150f426fc1eb56c24')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --disable-static --disable-sane \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --libexecdir=/usr/lib32/colord --libdir=/usr/lib32 \
    --enable-introspection=no --enable-gtk=no
    
  make
}

package() {
  cd "${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,lib,var,usr/{bin,include,lib,share}}
}
