# Mantainer: Gianni Vialetto <gianni at rootcube dot net>
# Mantainer: Paul N. Maxwell <msg dot maxwel at gmail dot com>
# Contributor: Thomas Mudrunka <harvie@@email..cz>
# Contributor: Max Fierke <max@maxfierke.com>

pkgbase=apparmor
pkgname=apparmor
true && pkgname=($pkgbase apparmor-parser apparmor-libapparmor apparmor-utils apparmor-profiles apparmor-pam apparmor-vim)
pkgver=2.8.1
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url='http://wiki.apparmor.net/index.php/Main_Page'
groups=('apparmor')
makedepends=('flex' 'swig' 'perl' 'python2' 'ruby' 'perl-locale-gettext' 'perl-rpc-xml' 'wxgtk' 'audit')


test -n "$BASH_VERSION" && [[ "$pkgver" =~ ^([0-9]*\.[0-9]*) ]] && bigver="${BASH_REMATCH[1]}"
test -n "$bigver" || bigver="$(echo ${pkgver} | cut -d . -f -2)"

source=("http://launchpad.net/apparmor/${bigver}/${pkgver}/+download/apparmor-${pkgver}.tar.gz"
        "apparmor.rc"
        "apparmor_load.sh"
        "apparmor_unload.sh"
        "apparmor.service")

md5sums=('83ae6c8190e91cf2146da8942951d305'
         'baa14f0480f4e275014f32c9cc339ac7'
         '518e2227336b1b33df1b8a04b5a96489'
         'f083aa1926430bc48eb332f1b77fca08'
         '64d6bab30fe71ffc4a38c2203f5bb875')

#Configuration
core_perl_dir='/usr/bin/core_perl'
export MAKEFLAGS+=" POD2MAN=${core_perl_dir}/pod2man"
export MAKEFLAGS+=" POD2HTML=${core_perl_dir}/pod2html"
export MAKEFLAGS+=" PROVE=${core_perl_dir}/prove"
export PYTHON='/usr/bin/python2'

build() {
  msg2 "Building: apparmor-parser"
  cd "${srcdir}/${pkgbase}-${pkgver}/parser"
  msg2 'Patching: apparmor-parser'
  # Patch (maybe we can avoid patching by ./configuring things better)
  patch=Makefile; { rm "$patch"
    sed -e 's/pdflatex/true/g' > "$patch" # just workaround until we'll get pdflatex package
  } < "$patch"
  echo '#!/bin/true' > tst/caching.sh # Can't pass this test with current kernel
	make
	
  # Perl AppArmor library does not support modern perl installation paths
  # Force us to unset $PERL_MM_OPT before working on the library.
  unset PERL_MM_OPT

  msg2 "Building: apparmor-libapparmor"
  cd "${srcdir}/${pkgbase}-${pkgver}/libraries/libapparmor"
  ./autogen.sh
  ./configure --prefix=/usr --with-perl --with-python --with-ruby
  make

  msg2 "Building: apparmor-utils"
  cd "${srcdir}/${pkgbase}-${pkgver}/utils"
  # Obvious python2 is obvious
  patch=Makefile; { rm "$patch"
    sed -e 's/python /python2 /g' > "$patch"
  } < "$patch"
  patch=vim/Makefile; { rm "$patch"
    sed -e 's/python /python2 /g' > "$patch"
  } < "$patch"
  
  make

  # We have to patch the logprof.conf file to make it compatible
  # with archlinux syslog-ng standard configuration. And, since
  # we use /usr/sbin for binary commands, we also must patch
  # logprof.conf so utils will use that path instead of the default.
  patch=logprof.conf; { rm "$patch"
     sed -e '/logfiles/ s/syslog /syslog.log /g' \
         -e '/logfiles/ s/messages/messages.log/g' \
         -e '/parser/ s# /sbin/# /usr/sbin/#g' > "$patch"
  } < "$patch"

  # We should also patch python based commands so they use python2
  for util in `file aa-* | grep Python | cut -d ':' -f 1`; do
    { rm "$util"
      sed -e '1 s/python/python2/g' > "$util"
    } < "$util"
  done

  msg2 "Building: apparmor-profiles"
  cd "${srcdir}/${pkgbase}-${pkgver}/profiles"
  make

  msg2 "Building: apparmor-pam"
  cd "${srcdir}/${pkgbase}-${pkgver}/changehat/pam_apparmor"
  make

  msg2 "Building: apparmor-vim"
  cd "${srcdir}/${pkgbase}-${pkgver}/utils/vim/"
  make || make || true #FIXME: ??????
}

package_apparmor() {
  pkgdesc='Linux application security framework - mandatory access control for programs (metapackage for AUR)'
  depends=(apparmor-parser apparmor-libapparmor apparmor-utils apparmor-profiles apparmor-pam apparmor-vim)
  optdepends=('linux-apparmor: an arch kernel with AppArmor patches')
  install='apparmor.install'
}

package_apparmor-parser() {
  pkgdesc='AppArmor parser - loads AA profiles to kernel module'
  depends=('apparmor-libapparmor' 'bash')

  cd "${srcdir}/${pkgbase}-${pkgver}/parser"
  make install DESTDIR=${pkgdir}
  mv ${pkgdir}/lib ${pkgdir}/usr/lib
  mv ${pkgdir}/sbin ${pkgdir}/usr/sbin
}

package_apparmor-libapparmor() {
  pkgdesc='AppArmor library'
  makedepends=('swig' 'perl' 'python2' 'ruby')
  depends=('ruby' 'python2')
  provides=('apparmor-lib' 'libapparmor' 'apparmor-perl' 'apparmor-python' 'apparmor-ruby')

  cd "${srcdir}/${pkgbase}-${pkgver}/libraries/libapparmor"
  make install DESTDIR=${pkgdir}
  #FIXME: this file should install automatically:
  install -m644 "swig/perl/LibAppArmor.pm" "${pkgdir}/usr/lib/perl5/vendor_perl/"
  mv ${pkgdir}/usr/lib/ruby/site_ruby ${pkgdir}/usr/lib/ruby/vendor_ruby
}

package_apparmor-utils() {
  pkgdesc='AppArmor userspace utilities'
  arch=('any')
  depends=('perl' 'perl-locale-gettext' 'perl-term-readkey' 
    'perl-file-tail' 'perl-rpc-xml' 'python2' 'bash')
  provides=('apparmor-notify');
  install='apparmor-utils.install'

  cd "${srcdir}/${pkgbase}-${pkgver}/utils"
  make install DESTDIR=${pkgdir}
  install -dm755 "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/apparmor.rc" "${pkgdir}/etc/rc.d/apparmor"
  install -m755 "${srcdir}/apparmor_load.sh" "${pkgdir}/usr/sbin/apparmor_load.sh"
  install -m755 "${srcdir}/apparmor_unload.sh" "${pkgdir}/usr/sbin/apparmor_unload.sh"
  install -Dm644 "${srcdir}/apparmor.service" "${pkgdir}/usr/lib/systemd/system/apparmor.service"
}

package_apparmor-profiles() {
  pkgdesc='AppArmor sample pre-made profiles'
  arch=('any')

  cd "${srcdir}/${pkgbase}-${pkgver}/profiles"
  make install DESTDIR=${pkgdir}
}

package_apparmor-pam() {
  pkgdesc='AppArmor PAM library'
  depends=('apparmor-libapparmor' 'pam')

  cd "${srcdir}/${pkgbase}-${pkgver}/changehat/pam_apparmor"
  install -dm755 "${pkgdir}/usr/"
  make install DESTDIR=${pkgdir}/usr
}
package_apparmor-vim() {
  pkgdesc='AppArmor VIM support'
  arch=('any')
  depends=('vim')

  install -dm755 "${pkgdir}/usr/share/vim/vimfiles/"
  install -m644 \
    "${srcdir}/${pkgbase}-${pkgver}/utils/vim/apparmor.vim" \
    "${pkgdir}/usr/share/vim/vimfiles/apparmor.vim"
}
#AUR hack:
pkgdesc='Linux application security framework - mandatory access control for programs'
depends=('perl' 'perl-locale-gettext' 'perl-term-readkey' 
  'perl-file-tail' 'perl-rpc-xml' 'python2' 'bash' 'vim')
