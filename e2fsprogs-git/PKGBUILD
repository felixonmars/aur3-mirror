#  Maintainer: sudokode <sudokode@gmail.com>
# Contributor: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Matt Parnell <parwok aatt gmail ddoott ccoomm>
# Contributor: Ronald van Haren <ronald.archlinux.org> 
# Contributor: judd <jvinet@zeroflux.org>

pkgname="e2fsprogs-git"
pkgver=1.42.7.101.g1b8c4c1
pkgrel=1
pkgdesc="Ext2/3/4 filesystem utilities - GIT Version"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'MIT')
url="http://e2fsprogs.sourceforge.net"
groups=('base')
depends=('util-linux')
makedepends=('bc' 'util-linux')
conflicts=('e2fsprogs')
provides=('e2fsprogs')
backup=('etc/mke2fs.conf')
install=e2fsprogs.install
source=("$pkgname::git://git.kernel.org/pub/scm/fs/ext2/e2fsprogs.git"
        'MIT-LICENSE'
        'mke2fs.conf')
sha256sums=('SKIP'
            'cc45386c1d71f438ad648fd7971e49e3074ad9dbacf9dd3a5b4cb61fd294ecbb'
            '9c464f161974e0049e31adf42d5d331817dc3a1ff30045bd15cfff8f06038a9d')

pkgver() {
  cd "$srcdir"/$pkgname

  git describe | sed 's/^v//; s/-/./g'
}

build() {
  cd "$srcdir"/$pkgname

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' "$srcdir"/$pkgname/misc/Makefile.in

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --with-root-prefix="" \
    --enable-elf-shlibs \
    --disable-fsck \
    --disable-uuidd \
    --disable-libuuid \
    --disable-libblkid \
    --enable-compression \
    --enable-htree \
    --enable-blkid-debug \
    --enable-verbose-makecmds \
    --enable-e2initrd-helper \
    --enable-libquota
    #--enable-jbd-debug # breaks the build (2013/04/22)

  make
}

package() {
  cd "$srcdir"/$pkgname

  make DESTDIR="$pkgdir" install install-libs

  sed -i 's/^AWK=.*/AWK=awk/' "$pkgdir"/usr/bin/compile_et

  # remove references to build directory
  sed -i 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "$pkgdir"/usr/bin/mk_cmds
  sed -i 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "$pkgdir"/usr/bin/compile_et

  # install MIT license
  install -D -m644 "$srcdir"/MIT-LICENSE "$pkgdir"/usr/share/licenses/e2fsprogs/MIT-LICENSE

  find "$pkgdir" -type d -name '.git' -exec rm -r '{}' +
}

# vim:set ts=2 sw=2 et:
