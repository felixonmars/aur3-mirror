# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgbase=nwn
pkgname=('nwn')   # For the AUR
[ "x$CARCH" = "xi686" ] && pkgname=('nwn' 'nwn-original-resources' 'nwn-original-en')
[ "x$CARCH" = "xx86_64" ] && pkgname=('bin32-nwn' 'nwn-original-resources' 'nwn-original-en')
pkgver=1.69
pkgrel=1
arch=(i686 x86_64)
license=("custom")
url="http://nwn.bioware.com/downloads/linuxclient.html"
source=("http://files.bioware.com/neverwinternights/updates/linux/nwresources129.tar.gz"
        "http://files.bioware.com/neverwinternights/updates/linux/nwclient129.tar.gz"
        "http://files.bioware.com/neverwinternights/updates/linux/${pkgver//.}/English_linuxclient${pkgver//.}_orig.tar.gz"
        "http://kde-look.org/CONTENT/content-files/5492-nwnicons.tar.bz2"
        "99-base.conf"
        "98-original-resources.conf"
        "90-original-l10n.conf"
        "01-user.conf"
        "libnwn.sh"
        "nwn.sh"
        "dmclient.sh"
        "nwserver.sh"
        "rc.nwserver.sh"
        "nwserver.conf"
        "nwn.desktop"
        "dmclient.desktop")
md5sums=('8222401efe25235b56d01b38ae880b5b'
         '73c6515fd6aa5f860802098a55000ae8'
         '1a8ea3a8bc03c1b33e1207e574f95023'
         '3e604a841b6c56280f6b55be4cf99ebe'
         '75002dff2d34726defeb1cd18f9fe95e'
         '7f21f59e11b0471b495d413aacd2955e'
         '431879b4ee10edc4869e654ce8a096f7'
         'c1549ac03b636273288afaf4046f3ad0'
         '40840b869cafe05b80bc59a5eb23cf50'
         'ce95616e9313257b392adcd0d088928a'
         '36321803242e82117eb2cc8374414a14'
         '15b83bff4b350d2c95d92d9aa367bd92'
         'e61efb4ea87eda6daefcf062a6293a55'
         '3fb21ae974c4a09e805dee3b8df26aba'
         '9733e7318721c446f0d714a029f57ea7'
         'be90448c6129d12a682ca567e67a7b54')
noextract=("nwclient129.tar.gz" "English_linuxclient${pkgver//.}_orig.tar.gz")

# Package options
# Set to true to use the Bioware-provided SDL
_useBuiltinSDL=false

build() {
  local i

  # Extract the client and the update in the proper directory
  cd "${srcdir}"
  install -d nwn
  for i in "${noextract[@]}"; do
    bsdtar -x -C nwn -f "${i}"
  done

  # Remove provided SDL
  if ! "${_useBuiltinSDL}"; then
    rm nwn/lib/*
  fi

  # Roughly fix permissions
  cd nwn
  find . -type d -exec chmod 755 '{}' \;
  find . -type f -exec chmod 644 '{}' \;
}

_nwn.base() {
  cd "${srcdir}/nwn"
  local i
  for i in dmclient nwmain nwn nwserver; do
    install -Dm755 "${i}" "${pkgdir}/opt/nwn/base/${i}"
  done
  for i in lib nwn.ini miles readme*; do
    cp -r "${i}" "${pkgdir}/opt/nwn/base"
  done
  install -d "${pkgdir}/etc/nwn/hooks.d"
  install -d "${pkgdir}/srv/nwn"
  install -d "${pkgdir}/var/run/nwserver"
  install -d "${pkgdir}/var/log/nwserver"
  install -Dm644 "${srcdir}/99-base.conf" "${pkgdir}/etc/nwn/branches.d/99-base.conf"
  install -Dm644 "${srcdir}/01-user.conf" "${pkgdir}/etc/nwn/branches.d/01-user.conf"
  install -Dm644 "${srcdir}/libnwn.sh" "${pkgdir}/usr/lib/libnwn.sh"
  install -Dm755 "${srcdir}/nwn.sh" "${pkgdir}/usr/bin/nwn"
  install -Dm755 "${srcdir}/dmclient.sh" "${pkgdir}/usr/bin/dmclient"
  install -Dm755 "${srcdir}/nwserver.sh" "${pkgdir}/usr/bin/nwserver"
  install -Dm755 "${srcdir}/rc.nwserver.sh" "${pkgdir}/etc/rc.d/nwserver"
  install -Dm644 "${srcdir}/nwserver.conf" "${pkgdir}/etc/conf.d/nwserver"
  install -Dm644 "${srcdir}/nwn.desktop" "${pkgdir}/usr/share/applications/nwn.desktop"
  install -Dm644 "${srcdir}/dmclient.desktop" "${pkgdir}/usr/share/applications/dmclient.desktop"

  cd "${srcdir}/nwnicons"

  for i in 32 48 64 128; do
    install -Dm644 "${i}x${i}/nwn-alt-${i}x${i}.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/nwn.png"
    install -Dm644 "${i}x${i}/nwn-alt2-${i}x${i}.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/dmclient.png"
  done
}

package_nwn() {
  pkgdesc="Neverwinter Nights RPG from BioWare"
  provides=("nwn-base" "nwn-server")
  conflicts=("nwn-base" "nwn-server" "nwn-diamond")
  depends=("nwn-original-resources=${pkgver}" "mesa" "hicolor-icon-theme" "xdg-utils" "expect" "detachtty" "modfs-git")
  ${_useBuiltinSDL} || depends+=("sdl")
  optdepends=("intel-dri: 3D acceleration for Intel chipsets"
              "nvidia-utils: 3D acceleration for NVIDIA cards"
              "catalyst-utils: 3D acceleration for AMD cards")
  arch=("i686")
  options=(emptydirs)
  install=nwn.install

  _nwn.base
}

package_bin32-nwn() {
  pkgdesc="Neverwinter Nights RPG from BioWare"
  provides=("nwn" "nwn-base" "nwn-server")
  conflicts=("nwn" "nwn-base" "nwn-server" "nwn-diamond")
  true && depends=("nwn-original-resources=${pkgver}" "lib32-mesa" "hicolor-icon-theme" "xdg-utils" "expect" "detachtty" "modfs-git")
  ${_useBuiltinSDL} || depends+=("lib32-sdl")
  optdepends=("lib32-intel-dri: 3D acceleration for Intel chipsets"
              "lib32-nvidia-utils: 3D acceleration for NVIDIA cards"
              "lib32-catalyst-utils: 3D acceleration for AMD cards")
  arch=("x86_64")
  options=(emptydirs)
  install=nwn.install

  _nwn.base
}

package_nwn-original-resources() {
  true && pkgdesc="Neverwinter Nights RPG from BioWare (original game resources)"
  arch=(any)
  options=(!strip)
  install="nwn-original-resources.install"

  cd "${srcdir}/nwn"
  local i
  install -d "${pkgdir}/opt/nwn/original-resources"
  install -Dm644 EULA.txt "${pkgdir}/usr/share/licenses/nwn/EULA.txt"
  for i in patch.key xp3.key docs \
           data dmvault docs hak localvault modules \
           music nwm override portraits saves       \
           servervault texturepacks ambient readme*; do
    cp -r "${i}" "${pkgdir}/opt/nwn/original-resources/${i}"
  done
  rm "${pkgdir}/opt/nwn/original-resources/data/convo.bif" "${pkgdir}/opt/nwn/original-resources/data/voicesets.bif"
  install -Dm644 "${srcdir}/98-original-resources.conf" "${pkgdir}/etc/nwn/branches.d/98-original-resources.conf"
  install -Dm644 "${srcdir}/90-original-l10n.conf" "${pkgdir}/etc/nwn/branches.d/90-original-l10n.conf"
}

package_nwn-original-en() {
  true && pkgdesc="Neverwinter Nights RPG from BioWare (original game English localization)"
  arch=(any)
  provides=('nwn-original-l10n')
  conflicts=('nwn-original-l10n')
  options=(!strip)

  cd "${srcdir}/nwn"
  local i
  install -d "${pkgdir}/opt/nwn/original-l10n/data"
  for i in dialog.tlk chitin.key data/voicesets.bif data/convo.bif; do
    cp -r "${i}" "${pkgdir}/opt/nwn/original-l10n/${i}"
  done
}
