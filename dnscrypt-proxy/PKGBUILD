# Maintainer: solsTiCe d'Hiver <solstice.dhiver@gmail.com>

pkgname=dnscrypt-proxy
pkgver=0.9.5
pkgrel=1
pkgdesc="A tool for securing communications between a client and a DNS resolver"
arch=('i686' 'x86_64')
url="https://github.com/opendns/dnscrypt-proxy"
license=('custom')
source=(https://github.com/downloads/opendns/dnscrypt-proxy/dnscrypt-proxy-$pkgver.tar.bz2 rc.d.script conf.d.file\
	dnscrypt-proxy.service)
md5sums=('446c3063bcc5af09e94d2d686781592a'
         '1f8cd3eb4de8912dea8baa5094014d7f'
         '4d486b3c2a4d47d4f998ed75c5033f77'
         'f57d32f6f133ed3640c6c7dc25e0e04b')

build() {
	cd $srcdir/$pkgname-$pkgver
	#./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	mkdir -p $pkgdir/usr/share/{licenses,doc}/$pkgname
	install -m 644 COPYING $pkgdir/usr/share/licenses/$pkgname
	install -m 644 AUTHORS NEWS README README.markdown $pkgdir/usr/share/doc/$pkgname

	mkdir -p $pkgdir/etc/{rc.d,conf.d}
	install -m 755 $srcdir/rc.d.script $pkgdir/etc/rc.d/dnscrypt-proxy
	install -m 644 $srcdir/conf.d.file $pkgdir/etc/conf.d/dnscrypt-proxy

	# systemd related stuff
	# comment the folowing 2 lines if you don't want the systemd file to be installed
	mkdir -p $pkgdir/usr/lib/systemd/system
	install -m 644 $srcdir/dnscrypt-proxy.service $pkgdir/usr/lib/systemd/system
}

