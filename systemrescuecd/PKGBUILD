# Maintainer: PyroDevil <p dot devil at gmail dot com>
pkgname=systemrescuecd
pkgver=3.7.1
pkgrel=1
epoch=
pkgdesc="Installs a rescue system into the boot partition to allow grub to boot it."
arch=('i686' 'x86_64')
url="http://www.sysresccd.org"
license=('GPL')
groups=()
depends=('grub>=2.00')
makedepends=('p7zip')
checkdepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install='systemrescuecd.install'
changelog=
source=("http://downloads.sourceforge.net/project/systemrescuecd/sysresccd-x86/$pkgver/$pkgname-x86-$pkgver.iso"
        "25_systemrescue")
md5sums=('1bc0f967f0e1e3b860e866aa6329bb4b'
         'a55e81c8b59ee49e5bd6399c3c386135')
noextract=("$pkgname-x86-$pkgver.iso")

# Here you can add bootoptions
# For all available bootoptions see:
# http://www.sysresccd.org/Sysresccd-manual-en_Booting_the_CD-ROM
# For example add "setkmap=de" for german keymap on default
_sysresccd_bootoptions=""

build() {
  mkdir -p "$srcdir/$pkgname-$pkgver"
  7z x "$srcdir/$pkgname-x86-$pkgver.iso" -o"$srcdir/$pkgname-$pkgver" > /dev/null
  sed -i "s/export SYSRESCCD_BOOTOPTIONS=\"[-=_ 0-9a-zA-Z]*\"/export SYSRESCCD_BOOTOPTIONS=\"${_sysresccd_bootoptions}\"/g" "$srcdir/25_systemrescue"
}

package() {
  mkdir -p "$pkgdir/boot/sysrescue"
  mkdir -p "$pkgdir/etc/grub.d"
  install -m 755 "$srcdir/25_systemrescue"                       "$pkgdir/etc/grub.d/"
  install -m 644 "$srcdir/$pkgname-$pkgver/isolinux/rescue32"    "$pkgdir/boot/sysrescue/"
  install -m 644 "$srcdir/$pkgname-$pkgver/isolinux/rescue64"    "$pkgdir/boot/sysrescue/"
  install -m 644 "$srcdir/$pkgname-$pkgver/isolinux/altker32"    "$pkgdir/boot/sysrescue/"
  install -m 644 "$srcdir/$pkgname-$pkgver/isolinux/altker64"    "$pkgdir/boot/sysrescue/"
  install -m 644 "$srcdir/$pkgname-$pkgver/isolinux/initram.igz" "$pkgdir/boot/sysrescue/"
  install -m 644 "$srcdir/$pkgname-$pkgver/sysrcd.dat"           "$pkgdir/boot/sysrescue/"
}

# vim:set ts=2 sw=2 et:
