# Maintainer: Florian Hahn <flo@fhahn.com>

pkgname=neovim-git
_gitname=neovim
pkgdesc="Vim's rebirth for the 21st century"
license=('custom:vim')
pkgver=0.r516.1b55b12
pkgrel=1
depends=('libuv' 'ncurses')
makedepends=('cmake' 'git' 'msgpack')
conflicts=('')
provides=('neovim')
arch=('i686' 'x86_64')
url=('https://github.com/neovim/neovim')
source=('git+https://github.com/neovim/neovim.git')
md5sums=('SKIP')
install=neovim-git.install
optdepends=(
  'vim-runtime: provides useful .vim files, although some may not work with neovim'
)

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  make cmake
  cd build
  make
}

package() {
  install -Dm755 "${srcdir}/${_gitname}/build/bin/nvim" "${pkgdir}/usr/bin/nvim"
  install -Dm644 "${srcdir}/${_gitname}/vim-license.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 
}

# vim:set sw=2 sts=2 et:
