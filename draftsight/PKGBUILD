# Contributor: mickele
# Contributor: Dan Serban
# Contributor: Loui Chang <louipc dot ist at gmail company> (SPAMMERS!)

pkgname=draftsight
pkgver=V1R2.0
pkgrel=1
pkgdesc="Free CAD software for your DWG files."
arch=(i686 x86_64)
url="http://www.3ds.com/products/draftsight/download-draftsight/"
source=(draftsight-$pkgver.rpm::http://dl-ak.solidworks.com/nonsecure/draftsight/$pkgver/draftSight.rpm)
license=(Commercial)
install=draftsight.install

_pkgprefix=opt/dassault-systemes/draftsight

depends=(
    fontconfig
    gcc-libs
    glib2
    glibc
    libcups
    libgl
    libice
    libsm
    libx11
    libxext
    libxrender
    libxt
    mesa
    nas
    util-linux
    zlib
)

# Build and replace dependency list for x86_64
if [[ "$CARCH" == 'x86_64' ]]; then
    for i in ${depends[@]}
    do
        _depends_64+=("lib32-$i")
    done
    depends=("${_depends_64[@]}")
fi

package() {
    # Perhaps it should be a copy instead, but this is fast on the
    # same filesystem.
    mv opt "${pkgdir}"/

    mkdir -p "${pkgdir}"/etc/profile.d
    echo "export PATH=\$PATH:/${_pkgprefix}/bin" > $pkgdir/etc/profile.d/$pkgname.sh
    chmod 755 "${pkgdir}"/etc/profile.d/$pkgname.sh

    # Some of these are incompatible with system libraries of the same soname,
    # which will cause problems with applications that depend on them.
    #mkdir -p "${pkgdir}"/etc/ld.so.conf.d
    #echo /${_pkgprefix}/lib > $pkgdir/etc/ld.so.conf.d/$pkgname.conf

    # ln -s "${pkgdir}"/opt/dassault-systemes/draftsight/Eula "${pkgdir}"/usr/share/licenses/draftsight
    install -D "${pkgdir}"/opt/dassault-systemes/draftsight/Eula/english/eula.htm "${pkgdir}"/usr/share/licenses/draftsight/eula.htm
}



md5sums=('ebb0a3533cf97efabcfada088b9c48ce')
