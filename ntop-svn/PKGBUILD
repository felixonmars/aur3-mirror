# Maintainer: srijan <srijan4@gmail.com>
# Contributor: archtux <antonio.arias99999@gmail.com>

pkgname=ntop-svn
pkgver=5814
pkgrel=1
pkgdesc="A network traffic probe that shows the network usage (svn version with python2 patch)."
arch=('i686' 'x86_64')
url="http://www.ntop.org/"
license=('GPL2')
depends=('gd' 'gdbm' 'geoip' 'libevent' 'libpcap' 'libxml2' 'lua' 'openssl' 'pcre' 'rrdtool' 'python2')
makedepends=('subversion')
optdepends=('python2-mako')
provides=('ntop')
conflicts=('ntop')
install='ntop.install'
source=(  'ntop'
          'ntop.conf.d'
          'ntop.service')
_svntrunk=https://svn.ntop.org/svn/ntop/trunk/ntop
_svnmod=ntop-svn
md5sums=('7b0d7bb57432e768ff387e9f7236f87c'
         '6a9371de1dcf31d8ef13a4cf349c02b3'
         '0e149d20f881600e8387d850ac268483')

build() {
   cd "$srcdir"
   msg "Connecting to SVN server...."

   if [[ -d "$_svnmod/.svn" ]]; then
     (cd "$_svnmod" && svn up -r "$pkgver")
   else
     svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
   fi

   msg "SVN checkout done or server timeout"
   msg "Starting build..."

   rm -rf "$srcdir/$_svnmod-build"
   cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
   cd "$srcdir/$_svnmod-build"

   # Patching for Python 2
   sed -i "s/python-config/python2-config/g" configure.in
   sed -i "s/\/usr\/bin\/env python/\/usr\/bin\/env python2/" python/sankey.py
   sed -i "s/\/usr\/bin\/env python/\/usr\/bin\/env python2/" www/Python/ntopTest.py
   sed -i "s/\/usr\/bin\/python/\/usr\/bin\/python2/" configureextra/python-config

   ./autogen.sh --noconfig
   ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var/lib
   make
}

package() {
  cd "$srcdir/$_svnmod-build"

   make DESTDIR="$pkgdir/" install

   install -dm0755 -o nobody -g nobody "${pkgdir}/var/lib/ntop"
   install -dm0755 -o nobody -g nobody "${pkgdir}/var/lib/ntop/rrd"

   install -Dm0755 "${srcdir}/ntop" "${pkgdir}/etc/rc.d/ntop"
   install -Dm0644 "${srcdir}/ntop.conf.d" "${pkgdir}/etc/conf.d/ntop"

   for _f in "${pkgdir}/usr/lib/ntop/plugins/"*.so; do
     _plug="$(basename ${_f})"
     ln -sf "../../lib${_plug}" "${_f}"
   done

   install -Dm0644 "${srcdir}/ntop.service" "${pkgdir}/usr/lib/systemd/system/ntop.service"

}
