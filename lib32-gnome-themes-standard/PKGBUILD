# Maintainer: jtts
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=gnome-themes-standard
pkgname=lib32-$_pkgbasename
pkgver=3.10.0
pkgrel=2
pkgdesc="Default themes for the GNOME desktop (32-bit)"
arch=(x86_64)
url="http://www.gnome.org"
license=(GPL2)
depends=(lib32-librsvg $_pkgbasename)
makedepends=(gcc-multilib intltool lib32-gtk2 lib32-gtk3)
optdepends=('lib32-gtk-engines: HighContrast GTK2 theme')
#groups=(gnome)
replaces=(lib32-gnome-themes)
conflicts=(lib32-gnome-themes)
options=('!emptydirs')
source=(http://download.gnome.org/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('812c6cfc534fced921dc327b436e70020d8979529284bb59d72d6f37af1f4b38')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="${pkgdir}" install

  # remove unneeded stuff
  rm -fr ${pkgdir}/{usr/{bin,include,share},etc}
}