pkgname=bemenu-git
pkgver=r169.64c4bbd
pkgrel=2

pkgdesc='Dynamic menu library and client program inspired by dmenu'
url='https://github.com/Cloudef/bemenu'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')

depends=('ncurses' 'wayland-git' 'cairo' 'libxkbcommon')
makedepends=('git' 'cmake')

provides=('bemenu')
conflicts=('bemenu')

source=('git://github.com/Cloudef/bemenu')

md5sums=('SKIP')

pkgver() {
    cd bemenu
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git describe --always)"
}

build() {
    cd bemenu
    cmake -DCMAKE_INSTALL_PREFIX=/usr
    make
}

check() {
    cd bemenu
    make test
}

package() {
    cd bemenu
    make DESTDIR="$pkgdir" install
}
