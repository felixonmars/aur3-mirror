# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=yafaray-git
pkgver=20120805
_blendver=2.63
pkgrel=1
pkgdesc="A free open-source raytracing engine"
arch=('i686' 'x86_64')
url="http://www.yafaray.org/"
license=('LGPL2.1')
depends=('libxml2' 'openexr' 'libjpeg')
optdepends=('qt: QT GUI' 'python: Python bindings, Blender exporter')
makedepends=('cmake' 'git' 'qt' 'swig' 'python')
provides=('yafaray')
conflicts=('yafaray')

_gitroot=git://github.com/YafaRay/Core.git
_gitname=yafaray
_gitrootexport=git://github.com/YafaRay/Blender-Exporter.git
_gitnameexport=exporter

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  # core
  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    cd ..
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi
  # exporter
  if [[ -d "$_gitnameexport" ]]; then
    cd "$_gitnameexport" && git pull origin
    cd ..
    msg "The local files are updated."
  else
    git clone "$_gitrootexport" "$_gitnameexport"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  if [ -e "$srcdir"/$_gitname-build ]; then
      rm -rf "$srcdir"/$_gitname-build
  fi
  mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DYAF_PY_VERSION=3.2 \
    -DYAF_BINDINGS_PY_DIR=/usr/lib/python3.2 \
    "../$_gitname"
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  # install exporter
  cd "$srcdir/$_gitnameexport"
  git checkout-index -a -f --prefix="$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/"
  # create symlinks to make exporter fully work
  install -d -m755 "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin"
  ln -sf "/usr/lib/libyafaraycore.so" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/libyafaraycore.so"
  ln -sf "/usr/lib/libyafarayplugin.so" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/libyafarayplugin.so"
  ln -sf "/usr/lib/yafaray" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/plugins"
}

# vim:set ts=2 sw=2 et:
