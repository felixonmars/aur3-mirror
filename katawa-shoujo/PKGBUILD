# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>

pkgname=katawa-shoujo
pkgver=1.1
pkgrel=1
pkgdesc="Bishoujo-style visual novel game that tells a story of a young man and five other girls suffering with varying disabilities"
arch=('i686' 'x86_64')
url='http://katawa-shoujo.com/'
license=('CCPL:by-nc-nd' 'MIT')

if [ "$CARCH" == "x86_64" ]; then
  depends=('lib32-zlib')
fi

if [ "$CARCH" == "i686" ]; then
  optdepends=('pulseaudio: audio support')
else
  optdepends=('lib32-alsa-lib: audio support'
              'lib32-libpulse: audio support')
fi

install="${pkgname}.install"

source=('http://dl.katawa-shoujo.com/gold_1.1/%5b4ls%5d_katawa_shoujo_1.1-%5blinux-x86%5d%5bB5C707D5%5d.tar.bz2'
        "${pkgname}.sh"
        "${pkgname}.desktop"
        "${pkgname}.png")

md5sums=('da688a5c13735fc73f2e7de5ca792a23'
         '5b530abf92fd084c829f626ba5e73b51'
         '52da5f2a8365a9b88d65fad1301e439b'
         '6d70e339e33d7c13a8ab03a5d3498c4f')

package() {
  # copy main files
  cd "${srcdir}/Katawa Shoujo-linux-x86/"
  install -dm755 "${pkgdir}/opt/${pkgname}"
  cp -r common game lib renpy "${pkgdir}/opt/${pkgname}"
  install -m644 'Katawa Shoujo.py' "${pkgdir}/opt/${pkgname}"
  install -m755 'Katawa Shoujo.sh' "${pkgdir}/opt/${pkgname}"

  # copy game manual
  install -D -m644 'Game Manual.pdf' "${pkgdir}/usr/share/doc/${pkgname}/gamemanual.pdf"

  # copy license
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # copy bash script
  install -D -m755 "${srcdir}/katawa-shoujo.sh" "${pkgdir}/usr/bin/${pkgname}"

  # install desktop entry file
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

# vim:set ts=2 sw=2 et:
