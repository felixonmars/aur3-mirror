# Maintainer: Andrew Crerar <asc9003 [at] rit [dot] edu>
# Contributor: RKA KriK <rka_krik@mail.ru>
# Contributor: Boohbah <boohbah at gmail.com>

pkgname=gtk3-git
_gitname=gtk+
pkgver=20130429
pkgrel=3
pkgdesc="GObject-based multi-platform GUI toolkit (git version)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('atk-git' 'cairo-git' 'gtk-update-icon-cache' 'libcups' 'libxcursor' \
	'libxinerama' 'libxrandr' 'libxi-git' 'libxcomposite' 'libxdamage' 'pango-git' \
	'shared-mime-info' 'colord' 'at-spi2-atk-git' 'wayland-git' 'libxkbcommon-git')
makedepends=('gobject-introspection')
provides=('gtk3=3.9.0')
conflicts=('gtk3')
backup=('etc/gtk-3.0/settings.ini')
options=('!libtool')
install=gtk3-git.install
source=('git://git.gnome.org/gtk+' 'settings.ini')
md5sums=('SKIP' 'fd06f6b71336389ffcc97b5df818fbf9')

pkgver() {
	cd $_gitname
	git describe --always | sed 's/-/./g'
}

build() {
	cd $_gitname
	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-gtk2-dependency \
		--disable-schemas-compile \
		--enable-x11-backend \
		--enable-broadway-backend \
		--enable-wayland-backend

	#https://bugzilla.gnome.org/show_bug.cgi?id=655517
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd $_gitname
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}

