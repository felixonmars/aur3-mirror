_pkgname=clanlib
pkgname='clanlib-git'
true && pkgname=('clanlib-git' 'clanlib-docs-git')
pkgver=3466.32fb60a
pkgrel=1
pkgdesc="ClanLib is a cross platform C++ toolkit library."
arch=('i686' 'x86_64')
url="https://github.com/sphair/ClanLib"
license=('zlib')
depends=('alsa-lib' 'libjpeg' 'libpng' 'libvorbis' 'libxi' 'libxmu' 'sdl_gfx' 'freetype2' 'pcre' 'sqlite' 'libgl')
makedepends=('doxygen' 'graphviz')

source=("${_pkgname}::git+https://github.com/sphair/ClanLib.git")
md5sums=('SKIP')

pkgver() {
	cd ${_pkgname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${_pkgname}
	./autogen.sh
	./configure --prefix=/usr --disable-static --enable-docs

	make
	make html
}

package_clanlib-git() {
	cd ${_pkgname}
	make DESTDIR="${pkgdir}" install
}

package_clanlib-docs-git() {
	cd ${_pkgname}
	make DESTDIR="${pkgdir}" install-html
}
