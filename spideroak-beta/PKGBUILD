# Maintainer: ava1ar (mail(dot)avatar(at)gmail(dot)com)

pkgname=spideroak-beta
_pkgname=spideroak
pkgver=4.6.1.9946
pkgrel=3
pkgdesc="Secure file backup, sync and sharing client (beta version)"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
options=('!strip')
provides=($_pkgname)
conflicts=($_pkgname)
install=$pkgname.install
_arch="$CARCH"
[ "$CARCH" = 'i686' ] && _arch='i386'
source=("${pkgname}_${pkgver}_${_arch}.rpm"::"https://spideroak.com/directdownload?platform=fedora&arch=$_arch&revision=${pkgver##4.6.1.}&code="
        "terms.txt")
sha1sums=('4517beb4a6bbbaaed0ea403e91185ff7f0256e36'
          'f287fdbad966ac9ae4951a1932e9be7e82112728')
[ "$CARCH" = 'i686' ] && sha1sums[0]='8ca0af0f3bd3322306711cce6c783a93d8a13cfa'

package() {

  # install config files
  install -d -m 755 $pkgdir/etc/dbus-1/system.d
  install -m 644 $srcdir/etc/dbus-1/system.d/$_pkgname.dbus.conf $pkgdir/etc/dbus-1/system.d/

  # install app in /opt
  install -d -m 755 $pkgdir/opt/SpiderOak
  cp -r $srcdir/usr/lib/SpiderOak/* $pkgdir/opt/SpiderOak

  # clean up a bit
  rm -f $pkgdir/usr/lib/SpiderOak/lib{gcc_s,stdc++,z}.so.*
  rm -f $pkgdir/usr/lib/SpiderOak/*/*/*.exe

  # install start script file
  install -d -m 755 $pkgdir/usr/bin
  install -m 755 $srcdir/usr/bin/SpiderOak $pkgdir/usr/bin/

  # change /usr to /opt in start script file
  sed -i 's:/usr/lib:/opt:g' $pkgdir/usr/bin/SpiderOak

  # install desktop and pixmap files
  install -d -m 755 $pkgdir/usr/share/{applications,pixmaps}
  install -m 644 $srcdir/usr/share/applications/$_pkgname.desktop $pkgdir/usr/share/applications/
  install -m 644 $srcdir/usr/share/pixmaps/$_pkgname.png $pkgdir/usr/share/pixmaps/

  # fix desktop file
  sed -i \
    -e "/Encoding=UTF-8/d" \
    -e "/^Name=/s: Backup::" \
    -e "/^Comment=/s:SpiderOak ::" \
    -e "/^Categories=/s:SpiderOak;::" \
    -e "/^Icon=/s:=.*$:=$_pkgname:" \
    -e "/^Exec=/s:=.*$:=SpiderOak:" \
    $pkgdir/usr/share/applications/$_pkgname.desktop

  # install custom license file
  install -d -m 755 $pkgdir/usr/share/licenses/$_pkgname
  install -m 644 terms.txt $pkgdir/usr/share/licenses/$_pkgname/
}
