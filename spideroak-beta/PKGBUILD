# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Limao Luo <luolimao@gmail.com>
# Maintainer: ava1ar (mail(dot)avatar(at)gmail(dot)com)

pkgname=spideroak-beta
_pkgname=spideroak
pkgver=4.6.1.9948
pkgrel=3
pkgdesc="Secure file backup, sync and sharing client"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
depends=(qt krb5 python2)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!strip')
install=$pkgname.install
_arch="$CARCH"
[ "$CARCH" = 'i686' ] && _arch='i386'
source=("${_pkgname}_${pkgver}_${_arch}.rpm"::"https://spideroak.com/directdownload?platform=fedora&arch=$_arch&revision=${pkgver##4.6.1.}&code="
        "terms.txt")
sha1sums=('b305465c8b193c13ebc82a3b7887a712c51f3723'
          'f287fdbad966ac9ae4951a1932e9be7e82112728')
[ "$CARCH" = 'i686' ] && sha1sums[0]='fc617cb0e8da60814d89f4642d2ebda339e387ed'

package() {
    # install config files
    install -Dm644 $srcdir/etc/dbus-1/system.d/$_pkgname.dbus.conf $pkgdir/etc/dbus-1/system.d/$_pkgname.dbus.conf

    # clean up a bit
    rm -f $srcdir/usr/lib/SpiderOak/lib{gcc_s,stdc++,z}.so.*
    rm -f $srcdir/usr/lib/SpiderOak/*/*/*.exe

    # install app in /opt
    install -d $pkgdir/opt/
    cp -r $srcdir/usr/lib/SpiderOak/ $pkgdir/opt/

    # install start script file
    install -Dm755 $srcdir/usr/bin/SpiderOak $pkgdir/usr/bin/SpiderOak

    # change /usr to /opt in start script file
    sed -i 's:/usr/lib:/opt:g' $pkgdir/usr/bin/SpiderOak

    # install desktop and pixmap files
    install -Dm644 $srcdir/usr/share/applications/$_pkgname.desktop $pkgdir/usr/share/applications/$_pkgname.desktop
    install -Dm644 $srcdir/usr/share/pixmaps/$_pkgname.png $pkgdir/usr/share/pixmaps/$_pkgname.png

    # fix desktop file
    sed -i \
	-e "/Encoding=UTF-8/d" \
	-e "/^Name=/s: Backup::" \
	-e "/^Comment=/s:SpiderOak ::" \
	-e "/^Categories=/s:SpiderOak;::" \
	-e "/^Icon=/s:=.*$:=$_pkgname:" \
	-e "/^Exec=/s:=.*$:=SpiderOak:" \
	$pkgdir/usr/share/applications/$_pkgname.desktop

    # install custom license file
    install -Dm644 terms.txt $pkgdir/usr/share/licenses/$_pkgname/terms.txt
}
