# Maintainer: spider-mario <spidermario@free.fr>
pkgname=qtdesktopcomponents-git
pkgver=20121017
pkgrel=1
pkgdesc="QtÂ Quick components styled with QStyle"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/QtDesktopComponents"
license=('custom:BSD')
depends=('qt')
makedepends=('git')
provides=('qtdesktopcomponents' 'qt-components-desktop' 'qt-desktop-components')
conflicts=('qtdesktopcomponents' 'qt-components-desktop' 'qt-desktop-components')
replaces=('qt-components-desktop' 'qt-desktop-components')

_gitname=qtdesktopcomponents
_gitroot=git://gitorious.org/qtplayground/$_gitname.git

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git checkout qt4 && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname" && git checkout qt4
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  qmake
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make INSTALL_ROOT="$pkgdir/" install

  install --directory "$pkgdir/usr/bin"
  install qmldesktopviewer/qmldesktopviewer "$pkgdir/usr/bin/"

  install --directory "$pkgdir/usr/share/licenses/$pkgname"
  cp header.BSD "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
