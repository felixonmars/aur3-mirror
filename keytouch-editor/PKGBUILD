# $Id: PKGBUILD 122686 2011-05-06 05:33:49Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=keytouch-editor
pkgver=3.1.3
pkgrel=3
pkgdesc="An utility to create keyboard files for keytouch"
arch=('i686' 'x86_64')
url="http://keytouch.sourceforge.net/"
license=('GPL')
depends=('gtk2')
install=keytouch-editor.install
source=(http://downloads.sourceforge.net/keytouch/${pkgname}-${pkgver}.tar.gz keytouch-editor-3.1.3-glibc28.patch)
md5sums=('046e0525d2b1275f1ec1b5140bd93565'
         'def8cf803d7f25dd919b8ad090083bf1')
sha1sums=('a8c0b28bac4b1f9de3897e1a78dc14b4d994334e'
          'c6b725267a628cfc93388044cfd72974f53debaf')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../keytouch-editor-3.1.3-glibc28.patch
  sed -i 's+AM_CONFIG_HEADER+AC_CONFIG_HEADERS+' configure.in
  sed -i 's+AM_PROG_CC_STDC+AC_PROG_CC+' configure.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
