# Maintainer: tailinchu <use_my_id at gmail dot com>
# Contributor: lspci <agm2819 at gmail dot com>
# Contributor: TuxSpirit <tuxspirit AT archlinux DOT fr>

pkgname=rar
pkgver=5.2.0
pkgrel=4
pkgdesc="A command-line port of the rar compression utility"
arch=('i686' 'x86_64')
url="http://www.rarlab.com"
license=('custom')
depends=('gcc-libs')
backup=('etc/rarfiles.lst')

source_i686=(http://www.rarlab.com/rar/rarlinux-$pkgver.tar.gz)
source_x86_64=(http://www.rarlab.com/rar/rarlinux-x64-$pkgver.tar.gz)

md5sums_i686=('11043888e0322a9035e2f24e4b248dc5')
md5sums_x86_64=('82d9266a8fcc40636057fa1cdf62e398')

package() {
	cd "$srcdir/rar"
	install -Dm755 rar "$pkgdir/usr/bin/rar"
	install -Dm755 rar_static "$pkgdir/usr/bin/rar_static"
	install -Dm755 default.sfx "$pkgdir/usr/lib/default.sfx"
	install -Dm644 rarfiles.lst "$pkgdir/etc/rarfiles.lst"
	install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
