# Maintainer:  cuihao <cuihao dot leo at gmail dot com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

# Original PKGBUILD (extra/xfce4-session):
# $Id: PKGBUILD 150131 2012-02-13 08:12:40Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-session-devel
_pkgname=xfce4-session
pkgver=4.9.1
pkgrel=1
pkgdesc="A session manager for Xfce (Development Version)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
# keep xorg-server-utils for https://bugs.archlinux.org/task/21096
# upower and consolekit for reboot/shutdown/hibernate/suspend
depends=('gconf' 'libgnome-keyring' 'libwnck' 'libsm' 'libxfce4ui-devel>=4.9.0'
         'xorg-iceauth' 'hicolor-icon-theme' 'libxfce4util-devel>=4.9.0')
makedepends=('intltool')
optdepends=('fortune-mod: for xfce4-tips')
replaces=('xfce4-toys')
options=('!libtool')
install=$_pkgname.install
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.9/$_pkgname-$pkgver.tar.bz2)
sha1sums=('5d97dee614b661751c2f5862711ff355f67f7207')

groups=('xfce4-devel')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" "$_pkgname-git" "xfce-utils")

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --enable-libgnome-keyring \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
