# Maintainer: Que Quotion <quequotion@mailinator.com>
# Contributor: Xiao-Long Chen <chenxiaolongcxl.epac.to>

pkgname=indicator-powersave
pkgver=r93
pkgrel=1
pkgdesc="CPU frequency indicator (bzr version) with system throttle switch"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-cpufreq"
license=('GPL')
depends=('cpupower' 'libappindicator' 'python2' 'python2-dbus' 'python2-gobject' 'pygtk')
makedepends=('python2-distutils-extra')
provides=('indicator-cpufreq' 'indicator-cpufreq-bzr')
conflicts=('indicator-cpufreq' 'indicator-cpufreq-bzr')
source=("bzr+lp:indicator-cpufreq"
        'indicator-cpufreq.rules'
        '0001_Use_cpupower.patch'
        '0002_indicator.py.patch'
        '0003_indicator-cpufreq-selector.patch'
        '0004_com.ubuntu.indicatorcpufreq.policy.in.patch'
        'throttle')
sha512sums=('SKIP'
            '16997d88f7253fb1b7f0c4a7bd9e87597b6f2b859e531f038f1bde47fa531c683e44fd4b3aacdc58f75a086e9f7e5dba37b369007220b70b689d9ecad89ea391'
            '8aad00c00a95d71f221647263422eef89a0207bdb55d883520f3871f28551c316860560207c96144e22dda2ab5f5fc9b8c7bfa0fc642283de160bfd61458b8c0'
            'be501267785f3edce95bcd2088645ad56a5f39ba6bd1709892c647cd91f5f1dbfc61ac3e4a7d70f07c39a8aa688627bcbbe4c26988359b9f6637558339bf45a4'
            '8acfa969a1a0410e4e14aa3eab0ce9f6c11d72294f64c3b32307f9e88b8419ba0dd55527eb78a865b6d3a1edec0ab0ebd8239532906b897f8ced65d6dddb0185'
            '75a82890ceef22bd256eb3ebb05ad9497254d35a40de590f3c1e5d82b0afdb3b4772246ad410db52ee4a62acd5a04a662784944d187b44412d0ae36e28f2d132'
            'd02fca4406b7802ef30a5cd527f45f2505109392472cfaee76a55715f9fddbb0148ceb255ebd9401c6111c7eb83145d886ea51f6fd279f702e32e811bcae7106')

pkgver() {
  cd indicator-cpufreq

  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd "${srcdir}/indicator-cpufreq"

  patch -Np1 -i "${srcdir}/0001_Use_cpupower.patch"
  patch -Np1 -i "${srcdir}/0002_indicator.py.patch"
  patch -Np1 -i "${srcdir}/0003_indicator-cpufreq-selector.patch"
  patch -Np1 -i "${srcdir}/0004_com.ubuntu.indicatorcpufreq.policy.in.patch"
}

package() {
  cd "${srcdir}/indicator-cpufreq"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -dm700 "${pkgdir}/usr/share/polkit-1/rules.d/"
  install -m644 "${srcdir}/indicator-cpufreq.rules" \
    "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Don't install throttle script if user has a personalized copy
  if [ ! -f "/usr/local/sbin/throttle" ]; then
    install -m755 "${srcdir}/throttle" "${pkgdir}/usr/bin/"
  fi
}
