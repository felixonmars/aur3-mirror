# Maintainer:  Pedro Martinez-Julia (pdromj@um.es)
# Contributor:  Matt Monaco <net 0x01b dgbaley27>

pkgname=openvswitch
pkgver=1.9.0
pkgrel=1
pkgdesc="Production Quality, Multilayer Open Virtual Switch"
url="http://openvswitch.org"
license=(APACHE)
arch=(x86_64 i686)
install=openvswitch.install
source=(
	'http://openvswitch.org/releases/openvswitch-1.9.0.tar.gz'
	openvswitch.install
	openvswitch.tmpfiles
	ovsdb-server.service
	ovs-vswitchd.service
)
sha256sums=(
	825730db82c7b46a142f057a43345590c299e66704ef7a88d9ed4ffd0c3d6b64
	9d30ed859214f4cdeb6b6450d5c5defaf31f2c593c05201109df66fa9373ef0c
	e8dc21e50fc886bfd6aa55991bdb3cb66907e11b071045452bb12de01a3ecbd9
	5900728bca0292ec66e6da234eaa6f32f28ead6bda163d89a480eac08558c657
	c2cb5e2685240ea50b1aba4e77784f4dd66d157e7cd05a17853868aa8d8a2a45
)
depends=(openssl)
makedepends=(git python2)

build() {
	cd "$srcdir/$pkgname-$pkgver"

	./boot.sh
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-rundir=/run/openvswitch \
		PYTHON=/usr/bin/python2
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	cd "$srcdir"
	install -D openvswitch.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/openvswitch.conf"
	install -D ovsdb-server.service "$pkgdir/usr/lib/systemd/system/ovsdb-server.service"
	install -D ovs-vswitchd.service "$pkgdir/usr/lib/systemd/system/ovs-vswitchd.service"
	install -d "$pkgdir/etc/openvswitch"
	install -m 0770 -d "$pkgdir/run/openvswitch"
}
