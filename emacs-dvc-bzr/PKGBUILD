# Contributor: Richard Murri <admin@richardmurri.com> (snapshot version)
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=emacs-dvc-bzr
pkgver=591
pkgrel=1
pkgdesc="Common Emacs front-end for a number of distributed version control systems (currently arch, bazaar, git, mercurial, monotone)"
arch=('any')
url="http://download.gna.org/dvc/"
license=('GPL')
depends=('emacs')
makedepends=('bzr')
provides=('emacs-dvc')
install=dvc.install
source=()
md5sums=()
_bzrtrunk=http://bzr.xsteve.at/dvc/
_bzrmod=dvc
build() {
  cd "$srcdir"
  msg "Connecting to bazaar server"
  rm -rf dvc-build 
  if [ -d dvc/.bzr  ]; then
    msg "fetching only new files"
    cd dvc
    bzr pull ${_bzrtrunk} 
    msg "Local repository updated."
    cd ..
  else
    msg "fetching branch"
    bzr checkout  ${_bzrtrunk}     
    msg "Local repository updated."
  fi
  [ -d dvc-build ] && rm -rf dvc-build
  cp -r dvc dvc-build
  cd dvc-build
  autoconf
  ./configure --prefix="$pkgdir"/usr 
  find . -type d -exec sed -i 's+@MKDIR_P@+install+g' {}/Makefile \;
  make
}
package() {
  cd "$srcdir"/dvc-build
  install -Dm644 texinfo/dvc.info $pkgdir/usr/share/info/dvc.info
  install -d $pkgdir/usr/share/emacs/site-lisp/dvc
  cp lisp/*.{el,elc} $pkgdir/usr/share/emacs/site-lisp/dvc
  install -d $pkgdir/usr/share/doc/emacs-dvc
  cp -r docs/* $pkgdir/usr/share/doc/emacs-dvc
}
