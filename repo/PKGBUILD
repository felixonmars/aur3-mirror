# Maintainer: mastero23 <mastero23 at gmail dot com>
# Based on package repo-git by Alexis Robert <alexis.robert@gmail.com>

pkgname=repo
pkgver=1.9.1
pkgrel=1
pkgdesc="The Multiple Git Repository Tool from the Android Open Source Project"
arch=('i686' 'x86_64')
url="http://source.android.com/source/git-repo.html"
license=('APACHE')
depends=('git' 'python2')
makedepends=('git')
source=(python2-patch.diff)
md5sums=('952081f8bafa2c97f940687421c5603f')

_gitrepo="http://android.googlesource.com/tools/repo.git"
_gittag="v$pkgver"
_gitname="repo"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitrepo $_gitname
  fi

  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  git checkout "$_gittag"

  msg "Patching for python2..."
  patch -p1 < $srcdir/python2-patch.diff
}

package() {
  cd "$srcdir/$_gitname-build"

  install -D -m 755 repo "$pkgdir/usr/bin/repo"
  install -D -m 644 docs/manifest-format.txt "$pkgdir/usr/share/doc/$pkgname/manifest-format.txt"
} 
