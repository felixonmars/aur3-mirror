# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  Patrick Palka <patrick@parcs.ath.cx>
# Contributor:  Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>
# Development: http://closure.ath.cx/aur-dev

_pkgname=cgit
pkgname=cgit-git
pkgver=20120617
pkgrel=1
pkgdesc="A web interface for git written in plain C"
arch=('i686' 'x86_64')
url="http://hjemli.net/git/cgit"
license=('GPL')
depends=('openssl')
makedepends=('git' 'zlib' 'curl' 'asciidoc')
install=$_pkgname.install

source=("apache.example.conf"
        "$_pkgname.install")

_gitroot=git://hjemli.net/pub/git/cgit
_gitname=cgit

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  git submodule init
  git submodule update
  make
  make doc-man
}

package() {
  cd "$srcdir"/"$_gitname-build"
  make CGIT_SCRIPT_PATH=/usr/share/webapps/cgit DESTDIR="$pkgdir" install install-man
  install -vDm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
  install -vd "$pkgdir"/var/cache/"$pkgname"
  install -vDm0644 "$srcdir"/apache.example.conf "$pkgdir"/etc/webapps/cgit/apache.example.conf
  mkdir -p "$pkgdir"/usr/lib/cgit
  mv "$pkgdir"/usr/share/webapps/cgit/cgit.cgi "$pkgdir"/usr/lib/cgit
  ln -sf ../../../lib/cgit/cgit.cgi "$pkgdir"/usr/share/webapps/cgit/cgit.cgi
}
md5sums=('bd5696bea0654bfcf98f249b1a36737d'
         'aa05c5ed556bf30a4dd32281fd00e0c4')
