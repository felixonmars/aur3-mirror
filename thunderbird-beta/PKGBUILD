# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Want a PGO (Profile-Guided Optimization) build?: http://en.wikipedia.org/wiki/Profile-guided_optimization
_pgo="0"  # "1" to enable

pkgname=thunderbird-beta
pkgver=12.0
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite' 'startup-notification')
makedepends=('autoconf2.13' 'libgnomeui' 'libnotify' 'mesa' 'python2' 'unzip' 'yasm' 'zip')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$pkgname.install
# In case releases.mozilla.org is not responding or the file(s) aren't yet uploaded there:
# source=(http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2
        mozconfig
        $pkgname.desktop
        vendor.js
        gcc47.patch)
md5sums=(`curl -s ${source/so*}/MD5SUMS | grep source | cut -d " " -f1` # thunderbird-$pkgver.source.tar.bz2
         '41511a6bcdd04cf56706218e4670e631'  # mozconfig
         'f8a9b91202e8c85ad7b8f23fb82a4d7b'  # $pkgname.desktop
         '5a53179d14ae9631b7afe5e4d0fc0b25'  # vendor.js
         'a9a95ae860cfd37936a6d0a2b92eddab'  # gcc47.patch)

build() {
  cd comm-*

  msg2 "gcc47.patch"
  patch -Np1 -i ../gcc47.patch

  cp ../mozconfig .mozconfig

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/opt/$pkgname-$pkgver"
  export PYTHON="/usr/bin/python2"

  if [[ "$_pgo" = "1" ]]; then
     LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
     LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
     kill $! || true
  else
     make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  fi
}

package() {
  cd comm-*/obj-*
  make -j1 package MOZ_MAKE_FLAGS="$MAKEFLAGS"

  cd mozilla/dist/
  tar -x -f thunderbird-*.tar.bz2
  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r thunderbird/ "$pkgdir/opt/$pkgname-$pkgver/"
  ln -sf /opt/$pkgname-$pkgver/thunderbird "$pkgdir/usr/bin/$pkgname"

  cp "$srcdir"/vendor.js "$pkgdir/opt/$pkgname-$pkgver/defaults/pref/"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 "$srcdir"/comm-*/other-licenses/branding/thunderbird/mailicon${i/x*/}.png "$pkgdir/usr/share/icons/hicolor/$i/apps/$pkgname.png"
  done

  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  rm -rf "$pkgdir/opt/$pkgname-$pkgver/dictionaries"
  ln -sf /usr/share/hunspell "$pkgdir/opt/$pkgname-$pkgver/dictionaries"
  
  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "$pkgdir/opt/$pkgname-$pkgver/defaults/pref/channel-prefs.js"
}