# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Enable PGO on x86_64?: http://en.wikipedia.org/wiki/Profile-guided_optimization
_pgo="0"  # "1" to enable

pkgname=thunderbird-beta
pkgver=26.0b1
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="https://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libnotify' 'libvpx' 'libxt' 'mime-types' 'mozilla-common' 'nss' 'sqlite' 'startup-notification')
makedepends=('autoconf2.13' 'mesa' 'python2' 'unzip' 'wireless_tools' 'yasm' 'zip')
[ "$_pgo" = "1" ] && makedepends+=('imake' 'xorg-server-xvfb')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$pkgname.install
options=('!emptydirs' '!makeflags')
source=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2"
        'mozconfig'
        'mozconfig-pgo'
        "$pkgname.desktop"
        'vendor.js')
md5sums=(`curl -sL ${source/\/so*}/MD5SUMS | grep source | sed -n 3p | cut -d " " -f1` # thunderbird-$pkgver.source.tar.bz2
         '16887fc4de525cd624b384fe5793abfa'   # mozconfig
         'c2e9b0736543722fa7ee71aabae6c106'   # mozconfig-pgo
         'f8a9b91202e8c85ad7b8f23fb82a4d7b'   # $pkgname.desktop
         '5a53179d14ae9631b7afe5e4d0fc0b25')  # vendor.js
# # Alternative mirror:
# source[0]="http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2"

prepare() {
  cd comm-*

  if [ $CARCH = x86_64 ] && [ $_pgo = 1 ]; then
    cp "$srcdir"/mozconfig-pgo .mozconfig
  else
    cp "$srcdir"/mozconfig .mozconfig
  fi

  # configure script misdetects the preprocessor without an optimization level
  # https://bugs.archlinux.org/task/34644
  sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure
}

build() {
  cd comm-*

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/opt/$pkgname-$pkgver"
  export PYTHON="/usr/bin/python2"

  if [ $CARCH = i686 ]; then
    # Work around memory address space exhaustion during linking on i686
    LDFLAGS+=' --no-keep-memory'
    make -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  elif [ $CARCH = x86_64 ] && [ $_pgo = 0 ]; then
    make -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  else
    # Set up PGO
    export DISPLAY=:99
    Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

    if ! make -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS" MOZ_PGO=1; then
      kill $!
      return 1
    fi

    kill $! || true
  fi
}

package() {
  cd comm-*/obj-*
  make package

  cd mozilla/dist/
  tar -xf thunderbird-*.tar.bz2
  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r thunderbird/ "$pkgdir"/opt/$pkgname-$pkgver/
  ln -sf /opt/$pkgname-$pkgver/thunderbird "$pkgdir"/usr/bin/$pkgname

  install -Dm644 "$srcdir"/vendor.js "$pkgdir"/opt/$pkgname-$pkgver/defaults/preferences/vendor.js

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 "$srcdir"/comm-*/other-licenses/branding/thunderbird/mailicon${i/x*/}.png "$pkgdir"/usr/share/icons/hicolor/$i/apps/$pkgname.png
  done

  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

  rm -rf "$pkgdir"/opt/$pkgname-$pkgver/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir"/opt/$pkgname-$pkgver/dictionaries
  ln -s /usr/share/hyphen "$pkgdir"/opt/$pkgname-$pkgver/hyphenation

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "$pkgdir"/opt/$pkgname-$pkgver/defaults/pref/channel-prefs.js
}