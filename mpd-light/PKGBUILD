# $Id: PKGBUILD 149829 2012-02-11 09:25:31Z angvp $
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd-light
pkgver=0.17
pkgrel=1
pkgdesc="Music daemon that plays MP3, FLAC, and Ogg Vorbis files. Light version without jack, pulseaudio, ffmpeg, libao, libmodplug and libshout."
arch=('i686' 'x86_64' 'arm')
license=('GPL')
url="http://mpd.wikia.com/wiki/Server"
depends=('audiofile' 'libmad' 'curl' 'faad2' 'sqlite' 'libmms' 'wavpack' 'libmpcdec' 'avahi' 'libid3tag')
makedepends=('pkgconfig' 'doxygen')
provides=(mpd=${pkgver})
conflicts=('mpd')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/musicpd/mpd-$pkgver.tar.bz2"
'mpd') 

build() {
  cd "$srcdir/mpd-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-lastfm \
    --disable-jack \
    --disable-pulse \
    --enable-documentation \
    --disable-libwrap \
    --disable-sidplay \
    --disable-ao \
    --disable-mikmod \
    --disable-shout \
    --with-systemdsystemunitdir=/lib/systemd/system

  make
}

package() {
  cd "$srcdir/mpd-$pkgver"

  make DESTDIR="$pkgdir" install

  # set our dirs in mpd.conf file
  sed -i 's|^music_directory.*$|#music_directory "path_to_your_music_collection"|1' doc/mpdconf.example
  sed -i 's|playlist_directory.*$|playlist_directory "/var/lib/mpd/playlists"|1' doc/mpdconf.example
  sed -i 's|db_file.*$|db_file "/var/lib/mpd/mpd.db"|1' doc/mpdconf.example
  sed -i 's|log_file.*$|log_file "/var/log/mpd/mpd.log"|1' doc/mpdconf.example
  sed -i 's|error_file.*$|error_file "/var/log/mpd/mpd.error"|1' doc/mpdconf.example
  sed -i 's|#pid_file.*$|pid_file "/var/run/mpd/mpd.pid"|1' doc/mpdconf.example
  sed -i 's|#state_file.*$|state_file "/var/lib/mpd/mpdstate"|1' doc/mpdconf.example
  sed -i 's|#user.*$|user "mpd"|1' doc/mpdconf.example

  install -Dm644 "doc/mpdconf.example" "$pkgdir/usr/share/mpd/mpd.conf.example"

  install -Dm755 "$srcdir/mpd" "$pkgdir/etc/rc.d/mpd"
  install -d "$pkgdir"/var/{lib/mpd/playlists,log/mpd}
}

md5sums=('06be323c1655d5503912d76786284f30'
         'e5669c2bff4031928531e52475addeb1')
