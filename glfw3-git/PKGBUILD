# Maintainer: Axper Jan <483ken _at_ gmail _dot_ com
# Contributor: Daniel Kirchner <ekpyron at lavabit dot com>

pkgname=glfw3-git
pkgver=3.0.4.46.g58db862
pkgrel=1
pkgdesc="A free, open source, portable framework for OpenGL application development. (git version)"
arch=('i686' 'x86_64')
url="http://www.glfw.org/"
license=('custom:ZLIB')
depends=('libxxf86vm' 'libxi' 'libgl' 'libxrandr')
makedepends=('cmake')
conflicts=('glfw')
source=("git+git://github.com/glfw/glfw.git")
md5sums=('SKIP')

pkgver() {
	cd glfw
	local ver="$(git describe --long)"
	printf "%s" "${ver//-/.}"
}

build() {
	cd "$srcdir/glfw"
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=YES \
        -DCMAKE_C_FLAGS='-lpthread' \
        .
	make
}

package() {
	cd "$srcdir/glfw"
	make DESTDIR="$pkgdir" install
	mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
	cp "$srcdir/glfw/COPYING.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
