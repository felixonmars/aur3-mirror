# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=geppetto
pkgver=2.2.0
_revnumber=201205011340
pkgrel=1
pkgdesc="An Eclipse based toolset for developing Puppet modules and manifests."
arch=(i686 x86_64)
url="http://cloudsmith.github.com/geppetto"
license=(APACHE EPL)
depends=('java-runtime>=6' 'gtk2')
install=${pkgname}.install
changelog=Changelog
if [[ ${CARCH} = x86_64 ]];
  then _arch=x86_64; md5sums=('4e2947c882c7ff98d2212355d59b3b93');
  else _arch=x86;    md5sums=('2d9f6898586b5a1528a8a228cf611cad');
fi
source=("https://github.com/downloads/cloudsmith/${pkgname}/${pkgname}-linux.gtk.${_arch}-${pkgver}-R${_revnumber}.zip")

build() {
  msg2 "Generate desktop application entry..."
  cat > "${srcdir}"/${pkgname}.desktop << EOF
[Desktop Entry]
Version=${pkgver}
Encoding=UTF-8
Name=Geppetto
Comment=${pkgdesc}
Exec=/usr/bin/${pkgname}
Icon=/opt/${pkgname}/icon.xpm
Terminal=false
Type=Application
Categories=Development;
EOF
}

package() {
  msg2 "Install the assembly at /opt/${pkgname}..."
  install -dm755                 "${pkgdir}"/opt/${pkgname}
  cp -a "${srcdir}"/${pkgname}/* "${pkgdir}"/opt/${pkgname}

  msg2 "Install link to the executable in /usr/bin..."
  install -dm755                   "${pkgdir}"/usr/bin
  ln -s /opt/${pkgname}/${pkgname} "${pkgdir}"/usr/bin/${pkgname}

  msg2 "Install link to the config file in /etc..."
  install -dm755                       "${pkgdir}"/etc
  ln -s /opt/${pkgname}/${pkgname}.ini "${pkgdir}"/etc/${pkgname}.ini

  msg2 "Install links to copyright resources at /usr/share/licenses/${pkgname}..."
  install -dm755                     "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /opt/${pkgname}/epl-v10.html "${pkgdir}/usr/share/licenses/${pkgname}/"
  ln -s /opt/${pkgname}/notice.html  "${pkgdir}/usr/share/licenses/${pkgname}/"

  msg2 "Install desktop application entry in /usr/share/applications..."
  install -Dm644 "${srcdir}"/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
}
