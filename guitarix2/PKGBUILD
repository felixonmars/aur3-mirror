# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=guitarix
pkgname=${_name}2
pkgver=0.21.0
pkgrel=3
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack' 'fftw' 'boost-libs')
makedepends=('python2' 'boost' 'intltool')
optdepends=('meterbridge: sound meters')
conflicts=("$_name" 'gx_head')
replaces=("$_name" 'gx_head')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/$_name/$_name/$pkgname-$pkgver.tar.bz2"
        "guitarix-0.21.0-glib.patch")
md5sums=('ed40664453c9b2b61240f46ffee62f14'
         'e072f1abfe770aa6e55900e8d8bacb60')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # glib - gcc 4.7 patch
  patch -p1 -i ../${source[1]}

  # do not call both ldconfig
  # and update-desktop-database
  sed -e '/bld.env.*GX_LIB_SHARED/,+1d' \
      -e "/'install'/,/pass/d" -i wscript

  python2 waf configure --prefix=/usr \
                        --includeresampler \
                        --includeconvolver \
                        --no-faust \
                        --optimization \
                        --shared-lib
  python2 waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 waf install --destdir="$pkgdir"
}
