# Maintainer: Garrett <floft.net/contact>
pkgname=openlp-bzr
pkgver=2007
pkgrel=1
pkgdesc="Church presentation software (development version)."
arch=('any')
url='http://openlp.org/'
provides=('openlp')
conflicts=('openlp')
license=('GPLv2')
makedepends=('bzr')
depends=('qt' 'python2' 'python2-pyqt' 'python2-distribute' 'phonon' 'python2-chardet'
	'python2-lxml' 'python2-beautifulsoup3' 'python2-sqlalchemy' 'python2-sip'
	'python-pysqlite-legacy' 'python2-pyenchant' 'python2-mako' 'python2-migrate')
optdepends=('libreoffice-impress: display impress presentations')
install=openlp.install
source=(openlp.sh)
md5sums=('bb78637dd085de567dc8aea8699939bf')
sha256sums=('c85060eeb512410b8b9b9a3a638c70006f45ded0e4b551fc7135fdcd2ff915d5')

_bzrtrunk=lp:openlp
_bzrmod=openlp

build() {
  cd "$srcdir"
  
  if [ ! -d "./$_bzrmod" ]; then
    bzr co "$_bzrtrunk" "$_bzrmod"
  else
    bzr up "$_bzrmod"
  fi
  
  [ -d "./$_bzrmod-build" ] && rm -rf "./$_bzrmod-build"
  cp -r "./$_bzrmod" "./$_bzrmod-build"
  
  cd "$srcdir/$_bzrmod-build"
  python2 setup.py build
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv "$pkgdir/usr/bin/openlp"{.pyw,}
  
  echo "$pkgver" > "$pkgdir/usr/lib/python2.7/site-packages/openlp/.version"

  install -Dm0755 "$srcdir/openlp.sh" "$pkgdir/etc/profile.d/openlp.sh"
  install -Dm0644 "resources/openlp.desktop" "$pkgdir/usr/share/applications/openlp.desktop"
  install -Dm0644 "resources/images/openlp-logo.svg" "$pkgdir/usr/share/pixmaps/openlp.svg"
  install -Dm0644 "resources/images/openlp-logo-48x48.png" "$pkgdir/usr/share/pixmaps/openlp.png"

  #translations
  tsrcdir="resources/i18n"
  tdestdir="$pkgdir/usr/share/$pkgname/i18n"
  mkdir -p "$tdestdir"
  cd "$tsrcdir"
  
  for file in *.ts; do
    lconvert -i "$file" -o "$tdestdir/${file%%ts}qm"
  done
}
