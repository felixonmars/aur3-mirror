# Maintainer: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# Contributor: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# vim:set ts=2 sw=2 et ft=sh tw=100: expandtab

_cfgdir=/etc/nginx
_tmpdir=/var/lib/nginx

pkgname=nginx-devel
_pkgname=nginx
pkgver=1.3.7
pkgrel=1
pkgdesc="lightweight HTTP server and IMAP/POP3 proxy server - development version"
arch=('i686' 'x86_64')
depends=(
pcre
openssl
geoip
gd
libxslt
#google-perftools
)
makedepends=(
passenger
geoip
libatomic_ops
#google-perftools
)
optdepends=('passenger')
url="http://nginx.org"
license=('custom')
install='nginx.install'
backup=(${_cfgdir:1}/fastcgi.conf
		${_cfgdir:1}/fastcgi_params
		${_cfgdir:1}/koi-win
		${_cfgdir:1}/koi-utf
		${_cfgdir:1}/mime.types
		${_cfgdir:1}/nginx.conf
		${_cfgdir:1}/scgi_params
		${_cfgdir:1}/uwsgi_params
		${_cfgdir:1}/win-utf
		etc/logrotate.d/nginx)
#naxsi
_naxsiver=0.48
source=(
http://nginx.org/download/${_pkgname}-$pkgver.tar.gz
http://naxsi.googlecode.com/files/naxsi-core-${_naxsiver}.tgz
rc.d
service
nginx.logrotate
naxsi.rules
naxsi_core.rules
nginx.conf.example
)

conflicts=('nginx' 'nginx-unstable' 'nginx-svn')
provides=('nginx')
#options=('!emptydirs')

build() {
	cd $srcdir/${_pkgname}-${pkgver}

	./configure \
    --prefix=$_cfgdir \
    --sbin-path=/usr/sbin/nginx \
    --conf-path=$_cfgdir/nginx.conf \
    --error-log-path=/var/log/nginx/error.log \
    --pid-path=/var/run/nginx.pid \
    --lock-path=/var/lock/nginx.lock \
    \
    --user=http \
    --group=http \
    \
    --with-rtsig_module \
    --with-select_module \
    --with-poll_module \
    \
    --with-file-aio \
    --with-ipv6 \
    \
    --with-http_ssl_module \
    --with-http_realip_module \
    --with-http_addition_module \
    --with-http_xslt_module \
    --with-http_image_filter_module \
    --with-http_geoip_module \
    --with-http_sub_module \
    --with-http_dav_module \
    --with-http_flv_module \
    --with-http_mp4_module \
    --with-http_gunzip_module \
    --with-http_gzip_static_module \
    --with-http_random_index_module \
    --with-http_secure_link_module \
    --with-http_degradation_module \
    --with-http_stub_status_module \
    \
    --with-http_perl_module \
    \
    --http-log-path=/var/log/nginx/access.log \
    --http-client-body-temp-path=$_tmpdir/client-body \
    --http-proxy-temp-path=$_tmpdir/proxy \
    --http-fastcgi-temp-path=$_tmpdir/fastcgi \
    --http-uwsgi-temp-path=$_tmpdir/uwsgi \
    --http-scgi-temp-path=$_tmpdir/scgi \
    \
    --with-mail \
    --with-mail_ssl_module \
    --with-imap \
    --with-imap_ssl_module \
    \
    --with-cpp_test_module \
    \
    --with-pcre \
    --with-pcre-jit \
    --with-libatomic \
    --with-debug \
    \
    --add-module=/usr/lib/passenger/ext/nginx \
    --add-module=$srcdir/naxsi-core-${_naxsiver}/naxsi_src

#    --with-google_perftools_module \

  make
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/etc/logrotate.d/
  install -m644 "$srcdir"/nginx.logrotate "$pkgdir"/etc/logrotate.d/nginx

  sed -e 's|\<user\s\+\w\+;|user html;|g' \
    -e '44s|html|/usr/share/nginx/html|' \
    -e '54s|html|/usr/share/nginx/html|' \
    -i "$pkgdir"/etc/nginx/nginx.conf
  rm "$pkgdir"/etc/nginx/*.default

  install -d "$pkgdir"/$_tmpdir

  install -d "$pkgdir"/usr/share/nginx
  mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

  install -Dm755 "$srcdir"/rc.d "$pkgdir"/etc/rc.d/nginx
  install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/nginx.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/nginx-devel/LICENSE
  rm -rf "$pkgdir"/var/run
  #man
  install -d "$pkgdir"/usr/share/man/man8
  gzip "$srcdir"/${_pkgname}-$pkgver/man/${_pkgname}.8 -c > "${pkgdir}"/usr/share/man/man8/${_pkgname}.8.gz
  #naxsi
  install -Dm644 "$srcdir"/naxsi_core.rules "$pkgdir"/${_cfgdir}/naxsi_core.rules
  install -Dm644 "$srcdir"/naxsi.rules "$pkgdir"/${_cfgdir}/naxsi.rules
  
  install -Dm644 "$srcdir"/nginx.conf.example "$pkgdir"/${_cfgdir}/nginx.conf.example
}
md5sums=('80cb727c31c80b9afcce277b9afa2491'
         'af8344573a997caae94694ce48b08b11'
         'f62c7c9b5a53471d4666a4c49ad363fb'
         '62d494d23aef31d0b867161f9fffa6eb'
         'b38744739022876554a0444d92e6603b'
         '8510ce5ff8d3a7a710fa42e127651594'
         'cf67e931f4263830189196b2d8560502'
         '3a23f1f2478abb5847a8042a0c3b747b')
