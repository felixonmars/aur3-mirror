pkgname=mingw-w64-python-native
pkgver=3.4.0
pkgrel=1
_pybasever=34
pkgdesc="Next generation of the python high-level scripting language (native MSVC version) (mingw-w64)"
arch=('any')
license=('PSF')
url="http://www.python.org/"
provides=('mingw-w64-python')
conflicts=('mingw-w64-python')
depends=('mingw-w64-crt')
makedepends=('wine' 'mingw-w64-tools')
options=('staticlibs' '!buildflags' '!strip')
source=("http://www.python.org/ftp/python/${pkgver}/python-${pkgver}.msi"
        "http://www.python.org/ftp/python/${pkgver}/python-${pkgver}.amd64.msi")
md5sums=('e3be8a63294e42e126493ca96cfe48bd'
         'd59aaf1bb32d1bc01a051b3613ab0966')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}"
  for _arch in ${_architectures}; do
    target=""
    if test "${_arch}" = x86_64-w64-mingw32
    then
      target=".amd64"
    fi
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    msiexec /a "${srcdir}"/python-${pkgver}${target}.msi /qb TARGETDIR=$PWD
    if ! test -f libs/libpython${_pybasever}.a
    then
      gendef python${_pybasever}.dll 
      ${_arch}-dlltool --dllname python${_pybasever}.dll --def python${_pybasever}.def --output-lib libpython${_pybasever}.a
      mv libpython${_pybasever}.a libs
    fi
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    install -d "$pkgdir"/usr/${_arch}/lib
    install -m644 libs/libpython${_pybasever}.a "$pkgdir"/usr/${_arch}/lib
    install -d "$pkgdir"/usr/${_arch}/bin
    cp -r include "$pkgdir"/usr/${_arch}/
    install -m755 python${_pybasever}.dll "$pkgdir"/usr/${_arch}/bin
    install -m755 python.exe "$pkgdir"/usr/${_arch}/bin/python3.exe
    cp -r Lib "$pkgdir"/usr/${_arch}/lib/python${_pybasever} 
  done
}
