# Maintainer: Ivan de Jesús Pompa García <ivan.pompa@gmx.com>
pkgname=bamf2
_dname=bamf
pkgver=0.2.122
pkgrel=1
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library, 0.2 branch"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('dbus-glib' 'libwnck3' 'libgtop')
makedepends=('libwnck')
optdepends=('gtk2: GTK+ 2 library')
options=(!libtool)
source=(http://launchpad.net/${_dname}/0.2/${pkgver}/+download/${_dname}-${pkgver}.tar.gz)
md5sums=('4c11ee4c0b5f3bbbdd3d4b689c699d20')

conflicts=('bamf')
provides=('bamf')

build() {
  cd "$srcdir/${_dname}-${pkgver}"

  # Disable building tests
  sed -i '/tests/ d' Makefile.in

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${_dname} \
              --disable-static
  make
  popd

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/${_dname} \
              --disable-static --with-gtk=2
  make -C lib/libbamf
  popd
}

package() {
  cd "$srcdir/${_dname}-${pkgver}/build-gtk3"
  make DESTDIR="${pkgdir}/" install

  cd "$srcdir/${_dname}-${pkgver}/build-gtk2"
  make -C lib/libbamf DESTDIR="${pkgdir}" install
}
