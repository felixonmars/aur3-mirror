# Maintainer Max Nemeth <max.nemeth@gmail.com>
# Contributor: Henri HÃ¤kkinen <henuxd@gmail.com>

pkgname=pidgin-mini
pkgver=2.10.6
pkgrel=1
pkgdesc="A minimal version of Pidgin; without finch, gtkspell, avahi etc. and does not install development files"
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
depends=('gtk2' 'startup-notification' 'nss' 'hicolor-icon-theme')
makedepends=('ca-certificates' 'python2' 'intltool')
provides=("pidgin=$pkgver" "libpurple=$pkgver")
conflicts=('pidgin' 'libpurple')
options=('!libtool')
install=pidgin.install
source=(http://downloads.sourceforge.net/pidgin/pidgin-$pkgver.tar.bz2)
md5sums=('a0ebb55083e2dfc22a75c504acde6a07')

build() {
  cd "$srcdir/pidgin-$pkgver/libpurple"

  sed -i 's/python/python2/' purple-remote
  sed -i 's/python/python2/' purple-url-handler

  cd "$srcdir/pidgin-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-install \
    --disable-meanwhile --disable-nm --disable-perl --disable-gnutls \
    --disable-doxygen --disable-gtkspell --disable-gestures \
    --disable-consoleui --disable-tk --disable-tcl --disable-avahi \
    --disable-gstreamer --disable-screensaver --disable-idn --disable-mono\
    --disable-vv --with-python=/usr/bin/python2 \
    --with-system-ssl-certs=/etc/ssl/certs
  make
 }
 
 package() {
  cd "$srcdir/pidgin-$pkgver"
  make DESTDIR="$pkgdir" install

  # Remove GConf schema file
  rm -rf "$pkgdir/etc"

  # Remove libpurple and pidgin development headers
  rm -rf "$pkgdir/usr/include"

  # Remove pkgconfig scripts
  rm -rf "$pkgdir/usr/lib/pkgconfig"

  # Remove libpurple aclocal macro file
  rm -rf "$pkgdir/usr/share/aclocal"
}
