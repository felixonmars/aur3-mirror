# Contributor: Antonio Rojas

pkgname=libkscreen-frameworks
pkgver=5.0.93
plasmaver=5.0.2
pkgrel=1
pkgdesc='KDE screen management software'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libkscreen'
license=('LGPL')
depends=('qt5-x11extras' 'libxrandr')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/stable/plasma/$plasmaver/libkscreen-$pkgver.tar.xz")
md5sums=('3b504e4be29b8a9739be91f19c44c9e7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libkscreen-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
