# Maintainer: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libsoup
pkgname=lib32-libsoup
pkgver=2.32.2
pkgrel=1
pkgdesc="GNOME HTTP Library - base library (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2>=2.26.1' 'lib32-libxml2>=2.7.8' 'lib32-gnutls>=2.8.6')
makedepends=('gcc-multilib' 'lib32-glib2>=2.26.1' 'lib32-gnutls>=2.8.6' 'lib32-libxml2>=2.7.8' 'lib32-libproxy>=0.4.6' 'lib32-sqlite3>=3.7.2' 'lib32-gconf>=2.32.0' 'lib32-libgnome-keyring>=2.32.0' 'intltool')
#makedepends=('glib2>=2.26.1' 'gnutls>=2.8.6' 'libxml2>=2.7.8' 'libproxy>=0.4.6' 'sqlite3>=3.7.2' 'gconf>=2.32.0' 'libgnome-keyring>=2.32.0' 'intltool' 'gobject-introspection>=0.9.12')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.32/${_pkgbasename}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('96e6973c8b7459523c0f44e7aec69528ff2fbd388e8ddc415f91bcc42f50777f')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32 \
      --localstatedir=/var --disable-static --enable-introspection=no
  make
}

package(){
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}"/usr/lib32/libsoup-gnome-2.4.*
  rm -f "${pkgdir}/usr/lib32/pkgconfig/libsoup-gnome-2.4.pc"
  rm -f "${pkgdir}/usr/lib32/girepository-1.0/SoupGNOME-2.4.typelib"  
  rm -rf ${pkgdir}/usr/{include,share}
}
