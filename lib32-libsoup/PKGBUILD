# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libsoup
pkgname=lib32-${_pkgbase}
pkgver=2.40.2
pkgrel=1
pkgdesc="GNOME HTTP Library - base library (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gnome.org"
depends=('lib32-glib2' 'lib32-libxml2' "${_pkgbase}")
makedepends=('lib32-sqlite' 'lib32-libgnome-keyring' 'lib32-glib-networking' 'python2' 'intltool' 'gcc-multilib' 'gobject-introspection' "${_pkgbase}")
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/2.40/${_pkgbase}-${pkgver}.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('32e81220f53abb1f5bbe7d8b0717119df70667fc48e2342d82209ed1593e71dc')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libdir=/usr/lib32 --disable-tls-check
  make
}

package() {
  make -C ${_pkgbase}-${pkgver} DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib32/libsoup-gnome-2.4.* \
        "${pkgdir}"/usr/lib32/pkgconfig/libsoup-gnome-2.4.pc \
        "${pkgdir}"/usr/lib32/girepository-1.0/SoupGNOME-2.4.typelib
  rm -rf "${pkgdir}"/usr/include \
         "${pkgdir}"/usr/share
}
