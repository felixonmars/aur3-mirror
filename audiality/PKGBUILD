# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=audiality
pkgver=1.9.0
pkgrel=1
pkgdesc="A realtime audio and music engine, primarily intended for video games."
arch=('i686' 'x86_64')
url="http://www.audiality.org/"
license=('custom:zlib')
depends=('sdl')
source=("${url}download/Audiality2-$pkgver.tar.bz2")
md5sums=('24f67ab6443e72f00fd6e4d1fbb4bc30')

prepare() {
  cd Audiality2-$pkgver
  [ -d bld ] || mkdir bld

  # use absolute path for data in a2test
  sed -i "s|data/|/usr/share/$pkgname/&|" test/*.c
}

build() {
  cd Audiality2-$pkgver/bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE="Release" \
           -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd Audiality2-$pkgver/bld
  make DESTDIR="$pkgdir/" install

  # a2test
  install -Dm755 test/a2test \
    "$pkgdir/usr/bin/a2test"

  # a2test data
  install -d "$pkgdir/usr/share/$pkgname/data"
  install -Dm644 ../test/data/* \
    "$pkgdir/usr/share/$pkgname/data"

  #license
  install -Dm644 ../COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
