# Maintainer: Anthony Cornehl <accornehl@gmail.com>
# https://github.com/twinshadow/openstack-aur

_realpkgname="glanceclient"
pkgname="python2-${_realpkgname}-git"
pkgdesc="OpenStack Images API Python bindings"
pkgver=0.0.0
pkgrel=1
conflicts=("openstack-${_realpkgname}", "${_realpkgname}")
provides=("openstack-${_realpkgname}")
arch=("any")
license=("custom")
url="https://github.com/openstack/python-${_realpkgname}"
depends=(
    "python2"
    "python2-keystoneclient-git"
    "python2-prettytable"
    "python2-pyopenssl"
    "python2-simplejson"
    "python2-warlock"
)
makedepends=(
    "python2-setuptools"
    "git"
)
source=("$pkgname"::"git+https://github.com/openstack/python-${_realpkgname}")
md5sums=("SKIP")

pkgver() {
  cd "$pkgname"
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

build() {
  cd "$pkgname"
  python2 setup.py build
}

package() {
  cd "$pkgname"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${_realpkgname}/LICENSE"
}
