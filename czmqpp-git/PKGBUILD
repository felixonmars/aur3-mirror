# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=czmqpp-git
pkgver=20140316
pkgrel=1
pkgdesc="C++ wrapper for zeromq/czmq"
arch=('i686' 'x86_64')
depends=('czmq-git' 'zeromq-git')
makedepends=('autoconf' 'gcc' 'git' 'make' 'pkg-config')
url="https://github.com/darkwallet/czmqpp"
license=('LGPL3')
source=(git+https://github.com/darkwallet/czmqpp)
sha256sums=('SKIP')
provides=('czmqpp')
conflicts=('czmqpp')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  autoreconf -i
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/czmqpp \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/czmqpp \
    --localstatedir=/var/lib/czmqpp
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
