# Maintainer: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=enlightenment17-svn
pkgver=79454
pkgrel=1
pkgdesc="Enlightenment window manager DR17 (aka e17)"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('eeze-svn' 'elementary-svn' 'xcb-util-keysyms' 'hicolor-icon-theme'
         'alsa-lib' 'pam' 'systemd' 'desktop-file-utils')  
makedepends=('subversion')
conflicts=('e' 'e-svn' 'enlightenment17')
provides=('e' 'e-svn' 'enlightenment17' 'notification-daemon')
backup=('etc/enlightenment/sysactions.conf')
options=('!libtool' '!emptydirs')
install=enlightenment17.install
source=('e-applications.menu')
sha1sums=('e08cc63cb8a188a06705b42d03e032b9fcfa7ee5')

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/e"
_svnmod="e"

build() {
  cd "$srcdir"

  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"
  
  # set suspend and hibernate to use systemd
  sed -i -e 's:@SUSPEND@:systemctl suspend:g' \
         -e 's:@HIBERNATE@:systemctl hibernate:g' \
         data/etc/sysactions.conf.in

  ./autogen.sh --prefix=/usr \
	--sysconfdir=/etc \
        --libexecdir=/usr/lib/enlightenment \
	--disable-mount-hal \
	--enable-mount-eeze \

  make
}
         
package() {
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 "$srcdir/$_svnmod-build/COPYING" \
	"$pkgdir/usr/share/licenses/$pkgname/COPYING"

  install -Dm644 "$srcdir/$_svnmod-build/AUTHORS" \
	"$pkgdir/usr/share/licenses/$pkgname/AUTHORS"

# install a default applications.menu file (mostly copy from gnome-menus)
  install -Dm644 "$srcdir/e-applications.menu" \
	"$pkgdir/etc/xdg/menus/e-applications.menu"

  rm -r "$srcdir/$_svnmod-build"
}


