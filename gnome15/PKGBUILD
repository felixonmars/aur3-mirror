# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15
pkgver=0.9.2
pkgrel=1
pkgdesc="Gnome tools for the Logitech G Series Keyboards And Z-10 Speakers"
arch=(any)
url="http://www.gnome15.org"
license=('GPL')
depends=(python2 python2-imaging python2-cairo python2-dbus pygtk python2-pyinotify
         python2-lxml python2-gobject2 python2-xdg pyusb python2-rsvg python-xlib 
         hicolor-icon-theme python2-virtkey python2-pyinputevent python2-wnck
         python2-gconf python2-uinput python2-pyudev python2-keyring
         python2-setproctitle)
optdepends=('libg15-gnome15: G15 keyboard support'
            'pylibg19:       G19 keyboard support')
conflicts=('gnome15-systemtray')
install=$pkgname.install
source=(http://www.gnome15.org/downloads/Gnome15/Required/${pkgname}-${pkgver}.tar.gz
        python2.patch)
md5sums=('f469c98d57f5f4f891163805f31c0f79'
         '8aa5312149d7c5cac569857b9dd74587')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np2 -i ${srcdir}/python2.patch

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --disable-kernel-driver \
    --enable-g19direct-driver --enable-g930-driver --enable-g15direct-driver \
    --disable-indicator --enable-systemtray --disable-applet \
    --enable-udev=/usr/lib/udev/rules.d --enable-udevgroup=gnome15 PYTHON=python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
