# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15
pkgver=0.8.4
pkgrel=4
pkgdesc="Gnome tools for the Logitech G Series Keyboards And Z-10 Speakers"
arch=(any)
url="http://www.gnome15.org"
license=('GPL')
depends=(python2 python-imaging python2-cairo dbus-python pygtk python2-pyinotify
         python2-lxml pygobject pyxdg pyusb python-rsvg python-xlib 
         hicolor-icon-theme python2-virtkey python2-pyinputevent python-wnck
         python2-gconf python2-uinput0.5)
optdepends=('python2-setproctitle: Allows a process to change its title')
conflicts=('gnome15-systemtray')
install=$pkgname.install
source=(http://www.gnome15.org/downloads/Gnome15/Required/${pkgname}-${pkgver}.tar.gz
        python2.patch)
md5sums=('9a8ba4482845c87b286978affe18d78f'
         '8aa5312149d7c5cac569857b9dd74587')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np2 -i ${srcdir}/python2.patch

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  # use python2-uinput0.5 to solve "capabilities" problem
  sed -i "s/import uinput/import uinput0_5/" src/main/python/gnome15/g15screen.py
  sed -i "s/import uinput/import uinput0_5/" src/main/python/gnome15/g15profile.py
  sed -i "s/import uinput/import uinput0_5/" src/main/python/gnome15/drivers/driver_kernel.py
  sed -i "s/import uinput/import uinput0_5/" src/main/python/gnome15/drivers/driver_g15direct.py
  sed -i "s/import uinput/import uinput0_5/" src/main/python/gnome15/g15uinput.py
  sed -i "s/import uinput/import uinput0_5/" configure
  sed -i "s/uinput\./uinput0_5\./g" src/main/python/gnome15/g15uinput.py
  sed -i "s/\buinput\./uinput0_5\./g" src/main/python/gnome15/drivers/driver_g15direct.py
  sed -i "s/uinput\.capabilities/uinput0_5\.capabilities/g" src/main/python/gnome15/g15profile.py

  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --disable-kernel-driver \
    --enable-g19direct-driver --enable-g19-driver \
    --enable-g15-driver --enable-g15direct-driver \
    --disable-indicator --enable-systemtray --disable-applet \
    --enable-udev=/usr/lib/udev/rules.d --enable-udevgroup=gnome15 PYTHON=python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
