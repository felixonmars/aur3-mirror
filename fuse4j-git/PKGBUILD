# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=fuse4j-git
_pkgname=fuse4j
pkgver=20120115
pkgrel=1
pkgdesc="Re-factor of Fuse-J to support Mac OS X and fork into the background."
arch=(x86 x86_64)
url="https://github.com/dtrott/fuse4j"
license=(LGPL)
depends=('java-environment>=5' 'fuse' 'bash')
makedepends=(git maven)
changelog=Changelog
provides=(${_pkgname})
conflicts=(${_pkgname})

_gitroot=git://github.com/dtrott/${_pkgname}.git
_gitname=${_pkgname}

build() {
  msg2 "Connecting to GIT server...."
  if [ -d "${srcdir}/${_gitname}" ] ; then
    ( cd "${srcdir}/${_gitname}" && git pull origin )
    msg2 "The local files are updated."
  else
    git clone $_gitroot "${srcdir}/${_gitname}"
  fi

  msg2 "Building native libraries and executables..."
  cd "${srcdir}/${_gitname}/native"
  [[ ${CARCH} = x86_64 ]] && _jdk_arch=amd64 || _jdk_arch=i386
  cat <<EOF > make.flags
FUSE_HOME := /usr
JAVA_HOME := /opt/java
INCLUDES := -I\${JAVA_HOME}/include -I\${JAVA_HOME}/include/linux -I\${FUSE_HOME}/include/fuse
LDPATH = -L\${FUSE_HOME}/lib -L\${JAVA_HOME}/jre/lib/${_jdk_arch}/server
LIB_SO=libjavafs.so
FS_EXE=javafs
CC=gcc
DEPFLAGS=-D_FILE_OFFSET_BITS=64
CFLAGS=-Wall -g -fPIC \${DEPFLAGS}
LD=gcc -shared
LDFLAGS=-ljvm -lfuse -lpthread
EOF
  make || return 1

  msg2 "Building Java libraries..."
  cd "${srcdir}/${_gitname}/maven"
  mvn --batch-mode --update-snapshots clean install || return 1
}

package() {
  msg2 "Install native libraries in /usr/lib..."
  install -Dm755 "${srcdir}/${_gitname}/native/libjavafs.so" "${pkgdir}/usr/lib/libjavafs.so"

  msg2 "Install java libraries in /usr/share/java/${_pkgname}..."
  install -Dm644 "${srcdir}"/${_gitname}/maven/capi-generator/target/capi-generator-*.jar   "${pkgdir}/usr/share/java/${_pkgname}/capi-generator.jar"
  install -Dm644 "${srcdir}"/${_gitname}/maven/fuse4j-core/target/fuse4j-core-*.jar         "${pkgdir}/usr/share/java/${_pkgname}/fuse4j-core.jar"
  install -Dm644 "${srcdir}"/${_gitname}/maven/fuse4j-fakefs/target/fuse4j-fakefs-*.jar     "${pkgdir}/usr/share/java/${_pkgname}/fuse4j-fakefs.jar"
  install -Dm644 "${srcdir}"/${_gitname}/maven/fuse4j-staticfs/target/fuse4j-staticfs-*.jar "${pkgdir}/usr/share/java/${_pkgname}/fuse4j-staticfs.jar"
  install -Dm644 "${srcdir}"/${_gitname}/maven/fuse4j-zipfs/target/fuse4j-zipfs-*.jar       "${pkgdir}/usr/share/java/${_pkgname}/fuse4j-zipfs.jar"

  msg2 "Install executables in /usr/bin..."
  install -Dm755 "${srcdir}/${_gitname}/native/javafs" "${pkgdir}/usr/bin/javafs"

  msg2 "Install documentation resources at /usr/share/doc/${pkgname}..."
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a "${srcdir}"/${_gitname}/{extra,README,TODO,TROUBLESHOOTING.txt} "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
