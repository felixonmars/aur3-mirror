# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=scrotwm
pkgver=0.9.30
pkgrel=1
pkgdesc="A minimalistic dynamic tiling window manager that tries to stay out of the way."
arch=('i686' 'x86_64')
url="http://www.scrotwm.org"
license=('custom:ISC')
depends=('dmenu' 'libxrandr' 'profont')
optdepends=('scrot: screenshots' 'xlockmore: screenlocking' 'terminus-font: great font')
backup=(etc/scrotwm.conf)
source=(http://opensource.conformal.com/snapshots/$pkgname/$pkgname-$pkgver.tgz \
	LICENSE \
        scrotwm.desktop \
        baraction.sh)
md5sums=('008d018a0ab546b4915e5aa7199f8982'
         'a67cfe51079481e5b0eab1ad371379e3'
         '2d6d6a434ee043336884d5ce2ef732ee'
         '6132ba773ee00f39d8f67ac0347a9814')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # it is like a patch, only less fragile
  sed -i 's|\"/usr/local/lib/libswmhack.so\"|\"libswmhack.so\"|' scrotwm.c
  sed -i 's/# modkey = Mod1/modkey = Mod4/' scrotwm.conf
  sed -i 's/-\*-terminus-medium-\*-\*-\*-\*-\*-\*-\*-\*-\*-\*-\*/-*-profont-*-*-*-*-12-*-*-*-*-*-*-*/' scrotwm.conf

  cd linux
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/linux"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  cd ..
  install -Dm644 scrotwm.conf "$pkgdir/etc/scrotwm.conf"
  install -Dm755 screenshot.sh "$pkgdir/usr/share/scrotwm/screenshot.sh"
  cd ../..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 scrotwm.desktop "$pkgdir/usr/share/xsessions/scrotwm.desktop"
  install -Dm755 baraction.sh "$pkgdir/usr/share/scrotwm/baraction.sh"

  ln -s /usr/lib/libswmhack.so.0.0 "$pkgdir/usr/lib/libswmhack.so.0"
  ln -s /usr/lib/libswmhack.so.0.0 "$pkgdir/usr/lib/libswmhack.so"
}
