# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-doc-utils
pkgver=20130101
pkgrel=1
pkgdesc="Documentation utilities for MATE"
url="https://github.com/mate-desktop/mate-common"
arch=('any')
license=('GPL' 'LGPL')
depends=('libxslt' 'python2' 'docbook-xml' 'rarian' 'gnome-doc-utils')
makedepends=('pkgconfig' 'intltool' 'mate-common')
#conflicts=('gnome-doc-utils')
options=('!emptydirs' '!libtool')
source=()
sha256sums=()
groups=('mate')

_gitroot=git://github.com/mate-desktop/mate-doc-utils.git
_gitname=mate-doc-utils

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  PYTHON=/usr/bin/python2 ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --localstatedir=/var \
    --disable-scrollkeeper || return 1
  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install || return 1

  # files with conflicts with gnome-doc-utils
  rm -rf "${pkgdir}/usr/share/xml/mallard/"
  rm "${pkgdir}/usr/share/pkgconfig/xml2po.pc"
  rm -rf "${pkgdir}/usr/share/man/"
  rm -rf "${pkgdir}/usr/lib/"
  rm "${pkgdir}/usr/bin/xml2po"
}
