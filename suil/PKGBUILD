# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=suil
pkgver=0.4.4
pkgrel=3
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs."
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('lv2core')
makedepends=('python2' 'lv2-ui' 'qt' 'gtk2')
optdepends=('qt: Qt UIs wrapping support'
            'gtk2: Gtk2 UIs wrapping support')
conflicts=("$pkgname-svn")
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('ace6a58b4e7b748e1506184760502b63')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  DESTDIR="$pkgdir" python2 ./waf install

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
