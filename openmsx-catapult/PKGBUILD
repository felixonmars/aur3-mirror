# Contributor: Tom < reztho at archlinux dot us >
pkgname=openmsx-catapult
pkgver=0.9.1
pkgrel=1
pkgdesc="Front-end for openMSX: the MSX emulator that aims for perfection."
arch=('i686' 'x86_64')
url="http://openmsx.sourceforge.net"
license=('GPL')
depends=('libxml2' 'wxgtk' 'zlib' 'libjpeg' 'libpng' 'libtiff' "openmsx=${pkgver}")
makedepends=('python2')
source=("http://downloads.sourceforge.net/${pkgname/-catapult/}/${pkgname}-${pkgver}.tar.gz"
	"openmsx-catapult-main_mk.patch")

build() {
  cd ${srcdir}/${pkgname}-${pkgver/_/-}

  # Patching build/main.mk so catapult can be installed in $pkgdir
  patch -p1 < ${srcdir}/openmsx-catapult-main_mk.patch

  # Catapult requires python2
  sed -i 's@=python@=python2@' build/main.mk

  # Changing some default configurations...
  sed -i 's@SYMLINK_FOR_BINARY:=true@SYMLINK_FOR_BINARY:=false@' build/custom.mk
  sed -i 's@/opt/openMSX-Catapult@/usr/share/openmsx-catapult@' build/custom.mk
  sed -i 's@/opt/openMSX/bin/openmsx@/usr/bin/openmsx@' build/custom.mk
  sed -i 's@/opt/openMSX/share@/usr/share/openmsx@' build/custom.mk
  echo 'INSTALL_DOC_DIR:=/usr/share/doc/openmsx-catapult' >> build/custom.mk
  echo 'INSTALL_SHARE_DIR:=/usr/share/openmsx-catapult' >> build/custom.mk
  echo 'INSTALL_BINARY_DIR:=/usr/bin' >> build/custom.mk
 
  # Compiling
  make
  mkdir -p ${pkgdir}/usr/share/applications
  make DESTDIR=${pkgdir} install

  # Fixing the .desktop file
  sed -i 's@/usr/share/openmsx-catapult/bin/catapult@/usr/bin/catapult@' \
  ${pkgdir}/usr/share/applications/openMSX-Catapult.desktop
  sed -i 's@/usr/share/openmsx-catapult/doc/@/usr/share/doc/openmsx-catapult/@' \
  ${pkgdir}/usr/share/applications/openMSX-Catapult.desktop
}

md5sums=('39741454167994df253a5eda45688fc9'
         '7417eddba99d34fd6445619ef13c1c28')
