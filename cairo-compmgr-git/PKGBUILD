# Maintainer: Cedric Girard <girard.cedric@gmail.com>
# Contributor: catalyst <catalyst@xaker.ru>
# Contributor: Jacob Winther <jacob.winther@gmail.com>

pkgname=cairo-compmgr-git
pkgver=20121026
pkgrel=1
pkgdesc="Cairo based composite manager - Git version"
arch=(i686 x86_64)
url="http://cairo-compmgr.tuxfamily.org/"
license=(LGPL3)
conflicts=(cairo-compmgr)
provides=(cairo-compmgr)
makedepends=(gettext git gtk-doc "intltool>=0.41" gconf)
depends=("gtk2>=2.16.0" "vala>=0.16" libsm libgl)
options=(!makeflags
         !libtool)

_gitroot="git://git.tuxfamily.org/gitroot/ccm/cairocompmgr.git"
_gitname="cairocompmgr"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server..."
  if [[ -d $_gitname ]]; then
    (cd $_gitname && git pull )
  else
    git clone $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $_gitname-build
  cp -r $_gitname $_gitname-build
  cd $_gitname-build

  ./autogen.sh --prefix=/usr LIBS="-ldl -lgmodule-2.0 -lm"

  make
}

package() {
  cd $_gitname-build

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/gconf/schemas/"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/cairo-compmgr.schemas" "$pkgdir/etc/gconf/schemas/*.schemas"
  rm -rf "$pkgdir/etc/gconf/schemas/"

}
