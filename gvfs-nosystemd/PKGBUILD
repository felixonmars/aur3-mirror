pkgname='gvfs-nosystemd'
pkgver=1.20.2
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('avahi' 'dbus-glib' 'fuse' 'intltool' 'libarchive' 'libcdio-paranoia' 'libgphoto2' 'libimobiledevice' 'libsoup' 'smbclient' 'udisks2' 'libsecret' 'docbook-xsl' 'gtk3' 'libmtp' 'gnome-online-accounts' 'libbluray')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/gvfs/${pkgver%.*}/gvfs-$pkgver.tar.xz)
pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio (without systemd dependency)"
depends=('avahi' 'dconf' 'fuse' 'libarchive' 'libcdio-paranoia' 'libsoup' 'udisks2' 'libsecret' 'libbluray')
replaces=('gvfs-obexftp')
provides=("gvfs=$pkgver")
conflicts=('gvfs')
optdepends=('gvfs-afc: AFC (mobile devices) support'
		  'gvfs-smb: SMB/CIFS (Windows client) support'
		  'gvfs-gphoto2: gphoto2 (PTP camera/MTP media player) support'
		  'gvfs-afp: Apple Filing Protocol (AFP) support'
		  'gvfs-mtp: MTP device support'
		  'gvfs-goa: gnome-online-accounts support'
		  'gtk3: Recent files support')
install=gvfs.install


build() {
  cd "gvfs-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gvfs \
      --with-bash-completion-dir=/usr/share/bash-completion/completions \
      --disable-obexftp \
      --disable-systemd
  make
}

package() {
  cd "gvfs-$pkgver"
  sed -e 's/^am__append_4/#am__append_4/' \
      -e 's/^am__append_5/#am__append_5/' \
      -e 's/^am__append_6/#am__append_6/' \
      -e 's/^am__append_7/#am__append_7/' \
      -i monitor/Makefile
  make DESTDIR="$pkgdir" install

  cd "$pkgdir"
  rm usr/lib/gvfs/gvfsd-{smb,smb-browse,afc,afp,afp-browse,gphoto2,mtp}
  rm usr/share/gvfs/mounts/{smb,smb-browse,afc,afp,afp-browse,gphoto2,mtp}.mount
  rm usr/share/glib-2.0/schemas/org.gnome.system.smb.gschema.xml
  rm usr/share/GConf/gsettings/gvfs-smb.convert
}

sha256sums=('84d9412e7bedfba902696ae7fb0cfceb0b66b98823f8388745b20693e3b24f8e')
