# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: Artyom Olshevskiy <siasia@siasia>

pkgname=java-service-wrapper
pkgver=3.5.20
pkgrel=1
pkgdesc="Enables a Java Application to be run as a Windows Service or Unix Daemon"
url="http://wrapper.tanukisoftware.com"
arch=('i686' 'x86_64' 'armv6h')
license=('GPL2')
conflicts=('java-service-wrapper-bin')
makedepends=('apache-ant' 'java-environment')
source=("http://wrapper.tanukisoftware.com/download/${pkgver}/wrapper_${pkgver}_src.tar.gz")
sha256sums=('69bb81932a49a64bb462ce629b8f12c34e92b5c2ef5fcd953a7c9e3f35ff6915')

prepare() {
    source /etc/profile.d/apache-ant.sh
    source /etc/profile.d/jdk.sh
    #sed -i 's:value="1.4":value="1.7":' "${srcdir}/wrapper_${pkgver}_src/build.xml"
}

build() {
    cd "${srcdir}/wrapper_${pkgver}_src"

    [[ "$CARCH" = "x86_64" ]] && _bits=64    || _bits=32
    [[ "$CARCH" = arm*     ]] && _arch=armhf || _arch=x86

    ant -Dbits=${_bits} -Ddist.arch=${_arch} jar compile-c-unix
}

package() {
    cd "${srcdir}/wrapper_${pkgver}_src"
    install -Dm644 doc/wrapper-community-license-1.1.txt "${pkgdir}/usr/share/licenses/java-service-wrapper/LICENSE"
    install -Dm755 bin/wrapper       "${pkgdir}/usr/bin/java-service-wrapper"
    install -Dm644 lib/libwrapper.so "${pkgdir}/usr/lib/java-service-wrapper/libwrapper.so"
    install -Dm644 lib/wrapper.jar   "${pkgdir}/usr/share/java/wrapper-${pkgver}.jar"
    ln -s /usr/share/java/wrapper-${pkgver}.jar "${pkgdir}/usr/share/java/wrapper.jar"
}
