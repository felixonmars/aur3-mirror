# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: Artyom Olshevskiy <siasia@siasia>

pkgname=java-service-wrapper
pkgver=3.5.24
pkgrel=1
pkgdesc="Enables a Java Application to be run as a Windows Service or Unix Daemon"
url="http://wrapper.tanukisoftware.com"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL2')
conflicts=('java-service-wrapper-bin')
makedepends=('apache-ant' 'java-environment')
source=("http://wrapper.tanukisoftware.com/download/${pkgver}/wrapper_${pkgver}_src.tar.gz")
sha256sums=('d205b322eb1de47e4f8a2db65d7bf95448d8ba8f787aabd5af23cf4463c7a793')

prepare() {
    local _ver
    source /etc/profile.d/apache-ant.sh

    source /etc/profile.d/jdk.sh 2>/dev/null &&
        _ver=1.7 || {
    source /etc/profile.d/jdk6.sh &&
        _ver=1.6
    }

    sed -i "s:value=\"1.4\":value=\"${_ver}\":" "${srcdir}/wrapper_${pkgver}_src/build.xml"
}

build() {
    cd "${srcdir}/wrapper_${pkgver}_src"

    [[ "$CARCH" = "x86_64" ]] && _bits=64    || _bits=32
    [[ "$CARCH" = arm*     ]] && _arch=armhf || _arch=x86

    ant -Dbits=${_bits} -Ddist.arch=${_arch} jar compile-c-unix
}

package() {
    cd "${srcdir}/wrapper_${pkgver}_src"
    install -Dm644 doc/wrapper-community-license-1.1.txt "${pkgdir}/usr/share/licenses/java-service-wrapper/LICENSE"
    install -Dm755 bin/wrapper       "${pkgdir}/usr/bin/java-service-wrapper"
    install -Dm644 lib/libwrapper.so "${pkgdir}/usr/lib/java-service-wrapper/libwrapper.so"
    install -Dm644 lib/wrapper.jar   "${pkgdir}/usr/share/java/wrapper-${pkgver}.jar"
    ln -s /usr/share/java/wrapper-${pkgver}.jar "${pkgdir}/usr/share/java/wrapper.jar"
}
