# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
pkgname=gnome-boxes
pkgver=3.4.2
pkgrel=1
pkgdesc="A simple GNOME 3 application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Boxes"
license=('LGPL2.1')
depends=('gtk3' 'clutter-gtk' 'spice-gtk3' 'libvirt-glib' 'libtracker-sparql'
         'gtk-vnc' 'udev' 'libosinfo' 'dconf' 'hicolor-icon-theme'
         'desktop-file-utils' 'shared-mime-info')
optdepends=('qemu-kvm-spice: to create new VMs')
install=gnome-boxes.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5459027e10d518d88fd903da986efbbc163d040b9cb4f53cbbf9654dcc493742')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
