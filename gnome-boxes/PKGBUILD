# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
pkgname=gnome-boxes
pkgver=3.6.2
pkgrel=1
pkgdesc="A simple GNOME 3 application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Boxes"
license=('LGPL2.1')
depends=('gtk3' 'clutter-gtk' 'spice-gtk3' 'libvirt-glib' 'tracker'
         'gtk-vnc' 'udev' 'libosinfo' 'dconf' 'hicolor-icon-theme'
         'desktop-file-utils' 'shared-mime-info')
optdepends=('qemu-kvm-spice: to create new VMs')
makedepends=('intltool')
install=gnome-boxes.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('8ac18585fd29770961a39a13de38809fe6175d432666aa1feb81587edfddbd64')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --libexec=/usr/lib/gnome-boxes
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
