# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: hybraries <macwolf@archlinux.de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Navi <navitwo.at.gmail.dot.com>
# Contributor: rabyte <rabyte.at.gmail.dot.com>
# Contributor: Johannes Krampf <wuischke.at.amule.dot.org>

pkgname=midori-bzr
pkgver=6352
pkgrel=1
pkgdesc='Lightweight web browser based on WebKit and GTK3 (development version)'
arch=('x86_64' 'i686')
url='http://twotoasts.de/index.php?/pages/midori_summary.html'
license=('LGPL')
depends=('libzeitgeist' 'webkitgtk3' 'libnotify' 'libxss' 'hicolor-icon-theme' 'desktop-file-utils' 'libunique3' 'gcr') # 'granite'
makedepends=('pkg-config' 'bzr' 'python2' 'libxml2' 'gtk3' 'intltool' 'python2-docutils' 'libsoup' 'vala' 'librsvg')
optdepends=('aria2: download utility'
            'steadyflow: download manager')
provides=('midori')
replaces=('midori-git')
conflicts=('midori' 'midori-gtk2-git' 'midori-git' 'midori-gtk2-bzr')
options=('!emptydirs')
install='midori.install'
source=("midori::bzr+http://bazaar.launchpad.net/~midori/midori/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/midori"

  bzr revno
}

build() {
  cd "$srcdir/midori"

  ./configure \
    --prefix=/usr \
    --enable-gtk3 \
    --disable-granite
  make
}

package() {
  cd "$srcdir/midori"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
