# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=nautilus-elementary-bzr
_pkgname=nautilus
pkgver=171
pkgrel=2
pkgdesc="Nautilus patched for simplicity"
arch=('i686' 'x86_64')
url="https://launchpad.net/nautilus-elementary"
license=('GPL')
depends=('clutter-gtk2' 'desktop-file-utils' 'exempi' 'gnome-desktop2' 'gvfs' 'libexif' 'vte')
makedepends=('bzr' 'gnome-common' 'gobject-introspection' 'gtk-doc' 'intltool')
optdepends=('gloobus-preview-bzr: for preview support'
            'ne-terminal-config: for embedded terminal')
provides=(${_pkgname}=3.0.1.1)
conflicts=(${_pkgname} ${_pkgname}-elementary)
options=('!libtool' '!emptydirs')
install=${pkgname}.install

_bzrtrunk="lp:${_pkgname}-elementary"
_bzrmod="${_pkgname}-elementary"

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
              --libexecdir=/usr/lib/nautilus \
              --disable-update-mimedb \
              --disable-packagekit \
              --disable-appindicator
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/"${_pkgname}".schemas \
    --domain "${_pkgname}" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}

