# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-bluetooth
pkgver=1.6.0
pkgrel=6
pkgdesc="A collection of Bluetooth utilities and modules for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'gtk2' 'gvfs' 'libnotify' 'libunique'
         'obex-data-server')
makedepends=('gobject-introspection' 'mate-common' 'mate-doc-utils'
             'mate-file-manager-sendto' 'perl-xml-parser')
optdepends=('mate-user-share: Enable sharing via bluetooth and other protocols.')
options=('!emptydirs')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('fc8810f16a1b70bd24dd329b505d932f5b672a56')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-introspection \
        --disable-scrollkeeper \
        --disable-desktop-update \
        --disable-icon-update
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
