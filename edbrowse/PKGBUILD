# Maintainer: Chris Brannon <cmbrannon (at) cox.net>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>
# Contributor: Chuck Boucher <jofywyxaxukyvofa (at) tempomail.fr>
pkgname=edbrowse
pkgver=3.4.9
pkgrel=1
pkgdesc="Editor, browser, and mail client based on /bin/ed"
arch=('i686' 'x86_64')
url="http://the-brannons.com/edbrowse/"
depends=('js185' 'openssl' 'pcre' 'curl>=7.17.0' 'readline')
license=('GPL' 'openssl')
source=("http://the-brannons.com/${pkgname}/${pkgname}-${pkgver}.zip")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"

  make STRIP=''
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"

  make DESTDIR="$pkgdir" prefix=/usr install
  # setup.ebrc is a shell script that builds a config file for you.  It's
  # in doc/.
  install -m755 ../doc/setup.ebrc "${pkgdir}/usr/bin"

  # How much of the documentation should be installed?
  cd ../doc
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 usersguide.html philosophy.html sample.ebrc "$pkgdir/usr/share/doc/edbrowse"
}
md5sums=('ab0db08fb6d871705c54551662ad4c10')
