# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
#
# BETA: https://litecointalk.org/index.php?topic=18608.0
# pooler PGP key: gpg --recv-key 6FC4C9F7F1BE8FEA

pkgname=electrum-ltc
pkgver=1.9.8.4
pkgrel=5
pkgdesc="Lightweight Litecoin wallet"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-pbkdf2' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-pyqt4' 'python2-qrcode' 'python2-requests' 'python2-sip' 'python2-slowaes' 'python2-tlslite' 'qt4' 'sip')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools')
optdepends=('desktop-file-utils: update desktop database'
            'python2-ltc_scrypt: much faster block verification'
            'python2-rpyc: send commands to Electrum-LTC Python console from an external script'
            'python2-zbar: QR code reading support')
url="https://electrum-ltc.org"
license=('GPL3')
source=("https://electrum-ltc.org/download/Electrum-LTC-${pkgver}.tar.gz"{,.asc}
        setup.py.patch)
md5sums=('39d63fa6994e1445c84ad969a17b8405'
         'SKIP'
         '081fc1f6723fe8419cefdfe40f0301dd')
sha256sums=('e64cbbe8b11c503689da168e2a0fb27d9c69d03030b1b74bd27f0d8ccde00552'
            'SKIP'
            '7e8be72fe219baf8c03a3fd3c081eac680ad4f57b56cd5184827ea7cbcce2703')
install=electrum-ltc.install

prepare() {
  cd "$srcdir/Electrum-LTC-$pkgver"

  msg 'Patching setup.py...'
  patch -p1 < "$srcdir/setup.py.patch"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  msg 'Changing electrum.mo to electrum-ltc.mo...'
  find . -type f -print0 | xargs -0 sed -i 's#electrum.mo#electrum-ltc.mo#g'
}

build() {
  cd "$srcdir/Electrum-LTC-$pkgver"

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd "$srcdir/Electrum-LTC-$pkgver"

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}
