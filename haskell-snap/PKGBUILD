# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Arch Haskell Team <>

pkgname=haskell-snap
pkgver=0.9.2.2
_hkgname=snap
_licensefile=LICENSE
pkgrel=1
pkgdesc="Snap: A Haskell Web Framework: project starter executable and glue code library"
url="http://snapframework.com/"
license=('BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-aeson'
         'haskell-attoparsec'
         'haskell-bytestring'
         'haskell-cereal'
         'haskell-clientsession'
         'haskell-configurator'
         'haskell-containers'
         'haskell-data-lens'
         'haskell-data-lens-template'
         'haskell-directory'
         'haskell-directory-tree'
         'haskell-filepath'
         'haskell-hashable'
         'haskell-heist'
         'haskell-logict'
         'haskell-monadcatchio-transformers'
         'haskell-mtl'
         'haskell-mwc-random'
         'haskell-old-time'
         'haskell-pwstore-fast'
         'haskell-snap-core'
         'haskell-snap-server'
         'haskell-stm'
         'haskell-syb'
         'haskell-template-haskell'
         'haskell-text'
         'haskell-time'
         'haskell-transformers'
         'haskell-unordered-containers'
         'haskell-utf8-string'
         'haskell-vector'
         'haskell-vector-algorithms'
         'haskell-xmlhtml')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('5cffd4c80b9afac3566192a631ae3e08aad808bd316a4ef9478d6595166289e2')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}

