# Maintainer: kfgz <kfgz at interia dot pl>
# Previous Maintainer: Geoffroy Carrier <geoffroy dot carrier at aur dot archlinux dot org>
# Previous Maintainer: Jason Chu <jason at archlinux dot org>
# Contributor: Charles Ghislain <charlyghislain at gmail dot com>
pkgname=jdk-dev
_jdkver=7
# in order to update the PKGBUILD usually only the following two lines need to be changed + the md5sums
_jdkbuild=b145
_jdkdate=07_jun_2011

pkgver=${_jdkver}${_jdkbuild}
pkgrel=1
pkgdesc="Sun's Java Development Kit, snapshot release"
arch=(i686 x86_64)
  [ "${CARCH}" = "i686" ]   && ARCH=i586
  [ "${CARCH}" = "x86_64" ] && ARCH=x64

_filename=jdk-${_jdkver}-ea-bin-${_jdkbuild}-linux-${ARCH}-${_jdkdate}.tar.gz
url="http://jdk7.java.net/"
depends=(j2re)
install=jdk.install
source=(http://www.java.net/download/jdk7/archive/${_jdkbuild}/binaries/${_filename}
	jdk.profile
	jdk.profile.csh
	java-control-panel.desktop
	java-monitoring-and-management-console.desktop
	java-policy-settings.desktop
	java-visualvm.desktop
	java-web-start.desktop)
	
if [ "${CARCH}" = "x86_64" ]; then 
   md5sums[0]=5f1dfe3d99c3705022c146510099cba4
  else
   md5sums[0]=9f63912030d36fd3ebc430a9fd22b232
fi

md5sums[1]=c9342ba526a48400dbab6d38a112c395
md5sums[2]=dadf31f20d7c01f1f4b0a2adb9da5fd6
md5sums[3]=2e6499954d3a36ae3da610204b915547
md5sums[4]=4bc2b0be93f76ed07b471e3c89eb5a8a
md5sums[5]=8e3cec7ab641ec449cabd068be583f5c
md5sums[6]=9ba148ebabce8ed3351b813a9a66b6e5
md5sums[7]=568451cf45fc8c7adb83a222a61ca2c6

replaces=(j2sdk)
conflicts=(java-environment j2sdk)
provides=(java-environment=7 j2sdk jdk)
license=(custom)

package() {
  cd ${srcdir}/jdk1.7.0

  #copy icon
  install -Dm644 jre/lib/desktop/icons/hicolor/48x48/apps/sun-java.png \
                 ${pkgdir}/usr/share/pixmaps/java.png

  rm -r jre

  install -dm755 ${pkgdir}/opt
  cp -ar ${srcdir}/jdk1.7.0 ${pkgdir}/opt/java/
  rm ${pkgdir}/opt/java/src.zip

  install -D -m755 ${srcdir}/jdk.profile ${pkgdir}/etc/profile.d/jdk.sh
  install -D -m755 ${srcdir}/jdk.profile.csh ${pkgdir}/etc/profile.d/jdk.csh

  install -dm755 ${pkgdir}/usr/share/licenses/jdk
  install -D -m644 ${pkgdir}/opt/java/COPYRIGHT ${pkgdir}/usr/share/licenses/jdk

  #desktop entries
  install -Dm644 ${srcdir}/java-control-panel.desktop \
                 ${pkgdir}/usr/share/applications/java-control-panel.desktop
                 
  install -Dm644 ${srcdir}/java-monitoring-and-management-console.desktop \
                 ${pkgdir}/usr/share/applications/java-monitoring-and-management-console.desktop
                 
  install -Dm644 ${srcdir}/java-policy-settings.desktop \
                 ${pkgdir}/usr/share/applications/java-policy-settings.desktop
  
  install -Dm644 ${srcdir}/java-visualvm.desktop \
                 ${pkgdir}/usr/share/applications/java-visualvm.desktop
                 
  install -Dm644 ${srcdir}/java-web-start.desktop \
                 ${pkgdir}/usr/share/applications/java-web-start.desktop
}
