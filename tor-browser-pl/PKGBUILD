# Maintainer: Piotr Rogoza <piotr dot r dot public at gmail dot com>
# Contributor: Max Roder <maxroder@web.de>

# To port this PKGBUILD to another language of tor-browser you
# have to change $pkgname, $_language, $pkgdesc and $url in PKGBUILD
# AND (!) the first line in the .install file!

pkgname=tor-browser-pl
pkgver=4.0.3
pkgrel=1
_language=pl
pkgdesc='Tor Browser Bundle: Anonymous browsing using firefox and tor'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
depends=(bash)
optdepends=(
'zenity: simple dialog boxes'
'kdebase-kdialog: KDE dialog boxes'
'libnotify: Gnome dialog boxes'
)
install="${pkgname}.install"
#CARCH=i686
if [ "$CARCH" == x86_64 ]; then
_pkgarch=64
sha256sums=('f3ca93b38f9b30b398547ef59e247e1a8690f29438c1cb441ef0534a504e968c'
            'SKIP'
            '3d5fc01f2cfbae0a00b7117b0b0a24028d1686e6f81a347809f74de2d8522ff7'
            '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
            'f25fedfa77b19a96488529e6d387411fb49a82e99e3a1a9708b88ad8b6e7d2aa')
elif [ "$CARCH" == "i686" ]; then
_pkgarch=32
sha256sums=('062e5577eba9370c303b627ff2df51c2f1ee3ece85a373038ecc273c28f0b299'
            'SKIP'
            '3d5fc01f2cfbae0a00b7117b0b0a24028d1686e6f81a347809f74de2d8522ff7'
            '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
            'f25fedfa77b19a96488529e6d387411fb49a82e99e3a1a9708b88ad8b6e7d2aa')
fi
source=(
"https://torproject.org/dist/torbrowser/$pkgver/tor-browser-linux${_pkgarch}-${pkgver}_${_language}.tar.xz"
"https://torproject.org/dist/torbrowser/$pkgver/tor-browser-linux${_pkgarch}-${pkgver}_${_language}.tar.xz.asc"
"${pkgname}.desktop"
"${pkgname}.png"
"${pkgname}.sh"
)
validpgpkeys=(8738A680B84B3031A630F2DB416F061063FEE659) # Erinn Clark
noextract=("tor-browser-linux${_pkgarch}-${pkgver}_${_language}.tar.xz")
package() {
    cd "$srcdir"

    sed -i "s/REPL_NAME/${pkgname}/g"       ${pkgname}.sh
    sed -i "s/REPL_VERSION/${pkgver}/g"     ${pkgname}.sh
    sed -i "s/REPL_LANGUAGE/${_language}/g" ${pkgname}.sh

    sed -i "s/REPL_NAME/${pkgname}/g"       ${pkgname}.desktop
    sed -i "s/REPL_LANGUAGE/${_language}/g" ${pkgname}.desktop
    sed -i "s/REPL_COMMENT/${pkgdesc}/g"    ${pkgname}.desktop

    install -Dm 644 ${pkgname}.desktop      ${pkgdir}/usr/share/applications/${pkgname}.desktop
    install -Dm 644 ${pkgname}.png          ${pkgdir}/usr/share/pixmaps/${pkgname}.png
    install -Dm 755 ${pkgname}.sh           ${pkgdir}/usr/bin/${pkgname}

    install -Dm 644 tor-browser-linux${_pkgarch}-${pkgver}_${_language}.tar.xz ${pkgdir}/opt/${pkgname}/tor-browser-linux${_pkgarch}-${pkgver}_${_language}.tar.xz
}
