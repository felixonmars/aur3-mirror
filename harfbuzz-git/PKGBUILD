#Contributor:	Spyros	Stathopoulos	<gmail.com: foucault.online>
#Maintainer:	Jesse	Jaara		<gmail.com: jesse.jaara>

pkgname=harfbuzz-git
pkgver=0.9.14.8.g269de14
pkgrel=1
pkgdesc="HarfBuzz is an OpenType text shaping engine."
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('icu'  'cairo' 'graphite')
makedepends=('ragel' 'gtk-doc')
provides=('harfbuzz=0.9.14')
conflicts=('harfbuzz')
options=(!libtool)
source=('harfbuzz::git://anongit.freedesktop.org/harfbuzz')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/harfbuzz"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${srcdir}/harfbuzz"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/harfbuzz"

  mkdir -p "${pkgdir}/usr/share/licenses/harfbuzz-git"
  cp COPYING "${pkgdir}/usr/share/licenses/harfbuzz-git/"

  make DESTDIR="${pkgdir}" install
}

