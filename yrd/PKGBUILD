# Maintainer: Kevin MacMartin <prurigro@gmail.com>

pkgname=yrd
pkgver=0.2
pkgrel=2
pkgdesc='A cjdns config tool for humans and cyborgs'
url='https://github.com/kpcyrd/yrd'
license=('GPL3')
arch=('any')
depends=('python2-argh' 'python2-requests' 'cjdns-git' 'systemd')
install=$pkgname.install
source=("$url/archive/v$pkgver.tar.gz"
        "$pkgname.tmpfiles.conf")
sha512sums=('45821f04170dcb24c9b4bf0659f77b1ea9bd4302288e68fe5d44f5179f792efedce8532fd92423cf5303cd36615d6e65743a80da59a27eab3cd4408eaf0c0a84'
            '45d48ed3306d60daca9583ed657521ca5ffef039d167b68aa8c595474a4c3e0f910afe8abd3e7b86f3b16588e8f25161fd4b6c55802c68944c4a31de084d85ef')

prepare() {
  # Configure cjdroute.conf to be located @ /etc/cjdroute.conf
  cd $pkgname-$pkgver
  sed -i 's|/var/lib/yrd/cjdroute.conf|/etc/cjdroute.conf|' $pkgname.py
}

package() {
  # Install the tmpfiles.d config
  install -Dm644 $pkgname.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  # Install the main executable
  cd $pkgname-$pkgver
  install -Dm755 $pkgname.py "$pkgdir/usr/lib/python2.7/site-packages/$pkgname/$pkgname"
  # Install the rest of the python files
  install -m644 *.py "$pkgdir/usr/lib/python2.7/site-packages/$pkgname/"
  # Install a symlink to the main executable in /usr/bin
  install -dm755 "$pkgdir/usr/bin"
  ln -sf /usr/lib/python2.7/site-packages/$pkgname/$pkgname.py "$pkgdir/usr/bin/$pkgname"
  # Install the README.md to the shared docs folder
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
