# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=audacious-plugins-gtk2
_pkgalias=audacious-plugins
pkgver=3.2.2
pkgrel=1
pkgdesc='Plugins for Audacious'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
conflicts=('audacious-plugins')
provides=('audacious-plugins')
license=('GPL')

makedepends=("audacious-gtk2>=${pkgver}")
optdepends=('libpulse: PulseAudio output plugin'
            'mpg123: MP3 plugin'
            'pango: Audacious OSD plugin'
            'libxcomposite: Audacious OSD X Composite Support'
            'libnotify: notify plugin'
            'libbinio: AdPlug plugin'
            'libvorbis: Ogg Vorbis input plugin and FileWriter Vorbis output part'
            'flac: flac input plugin and FileWriter FLAC output part'
            'wavpack: WavPack input plugin'
            'faad2: aac plugin'
            'libsndfile: sndfile extensions'
            'libmodplug: ModPlug plugin'
            'ffmpeg: ffaudio plugin'
            'jack2: JACK output plugin'
            'libsidplay: SID input plugin'
            'oss: OSS4 output plugin'
            'alsa-lib: ALSA output plugin and amini-plug ALSA backend'
            'sdl: SDL output plugin'
            'fluidsynth: MIDI FluidSynth backend input'
            'libcdio: cdaudio-ng input plugin'
            'curl: Scrobbler plugin'
            'neon: neon HTTP support'
            'libmms: mms support'
            'smbclient: SMB transport'
            'libmtp: mtp upload support'
            'libcue: cue support'
            'lame: FileWriter MP3 output part'
            'libbs2b: BS2B effect plugin'
            'libsamplerate: resample effect plugin')

source=("http://distfiles.audacious-media-player.org/$_pkgalias-$pkgver.tar.bz2")
md5sums=('7aaf8ad389a2983baddbdd57207e0724')

_ods=()
_tmpfile="$pkgdir/.tmp"

add_ods_pc()
{
    pkg-config $1 && echo "$3" >> "$_tmpfile" || _ods=("${_ods[@]}" $2)
}

add_ods_file()
{
    [ -e "$1" ] && echo "$3" >> "$_tmpfile" || _ods=("${_ods[@]}" $2)
}

unset_optdepend()
{
    for _idx in ${!optdepends[@]}
    do
        if [[ "${optdepends[$_idx]}" =~ ^$1:* ]]
        then
            unset optdepends[$_idx]
            break
        fi
    done
}

build()
{
    cd "$srcdir/$_pkgalias-$pkgver"

    rm -rf "$_tmpfile"
    add_ods_pc libpulse '--disable-pulse' libpulse
    add_ods_pc libmpg123 '--disable-mp3' mpg123
    add_ods_pc pango '--disable-aosd' pango
    add_ods_pc 'pango xcomposite' '--disable-aosd-xcomp' 'pango libxcomposite'
    add_ods_pc libnotify '--disable-notify' libnotify
    add_ods_pc libbinio '--disable-adplug' libbinio
    add_ods_pc vorbis '--disable-vorbis --disable-filewriter_vorbis' libvorbis
    add_ods_pc flac '--disable-flacng --disable-filewriter_flac' flac
    add_ods_pc wavpack '--disable-wavpack' wavpack
    add_ods_file /usr/include/neaacdec.h '--disable-aac' faad2
    add_ods_pc sndfile '--disable-sndfile' libsndfile
    add_ods_pc libmodplug '--disable-modplug' libmodplug
    add_ods_pc libavcodec '--disable-ffaudio' ffmpeg
    add_ods_pc "jack >= 1.9.7" '--disable-jack' jack2
    add_ods_file /usr/include/sidplay/sidtune.h '--disable-sid' libsidplay
    add_ods_file /etc/oss.conf '--disable-oss4' oss
    add_ods_pc alsa '--disable-alsa --disable-amidiplug-alsa' alsa-lib
    add_ods_pc sdl '--disable-sdlout' sdl
    add_ods_pc fluidsynth '--disable-amidiplug-flsyn' fluidsynth
    add_ods_pc libcdio '--disable-cdaudio' libcdio
    add_ods_pc libcurl '--disable-scrobbler' curl
    add_ods_pc neon '--disable-neon' neon
    add_ods_pc libmms '--disable-mms' libmms
    add_ods_file /usr/include/libsmbclient.h '--disable-smb' smbclient
    add_ods_pc libmtp '--disable-mtp_up' libmtp
    add_ods_pc libcue '--disable-cue' libcue
    add_ods_file /usr/bin/lame '--disable-filewriter_mp3' lame
    add_ods_pc libbs2b '--disable-bs2b' libbs2b
    add_ods_pc samplerate '--disable-resample' libsamplerate

    ./configure --prefix=/usr \
        --disable-oss \
        --disable-sdltest \
        --disable-gtk3 \
        "${_ods[@]}"
    make
}

package()
{
    cd "$srcdir/$_pkgalias-$pkgver"
    make DESTDIR="$pkgdir" install

    for _ch in $(cat "$_tmpfile")
    do
        if [ 1 -gt 0 ]; then depends=("${depends[@]}" "$_ch"); fi
        unset_optdepend "$_ch"
    done
    optdepends=("${optdepends[@]}")
    rm -rf "$_tmpfile"
}
