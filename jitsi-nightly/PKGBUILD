# Maintainer: David Zaragoza <david@zaragoza.com.ve>
# Contributors: Keshav P R, atommix aka Aleks Lifey, Xavion, Ananda Samaddar, Dan Serban, Xavier Devlamynck

pkgname=jitsi-nightly
pkgver=1.1.4192
pkgrel=1
pkgdesc="An audio/video SIP VoIP phone and instant messenger written in Java (formerly SIP-Communicator)"
arch=('i686' 'x86_64')
url="http://jitsi.org"
license=('LGPL')
provides=jitsi
depends=('java-runtime')
makedepends=('apache-ant' 'java-environment')
conflicts=(jitsi jitsi-stable)
options=(!strip !emptydirs zipman !libtool docs)
source=("http://download.jitsi.org/jitsi/nightly/src/jitsi-src-1.1-nightly.build.${pkgver##*.}.zip"
	jitsi.desktop
	jitsi.sh)
md5sums=('322ff2e0b662a0239df02ca4f1602f4a'
         'aad7cf1fb18ff5d7c964834ecc38aed8'
         'f02e076047f80d26123987b3ba45dd2b')

build()
{
	cd ${srcdir}/jitsi
	# append the build revision to the jitsi version
	sed -i "s/0\.build\.by\.SVN/build.${pkgver##*.}/" src/net/java/sip/communicator/impl/version/NightlyBuildID.java
	. /etc/profile.d/apache-ant.sh
	ant rebuild
}

package() {
	cd ${srcdir}/jitsi
	find lib/ lib/bundle/ -maxdepth 1 -type f \
		-exec install -Dm644 {} "${pkgdir}/usr/lib/jitsi/"{} \;
	find lib/os-specific/linux/ -maxdepth 1 -type f \
		-execdir install -Dm644 {} "${pkgdir}/usr/lib/jitsi/lib/"{} \;
	shopt -sq extglob
	find lib/native/linux$(sed 's/_/-/g' <<<${CARCH/#*(i?86|x86)/})/ -maxdepth 1 -type f \
		-execdir install -Dm644 {} "${pkgdir}/usr/lib/jitsi/lib/native/"{} \;
	find sc-bundles/{,os-specific/linux/} -maxdepth 1 -type f \
		-execdir install -Dm644 {} "${pkgdir}/usr/lib/jitsi/sc-bundles/"{} \;
	install -Dm755 "$srcdir/jitsi.sh" "${pkgdir}/usr/bin/jitsi"
	install -Dm644 "$srcdir/jitsi.desktop" "${pkgdir}/usr/share/applications/jitsi.desktop"
	local _file
	for _file in resources/install/debian/*.{svg,xpm}; do
		install -Dm644 "$_file" "${pkgdir}/usr/share/pixmaps/jitsi${_file/*sip-communicator/}"
	done
}
