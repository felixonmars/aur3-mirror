# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Michael Witten <mfwitten>

langname=flash
appname=${langname}player
basename=${langname}plugin
ver1=10
ver2=3
ver3=183
ver4=18
pkgname=${basename}-${ver1}
pkgver=${ver1}.${ver2}.${ver3}.${ver4}
dirver=${ver1}_${ver2}r${ver3}_${ver4}
pkgrel=1
pkgdesc="Adobe Flash Player v10 (32-bit) - with GPU hardware acceleration"
arch=("i686" "x86_64")
url="http://kb2.adobe.com/cps/142/tn_14266.html#main_Archived_versions"
license=("custom")
if [ "${CARCH}" == "i686" ]; then
	depends=("mozilla-common" "hicolor-icon-theme" "libxt" "gtk2" "nss" "curl" "alsa-lib")
	optdepends=("libvdpau: video hardware decoding for supporting players")
elif [ "${CARCH}" == "x86_64" ]; then
	depends=("mozilla-common" "hicolor-icon-theme" "lib32-libxt" "lib32-gtk2" "lib32-nss" "lib32-curl" "lib32-alsa-lib")
	optdepends=("lib32-libvdpau: video hardware decoding for supporting players"
				"nspluginwrapper: make this plugin work with 64-bit browsers"
				"firefox32: 32-bit Firefox for 64-bit systems")
fi
provides=("${basename}=${pkgver}")
conflicts=("${basename}")
options=(!strip)
install="${pkgname}.install"
backup=("etc/adobe/mms.cfg")
archive="fp_${pkgver}_archive.zip"
source=("http://fpdownload.macromedia.com/get/${appname}/installers/archive/${archive}"
		"mms.cfg")

package() {
	cd "${srcdir}"

	# Variables
	local directory tarball licfile;
	directory="${archive%.*}"
	tarball=${directory}/${dirver}/${appname}${dirver}_linux.tar.gz
	licfile=${directory}/readme.txt

	# Directories
	install -d -m755 "${pkgdir}/usr/lib/mozilla/plugins"
	install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -d -m755 "${pkgdir}/etc/adobe"

	# Files
	unzip -p "${archive}" "${tarball}" | tar -zx -C "${pkgdir}/usr/lib/mozilla/plugins" lib${appname}.so
	unzip -p "${archive}" "${licfile}" > "${pkgdir}/usr/share/licenses/${pkgname}/readme.txt"
	install -Dm644 "${srcdir}/mms.cfg" "${pkgdir}/etc/adobe/mms.cfg"
}

sha1sums=('fea6fd5d78e570d3354932d5ef6a2b7d90d7ad99'
          '4b5fb41986cf78628dc50050cfb847f3021cbe47')
