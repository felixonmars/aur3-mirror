# Maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: Ari Mizrahi <codemunchies@gmail.com>

pkgname=ntopng
pkgver=1.1_6932
pkgrel=1
pkgdesc='The next generation version of the original ntop, a network traffic probe that shows the network usage'
arch=('x86_64' 'i686')
url='http://www.ntop.org/'
license=('GPL3')
options=('!makeflags')
depends=('redis' 'libxml2' 'geoip' 'libpcap' 'libnet' 'lua' 'zeromq' 'gd'
         'gdbm' 'pcre' 'rrdtool' 'psutils')
source=("http://downloads.sourceforge.net/project/ntop/ntopng/ntopng-$pkgver.tgz"
        ntopng@.service)
md5sums=('7d38e1e764a4cb4bd399556bb98f2086'
         '5d3dd12fd77756138f45769e31b67505')

build() {
  cd "$srcdir/ntopng-$pkgver"
  ./configure --prefix=/usr
  make geoip
  make
}

package() {
  cd "$srcdir/ntopng-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/ntopng"
  install -dm755 "$pkgdir/usr/lib/systemd/system"

  # Bin.
  install -m755 ntopng "$pkgdir/usr/share/ntopng"

  # Docs.
  cp --no-preserve=ownership -R httpdocs scripts "$pkgdir/usr/share/ntopng"

  # Systemd service.
  install -m644 "$srcdir/ntopng@.service" "$pkgdir/usr/lib/systemd/system"

  cat > "$pkgdir/usr/bin/ntopng" <<EOF
#!/bin/sh
cd /usr/share/ntopng
./ntopng "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/ntopng" 
}
