# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: thacrazze <thacrazze|googlemail|com>

pkgname=clipgrab
pkgver=3.2.0.7
pkgrel=1
pkgdesc='A video downloader and converter for YouTube, Veoh, DailyMotion, MyVideo, ...'
arch=('i686' 'x86_64')
url='http://clipgrab.de/en'
license=('GPL3')
depends=('qtwebkit')
optdepends=('ffmpeg: for some encodings')
source=(http://clipgrab.de/download/$pkgname-$pkgver.tar.bz2
        $pkgname.desktop)
md5sums=('e5f1a63015270e26675042da0151f7a4'
         '9a141791f6952917c441050c3dac81ce')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix build
  for _i in {gpl,qt,ffmpeg}; do
    ln -s logo.png logo-$_i.png
  done

  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
