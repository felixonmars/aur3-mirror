pkgname=higan-git
pkgver=093.4e2eb23
pkgrel=1
pkgdesc="A Nintendo multi-system emulator formerly known as bsnes"
arch=('i686' 'x86_64')
url=http://byuu.org/higan/
license=(GPL)
replaces=('bsnes')
conflicts=('higan')
makedepend=('libao')
depends=('openal' 'libgl')
optdepends=('alsa' 'pulseaudio' 'sdl')
source=('higan::git+git://gitorious.org/bsnes/bsnes.git'
        '001-gcc-fix.patch'
        'Higan')
install=higan.install
_profiles="accuracy balanced performance"
 
pkgver() {
        cd "higan"
        echo 093.$( git describe --always | sed 's#-#_#g;s#v##' )
}
 
prepare(){
        cd "${srcdir}/higan/nall"
        patch -p0 -i ${srcdir}/001-gcc-fix.patch
        cd "${srcdir}/higan"
        sed -i "s/flags   += -I. -O3 -fomit-frame-pointer/flags   += -I. -fomit-frame-pointer -std=gnu++11/
         s/flags += -march=native/flags += $CXXFLAGS/g" Makefile
}
 
build(){
# libananke
        cd "${srcdir}/higan"
        make compiler=g++ platform=x flags="$CXXFLAGS -I.. -fomit-frame-pointer -std=gnu++11" -C ananke
 
# higan
        cd "${srcdir}/higan"
        make clean
        for _profile in ${_profiles}; do
                make compiler=g++ platform=x target=ethos profile=${_profile}
                mv out/higan{,-${_profile}}
        done
}
 
package(){
# Install higan
        cd "${srcdir}/higan"
 
# Common files
        install -dm 755 "${pkgdir}"/usr/{bin,lib,share/{applications,pixmaps,higan/Video\ Shaders}}
        install -m 755 "${srcdir}"/Higan "${pkgdir}"/usr/bin/higan
        install -m 644 "${srcdir}"/higan/data/higan.desktop "${pkgdir}"/usr/share/applications/higan.desktop
        install -m 644 "${srcdir}"/higan/data/higan512.png "${pkgdir}"/usr/share/pixmaps/higan.png
        cp -dr --no-preserve=ownership profile/* data/cheats.bml "${pkgdir}"/usr/share/higan/
        cp -dr --no-preserve=ownership shaders/*.shader "${pkgdir}"/usr/share/higan/Video\ Shaders/
 
# libananke
        install -m 644 "${srcdir}"/higan/ananke/libananke.so "${pkgdir}"/usr/lib/libananke.so.1
        cd "${pkgdir}"/usr/lib/
        ln -s libananke.so.1 libananke.so
 
# higan
        cd "${srcdir}/higan"
        for _profile in ${_profiles}; do
                install -m 755 {out,"${pkgdir}"/usr/bin}/higan-${_profile}
        done
 
# Fix permissions
        find "${pkgdir}"/usr/share/higan/ -type d -exec chmod 755 {} +
        find "${pkgdir}"/usr/share/higan/ -type f -exec chmod 644 {} +
}

md5sums=('SKIP'
         '44ae6f98a892db0bd9fc7af435e6225f'
         '8f51b56d8584c3f833d51e8e26a0f139')
