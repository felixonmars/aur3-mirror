# Contributor: noonov <noonov@gmail.com>

pkgname=jd-svn
pkgver=4038
pkgrel=1
pkgdesc="A 2channel browser written in C++ using gtkmm"
arch=('i686' 'x86_64')
url="http://jd4linux.sourceforge.jp/"
license=('GPL2')
depends=('libsm' 'libgcrypt' 'gtkmm')
makedepends=('subversion')
provides=('jd')
conflicts=('jd')
source=()
md5sums=()

_svntrunk="http://svn.sourceforge.jp/svnroot/jd4linux/jd/trunk"
_svnmod="jd"

build() {
  cd ${srcdir}

  msg "Connecting to SVN server..."
  if [[ -d ${_svnmod}/.svn ]]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_svnmod}-build
  cp -r ${_svnmod} ${_svnmod}-build

  cd ${srcdir}/${_svnmod}-build

  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${_svnmod}-build

  make DESTDIR=${pkgdir} install
}
