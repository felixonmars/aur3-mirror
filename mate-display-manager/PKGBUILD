# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-display-manager
pkgver=20120308
pkgrel=1
pkgdesc="The MATE Display Manager"
arch=(i686 x86_64)
license=('GPL')
depends=('pam' 'libdmx' 'mate-panel' 'libmatecanvas' 'librsvg' 'gksu' 'dbus-glib' 'consolekit' 'mate-dialogs')
makedepends=('pkgconfig' 'intltool' 'mate-doc-utils' 'xtrans' 'xorg-server')
# do not add !emptydirs
options=('!libtool')
install=mate-display-manager.install
url="http://matsusoft.com.ar/projects/mate/"
groups=('mate-extras')
source=(mdm
        mdm.pam
        mdm-autologin.pam)
sha256sums=('512de7022bd88d14cfe84b43bc29dc6f211fec28ba76e0e71470799c4292f036'
            'ab7931f226d437900000daee29fa33a8d3fd5ccd1f9b30522d269e8a699372f4'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5')
backup=('etc/pam.d/mdm' 'etc/pam.d/mdm-autologin' 'etc/mdm/custom.conf')

_gitroot=git://github.com/mate-desktop/mate-display-manager.git
_gitname=mate-display-manager

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"


    ./autogen.sh --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/mdm \
        --localstatedir=/var \
        --disable-static \
        --with-at-spi-registryd-directory=/usr/lib/at-spi \
        --disable-scrollkeeper \
        --without-tcp-wrappers \
        --disable-debug || return 1

    make
}

package() {
	cd "$srcdir/$_gitname-build"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas

    # fix for wrong path to executables
    _path=${pkgdir}/usr/share/mdm/autostart/LoginWindow

    sed -i 's_/usr/lib/mdm/_/usr/lib/polkit-mate/_' ${_path}/polkit-mate-authentication-agent-1.desktop
    sed -i 's_/usr/lib/mdm/__' ${_path}/mate-settings-daemon.desktop


    install -m644 "${srcdir}/mdm.pam" "${pkgdir}/etc/pam.d/mdm"
    install -m644 "${srcdir}/mdm-autologin.pam" "${pkgdir}/etc/pam.d/mdm-autologin"

    install -m755 -d "${pkgdir}/etc/rc.d"
    install -m755 "${srcdir}/mdm" "${pkgdir}/etc/rc.d/"

    #rmdir "${pkgdir}/var/mdm"
    #mdkir "${pkgdir}/var/log/mdm"
    #chmod 1770 "${pkgdir}/var/log/mdm"
    #rm -rf "${pkgdir}/var/run"
}
