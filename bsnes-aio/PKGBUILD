# Maintainer : Alucryd <alucryd at gmail dot com>
# Thx vEX for the Qt 4.8.0 fix

pkgname=bsnes-aio
pkgver=088
pkgrel=2
pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code. Includes snespurify, bps, filters, shaders and cheats database."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
makedepends=('mesa')
source=('http://bsnes.googlecode.com/files/bsnes_v088-source.tar.xz' 'http://byuu.org/files/bps_v03.tar.bz2' 'bsnes.sh')
md5sums=('a3b2e9ba28b752768bb9f777049b1239'
         '65c425a5843556bef6f333c4084af8c6'
         'd1288d0ed6f42f09c1052ed64acff473')
provides=('bsnes' 'purify' 'bps')
conflicts=('bsnes' 'snesfilter')

# By default, the script will compile an accuracy build using a GTK interface without the debugger. Comment/uncomment the following variables to suit your preferences.

# Profiles: Accuracy (slower), Compatibility, or Performance (faster)
_profile=accuracy
#_profile=compatibility
#_profile=performance

# Interface: GTK or QT
depends=('gtk2' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
_interface=gtk
#depends=('qt>=4.7.0' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
#_interface=qt

# Debugger
#_debug=true
_debug=false

build() {
# QT 4.8.0 fix
  if [ $_interface == "qt" ]
  then
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp
  fi

# Compile laevateinn
  cd "${srcdir}/bsnes_v$pkgver-source"
  if [ $_debug == "true" ]
  then
    cp -R bsnes laevateinn
    cd laevateinn
    make compiler=gcc platform=x phoenix=$_interface profile=accuracy target=debugger
  fi    

# Compile bsnes
  cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
  make compiler=gcc platform=x phoenix=$_interface profile=$_profile target=ui

# Compile purify
  cd "${srcdir}/bsnes_v$pkgver-source/purify"
  sed -i 's|gtk+-2.0|gtk+-2.0 x11|g' Makefile
  make compiler=gcc platform=x phoenix=$_interface

# Compile filters
  cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
  make compiler=gcc platform=x

# Compile bps
  cd "${srcdir}/bps"
  if [ $_interface == "qt" ]
  then
    sed -e 's|g++-4.5|g++|' -i cc-qt.sh
    ./cc-qt.sh
  else
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh
  fi
}

package() {
  cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
  sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' target-ui/Makefile
  sed -i 's|sudo install|install|g' target-ui/Makefile
  make target=ui install DESTDIR="${pkgdir}" prefix=/usr

  cd "${srcdir}/bsnes_v$pkgver-source"
  if [ $_debug == "true" ]
  then
    cd laevateinn
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' target-debugger/Makefile
    sed -i 's|sudo install|install|g' target-debugger/Makefile
    make target=debugger install DESTDIR="${pkgdir}" prefix=/usr
    rm -rf $pkgdir/usr/share/laevateinn
  fi
    
  cd "${srcdir}/bsnes_v$pkgver-source/purify"
  install -Dm755 purify "${pkgdir}/usr/bin/purify"
    
  cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
  sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
  make install DESTDIR="${pkgdir}" prefix=/usr

  cd "${srcdir}/bsnes_v$pkgver-source/snesshader"
  sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
  make install DESTDIR="${pkgdir}" prefix=/usr

  cd "${srcdir}/bps"
  if [ $_interface == "qt" ]
  then
    install -Dm755 bps-qt "${pkgdir}/usr/bin/bps"
  else
    install -Dm755 bps-gtk "${pkgdir}/usr/bin/bps"
  fi

# Tweak to copy shares to ~./config
  install -Dm755 "${srcdir}/bsnes.sh" "${pkgdir}/usr/bin/bsnes.sh"
  sed -i 's|Exec=bsnes|Exec=/usr/bin/bsnes.sh|g' "${pkgdir}/usr/share/applications/bsnes.desktop"
}
