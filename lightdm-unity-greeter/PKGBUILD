# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu4
_translations=20130502
pkgver=13.10.1
pkgrel=100
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'ido' 'libindicator3' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'vala0.18' 'archlinux-artwork' 'imagemagick' 'librsvg' 'intltool')
options=('!libtool')
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}-${_ubuntu_rel}.diff.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-unity-greeter.tar.gz"
        '10-unity.defaults'
        '50-unity-greeter.rules'
        '0001_autoconf.patch'
        'logo.png')
sha512sums=('d42d7d29d0476a39d57ca4305740e9204254640d886cc24ef9e8a41525f8c697ca8f407ba7a4dc518d2d894fa66dc2edfba29f8a1498367257351edb8168d2e0'
            '32fad97048eb30e7ac0e3e5d93d289cb11bbf4579edad94965aee6f92fa362327b52621a25b9a520f2a1c06c2cf89931d6850b3e273aba515e1298cfe5a6395c'
            '58b41942f06f13b90a356d493608df9723be9b8442af9a07a9fd56a3f1db8855d98c11a879887f18abe66de67b0b306f57102b6a6424b694bc90a9bc59640b8d'
            'ee5d1f17dddd99ba55bbaca8aff5e8487c9b9f7e1eef570d2adb3d2519e19ef437b160414468ef85a8c1b14af1eee23c714e2086291701edfbab2f43064e2cd7'
            '5d0f1b8221dfe02670df3ba88011dc0ce744bfd4205a9900dd8096de222358a740a384a47f13ed4e5b94e24d4a8ff5639117464c61a689dd7bd9025a0900f529'
            'c31d52509484f26361de343f0bd53cba97ebe55ec7f028ec2afa166fec2f6d073e172788b5fcb2cd9bf7f4f2bab4a1f2268d30c3cefbbbd3e62948f64d0b1272'
            '4cc7e3600a8f5afc7edf574ccdb21dbbff9c7492a46b50c696fb7b38446829f2cc99ae029688e289da2100a9cac6ae0471cc8bac03f8db3799f9929a1e2f2679')

prepare() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  # Apply Ubuntu patches
  patch -p1 -i "${srcdir}"/${pkgname#*-}_${pkgver}-${_ubuntu_rel}.diff
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  # Use Arch Linux logo (retain the same 245px height as the Ubuntu logo)
  rm data/logo.png
  # Stupid segfaulting imagemagick...
  if ! convert -background none -resize 245 \
         /usr/share/archlinux/logos/archlinux-mono-white.svg \
         data/logo.png; then
    cp "${srcdir}/logo.png" data/logo.png
  fi

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/unity-greeter-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  # Link against libm and libx11
  export CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/lightdm

   make
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install PolicyKit file for allowing the lightdm user to use NetworkManager
  # Note: PolicyKit no longer reads pkla files after version 0.107, so Ubuntu's
  # policy file won't work.
  install -dm700 "${pkgdir}/usr/share/polkit-1/rules.d/"
  install -m644 "${srcdir}/50-unity-greeter.rules" \
    "${pkgdir}/usr/share/polkit-1/rules.d/"
  #install -dm700 "${pkgdir}/var/lib/polkit-1/"
  #install -dm755 "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"
  #install -m644 "${srcdir}/debian/unity-greeter.pkla" \
  #  "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"

  # Install default GSettings/dconf settings for the guest account (requires
  # lightdm-ubuntu).
  install -dm755 "${pkgdir}/etc/guest-session/gsettings/"
  install -m644 "${srcdir}/10-unity.defaults" \
    "${pkgdir}/etc/guest-session/gsettings/"

  # Install LightDM configuration file to set the Unity greeter as the default
  install -dm755 "${pkgdir}/etc/lightdm/lightdm.conf.d/"
  install -m644 debian/50-unity-greeter.conf \
                "${pkgdir}/etc/lightdm/lightdm.conf.d/"
}

# vim:set ts=2 sw=2 et:
