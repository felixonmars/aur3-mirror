# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu1
pkgver=0.2.8.${_ubuntu_rel}
pkgrel=100
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'libindicator' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'vala>=0.16.0' 'autoconf')
options=('!libtool')
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}.orig.tar.gz"
        "valaflags.patch")
sha512sums=('b9e3b2a122280c211b3a6465d25ff906b0ef47f1c25ac7bb9d43058b8fe9325bce5993b68b8d126691abf607c5dabd1735d53b422e0f0359bfc2f10bc3716687'
            '4cad59e1ab444f40398d649b4ba33e19445e49f4faf58a2ed916814795e0c7e7fda528683064820ca6811c02a58df3cc74bb3284403605b8efff624117258ed8'
            'e231780d657abbd2c9990acc3eb03ec5628c8bcfd0ef42c459a1e30a9227e0377c687a977e1d510ec2c0d375963469752fdd9c7bff686c8fd9b8feeabd55a7ca')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Patch by ecopunk to fix FTBFS on some systems
  #patch -Np1 -i "${srcdir}/valaflags.patch"

  # Link against libm and libx11
  CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
