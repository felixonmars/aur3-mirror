# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu2
pkgver=0.2.7.${_ubuntu_rel}
pkgrel=101
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-doc-utils' 'lightdm-ubuntu' 'gnome-common' 'libindicator' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
makedepends=('gnome-settings-daemon' 'pkg-config' 'vala>=0.16.0' 'autoconf')
options=(!libtool)
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}.orig.tar.gz"
        "valaflags.patch")
sha512sums=('3db7f1e2cef54e9c4bc3503c780c480dc8c03bcff527ccfd0282d349cb322a5511287723f7bd89b1dd5f7e77192801f4bb41123f44df9291ab3a83fe5a75bbbf'
            'e250a9e44790c6740e74e370c4bf7d8e74815a6946a5b7a07eabfc958f05d1482bd55027ddae2f817eee149df6454538569f437ba1ae12c20558c1897900b6a5'
            'e231780d657abbd2c9990acc3eb03ec5628c8bcfd0ef42c459a1e30a9227e0377c687a977e1d510ec2c0d375963469752fdd9c7bff686c8fd9b8feeabd55a7ca')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Patch by ecopunk to fix FTBFS on some systems
  patch -Np0 -i "${srcdir}/valaflags.patch"

  # Link against libm and libx11
  CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install
}
