# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=redeclipse-server
pkgver=1.4
pkgrel=1
pkgdesc="A single-player and multi-player first-person ego-shooter, built as a total conversion of Cube Engine 2 - server only"
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('custom' 'CCPL' 'ZLIB' 'MIT')
depends=('bash' 'gcc-libs' 'zlib')
conflicts=('redeclipse')
source=(http://downloads.sourceforge.net/redeclipse/redeclipse_${pkgver}_nix.tar.bz2)
md5sums=('3d478f5cf4838adcf5cde8d3421f2ffe')

build() {
  cd "$srcdir/redeclipse-$pkgver/src"

  make server
}

package() {
  cd "$srcdir/redeclipse-$pkgver/src"

  make DESTDIR="$pkgdir" prefix=/usr system-install-server
  make DESTDIR="$pkgdir" prefix=/usr system-install-docs

  # remove unneeded client man page
  rm -f $pkgdir/usr/share/man/man6/redeclipse.6.gz

  install -Dm644 ../doc/license.txt   "$pkgdir/usr/share/licenses/redeclipse-server/LICENSE"
  install -Dm644 ../doc/trademark.txt "$pkgdir/usr/share/licenses/redeclipse-server/TRADEMARK"

  # workaround for finding data directory
  install -Dm644 ../data/config/keymap.cfg "$pkgdir/usr/share/redeclipse/data/config/keymap.cfg"
  ln -sf /usr/share/redeclipse/data "$pkgdir/usr/lib/redeclipse/data"

  install -Dm644 $srcdir/../redeclipse.service "$pkgdir/usr/lib/systemd/system/redeclipse.service"
}
