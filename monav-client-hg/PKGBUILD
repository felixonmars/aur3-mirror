# Maintainer: Micael Dias <kam1kaz3@gmail.com>
pkgname=monav-client-hg
_pkg="${pkgname%-hg}"
pkgver=611
pkgrel=1
pkgdesc="A Desktop/Mobile application that offers fast and exact routing with OpenStreetMap data."
arch=("i686" "x86_64")
url="http://code.google.com/p/monav/"
license=("GPL3")
depends=("qt-mobility" "proj")
makedepends=("mercurial")
provides=("$_pkg")
conflicts=("$_pkg")

_hgrepo="$_pkg"
_hgroot=https://monav.googlecode.com/hg/

build() {
  cd "$srcdir"

  rm -rf "${srcdir}/${_hgrepo}-build"
  cd "${srcdir}/${_hgrepo}" && hg archive "${srcdir}/${_hgrepo}-build"
  cd "${srcdir}/${_hgrepo}-build"

  # hack to force the client config to output to correct prefix
  sed -i -e 's#target.path = /usr/local/bin#target.path = /usr/bin#' ./client/client.pro

  # generate make files
  qmake CONFIG+=release monavclient.pro

  # make
  make
}

package() {
  cd "${srcdir}/${_hgrepo}-build"
  make INSTALL_ROOT="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
