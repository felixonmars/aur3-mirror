# Maintainer: Eric Anderson <ejona86 at gmail dot com>
# Contributor: Zachary A. Jones <jazzplayerl9@gmail.com>

pkgname=uplink-hib
_pkgname=uplink
pkgver=1.6_1
_pkgver=1.6-1
pkgrel=6
pkgdesc="A futuristic computer crime game (requires uplink from Humble Bundle)"
arch=('i686' 'x86_64')
url="http://www.introversion.co.uk/uplink"
license=('custom')
depends=('mesa' 'libjpeg6' 'sdl_mixer' 'libtiff3' 'freetype2')
conflicts=('uplink')
if [ "$CARCH" = "x86_64" ]; then
  _gamepkg="${_pkgname}_${_pkgver}_amd64.tar.gz"
  _gamepkgsum='8ee01e7d0d688339fbd84e2005d4625b'
else
  _gamepkg="${_pkgname}_${_pkgver}_i386.tar.gz"
  _gamepkgsum='db4f1b9506cff9e348dd52709f6a8ad7'
fi
source=("${_pkgname}.desktop" "${_gamepkg}")
md5sums=('e4c6a0050cb3a0894fb5c70a46c51414' "${_gamepkgsum}")
PKGEXT=.pkg.tar.gz

if [ ! -f "${startdir}/${_gamepkg}" ]; then
  warning ""
  warning "Requires purchased game download placed at ${startdir}/${_gamepkg}"
  warning ""
fi

package() {
  if [ "$CARCH" = "x86_64" ]; then
    _archi='x64'
    _aarchi='x86_64'
    _libi='lib64'
  else
    _archi='x86'
    _aarchi='x86'
    _libi='lib'
  fi

  install -d "${pkgdir}/opt"
  cp -r "${srcdir}/${_pkgname}-${_archi}" "${pkgdir}/opt/"
  mv "${pkgdir}/opt/${_pkgname}-${_archi}" "${pkgdir}/opt/${_pkgname}"

  # Use system-provided libraries
  rm -r "${pkgdir}/opt/${_pkgname}/${_libi}"

  # Fix permissions
  chmod -R 644 "${pkgdir}/opt/${_pkgname}"
  chmod +x "${pkgdir}/opt/${_pkgname}"
  chmod +x "${pkgdir}/opt/${_pkgname}/uplink.bin.${_aarchi}"

  # Install Desktop File
  install -D -m644 "${srcdir}/${_pkgname}.desktop" \
      "${pkgdir}/usr/share/applications/${_pkgname}.desktop"

  # Install License
  install -D -m644 "${pkgdir}/opt/${_pkgname}/license.txt" \
      "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  # Link Executable
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/${_pkgname}/uplink.bin.${_aarchi}" "${pkgdir}/usr/bin/${_pkgname}"
}
