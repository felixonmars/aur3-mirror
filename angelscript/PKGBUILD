# Maintainer: Hilton Medeiros <medeiros.hilton@gmail.com>

pkgname=angelscript
pkgver=2.25.2
pkgrel=1
pkgdesc="An extremely flexible cross-platform scripting library."
arch=('i686' 'x86_64')
url="http://www.angelcode.com/angelscript/"
license=('custom:ZLIB')
depends=('gcc-libs')
source=("http://www.angelcode.com/$pkgname/sdk/files/${pkgname}_${pkgver}.zip"
        COPYING)
md5sums=('9c35fdeb0bd0226097300d5b1629f9dc'
         '2b4e5cd7caef6ec2adfdc8f5dcf37fe0')

build() {
  cd "$srcdir/sdk/$pkgname/projects/gnuc"

  sed -i -e "/^LOCAL/s:=.*:= ${pkgdir}/usr:" \
    -e '/\sCXXFLAGS/s:-g::' \
    -e '/^install/s/:.*/:/' \
    -e 's/ln -s/ln -fs/' \
    makefile

  make
  make SHARED=1 VERSION=$pkgver
}

package() {
  cd "$srcdir/sdk/$pkgname/projects/gnuc"

  install -d "$pkgdir"/usr/{lib,include} \
             "$pkgdir"/usr/share/{licenses,doc}/$pkgname \
             "$pkgdir"/usr/share/$pkgname \
             "$pkgdir"/usr/share/doc/$pkgname/{articles,manual}

  make install
  make install SHARED=1 VERSION=$pkgver

  chmod 644 "$pkgdir"/usr/include/*

  msg "Installing add-ons..."
  cd "$srcdir/sdk"
  cp -rf add_on "$pkgdir/usr/share/$pkgname/"

  msg "Installing documentation and license..."
  cd "$srcdir/sdk/docs"
  install -m644 articles/* "$pkgdir/usr/share/doc/$pkgname/articles"
  install -m644 manual/* "$pkgdir/usr/share/doc/$pkgname/manual"
  install -m644 index.html "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname"
}
