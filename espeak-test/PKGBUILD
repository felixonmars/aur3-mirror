# Maintainer: Kyle <kyle@gmx.ca>

pkgname=espeak-test
pkgver=1.46.46
pkgrel=1
pkgdesc="Text to Speech engine for good quality English, with support for other languages: latest test version"
arch=('i686' 'x86_64')
url="http://espeak.sourceforge.net/test/latest.html"
license=('GPL')
depends=('gcc-libs' 'portaudio')
conflicts=('espeak')
provides=('espeak')
source=(http://espeak.sourceforge.net/test/espeak-$pkgver.zip)
md5sums=('f9d854ab3287656dbabf3b9f37e04fef')

build() {
	cd $srcdir/espeak-$pkgver/src
	cp portaudio19.h portaudio.h
	make
}

package() {
	cd $srcdir/espeak-$pkgver/src
	make DESTDIR=$pkgdir install
	find "$pkgdir/usr/share/espeak-data" -type f -print0 |xargs -0 chmod 644
}
