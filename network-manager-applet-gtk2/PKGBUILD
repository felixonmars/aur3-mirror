# Maintainer: Jove Yu <yushijun110[AT]gmail.com>

pkgname=network-manager-applet-gtk2
_pkgname=network-manager-applet
pkgver=0.9.8.0
pkgrel=1
pkgdesc="GNOME frontend to NetWorkmanager compiled for gtk2"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager' 'gtk2' 'libgnome-keyring'
         'libnotify' 'gnome-icon-theme'
         'mobile-broadband-provider-info' 'iso-codes')
makedepends=('intltool' 'gtk-doc')
optdepends=('gnome-bluetooth: for PAN/DUN support')
options=('!libtool')
provides=('network-manager-applet')
conflicts=('network-manager-applet')
install=network-manager-applet.install
source=("http://ftp.gnome.org/pub/GNOME/sources/network-manager-applet/0.9/network-manager-applet-${pkgver}.tar.xz")
md5sums=('531ce56c51ec86c5d2dc4cbe58649583')


build() {
  cd ${_pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-gtkver=2 \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-static \
      --disable-more-warnings \
      --disable-maintainer-mode \
      --disable-migration
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}
