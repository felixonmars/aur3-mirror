# Maintainer: Mikael Eriksson <mikael_eriksson@miffe.org>

# http://global.download.synology.com/download/Tools/CloudStation/2577/Linux/

pkgname=cloudstation
pkgver=3005
pkgrel=1
pkgdesc="File synchronization for Synology NAS"
arch=(i686 x86_64)
url="http://www.synology.com/"
license=('Unknown')
depends=(libpng12 libjpeg6 fontconfig util-linux gtk-update-icon-cache)
optdepends=('nautilus: For nautilus integration')
source=('syno-cloud-linux' 'cloudstation.desktop')
install=cloudstation.install
md5sums=('48805becaf0b682417587686e816f398' 'a925ac6073d1330555d193f807abc2dc')

if test "$CARCH" = i686; then
	source+=("http://global.download.synology.com/download/Tools/CloudStation/$pkgver/Linux/CloudStation-Linux-Installer-$pkgver-32bit.tgz")
	md5sums+=('8567d9eb71002994fce41bb7181cf19a')
fi

if test "$CARCH" = x86_64; then
	source+=("http://global.download.synology.com/download/Tools/CloudStation/$pkgver/Linux/CloudStation-Linux-Installer-$pkgver-64bit.tgz")
	md5sums+=('55c40a98c457792c8bd7e4efb5ce42f9')
fi

prepare() {
	cd "$srcdir/CloudStation-Linux-Installer-$pkgver"
	tar xf CloudStation.spkg
	tar xf CloudStation.tgz
}

package() {
	cd "$srcdir/CloudStation-Linux-Installer-$pkgver"
	mkdir -p "$pkgdir/opt"
	cp -r "CloudStation-app" "$pkgdir/opt/CloudStation"

	install -Dm755 libnautilus-cloud-extension.so "$pkgdir/usr/lib/nautilus/extensions-3.0/libnautilus-cloud-extension.so"
	install -Dm755 "$srcdir/syno-cloud-linux" "$pkgdir/usr/bin/syno-cloud-linux"

	mkdir -p "$pkgdir/usr/share/icons/hicolor/scalable/emblems"
	cp -r "CloudStation-app/images/emblems/"* "$pkgdir/usr/share/icons/hicolor/scalable/emblems"

	install -Dm644 "$srcdir/cloudstation.desktop" "$pkgdir/usr/share/applications/cloudstation.desktop"
}
