# Contributor: Lawrence Lee <valheru@facticius.net>
pkgname=kdebase4-runtime-git
pkgver=20111026
pkgrel=1
pkgdesc="Plugins and applications necessary for the running of KDE applications."
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs4-git' 'ntrack' 'smbclient' 'libssh' 'libcanberra' 'oxygen-icons'
        'xorg-xauth' 'hicolor-icon-theme' 'nepomuk-core-git')
makedepends=('pkg-config' 'cmake' 'automoc4' 'kdepimlibs4-git' 'openslp' 'xine-lib' 'git')
optdepends=('htdig: to build the search index in khelpcenter'
            'rarian: needed by khelpcenter'
            'gdb: drkonq crash handler')
install="kdebase4-runtime-git.install"
conflicts=('kdebase-runtime')
provides=('kdebase-runtime')
groups=('kde4-git')

_gitroot="git://anongit.kde.org/kde-runtime"
_gitname="kde-runtime"

build() {
  cd $srcdir
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi
  mkdir $srcdir/build
  cd $srcdir/build
  cmake $startdir/src/$_gitname -DCMAKE_BUILD_TYPE=debugfull \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON \
		-DBUILD_nepomuk:BOOL="0" 
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
  rm -f "${pkgdir}/usr/share/icons/hicolor/index.theme"
  ln -sf /usr/lib/kde4/libexec/kdesu "${pkgdir}/usr/bin/"
}