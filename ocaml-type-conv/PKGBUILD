# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor : Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>

pkgname=ocaml-type-conv
_pkgname=type_conv
pkgver=108.07.00
pkgrel=1
pkgdesc="Mini library with common code-generation helpers for OCaml"
arch=('i686' 'x86_64')
url="http://bitbucket.org/yminsky/ocaml-core/wiki/Home"
license=('LGPL')
depends=('ocaml')
makedepends=('ocaml-findlib')
source=(http://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz)
md5sums=('e3a4bd63e2c24d1396ce64e42293f1d8')
options=(!strip !makeflags)

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package(){
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR"
  make install
}
