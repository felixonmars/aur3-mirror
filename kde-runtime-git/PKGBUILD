# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-runtime-git
pkgver=plasma2tp.r4.g50bf8d6
pkgrel=1
pkgdesc='http://blog.andreascarpino.it/kde-frameworks-packages-on-arch-linux/'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('LGPL')
depends=('qt5-x11extras' 'qt5-svg' 'openexr' 'exiv2' 'smbclient'
         'kcmutils-git' 'kde4support-git' 'kemoticons-git'
         'kio-git' 'knewstuff-git' 'knotifyconfig-git' 'kunitconversion-git')
makedepends=('extra-cmake-modules-git' 'git' 'openslp')
provides=('kde-runtime')
conflicts=('kde-runtime')
source=('git://anongit.kde.org/kde-runtime.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kde-runtime
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kde-runtime \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
