# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>

pkgname=mdbtools
pkgver=0.7.1
pkgrel=1
pkgdesc="Utilities for viewing data and exporting schema from Microsoft Access Database files"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mdbtools/"
license=('LGPL' 'GPL')
depends=('libgnomeui' 'unixodbc')
makedepends=('bison' 'flex' 'txt2man' 'autoconf')
options=('!libtool')
source=(https://github.com/brianb/${pkgname}/archive/${pkgver}.tar.gz
        gmdb2.desktop)
md5sums=('477c7af98e75f8e6c987b020d6a822d8'
         '289f236ca91d73510e842922b5776999')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -i -f
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --with-unixodbc=/usr
  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rmdir "${pkgdir}/usr/share/man/mant"
  install -Dm644 ../gmdb2.desktop "${pkgdir}/usr/share/applications/gmdb2.desktop"
}
