# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-bfs-beta
pkgver=280.04
_kernver='2.6.39-bfs'
pkgrel=1
pkgdesc="NVIDIA BETA drivers for kernel26-bfs."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-bfs>=2.6.39' 'kernel26-bfs<2.6.40' "nvidia-utils-beta=${pkgver}")
makedepends=('kernel26-bfs-headers>=2.6.39' 'kernel26-bfs-headers<2.6.40')
conflicts=('nvidia-bfs' 'nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-all' 'nvidia-beta-all')
provides=("nvidia=${pkgver}" "nvidia-bfs=${pkgver}")
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('15f030a5af37073eb7cf58ae6e08e7a9')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('b6699fa410b3154c0891e72007f233d7')
fi

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC="/lib/modules/${_kernver}/build" module
}

package() {
    install -D -m644 "$srcdir/${_pkg}/kernel/nvidia.ko" \
        "$pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
    install -d -m755 "$pkgdir/etc/modprobe.d"
    echo "blacklist nouveau" >> "$pkgdir/etc/modprobe.d/nouveau_blacklist_bfs.conf"
    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/nvidia.install"
    find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
