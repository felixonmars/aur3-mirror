## PKGBUILD [plain_text]
# Contributor: tobias [tobias [at] archlinux.org]

pkgname=mutt-sidebar
pkgver=1.5.21
pkgrel=7
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i686' 'x86_64')
conflicts=('mutt')
optdepends=('smtp-forwarder: to send mail')
depends=('gpgme' 'ncurses' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types' 'krb5')
provides=('mutt')
conflicts=('mutt')
source=("ftp://ftp.mutt.org/mutt/devel/mutt-${pkgver}.tar.gz"
"http://dbg.download.sourcemage.org/grimoire/codex/stable/mail/mutt/patches/trash_folder/trash_folder-1.5.18.patch.bz2"
"http://lunar-linux.org/~tchan/mutt/patch-1.5.21.sidebar.20120829.txt")

md5sums=('a29db8f1d51e2f10c070bf88e8a553fd'
         '9d95c08295f8ec5171f774f776ef8413'
         'e4bb3b47ebf56da15bbd778b8e1ce002')

build() {
        cd "${srcdir}/mutt-${pkgver}"

        msg "Patching... Sidebar"
        patch -p1 < ${srcdir}/patch-1.5.21.sidebar.20120829.txt
        msg "Patching... Trashfolder"
        patch -p1 -i ${srcdir}/trash_folder-1.5.18.patch

        ./configure \
                --prefix=/usr \
                --sysconfdir=/etc \
                --enable-gpgme \
                --enable-pop \
                --enable-imap \
                --enable-smtp \
                --enable-hcache \
                --with-curses=/usr \
                --with-regex \
                --with-gss=/usr \
                --with-ssl=/usr \
                --with-sasl \
                --with-idn \

        make
}

package() {
        cd "${srcdir}/mutt-${pkgver}"
        make DESTDIR="${pkgdir}" install

        rm "${pkgdir}"/usr/bin/{flea,muttbug}
        rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
        rm "${pkgdir}"/etc/mime.types{,.dist}
        install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
