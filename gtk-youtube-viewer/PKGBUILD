# Maintainer: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

_pkgname=youtube-viewer
pkgname=gtk-youtube-viewer
pkgver=3.0.9
pkgrel=1
pkgdesc="Search and play YouTube videos with MPlayer."
arch=('any')
url="https://github.com/trizen/youtube-viewer"
license=('GPLv3')
makedepends=('git')

provides=('youtube-viewer')
conflicts=('youtube-viewer')

depends=('gtk2-perl' 'perl>=5.16.0' 'mplayer' 'perl-data-dump' 'perl-libwww' 'perl-file-sharedir')
optdepends=('perl-xml-fast: faster XML to HASH conversion'
            'perl-text-charwidth: to print the results in a fixed-width format (-W)'
            'perl-term-readline-gnu: for better STDIN support'
            'perl-lwp-protocol-https: for login and https support'
            'gcap: for retrieving Youtube closed captions. [in AUR]'
            'wget: for downloading videos with wget')
source=('git://github.com/trizen/youtube-viewer.git')
md5sums=('SKIP')

pkgver() {
    cd $_pkgname
    git describe --always | sed -e 's|-|.|g'
}

package() {
    cd "$_pkgname/WWW-YoutubeViewer"
    perl Build.PL --destdir "$pkgdir" --gtk-youtube-viewer
    ./Build
    ./Build test
    ./Build install --install_path script=/usr/bin

    install -Dm 644 "share/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm 644 "share/$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
