pkgname=pump
pkgver=0.8.24
_pkgrel=7
pkgrel=8
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="BootP and DHCP client"
makedepends=('libnewt')
depends=('glibc')
source=(http://ftp.de.debian.org/debian/pool/main/p/pump/pump_${pkgver}.orig.tar.gz
        http://ftp.de.debian.org/debian/pool/main/p/pump/pump_${pkgver}-${_pkgrel}.diff.gz
        pump.rc pump.rc-conf)
md5sums=('866fc9f62b8161eb1514a6a06597edc9'
         '29b66e30daaccb1ec34123e292aa36b4'
         '448cc87449887ec65582b80eeb3dc583'
         '8b7892bd084934fc698a4f0ce186c844')
url="http://packages.debian.org/wheezy/pump"
backup=(etc/conf.d/pump)
install="pump.install"

build() {
  sed -i -e "s|/usr/bin/install|/bin/install|g" $srcdir/pump-$pkgver/po/Makefile
  sed -i -e 's|install -m 755 netconfig $(USRSBINPATH)/netconfig||g' $srcdir/pump-$pkgver/Makefile

  cd $srcdir/pump-$pkgver
  patch -Np1 -i $srcdir/pump_${pkgver}-${_pkgrel}.diff

  for i in `ls debian/patches/*.patch* | sort`; do
    patch -p1 -s -i $i
    if [ $? = 0 ]; then
      echo "$i applied"
    else
      echo "Error processing $i"
      return 1
    fi
  done

  sed -i 's#/bin/install#install#g' $srcdir/pump-$pkgver/po/Makefile
  sed -i 's#-Wall -Werror ##g' $srcdir/pump-$pkgver/Makefile
  make pump
}

package() {
  cd $srcdir/pump-$pkgver
  mkdir -p $pkgdir/sbin $pkgdir/usr/{man/man8,share/locale}
  RPM_BUILD_ROOT=$pkgdir/usr sbindir=$pkgdir/usr/sbin libdir=$pkgdir/usr/lib includedir=$pkgdir/usr/include mandir=$pkgdir/usr/man datadir=$pkgdir/usr/share make install

  install -D -m644 $srcdir/pump.rc-conf $pkgdir/etc/conf.d/pump
  install -D -m755 $srcdir/pump.rc $pkgdir/etc/rc.d/pump
}
