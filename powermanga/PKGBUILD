# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Contributor: Daniele Marinello <m2346zc5-aur@yahoo.it>
# patch from Fedora's srpm

pkgname=powermanga
pkgver=0.90
pkgrel=6
pkgdesc="Arcade 2D shoot-them-up game with 41 levels and more than 200 sprites"
arch=('i686' 'x86_64')
url="http://linux.tlk.fr/"
license=('GPL3')
depends=('sdl_mixer' 'libxxf86dga')
backup=('var/games/powermanga.hi' 'var/games/powermanga.hi-easy' 'var/games/powermanga.hi-hard')
install=pm.install
source=(http://linux.tlk.fr/games/Powermanga/download/${pkgname}-${pkgver}.tgz 
        powermanga-0.90-install.patch
        powermanga.desktop
        powermanga.png)

md5sums=('7ef98d24b35fecdcdff790c5fe089bd4'
         '1f543c28fe0a6210a58af80272b32eb2'
         '173ef3aa5b14f9252b2d949002adce79'
         '8e093648f86e9d8750ff3758ce143b8c')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/powermanga-0.90-install.patch
  ./configure --prefix=/usr LDFLAGS="-lm"
  make 
}

package() {
    install -Dm644 powermanga.desktop $pkgdir/usr/share/applications/powermanga.desktop
    install -Dm644 powermanga.png $pkgdir/usr/share/pixmaps/powermanga.png
    cd $pkgname-$pkgver
    make DESTDIR=$pkgdir install
    ## fix permissions
    chmod -R 775 $pkgdir/var/games/
    chmod 755 $pkgdir/usr/bin/$pkgname
    chown root:root $pkgdir/usr/bin/$pkgname

}

