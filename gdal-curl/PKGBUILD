# Maintainer: Dražen Odobašić <dodobasic at gmail>

pkgname=gdal-curl
_pkgname=gdal
pkgver=1.9.1
pkgrel=1
pkgdesc="A translator library for raster geospatial data formats with curl support"
arch=('i686' 'x86_64')
url="http://www.gdal.org/"
license=('custom')
depends=('curl' 'geos' 'giflib' 'hdf5' 'libgeotiff' 'libjpeg' 'libpng' 'libtiff' 'netcdf'  'python2' 'python2-numpy' 'cfitsio' 'sqlite3' 'libmysqlclient' 'postgresql-libs' 'podofo')
makedepends=('perl' 'swig')
optdepends=('postgresql: postgresql database support'
            'mysql: mysql database support'
            'swig:  perl binding support'
	    'poppler: pdf conversion support')
options=('!libtool' '!makeflags')
source=(http://download.osgeo.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz \
	gdal-1.5.1-python-install.patch \
	configure.1.patch) 

md5sums=('c5cf09b92dac1f5775db056e165b34f5'
         '81afc1c26d29cee84aadb6924fe33861'
	 'de4ff6d5d44d22aba64051ce4cc96a38')

provides=('gdal')
conflicts=('gdal')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  export LDFLAGS="$LDFLAGS -Wl,--no-as-needed"

  cd ${srcdir}/$_pkgname-$pkgver
  patch -Np0 -i ${srcdir}/gdal-1.5.1-python-install.patch
  patch -Np0 -i ${srcdir}/configure.1.patch

# python2 fixes
  sed -i 's_python python1.5_python2 python python1.5_' configure
  for file in swig/python/{,osgeo/,samples/,scripts/}*.py; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr --with-netcdf --with-libtiff --with-sqlite3 \
              --with-geotiff --with-mysql --with-python --without-libtool --with-curl \
              --with-hdf5 --with-perl --with-geos --with-png --with-podofo

# workaround for bug #13646
  sed -i 's/PY_HAVE_SETUPTOOLS=1/PY_HAVE_SETUPTOOLS=/g' ./GDALmake.opt
  sed -i 's/EXE_DEP_LIBS/KILL_EXE_DEP_LIBS/' apps/GNUmakefile
  install -d ${pkgdir}/usr/lib/python2.7/site-packages/

  make
}

package () {
  cd ${srcdir}/$_pkgname-$pkgver

  make DESTDIR=${pkgdir} install

# install license
  install -D -m644 LICENSE.TXT ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE

#FS15477 clean up junks
  rm -f ${pkgdir}/usr/bin/*.dox
}
