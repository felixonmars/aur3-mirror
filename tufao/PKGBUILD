# Maintainer: Vin√≠cius dos Santos Oliveira <vini.ipsmaker@gmail.com>
pkgname=tufao
pkgver=0.4.0
pkgrel=1
pkgdesc="An asynchronous web framework for C++ built on top of Qt"
arch=('i686' 'x86_64')
url="http://tufao.googlecode.com/"
license=('LGPL2')
depends=('qt')
source=("http://tufao.googlecode.com/files/tufao-${pkgver}-src.tar.bz2")
md5sums=('590d2dbfbfa8ab0e91b95dc782d8f5d1')

build() {
  mkdir "$srcdir/tufao/build"
  cd "$srcdir/tufao/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make

  cd "$srcdir/tufao/tools/pluginserverconfigeditor"
  qmake
  make
}

package() {
  cd "$srcdir/tufao/build"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/tufao/tools/pluginserverconfigeditor"
  install -D tufao-routes-editor "$pkgdir/usr/bin/tufao-routes-editor"
}
