# Maintainer: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=qbittorrent
pkgver=2.9.9
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt4 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt' 'libtorrent-rasterbar' 'xdg-utils')
makedepends=('boost' 'geoip')
optdepends=('python: needed for search'
            'geoip: improves peer country resolution')
install='qbittorrent.install'
source=("http://downloads.sourceforge.net/sourceforge/qbittorrent/${pkgname}-${pkgver}.tar.xz"
        0001-fix-compilation-error.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ../0001-fix-compilation-error.patch

  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make INSTALL_ROOT=${pkgdir} install
}
md5sums=('5e7ddb6b6c9795873489a86167de9b73'
         'e29eb80f5508ef1828da6084af6e7614')
