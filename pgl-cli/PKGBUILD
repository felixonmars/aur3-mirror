# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=pgl-cli
pkgver=2.1.3
pkgrel=3
pkgdesc='PeerGuardian is a privacy oriented firewall application (Daemon & CLI).'
url='http://sourceforge.net/projects/peerguardian/'
arch=('i686' 'x86_64')
depends=('libnfnetlink' 'libnetfilter_queue' 'iptables' 'wget')
optdepends=('zlib: to directly load gz compressed blocklists'
	    'p7zip: for blocklists packed as .7z'
	    'unzip: for blocklists packed as .zip'
	    'net-tools: to auto-whitelist local ip addresses'
	    'networkmanager: to run a white-listing script when detecting new network interfaces')
license=('GPL')
replace=('pgl-nogui')
conflicts=('blockcontrol' 'moblock' 'pgl')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf'
	'etc/logrotate.d/pglcmd'
	'etc/logrotate.d/pgld')
source=(http://downloads.sourceforge.net/project/peerguardian/PeerGuardian%20Linux/${pkgver}/pgl-${pkgver}.tar.gz)
md5sums=('b478f01e133626d724e3acf676cfcbfa')
install=pgl.install

build() {
    cd ${srcdir}/pgl-${pkgver}/

    # Use bash instead of sh.
    sed -i -e 's|^#!/bin/sh$|#!/bin/bash|' $(find ${srcdir} -type f)

    make MAKE_PGLD=yes MAKE_PGLCMD=yes MAKE_PGLGUI=no \
         DBUS=no PIDDIR=/run INITDIR=/etc/rc.d
}

package() {
    cd ${srcdir}/pgl-${pkgver}/
    make MAKE_PGLD=yes MAKE_PGLCMD=yes MAKE_PGLGUI=no \
	 DBUS=no PIDDIR=/run INITDIR=/etc/rc.d \
	 DESTDIR=${pkgdir} install

    # Man
    gzip ${srcdir}/pgl-${pkgver}/docs/pgld.1
    install -Dm644 ${srcdir}/pgl-${pkgver}/docs/pgld.1.gz ${pkgdir}/usr/share/man/man1/pgld.1.gz
    ln -s /usr/share/man/man1/pgld.1.gz ${pkgdir}/usr/share/man/man1/pglcmd.wd.1.gz
    # Texts
    install -d ${pkgdir}/usr/share/doc/pgl/
    install -Dm644 ${srcdir}/pgl-${pkgver}/docs/{AUTHORS,BUGS,README,README.blocklists,THANKS,TODO} ${pkgdir}/usr/share/doc/pgl/
    install -Dm644 ${srcdir}/pgl-${pkgver}/debian/copyright ${pkgdir}/usr/share/licenses/pgl/LICENSE
}