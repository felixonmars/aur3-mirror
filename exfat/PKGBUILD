# Contributor: Doug Newgard <scimmia22 at outlook dot com>

pkgname=exfat
pkgver=1.2.4
pkgrel=1
# By default, this build the modules for the currently running kernel.
# To change this, change the next line to the kernel name you want to build for.
_kernver=$(uname -r)
pkgdesc="Native module for exfat"
arch=('i686' 'x86_64')
url="http://opensource.samsung.com/"
license=('GPL')
depends=('linux')
makedepends=('linux-headers')
install="$pkgname.install"
# Go to the URL and search for exfat. You need to download the source yourself and put the zip file
# in the same dir as the PKGBUILD file.
source=("${pkgname}_${pkgver}.zip"
        "3.9-compat.patch"
        "3.10-compat.patch")
sha256sums=('4cb39c4f44b7a68cc391596384744a32f948ba659697542ff771764b215810f3'
            'ddc89bccd6b9b6c74a726f1623beaced757772f35794e47f0772562cef7b85d5'
            '41ef7b3de12f2bcc02246b2ddfb50100dfab327ec2b61916a09935cbe075a0bb')

prepare() {
  cd "$srcdir"

  patch -Np1 < 3.9-compat.patch
  patch -Np1 < 3.10-compat.patch
}

build() {
  cd "$srcdir"

  make -C /usr/lib/modules/$_kernver/build M="$PWD" modules
}

package() {
  find "$srcdir" -name '*.ko' -execdir install -Dm644 {} "$pkgdir/usr/lib/modules/$_kernver/extramodules/{}" \;

  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
