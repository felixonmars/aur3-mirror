# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libunity
pkgver=3.4.2
pkgrel=1
pkgdesc="A library for instrumenting- and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee>=0.6.0' 'dee>=0.5.2' 'dbus-glib')
makedepends=('vala>=0.11.3')
options=(!libtool)
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('409dbedaec1b16bbdeb98c32147e7928')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #fix Dbusmenu-0.4.gir filename
  sed -i 's/--pkg Dbusmenu-Glib-0\.4/--pkg Dbusmenu-0\.4/' configure{,.ac}
  sed -i 's/name="Dbusmenu" version="Glib-0\.4"/name="Dbusmenu" version="0\.4"/' src/Unity-3.0.gir

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
