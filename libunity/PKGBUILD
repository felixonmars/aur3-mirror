# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libunity
pkgver=6.12.0
pkgrel=1
pkgdesc="Library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee' 'dee' 'libdbusmenu' 'gtk3' 'dconf')
makedepends=('vala' 'gobject-introspection' 'intltool')
options=('!libtool')
source=(http://launchpad.net/$pkgname/6.0/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('d7a4d5b1ab317b8ee23e2bae716d67da')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Install GI overrides for python 3 as well
  install -dm755 "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides"
  ln -s ../../../../python2.7/site-packages/gi/overrides/Unity.py "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides/Unity.py"
}
