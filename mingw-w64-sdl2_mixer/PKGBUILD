pkgname=mingw-w64-sdl2_mixer
pkgver=2.0.0
pkgrel=4
pkgdesc="SDL2 mixer libraries (mingw-w64)"
arch=(any)
url="http://www.libsdl.org/projects/SDL_mixer"
license=("MIT")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-sdl2
mingw-w64-smpeg2
mingw-w64-libvorbis
mingw-w64-libmikmod
mingw-w64-flac)
options=(!libtool !strip !buildflags)
source=("http://www.libsdl.org/tmp/SDL_mixer/release/SDL2_mixer-$pkgver.tar.gz")
md5sums=('4c85cdc7b6d162afb918aed626436109')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/SDL2_mixer-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-sdltest \
      --disable-smpegtest \
      --with-smpeg-prefix=/usr/${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
