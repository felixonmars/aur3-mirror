# Maintainer: K. Hampf <khampf@users.sourceforge.net>
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=gsdjvu
pkgver=1.6
_gsver=8.64
_fontsver=8.11
pkgrel=1
pkgdesc="Very efficient way of converting PostScript and PDF documents into DjVu"
arch=(i686 x86_64)
# This program mixes GPL and CPL licensed codes, so the binaries are not redistributable.
license=('custom:unredistributable')
depends=('libcups>=1.3.9-3' 'cairo' 'libpng12>=1.2.6' 'zlib>=1.2.1')
optdepends=('djvulibre: for the djvudigital converter')
url="http://djvu.sourceforge.net/gsdjvu.html"
source=("http://downloads.sourceforge.net/sourceforge/djvu/gsdjvu-${pkgver}.tar.gz" 
        "http://downloads.sourceforge.net/ghostscript/ghostscript-${_gsver}.tar.bz2" 
        "http://downloads.sourceforge.net/ghostscript/ghostscript-fonts-std-${_fontsver}.tar.gz" )
noextract=("ghostscript-${_gsver}.tar.bz2"
           "ghostscript-fonts-std-${_fontsver}.tar.gz")
options=('!libtool' '!makeflags')

build() {
    cd "${srcdir}/gsdjvu-${pkgver}"

    mkdir -p BUILD
    ln -sf "${srcdir}/ghostscript-${_gsver}.tar.bz2" BUILD
    ln -sf "${srcdir}/ghostscript-fonts-std-${_fontsver}.tar.gz" BUILD

    echo -e 'YES\n\n' | "${srcdir}/gsdjvu-${pkgver}/build-gsdjvu" || return 1

    install -dm755 "${pkgdir}/usr/lib" || return 1
    cp -a BUILD/INST/gsdjvu "${pkgdir}/usr/lib" || return 1

    install -dm755 "${pkgdir}/usr/bin" || return 1
    ln -s "/usr/lib/gsdjvu/gsdjvu" "${pkgdir}/usr/bin/gsdjvu" || return 1

    sed -i -e 's,/usr/bin/gs,/usr/lib/gsdjvu/bin/gs,' "${pkgdir}/usr/lib/gsdjvu/gsdjvu" || return 1

    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}" || return 1
    install -m644 COPYING* "${pkgdir}/usr/share/licenses/${pkgname}" || return 1
}
md5sums=('60be62c4f167c8ef1ac0b8276921298f'
         'b13289cb2115f38f40c5e064f87e228a'
         '6865682b095f8c4500c54b285ff05ef6')
