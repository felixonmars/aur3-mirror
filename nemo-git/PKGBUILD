# Maintainer: Ner0

pkgname=nemo-git
pkgver=20121020
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork), development version"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('dconf' 'desktop-file-utils' 'exempi' 'gnome-desktop' 'gnome-icon-theme' 'hicolor-icon-theme' 'libnotify' 'libtracker-sparql')
makedepends=('git' 'gtk-doc' 'gnome-common' 'gobject-introspection' 'intltool')
optdepends=('nemo-dropbox-git: Dropbox integration'
             'nemo-fileroller-git: File Roller integration'
             'gksu: Open as Root')
conflicts=('nemo-fm')
provides=('nemo-fm')
options=('!emptydirs' '!libtool')
install=nemo.install

_gitroot=git://github.com/linuxmint/nemo.git
_gitname=nemo

build() {
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$_gitname-build"
  git clone "$_gitname" "$_gitname-build"
  cd "$_gitname-build"

  # Get default terminal value
  _terminal=$(gsettings get org.gnome.desktop.default-applications.terminal exec | tr -d "'")

  # Set "Open in Terminal" to default terminal (Arch doesn't use x-terminal-emulator)
  sed -i "s/x-terminal-emulator/$_terminal/" src/nemo-view.c

  # Fix "Open as Root"
  sed -i "s/gksu/gksudo/" src/nemo-view.c

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-gtk-doc-html \
      --disable-schemas-compile
  make
}

package() {
  cd "$_gitname-build/"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
