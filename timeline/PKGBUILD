# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=timeline
pkgver=0.19.0
pkgrel=1
pkgdesc="A cross-platform application for displaying and navigating events on a timeline."
arch=(any)
url="http://thetimelineproj.sourceforge.net/"
license=('GPL')
depends=('wxpython')
makedepends=('scons' 'python2-markdown')
optdepends=('python2-pysvg: export in svg'
            'python2-markdown: view help pages'
            'python2-icalendar: ics support')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/thetimelineproj/thetimelineproj/$pkgver/$pkgname-$pkgver.zip"
        $pkgname.{desktop,sh})
md5sums=('62fbc9869a5d816795f7b5f9055c12a0'
         'df26b7f1fbe00c8a1722118363d8136c'
         '96bc4d0f76cdfb36c0eb619adc7d83ca')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # path fix
  sed -i "s|\(_ROOT = \).*|\1\"/usr/share/$pkgname\"|" \
    timelinelib/config/paths.py

  # python2 fix
  sed -i "s|\(env python\).*|\12|" $pkgname.py

  scons
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # prepare dirs
  install -d ${pkgdir}/usr/{bin,lib/python2.7/site-packages/,share/{locale,$pkgname}}

  # data and libs
  cp -a timeline* "$pkgdir/usr/lib/python2.7/site-packages"
  cp -a po/*[^pot] "$pkgdir/usr/share/locale"
# cp -a man "$pkgdir/usr/share" # gone
  cp -a icons "$pkgdir/usr/share/$pkgname"
# help_resources # gone

  # launcher
  install -Dm755 ../$pkgname.sh \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 icons/48.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
