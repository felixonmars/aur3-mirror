# Contributor: Tom Kuther <gimpel@sonnenkinder.org>

pkgbase=globalmenu-extension
pkgname=globalmenu-extension
true && pkgname=(globalmenu-extension-firefox globalmenu-extension-thunderbird)
pkgver=20120428
pkgrel=1
url="https://launchpad.net/globalmenu-extension"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('bzr' 'autoconf2.13' 'python2' 'yasm' 'zip' 'unzip' 'xulrunner' 'libidl2')
optdepends=()
options=()
conflicts=()
replaces=()
install=
source=()
md5sums=()

_bzrroot=lp:globalmenu-extension/3.2
_bzrname=globalmenu-extension

build() {
	cd "$srcdir"
	msg "Connecting to BZR server...."

	if [ -d $_bzrname ] ; then
		cd $_bzrname && bzr update
		msg "The local files are updated."
	else
		bzr branch $_bzrroot $_bzrname
	fi
	msg "BZR checkout done"

	msg "Starting make..."
	rm -rf "$srcdir/${_bzrname}-build"
	cp -r "$srcdir/${_bzrname}" "$srcdir/${_bzrname}-build"
	cd "$srcdir/${_bzrname}-build"

	autoconf-2.13
	sed -i 's/^ \t/\t/g' config/rules.mk
	sed -i '/^XPIDL_COMPILE[[:space:]]*=/s@\$(LIBXUL_DIST)/@&sdk/@' config/config.mk

	./configure --with-libxul-sdk=`pkg-config --variable=sdkdir libxul` \
		--with-system-libxul \
		--with-system-nspr \
		--with-system-nss \
		--enable-application=extensions \
		--enable-extensions=globalmenu \
		--disable-tests \
		--disable-necko-wifi
	make || return 1
}

package_globalmenu-extension-firefox() {
	pkgdesc="Firefox extension that exports the standard menu bar via libdbusmenu"
	depends=('libdbusmenu-gtk2' 'firefox')
	
	cd "$srcdir/${_bzrname}-build"
	emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' extensions/globalmenu/install.rdf)
	install -d ${pkgdir}/usr/lib/firefox/extensions/${emid}
	unzip -d ${pkgdir}/usr/lib/firefox/extensions/${emid} dist/xpi-stage/globalmenu.xpi
}

package_globalmenu-extension-thunderbird() {
	pkgdesc="Thunderbird extension that exports the standard menu bar via libdbusmenu"
	depends=('libdbusmenu-gtk2' 'thunderbird')
	
	cd "$srcdir/${_bzrname}-build"
	emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' extensions/globalmenu/install.rdf)
	install -d ${pkgdir}/usr/lib/thunderbird/extensions/${emid}
	unzip -d ${pkgdir}/usr/lib/thunderbird/extensions/${emid} dist/xpi-stage/globalmenu.xpi
}

pkgdesc="global appmenu extension for Firefox and Thunderbird"
depends=('libdbusmenu' 'libdbusmenu-gtk2' 'firefox' 'thunderbird')
true && depends=()
# vim:syntax=sh
