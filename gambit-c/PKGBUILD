_distname=gambc-v4_6_6
pkgname=gambit-c
pkgver=4.6.6
pkgrel=1
pkgdesc="Scheme R5RS interpreter and compiler (into portable C)"
arch=(i686 x86_64)
url='http://www.iro.umontreal.ca/~gambit/'
license=('LGPL' 'APACHE')
makedepends=('gcc')
source=(
  "http://www.iro.umontreal.ca/~gambit/download/gambit/v4.6/source/${_distname}.tgz"
  gambit-c.install
)
install=gambit-c.install
md5sums=('27b27b106fcfa025b9ddea8e601019ec'
         '1dee672a1105f5091149d166bffe5cfc')

# Comment out the line below if you don't have about 500 megs of RAM and
# compilation fails...
_optimize=YUP

build() {
  CFGOPTS='--prefix=/usr --docdir=/usr/share/doc/gambit-c
    --infodir=/usr/share/info
    --libdir=/usr/lib/gambit-c
    --enable-gcc-opts'

  if [ "$_optimize" ] ; then
    CFGOPTS="$CFGOPTS --enable-single-host"
  fi

  cd "$srcdir/$_distname"
  ./configure $CFGOPTS
  make

  return 0
}

package() {
  cd "$srcdir/$_distname"
  make DESTDIR="$pkgdir" install

  # gsc conflicts with the one provided by ghostscript...
  cd "$pkgdir/usr/bin"
  mv gsc gambitc
  rm gsc-script
  ln -s gambitc gsc-script
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
