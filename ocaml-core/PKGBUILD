# Maintainer: Sergei Lebedev <superbobry@gmail.com>

pkgname=ocaml-core
pkgver=107.01
pkgrel=3
pkgdesc="Jane Street's standard library for OCaml"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12'
         'ocaml-res'
         'ocaml-sexplib>=5.1.2'
         'ocaml-bin-prot>=1.3.1'
         'ocaml-fieldslib>=0.1.2')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
url="http://ocaml.janestcapital.com/?q=node/13"
source=("http://www.janestreet.com/ocaml/core-$pkgver.tar.gz")
md5sums=('375d8f46e47d44e1de35cf36b78b9c8e')

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir --disable-tests" build
}

package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
