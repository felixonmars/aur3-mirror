# Maintainer: Daniel Scharrer <daniel@constexpr.org>

pkgname=innoextract
pkgver=1.3
pkgrel=2
pkgdesc='A tool to extract installers created by Inno Setup'
url='http://constexpr.org/innoextract/'
arch=('i686' 'x86_64')
license=('ZLIB')
depends=('boost-libs' 'xz')
makedepends=('boost' 'cmake')
source=("https://github.com/downloads/dscharrer/InnoExtract/${pkgname}-${pkgver}.tar.gz")
sha256sums=('bee65d1df2223870acb91ab90e9d933157167b8f7f5691ddd70f6a041d5bf671')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	cmake . -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release
	
	# To get a debug build change -DCMAKE_BUILD_TYPE=Release to
	# -DCMAKE_BUILD_TYPE=Debug - this will run signifincantly slower but
	# enable more runtime checks and generate better crash reports.
	
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	make DESTDIR="${pkgdir}" install
	
	install -Dm644 README.md \
		"${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm644 CHANGELOG \
		"${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG"
	install -Dm644 LICENSE \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
