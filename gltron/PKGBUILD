# Contributor: Dragonlord <dragonlord at seznam dot cz>
pkgname=gltron
pkgver=0.70
pkgrel=3
pkgdesc="A tron-like lightcycle game with a nice 3D perspective."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgl' 'gcc-libs' 'smpeg' 'sdl_sound' 'libpng')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver-source.tar.gz \
        gltron-0.70-configure.patch \
        gltron-0.70-prototypes.patch \
        gltron-0.70-debian.patch \
        gltron.desktop \
        gltron.png)
url="http://www.gltron.org"
md5sums=('300e54914844f36c199415d6d8b0372a'
         '9cd844f561d537df49a35fac21d5b51c'
         '744c0f80394f8713aebdf7038044570e'
         'd53c903bba366af12c1fdcc578d32da1'
         '1f36bf3e65cc8791301b79b9024ab51a'
         '132f0b0fbd5340c618e235dac27bda9d')

build() {
  cd $startdir/src/$pkgname-$pkgver

  patch -Np0 -i $startdir/gltron-0.70-configure.patch || return 1
  patch -Np0 -i $startdir/gltron-0.70-prototypes.patch || return 1

  if [ "$CARCH" = "x86_64" ]; then
   patch -Np0 -i $startdir/gltron-0.70-debian.patch || return 1
  fi

  ./configure --prefix=/usr --enable-warn=off
  make || return 1
  make DESTDIR="$startdir/pkg" install

  install -D -m644 "$startdir/gltron.png" \
    "$startdir/pkg/usr/share/gltron/pics/gltron.png"
  install -D -m644 "$startdir/gltron.desktop" \
    "$startdir/pkg/usr/share/applications/gltron.desktop"
}
