# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>
# Contributor SanskritFritz (gmail)

pkgname=tinyproxy-git
pkgver=20120906
pkgrel=1
pkgdesc='A light-weight HTTP proxy daemon for POSIX operating systems.'
arch=('i686' 'x86_64')
url='https://banu.com/tinyproxy/'
license=('GPL')
makedepends=('asciidoc' 'git')
provides=('tinyproxy')
conflicts=('tinyproxy')
backup=('etc/tinyproxy/tinyproxy.conf')
source=('tinyproxy.rc' 'tinyproxy.tmpfiles.conf' 'tinyproxy.service')
md5sums=('90eb2b3371e56d5a784fd826d4a6e5f8'
         '3c2764578f26581346fe312da0519a3e'
         '8e97b05cc8c87f7efefbf957e77c7f18')

_gitroot="git://banu.com/tinyproxy.git"
_gitname="tinyproxy"

build() {
        cd "${srcdir}"

        if [ -d "${srcdir}/${_gitname}" ] ; then
                cd "${_gitname}" && git pull origin
                msg "The local files are updated."
        else
                git clone "${_gitroot}"
        fi

        cd "${srcdir}/${_gitname}"

	./autogen.sh
        ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-transparent
        make
}

package() {
	cd "${srcdir}/${_gitname}"
	make DESTDIR="${pkgdir}" install

	install -Dm0755 "${srcdir}/tinyproxy.rc" "${pkgdir}/etc/rc.d/tinyproxy"

	install -dm0755 -o nobody -g nobody "${pkgdir}/var/log/${_gitname}"
	install -dm0755 -o nobody -g nobody "${pkgdir}/var/run/${_gitname}"

	install -Dm644 -o nobody -g nobody "${srcdir}/tinyproxy.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/tinyproxy.conf"

	# Provide sane defaults
	sed -i '/^#Listen/a\Listen 127.0.0.1' "${pkgdir}/etc/tinyproxy/tinyproxy.conf"
	install -Dm0644 "${srcdir}/tinyproxy.service" "${pkgdir}/usr/lib/systemd/system/tinyproxy.service"
}
