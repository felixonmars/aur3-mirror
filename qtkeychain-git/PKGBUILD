# Maintainer: Steven Allen <steven(at)stebalien(dot)com>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# Contributor: Teo Mrnjavac <teo@kde.org>

_buildtype="Release"

_pkgname=qtkeychain
pkgname=qtkeychain-git
pkgver=0.1.0.25.g3414b26
pkgrel=1
pkgdesc="Provides Qt4 support for secure credentials storage"
arch=('i686' 'x86_64')
url="https://github.com/frankosterfeld/qtkeychain"
provides=($_pkgname)
conflicts=($_pkgname)
license=('BSD')
depends=('qt4')
makedepends=('cmake')
source=(git+https://github.com/frankosterfeld/qtkeychain.git)
md5sums=('SKIP')

if [[ ! ${_buildtype} == "Release" ]] && [[ ! ${_buildtype} == "release" ]]; then
  options=(!strip)
fi

pkgver() {
    cd ${srcdir}/${_pkgname}
    git describe --tags --always | sed -e 's/^v//' -e 's/-/./g'
}

build() {
    [[ -e ${srcdir}/${_pkgname}-build ]] && rm -rf ${srcdir}/${_pkgname}-build
    mkdir ${srcdir}/${_pkgname}-build

    cd ${srcdir}/${_pkgname}-build
    cmake -DBUILD_WITH_QT4=on \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${_pkgname} \
          -DCMAKE_BUILD_TYPE=${_buildtype} \
          ../${_pkgname}
    make
}

package() {
    cd ${srcdir}/${_pkgname}-build
    make DESTDIR=${pkgdir} install
    install -D -m644 ${srcdir}/${_pkgname}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
