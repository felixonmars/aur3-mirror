# Contributor: Jussi Timperi <gmail.com: jussi.timperi>
# Maintainer:  Jesse Jaara   <gmail.com: jesse.jaara>

pkgname=voikko-libreoffice
pkgver=3.3
pkgrel=3
pkgdesc="Finnish spellchecker, grammar checker and hyphenator extension for libreoffice"
arch=('i686' 'x86_64')
url="http://voikko.sourceforge.net"
license=('GPL3')
depends=('libvoikko' 'libreoffice-common')
makedepends=('zip' 'libreoffice-sdk')
install=voikko-libreoffice.install
source=("http://downloads.sourceforge.net/voikko/libreoffice-voikko-${pkgver}.tar.gz")

build() {
  cd "${srcdir}/libreoffice-voikko-${pkgver}"

  sed 's|LINK_LIBS += $(STLPORTLIB)||g' -i Makefile
  . /usr/lib/libreoffice/sdk/setsdkenv_unix.sh

  make oxt
}

package() {
  cd "${srcdir}/libreoffice-voikko-${pkgver}/build" 

  mkdir -p "${pkgdir}/usr/lib/libreoffice/share/extensions/install"
  cp voikko.oxt "${pkgdir}/usr/lib/libreoffice/share/extensions/install/"
}

md5sums=('9b52dd9cb5bff1f382b2a8056fa973f3')
