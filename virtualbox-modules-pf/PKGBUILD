# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Maintainer : Trevor Turner <turn3r.tr3v0r at gmail dot com>

pkgname=virtualbox-modules-pf
pkgver=4.2.6
pkgrel=1
pkgdesc='Host linux-pf kernel modules for VirtualBox'
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
depends=('linux-pf>=3.7' 'linux-pf<3.8')
makedepends=('linux-pf-headers'
             "virtualbox-host-dkms=$pkgver")
provides=('virtualbox-host-modules')
install=virtualbox-modules-pf.install

_extramodules=extramodules-3.7-pf
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

build() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
}

package(){
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/virtualbox-modules-pf.install"
}
