_pkgbasename=ppl
pkgname=lib32-$_pkgbasename
pkgver=0.11.2
pkgrel=2
pkgdesc="A modern library for convex polyhedra and other numerical abstractions. (32 bit)"
arch=('x86_64')
url="http://www.cs.unipr.it/ppl"
license=('GPL3')
depends=('lib32-gmp>=4.1.3' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
options=('!docs' '!libtool')
source=(http://www.cs.unipr.it/ppl/Download/ftp/releases/$pkgver/ppl-$pkgver.tar.gz)
md5sums=('ce014f153a28006009db207ca953a984')

build() {
  cd $srcdir/ppl-$pkgver
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS="$LDFLAGS -I/usr/lib32/gmp" #force lib32-gmp headers

  ./configure --prefix=/usr --enable-interfaces="c,cxx" \
		--libdir=/usr/lib32 --libexecdir=/usr/lib32
  make
}

package() {
  cd $srcdir/ppl-$pkgver
  make DESTDIR=$pkgdir install
  rm -Rf ${pkgdir}/usr/{bin,include,share}
}
