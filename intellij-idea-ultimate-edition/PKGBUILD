# Maintainer: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=intellij-idea-ultimate-edition
pkgver=13.0.1
_buildver=133.331
pkgrel=1
pkgdesc="An intelligent IDE for Java, Groovy and other programming languages with advanced refactoring features intensely focused on developer productivity."
arch=('i686' 'x86_64') # not 'any' because of fsnotifier
url="http://www.jetbrains.com/idea/"
backup=("usr/share/${pkgname}/bin/idea.vmoptions" "usr/share/${pkgname}/bin/idea64.vmoptions")
license=('Comercial')
depends=('java-environment' 'giflib')
options=(!strip)
source=(http://download.jetbrains.com/idea/ideaIU-$pkgver.tar.gz \
        intellijidea.sh \
        jetbrains-idea.desktop
)
md5sums=('90b31415ab686a8d959c9d4f8f416ad7'
         '8c836f1265a3d02d8ce4ded2dab384d6'
         '243d8fab38f2d2d4a65fd798e86c0036'
)

package() {
  cd "$srcdir"

  install -d -m755 $pkgdir/usr/{bin,share}
  cp -a "idea-IU-$_buildver" "$pkgdir/usr/share/$pkgname"
  chown -R root:root $pkgdir/usr/share
  find $pkgdir/usr/share/$pkgname -type d -exec chmod 0755 {} ';'
  find $pkgdir/usr/share/$pkgname -type f -exec chmod 0644 {} ';'

  # never wait on user input when starting idea; copied from -ce PKGBUILD
  sed -i '/.*read IGNORE.*/ d' "$pkgdir"/usr/share/$pkgname/bin/idea.sh

  chmod +x $pkgdir/usr/share/$pkgname/bin/idea.sh
  chmod +x "$pkgdir"/usr/share/$pkgname/bin/fsnotifier
  chmod +x "$pkgdir"/usr/share/$pkgname/bin/fsnotifier64

  install -D -m755 $srcdir/intellijidea.sh $pkgdir/usr/bin/$pkgname
  install -D -m644 $srcdir/jetbrains-idea.desktop $pkgdir/usr/share/applications/jetbrains-idea.desktop
  install -D -m644 $srcdir/idea-IU-$_buildver/bin/idea.png $pkgdir/usr/share/pixmaps/$pkgname.png
  install -D -m644 $srcdir/idea-IU-$_buildver/license/IDEA_license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
