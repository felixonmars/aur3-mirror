# Maintainer: Filip Brcic <brcha@gna.org>
# Update by: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-gettext
pkgver=0.18.3.1
pkgrel=3
arch=(any)
pkgdesc="GNU internationalization library (mingw-w64)"
depends=(mingw-w64-crt mingw-w64-win-iconv mingw-w64-termcap)
makedepends=(mingw-w64-gcc)
options=(!strip !buildflags staticlibs)
license=(GPL LGPL)
url="http://www.gnu.org/software/gettext/"
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-$pkgver.tar.gz"{,.sig})
md5sums=('3fc808f7d25487fc72b5759df7419e02'
         'SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "$srcdir/gettext-$pkgver"

  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  unset LDFLAGS
  
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --disable-java \
      --disable-native-java \
      --disable-csharp \
      --enable-static \
      --enable-threads=win32 \
      --without-emacs \
      --disable-openmp
    make
    popd
  done
}

package() {
  cd "$srcdir/gettext-$pkgver"

  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make DESTDIR="$pkgdir" install

    ${_arch}-strip "$pkgdir/usr/${_arch}/bin/"*.exe
    ${_arch}-strip -x -g "$pkgdir/usr/${_arch}/bin/"*.dll
    ${_arch}-strip -g "$pkgdir/usr/${_arch}/lib/"*.a

    # Drop some useless tools & files
    rm -rf "$pkgdir/usr/${_arch}/lib/gettext"
    rm -rf "$pkgdir/usr/${_arch}/share"
    popd
  done
}
