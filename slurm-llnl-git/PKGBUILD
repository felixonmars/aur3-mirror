# Maintainer: Jimmy Tang <jcftang@gmail.com>
# Derived from package slurm-llnl by Fredrik Tegenfeldt <fredrik.tegenfeldt AT unige DOT ch>
pkgname=slurm-llnl-git
pkgver=20110710
pkgrel=1
pkgdesc="Simple Linux Utility for Resource Management"
arch=('i686' 'x86_64')
license=('GPL')
url="https://computing.llnl.gov/linux/slurm/"
groups=()
depends=('python2')
makedepends=('python2' 'libmysqlclient>=5.0.0' 'openssl>=0.9.6' 'munge' 'readline' 'git')
provides=(slurm)
conflicts=()
replaces=()
backup=()
options=()
install=${pkgname}.install
changelog=
source=("arch_rc.d.slurm" "shtml2html.py.patch")
noextract=()
md5sums=('e4607c0e25c657a9dbf6e12cc7b3209f' '9d8f907b340cedc28f6148fb8665f0c2')

_gitroot="git://github.com/SchedMD/slurm.git"
_gitname="slurm"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -p0 -i ../shtml2html.py.patch

  ./configure --prefix=/usr           \
              --sysconfdir=/etc/slurm \
              --with-mysql            \
              --with-ssl              \
              --with-munge            \
              --with-readline
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install

  install -D -m755 ../arch_rc.d.slurm "${pkgdir}/etc/rc.d/slurm"
  install -D -m755 etc/init.d.slurmdbd "${pkgdir}/etc/rc.d/slurmdbd"
  install -D -m644 etc/slurm.conf.example "${pkgdir}/etc/slurm/slurm.conf.example"
  install -D -m644 etc/slurmdbd.conf.example "${pkgdir}/etc/slurm/slurmdbd.conf.example"
  install -D -m644 LICENSE.OpenSSL "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.OpenSSL"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
