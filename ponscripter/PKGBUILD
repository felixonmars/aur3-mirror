# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>

pkgname=ponscripter
pkgver=20100502
pkgrel=6
pkgdesc="NScripter-style novel-game interpreter with an emphasis on supporting games in Western languages"
arch=('i686' 'x86_64')
url="http://onscripter.unclemion.com/"
license=('GPL')
depends=('sdl_mixer' 'sdl_image' 'bzip2' 'freetype2' 'gcc-libs')
# Dependency 'smpeg' provides by 'sdl_mixer'
source=(http://onscripter.unclemion.com/pub/ponscr/$pkgname-$pkgver-minimal.tar.bz2)
md5sums=('6f49000bf34d14149c8a4d0f8b41ecaf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --no-werror
  make
}

package() {
  install -D -m755 "${srcdir}/${pkgname}-${pkgver}/src/ponscr" "${pkgdir}/usr/bin/${pkgname}"

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -m644 BUGS CHANGES INSTALL MANUAL README TODO "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
