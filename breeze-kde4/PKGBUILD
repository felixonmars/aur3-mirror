# Contributor: Antonio Rojas

pkgname=breeze-kde4
pkgver=5.0.95
pkgrel=1
pkgdesc='Breeze widget style for KDE4 applications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
depends=('kdelibs')
makedepends=('extra-cmake-modules' 'automoc4' 'ki18n')
source=("http://download.kde.org/unstable/plasma/$pkgver/breeze-$pkgver.tar.xz")
md5sums=('aa1c8f457671daa7d21a97d64a11486d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../breeze-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_KDE4=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # move translations
  mv "$pkgdir"/usr/share/locale "$srcdir"
  install -d "$pkgdir"/usr/share/locale/kde4
  mv "$srcdir"/locale/* "$pkgdir"/usr/share/locale/kde4
}
