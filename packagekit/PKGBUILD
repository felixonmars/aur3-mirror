# Maintainer; BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: onestep_ua <onestep@ukr.net>
# Contributor: Zom <zom@eevul.org>

pkgbase='packagekit'
pkgname='packagekit-split' # aur faker
true && pkgname=('packagekit' 'packagekit-qt' 'packagekit-python' 'packagekit-backend-alpm' 'packagekit-backend-pacman')
pkgver=0.6.13
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
makedepends=('networkmanager>=0.8' 'polkit' 'pacman-glib>=3.4.0' 'python2' 'sqlite3' 'qt' 'intltool' 'dbus-glib')
options=('!libtool')
source=("http://www.packagekit.org/releases/PackageKit-${pkgver}.tar.bz2")
sha256sums=('cec67a54cd615163402b1e5100a6a9be0bd1838896d6183407b4d9274de47249')

build() {
	cd PackageKit-${pkgver}
	sed -i 's/SUBDIRS = test/SUBDIRS =/' backends/Makefile.in

	PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-tests \
		--disable-debuginfo-install \
		--disable-dummy \
		--enable-alpm \
		--enable-pacman \
		--with-default-backend=pacman
	make

	PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-gtk-doc-html \
		--disable-gtk-doc-pdf \
		--disable-qt \
		--disable-managed \
		--disable-service-packs \
		--disable-tests \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-debuginfo-install \
		--disable-device-rebind \
		--disable-pm-utils \
		--disable-dummy \
		--enable-alpm \
		--enable-pacman \
		--with-default-backend=pacman
}

package_packagekit() {
	backup=('var/lib/PackageKit/transactions.db'
			'etc/PackageKit/pacman.d/pacman.conf'
			'etc/PackageKit/pacman.d/repos.list')
	install=(${pkgname}.install)
	depends=('sqlite3' 'polkit' 'packagekit-backend-arch' 'networkmanager>=0.8')
	pkgdesc='PackageKit is a system designed to make installing and updating software on your computer easier with glib bindings'
	cd PackageKit-${pkgver}
	make DESTDIR="${pkgdir}" install

	# will be in packagekit-python
	rm -rf ${pkgdir}/usr/lib/python*

	# other stuff to cleanup
	rm -rf ${pkgdir}/usr/share/gtk-doc
	
	# clean up unneeded stuff
	rm -rf ${pkgdir}/usr/share/PackageKit/website
	
	# rename bash completion script
	mv ${pkgdir}/etc/bash_completion.d/pk-completion.bash ${pkgdir}/etc/bash_completion.d/pkcon
	
	# make sure log gets removed
	touch ${pkgdir}/var/log/PackageKit

	# store backends for later packaging
	[ -d ${srcdir}/packagekit-backend-install ] && rm -rf ${srcdir}/packagekit-backend-install
	mv ${pkgdir}/usr/lib/packagekit-backend ${srcdir}/packagekit-backend-install
}

package_packagekit-qt() {
	depends=('packagekit' 'qt')
	pkgdesc='Packagekit qt bindings'
	cd PackageKit-${pkgver}/lib/packagekit-qt/
	make DESTDIR="${pkgdir}" install
}

package_packagekit-python() {
	depends=('packagekit' 'python2')
	pkgdesc='Packagekit python bindings'
	cd PackageKit-${pkgver}/lib/python/
	make DESTDIR="${pkgdir}" install
}

package_packagekit-backend-alpm() {
	depends=('pacman>=3.4.1')
	pkgdesc='Packagekit alpm backend'
	provides=('packagekit-backend-arch')
	install=(packagekit-backend-alpm.install)
	install -Dm755 ${srcdir}/packagekit-backend-install/libpk_backend_alpm.so \
		${pkgdir}/usr/lib/packagekit-backend/libpk_backend_alpm.so
}

package_packagekit-backend-pacman() {
	depends=('pacman-glib>=3.4.0')
	pkgdesc='Packagekit pacman-glib backend'
	provides=('packagekit-backend-arch')
	install=(packagekit-backend-pacman.install)
	install -Dm755 ${srcdir}/packagekit-backend-install/libpk_backend_pacman.so \
		${pkgdir}/usr/lib/packagekit-backend/libpk_backend_pacman.so
}
pkgdesc='PackageKit is a system designed to make installing and updating software on your computer easier with glib bindings' # aur faker
if [ "aur" == "there" ]; then # aur faker
	depends=('networkmanager>=0.8' 'polkit' 'pacman-glib>=3.4.0' 'python2' 'sqlite3' 'qt' 'intltool' 'dbus-glib')
fi
