# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lnav
pkgver=0.4.0
pkgrel=2
pkgdesc="A curses-based tool for viewing and analyzing log files."
arch=(i686 x86_64)
url="http://tstack.github.com/lnav/"
license=('custom:BSD')
depends=('pcre' 'zlib' 'bzip2' 'sqlite3')
source=("http://cloud.github.com/downloads/tstack/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('827a5d175796d13229c1d5a1bdd7b765')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove unneeded termcap link
  sed -i "s| \-ltermcap||" configure

  # use right type (cause build failure on i686)
  sed -i "s|unsigned long|size_t|g" src/line_buffer.cc

  ./configure --prefix=/usr \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # readme
  install -Dm644 README "$pkgdir/usr/share/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
