# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgname=libglade
pkgname=$_pkgname-old
pkgver=0.17
pkgrel=1
pkgdesc="Allows you to load glade interface files in a program at runtime. Older version"
arch=(i686 x86_64)
url="http://ftp.gnome.org/pub/GNOME/sources/libglade/0.17/"
license=('GPL')
depends=('python2' 'gtk' 'libxml')
options=('!libtool')
source=("http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.gz")
md5sums=('38b2e2cfd813783fe157617813bfe3b3')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Python2 fixes
  sed -i "s|python|&2|" libglade-xgettext

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
