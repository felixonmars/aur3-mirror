# Maintainer: Duncan Townsend <duncant@mit.edu>
# Creator of nvidia-ck-bumblebee
#  igordcard <igordcard at gmail dot com>
# Creator of other packages from which this was derived
#  graysky <graysky AT archlnux.us>
#  A.J. Korf <jacobkorf at gmail dot com>
#  Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-ck-pax-bumblebee
pkgver=310.19
_extramodules=extramodules-3.6-ck-pax
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=1
pkgdesc="NVIDIA drivers for linux-ck-pax. Packaged for Bumblebee."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-ck-pax>=3.6' 'linux-ck-pax<3.7' "nvidia-utils-bumblebee=${pkgver}" "bumblebee>=3.0")
makedepends=('linux-ck-pax-headers>=3.6' 'linux-ck-pax-headers<3.7')
conflicts=('nvidia' 'dkms-nvidia' 'nvidia-96xx' 'nvidia-173xx' 'nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-ck-stable' 'nvidia-beta-ck' 'nvidia-ck' 'nvidia-ck-bumblebee')
provides=("nvidia=${pkgver}" "nvidia-ck=${pkgver}" "nvidia-ck-bumblebee=${pkgver}" "nvidia-bumblebee=${pkgver}")
license=('custom')
options=(!strip)
install=nvidia-ck-pax-bumblebee.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run" "nvidia-${pkgver}-pax.patch")
	sha256sums=('85d082c06a1ea07f514d22c0de4d5d2af065bcac37c377c3d755904bbf780c2f'
	            '55b36e9da4e4e5b6e29c10869dbbaf790e9392f9bd8b5d30ceb72bef9a302c04')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run" "nvidia-${pkgver}-pax.patch")
	sha256sums=('07b59edfaffb178c947af2c083d22dc6af90974aa53a61bb10792a682aaa5c72'
	            '55b36e9da4e4e5b6e29c10869dbbaf790e9392f9bd8b5d30ceb72bef9a302c04')
fi

build() {
	cd "${srcdir}"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}
	msg "Patching NVIDIA drivers for -pax"
	patch -Np1 -i "${srcdir}/nvidia-${pkgver}-pax.patch"
	cd kernel
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-ck-pax.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-ck-pax-bumblebee.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
