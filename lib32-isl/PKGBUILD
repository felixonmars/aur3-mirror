# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=isl
pkgname=lib32-$_pkgbasename
pkgver=0.07
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints (32 bit)"
arch=('x86_64')
url="http://www.kotnet.org/~skimo/isl/"
depends=('lib32-gmp' $_pkgbasename)
makedepends=('gcc-multilib')
license=('LGPL2.1')
options=('!libtool')
source=(http://www.kotnet.org/~skimo/isl/$_pkgbasename-$pkgver.tar.bz2)
md5sums=('0d1a38db9f1c7d526758092b985827ea')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32'
  make
}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make check
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install
  rm -rf $pkgdir/usr/{include,lib32/libisl.so.?.?.?-gdb.py}
}
