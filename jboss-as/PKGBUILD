# JBoss Application Server package
# Maintainer: Zdenek Obst <zdenek dot obst at gmail dot com>

pkgname=jboss-as
_majorver=7.1
pkgver=${_majorver}.1.Final
pkgrel=2
pkgdesc='Java EE 6 Application Server'
arch=('any')
url='http://www.jboss.org/jbossas'
license=('LGPL')
depends=('java-environment')
makedepends=('java-environment>=6')
_pkgloc=usr/share
backup=(etc/profile.d/${pkgname}.sh
        ${_pkgloc}/jboss-as/standalone/configuration/standalone.xml
        ${_pkgloc}/jboss-as/standalone/configuration/mgmt-users.properties
        ${_pkgloc}/jboss-as/standalone/configuration/logging.properties
        ${_pkgloc}/jboss-as/domain/configuration/domain.xml
        ${_pkgloc}/jboss-as/domain/configuration/mgmt-users.properties
        ${_pkgloc}/jboss-as/domain/configuration/logging.properties
        ${_pkgloc}/jboss-as/bin/standalone.conf
        ${_pkgloc}/jboss-as/bin/domain.conf)
install=${pkgname}.install
source=(http://download.jboss.org/jbossas/${_majorver}/jboss-as-${pkgver}/jboss-as-${pkgver}.tar.gz
        jboss-as.service
        jboss-as.sh
	systemd-wrapper.sh
        jboss-as.install)
md5sums=('1e33096182ac7cac96089e39f7479b35'
         '7041e98670bc8d00489547f1425aa335'
         'ec0cc091b71d34ec7389b6a98a2e63a0'
         '7fd581aabaf8da543c1e2a0a3b7acc59'
         'b6ce54053ddb2b894f6c5dd0ee9b004d')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rm -f bin/*.{bat,exe}
  install -dm755 ${pkgdir}/${_pkgloc}
  cp -R ${srcdir}/${pkgname}-${pkgver} ${pkgdir}/${_pkgloc}/${pkgname}

  # Prepare systemd service file
  install -Dm755 ${srcdir}/jboss-as.service ${pkgdir}/usr/lib/systemd/system/jboss-as.service

  # Prepare profile.d for environment variables
  install -Dm755 ${srcdir}/jboss-as.sh ${pkgdir}/etc/profile.d/jboss-as.sh

  # Include wrapper script for systemd startup
  install -Dm755 ${srcdir}/systemd-wrapper.sh ${pkgdir}/${_pkgloc}/jboss-as/bin/systemd-wrapper.sh
}
