# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=ido
pkgname=ido
true && pkgname=('ido' 'ido3')
pkgver=0.3.1
pkgrel=3
pkgdesc="Widgets and other objects used for indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('LGPL')
makedepends=('gtk2-ubuntu' 'gtk3')
options=(!libtool)
source=(http://launchpad.net/$pkgbase/0.3/$pkgver/+download/$pkgbase-$pkgver.tar.gz
        rgba.patch
        style.patch)
md5sums=('dddc90364b01a0a6259a1872c47b77f3'
         'c877e6d56c6db30459988c96eb2c7280'
         'b24bf879ef6a503dcab5baa1d79da491')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  patch -Np0 -i "$srcdir/rgba.patch"
  patch -Np0 -i "$srcdir/style.patch"

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static
  make
  popd
}

package_ido() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2-ubuntu')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make DESTDIR="$pkgdir/" install
}

package_ido3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3-ubuntu')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make DESTDIR="$pkgdir/" install
}

depends=('gtk2-ubuntu' 'gtk3-ubuntu')
true && depends=()
