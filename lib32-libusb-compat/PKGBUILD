# Maintainer: PyroPeter <googlemail.com@abi1789>
# Contributor: Florian Pritz <flo@xssn.at>
 
_pkgbasename=libusb-compat
pkgname=lib32-$_pkgbasename
pkgver=0.1.3
pkgrel=1
pkgdesc="Library to enable user space application programs to communicate with USB devices (32-bit)"
arch=(x86_64)
depends=(gcc-libs-multilib 'lib32-libusb>=1.0' $_pkgbasename)
makedepends=(gcc-multilib)
url="http://libusb.sourceforge.net/"
license=('LGPL')
options=('!libtool')
source=("http://downloads.sourceforge.net/libusb/libusb-compat-0.1/$_pkgbasename-$pkgver/$_pkgbasename-$pkgver.tar.bz2")
md5sums=('570ac2ea085b80d1f74ddc7c6a93c0eb')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
 
  cd "$srcdir/$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}
 
package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
 
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
