# Maintainer: speps <speps at aur dot archlinux dot org>

_name=firebug
pkgname=firefox-$_name
pkgver=1.10.2
pkgrel=1
pkgdesc="Tools for web development. Inspect, edit and monitor CSS, HTML, JavaScript and Net requests."
arch=(any)
url="http://getfirebug.com/"
license=('custom:BSD')
depends=('firefox')
provides=("$_name")
source=("${url}releases/$_name/${pkgver%.*}/$_name-$pkgver.xpi")
md5sums=('ebaa50223fe3da38995761387a76e739')

package() {
  cd "$srcdir"

  # extension
  install -d "$pkgdir/usr/lib/firefox/extensions/firebug@software.joehewitt.com"
  cp -a *[^xpi] ${pkgdir}/usr/lib/firefox/extensions/*

  # license
  install -Dm644 license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et: