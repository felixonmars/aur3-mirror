# Maintainer: Bartek Piotrowski <barthalion@gmail.com>

pkgname=mplayer2
pkgver=2.0_rc2
pkgrel=2
pkgdesc="A media player for Linux, Uoti Urpala's fork"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
depends=('fontconfig' 'libgl' 'libcss' 'libxxf86dga' 'sdl' 'ttf-dejavu' 'libxss')
makedepends=('live-media' 'mesa' 'unzip' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
replaces=('mplayer')
provides=('mplayer')
conflicts=('mplayer' 'mplayer2-git')
source=(http://ftp.mplayer2.org/pub/release/${pkgname}-build-${pkgver/_/-}.tar.xz)
md5sums=('92793d629181e059384c43972fa9a701')

build() {
    unset CFLAGS LDFLAGS

    cd ${srcdir}/${pkgname}/${pkgname/2/}

    ./configure --prefix=/usr \
                --enable-runtime-cpudetection \
                --disable-arts \
                --disable-speex \
                --disable-openal \
                --disable-libdv \
                --disable-musepack \
                --disable-esd \
                --disable-mga \
                --disable-fribidi \
                --enable-xvmc \
                --language=all \
                --confdir=/etc/mplayer

    make
}

package() {
    cd ${srcdir}/${pkgname}/${pkgname/2/}

    make DESTDIR="${pkgdir}" install
    install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
    install -dm755 ${pkgdir}/usr/share/mplayer/
    ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
    rm -rf ${pkgdir}/usr/share/mplayer/font
}
