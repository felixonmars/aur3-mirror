# Maintainer: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>

pkgname=dwarftherapist-hg
pkgver=0.6.12
pkgrel=5
pkgdesc="An extension application for the fantastic game Dwarf Fortress"
arch=('i686' 'x86_64')
url="http://code.google.com/p/dwarftherapist/"
license=('MIT')
depends=('qt')
makedepends=('mercurial')
conflicts=('dwarftherapist-svn')
replaces=('dwarftherapist-svn')
source=('fix_file_path.diff'
        'dwarftherapist.desktop')

_hgtrunk=https://dwarftherapist.googlecode.com/hg/
_hgmod=dwarftherapist
_hgrev=5216190557b0afe4c18add9b75e62d56be87fb60

build() {
  cd "${srcdir}"

  if [ -d ${_hgmod} ]; then
    (cd ${_hgmod} && hg pull)
  else
    hg clone $_hgtrunk $_hgmod
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_hgmod}-build"
  cp -r "${srcdir}/${_hgmod}" "${srcdir}/${_hgmod}-build"
  cd "${srcdir}/${_hgmod}-build"
  hg checkout ${_hgrev}

  # Change to more suitable linux paths
  patch -Np0 < "${srcdir}/fix_file_path.diff"

  # Build
  qmake dwarftherapist.pro
  make
}
package() {
  cd "${srcdir}/${_hgmod}-build"

  install -d "${pkgdir}/usr/share/dwarftherapist/memory_layouts/linux/"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 bin/release/DwarfTherapist "${pkgdir}/usr/bin/dwarftherapist"
  install -Dm644 etc/game_data.ini \
    "${pkgdir}/usr/share/dwarftherapist/game_data.ini"
  install -Dm644 etc/memory_layouts/linux/*.ini \
    "${pkgdir}/usr/share/dwarftherapist/memory_layouts/linux/"
  # install -Dm644 "${srcdir}"/*.ini \
  #   "${pkgdir}/usr/share/dwarftherapist/memory_layouts/linux/"

  install -Dm644 "${srcdir}"/dwarftherapist.desktop \
    "${pkgdir}"/usr/share/applications/dwarftherapist.desktop
  install -Dm644 img/hammer.png \
    "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/dwarftherapist.png
}

md5sums=('e5109f6cadbd19865f72a8ad86279d92'
         '32c80fd6e3b86900c1677b4e2859b07b')

# vim:set ts=2 sw=2 et:
