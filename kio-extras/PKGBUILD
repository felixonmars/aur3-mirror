# Contributor: Antonio Rojas

pkgname=kio-extras
pkgver=5.0.2
pkgrel=2
pkgdesc="Additional components to increase the functionality of KIO"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kio-extras'
license=('LGPL')
depends=('kdelibs4support' 'khtml' 'kdnssd' 'libssh' 'smbclient' 'exiv2' 'openexr')
makedepends=('extra-cmake-modules' 'openslp' 'kdoctools')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('cb02e13fb8a09d55a406f26eb67e32cd')

prepare() {
  mkdir -p build

# included in kio 5.3
  sed -i '/trash/d' $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# conflicts with kdebase-runtime
  rm "$pkgdir"/usr/lib/libmolletnetwork.so
  mv "$pkgdir"/usr/share/config.kcfg/jpegcreatorsettings{,5}.kcfg
}

