# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be> 
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=garcon-devel
pkgver=0.1.9
pkgrel=1
pkgdesc="Implementation of the freedesktop.org menu specification (Development Version)"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://wiki.xfce.org/dev/garcon"
groups=('xfce4-devel')
depends=('glib2' 'perl')
makedepends=('pkgconfig' 'intltool' 'xfce4-dev-tools-devel>=4.7.4')
conflicts=('garcon' 'garcon-git')
replaces=('libxfce4menu')
options=('!libtool')
source=(http://archive.xfce.org/src/libs/garcon/0.1/garcon-${pkgver}.tar.bz2)
md5sums=('a3ca1e54ad731c98f688900f6398fc20')

build() {
  cd ${srcdir}/garcon-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/garcon-${pkgver}
  make DESTDIR=${pkgdir} install
}
