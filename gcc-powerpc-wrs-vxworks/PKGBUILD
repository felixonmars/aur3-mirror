#Maintainer: Alex Brinister (alex_brinister@yahoo.com)
pkgname=gcc-powerpc-wrs-vxworks
pkgver=4.8.2
pkgrel=2
pkgdesc="GCC modded for VxWorks 6.3"
arch=('i686' 'x86_64')
url="http://firstforge.wpi.edu/sf/projects/c--11_toochain"
license=('GPL')
provides=('gcc-powerpc-wrs-vxworks')
conflicts=('vxworks-gcc-toolchain-bin')
depends=('wrs-vxworks-headers' 'binutils-powerpc-wrs-vxworks')
makedepends=('wget')
options=('!strip' '!libtool' '!zipman' '!buildflags' '!makeflags' 'staticlibs')
source=(http://ftp.gnu.org/gnu/gcc/gcc-"$pkgver"/gcc-"$pkgver".tar.bz2)
sha512sums=('2a3927481707b3bb7171076b596d9b69084edac82460df6fb35395592dd1e8fc5665577702ca746d5b454dec68b1c4e31b9de6adc9865d482f4b8736ec648b10')
_gcc_name="gcc-$pkgver"

build() {
	cd $srcdir/$_gcc_name
	./contrib/download_prerequisites
	cd $srcdir
	mkdir -p build && cd build

	export CFLAGS_FOR_TARGET="-g -O2 -mlongcall"
	../$_gcc_name/configure \
		--prefix=/usr \
		--target=powerpc-wrs-vxworks \
		--with-gnu-as \
		--with-gnu-ld \
		--with-headers \
		--disable-shared \
		--disable-libssp \
		--disable-multilib \
		--with-float=hard \
		--enable-languages=c,c++ \
		--enable-libstdcxx \
		--enable-threads=vxworks \
		--without-gconv \
		--disable-libgomp \
		--disable-nls \
		--with-cpu-PPC603 \
		--disable-libmudflap \
		--disable-symvers \
		--enable-cxx-flags=-mlongcall
	source /etc/profile.d/wind_base.sh
	make ${MAKEFLAGS} || return 1
}
package(){
  cd "$srcdir/build"
  make ${MAKEFLAGS} DESTDIR=$pkgdir install || return 1

  rm -r $pkgdir/usr/share/{info,man/man7}
  rm -r $pkgdir/usr/share/gcc-$pkgver
  rm $pkgdir/usr/lib/libiberty.a
}

# vim:set ts=2 sw=2:
