# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Simon Tunnat <simon+aur@tunn.at>
# Based on thunderbird-beta-bin: https://aur.archlinux.org/packages/thunderbird-beta-bin/

pkgname=thunderbird-esr-bin
pkgver=17.0.11
pkgrel=1
pkgdesc="Standalone Mail/News reader - Extended Support Release"
arch=('i686' 'x86_64')
url="https://www.mozilla.org/thunderbird/organizations"
license=('GPL' 'LGPL' 'MPL')
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite' 'startup-notification')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$pkgname.install
_arch=i686  # Workaround for the AUR Web interface Source parser
[ "$CARCH" = 'x86_64' ] && _arch=x86_64
source=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}esr/linux-$_arch/en-US/thunderbird-${pkgver}esr.tar.bz2"
        'thunderbird-esr.desktop'
        'vendor.js')
md5sums=(`curl -sL ${source/\/li*}/MD5SUMS | grep en-US | grep "$CARCH" | grep bz2 | cut -d " " -f1`
         'c4162b713e9d063202a079d2243fdc93'
         '90264419778975f57d74696ac2ad7107')
# # Alternative mirror:
# source[0]="http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}esr/linux-$_arch/en-US/thunderbird-${pkgver}esr.tar.bz2"

package() {
  msg2 "Creating directories"
  # Create directories
  install -d "$pkgdir"/{usr/bin,opt}

  msg2 "Moving stuff in place"
  # Install
  cp -r thunderbird/ "$pkgdir"/opt/${pkgname/-bin}/
  cp vendor.js "$pkgdir"/opt/${pkgname/-bin}/defaults/pref/
  
  # /usr/bin symlink
  ln -s /opt/${pkgname/-bin}/thunderbird "$pkgdir"/usr/bin/${pkgname/-bin}
  
  # Icons
  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 thunderbird/chrome/icons/default/default${i/x*/}.png "$pkgdir"/usr/share/icons/hicolor/$i/apps/${pkgname/-bin}.png
  done
  
  # Desktop
  install -Dm644 ${pkgname/-bin}.desktop "$pkgdir"/usr/share/applications/${pkgname/-bin}.desktop
  
  # Dictionaries
  rm -rf "$pkgdir"/opt/${pkgname/-bin}/dictionaries/
  ln -sf /usr/share/hunspell/ "$pkgdir"/opt/${pkgname/-bin}/dictionaries
  
  # Hyphenation
  ln -sf /usr/share/hyphen/ "$pkgdir"/opt/${pkgname/-bin}/hyphenation
}