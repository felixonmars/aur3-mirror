# Maintainer: Simon Tunnat <simon+aur@tunn.at>
# Based on Det's thunderbird-beta-bin and Barthalion's firefox-esr-bin

pkgname=thunderbird-esr-bin
_pkgname=${pkgname/-bin/}
pkgver=17.0
pkgrel=1
pkgdesc="Standalone Mail/News reader - Extended Support Release"
arch=('i686' 'x86_64')
url="http://www.mozilla.org/en-US/thunderbird/organizations/"
license=('GPL' 'LGPL' 'MPL')
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite' 'startup-notification')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$_pkgname.install
source=(
  ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}esr/linux-$CARCH/en-US/thunderbird-${pkgver}esr.tar.bz2
  $_pkgname.desktop
  vendor.js
)
md5sums=(
  'SEE_BELOW'
  '5053265fdf05313655271bbb0cb6f2c7'
  '90264419778975f57d74696ac2ad7107'
)
[ "$CARCH" == "i686" ] && md5sums[0]='0514d33877ba86e99147e21ee59f6cbb'
[ "$CARCH" == "x86_64" ] && md5sums[0]='968f02d43ad00c64112ddd109e1ac1ae'

package() {
  cd $srcdir

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
    install -Dm644 thunderbird/chrome/icons/default/default${i/x*/}.png "$pkgdir"/usr/share/icons/hicolor/$i/apps/$_pkgname.png
  done

  install -d $pkgdir/{usr/{bin,share/applications},opt}
  install -Dm644 $_pkgname.desktop "$pkgdir"/usr/share/applications/$_pkgname.desktop

  cp -a thunderbird $pkgdir/opt/$_pkgname
  cp vendor.js "$pkgdir"/opt/$_pkgname/defaults/pref/

  ln -s "/opt/$_pkgname/thunderbird" "$pkgdir/usr/bin/$_pkgname"

  rm -rf "$pkgdir"/opt/$_pkgname/dictionaries/
  ln -sf /usr/share/hunspell/ "$pkgdir"/opt/$_pkgname/dictionaries
}
