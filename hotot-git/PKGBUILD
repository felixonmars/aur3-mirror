# Maintainer: speps <speps at aur dot archlinux dot org>

pkgbase=hotot-git
pkgname=hotot-git
true && pkgname=('hotot-data-git' 'hotot-gtk2-git' 'hotot-gtk3-git' 'hotot-qt-git' 'hotot-kde-git')
pkgver=20120604
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
makedepends=('git' 'cmake' 'intltool' 'python2')
makepdepends+=('kdebase-runtime')
install="hotot-git.install"

_gitroot="https://github.com/shellex/Hotot.git"
_gitname="hotot"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"*

  #
  # BUILD HERE
  #

  for _p in ${pkgname[@]/-git}; do
    _gui=${_p/hotot-}
    msg2 "Building $_gui .."
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build-$_gui"
    cd "$srcdir/$_gitname-build-$_gui"

    cmake . -DCMAKE_INSTALL_PREFIX=/usr \
            -DWITH_GTK=`[[ $_gui = gtk[23] ]] && echo ON || echo OFF` \
            -DWITH_GIR=`[[ $_gui = gtk3 ]] && echo ON || echo OFF` \
            -DWITH_QT=`[[ $_gui =~ (qt|kde) ]] && echo ON || echo OFF` \
            -DWITH_KDE=`[[ $_gui = kde ]] && echo ON || echo OFF` \
            -DWITH_CHROME=OFF \
            -DPYTHON_EXECUTABLE=/usr/bin/python2
    make
  done
}

package_hotot-data-git() {
  true && depends=('hicolor-icon-theme')
  true && provides=("${pkgname/-git}")
  true && conflicts=("${pkgname/-git}")
  cd "$srcdir/$_gitname-build-data"

  make DESTDIR="$pkgdir/" install

  # rm unneded
  rm -rf "$pkgdir/usr/share/applications"
}

package_hotot-qt-git() {
  true && arch=('i686' 'x86_64')
  true && depends=('hotot-data-git' 'qtwebkit')
  true && provides=("${pkgname/-git}")
  true && conflicts=("${pkgname/-git}")
  cd "$srcdir/$_gitname-build-qt"

  # bin
  install -Dm755 qt/src/hotot-qt \
    "$pkgdir/usr/bin/hotot-qt"

  # desktop file
  install -Dm644 misc/hotot-qt.desktop \
    "$pkgdir/usr/share/applications/hotot-qt.desktop"
}

package_hotot-kde-git() {
  true && arch=('i686' 'x86_64')
  true && depends=('hotot-data-git' 'kdebase-runtime')
  true && provides=("${pkgname/-git}")
  true && conflicts=("${pkgname/-git}")
  cd "$srcdir/$_gitname-build-kde"

  # bin
  install -Dm755 qt/src/hotot-qt \
    "$pkgdir/usr/bin/hotot-kde"

  # desktop file
  install -Dm644 misc/hotot-qt.desktop \
    "$pkgdir/usr/share/applications/hotot-kde.desktop"
  sed -i "s/qt/kde/;s/Qt/Kde/" \
    "$pkgdir/usr/share/applications/hotot-kde.desktop"
}

package_hotot-gtk2-git() {
  true && depends=('hotot-data-git' 'pywebkitgtk' 'python-notify'
                   'python-keybinder' 'dbus-python' 'desktop-file-utils')
  true && optdepends=('libappindicator: unity menubar integration')
  true && provides=("${pkgname/-git}" 'hotot')
  true && conflicts=("${pkgname/-git}" 'hotot')
  cd "$srcdir/$_gitname-build-gtk2"

  # bin
  install -Dm755 scripts/hotot \
    "$pkgdir/usr/bin/hotot"

  # libs
  install -d "$pkgdir/usr/lib/python2.7/site-packages/hotot"
  install -Dm644 hotot/*.py* \
    "$pkgdir/usr/lib/python2.7/site-packages/hotot"

  # desktop file
  install -Dm644 misc/hotot.desktop \
    "$pkgdir/usr/share/applications/hotot.desktop"
}

package_hotot-gtk3-git() {
  true && depends=('hotot-data-git' 'python2-gobject' 'dbus-python'
                   'libwebkit3' 'desktop-file-utils')
  true && optdepends=('libappindicator: unity menubar integration')
  true && provides=("${pkgname/-git}")
  true && conflicts=("${pkgname/-git}")
  cd "$srcdir/$_gitname-build-gtk3"

  # bin
  install -Dm755 scripts/hotot \
    "$pkgdir/usr/bin/hotot-gtk3"
  sed -i "s/hotot/&_gir/" "$pkgdir/usr/bin/hotot-gtk3"

  # libs
  install -d "$pkgdir/usr/lib/python2.7/site-packages/hotot_gir"
  install -Dm644 hotot-gir/*.py* \
    "$pkgdir/usr/lib/python2.7/site-packages/hotot_gir"

  # desktop file
  install -Dm644 misc/hotot.desktop \
    "$pkgdir/usr/share/applications/hotot-gtk3.desktop"
  sed -i "/Exec/s/hotot/&-gtk3/;/^Name/s/$/ Gtk3/;s/2/3/" \
    "$pkgdir/usr/share/applications/hotot-gtk3.desktop"
}

# vim:set ts=2 sw=2 et:
