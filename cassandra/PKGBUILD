# Maintainer: Guillaume ALAUX <guillaume at alaux dot net>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Konstantin Nikiforov <helllamer@gmail.com> 
# Contributor: Alper Kanat <alperkanat@raptiye.org>

# check() function is used to verify GPG signature. check() imports 3 keys into your GPG keyring at first build.
# See http://scarybeastsecurity.blogspot.com/2011/07/alert-vsftpd-download-backdoored.html for reason of this step.
# If you have problems with gpg, you can remove check() function, and all will be ok.

pkgname=cassandra
pkgver=1.2.5
pkgrel=1
pkgdesc='NoSQL database (precompiled version)'
arch=('any')
url='http://cassandra.apache.org/'
license=('APACHE')
depends=('java-runtime')
makedepends=('gnupg')
checkdepends=('wget')
optdepends=('python: to use Python CLI administration scripts')
backup=(etc/cassandra/cassandra-env.sh
        etc/cassandra/cassandra-rackdc.properties
        etc/cassandra/cassandra-topology.properties
        etc/cassandra/cassandra.yaml
        etc/cassandra/commitlog_archiving.properties
        etc/cassandra/log4j-server.properties
        etc/cassandra/log4j-tools.properties)
_url_tgz="http://archive.apache.org/dist/${pkgname}/${pkgver}/apache-${pkgname}-${pkgver}-bin.tar.gz"
source=(${_url_tgz}
        cassandra.rc.d
        cassandra.service.systemd)
sha256sums=('45d7c89000c0f2c8bc44de2de1bfb2943b74517591361ce8d3f4d97ad4508a0c'
            '2176729fc3b25e8e86621bc2e9d114d07d31acdf53aee4a1c205c01f89e25161'
            'dd25888b72a0b493c1710720cdb31a0c5582cc4bd370a969c97a2808589f30a5')

## to check gpg signature
check() {
  msg "Checking GPG signature..."
  msg2 "(To disable gpg-check: build with '--nocheck')"
  gpg --list-keys | grep 'sylvain@datastax.com' || {
    _url_keys='https://www.apache.org/dist/cassandra/KEYS'
    msg "No maintainers' GPG keys found. Importing from ${_url_keys} ..."
    wget --quiet -O - ${_url_keys} | gpg --import -
  }
  # no need to add signature to package dependences
  echo "${_url_tgz}.asc"
  wget --quiet -O - "${_url_tgz}.asc" | gpg --verify - "apache-${pkgname}-${pkgver}-bin.tar.gz"
  msg2 "Detached GPG signature is valid."
}

package() {
  cd ${srcdir}/apache-cassandra-${pkgver}

  mkdir -p ${pkgdir}/{usr/bin,etc/cassandra,var/log/cassandra,var/log/cassandra}
  mkdir -p ${pkgdir}/{usr/bin,usr/share/cassandra,usr/share/java/cassandra}

  cp -a interface pylib tools ${pkgdir}/usr/share/cassandra/

  mkdir -p ${pkgdir}/usr/share/cassandra/bin/
  for f in bin/*; do
    if [[ ! "${f}" == *.bat && -x ${f} ]]; then
      cp -a ${f} ${pkgdir}/usr/share/cassandra/bin/
      ln -s /usr/share/cassandra/${f} ${pkgdir}/usr/${f}
    fi
  done
  sed -i 's_`dirname $0`/.._/usr/share/cassandra_' bin/cassandra.in.sh
  cp -a bin/cassandra.in.sh ${pkgdir}/usr/share/cassandra/

  cp -a lib/* ${pkgdir}/usr/share/java/cassandra/
  ln -s ../java/cassandra ${pkgdir}/usr/share/cassandra/lib

  cp -a conf/* ${pkgdir}/etc/cassandra/
  ln -s /etc/cassandra ${pkgdir}/usr/share/cassandra/conf

  install -D -m755 ${srcdir}/cassandra.rc.d ${pkgdir}/etc/rc.d/cassandra
  install -Dm644 ${srcdir}/cassandra.service.systemd \
                 ${pkgdir}/lib/systemd/system/cassandra.service

  chown http:log ${pkgdir}/var/log/cassandra
}
