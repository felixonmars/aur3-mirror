# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-eyesight-git
pkgver=0.0.0
pkgrel=1
pkgdesc="EyeSight: An image viewing program"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL2')
depends=('qtbase-git' 'qttools-git')
optdepends=('qtsvg-git: for svg support'
            'qtimageformats-git: for mng, tga, tiff, bmp image support')
makedepends=('git' 'cmake')

_gitroot="git://github.com/hawaii-desktop/eyesight.git"
_gitbranch=master
_gitname=eyesight
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${_gitname}
	#echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir build
}

build() {
	export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
	export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib
	export QT_SELECT=git

	cd build
	cmake ../${_gitname} \
		-DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
		-DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
		-DCMAKE_INSTALL_LIBDIR=/opt/hawaii-git/lib \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
