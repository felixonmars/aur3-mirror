# Maintainer: Graham Smith <gps1539@gmail.com

pkgname=logitechmediaserver-nightly
pkgver=7.9.0
pkgrel=1
pkgdate=-1428327734
pkgdesc='Streaming audio server'
arch=('i686' 'x86_64' 'arm' 'armv7h')
url='http://downloads.slimdevices.com/'
license=('GPL' 'custom')
depends=('perl<5.21')
makedepends=('nasm' 'yasm' 'rsync' 'subversion')
[[ $CARCH == "x86_64" ]] && optdepends=(lib32-{glibc,gcc-libs}': transcoding on 64-bit systems')
install=install
source=("http://downloads.slimdevices.com/nightly/7.9/sc/a6dacd2/logitechmediaserver-${pkgver}${pkgdate}.tgz"
        'CPAN.upstream::svn+http://svn.slimdevices.com/repos/slim/7.8/trunk/vendor/CPAN/'
        'http://search.cpan.org/CPAN/authors/id/S/SM/SMUELLER/Class-XSAccessor-1.19.tar.gz'
        'perl-recent.patch'
        'service')
sha256sums=('ddaddf9d51b84cb03de89b65ec76b3678ee5208b14d9fd1543585a79286e1f16'
            'SKIP'
            '99c56b395f1239af19901f2feeb125d9ecb4e351a0d80daa9529211a4700a6f2'
            '8b53d7589af4d8009b6c74dec558fe41c51d56a2e0d11f201f40e5b015ba33f2'
            '41718cf74b2f9b542089e7bd238fb974d049a6132e8d3e0ed598d6689e5d16c2')


prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}${pkgdate}"
	rm -r CPAN/arch Bin/{powerpc,sparc}-linux
	case $CARCH in
		x86_64) rm -r Bin/arm-linux ;;
		i686) rm -r Bin/arm-linux ;;
		arm*) rm -r Bin/i386-linux ;;
	esac
#	rm ../CPAN.upstream/Compress-Raw-Zlib-2.033.tar.gz CPAN/Compress/Raw/Zlib.pm 
	cp ../Class-XSAccessor-1.19.tar.gz ../CPAN.upstream
	patch -p1 -i ../perl-recent.patch
	sed \
		-e '/build_module Compress-Raw-Zlib/c true' \
		-e 's/XSAccessor-1.05/XSAccessor-1.19/g' \
		-e '/RUN_TESTS=1/c RUN_TESTS=0' \
		-e 's/perl5.12/perl/g' \
		-e 's/5.12/5.20/g' \
		-i ../CPAN.upstream/buildme.sh
}

build() {
	cd "${srcdir}/CPAN.upstream"
	export CFLAGS=${CFLAGS//-O2/-Os}
	sh buildme.sh
	rm -f build/5.20/lib/perl5/*-linux-thread-multi/XML/Parser/Expat.pm
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}${pkgdate}"
	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}"
	cp -a ../CPAN.upstream/build/5.20/lib/perl5/*linux*/* "${pkgdir}/opt/${pkgname}/CPAN"
	ln -s "/opt/${pkgname}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
