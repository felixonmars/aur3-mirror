# Contributor: Troels Kofoed Jacobsen <tkjacobsen@gmail.com>
# Contributor: bender02 at archlinux dot us
pkgname=pam_ssh
pkgver=1.97
pkgrel=4
pkgdesc="PAM module providing single sign-on behavior for SSH."
arch=('i686' 'x86_64')
url="http://pam-ssh.sourceforge.net/"
license=('custom')
depends=('pam' 'openssl' 'openssh')
options=(!libtool)
source=(http://downloads.sf.net/sourceforge/pam-ssh/$pkgname-$pkgver.tar.bz2
        empty-pw-segfault-gentoo.patch
        log.patch)
md5sums=('ef114d67b4951c88a62893437f850784'
         '1a02c8ad6f27b7041fa22b0af51a615d'
         '6e152ab561f6bb7586e3e76f5bbf34c6')


build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch < $srcdir/log.patch
  patch -p1 < $srcdir/empty-pw-segfault-gentoo.patch

  ./configure --prefix=/usr --with-pam-dir=/usr/lib/security
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -m 644 -D COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

