# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Gary Wright <wriggary@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Christopher Cox <nuopus@gmail.com>

pkgname=gnomebaker
pkgver=0.6.4
pkgrel=2
pkgdesc="GnomeBaker is a full featured CD/DVD burning application for Gnome"
license=('GPL')
arch=('i686' 'x86_64')
url="http://gnomebaker.sourceforge.net"
install=gnomebaker.install
depends=('libgnomeui' 'dvd+rw-tools' 'cdrkit' 'cdrdao' 'gstreamer0.10-base-plugins' 'libnotify')
makedepends=('perlxml' 'rarian' 'patch')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gnomebaker-0.6.4-libnotify-0.7.patch')
md5sums=('db43492684e0ba349be85e81ce0a9e56'
         '137f6b576d0d49990ac5b9c34bc2fbc9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p0 < $srcdir/gnomebaker-0.6.4-libnotify-0.7.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-libnotify

  make LDFLAGS+="-Wl,--export-dynamic" || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema \
    ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
    ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
