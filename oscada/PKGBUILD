# Maintainer: Sergey Marochkin <me@ziggi.org>

pkgname=oscada
pkgver=0.8.0.7
pkgrel=1
pkgdesc="OpenSCADA is a open supervisory control and data acquisition system"
arch=('i686' 'x86_64')
url="http://oscada.org/"
license=('GPL')
groups=('devel')
depends=('gettext' 'zlib' 'pcre' 'gd' 'libmysqlclient' 'sqlite' 'postgresql-libs' 'libfbclient' 'openssl' 'bison' 'net-snmp' 'lm_sensors' 'glibc' 'portaudio' 'qt4' 'fftw' 'phonon')
conflicts=('openscada' 'openscada-svn')
source=("ftp://ftp.oscada.org/OpenSCADA/0.8.0/openscada-$pkgver-$pkgrel.src.rpm")
md5sums=('0ae2c321f1e27590bbaed3718f93cae3')

build() {
  msg "Preparing sources"
  bsdtar -xf openscada-$pkgver.tar.lzma
  bsdtar -xf openscada-res-$pkgver.tar.lzma
  
  msg "Starting build"
  cd openscada-$pkgver
  LDFLAGS="${LDFLAGS} -Wl,--no-as-needed"
  ./configure --enable-AllModuls
  make DESTDIR="$pkgdir"
}

package() {
  cd $srcdir/openscada-$pkgver
  
  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/share/{applications,pixmaps,doc/openscada}
  install -d $pkgdir/etc
  install -dm644 $pkgdir/var/spool/openscada/{icons,DATA,ARCHIVES/{MESS,VAL}}
  
  echo "OpenSCADA data dir" > $pkgdir/var/spool/openscada/DATA/.info
  echo "OpenSCADA messages archive dir" > $pkgdir/var/spool/openscada/ARCHIVES/MESS/.info
  echo "OpenSCADA values archive dir" > $pkgdir/var/spool/openscada/ARCHIVES/VAL/.info
  
  install -Dm755 ../data/openscada_start $pkgdir/usr/bin
  install -Dm644 ../data/{oscada.xml,oscada_start.xml} $pkgdir/etc
  install -Dm644 ../data/openscada.png $pkgdir/usr/share/pixmaps
  install -Dm644 {ChangeLog,COPYING,INSTALL,README*,TODO*} $pkgdir/usr/share/doc/openscada/
  install -Dm644 ../data/openscada.desktop $pkgdir/usr/share/applications
  install -Dm644 ../data/icons/* $pkgdir/var/spool/openscada/icons
  
  make DESTDIR="$pkgdir" install
}
