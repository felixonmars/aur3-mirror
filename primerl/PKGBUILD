# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=primerl
pkgver=2.3a
pkgrel=2
pkgdesc="A sci-fi roguelike, based on ZAPM."
arch=('i686' 'x86_64')
url="http://pesky-reticulans.org/"
license=('GPL')
depends=('ncurses' 'gcc-libs' 'libsigsegv' 'lua51' 'sdl_net' 'sdl_mixer')
source=("http://pesky-reticulans.org/game/prime_src-$pkgver.tar.bz2")
md5sums=('60e5bbfd94988b5234261bb8dd6d1091')

build()
{
  cd "$srcdir/prime"
  sed -is 's/-llua /-llua5.1 /g' noteye/Makefile* makefile.base
  #sed -i 's/cp help/cp docs/' makefile.std
  sed -i 's/cp help/#&/' makefile.std
  sed -i 's/cp data/cp user/' makefile.std

  # stuff that does not work
  # make -f makefile.std multi
  # PANIC: unprotected error in call to Lua API (attempt to index a thread value)
  # make -f makefile.std multiconsole
  # needs free pascal compiler?

  # So let's trick make into gui and c++ into console
  sed -i 's/multi: multi-user /& nogui /' makefile.std
  echo "#define NOGUI" >> config.h
  make -f makefile.std multi
}

package()
{
  cd "$srcdir/prime"
  make PREFIX="$pkgdir" -f makefile.std install
  mkdir -p "$pkgdir/usr/bin/"
  ln -s /usr/games/bin/prime "$pkgdir/usr/bin/primerl"
  chmod -x "$pkgdir/var/games/prime/"*
  install -Dm644 libnoteye.so "$pkgdir/usr/lib/libnoteye.so"
}

