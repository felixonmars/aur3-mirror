# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Maintainer; Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kpar2
pkgver=0.4.5
pkgrel=6
pkgdesc="PAR2 verification and repair program for KDE4"
arch=('i686' 'x86_64')
url="http://code.google.com/p/kpar2kde4/"
license=('GPL3')
depends=('qt4' 'kdebase-runtime' 'kdelibs' 'libpar2' 'libsigc++2.0')
makedepends=('cmake' 'automoc4')
source=("http://kpar2kde4.googlecode.com/files/kpar2-${pkgver}.tar.gz"
        'kpar2.desktop.diff')
md5sums=('edfa8ebb8da3a9388f6f8fb9a17135d5'
         'cbaaacc049faa46910cbbcb1e84794fe')
install="${pkgname}.install"

prepare() {
  rm -fr "${pkgname}-build"
  cp -R "${pkgname}-${pkgver}" "${pkgname}-build"

  cd "${pkgname}-build"
  patch -p0 -i "${srcdir}/kpar2.desktop.diff"
}

build() {
  cd "${pkgname}-build"
  cmake . -DQT_QMAKE_EXECUTABLE=qmake-qt4 -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname}-build"
  make DESTDIR="${pkgdir}" install
}
