pkgname=mingw-w64-libwebp
pkgver=0.2.0
pkgrel=2
pkgdesc="WebP library and conversion tools (mingw-w64)"
arch=(any)
url="http://code.google.com/intl/en/speed/webp"
license=("BSD")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-libjpeg-turbo
mingw-w64-libpng
mingw-w64-libtiff)
options=(!libtool !strip !buildflags)
source=("http://webp.googlecode.com/files/libwebp-$pkgver.tar.gz")
md5sums=('75f33cabe4af0dfb51d751c67ae1b3d0')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/libwebp-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.a' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
