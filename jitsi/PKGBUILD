# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributors: Keshav P R, atommix aka Aleks Lifey, Xavion, Ananda Samaddar, Dan Serban

pkgname=jitsi
pkgver=1.0.3967
pkgrel=1
pkgdesc="An audio/video SIP VoIP phone and instant messenger written in Java (formerly SIP-Communicator)"
arch=('i686' 'x86_64')
url="http://jitsi.org"
license=('LGPL')
depends=('java-runtime')
makedepends=('apache-ant' 'java-environment')
options=(!strip !emptydirs zipman !libtool docs)
source=("http://download.jitsi.org/jitsi/src/jitsi-src-1.0-build.${pkgver##*.}.zip"
	jitsi.desktop
	jitsi.sh)
md5sums=('0fb7aaaad551177c3037ae570ca2f2e1'
	'aad7cf1fb18ff5d7c964834ecc38aed8'
	'c75c74f41e613a7d17bf2f1f310adee3')

build()
{
	cd ${srcdir}/${pkgname}
	# append the build revision to the jitsi version
	sed -i "s/0\.build\.by\.SVN/build.${pkgver##*.}/" src/net/java/sip/communicator/impl/version/NightlyBuildID.java
	. /etc/profile.d/apache-ant.sh
	ant rebuild
}

package() {
	cd ${srcdir}/${pkgname}
	find lib/ lib/bundle/ -maxdepth 1 -type f \
		-exec install -Dm644 {} "${pkgdir}/usr/lib/${pkgname}/"{} \;
	find lib/os-specific/linux/ -maxdepth 1 -type f \
		-execdir install -Dm644 {} "${pkgdir}/usr/lib/${pkgname}/lib/"{} \;
	shopt -sq extglob
	find lib/native/linux$(sed 's/_/-/g' <<<${CARCH/#*(i?86|x86)/})/ -maxdepth 1 -type f \
		-execdir install -Dm644 {} "${pkgdir}/usr/lib/${pkgname}/lib/native/"{} \;
	find sc-bundles/{,os-specific/linux/} -maxdepth 1 -type f \
		-execdir install -Dm644 {} "${pkgdir}/usr/lib/${pkgname}/sc-bundles/"{} \;
	install -Dm755 "$srcdir/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 "$srcdir/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	local _file
	for _file in resources/install/debian/*.{svg,xpm}; do
		install -Dm644 "$_file" "${pkgdir}/usr/share/pixmaps/${pkgname}${_file/*sip-communicator/}"
	done
}
