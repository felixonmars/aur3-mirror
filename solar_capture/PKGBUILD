# $Id: ff2865d3532cca55e3cf63e927a240f19e214043 $
# Maintainer: Ido Rosen <ido@kernel.org>
#
# NOTE: To request changes to this package, please submit a pull request
#       to the GitHub repository at https://github.com/ido/packages-archlinux
#       Otherwise, open a GitHub issue.  Thank you! -Ido
# 
# From the Solarflare Support website, http://support.solarflare.com/ :
#   ``SolarCapture is a set of tools for capturing, timestamping, processing
#   and injecting network traffic. SolarCapture is able to capture packets
#   received from the network at very high rates, apply filtering in hardware
#   and software, perform custom processing, and write packets to disk in PCAP
#   format.'' (06/24/2014)

pkgname='solar_capture'
pkgdesc="A set of tools for capturing, timestamping, processing, and injecting network traffic."
pkgver='1.2.2'
_kernelver="$(uname -r)"
_sfcrel='0'
pkgrel=1
arch=('x86_64')
url='http://www.solarflare.com/SolarCapture-Packet-Capture-Software'
license=('custom')
depends=('linux-lts' 'openonload' 'libpcap' 'python2' 'libaio')
makedepends=('rpmextract' 'linux-lts' 'linux-lts-headers' 'openonload' 'python2' 'libaio' 'libpcap')
options=('!libtool' '!strip' '!makeflags' '!buildflags' 'staticlibs')
source=("http://127.0.0.1/SF-111817-DH-6_(SF-111256-LS-5)_SolarCapture_Pro_RPMs.zip"
        'Makefile.patch')
sha512sums=('548bd7310d7fcc5373d7991a88c9b3f9cb19990c7561f792b870902fff501533a86f3cc6e3a5b1a0985c70e8335beabd436058452c8ac9396b286a91773df8da'
            '464449c9536eec194ccc94a1964eb91f833cde5a62d53f3923acc2fee64b4c05ea6a837fbac1cbaa1a3b0f6ca46a6ecf358925f12ac3570bccfad519c0cc385a')
#install=
_python_sitelib="$(python2 -c 'from distutils.sysconfig import get_python_lib; import sys; sys.stdout.write(get_python_lib())')"

prepare() {
  rpmextract.sh ${srcdir}/${pkgname}-core-${pkgver}-${_sfcrel}.x86_64.rpm
  rpmextract.sh ${srcdir}/${pkgname}-${pkgver}-${_sfcrel}.src.rpm
  pushd ${srcdir}/usr/lib64/
    ln -s libsolarcapture1.so.2.2 libsolarcapture1.so
  popd
  tar xvzf ${pkgname}-${pkgver}.tgz
  patch ${srcdir}/${pkgname}-${pkgver}/src/Makefile < Makefile.patch
}

build() {
  case $CARCH in
    "x86_64")
      # x64 build
      pushd ${pkgname}-${pkgver}/src
        SC_LIBRARY=${srcdir}/usr/lib64 \
        SC_INCLUDE=${srcdir}/usr/include \
        SC_RPM_BUILD=1 \
        SC_PYTHON=python2 \
        SC_PYTHON_CONFIG=python2-config \
        make python/solar_capture_c.so
      popd
    ;;
    *)
      # unsupported
      echo "Unsupported architecture."
      exit 1
    ;;
  esac
}

package() {
  cd "${srcdir}"

  mv "${srcdir}/usr" "${pkgdir}/."

  cd "${srcdir}/${pkgname}-${pkgver}"

  # Binaries:
  install -dm755 "${pkgdir}/usr/bin"
  install -m755 "src/solar_capture" "${pkgdir}/usr/bin"
  install -m755 "src/solar_capture_monitor" "${pkgdir}/usr/bin"

  # Python module:
  install -dm755 "${pkgdir}/${_python_sitelib}/${pkgname}"
  install -m644 "src/python/solar_capture_c.so" \
      "${pkgdir}/${_python_sitelib}/"
  for i in __init__.py shm.py stats.py tabulate.py
  do
    install -m644 "src/python/${pkgname}/${i}" \
        "${pkgdir}/${_python_sitelib}/${pkgname}"
  done

  # Documentation:
  install -dm755                  "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 docs/ChangeLog    "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 docs/LICENSE.txt  "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 docs/README       "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 docs/ReleaseNotes "${pkgdir}/usr/share/doc/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}/examples/c_api"
  install -m 644 src/examples/c_api/* \
      "${pkgdir}/usr/share/doc/${pkgname}/examples/c_api"
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}/examples/extensions_api"
  install -m 644 src/examples/c_api/* \
      "${pkgdir}/usr/share/doc/${pkgname}/examples/extensions_api"
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}/examples/unmanaged"
  install -m 644 src/examples/c_api/* \
      "${pkgdir}/usr/share/doc/${pkgname}/examples/unmanaged"
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}/examples/py_api"
  install -m 644 src/examples/c_api/* \
      "${pkgdir}/usr/share/doc/${pkgname}/examples/py_api"

  # Install LICENSE file:
  install -dm755                  "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/${pkgname}/LICENSE.txt" \
      "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
 :
}
