# Maintainer: jimmy-6 <jakub.jarozek {at} gmail.com>

# PKGEXT=.tar
pkgname=shadowgrounds-survivor
_gamepkg="survivorUpdate1.run"
pkgver=beta11_update1
pkgrel=1
pkgdesc="A 3D sci-fi alien shooter - sequel to Shadowgrounds (requires copy of the full game)"
url="http://shadowgroundsgame.com/survivor/"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
makedepends=('unzip')
source=('shadowgrounds-survivor.sh' 'shadowgrounds-survivor-launcher.sh' 'shadowgrounds-survivor.desktop')
md5sums=('d2b791e247b8d96f24fa0053d0553673'
         '79867f5da2abef43f180f13cd4f3b140'
         '65ab5514944f8db18396c95cd84baeb3')

depends=('libglade' 'mesa')
_depends64=('lib32-libglade' 'lib32-mesa')

if [[ $CARCH == x86_64 ]]; then
  depends=(${_depends64[@]})
fi

build() {
  cd ${srcdir}
  
  msg "You need a full copy of this game in order to install it"
  msg "Searching for ${_gamepkg} in dir: $(readlink -f `pwd`/..)"
  if [[ -f "../${_gamepkg}" ]]; then
    msg "Found game package, installing..."
    ln -fs "../${_gamepkg}" .
    unzip ${_gamepkg} || true
  else
    error "Game package not found, please type absolute path to ${_gamepkg}:"
    read pkgpath
    if [[ -f "${pkgpath}/${_gamepkg}" ]]; then
      msg "Found game package, installing..."
      ln -fs "${pkgpath}/${_gamepkg}" .
      unzip ${_gamepkg} || true
    else
      error "Unable to find game package."
      return 1
    fi
  fi
}

package(){

  # directories
  mkdir -p ${pkgdir}/opt/Shadowgrounds-Survivor
  cd ${srcdir}
  cp -r Config data Profiles survival ${pkgdir}/opt/Shadowgrounds-Survivor/
  #fix permissions
  find ${pkgdir}/opt/Shadowgrounds-Survivor -type d -exec chmod 755 {} \;
  find ${pkgdir}/opt/Shadowgrounds-Survivor -type f -exec chmod 644 {} \;
  
  cp -r lib32 ${pkgdir}/opt/Shadowgrounds-Survivor/
  chmod -R 755 ${pkgdir}/opt/Shadowgrounds-Survivor/lib32

  #other data files
  install -Dm644 data{1,2,3,4,5}.fbz README survivor-launcher.glade \
                 survivor-logo.png Survivor.xpm ${pkgdir}/opt/Shadowgrounds-Survivor/
  
  # startup scripts
  install -Dm755 ${srcdir}/shadowgrounds-survivor.sh ${pkgdir}/usr/bin/shadowgrounds-survivor
  install -Dm755 ${srcdir}/shadowgrounds-survivor-launcher.sh ${pkgdir}/usr/bin/shadowgrounds-survivor-launcher

  # execs
  install -Dm755 ${srcdir}/survivor-bin ${pkgdir}/opt/Shadowgrounds-Survivor/survivor-bin
  install -Dm755 ${srcdir}/survivor-launcher ${pkgdir}/opt/Shadowgrounds-Survivor/survivor-launcher

  # desktop entry
  install -Dm644 ${srcdir}/shadowgrounds-survivor.desktop ${pkgdir}/usr/share/applications/shadowgrounds-survivor.desktop
  
}
