# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jekyll Wu <adaptee@gmail.com>
# Contributor: riverscn <riverscn@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>

_pkgname=ibus
pkgname=$_pkgname-git
pkgver=20121121
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=(i686 x86_64)
license=(LGPL)
url=http://$_pkgname.googlecode.com
depends=(hicolor-icon-theme gtk2 gconf)
optdepends=(notification-daemon)
makedepends=(gobject-introspection dconf iso-codes intltool)
provides=($_pkgname=1.5)
options=(!emptydirs !libtool)
conflicts=($_pkgname)
install=$_pkgname.install

_gitroot=git://github.com/$_pkgname/$_pkgname.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    # Add a missing file which breaks gnome-autogen.sh
    touch ChangeLog

    # python2 fix
    for file in setup/$_pkgname-setup.in ui/gtk2/$_pkgname-ui-gtk.in; do
        sed -i 's_exec python_&2_' $file
    done

    msg "Building..."
    ./autogen.sh \
        PYTHON=/usr/bin/python2 \
        --prefix=/usr \
        --libexecdir=/usr/lib/$_pkgname \
        --sysconfdir=/etc \
        --enable-gtk3 \
        --enable-gtk-doc \
        --enable-gtk-doc-html \
        --with-gconf-schema-file-dir=/usr/share/gconf/schemas
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
