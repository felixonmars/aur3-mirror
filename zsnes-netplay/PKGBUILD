# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Frozen Fox <frozenfoxz@gmail.com>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=zsnes-netplay
_pkgname=zsnes
pkgver=1.42n
pkgrel=2
pkgdesc="Super Nintendo emulator (latest version supporting netplay)"
url="http://zsnes.com"
arch=('i686' 'x86_64')
license=('GPL')
options=(!buildflags)
conflicts=('zsnes')
provides=('zsnes')
if [[ $CARCH == "x86_64" ]]; then
  depends=('gcc-libs-multilib' 'lib32-sdl' 'lib32-libpng>=1.5.0' 
'lib32-libgl' 'lib32-ncurses' 'lib32-mesa')
  makedepends=('nasm' 'gcc-multilib')
else
  depends=('gcc-libs' 'sdl' 'libpng>=1.5.0' 'libgl' 'ncurses' 
'mesa')
  makedepends=('nasm' 'gcc')
fi
source=(http://snesemu.black-ship.net/emus/zsnes/zsnesnet.tar.bz2
        $_pkgname.patch
        libpng.patch
        ${_pkgname}_icon.png)
md5sums=('890fa9174edb127b1a3e7607258db32d'
         'aa250af3ef8dbf8a44e095d4cd132db2'
         'f7366961f21ed63d0844a3a6d4780bcf'
         'b5260c02adfc3b7ff725041214b00bfd')

build() {
  cd "$srcdir"

  patch -Np1 -i "$srcdir"/libpng.patch

  if [ $CARCH == "x86_64" ]; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
  fi

  chmod +x autogen.sh

  ./autogen.sh --prefix=/usr x_libraries=/usr/lib force_arch=i686 \
    --enable-release

  make
}

package() {
  cd "$srcdir"

  make DESTDIR="$pkgdir" install
  
  # adding --mandir= to autoconf line fails
  install -dm755 ${pkgdir}/usr/share
  install -Dm644 ${srcdir}/zsnes_icon.png ${pkgdir}/usr/share/pixmaps/zsnes_icon.png

  mv ${pkgdir}/usr/man ${pkgdir}/usr/share
}
