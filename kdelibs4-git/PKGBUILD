pkgname=kdelibs4-git
pkgver=20111026
pkgrel=1
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('strigi-git' 'attica-git' 'libxss' 'xz' 'openssl' 'soprano-git' 'krb5'
        'shared-desktop-ontologies' 'qca' 'libdbusmenu-qt-bzr' 'polkit-qt-git' 'grantlee'
        'shared-mime-info' 'enchant' 'giflib' 'jasper' 'openexr' 'xdg-utils'
        'phonon-git' 'hicolor-icon-theme' 'upower' 'udisks' 'libxcursor'
        'docbook-xsl' 'herqq')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl'
        'hspell')
conflicts=('kdelibs')
provides=('kdelibs')
groups=('kde4-git')
install='kdelibs4-git.install'

_gitroot="git://anongit.kde.org/kdelibs"
_gitname="kdelibs"



build() {
  cd $srcdir
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi
  mkdir $srcdir/build
  cd $srcdir/build
  cmake $startdir/src/$_gitname -DCMAKE_BUILD_TYPE=debugfull \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DEFAULT_HOME='.kde4git' \
		-DWITH_FAM=OFF \
		-DBUILD_libkactivities:BOOL="0" 
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
  rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
  ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}