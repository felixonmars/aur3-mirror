# Maintainer: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Sébastien Luttringer
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=weston-git
pkgver=1.1.90.3036.c228e23
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=(i686 x86_64)
url='http://wayland.freedesktop.org'
license=('MIT')
options=(!libtool)
makedepends=('git')
depends=('libxkbcommon' 'wayland-git' 'mesa' 'cairo' 'poppler-glib' 'mtdev' 'libxcursor' 'pixman' 'glu' 'libunwind')
conflicts=('weston')

source=('git://anongit.freedesktop.org/wayland/weston')
sha1sums=('SKIP')

pkgver() {
    cd "weston"

    for i in major_version minor_version micro_version; do
        local _$i=$(grep -m 1 $i configure.ac | sed 's/m4//' | grep -o "[[:digit:]]*")
    done

    echo $_major_version.$_minor_version.$_micro_version.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
    cd "weston"
	./autogen.sh --prefix=/usr --libexecdir=/usr/lib/weston
	make
}

package() {
    cd "weston"
	make DESTDIR="${pkgdir}" install

	for _c in clickdot cliptest dnd editor eventdemo flower gears image \
	 resizor simple-egl simple-shm smoke view; do
		install -Dm755 "clients/$_c" "$pkgdir/usr/bin/weston-$_c"
	done
}

