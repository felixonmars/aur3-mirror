# Maintainer: LeCrayonVert
pkgname=freetuxtv-svn
pkgver=703
pkgrel=1
pkgdesc="WebTV player to watch ADSL TV on the PC with ISPs Free and SFR."
arch=('i686' 'x86_64')
url="http://code.google.com/p/freetuxtv/"
license=('GPL')
depends=('vlc' 'gtk2' 'sqlite3' 'curl' 'libnotify')
makedepends=('subversion' 'libtool')
_svntrunk=http://freetuxtv.googlecode.com/svn/trunk/
_svnmod=freetuxtv-read-only

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD HERE
  #
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
