# Contributor:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>
# Maintainer: Yosef Or Boczko <yoseforb@gnome.org>

_pkgname=gedit-plugins
pkgname=$_pkgname-git
pkgver=3.12.1.2.g268042d
pkgrel=1
pkgdesc="Plugins for gedit"
arch=(x86_64 i686)
license=(GPL)
url="https://wiki.gnome.org/Apps/Gedit/ShippedPlugins"
depends=("gedit>=3.11.3" python-dbus)
makedepends=(intltool gnome-doc-utils vte3 libgit2-glib git)
optdepends=('gucharmap: for charmap plugin'
            'vte3: for embedded terminal'
            'libgit2-glib: for git plugin')
options=('!libtool' '!emptydirs')
install=gedit-plugins.install
replaces=('gedit-plugins')
provides=('gedit-plugins=3.12.1')
conflicts=('gedit-plugins')
source=('git://git.gnome.org/gedit-plugins')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/$_pkgname"
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --with-plugins=all \
      --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
