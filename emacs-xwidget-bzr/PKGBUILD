# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# based on a PKGBUILD of Mathias Nedreb√∏ <mathias <at> nedrebo.org>

pkgname=emacs-xwidget-bzr
pkgver=101552
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Emacs, bazaar-version from the xwidget branch"
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('alsa-lib' 'gpm' 'desktop-file-utils' 
  'hicolor-icon-theme' 'webkitgtk3' 'm17n-lib' 
  'gobject-introspection' 'imagemagick' 'librsvg' 'gconf')
makedepends=('bzr' 'texinfo' 'glproto')
options=('docs' '!emptydirs')
provides=('emacs')
conflicts=('emacs')
install=emacs.install
backup=('usr/share/applications/emacs.desktop' 
  'usr/share/emacs/site-lisp/subdirs.el')
source=(bzr+http://bzr.savannah.gnu.org/r/emacs/xwidget/ 'emacs-24.3-giflib5.patch')
md5sums=('SKIP' '560cdf463299e059c5fadf474bcba218')
_bzrmod=xwidget

pkgver() {
  bzr version-info $srcdir/$_bzrmod --custom --template="{revno}\n"
}

prepare() {
  cd "$srcdir/${_bzrmod}"
  patch -Np0 -i ../emacs-24.3-giflib5.patch
}

build() {
  cd $srcdir/${_bzrmod}
  export LDFLAGS="`pkg-config --libs MagickWand `"
  ./autogen.sh && \
  ac_cv_lib_gif_EGifPutExtensionLast=yes ./configure --prefix=/usr \
    --with-xwidgets --with-x-toolkit=gtk3 --with-sound --libexecdir=/usr/lib \
    --localstatedir=/var --with-rsvg --with-gconf 

  make bootstrap 
  make
}
 
package() {
  cd $srcdir/${_bzrmod}
  make DESTDIR=$pkgdir install
  chown -R root:root $pkgdir/usr
  for _i in 16x16 24x24 32x32 48x48 128x128
  do
    mv $pkgdir/usr/share/icons/hicolor/${_i}/apps/emacs.png \
      $pkgdir/usr/share/icons/hicolor/${_i}/apps/$pkgname.png
  done
  mv $pkgdir/usr/share/icons/hicolor/scalable/apps/emacs.svg \
    $pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg
  mv $pkgdir/usr/share/icons/hicolor/scalable/mimetypes/emacs-document.svg \
    $pkgdir/usr/share/icons/hicolor/scalable/mimetypes/$pkgname-document.svg
  chmod 775 $pkgdir/var/games
  # adding the READMEs. 
  install -Dm644 $srcdir/${_bzrmod}/README \
    $pkgdir/usr/share/doc/$pkgname/README
  install -Dm644 $srcdir/${_bzrmod}/README.xwidget \
    $pkgdir/usr/share/doc/$pkgname/README.xwidget
  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1.gz}
  # remove conflict with the texinfo-package
  rm $pkgdir/usr/share/info/info.info.gz 
  # fix perms on /var/games
  chgrp -R games "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
}
