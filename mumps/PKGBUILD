# Maintainer: Michele Mocciola <mickele>
pkgname=mumps
pkgver=4.9.2
pkgrel=2
pkgdesc="MUMPS solves a sparse system of linear equations A x = b using Gaussian elimination"
url="http://mumps.enseeiht.fr"
license="custom"
depends=('lapack' 'mpich2' 'parmetis-mpich2' 'scotch_esmumps>5.1.7' 'ptscotch>5.1.7' 'blacs-mpi' 'scalapack')
makedepends=('make' 'gcc' 'binutils' 'sed' 'coreutils')
provides=()
conflicts=()
replaces=()
backup=()
arch=('i686' 'x86_64')
install=${pkgname}.install
source=(http://mumps.enseeiht.fr/MUMPS_${pkgver}.tar.gz)

build() {
  cd "${srcdir}/MUMPS_${pkgver}" || return 1

  sed -e "s|\${HOME}/scotch_5.1_esmumps|/usr|" \
      -e "s|#SCOTCHDIR  = /usr|SCOTCHDIR  = /usr|" \
      -e "s|#ISCOTCH    = -I\$(SCOTCHDIR)/include|ISCOTCH    = -I\$(SCOTCHDIR)/include/scotch|" \
      -e "s|#LSCOTCH    = -L\$(SCOTCHDIR)/lib -lptesmumps -lptscotch -lptscotcherr|LSCOTCH    = -L\$(SCOTCHDIR)/lib -lesmumps -lptscotch -lptscotcherr|" \
      -e "s|#LMETISDIR = /local/metis/|LMETISDIR = /opt/mpich2/lib|" \
      -e "s|#LMETIS    = -L\$(LMETISDIR) -lparmetis -lmetis|LMETIS    = -L\$(LMETISDIR) -lparmetis -lmetis|" \
      -e "s|#ORDERINGSF = -Dscotch -Dmetis -Dpord|ORDERINGSF = -Dscotch -Dmetis -Dpord|" \
      -e "s|ORDERINGSF  = -Dpord|#ORDERINGSF  = -Dpord|" \
      -e "s|#RANLIB = ranlib|RANLIB = ranlib|" \
      -e "s|RANLIB  = echo|#RANLIB  = echo|" \
      -e "s|SCALAP  = /local/SCALAPACK/libscalapack.a /local/BLACS/LIB/blacs_MPI-LINUX-0.a   /local/BLACS/LIB/blacsF77init_MPI-LINUX-0.a  /local/BLACS/LIB/blacs_MPI-LINUX-0.a|SCALAP  = -lscalapack -llapack -lblacs -lblacsf77 -lblacs|" \
      -e "s|INCPAR = -I/usr/local/mpich/include|INCPAR = -I/opt/mpich2/include|" \
      -e "s|LIBPAR = \$(SCALAP)  -L/usr/local/mpich/lib/ -lmpich|LIBPAR = \$(SCALAP) -L/opt/mpich2/lib/ -lmpich|" \
      -e "s|CC = gcc|CC = /opt/mpich2/bin/mpicc|" \
      -e "s|FC = gfortran|FC = /opt/mpich2/bin/mpif77|" \
      -e "s|FL = gfortran|FL = /opt/mpich2/bin/mpif77|" \
      -e "s|OPTF    = -O -Dintel_ -DALLOW_NON_INIT|OPTF    = -Dintel_ -DALLOW_NON_INIT ${CFLAGS} -fPIC|" \
      -e "s|OPTL    = -O|OPTL    = ${CFLAGS} -fPIC -I/usr/include -I/opt/mpich2/include|" \
      -e "s|OPTC    = -O|OPTC    = ${CFLAGS} -fPIC|" \
      -e "s|LIBBLAS = -L/local/BLAS -lblas|LIBBLAS = -lf77blas -latlas|" \
      < ${srcdir}/MUMPS_${pkgver}/Make.inc/Makefile.gfortran.PAR > ${srcdir}/MUMPS_${pkgver}/Makefile.inc

  make all || return 1

  # Install all headers
  cd "${srcdir}/MUMPS_${pkgver}/include" || return 1
  install -m 755 -d "${pkgdir}/usr/include" || return 1
  install -D -m644 *.h "${pkgdir}/usr/include" || return 1

  # Install all libraries
  cd "${srcdir}/MUMPS_${pkgver}/lib" || return 1
  install -m 755 -d "${pkgdir}/usr/lib" || return 1
  install -D -m644 lib*.a ${pkgdir}/usr/lib || return 1
  for _FILE in `ls *.a | sed "s|\.a||"`; do
      ld -Bshareable -o ${_FILE}.so.${pkgver} -x -soname ${_FILE}.so --whole-archive ${_FILE}.a || return 1
      install -m 644 -D ${_FILE}.a $startdir/pkg/usr/lib/${_FILE}.a || return 1
      install -m 755 ${_FILE}.so.${pkgver} $startdir/pkg/usr/lib || return 1
      ln -sf ${_FILE}.so.${pkgver} $startdir/pkg/usr/lib/${_FILE}.so.${pkgver:0:1} || return 1
  done

  # Install examples
  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}/examples" || return 1
  cd "${srcdir}/MUMPS_${pkgver}/examples" || return 1
  install -m 644 * "${pkgdir}/usr/share/doc/${pkgname}/examples" || return 1
  for _FILE in ssimpletest dsimpletest csimpletest zsimpletest c_example; do
    chmod 0755 "${pkgdir}/usr/share/doc/${pkgname}/examples/${_FILE}" || return 1
  done

  # Install license
  install -D -m644 "${srcdir}/MUMPS_${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
md5sums=('1c896cdb61878cf094b779404c6512fd')
