# Maintainer: Vinicius de Avila Jorge <vinicius.avila.jorge@gmail.com>

pkgname=('libpurple-meanwhile')
pkgver=2.10.3
pkgrel=2
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
pkgdesc="IM library extracted from Pidgin. Enable protocol Lotus Sametime."
makedepends=('startup-notification' 'gtkspell' 'libxss' 'nss' 'libsasl' 'libsm'
             'libidn' 'python2' 'hicolor-icon-theme' 'gstreamer0.10'
             'farstream' 'avahi' 'tk' 'ca-certificates' 'intltool'
             'networkmanager')
depends=('farstream' 'libsasl' 'libidn' 'dbus-glib' 'nss' 'meanwhile' 'gnome-keyring')
optdepends=('avahi: Bonjour protocol support'
            'dbus-python: for purple-remote and purple-url-handler'
            'telepathy-haze: Empathy support'
            'telepathy-salut: Empathy support')
conflicts=('libpurple')
provides=('libpurple')
options=('!libtool')
source=(http://downloads.sourceforge.net/pidgin/pidgin-$pkgver.tar.bz2
        port-to-farstream-v3.patch)
sha256sums=('0f67d96231fea3945c2735e6a3b4bd92590ef489fa1511fa69aa6a543cb4168b'
            'f0da29d2949ee2f8a47176437cc5ab9c4cc4b29fb45018aeac3e8a59d9dc67f6')

build() {
  cd "$srcdir/pidgin-$pkgver"

  # Apply patch for farsight -> farstream transition
  # http://developer.pidgin.im/ticket/14936
  patch -Np1 -i "$srcdir/port-to-farstream-v3.patch"

  # The farstream patch changes configure.ac
  autoreconf -vi

 # Use Python 2
  sed -i 's/env python$/&2/' */plugins/*.py \
    libpurple/purple-{remote,notifications-example,url-handler}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-schemas-install \
    --disable-gnutls \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --enable-nm \
    --with-python=/usr/bin/python2 \
    --with-system-ssl-certs=/etc/ssl/certs
    make libpurple
}

package(){
  cd "$srcdir/pidgin-$pkgver"

  for _dir in libpurple share/sounds share/ca-certs m4macros po; do
    make -C "$_dir" DESTDIR="$pkgdir" install
  done
}

# vim:set ts=2 sw=2 et:
