# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=sssd-git
pkgver=20120409
pkgrel=1
pkgdesc="System Security Services Daemon"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3')
depends=(
  'bind'          # for nsupdate
  'c-ares'
  'dbus-core'
  'ding-libs'
  'krb5'
  'ldb'
  'libldap'
  'libnl'
  'libsasl'
  'libunistring'
  'libxml2'
  'nspr'
  'nss'
  'pcre'
  'talloc'
  'tdb'
  'tevent'
)
makedepends=(
  'docbook-xsl'
  'doxygen'
  'git'
  'python2'
)
provides=('sssd')
conflicts=('sssd')
source=('sssd.service'
        'sssd.rcd')
sha1sums=('8d92cba4b2491eb677efe59d1088bb83f36569f5'
          '43784f1820a27e426df68e7121f29bc900b5b662')

_gitroot="git://git.fedorahosted.org/git/sssd.git"
_gitname="sssd"

build() {
  cd "$srcdir"
  msg "Updating from $_gitroot"

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
  else
    git clone $_gitroot $_gitname
  fi

  msg "Starting make"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  export PYTHON=/usr/bin/python2
  autoreconf -i -f
  ./configure \
    --prefix=/usr                             \
    --sysconfdir=/etc                         \
    --localstatedir=/var                      \
    --libexecdir=/usr/lib/sssd                \
    --enable-pammoddir=/lib/security          \
    --with-autofs                             \
    --with-initscript=systemd                 \
    --with-os=fedora                          \
    --with-pid-path=/run                      \
    --with-python-bindings                    \
    --without-selinux                         \
    --without-semanage                        \
    --with-ssh                                \
    --with-sudo                               \
    --with-systemdunitdir=/lib/systemd/system \
    ;
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/etc/rc.d"
  rm -rf "$pkgdir/lib/systemd"
  rm -f "$pkgdir/usr/modules/ldb/memberof.la"

  cd "$srcdir"
  install -Dm0644 sssd.service  "$pkgdir/usr/lib/systemd/system/sssd.service"
  install -Dm0755 sssd.rcd      "$pkgdir/etc/rc.d/sssd"
} 

# vim: ts=2:sw=2:et
