# Maintainer:  WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributer: J.W Birdsong < @ gmail>
# Contributor: Igor Nemilentsev <trezorg dog gmail.com>
# Contributor: Miroslaw "firestarter" Wojtylak mwojtylak@gmail.com
# Based on PKGBUILD from Dan Zwell <dzwell@zwell.net>

pkgname=btrfs-progs-git
_gitname=${pkgname/-git}
pkgver=v0.20.rc1.324.g650e656
pkgrel=2
pkgdesc="Btrfs filesystem utilities"
arch=("i686" "x86_64")
url="http://btrfs.wiki.kernel.org/index.php/Main_Page"
license=('GPL')
depends=('glibc' 'e2fsprogs' 'lzo2')
makedepends=('git')
provides=('btrfs-progs')
conflicts=('btrfs-progs')
source=("git://git.kernel.org/pub/scm/linux/kernel/git/mason/btrfs-progs.git")
md5sums=("SKIP")

pkgver() {
  cd "$_gitname"
  git --no-pager describe --tags | sed 's:-:.:g'
}

build() {
  cd "$_gitname"
  sed -i "s|-Werror|-Wno-error|" Makefile
  make all btrfs-zero-log
}

package() {
  cd "$_gitname"
  make prefix="$pkgdir/usr" bindir="$pkgdir/usr/bin" install
  install -Dm644 INSTALL "$pkgdir/usr/share/doc/btrfs/README"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share/"
  mv btrfs-zero-log "$pkgdir/usr/bin"
}

# vim:set ts=2 sw=2 et:
