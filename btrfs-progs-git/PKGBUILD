# Maintainer:  WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributer: J.W Birdsong < @ gmail>
# Contributor: Igor Nemilentsev <trezorg dog gmail.com>
# Contributor: Miroslaw "firestarter" Wojtylak mwojtylak@gmail.com
# Based on PKGBUILD from Dan Zwell <dzwell@zwell.net>

pkgname=btrfs-progs-git
_gitname=${pkgname%-git}
pkgver=3.14
pkgrel=1
pkgdesc="Btrfs filesystem utilities"
arch=("i686" "x86_64")
url="http://btrfs.wiki.kernel.org/index.php/Main_Page"
license=('GPL')
depends=('glibc' 'e2fsprogs' 'lzo2' 'zlib')
makedepends=('git')
provides=('btrfs-progs')
conflicts=('btrfs-progs')
_url=https://projects.archlinux.org/svntogit/packages.git/plain/trunk/
source=("git://git.kernel.org/pub/scm/linux/kernel/git/mason/btrfs-progs.git"
        "initcpio-hook-btrfs::${_url}initcpio-hook-btrfs?h=packages/btrfs-progs"
        "initcpio-install-btrfs::${_url}initcpio-install-btrfs?h=packages/btrfs-progs")
md5sums=('SKIP'
         'b09688a915a0ec8f40e2f5aacbabc9ad'
         '7241ba3a4286d08da0d50b7176941112')

pkgver() {
  cd "$_gitname"
  git --no-pager describe --tags | sed -e 's:^v::' -e 's:-:.:g'
}

build() {
  cd "$_gitname"
  make CFLAGS="$CFLAGS" all btrfs-select-super
}

package() {
  # install mkinitcpio files
  install -Dm644 initcpio-hook-btrfs "$pkgdir/usr/lib/initcpio/hooks/btrfs"
  install -Dm644 initcpio-install-btrfs "$pkgdir/usr/lib/initcpio/install/btrfs"

  cd "$_gitname"
  # install compiled software + readme
  make prefix="$pkgdir/usr" install
  install -m755 btrfs-select-super "$pkgdir/usr/bin/btrfs-select-super"
  install -Dm644 INSTALL "$pkgdir/usr/share/doc/btrfs/README"

  # fix manpage location
  mv "$pkgdir/usr/man" "$pkgdir/usr/share/"
}

check() {
  cd "$_gitname"
  make test
}

# vim:set ts=2 sw=2 et:
