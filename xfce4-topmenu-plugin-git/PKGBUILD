# $Id: PKGBUILD 111946 2014-05-27 08:42:58Z jgc $
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=xfce4-topmenu-plugin-git
_gitname=topmenu-gtk
pkgver=2015.02.15
pkgrel=1
pkgdesc="TopMenu Xfce panel plugin"
url="https://gitorious.org/topmenu"
arch=('i686' 'x86_64')
license=('LGPL3')
depends=('topmenu-gtk-git' 'xfce4-panel')
makedepends=('git')
optdepends=('topmenu-gtk3-git: for gtk3 apps menu'
            'topmenu-qt-git: for qt apps menu'
            'topmenu-mozilla-git: for mozilla applications')
options=(!libtool)
source=("git://gitorious.org/topmenu/$_gitname.git")
sha1sums=('SKIP')

pkgver() {
   cd "$_gitname"
   git log -1 --format="%cd" --date=short | sed 's|-|.|g'
}

build() {
   cd "${srcdir}/${_gitname}"

   autoreconf -fi
   ./configure --prefix=/usr --with-gtk=2 --with-wnck=wnck1 --disable-static \
      --disable-mate-applet --enable-xfce-applet --libexecdir=/usr/lib/topmenu
   make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="${pkgdir}" install
    rm -rf ${pkgdir}/usr/{include,lib/gtk-2.0,lib/libtopmenu*,share/icons}
    #rm -rf "${pkgdir}/usr/{include,lib/gtk-2.0,lib/libtopmenu*,lib/topmenu,share/dbus-1,share/icons,share/mate-panel}"
}
