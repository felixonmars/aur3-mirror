# Maintainer: Prurigro
# Contributor: Werecat
# Contributor: Xyne

_pkgname=cjdns
pkgname=${_pkgname}-git
pkgver=0.3.2421
pkgrel=1
pkgdesc="A routing engine designed for security, scalability, speed and ease of use"
url="https://github.com/cjdelisle/${_pkgname}"
license=('GPL3')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
depends=('libseccomp')
makedepends=('git' 'nodejs' 'python2')
optdepends=('libnacl: speed up the build process by skipping the need to compile cnacl'
            'cjdnsify: allows you to run some programs bound to your cjdns address'
            'cjdns-git-sysvinit: support for sysvinit (rc.d)')
options=('!strip')

source=("git://github.com/cjdelisle/${_pkgname}.git#branch=master")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    git describe --always | sed 's|-|.|g;s|[^\.]*\.||;s|\.[^\.]*$||'
}

build() {
    cd $_pkgname
    unset CFLAGS CPPFLAGS CXXFLAGS
    ./do
}

package() {
    install -Dm644 ${_pkgname}/doc/man/cjdroute.conf.5 "${pkgdir}/usr/share/man/man5/cjdroute.conf.5"
    install -Dm755 ${_pkgname}/cjdroute "${pkgdir}/usr/bin/cjdroute"
    install -Dm644 ${_pkgname}/contrib/systemd/${_pkgname}.service "${pkgdir}/usr/lib/systemd/system/${_pkgname}.service"
}
