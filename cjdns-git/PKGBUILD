# Maintainer: Prurigro
# Contributor: Prurigro
# Contributor: Werecat
# Contributor: Xyne

_pkgname=cjdns
pkgname=${_pkgname}-git
pkgver=0.3.2326
pkgrel=1
pkgdesc="A routing engine designed for security, scalability, speed and ease of use"
url="https://github.com/cjdelisle/${_pkgname}"
license=('GPL3')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
depends=('glibc' 'libseccomp')
makedepends=('git' 'nodejs' 'python' 'python2')
optdepends=('libnacl: speed up the build process by skipping the need to compile cnacl'
            'cjdscript: provides the collection of working scripts included with cjdns'
            'cjdnsify: allows you to run programs bound exclusively to your cjdns connection'
            'cjdns-git-sysvinit: support for sysvinit style init (rc.d)')

source=("git://github.com/cjdelisle/${_pkgname}.git#branch=master")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    git describe --always | sed 's|-|.|g;s|[^\.]*\.||;s|\.[^\.]*$||'
}

prepare() {
    if [[ ! -e pybin/python ]]; then install -d pybin && ln -s /usr/bin/python2 pybin/python; fi
    sed '/^\.SH\ "SEE\ ALSO"$/Q' ${_pkgname}/doc/man/cjdroute.conf.5 | gzip > cjdroute.conf.5.gz
}

build() {
    cd $_pkgname
    unset MAKEFLAGS CFLAGS CPPFLAGS
    PATH="${srcdir}/pybin:$PATH" ./do
}

package() {
    install -Dm644 cjdroute.conf.5.gz "${pkgdir}/usr/share/man/man5/cjdroute.conf.5.gz"
    cd $_pkgname
    install -Dm755 cjdroute "${pkgdir}/usr/bin/cjdroute"
    install -Dm644 contrib/systemd/${_pkgname}.service "${pkgdir}/usr/lib/systemd/system/${_pkgname}.service"
}
