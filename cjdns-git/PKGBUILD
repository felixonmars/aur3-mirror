# Maintainer: Kevin MacMartin <prurigro at gmail dot com>
# Contributor: Werecat
# Contributor: Xyne

_pkgname=cjdns
pkgname=${_pkgname}-git
pkgver=0.3.2841
pkgrel=1
pkgdesc="A routing engine designed for security, scalability, speed and ease of use"
url="https://github.com/cjdelisle/cjdns"
license=('GPL3')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
options=('!distcc' '!strip')
depends=('glibc')
makedepends=('git' 'nodejs' 'python2')
optdepends=('cjdnsify: allows you to run some programs bound to your cjdns address'
            'libnacl: speed up the build process by skipping the need to compile cnacl'
            'cjdns-git-sysvinit: support for sysvinit (rc.d)')

source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    git describe --always | sed 's|-|.|g;s|[^\.]*\.||;s|\.[^\.]*$||'
}

build() {
    cd $_pkgname
    unset CFLAGS CPPFLAGS CXXFLAGS
    bash 'do'
}

package() {
    cd $_pkgname
    install -Dm644 doc/man/cjdroute.conf.5 "$pkgdir"/usr/share/man/man5/cjdroute.conf.5
    install -Dm755 cjdroute "$pkgdir"/usr/bin/cjdroute
    install -Dm755 contrib/bash/run-cjdroute.bash "$pkgdir"/usr/bin/run-cjdroute
    install -Dm644 contrib/systemd/${_pkgname}.service "$pkgdir"/usr/lib/systemd/system/${_pkgname}.service
}
