# Maintainer: Prurigro
# Contributor: Prurigro
# Contributor: Werecat
# Contributor: Xyne

_pkgname=cjdns
pkgname=${_pkgname}-git
pkgver=0.3.2323
pkgrel=1
pkgdesc="A routing engine designed for security, scalability, speed and ease of use"
url="https://github.com/cjdelisle/${_pkgname}"
license=('GPL3')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
groups=('archassault' 'archassault-networking')
depends=('glibc' 'libseccomp')
makedepends=('git' 'nodejs' 'python2')
optdepends=('libnacl: speed up the build process by skipping the need to compile cnacl' 'cjdscript: provides the collection of working scripts included with cjdns' 'cjdnsify: allows you to run programs bound exclusively to your cjdns connection' 'cjdns-git-sysvinit: support for sysvinit style init (rc.d)')
source=("git://github.com/cjdelisle/${_pkgname}.git#branch=master")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    git describe --always | sed 's|-|.|g;s|[^\.]*\.||;s|\.[^\.]*$||'
}

build() {
    cd $_pkgname
    unset MAKEFLAGS CFLAGS CPPFLAGS
    PATH="${srcdir}/pybin:$PATH" NO_DEBUG=1 ./do
}

package() {
    cd $_pkgname
    install -D -m755 cjdroute "${pkgdir}"/usr/bin/cjdroute
    install -D -m644 contrib/systemd/${_pkgname}.service "${pkgdir}"/usr/lib/systemd/system/${_pkgname}.service
}
