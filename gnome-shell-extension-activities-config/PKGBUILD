# Maintainer: XZS <d.f.fischer at web dot de>

pkgname="gnome-shell-extension-activities-config"
pkgdesc="Configure the Activities Button with the Activities Configurator Extension and the Gnome Shell Extension Preferences Tool."
pkgver=35
pkgrel=1
arch=(any)
url="https://extensions.gnome.org/extension/358/activities-configurator/"
license=(GPLv2)
depends=('gnome-shell>=3.8')
install=gschemas.install
source=("extension.html::$url"
        'archlinux-logo-gray-scalable.svg')
md5sums=('SKIP'
         '69063bc3433d20d9de0f9dce2269186b')

find_version() {
  if [ ! -d version ]; then
    grep -Po '(?<=data-svm=").*(?=")' extension.html |  # extract version information
    sed 's/&quot;/"/g' |  # unescape quotes
    sed 's/[{ ,]*"[[:digit:].]\+": {"pk": \([[:digit:]]\+\), "version": \([[:digit:]]\+\)}[ ,}]*/\2 \1\n/g' |  # list one version per line
    sort -nr | head -n1 | tr ' ' '\n' > version  # get newest and write
  fi
}

pkgver() {
  find_version
  head -n1 version
}

prepare() {
  find_version
  curl -Lo extension.zip "https://extensions.gnome.org/review/download/$(tail -n1 version).shell-extension.zip"
  unzip -d extension extension.zip
}

build() {
  cd extension
  rm face-smile-3.svg schemas/gschemas.compiled
  chmod 644 metadata.json
  find -type d -exec chmod 755 {} \;
  sed -i '/^const ICON_FILE =.*$/{
    s|face-smile-3.svg|archlinux-logo-gray-scalable.svg|;
    s|//.*|// Arch Linux Icon.|}' keys.js
  cp --remove-destination ../archlinux-logo-gray-scalable.svg archlinux-logo-gray-scalable.svg
}

package() {
  cd extension
  destdir="$pkgdir/usr/share/gnome-shell/extensions/activities-config@nls1729"
  schemadir="$pkgdir/usr/share/glib-2.0"
  install -d "$destdir" "$schemadir"
  mv schemas "$schemadir"
  mv * "$destdir"
}
