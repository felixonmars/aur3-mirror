# Contributor: Slash <demodevil5[at]yahoo[dot]com>

pkgname=mpd-git
pkgver=20100902
pkgrel=1
pkgdesc="music daemon that plays mp3, flac, aac, mod, wav, mpc and ogg files"
url="http://musicpd.org"
depends=(
    'alsa-lib' 'audiofile' 'curl' 'faad2' 'ffmpeg' 'flac' 'glib2'
    'libcdio' 'libid3tag' 'libmad' 'libmikmod' 'libmodplug' 'libmpcdec'
    'libsamplerate' 'libvorbis' 'libid3tag' 'libshout' 'mpg123' 'wavpack' 'zziplib')
makedepends=('autoconf' 'automake' 'git' 'libtool' 'pkgconfig')
optdepends=(
    'avahi: Support for Avahi Zeroconf Backend'
    'jack-audio-connection-kit: Support for JACK Audio'
    'lame: Required to serve mp3 streams (or use twolame)'
    'libao: Audio Library not recommended unless OSS/ALSA does not work'
    'libao-pulse: Support for the PulseAudio Sound Server (if using libao)'
    'libcue: Support for embedded cuesheets'
    'libmms: Support for MMS Protocol'
    'pulseaudio: Support for the PulseAudio Sound Server'
    'sqlite3: Support for SQLite Database'
    'twolame: Required to serve mp3 streams (or use lame)'
)
license=('GPL')
arch=('i686' 'x86_64')
conflicts=('mpd')
provides=('mpd')
replaces=('mpd-svn')
install=mpd.install
source=('mpd.init')
md5sums=('ab1e793097e612b7df7acee275206167')

_gitroot="git://git.musicpd.org/master/mpd.git"
_gitname="mpd"

build() {
    cd $srcdir

    msg "Connecting to GIT server..."

    if [ -d $srcdir/$_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    # Copy Latest files to Build Directory
    cp -r $srcdir/$_gitname $srcdir/$_gitname-build
    cd $srcdir/$_gitname-build

    # Configure Source
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-ao \
        --disable-jack \
        --disable-pulse \
        --enable-bzip2 \
        --enable-iso9660 \
        --enable-lastfm \
        --enable-modplug \
        --enable-zzip \
        --with-zeroconf=no
        #--enable-cue \
        #--enable-sqlite \
        #--enable-mms \
        #--enable-mvp \

    # Build Source
    make || return 1

    # Install Source
    make prefix=$pkgdir/usr install

    # Remove Build Directory
    rm -r $srcdir/$_gitname-build/

    # Create Directories
    install -d $pkgdir/var/{log/mpd,run/mpd,lib/mpd/playlists}

    # Install init Script
    install -D -m755 $srcdir/mpd.init \
        $pkgdir/etc/rc.d/mpd

    # Install Sample Config
    install -D -m644 $srcdir/mpd/doc/mpdconf.example \
        $pkgdir/etc/mpd.conf.example

    # Modify Sample Config with Proper Directories and User Settings
    /bin/sed -i 's|music_directory.*$|#music_directory "path_to_your_music_collection"|1' $pkgdir/etc/mpd.conf.example
    /bin/sed -i 's|playlist_directory.*$|playlist_directory "/var/lib/mpd/playlists"|1' $pkgdir/etc/mpd.conf.example
    /bin/sed -i 's|db_file.*$|db_file "/var/lib/mpd/mpd.db"|1' $pkgdir/etc/mpd.conf.example
    /bin/sed -i 's|log_file.*$|log_file "/var/log/mpd/mpd.log"|1' $pkgdir/etc/mpd.conf.example
    /bin/sed -i 's|error_file.*$|error_file "/var/log/mpd/mpd.error"|1' $pkgdir/etc/mpd.conf.example
    /bin/sed -i 's|#pid_file.*$|pid_file "/var/run/mpd/mpd.pid"|1' $pkgdir/etc/mpd.conf.example
    /bin/sed -i 's|#state_file.*$|state_file "/var/lib/mpd/mpdstate"|1' $pkgdir/etc/mpd.conf.example
    /bin/sed -i 's|#user.*$|user "mpd"|1' $pkgdir/etc/mpd.conf.example
}

