# $Id: PKGBUILD 72136 2012-06-09 00:24:01Z dreisner $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=courier-authlib
pkgver=0.65.0
pkgrel=4
pkgdesc="Authentification library for the courier mailserver(s)"
arch=(i686 x86_64)
license=('GPL2')
url="http://courier-mta.org/authlib/"
backup=('etc/authlib/authdaemonrc' 'etc/authlib/authldaprc' \
        'etc/authlib/authmysqlrc' 'etc/authlib/authpgsqlrc')
depends=('openssl' 'gdbm' 'perl' 'libtool' 'expect')
makedepends=('pam' 'expect' 'libldap' 'libmysqlclient' 'postgresql-libs>=8.3.0')
optdepends=('libmysqlclient' 'libldap' 'postgresql-libs')
conflicts=('courier-imap-mysql' 'courier-imap-pgsql' 'courier-imap-ldap')
provides=('courier-imap-mysql' 'courier-imap-pgsql' 'courier-imap-ldap')
options=(!libtool)
install=${pkgname}.install
source=(http://downloads.sourceforge.net/project/courier/authlib/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        courier-authlib.tmpfiles
        authdaemond.rc.d
	authdaemond.service)
sha512sums=('edc4a51bc0d748e7d6cdbf9e9c67c43eeedd9e3846d91a8eea24613f6fb5125d2e713ef7fd63cbbe1905205f2a406b4ff3f31d7c3fb7028939123dbc728d8e38'
            'eb18f71ed632f67c95f22c8bf637feb8fff96e6449dbcec737132574a0e04ad8acd80da259c63a98a8c6933d515eec1929d16a65d2414ef2283d42f16e4e486b'
            '9c6859070c64df80fcbaaca530c4b3fdac46ebce3522b53a872ea2e057319b5f66d5ed298090bfd99943ae9fca968be910cd69b6b2abfd6661cdbc82f23bd2a0'
	    '32e784fed40406e24a718ddec6cb14f426a2d2637f2039efd5133b55291869b4554f12b0ba395cfb3b8ca990a4f899c31558675cb0c62cd36f94b7d663c8482a')

build() {
  #export MAKEFLAGS="-j1"
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --with-db=gdbm \
    --with-mailuser=courier --with-mailgroup=courier \
    --with-authpam --with-authpwd --with-authshadow \
    --with-authldap --with-authmysql --with-authpgsql \
    --with-authuserdb --with-authcram --with-authdaemon \
    --with-authdaemonvar=/run/authdaemon
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  ###############################################################################
  # post_installation ---- rename the config file and change ownerschip
  for distfile in ${pkgdir}/etc/authlib/*.dist; do
    chown 72:72 ${distfile}
    mv ${distfile} ${pkgdir}/etc/authlib/`basename ${distfile} .dist`
  done
   # copy the .schema; mostly refered to as courier.schema -> rename it
  install -Dm 444 authldap.schema \
    ${pkgdir}/etc/openldap/schema/courier.schema
  ###############################################################################
  # Install daemon, that wraps couriers bashscript
  install -Dm 755 ${srcdir}/authdaemond.rc.d ${pkgdir}/etc/rc.d/authdaemond
  # Install systemd daemon
  install -Dm 644 "${srcdir}/authdaemond.service" "${pkgdir}/usr/lib/systemd/system/authdaemond.service"
  #mkdir -p ${pkgdir}/var/spool/authdaemon
  #chown -R 72:72 ${pkgdir}/var/spool/authdaemon
  mkdir -p ${pkgdir}/var/spool/courier
  chown -R 72:72 ${pkgdir}/var/spool/courier
  # docs say we can remove .a files after make
  find ${pkgdir} -name '*\.a' -exec rm -f {} \;
  # Make libs available to /usr/lib
  cd $pkgdir/usr/lib
  for lib in courier-authlib/*.so; do
      ln -s $lib .
  done

  install -Dm644 "$srcdir/courier-authlib.tmpfiles" "$pkgdir/usr/lib/tmpfiles/courier-authlib.conf"

  # avoid conflict with filesystem>=2012.06
  rmdir "$pkgdir/run/authdaemon"

}
