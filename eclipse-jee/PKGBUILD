# Maintainer: Giuliano Schneider <gs93@gmx.net>
# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

[ $CARCH = "x86_64" ] && _arch=-x86_64
_eclipse_name=kepler
_eclipse_release=SR2

pkgname=eclipse-jee
pkgver=4.3.2
pkgrel=1
pkgdesc="Eclipse IDE for Java EE Developers"
arch=('i686' 'x86_64')
url="http://www.eclipse.org"
license=('EPL')
depends=('java-environment>=6' 'gtk2' 'xulrunner' 'webkitgtk2')
provides=("eclipse=$pkgver")
conflicts=('eclipse')
install=${pkgname}.install
source=(eclipse.sh eclipse.desktop eclipse.ini.patch eclipse.svg
    "http://www.eclipse.org/downloads/download.php?file=/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/${pkgname}-${_eclipse_name}-${_eclipse_release}-linux-gtk${_arch}.tar.gz&r=1")

changelog=${pkgname}.ChangeLog.markdown

md5sums=('00598e0866353f7a7b1a5ed65dc01610'
         '0537090ceeb11a2af66676481e8cf797'
         'a68931ca9e59dc8e760a2976abab8fd0'
         '5e9975a49de88815a731cbd4c77a136e'
         'be87f4d997692994734b33dd10dc02bd')

[ "$CARCH" = "x86_64" ] && md5sums[4]='abea2e3c6e3402ae2912337b550cf61e'

# prevent compression
PKGEXT=".pkg.tar"

build() {
	cd "$srcdir"

	patch -p1 < eclipse.ini.patch
}

package() {
	local _icon_path="/usr/share/eclipse/plugins/org.eclipse.platform_${pkgver}.*"

	install -m755 -d "$pkgdir"/usr/{bin,share/applications}
	install -m755 -d "$pkgdir"/usr/share/icons/hicolor/{16x16,32x32,48x48,256x256,scalable}/apps

	cd "$srcdir"

	mv eclipse "$pkgdir/usr/share"
	install -D -m 755 eclipse.sh "$pkgdir/usr/bin/eclipse"
	install -D -m 644 eclipse.desktop "$pkgdir/usr/share/applications"
	install -D -m 644 eclipse.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse.svg"
	ln -s ${_icon_path}/eclipse16.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/eclipse.png"
	ln -s ${_icon_path}/eclipse32.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/eclipse.png"
	ln -s ${_icon_path}/eclipse48.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/eclipse.png"
	ln -s ${_icon_path}/eclipse256.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/eclipse.png"
}
