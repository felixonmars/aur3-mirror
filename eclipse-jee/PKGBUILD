# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

[ $CARCH = "x86_64" ] && _arch=-x86_64
_eclipse_name=juno
_eclipse_release=SR1
_eclipse_timestamp=201209141800

pkgname=eclipse-jee
pkgver=4.2.1
pkgrel=1
pkgdesc="Eclipse IDE for Java EE Developers"
arch=('i686' 'x86_64')
url="http://www.eclipse.org"
license=('EPL')
depends=('java-environment>=6' 'gtk2>=2.16.1' 'xulrunner>=1.9.0.10')
provides=('eclipse')
conflicts=('eclipse')
install=${pkgname}.install
source=(eclipse.sh eclipse.desktop eclipse.ini.patch eclipse.svg
		http://ftp.halifax.rwth-aachen.de/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/${pkgname}-${_eclipse_name}-${_eclipse_release}-linux-gtk${_arch}.tar.gz)
changelog=${pkgname}.ChangeLog.markdown

md5sums=('00598e0866353f7a7b1a5ed65dc01610'
         '0537090ceeb11a2af66676481e8cf797'
         'a68931ca9e59dc8e760a2976abab8fd0'
         '5e9975a49de88815a731cbd4c77a136e'
         'af0f47271f66ba92df6fa9b14f2820ca')
sha256sums=('4cca2873697a3af39a96449021d7fdc2fc2b01abc9f7883946081f6be7a5ed48'
            '4eb2189c96fcfa340886b049b34dc3636d7b2bfa865140dc72edb61455d900c3'
            '4a86578ebbe8adddf731b6bef31c05277ea792cb22d733b6a5c0a25f65546caf'
            '6fe3ab198af244f9c8c2463b6837855506e811f61e5fd8ac7c9d5fe348830a14'
            '941c3018a2fb541904ddd9eac3c356a43e506bfbc54b12613fa1e241a6b2e012')

[ "$CARCH" = "x86_64" ] && md5sums[4]='18f82378b0b99652e40004e9c9207ea5'
[ "$CARCH" = "x86_64" ] && sha256sums[4]='9d224523ecc3b09ad4c4edd41d6105833270b54445ed914dc0906ff8f1eccb7f'

build() {
	cd $srcdir

	patch -p1 < eclipse.ini.patch
}

package() {
	local _icon_path=/usr/share/eclipse/plugins/org.eclipse.platform_${pkgver}.v${_eclipse_timestamp}

	install -m755 -d $pkgdir/usr/{bin,share/applications}
	install -m755 -d $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,scalable}/apps

	cd $srcdir

	mv eclipse $pkgdir/usr/share
	install -D -m 755 eclipse.sh $pkgdir/usr/bin/eclipse
	install -D -m 644 eclipse.desktop $pkgdir/usr/share/applications
	install -D -m 644 eclipse.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse.svg
	ln -s ${_icon_path}/eclipse.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png
	ln -s ${_icon_path}/eclipse32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png
	ln -s ${_icon_path}/eclipse48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png
}
