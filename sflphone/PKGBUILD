# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Klaus Kurzmann <mok@mnet-online.de>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Maurizio Porrato <maurizio.porrato@gmail.com>

pkgname=sflphone
pkgver=0.9.13
pkgrel=1
pkgdesc="SIP/IAX2 compatible softphone"
arch=('i686' 'x86_64')
url="http://www.sflphone.org/"
license=('GPL3')
depends=('libgnomeui' 'evolution-data-server' 'pulseaudio' 'gsm>=0.9.7' 'libzrtpcpp' 'log4c' 'check' 'gnome-doc-utils' 'libyaml' 'libwebkit' 'celt-0.7')
makedepends=('autoconf' 'astyle')
source=("https://projects.savoirfairelinux.com/attachments/download/2162/sflphone-${pkgver}.tar.gz"
        "astylerc.diff"
        "patch-libnotify.patch")
md5sums=('203bae7342b80ab87c1c606232d9b38e'
         '47cd8f8a9ab0d66b63d041143c6ae7d5'
         '3c1d47cee1ba70d74fae81a8966c47aa')

# see https://projects.savoirfairelinux.com/wiki/sflphone/BuildInstructions
# for compilation procedure

build() {
  # CORE INSTALLATION
  # Fix astylerc
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../astylerc.diff
  patch -p1 -i $srcdir/patch-libnotify.patch
  # Compile pjsip
  cd "$srcdir/$pkgname-$pkgver/sflphone-common/libs/pjproject"

  ./autogen.sh
  ./configure
  MAKEFLAGS="" make dep
  make clean
  MAKEFLAGS="" make
  
  # compile core
  cd "$srcdir/$pkgname-$pkgver/sflphone-common"

  ./autogen.sh
  ./configure --prefix=/usr
  make
  make prefix="$pkgdir/usr" install

  # GNOME CLIENT INSTALLATION
  cd "$srcdir/$pkgname-$pkgver/sflphone-client-gnome"

  LDFLAGS="" ./autogen.sh
  LDFLAGS="" ./configure --prefix=/usr
  LDFLAGS="" make
  make prefix="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 et:
