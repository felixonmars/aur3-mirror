# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Klaus Kurzmann <mok@mnet-online.de>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Maurizio Porrato <maurizio.porrato@gmail.com>

pkgname=sflphone
pkgver=1.2.3
pkgrel=2
pkgdesc="SIP/IAX2 compatible softphone"
arch=('i686' 'x86_64')
url="http://www.sflphone.org/"
license=('GPL3')
depends=('libpulse' 'gsm>=0.9.7' 'libzrtpcpp' 'log4c' 'check' 'libyaml' 'webkitgtk3' 'celt' 'dbus-c++' 'libnotify' 'speex')
makedepends=('autoconf' 'astyle' 'gnome-doc-utils' 'libgnomeui' 'yelp-tools' 'gnome-common')
optdepends=('sflphone-plugins: additional plugins')
source=("https://projects.savoirfairelinux.com/attachments/download/6423/${pkgname}-${pkgver}.tar.gz")
sha1sums=('ba15d874b5d03ffa70f75f61b614194d2172cd07')
options=(!makeflags)

build() {
  # PJSIP
  cd "${srcdir}/${pkgname}-${pkgver}/daemon/libs/pjproject-2.0.1"
  ./configure --disable-ilbc-codec
  make dep
  make
  
  # daemon
  cd "${srcdir}/${pkgname}-${pkgver}/daemon"
  ./autogen.sh
  ./configure --prefix=/usr --disable-ilbc
  make

  # Gnome client
  cd "${srcdir}/${pkgname}-${pkgver}/gnome"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/daemon"
  make DESTDIR="${pkgdir}" install
  mkdir "${pkgdir}/usr/lib/sflphone/audio"
  mv "${pkgdir}/usr/lib/sflphone/codecs/" "${pkgdir}/usr/lib/sflphone/audio/"
  
  cd "${srcdir}/${pkgname}-${pkgver}/gnome"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
