## Maintainer : gee
## based of shahid's bitcoin pkgbuild

name=litecoin
pkgname=$name
pkgver=0.6.3
pkgrel=4
pkgdesc="Litecoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.litecoin.org/"
depends=('qt>=4.6' 'libpng>=1.4' 'expat' 'gcc-libs' 'boost-libs>=1.46' 'miniupnpc>=1.6' 'openssl')
makedepends=('boost' 'gcc' 'make' 'automoc4')
optdepends=('cpuminer')
license=('MIT')
commit="e4406b2fa04ae4f0cfb2192953b56bd5da4dffc8"
shortcommit="e4406b2"
pkgfile="https://github.com/litecoin-project/litecoin/zipball/${commit}"
source=("${pkgname}-${pkgver}.zip::${pkgfile}"
	"${pkgname}.desktop")


s1="${srcdir}/${pkgname}-project-${pkgname}-${shortcommit}"
src_qt=$s1/
src_d=$s1/src/
makefile_unix=makefile.unix
makefile_qt=Makefile
build() {
	cd $src_d
	
	msg "Patching $makefile_unix..."
	sed -i $makefile_unix \
		-e 's/\(-DNOPCH\)/\1 -DBOOST_FILESYSTEM_VERSION=2/' \
		-e 's/-Bstatic/--as-needed ${LDFLAGS}/g' \
		-e 's/\(USE_UPNP:=\)0/\11/' \
		-e 's/$(DEBUGFLAGS)//g' \
		-e 's/CXXFLAGS/CPPFLAGS/g' \
		-e 's/-O[0-3]/${CXXFLAGS}/g' \
		-e 's/${CXXFLAGS}/${CXXFLAGS} -fpermissive/g'

	# make litecoind
	make $MAKEFLAGS -f $makefile_unix litecoind

	# and make qt gui
	cd $src_qt
	qmake
	sed -i '/CXXFLAGS      / s/$/ -fpermissive/' $makefile_qt
	make $MAKEFLAGS
}


package() {
	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/share/pixmaps
	mkdir -p $pkgdir/usr/share/applications

	# get compiled binaries
	install -D -m755 $src_qt/litecoin-qt $pkgdir/usr/bin/
	install -D -m755 $src_d/litecoind $pkgdir/usr/bin/
	
	# add icon and .desktop file to pkg
	install -D -m644 $src_qt/src/qt/res/icons/bitcoin.png $pkgdir/usr/share/pixmaps/litecoin.png
	install -D -m644 $srcdir/$name.desktop $pkgdir/usr/share/applications/ 
}
md5sums=('d6774c7fdfdd7fd09bcf3ec58e181bb4'
         '77139ce8a40f46bd7d7ad0a743672113')
