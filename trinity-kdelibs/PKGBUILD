# $Id$
# Maintainer: David C. Rankin <drankinatty@gmail.com>
#
# All modifications and uses of this file are licensed under
# the software for which this file was made for, should the software
# be under an Open Source License, at least version 1.9, defined
# by the Open Source Initiative. In other cases, this file is automatically
# released to the Public Domain.
#

pkgname=trinity-kdelibs
pkgver=3513
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL')
groups=('trinity-base')
pkgdesc="Trinity - kdelibs"
depends=('libical'
        'libxslt'
        'avahi'
        'pam-krb5'
        'taglib'
        'trinity-arts')
makedepends=('pkgconfig' 'cmake' 'autoconf' )
provides=('trinity-kdelibs' 'kdelibs3')
conflicts=('trinity-kdelibs' 'kdelibs3')
replaces=('trinity-kdelibs')
options=('libtool' '!strip' '!makeflags')
source=(http://mirror.its.uidaho.edu/pub/trinity/releases/3.5.13/kdelibs-3.5.13.tar.gz)
md5sums=('f62eefb63b4ba2141b4c576f859806dd')


qtdir=/opt/qt
trinity_prefix="/opt/trinity"

build() {

  msg "Setting PATH, CMAKE and Trinity Environment variables"
  if [[ -r /etc/profile.d/qt3.sh ]]; then
    . /etc/profile.d/qt3.sh
    [[ -n $QTDIR ]] && _qtdir=$QTDIR
  else
    [[ ${PATH%%:*} =~ ${_qtdir}/bin ]] || export PATH=${_qtdir}/bin:$PATH
  fi
  export CMAKE_PREFIX_PATH=/opt/qt:/opt/trinity
  export CMAKE_INCLUDE_PATH=/opt/qt/include/tqt:/usr/include/dbus-1.0:/opt/trinity/bin
  export LD_LIBRARY_PATH=/opt/trinity/lib:/opt/qt/lib:/opt/trinity/lib/kde3:$LD_LIBRARY_PATH

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p build
  cd build

  msg "Starting cmake..."
  cmake ${srcdir}/kdelibs \
    -DCMAKE_INSTALL_PREFIX=${trinity_prefix} \
    -DCMAKE_VERBOSE_MAKEFILE=OFF \
    -DQT_VERSION=3 \
    -DWITH_QT3=ON \
    -DQTDIR=/opt/qt \
    -DQT_LIBRARY_DIRS=/opt/qt/lib \
    -DWITH_PAM=ON \
    -DBUILD_ALL=ON
  make
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build

  make DESTDIR="$pkgdir/" install

  install -d -m755 ${pkgdir}/etc/ld.so.conf.d/
  echo "${trinity_prefix}/lib" > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf

  install -d -m755 ${pkgdir}/etc/profile.d/
  install -m644 ${startdir}/trinity.sh ${pkgdir}/etc/profile.d/

#   rm -r ${srcdir}/${_svnmod}
}
