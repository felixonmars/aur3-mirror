# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=glib-networking
pkgname=lib32-${_pkgbase}
pkgver=2.32.1
pkgrel=1
pkgdesc="Network-related giomodules for glib (32 bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('lib32-glib2' 'lib32-libproxy' 'lib32-gnutls') # 'libgcrypt' 'ca-certificates' 'gsettings-desktop-schemas'
makedepends=('intltool' 'gcc-multilib')
options=('!libtool')
install=glib-networking.install
_ver=${pkgver%.*}
source=(http://download.gnome.org/sources/${_pkgbase}/${_ver}/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('051ab469356327404dc53ae36dda5b85b99a12e5573a838d927abb49e58fa950')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${_pkgbase}-${pkgver}"
  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib32/glib-networking \
    --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${_pkgbase}-${pkgver}"  
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share"
}

