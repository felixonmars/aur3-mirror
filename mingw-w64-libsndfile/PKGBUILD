# Maintainer: naelstrof <naelstrof@gmail.com>
pkgname=mingw-w64-libsndfile
pkgver=1.0.25
pkgrel=1
pkgdesc="A C library for reading and writing files containing sampled sound (mingw-w64)"
arch=(any)
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
makedepends=('mingw-w64-gcc')
depends=('mingw-w64-flac' 'mingw-w64-libvorbis' 'mingw-w64-crt')
options=('!libtool' '!strip')
source=(http://www.mega-nerd.com/libsndfile/files/libsndfile-${pkgver}.tar.gz)
md5sums=('e2b7bb637e01022c7d20f95f9c3990a2')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS CFLAGS CXXFLAGS
  for _arch in ${_architectures}; do
      mkdir -p ${srcdir}/libsndfile-${pkgver}-build-${_arch}
      cd ${srcdir}/libsndfile-${pkgver}-build-${_arch}
      ${srcdir}/libsndfile-${pkgver}/configure --enable-shared --enable-static --prefix=/usr/${_arch} --host=${_arch} --disable-sqlite --disable-test-coverage
      make
  done
}

package() {
  cd "${srcdir}/libsndfile-${pkgver}"

  for _arch in ${_architectures}; do
      cd ${srcdir}/libsndfile-${pkgver}-build-${_arch}
      make DESTDIR="${pkgdir}" install
      rm -r $pkgdir/usr/${_arch}/share/doc/
      rm -r $pkgdir/usr/${_arch}/share/man/
  done
}
