# Maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: Eric Renfro <erenfro@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Sandman <the0sandman@hotmail.com>
# Contributor: raw <spam@rw23.de>
# Contributor: Dave Simons <miouhpi@gmail.com>

pkgname=ceph
pkgver=0.56.3
pkgrel=3
pkgdesc='Massively distributed fault-tolerant file system'
arch=('x86_64' 'i686')
url='http://ceph.newdream.net/'
license=('GPL')
depends=('libedit' 'libsigc++' 'gtkmm' 'btrfs-progs' 'crypto++'
         'gperftools>=1.8.3-2' 'python2' 'fuse' 'keyutils'
         'libatomic_ops' 'curl' 'libaio' 'fcgi' 'expat' 'boost')
makedepends=('boost' 'boost-libs')
install=$pkgname.install
options=('!libtool' 'emptydirs')
source=("http://ceph.newdream.net/download/$pkgname-$pkgver.tar.bz2" 
        "ceph-osd@.service" "ceph-mon@.service" "ceph-mds@.service"
        "ceph.install")

sha256sums=('5ea39c9fce4741e0a4adb04262fa02d491834996c5819709463748095c9ad1b5'
            '29483c0f6718e8830cf52c0d31e391fb52dc1b460bcb65cf9c72dfab83e5b5ce'
            'a50811ce62fd6cdcc17d8f1e4d9700c1889ab4bfc5e9a22155bd725a27715e3c'
            'b8239a04cc42e3e4ced2e141df6804e61e875131a5c95d6bcbfc3b44f388d44b'
            'c1669b2bedc07a313b8bd29735296791abc0fd94eb353c9683b6015f2cc2c93c')


prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # Fix for newer version of automake.
  find . -name 'Makefile.am' | while read makefile ; do
    if grep -q AUTOMAKE_OPTIONS "$makefile" ; then
      sed -i 's/AUTOMAKE_OPTIONS = /&subdir-objects /' "$makefile"
    else
      sed -i '1a\AUTOMAKE_OPTIONS = subdir-objects' "$makefile"
    fi
  done
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  LIBS="-lpthread -lboost_system" ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-radosgw

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/var/run/ceph" "$pkgdir/var/log/ceph" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/ceph"

  # Systemd.
  install -dm755 "$pkgdir/usr/lib/systemd/system"
  install -Dm644 "$srcdir/ceph-osd@.service" "$pkgdir/usr/lib/systemd/system/ceph-osd@.service"
  install -Dm644 "$srcdir/ceph-mon@.service" "$pkgdir/usr/lib/systemd/system/ceph-mon@.service"
  install -Dm644 "$srcdir/ceph-mds@.service" "$pkgdir/usr/lib/systemd/system/ceph-mds@.service"
  install -Dm755 "src/init-ceph" "$pkgdir/etc/rc.d/ceph"

  # Fix bin directory.
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"

  # Sample config.
  install -Dm644 "$pkgdir/usr/share/doc/ceph/sample.ceph.conf" \
    "$pkgdir/etc/ceph/ceph.conf.sample"

  # License.
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # Clean up.
  rmdir "$pkgdir/var/run/ceph"
}
