# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>
pkgname=ceph
pkgver=0.38
pkgrel=1
pkgdesc="Massively distributed fault-tolerant file system"
arch=('x86_64' 'i686')
url="http://ceph.newdream.net/"
license=('GPL')
depends=('libedit' 'libsigc++' 'gtkmm' 'btrfs-progs-unstable' 'crypto++'
         'google-perftools>=1.8.3-2' 'python2' 'fuse' 'keyutils'
         'libatomic_ops' 'curl')
makedepends=('boost')
optdepends=('fcgi: radosgw - Amazon S3 compatibility'
            'expat: radosgw - Amazon S3 compatibility')
install=ceph.install
source=("http://ceph.newdream.net/download/$pkgname-$pkgver.tar.gz")
md5sums=('acbcf044233134d593d952d3d377d085')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Configuring..."
  ./configure --prefix=/usr --sysconfdir=/etc
  make clean
  msg2 "Compiling..."
  make
  msg2 "Patching python scripts..."
  sed -i '0,/on/s//on2/' src/obsync/boto_tool src/obsync/obsync
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/var/run/ceph" "$pkgdir/var/log/ceph" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/ceph"
  install -Dm755 "src/init-ceph" "$pkgdir/etc/rc.d/ceph"
  # Move /usr/sbin to /sbin, which is expected by the mount command
  mv "$pkgdir/usr/sbin" "$pkgdir/"
  msg2 "Packaging sample configuration..."
  install -Dm644 "$pkgdir/usr/share/doc/ceph/sample.ceph.conf" \
    "$pkgdir/etc/ceph/ceph.conf.sample"
  msg2 "Packaging license..."
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  msg2 "Cleaning up..."
  rmdir "$pkgdir/var/run/ceph"
}

# vim:set ts=2 sw=2 et:
