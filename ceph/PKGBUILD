# $Id: 2acdab7b441f4b115946ec0713d050de98bc0ab5 $
# Maintainer: Ido Rosen <ido@kernel.org>
# Contributor: Evan Teitelman <teitelmanevan@gmail.com>
# Contributor: Eric Renfro <erenfro@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Sandman <the0sandman@hotmail.com>
# Contributor: raw <spam@rw23.de>
# Contributor: Dave Simons <miouhpi@gmail.com>
# Contributor: David Anderson <dave@natulte.net>
#
# NOTE: To request changes to this package, please submit a pull request
#       to the GitHub repository at https://github.com/ido/packages-archlinux
#       Otherwise, open a GitHub issue.  Thank you! -Ido
#

pkgname=ceph
pkgver=0.87
pkgrel=1
pkgdesc='Distributed, fault-tolerant file system delivering object, block, and file storage in one unified system.'
arch=('x86_64' 'i686')
url='http://ceph.com/'
license=('GPL')
depends=('libedit' 'libsigc++' 'gtkmm' 'btrfs-progs' 'crypto++'
         'gperftools>=1.8.3-2' 'python2' 'fuse' 'keyutils'
         'libatomic_ops' 'curl' 'libaio' 'fcgi' 'expat' 'boost'
         'leveldb' 'xfsprogs')
makedepends=('boost' 'boost-libs' 'yasm')
install=ceph.install
options=('!libtool' 'emptydirs')
source=("http://ceph.com/download/$pkgname-$pkgver.tar.bz2"
        "boost-fix.patch"
        "rwunlock-sigill-fix.patch"
        "ceph-osd@.service"
        "ceph-mon@.service"
        "ceph-mds@.service"
        "ceph.install")
sha256sums=('6ca61e32c6b04f92d68f735408a981f95da699a65845908e5617b25defe90b32'
            'd7c36b7661c8a0745158bfaf54081eb9566f00cc3101ddeec3ea3817cd3594d4'
            'd326c13d56bc7657365e7acff53f1910240c62b88bba79447af8e83c78e7f737'
            '29483c0f6718e8830cf52c0d31e391fb52dc1b460bcb65cf9c72dfab83e5b5ce'
            'a50811ce62fd6cdcc17d8f1e4d9700c1889ab4bfc5e9a22155bd725a27715e3c'
            'b8239a04cc42e3e4ced2e141df6804e61e875131a5c95d6bcbfc3b44f388d44b'
            'c1669b2bedc07a313b8bd29735296791abc0fd94eb353c9683b6015f2cc2c93c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # Patch was pulled from git with `git diff 7fe6e10 a53ead`. It's
  # fixed in >0.87, so be sure to remove it when upgrading in future.
  patch -p1 -i $srcdir/boost-fix.patch

  # Patch was pulled from git with `git diff 42c85e8 77deeaa`. The
  # Ceph bug http://tracker.ceph.com/issues/10085 was closed noting
  # that a backport is needed, so there may be a 0.87.1 at some point
  # that fixes this upstream.
  patch -p1 -i $srcdir/rwunlock-sigill-fix.patch

  # fix python scripts to use python2
  find . -type f -exec sed -i 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,g' {} \;
  # Some python scripts are autogenerated through Makefiles. Fix those
  # too, or /usr/bin/ceph will have the wrong shebang.
  find . -type f -exec sed -i 's,"#!/usr/bin/env python","#!/usr/bin/env python2",g' {} \;
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  LIBS="-lpthread -lboost_system" PYTHON=/usr/bin/python2 LDFLAGS="" ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-radosgw

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/var/run/ceph" "$pkgdir/var/log/ceph" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/ceph"

  # Systemd.
  install -dm755 "$pkgdir/usr/lib/systemd/system"
  install -Dm644 "$srcdir/ceph-osd@.service" "$pkgdir/usr/lib/systemd/system/ceph-osd@.service"
  install -Dm644 "$srcdir/ceph-mon@.service" "$pkgdir/usr/lib/systemd/system/ceph-mon@.service"
  install -Dm644 "$srcdir/ceph-mds@.service" "$pkgdir/usr/lib/systemd/system/ceph-mds@.service"
  install -Dm755 "src/init-ceph" "$pkgdir/etc/rc.d/ceph"

  # Fix bin directory.
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"

  # Sample config.
  install -Dm644 "$pkgdir/usr/share/doc/ceph/sample.ceph.conf" \
    "$pkgdir/etc/ceph/ceph.conf.sample"

  # License.
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # Clean up.
  rmdir "$pkgdir/var/run/ceph"
}
