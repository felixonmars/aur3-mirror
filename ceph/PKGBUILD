# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Sandman <the0sandman@hotmail.com>
# Contributor: raw <spam@rw23.de>
# Contributor: Dave Simons <miouhpi@gmail.com>
# Contributor: Eric Renfro <erenfro@gmail.com>

pkgname=ceph
pkgver=0.48.2argonaut
pkgrel=2
pkgdesc='Massively distributed fault-tolerant file system'
arch=('x86_64' 'i686')
url='http://ceph.newdream.net/'
license=('GPL')
depends=('libedit' 'libsigc++' 'gtkmm' 'btrfs-progs' 'crypto++'
         'gperftools>=1.8.3-2' 'python2' 'fuse' 'keyutils'
         'libatomic_ops' 'curl' 'libaio' 'fcgi' 'expat' 'boost')
makedepends=('boost')
install=$pkgname.install
options=('!libtool' 'emptydirs')
source=("http://ceph.newdream.net/download/$pkgname-$pkgver.tar.bz2"
        "$pkgname-mon.service"
        "$pkgname-osd.service")
sha256sums=('f04562fc1b4741232516a150faa7760e865455aaba52fe3ba8d6cc2644a0c901'
            'c62ad70e1eca0de533c889888a2b92a6afd6cb2a460e9d0a1cf5a37ca30bc6cc'
            '3c8bd95033a0727e881fc92ad00f48902d863236f62115397c71f831dcb551c4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Configuring..."
  LIBS="-lpthread -lboost_system" ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-radosgw
  make clean
  msg2 "Compiling..."
  make
  msg2 "Fixing python scripts..."
  sed -i '0,/on/s//on2/' src/obsync/boto_tool src/obsync/obsync
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/var/run/ceph" "$pkgdir/var/log/ceph" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/ceph"
  install -Dm755 "src/init-ceph" "$pkgdir/etc/rc.d/ceph"
  # Move /usr/sbin to /sbin, which is expected by the mount command
  mv "$pkgdir/usr/sbin" "$pkgdir/"
  msg2 "Packaging sample configuration..."
  install -Dm644 "$pkgdir/usr/share/doc/ceph/sample.ceph.conf" \
    "$pkgdir/etc/ceph/ceph.conf.sample"
  msg2 "Packaging license..."
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  msg2 "Packaging systemd services..."
  install -Dm644 "$srcdir/$pkgname-mon.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname-mon.service"
  install -Dm644 "$srcdir/$pkgname-osd.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname-osd.service"
  msg2 "Cleaning up..."
  rmdir "$pkgdir/var/run/ceph"
}

# vim:set ts=2 sw=2 et:
