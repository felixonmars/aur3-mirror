# Maintainer: PyroPeter <googlemail.com@abi1789>

pkgname=arduino
pkgver=0023
pkgrel=1
pkgdesc="Arduino SDK (includes patched avrdude and librxtx)"
arch=('i686' 'x86_64')
url="http://arduino.cc/en/Main/Software"
license=('GPL')
depends=('avr-libc' 'libusb-compat' 'java-runtime')
install="arduino.install"
source=("http://arduino.googlecode.com/files/arduino-$pkgver.tgz"
	"http://arduino.googlecode.com/files/arduino-$pkgver-64.tgz"
	'arduino'
	'arduino.png'
	'arduino.desktop'
	'gcc-avr-progmem.patch')
md5sums=('416dbe4185af34e4c4e3bf44c65458e7' '22a53ea34467667ec42399648ab61775' '551304dc658224c867513f6099075f0e' '9e36d33891d5e68d38ec55d1494499a5' 'eebc4d6495864bea99ad057af801afb9' '02d54a77bd451ad1b62513f5a60b39cc')

if   [ "$CARCH" == 'i686' ]; then
  unset source[1]
  unset md5sums[1]
elif [ "$CARCH" == 'x86_64' ]; then
  unset source[0]
  unset md5sums[0]
fi

package() {
  cd "$srcdir/arduino-$pkgver"
  mkdir -p "$pkgdir"/usr/{bin,share/{doc,applications,pixmaps}}
  
  # apply patch to work around bug in gcc-avr 4.6.1
  patch -p0 < "$srcdir/gcc-avr-progmem.patch"
  
  # arduino excutable should accept arguments
  sed -i 's/^java .* processing.app.Base$/\0 "$*"/' arduino
  
  # copy the whole SDK to /usr/share/arduino/
  cp -r . "$pkgdir/usr/share/arduino"
  
  # at least support the FHS a little bit:
  install -m755 "$srcdir/arduino" "$pkgdir/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "$pkgdir/usr/share/doc/arduino"
  
  # desktop icon
  install -m644 "$srcdir/arduino.desktop" "$pkgdir/usr/share/applications/"
  install -m644 "$srcdir/arduino.png" "$pkgdir/usr/share/pixmaps/"
}
