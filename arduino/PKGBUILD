# Maintainer: PyroPeter <googlemail.com@abi1789>

pkgname=arduino
pkgver=0022
pkgrel=5
pkgdesc="Arduino SDK (includes patched avrdude and librxtx)"
arch=('i686' 'x86_64')
url="http://arduino.cc/en/Main/Software"
license=('GPL')
depends=('avr-libc' 'libusb-compat' 'java-runtime' 'sh')
source=("http://arduino.googlecode.com/files/arduino-$pkgver.tgz"
	"http://arduino.googlecode.com/files/arduino-$pkgver-64.tgz"
	'arduino'
	'arduino.png'
	'arduino.desktop')
md5sums=('2429c118381a4a761d7f5717183ac387' 'b2918a2e1934f63a41487adfb66db546' 'e2c80b367bfbd96e56e656c198516288' '9e36d33891d5e68d38ec55d1494499a5' 'eebc4d6495864bea99ad057af801afb9')

if   [ "$CARCH" == 'i686' ]; then
  unset source[1]
  unset md5sums[1]
elif [ "$CARCH" == 'x86_64' ]; then
  unset source[0]
  unset md5sums[0]
fi

package() {
  cd "$srcdir/arduino-$pkgver"
  mkdir -p "$pkgdir"/usr/{bin,share/{doc,applications,pixmaps}}
  
  # copy the whole SDK to /usr/share/arduino/
  cp -r . "$pkgdir/usr/share/arduino"
  
  # at least support the FHS a little bit:
  install -m755 "$srcdir/arduino" "$pkgdir/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "$pkgdir/usr/share/doc/arduino"
  
  # desktop icon
  install -m644 "$srcdir/arduino.desktop" "$pkgdir/usr/share/applications/"
  install -m644 "$srcdir/arduino.png" "$pkgdir/usr/share/pixmaps/"
}
