# Maintainer: Ng Oon-Ee <ngoonee.talk@gmail.com>

pkgname=evolution-plugins-experimental
_pkgname=evolution
pkgver=3.4.0.1
pkgrel=1
pkgdesc="Additional experimental (unmaintained) plugins for Gnome Evolution - external-editor"
arch=('i686' 'x86_64')
license=('GPL')
depends=("evolution=$pkgver")
makedepends=('gnome-doc-utils' 'intltool') 
options=('!libtool' '!emptydirs')
url=http://www.gnome.org/projects/evolution/
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${pkgver%.*.*}/${_pkgname}-${pkgver}.tar.xz)
sha256sums=('364eacfc54912ba26738729d0f662a821140a712e3607cf569eb342f1dab4f7a')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --disable-scrollkeeper \
      --enable-nss=yes \
      --with-openldap=yes \
      --enable-smime=yes \
      --with-krb5=/usr \
      --disable-image-inline --disable-goa
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
  cd ${srcdir}/${_pkgname}-${pkgver}/plugins/external-editor
  make
  make DESTDIR="${pkgdir}" install
}
