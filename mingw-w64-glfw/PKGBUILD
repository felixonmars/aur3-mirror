# Maintainer: Daniel Kirchner <daniel AT ekpyron DOT org>
pkgname=mingw-w64-glfw
pkgver=3.0.3
pkgrel=4
pkgdesc="A free, open source, portable framework for OpenGL application development (mingw-w64)"
arch=('any')
url="http://www.glfw.org/"
license=('ZLIB')
makedepends=('mingw-w64-gcc' 'mingw-w64-cmake')
depends=('mingw-w64-crt')
options=('!strip' '!buildflags' 'staticlibs')
source=(glfw-${pkgver}.tar.gz::"http://sourceforge.net/projects/glfw/files/glfw/${pkgver}/glfw-${pkgver}.tar.gz/download")
md5sums=('60e947851c400d603f34071d220e8d70')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
  	rm -rf "${srcdir}/build-${_arch}"
	mkdir "${srcdir}/build-${_arch}"
	cd "${srcdir}/build-${_arch}"

	${_arch}-cmake "${srcdir}/glfw-${pkgver}" \
	      -DGLFW_BUILD_EXAMPLES=OFF \
	      -DGLFW_BUILD_TESTS=OFF \
	      -DBUILD_SHARED_LIBS=ON
	make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    mkdir -p "${pkgdir}/usr/${_arch}/bin/"
    mv "${pkgdir}/usr/${_arch}/lib/"*.dll "${pkgdir}/usr/${_arch}/bin/"
    ${_arch}-strip -x -g "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
    mv "${pkgdir}/usr/${_arch}/lib/glfw3dll.a" "${pkgdir}/usr/${_arch}/lib/libglfw3dll.a"
  done

  #copy license file
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cp "${srcdir}/glfw-${pkgver}/COPYING.txt" "${pkgdir}/usr/share/licenses/${pkgname}"
}
