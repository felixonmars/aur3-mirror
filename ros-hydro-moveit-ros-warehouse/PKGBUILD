
pkgdesc="ROS - Components of MoveIt connecting to MongoDB."
url='http://www.ros.org/'

pkgname='ros-hydro-moveit-ros-warehouse'
pkgver='0.5.15'
_pkgver_patch=0
arch=('i686' 'x86_64')
pkgrel=1
license=('BSD')

ros_makedepends=(ros-hydro-moveit-ros-planning
  ros-hydro-rosconsole
  ros-hydro-roscpp
  ros-hydro-warehouse-ros
  ros-hydro-catkin
  ros-hydro-tf)
makedepends=('cmake' 'git' 'ros-build-tools'
  ${ros_makedepends[@]})

ros_depends=(ros-hydro-moveit-ros-planning
  ros-hydro-roscpp
  ros-hydro-warehouse-ros
  ros-hydro-rosconsole
  ros-hydro-tf)
depends=(${ros_depends[@]})

_tag=release/hydro/moveit_ros_warehouse/${pkgver}-${_pkgver_patch}
_dir=moveit_ros_warehouse
source=("${_dir}"::"git+https://github.com/ros-gbp/moveit_ros-release.git"#tag=${_tag}
        linker.patch)
md5sums=('SKIP'
         '0d12d692740790bd9c4c5e776d568ce6')

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Apply patch
  msg "Patching source code"
  cd ${srcdir}/${_dir}
  git apply ${srcdir}/linker.patch

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python2/Python3 conflicts
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Build project
  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
