# Maintainer: Koscheev 'Ashen' Mikhail <fresh19991 at yandex dot ru>
# Contributors: Pascal 'hardfalcon' Ernster <aur@hardfalcon.net>, Marcel Dijkstra - <marcel~dykstra@gmail~com>
_pkgname=xf86-video-openchrome
pkgname=$_pkgname-git
pkgver=0.3.3.15
pkgrel=1
pkgdesc="OpenChrome X driver for VIA UniChrome graphics cards - latest git version"
arch=('i686' 'x86_64')
url="http://www.openchrome.org"
license=('GPL2')
depends=('intltool' 'xf86driproto' 'glproto' 'mesa' 'libxvmc')
makedepends=('X-ABI-VIDEODRV_VERSION=15' 'git' 'pkgconfig' 'intltool' 'libtool' 'automake>=1.7.0' 'xorg-server-devel')
provides=('xf86-video-openchrome')
conflicts=('xf86-video-via' 'xf86-video-unichrome' 'xf86-video-openchrome' 'X-ABI-VIDEODRV_VERSION<15' 'openchrome'  'xorg-server<1.15.0')
optdepends=('unichrome-dri')
source=("git://anongit.freedesktop.org/openchrome/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
	cd $_pkgname
	git describe --always | tr - .
}

build() {
	cd $_pkgname
	msg2 "Starting autogen.sh..."
	sh ./autogen.sh --prefix=/usr --enable-viaregtool
	msg2 "Starting make..."
	make
}

package() {
	cd $_pkgname
	msg2 "Starting make install..."
	make DESTDIR="$pkgdir" install
}