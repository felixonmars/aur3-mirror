# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gwibber
pkgver=3.2.0.1
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku"
arch=(i686 x86_64)
url="http://gwibber.com/"
license=(GPL)
depends=(libgee 'libnotify>=0.7' 'dee>=0.5.18-2' json-glib gtkspell3 dbus-python gnome-keyring python-gnomekeyring python-wnck python-egenix-mx-base python2-oauth python-imaging python-pycurl python-simplejson pywebkitgtk pyxdg hicolor-icon-theme xdg-utils)
makedepends=('vala>=0.9.2' 'vala<0.13' 'intltool>=0.35.0' 'gobject-introspection>=0.10')
optdepends=('python2-gtkspell: spell-checking support'
            'python-notify: notification support')
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/$pkgname/3.2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('4038c998791806c09189110b2fc06f48'
         'd0baea6c9f164247e837ab62f4ac6a8a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install updated language files
  echo 'af an ar ast az be bg bn br bs ca ca@valencia cy cs da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fr fy ga gl gu he hi hr hu hy ia id is it ja ka kk km kn ko ku lb lo lt lv mg mk ml mn mr ms nb nl nn oc pa pl pt pt_BR ro ru si sk sl sq sr sv ta te th tr tt ug uk vi yo zh_CN zh_HK zh_TW zza' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  # python2 fix
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Remove unused dbusmenu dependency
  sed -i 's/--pkg Dbusmenu-0.4//' client/Makefile.{am,in}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile --disable-unity

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
