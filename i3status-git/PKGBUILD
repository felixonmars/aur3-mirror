# Maintainer: Thorsten TÃ¶pper <atsutane-aur@freethoughts.de>

pkgname=i3status-git
pkgver=20121003
pkgrel=1
pkgdesc="Generates status bar to use with dzen2 or wmii"
arch=('i686' 'x86_64')
url="http://i3.zekjur.net/i3status/"
license=('BSD')
depends=('wireless_tools' 'confuse' 'alsa-lib' 'yajl')
optdepends=("dzen2-svn: As statusbar, dzen2 stable doesn't support -dock yet.")
makedepends=('git' 'pkgconfig' 'asciidoc')
options=('docs')
install=i3status.install
conflicts=('i3status')
provides=('i3status')

_gitroot="git://code.stapelberg.de/i3status"
_gitname="i3status"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Remove comment if you want to use i3status with wmii
  # sed -i "s/CFLAGS+=-DDZEN/#CFLAGS+=-DDZEN/g" Makefile

  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  make clean
}

# vim:set ts=2 sw=2 et:

