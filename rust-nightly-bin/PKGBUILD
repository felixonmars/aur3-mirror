# Maintainer: Michael Sproul <micsproul@gmail.com>
# Request Changes: https://github.com/michaelsproul/rust-nightly-archlinux
pkgname=rust-nightly-bin
pkgver=1.0.0_2015.01.07
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A safe, concurrent, practical language from Mozilla.'
url='http://www.rust-lang.org/'
provides=('rust' 'cargo')
conflicts=('rust' 'rust-git' 'rust-nightly' 'cargo-nightly-bin')
depends=('shared-mime-info')
license=('MIT' 'Apache')
source=("https://static.rust-lang.org/dist/rust-nightly-${CARCH}-unknown-linux-gnu.tar.gz"
        'https://raw.githubusercontent.com/rust-lang/rust/master/src/etc/gedit/share/mime/packages/rust.xml'
        'rust-nightly.conf')
sha256sums=('SKIP' 'SKIP' 'SKIP')
install=rust.install
options=(staticlibs !strip)

package() {
    local INSTALL_DIR=/usr/local
    local MAN_DIR=/usr/local/man

    # Rust, Cargo and Documentation.
    cd rust-nightly-${CARCH}-unknown-linux-gnu
    ./install.sh --prefix=${pkgdir}${INSTALL_DIR} --mandir=${pkgdir}${MAN_DIR}

    # Establish .rs MIME type association.
    mkdir -p "${pkgdir}/usr/share/mime/packages"
    install -m 644 "${srcdir}/rust.xml" "${pkgdir}/usr/share/mime/packages/rust.xml"

    # Dynamic linker configuration (no LD_LIBRARY_PATH required).
    mkdir -p "${pkgdir}/etc/ld.so.conf.d"
    install -m 644 "${srcdir}/rust-nightly.conf" "${pkgdir}/etc/ld.so.conf.d/rust-nightly.conf"

    # Remove manifest file referencing $pkgdir.
    rm -f "${pkgdir}/usr/local/lib/rustlib/manifest-rustc"
}
