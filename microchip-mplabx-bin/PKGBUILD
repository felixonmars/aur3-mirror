# Maintainer: BxS <bxsbxs at gmail dot com>

pkgname=microchip-mplabx-bin
pkgver=1.20
pkgrel=1
pkgdesc="IDE for Microchip PIC and dsPIC development"
arch=(i686 x86_64)
url=http://www.microchip.com/mplabx
license=(custom)
depends=(java-runtime=6 libusb desktop-file-utils)
makedepends=(fakechroot)
[ $CARCH = x86_64 ] && depends=(${depends[@]} lib32-glibc)
optdepends=('microchip-mplabxc8-bin: C compiler for PIC18 MCUs'
            'microchip-mplabxc16-bin: C compiler for PIC24 MCUs and dsPIC DSCs'
            'microchip-mplabxc32-bin: C Compiler for PIC32 MCUs'
            'microchip-mplabc18_bin: C compiler for PIC18 MCUs'
            'microchip-mplabc30_bin: C compiler for PIC24 MCUs and dsPIC DSCs'
            'microchip-mplabc32_bin: C Compiler for PIC32 MCUs'
            'hitech-picc_bin: C compiler for PIC10/12/16 MCUs'
            'hitech-picc-18_bin: C compiler for PIC18 MCUs'
            'hitech-dspicc-bin: C Compiler for PIC24 MCUs and dsPIC DSCs'
            'hitech-picc32-bin: C compiler for PIC32 MCUs'
            'sdcc: C compiler for PIC16/18 MCUs')
provides=(mplab)
conflicts=(mplab)
options=(!strip docs libtool emptydirs !zipman)
install=$pkgname.install
instdir=/opt/microchip/mplabx
installer=mplabx-ide-v$pkgver-linux-installer.run
source=(http://ww1.microchip.com/downloads/mplab/X/$installer
        LICENSE)
[ $CARCH = x86_64 ] && source+=(http://schlunix.org/archlinux/extra/os/i686/fakechroot-2.16-1-i686.pkg.tar.xz
                                http://schlunix.org/archlinux/core/os/i686/fakeroot-1.18.3-1-i686.pkg.tar.xz)
md5sums=(86b34fb83365e3a907e34ed6ee1f5dfd
         2d1c400907ba208d3f06ec1ee84eb2c0)
[ $CARCH = x86_64 ] && md5sums+=(0a300f12b73d5bc18548f5b50682236f
                                 9d1ea0eecada69eb742091afc74597f1)

build() {
  cd $srcdir

  rm -r $pkgdir/* &> /dev/null || true

  mkdir -p $pkgdir/{bin,etc,usr/{bin,lib,local/lib},tmp}

  ln -s /bin/bash $pkgdir/bin/
  ln -s /bin/ln $pkgdir/bin/

  echo -e "#! /bin/bash\necho \"java version \\\"1.6\"" > $pkgdir/bin/java
  chmod 0755 $pkgdir/bin/java

  echo "root:x:0:0:root:/root:/bin/bash" > $pkgdir/etc/passwd
  echo "root:x:0:root" > $pkgdir/etc/group

  cp $srcdir/$installer $pkgdir/
  chmod 0755 $pkgdir/$installer

  echo -e "\n\n\n\n\n\n\n\n\n\n\n\n\n\n\ny\n\ny\n" > $pkgdir/inst_input

  echo "#!/bin/bash
LD_LIBRARY_PATH=$srcdir/usr/lib/libfakeroot/fakechroot:$srcdir/usr/lib/libfakeroot:\$LD_LIBRARY_PATH
./$installer --mode text --installdir $instdir < inst_input" > $pkgdir/chroot_input.sh
  chmod 0755 $pkgdir/chroot_input.sh

  fakechroot fakeroot chroot $pkgdir ./chroot_input.sh

  rm -r $pkgdir/{chroot_input.sh,inst_input,$installer,bin,etc/{group,passwd,.mplab_ide},tmp,usr/{bin/{mplab_ide,mplab_ipe},lib,local}}
  rm $pkgdir$instdir/Uninstall*

  ln -s $instdir/asm30/bin/c30_device.info $pkgdir$instdir/asm30/bin/c30_ce.infoinfo
  ln -s $instdir/asm30/bin/c30_device.info $pkgdir$instdir/asm30/device.infoinfo

  mkdir -p $pkgdir/usr/bin
  ln -s $instdir/mplab_ide/bin/mplab_ide $pkgdir/usr/bin/
  echo "#!/bin/sh
LD_LIBRARY_PATH=$instdir/mplab_ide/mplablibs/modules/lib $instdir/mplab_ide/bin/mplab_ipe \"\$@\"" > $pkgdir/usr/bin/mplab_ipe
  chmod 0755 $pkgdir/usr/bin/mplab_ipe

  mv $pkgdir/usr/share/applications/{mplab.desktop,microchip-mplabx.desktop}
  mv $pkgdir/usr/share/icons/{microchip.png,microchip-mplabx.png}
  sed -i 's/microchip.png/microchip-mplabx/g' $pkgdir/usr/share/applications/microchip-mplabx.desktop

  mv $pkgdir/usr/share/applications/{mplab_ipe.desktop,microchip-mplab_ipe.desktop}
  mv $pkgdir/usr/share/icons/{mplab_ipe.png,microchip-mplab_ipe.png}
  sed -i 's/mplab_ipe.png/microchip-mplab_ipe.png/g' $pkgdir/usr/share/applications/microchip-mplab_ipe.desktop

  sed -i 's|/etc/.mplab_ide/mchplinusbdevice|/opt/microchip/mplabx/mplab_ide/mplablibs/modules/lib/mchplinusbdevice|g' $pkgdir/etc/udev/rules.d/z010_mchp_tools.rules

  sed 's|/usr/local/lib/libmchpusb-1.0.so\x00|libusb-1.0.so.0\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00|' -i $pkgdir/opt/microchip/mplabx/mplab_ide/mplablibs/modules/lib/libUSBAccessLink.so
  rm $pkgdir$instdir/mplab_ide/mplablibs/modules/lib/libusb-1.0*

  sed -i 's|/usr/hitech|/opt/hitech|g' $pkgdir/opt/microchip/mplabx/mplab_ide/mplab_ide/modules/com-microchip-mplab-nbide-toolchainhitech.jar

  install -Dm 644 $srcdir/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package() {
  true
}
