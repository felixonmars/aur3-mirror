# Maintainer : M0Rf30 <morf3089@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_pkg=pjproject
pkgname=python2-$_pkg
pkgver=2.1
_pkgver=2.1.0
pkgrel=1
pkgdesc="Open source SIP stack and media stack (also known as pjsip)"
arch=('i686' 'x86_64')
url="http://www.pjsip.org/"
license=('GPL')
depends=('alsa-lib' 'util-linux' 'python2' 'openssl')
makedepends=('e2fsprogs' 'python2-distribute')
source=("http://www.pjsip.org/release/$pkgver/pjproject-$pkgver.tar.bz2")

build() {
   cd "$srcdir/$_pkg-$_pkgver"
 
  [ "$CARCH" = 'x86_64' ] && \
  export CFLAGS="$CFLAGS -fPIC" && \
  export CXXFLAGS="$CXXFLAGS -fPIC"

  ./configure --prefix=/usr --enable-ssl
  make

# Build python module
  cd pjsip-apps/src/python/
  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkg-$_pkgver"

  make DESTDIR="$pkgdir" install

  cd pjsip-apps/src/python/
  python2 setup.py install --root="$pkgdir"
}


md5sums=('310eb63638dac93095f6a1fc8ee1f578')
