# Maintainer: Scott Smith <jcdenton513 gmail com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=veracity
pkgver=1.5.1
_verextra=10659
pkgrel=2
pkgdesc="A new open source distributed version control system."
url="http://www.veracity-scm.com"
arch=('x86_64' 'i686')
license=('APACHE')
depends=('curl' 'icu' 'nspr')
makedepends=('cmake')
install="veracity.install"
source=("http://download.sourcegear.com/Veracity/release/$pkgver.$_verextra/veracity-source-$pkgver.$_verextra.tar.gz"
        "cmake.patch")
sha256sums=('51530e63e3f8117711894e26cbf824d14e16575b21f171dc00f48ab1f361b939'
            '48bc26a67ec75959d5b1dbda60224559b361bcc349b0bb6ec55687f00358d2be')

options=(!makeflags)
 
build() {
  # Failure in final linking, undefined symbols.
  export LDFLAGS="$LDFLAGS -Wl,--no-as-needed"

  cd "${srcdir}/${pkgname}/thirdparty"
  ./build_linux.sh

  cd "${srcdir}/${pkgname}"
  # supress -Wunused. causes build to fail
  patch -p1 < ../cmake.patch
  # don't bother with testsuite (it ends up being about 800MB in size)
  sed -i /add_subdirectory\(testsuite\)/d CMakeLists.txt
  cmake -G "Unix Makefiles" \
    -DVVTHIRDPARTY="${srcdir}/veracity/vv-thirdparty/$CARCH" \
    -DCMAKE_INSTALL_PREFIX="/usr"
  make
}
 
package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname/NOTICE.txt"
  install -Dm644 TRADEMARKS.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARKS.txt"
}
 
# vim:set ts=2 sw=2 et:
