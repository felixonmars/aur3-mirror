# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=klatexformula
pkgver=3.2.5
pkgrel=2
pkgdesc="KLatexFormula is a program to easily get an image from a LaTeX formula"
url="http://klatexformula.sourceforge.net/"
arch=("i686" "x86_64")
license=('GPL')
makedepends=('cmake' 'automoc4')
depends=('qt>=4.4' 'texlive-core' 'ghostscript')
optdepends=('kdelibs: KTextEditor plugin support')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        error_append.diff)
md5sums=('8dc6bdad75fd9825c8bb959d669caef3'
         'bf31cc121642919c001591ffb0a60af5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i $srcdir/error_append.diff

  mkdir -p build || return 1
  cd build || return 1
  # CXX_FLAGS are set to avoid linking error
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="-lX11 -lXext" \
    -DKLF_INSTALL_LIB_DIR="lib" \
    || return 1
  make || return 1
  make DESTDIR="$pkgdir/" install
}
