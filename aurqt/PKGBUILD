# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
pkgname=aurqt
_pyname=aurqt
pkgver=0.1.1
pkgrel=2
pkgdesc='A graphical AUR manager.'
arch=('any')
url='https://github.com/Kwpolska/aurqt'
license=('BSD')
depends=('python' 'pyalpm>=0.5.1-1' 'python-requests' 'pkgbuilder>=3.0.1-1'
         'pyqt' 'python-beautifulsoup4')
options=(!emptydirs)
source=("http://pypi.python.org/packages/source/$(echo ${_pyname} | cut -c1)/${_pyname}/${_pyname}-${pkgver}.tar.gz"
        "aurqt.desktop")
md5sums=('b7b31c42abc49774632f8d7779f0c7dc'
         '24dd5d405c4c35f5ac14d95332795209')

package() {
  cd "${srcdir}/${_pyname}-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 ../aurqt.desktop "${pkgdir}/usr/share/applications/aurqt.desktop"
}

# vim:set ts=2 sw=2 et:
