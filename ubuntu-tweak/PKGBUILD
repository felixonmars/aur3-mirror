# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-tweak
pkgver=0.7.3
pkgrel=100
pkgdesc="An easy configuration tool for system and desktop settings"
arch=('any')
url="http://ubuntu-tweak.com/"
license=('GPL')
groups=('unity-extra')
# python-defer is a split package. We actually need python2-defer.
depends=('compizconfig-python-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python-defer' 'python2-lxml')
true && depends=('compizconfig-python-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python2-defer' 'python2-lxml')
# Also depends on polkit-gnome or polkit-qt
makedepends=('python2-distutils-extra' 'python2-distribute')
options=(!emptydirs)
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/${pkgver%.*}.x/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        '01_arch_linux.patch')
sha512sums=('aa02b251b7922e3daf40a33fe64ac564c598c2666e954776f15d36d505e001669768573f9345539a4f8d97f3c37c78969a7284dcfb1d16a2c06d926ee6984e88'
            '1772127cd04b9acf4d17e38a55eb5712ac8d1bf9067cab3a0072b831242cba8e0dfef0741a32a7d97e9d6b2a7e4a0a671e9195b2ec9a760a8d669e29ae7c6159')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # A patch created by me that
  #  - Removes any apt/dpkg/deb related functions
  #  - Removes any checks and references to other distros and replaces them
  #    with something appropriate for Arch Linux
  #  - Remove any features that require root (only Login manager right now)
  #    since they use a PolicyKit API exposed by aptdaemon.
  #  - Remove code that handles ut:// URL's
  patch -Np1 -i "${srcdir}/01_arch_linux.patch"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  # Python 2 fix
  sed -i 's|\(#!.*bin.*python$\)|\12|g' $(grep -rl '#!.*python[ ]*$' "${pkgdir}")
}

# vim:set ts=2 sw=2 et:
