# Maintainer: Jan Cholasta <grubber at grubber cz>

pkgname=slade-svn
pkgver=1360
pkgrel=1
pkgdesc="SLADE3 Doom editor."
arch=('i686' 'x86_64')
url="http://slade.mancubus.net/"
license=('GPL')
depends=('fluidsynth' 'freeimage' 'ftgl' 'libmodplug' 'sfml' 'wxgtk2.9')
makedepends=('cmake' 'imagemagick' 'subversion' 'zip')
conflicts=('slade')
provides=('slade')
source=(slade.desktop)
md5sums=('49eb1692463e2aa93ea2754a5df7a6e4')

_svntrunk="http://mancubus.net/svn/hosted/slade/trunk/"
_svnmod="slade"

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  cmake -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.9 \
        -DwxWidgets_wxrc_EXECUTABLE=/usr/bin/wxrc-2.9 \
        -DCMAKE_CXX_FLAGS=-DNDEBUG \
        .
  make

  cd dist/res
  zip -r ../slade.pk3 *
  cd ../..

  convert slade.ico'[0]' slade.png
}

package() {
  install -Dm755 "$_svnmod-build"/slade "$pkgdir"/usr/bin/slade
  install -Dm644 "$_svnmod-build"/dist/slade.pk3 "$pkgdir"/usr/share/slade3/slade.pk3

  install -Dm644 slade.desktop "$pkgdir"/usr/share/applications/slade.desktop
  install -Dm644 "$_svnmod-build"/slade.png "$pkgdir"/usr/share/pixmaps/slade.png
}
