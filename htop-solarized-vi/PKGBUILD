# $Id$
# Maintainer: Connor Prussin <cprussin@bci-incorporated.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Evan LeCompte <evanlec@gmail.com>
# Contributor: Boohbah <boohbah at gmail.com>

_pkgname=htop
pkgname=$_pkgname-solarized-vi
pkgver=1.0.2
pkgrel=2
pkgdesc="Interactive process viewer with solarized patch and vi keybindings patch"
arch=('i686' 'x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
provides=('htop')
conflicts=('htop htop-vi htop-solarized')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/$_pkgname/$_pkgname-$pkgver.tar.gz"
				htop-solarized-patch.diff
				htop-vi.patch
        tree-crash.patch)
md5sums=('0d01cca8df3349c74569cefebbd9919e'
				 'd7fb999b6dd61e3fd1481331a43200b1'
				 'fd13a0137144fd9c2c7ac43c2d8b48b5'
         '48eba3c0303bfd19d761b859bc69d713')

build() {
  cd "$_pkgname-$pkgver"

  sed -i 's|ncursesw/curses.h|curses.h|' RichString.[ch] configure
  sed -i 's|python|python2|' scripts/MakeHeader.py

  # Boost field buffer size - crashes when trying to draw very deep UTF-8 trees
  # Test by nesting 30 shells
  patch -N -i ../tree-crash.patch

  # Solarized patch: https://gist.github.com/alexeiz/4657334
  patch -N -i ../htop-solarized-patch.diff

  # Vi patch
	patch -N -i ../htop-vi.patch

  ./configure \
      --prefix=/usr \
      --enable-unicode \
      --enable-openvz \
      --enable-vserver \
      --enable-cgroup

  make
}

package() {
  make -C "$_pkgname-$pkgver" DESTDIR="$pkgdir" install
}

