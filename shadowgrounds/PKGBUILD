# Maintainer: jimmy-6 <jakub.jarozek {at} gmail.com>

#PKGEXT=.tar
pkgname=shadowgrounds
_gamepkg="shadowgroundsUpdate1.run"
pkgver=beta12_update1
pkgrel=2
pkgdesc="A 3D sci-fi alien shooter (requires copy of the full game)"
url="http://shadowgroundsgame.com/"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
makedepends=('unzip')
source=('shadowgrounds.sh' 'shadowgrounds-launcher.sh' 'shadowgrounds.desktop')
md5sums=('10e6991bb4aee46a38a10276290c0416'
         '01406a97ef740c33c83091fd0d989304'
         'd05af923259375bb68ad0d53efe793d1')

depends=('libglade' 'mesa')
_depends64=('lib32-libglade' 'lib32-mesa')

if [[ $CARCH == x86_64 ]]; then
  depends=(${_depends64[@]})
fi

build() {
  cd ${srcdir}
  
  msg "You need a full copy of this game in order to install it"
  msg "Searching for ${_gamepkg} in dir: $(readlink -f `pwd`/..)"
  if [[ -f "../${_gamepkg}" ]]; then
    msg "Found game package, installing..."
    ln -fs "../${_gamepkg}" .
    unzip -o ${_gamepkg} || true
  else
    error "Game package not found, please type absolute path to ${_gamepkg}:"
    read pkgpath
    if [[ -f "${pkgpath}/${_gamepkg}" ]]; then
      msg "Found game package, installing..."
      ln -fs "${pkgpath}/${_gamepkg}" .
      unzip -o ${_gamepkg} || true
    else
      error "Unable to find game package."
      return 1
    fi
  fi
}

package(){

  # directories
  mkdir -p ${pkgdir}/opt/Shadowgrounds
  cd ${srcdir}
  cp -r Config data lib32 Profiles ${pkgdir}/opt/Shadowgrounds/
  #fix permissions
  find ${pkgdir}/opt/Shadowgrounds -type d -exec chmod 755 {} \;
  find ${pkgdir}/opt/Shadowgrounds/{Config,data,Profiles} -type f -exec chmod 644 {} \;
  chmod -R 755 ${pkgdir}/opt/Shadowgrounds/lib32

  #other data files
  install -Dm644 data1.fbz shadowgrounds-launcher.glade shadowgrounds-logo.png \
                 Shadowgrounds.xpm ${pkgdir}/opt/Shadowgrounds/
  
  # startup scripts
  install -Dm755 ${srcdir}/shadowgrounds.sh ${pkgdir}/usr/bin/shadowgrounds
  install -Dm755 ${srcdir}/shadowgrounds-launcher.sh ${pkgdir}/usr/bin/shadowgrounds-launcher

  # execs
  install -Dm755 ${srcdir}/shadowgrounds-bin ${pkgdir}/opt/Shadowgrounds/shadowgrounds-bin
  install -Dm755 ${srcdir}/shadowgrounds-launcher ${pkgdir}/opt/Shadowgrounds/shadowgrounds-launcher

  # desktop entry
  install -Dm644 ${srcdir}/shadowgrounds.desktop ${pkgdir}/usr/share/applications/shadowgrounds.desktop
  
}
