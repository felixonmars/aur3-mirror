# Maintainer: Arvid Norlander <anmaster A_T tele2 d_o_t (ccTLD for Sweden, which is se)>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=tup-git
pkgver=0.7.r22.g9ec4c98
pkgrel=1
pkgdesc="A fast, file-based build system"
arch=('i686' 'x86_64')
url="http://gittup.org/tup/index.html"
license=('GPL2')
depends=('fuse')
makedepends=('git')
install=tup.install
source=('tup::git://github.com/gittup/tup.git')
sha512sums=('SKIP' )

pkgver() {
  cd tup
  git describe | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd tup
  sh bootstrap.sh
}

check() {
  cd tup/test
  ./test.sh
}

package() {
  cd tup

  install -d $pkgdir/usr/bin $pkgdir/usr/share/man/man1 $pkgdir/usr/share/vim/vimfiles/{syntax,ftdetect}
  install -m755 -t $pkgdir/usr/bin tup
  install -m644 -t $pkgdir/usr/share/man/man1 tup.1
  install -m644 -t $pkgdir/usr/share/vim/vimfiles/syntax contrib/syntax/tup.vim
  echo 'au BufNewFile,BufRead Tupfile,*.tup setf tup' > $pkgdir/usr/share/vim/vimfiles/ftdetect/tup.vim
}
