# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=systemd-git
pkgver=20120411
pkgrel=1
pkgdesc="system and service manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('acl' 'dbus' 'dbus-core' 'kbd' 'kmod' 'libcap' 'pam' 'udev' 'util-linux' 'xz')
makedepends=('cryptsetup' 'docbook-xsl' 'git' 'gobject-introspection' 'gperf'
             'gtk-doc' 'intltool' 'libxslt')
optdepends=('cryptsetup: required for encrypted block devices'
            'dbus-python: systemd-analyze'
            'initscripts: legacy hostname, locale, and vconsole support'
            'selinux-usr-libselinux: integration with security enhanced Linux'
            'systemd-arch-units-git: collection of native unit files for Arch daemon/init scripts'
            'tcp_wrappers: filter remote access to services')
provides=('systemd' 'libsystemd' 'udev=182')
conflicts=('systemd' 'libsystemd' 'udev')
groups=('systemd')
options=('!libtool')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/logind.conf
        etc/systemd/journald.conf
        etc/udev/udev.conf)
install='systemd.install'
source=('os-release'
        'initcpio-hooks-udev'
        'initcpio-install-udev'
        0001-udev-support-reading-from-split-usr-setup.patch
        0002-reinstate-TIMEOUT-handling.patch)
md5sums=('752636def0db3c03f121f8b4f44a63cd'
         'a4dd853050bf2e0ae6b2e3d2c75499c2'
         'd2b16edc6d806b5dafdbbad43ae5a3de'
         'ef924630bf0577e4d9dddc2bf33da05e'
         'c36a683ebb54a88aa9e53185e098a33f')

_gitroot="git://anongit.freedesktop.org/systemd/systemd.git"
_gitname="systemd"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # allow udev to read from /lib and /usr/lib
  patch -Np1 <"$srcdir/0001-udev-support-reading-from-split-usr-setup.patch"

  # revert TIMEOUT handling
  patch -Np1 <"$srcdir/0002-reinstate-TIMEOUT-handling.patch"

  ./autogen.sh
  ./configure --libexecdir=/usr/lib \
              --localstatedir=/var \
              --sysconfdir=/etc \
              --enable-split-usr \
              --disable-audit \
              --with-pamlibdir=/lib/security \
              --with-distro=arch \
              --with-sysvinit-path= \
              --with-sysvrcd-path=

  make
}

package() {
  cd "$_gitname-build"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  printf "d /run/console 0755 root root\n" > "$pkgdir/usr/lib/tmpfiles.d/console.conf"

  # install external sd-{daemon,readahead} libraries
  install -m644 -t "$pkgdir/usr/share/doc/systemd" \
    src/sd-daemon.[ch] \
    src/readahead/sd-readahead.[ch]

  install -dm755 "$pkgdir/bin" "$pkgdir/lib/systemd"
  ln -s ../usr/lib/systemd/systemd "$pkgdir/bin/systemd"
  ln -s ../../usr/lib/systemd/systemd "$pkgdir/lib/systemd/systemd"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "$pkgdir/usr/bin/systemd-analyze"

  # fix .so links in manpage stubs
  find "$pkgdir/usr/share/man" -type f -name '*.[[:digit:]]' \
      -exec sed -i '1s|^\.so \(.*\)\.\([[:digit:]]\+\)|.so man\2/\1.\2|' {} +

  # rename man pages to avoid conflicts with sysvinit and initscripts
  cd "$pkgdir/usr/share/man"

  manpages=(man8/{telinit,halt,reboot,poweroff,runlevel,shutdown}.8
            man5/{hostname,{vconsole,locale}.conf}.5)

  for manpage in "${manpages[@]}"; do
    IFS='/' read section page <<< "$manpage"
    mv "$manpage" "$section/systemd.$page"
  done

  # move bash-completion and symlink for loginctl
  install -Dm644 "$pkgdir/etc/bash_completion.d/systemd-bash-completion.sh" \
    "$pkgdir/usr/share/bash-completion/completions/systemctl"
  ln -s systemctl "$pkgdir/usr/share/bash-completion/completions/loginctl"
  rm -rf "$pkgdir/etc/bash_completion.d"

  # the path to udevadm is hardcoded in some places
  install -d "$pkgdir/sbin"
  ln -s ../usr/bin/udevadm "$pkgdir/sbin/udevadm"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
             s#GROUP="tape"#GROUP="storage"#g;
             s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules

  # add mkinitcpio hooks for udev
  install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
  install -Dm644 "$srcdir/initcpio-hooks-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"
}
