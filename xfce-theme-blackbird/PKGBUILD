# Maintainer: flan_suse

# This suite contains the following:
#        * xfwm4 theme
#        * metacity theme
#        * gtk2 theme
#        * gtk3 theme
#        * xfce4-notify theme

pkgname=xfce-theme-blackbird
_pkgname=Blackbird
pkgver=0.2
pkgrel=1
_gitdir=Blackbird
_gitbranch=master
_giturl="git://github.com/shimmerproject/$_gitdir.git"
pkgdesc="A dark, ink black Xfce theme, based off of Greybird"
arch=('any')
url="http://shimmerproject.org/"
license=('GPL3')
groups=('xfce-themes-shimmer-collection')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('elementary-xfce-icons: matching icon set; use the dark icon theme'
            'gtk-engine-unico: required for gtk3 support')

build() {

    # Change into source directory
    cd "${srcdir}"

    # Clone or pull from GIT server, and notify user of the status
    msg "Connecting to GIT server...."
    if [[ -d $_gitdir ]] ; then
      cd "$_gitdir" && git pull origin "$_gitbranch"
      msg "The local files are updated."
    else
      git clone "$_giturl" "$_gitdir"
      cd "$_gitdir"
    fi

    # Checkout current git-tag stable release
    # Comment out the next line if you wish to use the master branch instead; not recommended
    git checkout "v$pkgver"

    # Notify the user that the build() process is complete
    echo "$_pkgname version $pkgver is ready to be packaged!"

}

package() {

    # Change into source directory
    cd "${srcdir}"

    # Create installation directories
    install -d -m755 "${pkgdir}/usr/share/themes/"

    # Install the rest of the theme
    cp -rf "$_pkgname" "${pkgdir}/usr/share/themes/"

    # Remove excess files
    rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/.gitignore"
    rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/.git"

}
