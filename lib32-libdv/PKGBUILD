# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=libdv
pkgname=lib32-$_pkgname
pkgver=1.0.0
pkgrel=1
pkgdesc="The Quasar DV codec (libdv) is a software codec for DV video"
arch=(i686 x86_64)
url=http://libdv.sourceforge.net/
license=('LGPL')
depends=($_pkgname popt)
makedepends=(lib32-gtk lib32-libxv)
optdepends=('lib32-gtk: for using playdv'
    'lib32-libxv: for using playdv')
options=(!libtool)
source=(http://downloads.sourceforge.net/$_pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('a305734033a9c25541a59e8dd1c254409953269ea7c710c39e540bd8853389ba')
sha512sums=('19887a6f452fa7da8924901ad841f5c4a434a3bde56c932ddb43717592aa744281e3a8ad220231e11d7e6bc3b0e704952a8324e5f85d4e088675edceeeca7dc8')

build() {
    cd $_pkgname-$pkgver/
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export CPPFLAGS+=" -I/usr/lib32/glib-2.0/include/ -I/usr/include/glib-2.0/"
    export CXXFLAGS+=" -I/usr/lib32/glib-2.0/include/ -I/usr/include/glib-2.0/"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib32/ \
        --build=i686-unknown-linux-gnu
    make
}

package() {
    make -C $_pkgname-$pkgver DESTDIR="$pkgdir" install
    rm -rf "$pkgdir"/usr/{bin,include,share}/
}
