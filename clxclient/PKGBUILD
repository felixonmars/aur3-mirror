# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Maintainer: Philipp Ãœberbacher <murks at lavabit dot com>
pkgname=clxclient
pkgver=3.6.1
pkgrel=4
pkgdesc="X library for aeolus, aliki, jaaa and japa."
arch=('i686' 'x86_64')
url="http://kokkinizita.linuxaudio.org/linuxaudio/downloads/index.html"
license=('LGPL')
depends=('gcc-libs' 'libxft')
makedepends=('clthreads>=2.4.0')
source=(http://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('bd47f80a855d3203fcf10365e79d85e4')

build() {
  cd "${pkgname}-${pkgver}"
  #Why kill the optimisation? -e 's|-O2||g'
  sed -e 's|usr/bin|bin|g' -i Makefile
  make PREFIX="${pkgdir}"/usr LIBDIR=lib
}

package(){
  cd "${pkgname}-${pkgver}"
  mkdir -p "${pkgdir}"/usr/{include,lib}
  make PREFIX="${pkgdir}"/usr LIBDIR=lib install
}

