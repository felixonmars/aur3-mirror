# Maintainer: Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Loya Liu <loya.liu@gmail.com>
# Contributor: Kristaps Esterlins <esterlinsh@gmail.com>

pkgname=gpick
pkgver=0.2.3
pkgrel=1
pkgdesc="Advanced color picker written in C++ using GTK+ toolkit"
arch=('i686' 'x86_64')
url="http://gpick.googlecode.com/"
license=('BSD')
depends=('dbus-glib>=0.76' 'gtk2>=2.12.0' 'hicolor-icon-theme' 'lua>=5.1')
makedepends=('scons' 'boost' 'flex' 'lemon')
install=$pkgname.install
source=(http://$pkgname.googlecode.com/files/${pkgname}_$pkgver.tar.gz
        $pkgname.desktop LICENSE)
md5sums=('914bdfbce2cd2f6d887d468bd2b4805c'
         '9bcf9591e2ff77dc34db093410aadb00'
         'a10a78fc5ebc11c552af2e459bb6b76d')

build() {
  cd "$srcdir/${pkgname}_$pkgver"
  scons
}

package() {
  cd "$srcdir/${pkgname}_$pkgver"
  scons DESTDIR="$pkgdir/usr" install
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
