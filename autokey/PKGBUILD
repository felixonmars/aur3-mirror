# Maintainer: speps <speps at aur dot archlinux dot org>

pkgbase=autokey
pkgname=autokey
true && pkgname=('autokey' 'autokey-gtk' 'autokey-qt')
pkgver=0.90.4
pkgrel=1
pkgdesc="A text expansion/replacement and hotkey utility for Linux and X11."
arch=('any')
url="http://code.google.com/p/autokey/"
license=('GPL3')
source=("http://autokey.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        $pkgname{,-frontend}.install)
md5sums=('c2a74ddc2cb06c9798b7fa57fd65019a'
         '4c176c0a760f3f2cfe4b43f80032896c'
         '399df93b1d42453015494d86ad1115c9')

_python2_fix() {
  sed -i "s_env python_&2_" \
    `grep -rl "env python" "$pkgdir"`
}

package_autokey() {
  true && depends=('autokey-frontend' 'wmctrl' 'hicolor-icon-theme'
                   'dbus-python' 'python-xlib' 'python2-pyinotify')
  true && optdepends=('pyatspi: at-spi support')
  true && install="$pkgname.install"

  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir/"

  # remove GUI stuff
  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/autokey/"{gtk,qt}*
  rm -rf "$pkgdir/usr/share/"{kde4,applications}
  rm -f "$pkgdir/usr/"{bin,share/man/man1}/autokey-{gtk,qt}*

  _python2_fix
}

package_autokey-gtk() {
  true && depends=("autokey>=$pkgver" 'python2-gobject' 'gtksourceview3' 'libnotify')
  true && provides=('autokey-frontend')
  true && install="autokey-frontend.install"

  cd "$srcdir/$pkgbase-$pkgver"

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # man
  install -Dm644 doc/man/$pkgname.1 \
     "$pkgdir/usr/share/man/man1/$pkgname.1"

  # libs
  install -d "$pkgdir/usr/lib/python2.7/site-packages/autokey"
  cp -a src/lib/gtk{ui,app.py} \
    "$pkgdir/usr/lib/python2.7/site-packages/autokey"

  # desktop file
  install -Dm644 config/$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  _python2_fix
}

package_autokey-qt() {
  true && depends=("autokey>=$pkgver" 'kdebindings-python2')
  true && provides=('autokey-frontend')
  true && install="autokey-frontend.install"

  cd "$srcdir/$pkgbase-$pkgver"

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # man
  install -Dm644 doc/man/$pkgname.1 \
     "$pkgdir/usr/share/man/man1/$pkgname.1"

  # libs
  install -d "$pkgdir/usr/lib/python2.7/site-packages/autokey"
  cp -a src/lib/qt{ui,app.py} \
    "$pkgdir/usr/lib/python2.7/site-packages/autokey"

  # desktop file
  install -Dm644 config/$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # KDE4 apps
  install -Dm644 config/autokeyui.rc \
    "$pkgdir/usr/share/kde4/apps/autokey/autokeyui.rc"

  _python2_fix
}