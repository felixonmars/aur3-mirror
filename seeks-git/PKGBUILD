# Maintainer: Enjolras <0enjolras0@laposte.net>
# Maintainer: Vincent B. <vbmithr@gmail.com>
pkgname=seeks-git
pkgver=20120719
pkgrel=1
pkgdesc="A social decentralized internet overlay for collaborative searches"
arch=('i686' 'x86_64')
url="http://www.seeks-project.info/site/"
license=('AGPL3' 'LGPL' 'BSD')
groups=()
depends=('curl' 'libevent>=2.0' 'libxml2' 'opencv' 'protobuf' 'icu' 'tokyotyrant' 'tokyocabinet')
makedepends=('pkg-config' 'docbook-xml' 'docbook2x')
checkdepends=()
conflicts=('seeks')
backup=()
install=seeks.install
source=('seeks.conf.d'
	'seeks.rc.d'
	'img_websearch.patch'
	'conf.patch'
	'seeks.logrotate')
sha1sums=('ea881740b7a216db6d9a18b674d33dc3c04c79e1'
          '67bface679402b9f75fd0c30062237bdc88083a5'
          '071a3edbf4c56599e550e98fd467d3959c397af0'
          'bd8992bb30db818610760a5b7314210284b989a9'
          '28e156c5d1a4b45c54309b084d04556b84817053')

_gitroot="git://seeks.git.sourceforge.net/gitroot/seeks/seeks"
_gitname="seeks"

build() {
    msg "Connecting to the GIT server...."

    if [[ -d ${srcdir}/${_gitname} ]] ; then
	cd $srcdir/${_gitname}
	git pull origin
	msg "The local files are updated..."
    else
	git clone ${_gitroot}
    fi


    msg "GIT checkout done."
    cd "$srcdir/${pkgname/-git/}"
    ./autogen.sh

  # replace <cxflann.h> by <cv.h>
  patch -p1 < "$srcdir/img_websearch.patch"

  # linking issue with --as-needed linker flag
  LDFLAGS="-Wl,--no-as-needed" ./configure --prefix=/usr --sysconfdir=/etc --enable-httpserv-plugin --enable-extended-host-patterns
  sed -i s/,--as-needed,/,/ config.status
  make
}

check() {
  cd "$srcdir/${pkgname/-git/}"
  make -k check-local
}

package() {
  cd "$srcdir/${pkgname/-git/}"
  # Patch config file to make seeks log in /var/log instead of ./ (needed for rc.d)
  patch -p1 < "$srcdir/conf.patch"
  make DESTDIR="$pkgdir/" install

  install -D -m644 Licenses  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 ../seeks.conf.d "${pkgdir}/etc/conf.d/seeks"
  install -D -m755 ../seeks.rc.d "${pkgdir}/etc/rc.d/seeks"
  install -D -m600 ../seeks.logrotate  "${pkgdir}/etc/logrotate.d/seeks"
  install -d -m755  "${pkgdir}/var/log/seeks"
  install -d -m755  "${pkgdir}/var/lib/seeks"
}

# vim:set ts=2 sw=2 et:
