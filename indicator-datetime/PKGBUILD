# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-datetime
pkgver=0.0.6
pkgrel=2
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
groups=('ayatana')
depends=('libindicator>=0.3.0' 'libdbusmenu>=0.1.1' 'ido>=0.1.10' 'dconf>=0.5.1')
makedepends=('intltool')
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/$pkgname/0.1/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('fef590ae0571d9a7ed386ff48f9beed7'
         '85418a96118d7990d271fe03722907da')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
