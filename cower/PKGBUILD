# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower
pkgver=3.0.0
pkgrel=2
pkgdesc="A simple AUR agent with a pretentious name"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('GPL')
depends=('curl' 'yajl' 'pacman')
makedepends=('perl')
source=("https://github.com/downloads/falconindy/$pkgname/$pkgname-$pkgver.tar.gz"
        "config")
md5sums=('1b80196a0b764471a6b2574faf8bf158'
         '1b4ae9c52f92842a5bc55a1d927b5666')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  readelf -s /usr/lib/libalpm.so | grep -q alpm_find_satisfier &&
    PMCHECK="-D_HAVE_ALPM_FIND_SATISFIER"
  readelf -s /usr/lib/libalpm.so | grep -q [^_]alpm_db_register_local &&
    PMCHECK+=" -D_HAVE_ALPM_DB_REGISTER_LOCAL"

  # one of these days i'll stop making brown bag releases
  cp "$srcdir/config" .

  make PMCHECK="$PMCHECK"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR=${pkgdir} install
}

# vim: ft=sh syn=sh
