# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=check_md_raid
pkgver=0.7.2
pkgrel=6
pkgdesc="Linux Software RAID Monitoring Plugin for Nagios"
arch=('any')
url="http://exchange.nagios.org/directory/Plugins/Operating-Systems/Linux/Linux-Software-Raid-Plugin-for-32-2Dbit-and-64-2Dbit-systems/details"
license=('GPL')
depends=('nagios' 'python2' 'mdadm')
optdepends=('sudo: check with non-root user')
source=("${pkgname}-${pkgver}.py::http://exchange.nagios.org/components/com_mtree/attachment.php?link_id=782&cf_id=24"
	'0001-use-python2.patch'
	'0002-strip-whitespace-from-state.patch'
	'0003-use-sudo-when-running-as-non-root-user.patch'
	'sudo')

build() {
	cp ${pkgname}-${pkgver}.py ${pkgname}
	patch -Np1 < ${srcdir}/0001-use-python2.patch
	patch -Np1 < ${srcdir}/0002-strip-whitespace-from-state.patch
	patch -Np1 < ${srcdir}/0003-use-sudo-when-running-as-non-root-user.patch
}

package() {
	install -D -m 0755 ${srcdir}/${pkgname} ${pkgdir}/usr/lib/monitoring-plugins/${pkgname}

	install -d -m 0750 ${pkgdir}/etc/sudoers.d
	install -D -m 0440 ${srcdir}/sudo ${pkgdir}/etc/sudoers.d/check_md_raid
}

sha256sums=('b85f0a77215870839cb18a74d9e38b594eaeda5c44ddc88aff8d2c2246f506f7'
            'cbc06304b096f195dda1ead8b1348df08c2e27d076ce523fa059a17e1c405c4a'
            'bb0e078bf5c5c3435ac7398148250c96ae844b6936d86480d9da93e5fe64eb49'
            'fff2cd2c0fcfda489b2a587bb2e58b16ebd449941bb3b11f1ad851cab4a85299'
            '4ffbd86b293665cd39a468257e3bf3fe35ce3cd429d7df576ef330762ad727d7')
