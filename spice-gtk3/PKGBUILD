# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Derived from the spice-gtk PKGBUILD by
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk3
_pkgname=spice-gtk
pkgver=0.12
pkgrel=2
pkgdesc="A Gtk client and libraries for SPICE remote desktop servers (gtk3 version)."
arch=('x86_64' 'i686')
url="http://spice-space.org"
license=('GPL')
makedepends=('intltool' 'vala')
depends=('libpulse' 'gtk3' 'spice' 'libusb' 'usbredir')
conflicts=('spice-gtk')
options=('!libtool')
source=("http://www.spice-space.org/download/gtk/$_pkgname-$pkgver.tar.bz2")
sha256sums=('5eea1622f351988eb6f2eb1a945fee6caba3d4038997a371eaf478ee9d3a00b3')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  sed -i 's,/usr/bin/env python,/usr/bin/python2,' spice-common/spice_codegen.py

  ./configure --prefix=/usr \
              --with-gtk=3.0 \
              --enable-vala \
              --enable-smartcard=no
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
