pkgname=libunity-bzr
pkgver=59
pkgrel=1
pkgdesc="A library for instrumenting- and integrating with all aspects of the Unity shell."
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('GPL')
depends=('libgee' 'dee-bzr' 'dbus-glib' 'libdbusmenu')
makedepends=('bzr' 'vala' 'gtk-doc' 'gobject-introspection')
provides=('libunity')
conflicts=('libunity')
groups=('unity2d-bzr')

_bzrtrunk=lp:libunity
_bzrmod=libunity

build() {
  cd ${srcdir}
  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build 
  fi
  bzr clone ${srcdir}/${_bzrmod} ${srcdir}/build
  cd ${srcdir}/build
  ./autogen.sh  --prefix=/usr --disable-introspection
  make 
}

package() { 
  cd ${srcdir}/build
  make DESTDIR="${pkgdir}" install
}
