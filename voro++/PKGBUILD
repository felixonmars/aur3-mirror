# Maintainer: archlid <a_becker {at} live {dot} de>
pkgname=voro++
pkgver=0.4.5
pkgrel=1
pkgdesc="A free software library for the computation of three dimensional Voronoi cells."
arch=('i686' 'x86_64')
url="http://math.lbl.gov/voro++/"
license=('BSD')
depends=('perl' 'gcc-libs')
source=(http://math.lbl.gov/voro++/download/dir/${pkgname}-$pkgver.tar.gz)
md5sums=('e609aa2394bd0526b0772c520d1cce18')
 
build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/(PREFIX)\/man/(PREFIX)\/share\/man/g' Makefile
  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=$pkgdir/usr install
  install -d $pkgdir/usr/share/licenses/$pkgname $pkgdir/usr/share/$pkgname/{examples,scripts} $pkgdir/usr/share/doc/$pkgname/{,html}
  install -m644 $srcdir/$pkgname-$pkgver/LICENSE $pkgdir/usr/share/licenses/$pkgname
  install -m644 $srcdir/$pkgname-$pkgver/NEWS $pkgdir/usr/share/doc/$pkgname
  install -m644 $srcdir/$pkgname-$pkgver/scripts/* $pkgdir/usr/share/$pkgname/scripts
  install -m644 $srcdir/$pkgname-$pkgver/html/* $pkgdir/usr/share/doc/$pkgname/html
  cp -r $srcdir/$pkgname-$pkgver/examples/* $pkgdir/usr/share/$pkgname/examples
  chmod -R 755 $pkgdir/usr/share/$pkgname/examples
}
