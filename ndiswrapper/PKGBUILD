# Maintainer: Christian Weber <christianweber802 at gmx dot net>

pkgname=ndiswrapper
pkgver=1.58
pkgrel=23
epoch=
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors. Fixed for kernel version 3.10."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/ndiswrapper/"
license=('GPL')
depends=('linux>=3.12' 'linux<3.13')
makedepends=('linux-headers>=3.12' 'linux-headers<3.13')
optdepends=('ndisgtk: GTK+ based frontend for ndiswrapper.')
provides=('ndiswrapper')
install=".install"
source=("http://download.sourceforge.net/ndiswrapper/$pkgname-$pkgver.tar.gz"
        "kernel-3.10.patch")
md5sums=('ba6f57e5eb2f9e94cb07c7f151b2afcd'
         'cecf6a14f1f7f228327eceb57b70ec55')

_EXTRAMODULES='extramodules-3.12-ARCH'
_KERNEL=$(cat /usr/lib/modules/${_EXTRAMODULES}/version)


prepare() {
	[ -z "$_KERNEL" ] && error "Could not get kernel version from '/usr/lib/modules/${_EXTRAMODULES}/version'..." && false
	msg "Found kernel version: $_KERNEL"
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i "$srcdir/kernel-3.10.patch"
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make -C driver KVERS_UNAME=${_KERNEL}
	make -C utils
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make -C driver DESTDIR=${pkgdir} KVERS_UNAME=${_KERNEL} install
	make -C utils DESTDIR=${pkgdir} install
}
