# Contributor: Chris Waddell <christopher at cwaddell . com >
# Contributor: Christoph Hoopmann <choopm at 0pointer.org>
# Contributor: Klaas Boesche <aurkagebe _at_ googlemail.com>
# Modified from aur package dwarffortress-mayday by
# Contributor: Patrick Chilton <chpatrick _at_ gmail _dot_ com>
# Original from community by
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>

pkgname=dwarffortress-ironhand
pkgver=0.40.24
_dfver=40_24
_pkgver=40_24A
pkgrel=1
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world. Packed with Ironhand's tileset and graphics pack.  Does not replace other dwarffortress packages."
arch=(i686 x86_64)
url="http://dffd.wimbli.com/file.php?id=8747"
install="dwarffortress-ih.install"
license=('custom:dwarffortress-ih')
depends=(gcc-libs glew glu gtk2 libsndfile libxdamage ncurses openal sdl_image sdl_ttf)
makedepends=(unrar unzip git gcc-multilib)
if [[ $CARCH == 'x86_64' ]]; then
  depends=(gcc-libs-multilib lib32-glew lib32-glu lib32-gtk2 lib32-libsndfile lib32-libxdamage lib32-ncurses lib32-openal lib32-sdl_image lib32-sdl_ttf)
  optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
              'lib32-catalyst-utils: If you have ATI graphics'
              'lib32-alsa-lib: for alsa sound'
              'lib32-libpulse: for pulse sound')
fi
backup=('opt/df_linux-ih/data/init/colors.txt'
        'opt/df_linux-ih/data/init/init.txt'
        'opt/df_linux-ih/data/init/d_init.txt'
        'opt/df_linux-ih/data/init/interface.txt')

source=("http://www.bay12games.com/dwarves/df_${_dfver}_linux.tar.bz2"
        "ironhand-${_pkgver}.zip::http://dffd.wimbli.com/download.php?id=8758&f=Ironhand_${_pkgver}_Upgrade.zip"
		'dwarffortress-ih'
		'dwarffortress-ih.desktop' 
		'dwarffortress-ih.png')
		
md5sums=('2804697e63b1bb9f6d0513a6fe42f7dc'
         '83540ec1a95afa5df453cfe5cd89cb9a'
         'ee1a5e9f751dab09f25bb1694d019108'
         'ebf89a2d8d9457b58b4c16babab529d7'
         'b1d51f82400073af9bb179e34a9209d0')

_installname=df_linux-ih

package() {

  cd $srcdir/df_linux
  install -dm755 $pkgdir/opt/
  install -dm775 -o root -g games $pkgdir/opt/${_installname}
  cp -r $srcdir/df_linux/* $pkgdir/opt/${_installname}/
  cp -rf $srcdir/Dwarf\ Fortress/* $pkgdir/opt/${_installname}/

  find $pkgdir/opt/${_installname} -type d -exec chmod 755 {} +
  find $pkgdir/opt/${_installname} -type f -exec chmod 644 {} +

  install -Dm755 $srcdir/dwarffortress-ih $pkgdir/usr/bin/dwarffortress-ih

  chmod 755 $pkgdir/opt/${_installname}/libs/Dwarf_Fortress
  
  ln -s /usr/lib32/libpng.so $pkgdir/opt/${_installname}/libs/libpng.so.3
  rm $pkgdir/opt/${_installname}/libs/{libgcc_s.so.1,libstdc++.so.6}

  install -d -m775 -o root -g games $pkgdir/opt/${_installname}/data/save

  chown -R root:games $pkgdir/opt/${_installname}/data
  find $pkgdir/opt/${_installname}/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/${_installname}/data -type f -exec chmod 664 {} +
  chown root:games $pkgdir/opt/${_installname}

  install -Dm644 $srcdir/dwarffortress-ih.desktop $pkgdir/usr/share/applications/dwarffortress-ih.desktop
  install -Dm644 $srcdir/dwarffortress-ih.png     $pkgdir/usr/share/pixmaps/dwarffortress-ih.png

  install -Dm644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress-ironhand/readme.txt

}
