# Maintainer: Klaas Boesche <aurkagebe _at_ googlemail.com>
# Modified from aur package dwarffortress-mayday by
# Contributor: Patrick Chilton <chpatrick _at_ gmail _dot_ com>
# Original from community by
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>
pkgname=dwarffortress-ironhand
pkgver=0.73
_pkgver=34_11
pkgrel=7
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world. Packed with Ironhand's tileset and graphics pack.  Does not replace other dwarffortress packages."
arch=(i686 x86_64)
url="http://www.bay12forums.com/smf/index.php?topic=53180.0"
install="dwarffortress-ih.install"
license=('custom:dwarffortress-ih')
depends=(gtk2 glu sdl_image libsndfile openal sdl_ttf glew)
makedepends=(unrar unzip git)
if [[ $CARCH == 'x86_64' ]]; then
  depends=(gcc-multilib lib32-gtk2 lib32-glu lib32-sdl_image lib32-libsndfile lib32-openal
           lib32-libxdamage lib32-ncurses lib32-sdl_ttf lib32-glew lib32-libpng) 
  optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
              'lib32-catalyst-utils: If you have ATI graphics'
              'lib32-alsa-lib: for alsa sound'
              'lib32-libpulse: for pulse sound')
fi
backup=('opt/df_linux-ih/data/init/colors.txt'
        'opt/df_linux-ih/data/init/init.txt'
        'opt/df_linux-ih/data/init/d_init.txt'
        'opt/df_linux-ih/data/init/interface.txt')
	# I made a fucking github repo with the sole purpose of unfucking df a bit
# We try to compile whatever little bit of df is open source
source=(http://www.bay12games.com/dwarves/df_${_pkgver}_linux.tar.bz2
        ironhand-${pkgver}.zip::"http://dffd.wimbli.com/download.php?id=6320&f=Ironhand+upgrade+${pkgver}.zip"
        git://github.com/svenstaro/dwarf_fortress_unfuck.git
	dwarffortress-ih 
	dwarffortress-ih.desktop 
	dwarffortress-ih.png)
md5sums=('33e26a93e5914f7545fa1aaa53706eeb'
         '56e37b308ca5f1d5582ff82814c2649e'
         'SKIP'
         '8501394572bc800d1217da5119013411'
         'ebf89a2d8d9457b58b4c16babab529d7'
         'b1d51f82400073af9bb179e34a9209d0')
	 
build() {
  cd $srcdir/dwarf_fortress_unfuck
  make
}

package() {

  cd $srcdir/df_linux
  install -dm755 $pkgdir/opt/
  install -dm775 -o root -g games $pkgdir/opt/df_linux
  cp -r $srcdir/df_linux/* $pkgdir/opt/df_linux/
  # copy ironhand files over the regular ones
  cp -rf $srcdir/Dwarf\ Fortress/* $pkgdir/opt/df_linux/

  # Yay for precompiled stuff with junk permissions! :D
  find $pkgdir/opt/df_linux -type d -exec chmod 755 {} +
  find $pkgdir/opt/df_linux -type f -exec chmod 644 {} +

  install -Dm755 $srcdir/dwarffortress-ih $pkgdir/usr/bin/dwarffortress-ih

  chmod 755 $pkgdir/opt/df_linux/libs/Dwarf_Fortress
  
  install -Dm755 $srcdir/dwarf_fortress_unfuck/libs/libgraphics.so $pkgdir/opt/df_linux/libs/libgraphics.so
  ln -s /usr/lib32/libpng.so $pkgdir/opt/df_linux/libs/libpng.so.3
  rm $pkgdir/opt/df_linux/libs/{libgcc_s.so.1,libstdc++.so.6}

  install -d -m775 -o root -g games $pkgdir/opt/df_linux/data/save

  # This probably is overkill, but I don't know what specific files df needs permission for in here.
  chown -R root:games $pkgdir/opt/df_linux/data
  find $pkgdir/opt/df_linux/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/df_linux/data -type f -exec chmod 664 {} +
  chown root:games $pkgdir/opt/df_linux
  chmod 775 $pkgdir/opt/df_linux

  # Desktop launcher with icon
  install -Dm644 $srcdir/dwarffortress-ih.desktop $pkgdir/usr/share/applications/dwarffortress-ih.desktop
  install -Dm644 $srcdir/dwarffortress-ih.png     $pkgdir/usr/share/pixmaps/dwarffortress-ih.png

  install -Dm644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress-ironhand/readme.txt

  # move the whole thing so we're in a different install than the normal dwarf fortress
  mv $pkgdir/opt/df_linux $pkgdir/opt/df_linux-ih


}

# vim:set ts=2 sw=2 et:
