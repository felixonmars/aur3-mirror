# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Gergely Imreh <imrehg(at)gmail(dot)com>
# Contributor: Stefan Lohmaier <noneuss at gmail dot com

pkgname=navit
pkgver=0.2.0
pkgrel=1
pkgdesc="A modular turn-by-turn car navigation system"
arch=('i686' 'x86_64')
url="http://www.navit-project.org/"
license=('GPL')
depends=('qt' 'cairo' 'dbus-glib' 'fribidi' 'imlib2' 'sdl_image' 'gtk2' 'gpsd' 'postgresql-libs')
optdepends=('cegui>=0.5.0: OpenGL gui'
            'quesoglc: OpenGL gui')
options=('!libtool')
install=navit.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('9c9e8f16d36c318cdc352ef3627ce70e4ac319bfd7261c2155b4c96d76716dca')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find -name "*.moc" -delete
  ./configure --prefix=/usr --disable-samplemap
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
