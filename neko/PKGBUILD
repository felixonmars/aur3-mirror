# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Dwight Schauer <dschauer at gmail dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>

pkgname=neko
pkgver=1.8.2
_fixedrevision=1879
pkgrel=8
pkgdesc="High-level language which can also be used as an embedded scripting language"
url="http://nekovm.org/"
license=('LGPL')
arch=('x86_64' 'i686')
depends=('gc' 'gtk2' 'libmysqlclient' 'pcre' 'zlib' 'sqlite' 'apache')
makedepends=('pkgconfig' 'setconf' 'apr' 'subversion')
options=('!strip')
source=('install.neko.patch')
sha256sums=('999e85cb77941c908bd4650f9d7177519fb2b35e50cc24f9d8465506c03d8d4f')

build() {
  cd "$srcdir"

  msg2 "Checking out svn revision $_fixedrevision..."
  if [[ -d "$pkgname/.svn" ]]; then
    (cd "$pkgname" && svn up -r "$_fixedrevision")
  else
    svn co \
      http://nekovm.googlecode.com/svn/trunk/ \
      --config-dir ./ -r "$_fixedrevision" "$pkgname"
  fi

  msg2 "Setting up build directory..."
  rm -rf "$srcdir/$pkgname-build"
  cp -r "$srcdir/$pkgname" "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  setconf Makefile MAKESO '$(CC) -shared'

  sed 's|= -Wall -O3|+=|g;s|-mkdir|mkdir -p|g;/strip/d' -i Makefile
  mkdir -p bin

  [ "$CARCH" = "x86_64" ] && export CFLAGS+=" -D_64BITS"

  patch -p2 -i $srcdir/install.neko.patch --verbose

  make clean
  make libneko
  make neko std
  CFLAGS+=`pkg-config --cflags apr-1` make -j1
}

check() {
  cd "$srcdir/$pkgname-build"
  make test
}

package() {
  cd "$srcdir/$pkgname-build"

  install -d "$pkgdir"/usr/{bin,lib}
  make INSTALL_PREFIX="$pkgdir"/usr install

  find "$pkgdir"/usr/lib -type f -exec chmod 644 {} \;
}

# vim:set ts=2 sw=2 et:
