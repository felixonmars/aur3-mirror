# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Dwight Schauer <dschauer at gmail dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>

pkgname=neko
pkgver=1.8.2
_fixedrevision=1878
pkgrel=4
pkgdesc="High-level language which can also be used as an embedded scripting language"
url="http://nekovm.org/"
license=('LGPL')
arch=('x86_64' 'i686')
depends=('gc' 'gtk2' 'libmysqlclient')
makedepends=('pkgconfig' 'setconf')
options=('!strip')
source=('neko_mod.patch')
sha256sums=('b84765649668b3c719e71eba91aea0a7936edf9caf6c131b46bb6a441771a2c1')

build() {
  cd "$srcdir"

  msg2 "Checking out svn revision $_fixedrevision..."
  if [[ -d "$pkgname/.svn" ]]; then
    (cd "$pkgname" && svn up -r "$_fixedrevision")
  else
    svn co \
      http://nekovm.googlecode.com/svn/trunk/ \
      --config-dir ./ -r "$_fixedrevision" "$pkgname"
  fi

  msg2 "Setting up build directory..."
  rm -rf "$srcdir/$pkgname-build"
  cp -r "$srcdir/$pkgname" "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  setconf Makefile MAKESO '$(CC) -shared'

  sed 's|= -Wall -O3|+=|g;s|-mkdir|mkdir -p|g;/strip/d' -i Makefile
  mkdir -p bin

  [ "$CARCH" = "x86_64" ] && export CFLAGS+=" -D_64BITS"

  patch -p0 -i $srcdir/neko_mod.patch --verbose

  make clean
  make libneko
  make neko std
  CFLAGS+=`pkg-config --cflags apr-1` make -j1
}

package() {
  cd "$srcdir/$pkgname-build"

  install -d "$pkgdir"/usr/{bin,lib}
  make INSTALL_PREFIX="$pkgdir"/usr install

  find "$pkgdir"/usr/lib -type f -exec chmod 644 {} \;
}

# vim:set ts=2 sw=2 et:
