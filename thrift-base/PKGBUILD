# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Byron Clark <byron@theclarkfamily.name>
pkgname=thrift-base
pkgver=0.9.0
pkgrel=1
pkgdesc="The compiler for Thrift, a scalable cross-language services framework for IPC/RPC"
arch=(i686 x86_64)
url="http://thrift.apache.org/"
license=(APACHE)
conflicts=(thrift)
depends=(zlib libevent openssl)
makedepends=(automake autoconf libtool)
optdepends=('vim-thrift: for syntax highlighting in vim')
options=(!emptydirs !makeflags)
source=(http://www.apache.org/dist/thrift/$pkgver/thrift-$pkgver.tar.gz)
md5sums=('beb2c8290e97c93e3b2844f558cc5c7d')

build() {
  cd $srcdir/thrift-$pkgver

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --without-cpp \
    --without-qt4 \
    --without-c_glib \
    --without-csharp \
    --without-java \
    --without-erlang \
    --without-perl \
    --without-php \
    --without-php_extension \
    --without-python \
    --without-ruby \
    --without-haskell \
    --without-go \
    --without-d

  make
}

package() {
  cd $srcdir/thrift-$pkgver
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
