# Maintainer: Whitney Marshall <whitney.marshall@gmail.com>
pkgname=icaclient
pkgver=12.1.0.203066
pkgrel=1
pkgdesc="Citrix Receiver for Linux (ICAClient)"
arch=('x86_64')
url="http://www.citrix.com/English/ps2/products/product.asp?contentID=1689163&ntref=prod_top"
license=('custom:Citrix')
depends=(
    'gstreamer0.10-base'
    'lib32-alsa-lib'
    'lib32-libvorbis'
    'lib32-libxmu'
    'lib32-libxp'
    'lib32-libxpm'
    'lib32-gtk2'
    'lib32-openmotif'
    )
conflicts=('bin32-citrix-client' 'citrix-client')
install=$pkgname.install
source=(linuxx86_$pkgver.tar.gz::'http://www.citrix.com/English/SS/downloads/EULA_p.asp?downloadID=2323812&versionID=2323814&productID=1689163')
md5sums=('e09ae908e69bfe7aa227d648750af97a')

package() {
  ICAROOT=/opt/Citrix/ICAClient
  install --directory --owner=root --group=root --mode=555 \
    "$pkgdir$ICAROOT"{,/lib,/util,/keystore,/keystore/cacerts,/config/usertemplate,/keyboard,/help,/icons,/nls,/gtk,/gtk/glade,/nls/{en,de,ja}/{LC_MESSAGES,UTF-8}} 

  cd "$srcdir/linuxx86/linuxx86.cor"
  install --owner=root --group=root --mode=555 \
    wfica \
    wfcmgr \
    *.so \
    *.DLL \
    FlashContainer.bin \
    "$pkgdir$ICAROOT"
  install --owner=root --group=root --mode=555 \
    util/*.sh \
    util/*.so \
    util/echo_cmd \
    util/gst_{read,play}.64 \
    util/hinst \
    util/nslaunch \
    util/pacexec \
    util/pnabrowse \
    util/what \
    util/xcapture \
    "$pkgdir$ICAROOT/util"
  # The following 32-bit library causes false namcap errors
  rm util/libgstflatstm.32.so
  install --owner=root --group=root --mode=444 \
    util/pac.js \
    "$pkgdir$ICAROOT/util"
  install --owner=root --group=root --mode=444 \
    keystore/cacerts/*.crt \
    "$pkgdir$ICAROOT/keystore"
  install --owner=root --group=root --mode=444 \
    config/*.ini \
    "$pkgdir$ICAROOT/config"
  install --owner=root --group=root --mode=444 \
    config/usertemplate/*.ini \
    "$pkgdir$ICAROOT/config/usertemplate"
  install --owner=root --group=root --mode=444 \
    keyboard/*.ini keyboard/*.kbd \
    "$pkgdir$ICAROOT/keyboard"
  install --owner=root --group=root --mode=444 \
    icons/*.png icons/session.xpm \
    "$pkgdir$ICAROOT/icons"
  install --owner=root --group=root --mode=444 \
    gtk/resource.gtkrc \
    "$pkgdir$ICAROOT/gtk"
  install --owner=root --group=root --mode=444 \
    gtk/glade/*.glade \
    "$pkgdir$ICAROOT/gtk/glade"
  install --owner=root --group=root --mode=444 \
    lib/*.so \
    "$pkgdir$ICAROOT/lib"
  for locale in de en ja; do
     install --owner=root --group=root --mode=444 \
       nls/$locale/{*.{ini,txt,htm,ad,nls},Wfcmgr} \
       "$pkgdir$ICAROOT/nls/$locale"
     install --owner=root --group=root --mode=444 \
       nls/$locale/LC_MESSAGES/*.mo \
       "$pkgdir$ICAROOT/nls/$locale/LC_MESSAGES"
     install --owner=root --group=root --mode=444 \
       nls/$locale/UTF-8/{*.{txt,ad,nls},Wfcmgr} \
       "$pkgdir$ICAROOT/nls/$locale/UTF-8"
  done
  
  cd "$pkgdir$ICAROOT"
  ln -s en nls/C
  for locale in de en ja; do
    ln -s Npica.ad nls/$locale/Npica
    ln -s UTF-8 nls/$locale/utf8
    ln -s XCapture.ad nls/$locale/UTF-8/XCapture
    ln -s XCapture.ad nls/$locale/XCapture
  done

  cat > wfica.sh << EOF
#!/bin/sh
ICAROOT=$ICAROOT
export ICAROOT
\$ICAROOT/wfica -file \$1
EOF
  chmod 755 wfica.sh

  install --owner=root --group=root --mode=444 -D \
    nls/en/eula.txt \
    "$pkgdir/usr/share/licenses/$pkgname/eula.txt"
}

# vim:set ts=2 sw=2 et:
