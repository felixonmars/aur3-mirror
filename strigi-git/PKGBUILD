pkgname=strigi-git
_gitname=strigi
pkgver=v0.7.8
pkgrel=1
pkgdesc='Fast crawling desktop search engine with Qt4 GUI'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('qt4' 'bzip2' 'exiv2' 'libxml2' 'boost-libs')
makedepends=('git' 'cmake' 'boost')
conflicts=('strigi')
provides=('strigi')
groups=('kde-git')
source=('git://anongit.kde.org/strigi')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $srcdir/$_gitname
  git submodule update --init

  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi
  mkdir $srcdir/build
  cd $srcdir/build
  cmake $srcdir/$_gitname -DCMAKE_BUILD_TYPE=debugfull \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_INOTIFY=ON \
		-DENABLE_LOG4CXX=OFF \
		-DENABLE_FAM=OFF \
		-DENABLE_CLUCENE=OFF \
		-DENABLE_CLUCENE_NG=OFF \
		-DENABLE_FFMPEG=OFF
  make
}

package() {
  cd "${srcdir}/build/libstreams"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/libstreamanalyzer"
  make DESTDIR="${pkgdir}" install
  
  cd "${srcdir}/build/strigiutils"
  make DESTDIR="${pkgdir}" install
}
