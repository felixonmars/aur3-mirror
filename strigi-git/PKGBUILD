pkgname=strigi-git
pkgver=20121026
pkgrel=1
pkgdesc="Fast crawling desktop search engine with Qt4 GUI"
arch=('i686' 'x86_64')
url="http://strigi.sourceforge.net/"
license=('GPL')
source=('gcc47.patch')
depends=('qt' 'bzip2' 'exiv2' 'libxml2' 'boost-libs')
makedepends=('git' 'cmake' 'boost')
conflicts=('strigi')
provides=('strigi')
groups=('kde4-git')
md5sums=('1acb9b31a4859dc427fc4d387fd8aa2f')

_gitroot="git://anongit.kde.org/strigi"
_gitname="strigi"

build() {
  cd $srcdir
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $srcdir/$_gitname 
    git pull origin
    git submodule update
    msg "The local files are updated."
  else
    git clone $_gitroot
    cd $srcdir/$_gitname
    git submodule update --init
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi
  cp -r $srcdir/$_gitname $srcdir/build
  cd $srcdir/build/strigidaemon
  patch -p1 -i ${srcdir}/gcc47.patch
  cd $srcdir/build
  cmake . -DCMAKE_BUILD_TYPE=debugfull \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_INOTIFY=ON \
		-DENABLE_LOG4CXX=OFF \
		-DENABLE_FAM=OFF \
		-DENABLE_CLUCENE=OFF \
		-DENABLE_CLUCENE_NG=OFF \
		-DENABLE_FFMPEG=OFF 
  make
}

package() {
  cd "${srcdir}/build/libstreams"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/libstreamanalyzer"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/strigiclient/lib/searchclient/qtdbus"
  make DESTDIR="${pkgdir}" install
  
  cd "${srcdir}/build/strigiutils"
  make DESTDIR="${pkgdir}" install
}
