# Maintainer: megadriver <megadriver at gmx dot com>
# Based on gtk3 from [extra]

pkgname=gtk3-light
pkgver=3.4.3
pkgrel=1
pkgdesc="GTK3 without colord and its baggage"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info')
makedepends=('gobject-introspection')
options=('!libtool')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini wacom.patch)
md5sums=('e552d52c3b0824eb99842dc9c5f4875f'
         '085ece008fa3a0b7a72e9fe3dda3631a'
         '80864e2c15af1a412e0f04b5c510f12e')
conflicts=('gtk3')
provides=('gtk3')

build() {
    cd "gtk+-$pkgver"

    # Partially revert BGO#673440 in order to fix BGO#674157
    patch -Np1 -i ../wacom.patch

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-colord \
        --disable-schemas-compile
    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
