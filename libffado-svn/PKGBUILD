# Maintainer: Ray Rashif <schiv@archlinux.com>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado-svn
pkgver=2186
pkgrel=1
pkgdesc="Driver for FireWire audio devices (development version)"
arch=('i686' 'x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++'
         'libxml++' 'alsa-lib' 'dbus-c++' 'libconfig')
makedepends=('subversion' 'scons' 'python2-pyqt')
optdepends=('python2-pyqt: mixer applet')
provides=('libffado')
conflicts=('libffado' 'ffado-svn')
replaces=('ffado-svn')
source=('60-ffado.rules')
md5sums=('f03a3e950801c4405b8934084651bca8')

_svntrunk=http://subversion.ffado.org/ffado/trunk/libffado
_svnmod=libffado

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  # python2 fix
  sed -i 's:python $SOURCE:python2 $SOURCE:' SConstruct
  sed -i 's:python support/tools:python2 support/tools:' \
    support/tools/SConscript

  scons PREFIX=/usr ENABLE_OPTIMIZATIONS=yes
}

package() {
  cd "$srcdir/$_svnmod-build"

  scons DESTDIR="$pkgdir" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # add audio device rules
  # see https://bugs.archlinux.org/task/26342
  install -Dm644 "$srcdir/60-ffado.rules" \
    "$pkgdir/usr/lib/udev/rules.d/60-ffado.rules"

  # python2 fix
  for i in $(grep '^#!.*bin.*python' -R "$pkgdir" | sed 's/:.*//'); do
    sed -i 's:^#!.*bin.*python:#!/usr/bin/python2:' "$i"
  done

  # desktop files & icons
  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop \
    "$pkgdir/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
