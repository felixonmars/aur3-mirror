# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Archist archist@die-optimisten.net
# Contributor: C Anthony Risinger
pkgname=('zarafa-server' 'zarafa-webaccess')
pkgver=7.1.10
_pkgrel=44973
pkgrel=3
pkgdesc="Open Source Groupware Solution"
arch=('x86_64 i686')
url="http://www.zarafa.com/"
license=('AGPL3')
makedepends=('libical' 'php>=5.2.0' 'e2fsprogs' 'openldap' 'automake' 'swig' 'zarafa-libvmime' 'mysql' 'python2' 'kyotocabinet' 'clucene' 'boost' 'boost-libs')
optdepends=('php: for webaccess')
install=${pkgname}.install
source=("zarafa-${pkgver}.tar.gz::http://download.zarafa.com/community/final/7.1/${pkgver}-${_pkgrel}/sourcecode/zcp-${pkgver}.tar.gz"
	"zarafa-server.service"
	"zarafa-ical.service"
        "zarafa-gateway.service"
        "zarafa-spooler.service"
        "zarafa-dagent.service")

md5sums=('244bf320f811fd375cfb8cb82bd324eb'
         '0e2728f1e35b25ca679427fcb57315d8'
         'c25f3982217fe390d68ed9003a5988ed'
         '705ada3a8c4b904696e0c461c131b4f7'
         '846b76e5cb0239a488c81e11d74ad08b'
         '9666bf713645af11dd65b3ac5cbb42d9')

prepare() {
  cd ${srcdir}/zarafa-${pkgver}
  # Fix python shebang
  sed -i 's/python setup.py/python2 setup.py/' swig/python/Makefile.in
  sed -i 's/python setup.py/python2 setup.py/' swig/python/Makefile.am

  # Disable search, doesn't compile due to a boost error
  cd ${srcdir}/zarafa-${pkgver}/ECtools/
  sed -i 's/zarafa-search//g' Makefile.in
  sed -i 's/zarafa-search//g' Makefile.am

  # Fix boost, doesn't seem to work though
  #sed -i 's/\# Boost/BOOST_CPPFLAGS="$BOOST_CPPFLAGS -DBOOST_NO_CXX11_RVALUE_REFERENCES"/g' configure.ac
  
}

build() {
  cd ${srcdir}/zarafa-${pkgver}

  msg "Starting build..."
  CPPFLAGS=-I/usr/include/python2.7 ./configure --prefix=/usr \
    --enable-oss \
    --enable-release \
    --enable-python \
    --enable-swig \
    --with-python=/usr/bin/python2 \
    --disable-debug \
    --disable-static \
    --with-userscript-prefix=/etc/zarafa/userscripts \
    --with-quotatemplate-prefix=/etc/zarafa/quotamails \
    --with-searchscripts-prefix=/etc/zarafa/searchscripts

  make
}


package_zarafa-server() {
  pkgdesc="Open Source Groupware Solution"
  depends=('libical>=0.44' 'mysql' 'curl' 'libxml2' 'openssl' 'openldap' 'krb5' 'libical'
	 'boost>=1.52.0' 'bison' 'python2' 'swig' 'kyotocabinet' 'zarafa-libvmime' 'libmariadbclient')
  backup=('etc/zarafa/server.cfg'
	'etc/zarafa/dagent.cfg'
	'etc/zarafa/gateway.cfg'
	'etc/zarafa/monitor.cfg'
	'etc/zarafa/spooler.cfg'
	'etc/zarafa/ical.cfg')

  cd ${srcdir}/zarafa-${pkgver}

  make DESTDIR=${pkgdir} install
  # Finalize
  install -m 755 -o root -g root -d ${pkgdir}/var/log/zarafa
  install -m 755 -o root -g root -d ${pkgdir}/var/lib/zarafa

  # move /var/lib to the right place related to https://jira.zarafa.com/browse/ZCP-12249
  #mv  $pkgdir/usr/var/lib/zarafa/* $pkgdir/var/lib/zarafa/
  #rm -rf $pkgdir/usr/var

  # Systemd files
  install -d  ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-server.service ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-ical.service ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-spooler.service ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-dagent.service ${pkgdir}/usr/lib/systemd/system
  install -m 755 $srcdir/zarafa-gateway.service ${pkgdir}/usr/lib/systemd/system

  # Remove unused /usr/etc related to https://jira.zarafa.com/browse/ZCP-12249
  #rm -rf $pkgdir/usr/etc

  # copy example configs to their active locations
  for cfg in ${pkgdir}/usr/share/doc/zarafa/example-config/*.cfg; do
    install -m 755 -o root -g root -D ${cfg} ${pkgdir}/etc/zarafa
  done
}

package_zarafa-webaccess() {
  pkgdesc="Webaccess client for the Zarafa Open Source Groupware Solution"
  depends=('zarafa-server')

  # Webaccess
  install -d ${pkgdir}/etc/httpd/conf/extra/
  install -m 755 ${srcdir}/zarafa-${pkgver}/php-webclient-ajax/zarafa-webaccess.conf ${pkgdir}/etc/httpd/conf/extra/zarafa-webaccess.conf
  install -m 755 -d ${pkgdir}/usr/share/zarafa-webaccess/

  cp -r ${srcdir}/zarafa-${pkgver}/php-webclient-ajax/* ${pkgdir}/usr/share/zarafa-webaccess/
  mv ${pkgdir}/usr/share/zarafa-webaccess/config.php.dist ${pkgdir}/usr/share/zarafa-webaccess/config.php
}

