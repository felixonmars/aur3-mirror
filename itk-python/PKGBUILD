# Maintainer: Christofer Bertonha <chritoferbertonha@gmail.com>

pkgname=itk-python
pkgver=3.20.0
pkgrel=1
pkgdesc='Insight Segmentation and Registration Toolkit (ITK).'
arch=('i686' 'x86_64')
url='http://www.itk.org/'
license=('BSD')
depends=('python2' 'zlib' 'libpng' 'libtiff' 'vtk' 'cableswig')
makedepends=('cmake' 'python2' 'zlib' 'libpng' 'libtiff' 'vtk' 'cableswig')
source=("http://downloads.sourceforge.net/project/itk/itk/${pkgver:0:4}/InsightToolkit-${pkgver}.tar.gz" "vtkmetaio.patch")
provides=('insight-toolkit')
conflicts=('insight-toolkit')
md5sums=('5d6b8e6e641624c6a8c06c7f95f05c9e'
         '318b224e6e52f490c6537027e0383588')

build() {
  cd ${srcdir}/InsightToolkit-${pkgver}

  patch -p1 < ../../vtkmetaio.patch

  sed \
    -e '1i#include <cstddef>' \
    -i Utilities/MetaIO/metaUtils.cxx \
       Code/IO/itkImageIORegion.h

  sed \
    -e 's#png_set_gray_1_2_4_to_8#png_set_expand_gray_1_2_4_to_8#g' \
    -i Code/IO/itkPNGImageIO.cxx

  mkdir -p ../build && cd ../build

  cmake \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DBUILD_TESTING:BOOL=OFF \
    -DBUILD_EXAMPLES:BOOL=OFF \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DITK_USE_SYSTEM_GDCM:BOOL=ON \
    -DITK_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DITK_USE_SYSTEM_PNG:BOOL=ON \
    -DITK_USE_SYSTEM_TIFF:BOOL=ON \
    -DITK_USE_SYSTEM_ZLIB:BOOL=ON \
    -DITK_USE_REVIEW:BOOL=ON \
    -DUSE_WRAP_ITK:BOOL=ON \
    -DWRAP_ITK_JAVA:BOOL=OFF \
    -DWRAP_ITK_TCL:BOOL=OFF \
    -DWRAP_ITK_PYTHON:BOOL=ON \
    ../InsightToolkit-${pkgver}

  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install

  # install BSD license
  install -d ${pkgdir}/usr/share/licenses/insight-toolkit
  install -m 644 ../InsightToolkit-${pkgver}/Copyright.txt \
    ${pkgdir}/usr/share/licenses/insight-toolkit
}
