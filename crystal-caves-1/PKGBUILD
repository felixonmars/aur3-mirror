# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>

_appname="cc1"
pkgname="crystal-caves-1"
pkgdesc="Side-scrolling platform game (uses DOSBox)"
pkgver=1.0a
pkgrel=1
arch=(any)
license=("custom:shareware")
url="http://www.3drealms.com/caves/"
depends=("modfs-git" "dosbox")
makedepends=("dynamite-git")
source=("ftp://ftp.3drealms.com/share/1cc${pkgver//.}.zip"
        "${_appname}.desktop"
        "${_appname}.png"
        "lib${_appname}.sh"
        "${_appname}.sh"
        "01-user.conf"
        "99-base.conf")
md5sums=('2a873997d6b43adda1268dc8754ebaae'
         '31e06bc34c889decacf7fb98dc9b2dcc'
         'a3056bff9848ae203a9edd7432793358'
         '931797a81083327c11405a24d4fbcd84'
         '3fa193f3c2803e06aa69382e201738f4'
         'c1549ac03b636273288afaf4046f3ad0'
         '45322fb0729a5a643a6d9da0417d947a')
install="${pkgname}.install"

build() {
  # Extract original resources
  cd "${srcdir}"
  mkdir -p shr
  local shr="ccsw${pkgver//.}.shr"
  cd shr
  id-shr-extract ../"${shr^^*}"
}

package() {
  # Copy original resources
  cd "${srcdir}/shr"
  local i
  for i in cc1* order*; do
    install -Dm644 "${i}" "${pkgdir}/usr/share/games/${_appname}/base/${i}"
  done

  # License
  install -Dm644 "${srcdir}/vendor.doc" "${pkgdir}/usr/share/licenses/${pkgname}/vendor.doc"

  # Modfs
  install -Dm644 "${srcdir}/01-user.conf" "${pkgdir}/etc/${_appname}.d/01-user.conf"
  install -Dm644 "${srcdir}/99-base.conf" "${pkgdir}/etc/${_appname}.d/99-base.conf"

  # Scripts, launchers and icons
  install -Dm644 "${srcdir}/${_appname}.desktop" "${pkgdir}/usr/share/applications/${_appname}.desktop"
  install -Dm644 "${srcdir}/${_appname}.png" "${pkgdir}/usr/share/pixmaps/${_appname}.png"
  install -Dm644 "${srcdir}/lib${_appname}.sh" "${pkgdir}/usr/lib/lib${_appname}.sh"
  install -Dm755 "${srcdir}/${_appname}.sh" "${pkgdir}/usr/bin/${_appname}"
}
