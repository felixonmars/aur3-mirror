# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Francois Charette <firmicus@gmx.net>

_perlmod=Font-TTF
_modnamespace=Font
pkgname=perl-font-ttf
pkgver=1.02
pkgrel=1
pkgdesc="Font::TTF - Perl module for TrueType Font hacking"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-io-string')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('0115330def2df1489826631af2b93675f8496b2cf24891666fe5a6578a9374f2f889bf01aa8a95620fd4475987e1f60a76f6eb05fda6d899a72edb7772ce1d9a')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
