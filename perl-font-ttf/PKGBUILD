# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Francois Charette <firmicus@gmx.net>

_perlmod=Font-TTF
_modnamespace=Font
pkgname=perl-font-ttf
pkgver=1.04
pkgrel=1
pkgdesc="Font::TTF - Perl module for TrueType Font hacking"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('Artistic2.0')
depends=('perl-io-string')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('6d5a4e672b1c9b214ae4e9db6c5e7e3db13773bef5cdae89e62e2fd944dc9949a4c69761cfdbb95ff2f22ece88ee62532bddc1133371f7d38681358e64524078')

build() {
	cd "${srcdir}/${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${srcdir}/${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}/"
}
