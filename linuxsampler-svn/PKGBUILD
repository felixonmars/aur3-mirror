# Maintainer: Jiri Prochazka <ojirio at gmail dot com>
# Contributor: orbisvicis <orbisvicis at gmail dot com>
# Contributor: Tom Vincent <http://tlvince.com/contact/>

pkgname=linuxsampler-svn
_pkg="${pkgname%-svn}"
pkgver=2352
pkgrel=1
pkgdesc="Sampler backend, including sampler engine, audio and MIDI drivers, network layer (LSCP) API and native C++ API"
arch=("i686" "x86_64")
url="http://www.linuxsampler.org/"
license=("GPL" "custom")
depends=("libgig-svn" "jack" "sqlite3")
makedepends=("subversion" "ladspa" "dssi")
optdepends=("qsampler: QT frontend to LinuxSampler"
            "jsampler: Java frontend to LinuxSampler"
            "ladspa: effect plugin system for internal effects"
            "dssi: use linuxsampler as a plugin for DSSI hosts"
            "lv2core: use linuxsampler as a plugin for LV2 hosts")
options=("!libtool" "emptydirs")
sha256sums=("fe9613dc00d4dafdbe9132ba00e82b0c6f333c53dffe02e29a8dfe1753c7b622")
source=("license.txt")

_svntrunk="https://svn.linuxsampler.org/svn/${_pkg}/trunk"
_svnmod="$_pkg"

build() {
  cd "$srcdir"

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_svnmod}-build"
  cp -a "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  #
  # BUILD
  #

  make -f Makefile.cvs
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  local _svndir="${srcdir}/${_svnmod}-build"
  . <(sed -n "s|^\s*\(${_pkg^^}_RELEASE_[^[:blank:]]\{1,\}\)\s*=\s*\([[:digit:]]\{1,\}\).*$|_\1=\2|p" "${_svndir}/configure.in")
  provides=("${_pkg}=${_LINUXSAMPLER_RELEASE_MAJOR}.${_LINUXSAMPLER_RELEASE_MINOR}.${_LINUXSAMPLER_RELEASE_BUILD}" "${_pkg}-cvs")
  conflicts=("${_pkg}" "${_pkg}-cvs")

  cd "$_svndir"
  make DESTDIR=$pkgdir install

  # install GPL exception statement
  install -Dm0644 ../license.txt "${pkgdir}/usr/share/licenses/${_pkg}/GPL-EXCEPTION"

  # add linuxsampler library path to ld.so.conf.d
  install -m0755 -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${_pkg}" > "${pkgdir}/etc/ld.so.conf.d/${_pkg}.conf"
}

# vim:set ts=2 sw=2 et:
