# Maintainer: AkiraYB <brunoyb!yahoo,com,br>

pkgname=emacs-pov-mode
pkgver=3.2
_imname=povray-imenu
_imver=3.6
pkgrel=5
pkgdesc='POV-Ray mode for Emacs'
arch=('any')
url='http://xahlee.org/3d/povray_emacs.html'
license=('GPL3')
depends=('emacs')
install="${pkgname}.install"
changelog="${pkgname}.changelog"
source=("http://xahlee.org/emacs/${pkgname#*-}-${pkgver}.tar.gz"
        "http://www.imagico.de/imenu/${_imname}-${_imver}.tar.gz")
noextract=("${_imname}-${_imver}.tar.gz")
sha256sums=('b98f6c1a1778d4d7f9ecbccdf20fa233ccf9f502306a43fd2f52bf8ef3d019e6'
            'c99e00f3ba1de305b9859af43be4fbb4caf54426479461b9922dd7b2989c038b')

build()
{
	cd "${srcdir}/${pkgname#*-}-${pkgver}"

	sed -e 's|/usr/local|/usr|' -i pov-mode.el
}

package()
{
	cd "${srcdir}/${pkgname#*-}-${pkgver}"

	local _infodir="${pkgdir}/usr/share/info"
	local _installdir="${pkgdir}/usr/share/emacs/site-lisp/pov-mode"

	install -d "${_infodir}" "${_installdir}/InsertMenu"

	install -m 644 pov-mode.info "$_infodir"
	install -m 644 pov-mode.el povrender.xpm povview.xpm "$_installdir"

	bsdtar -xof "${srcdir}/${_imname}-${_imver}.tar.gz" \
	         -C "${_installdir}/InsertMenu"
}
