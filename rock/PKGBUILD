# Maintainer: Scott Olson <scott@scott-olson.org>

pkgname=rock
pkgver=0.9.4
pkgrel=1
pkgdesc="An ooc compiler written in ooc"
arch=('i686' 'x86_64')
license=('BSD')
url="http://ooc-lang.org/"
source=("http://github.com/downloads/nddrylliog/${pkgname}/${pkgname}-${pkgver}-source.tar.bz2")
makedepends=('gcc' 'tar' 'bzip2')
depends=('sh')
optdepends=(
  'gcc: can be used as backend C compiler'
  'clang: can be used as backend C compiler'
  'tcc: can be used as backend C compiler'
)
md5sums=('eaa4b0b74ee2aacb28b9d00e081cb0ab')

build() {
  cd "${srcdir}/rock"

  make rescue || return 1
}

package() {
  cd "${srcdir}/rock"

  install -d "$pkgdir/opt/$pkgname"
  cp -rt "$pkgdir/opt/$pkgname" libs sdk
  rm -rf "$pkgdir/opt/$pkgname/libs/sources"

  install -D bin/rock "$pkgdir/opt/$pkgname/bin/rock"
  install -d "$pkgdir/usr/bin"
  echo '#!/bin/sh' > "$pkgdir/usr/bin/rock"
  echo 'ROCK_DIST=/opt/rock /opt/rock/bin/rock "$@"' >> "$pkgdir/usr/bin/rock"
  chmod +x "$pkgdir/usr/bin/rock"

  install -D docs/rock.1 "$pkgdir/usr/share/man/man1/rock.1"
  install -D LICENSE "$pkgdir/usr/share/licenses/rock/LICENSE"
}
