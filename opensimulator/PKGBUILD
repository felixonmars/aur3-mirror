# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: LTSmash <lord.ltsmash@gmail.com>
# Contributor: Zauber Exonar <zauberexonar at gmail>
# Contributor: Simon Peter Nicholls <simon.peter.nicholls at googlemail>
# Maintainer: GordonGR <gordongr@freemail.gr>

pkgname=opensimulator
pkgver=0.7.3.1
pkgrel=1
pkgdesc="A 3D application server used to create a virtual environment or world"
arch=('i686' 'x86_64')
url="http://opensimulator.org/wiki/Main_Page"
license=('BSD')
depends=('mono' 'sqlite3')
optdepends=('mysql')
makedepends=('nant')
install=$pkgname.install
source=('http://opensimulator.org/dist/opensim-0.7.3.1-source.tar.gz'
	'opensimulator.sh')
md5sums=('c4548637270809027b366de0cc05bd8b'
         '58270c537e91b85c1121aacc2ccb194e')
backup=(opt/$pkgname/bin/OpenSim.ini)

build() {
  cd "$srcdir"/opensim-$pkgver-source

  # we need Mono
  export MONO_SHARED_DIR="$srcdir"/.wabi
  mkdir -p $MONO_SHARED_DIR

  # build opensimulator using nant
  ./runprebuild.sh
  nant
}

package() {
  cd "$srcdir"/opensim-$pkgver-source

  # delete unneeded and create log/ini files
  [[ `uname -m` = "i686" ]] && find bin -name "*x86_64.so" -delete

  # ensure log file already exists
  touch bin/OpenSim.log

  # create a default OpenSim.ini for installs
  sed 's/^\(\s*\)\; \(Include.*Standalone\.ini\)/\1\2/' bin/OpenSim.ini.example >bin/OpenSim.ini

  #copying Mono.Posix.dll so that OpenSimulator can use sockets, and by extension MySQL
  cp /usr/lib/mono/2.0/Mono.Posix.dll bin/Mono.Posix.dll

  # install
  install -d "$pkgdir"/opt/$pkgname/bin
  cp -r bin/* "$pkgdir"/opt/$pkgname/bin/
  install -Dm644 {CONTRIBUTORS,README}.txt "$pkgdir"/opt/$pkgname

  # set permissions
  find "$pkgdir"/opt/$pkgname/bin -type d -exec chmod 755 {} +
  find "$pkgdir"/opt/$pkgname/bin -type f -exec chmod 644 {} +
  find "$pkgdir"/opt/$pkgname/bin -name "*.exe" -exec chmod 755 {} +
  find "$pkgdir"/opt/$pkgname/bin -name "*.ini" -exec chmod 666 {} +
  find "$pkgdir"/opt/$pkgname/bin -name "*.xml" -exec chmod 666 {} +
  chmod 777 "$pkgdir"/opt/$pkgname/bin/{,*/}
  chmod 755 "$pkgdir"/opt/$pkgname/bin/opensim-ode.sh
  chmod 666 "$pkgdir"/opt/$pkgname/bin/OpenSim.log

  # install script and license files
  install -m755 -D ../$pkgname.sh "$pkgdir"/usr/bin/$pkgname
  install -m644 -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
