# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: andreas_baumann <abaumann at yahoo dot com>

pkgname=mutt-slang
pkgver=1.5.21
pkgrel=2
pkgdesc='Small but very powerful text-based mail client. Using slang insteand of ncurses'
url='http://www.mutt.org/'
arch=('i686' 'x86_64')
license=('GPL')
provides=(mutt)
depends=('gpgme' 'slang' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types')
source=("ftp://ftp.mutt.org/mutt/devel/mutt-${pkgver}.tar.gz")
sha1sums=('a8475f2618ce5d5d33bff85c0affdf21ab1d76b9')

install=install

build() {
	cd "${srcdir}/mutt-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-gpgme \
		--enable-pop \
		--enable-imap \
		--enable-smtp \
		--enable-hcache \
		--with-slang=/usr \
		--with-regex \
		--with-ssl=/usr \
		--with-sasl \
		--with-idn \

	make
}

package() {
	cd "${srcdir}/mutt-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/bin/{flea,muttbug}
	rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
	rm "${pkgdir}"/etc/mime.types{,.dist}
	install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
