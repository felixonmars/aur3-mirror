# Maintainer: Archan Paul <arp at archan dot org>
# Contributor: Clement Siuchung Cheung <clement.cheung@umich.edu>
# Contributor : mightyjaym <jmambrosino@gmail.com>
# Maintainer: Dany Martineau <dany.luc.martineau at gmail.com>
# Contributor: Harvey <harv@gmx.de>
# Contributor: <Tilo Brueckner>  blueperil@gmx.de

pkgbase=qtcurve-gtk2
pkgname=lib32-$pkgbase
pkgver=1.8.16
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('x86_64')
groups=('qtcurve')
license=('GPL')
depends=('lib32-gtk2' 'lib32-gtk-engines' "$pkgbase")
makedepends=('gcc-multilib' 'cmake')
options=('!libtool')
url="http://www.kde-look.org/content/show.php?content=40492"
source=("http://craigd.wikispaces.com/file/view/QtCurve-Gtk2-${pkgver}.tar.bz2")
md5sums=('8923caaa0704f6a30252a5eadb015d76')

build() {
  cd "$srcdir"

  mkdir -p build
  cd build

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake ../QtCurve-Gtk2-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr/lib32
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
