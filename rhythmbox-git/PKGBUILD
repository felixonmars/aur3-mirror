# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

_pkgname=rhythmbox
pkgname=$_pkgname-git
pkgver=20121009
pkgrel=1
pkgdesc="An iTunes-like music player/libary"
arch=(i686 x86_64)
license=(GPL)
url=http://www.rhythmbox.org
depends=(desktop-file-utils gstreamer0.10-good-plugins gstreamer0.10-python
    gvfs-afc json-glib libdmapsharing libgnome-media-profiles libgpod libmtp
    libmusicbrainz3 libnotify libpeas libwebkit3 lirc-utils media-player-info
    python-gnomekeyring python-mako totem-plparser)
makedepends=('gnome-doc-utils>=0.20.1' intltool brasero gnome-common gtk-doc gobject-introspection git)
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
    'gstreamer0.10-bad-plugins: Extra media codecs'
    'gstreamer0.10-ffmpeg: Extra media codecs'
    'brasero: cd burning')
provides=($_pkgname)
conflicts=($_pkgname)
options=(!libtool !emptydirs !strip)
install=$_pkgname.install

_gitroot=git://git.gnome.org/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]] ; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    export CFLAGS="$CFLAGS -g -O1"
    export CXXFLAGS="$CXXFLAGS -g -O1"
    PYTHON=/usr/bin/python2 ./autogen.sh
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/rhythmbox \
        --localstatedir=/var --disable-static \
        --enable-daap --enable-python --enable-musicbrainz \
        --with-mdns=avahi --disable-scrollkeeper
    make
}

package() {
    cd "$srcdir"/$_gitname-build/

    make DESTDIR="$pkgdir" install
}
