# Contributor: Ananda Samaddar <ananda@samaddar.co.uk>
# Contributor: Alessandro Nakamuta <alessandro dot ufms at gmail dot com>
# Maintainer:  Pauline Martin <321eniluap@gmail.com>

pkgname=gstreamer0.10-good-plugins-slim
pkgver=0.10.30
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('intltool' 'pkgconfig' "gstreamer0.10-good>=${pkgver}" 'libavc1394' 'libiec61883' 'libshout' 'libdv' 'flac' 'wavpack' 'bzip2')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.gz)
md5sums=('3332c35b11635b5ed3cd11371ebd11d2')
depends=("gstreamer0.10-good>=${pkgver}" 'libavc1394' 'libiec61883' 'libshout' 'libdv' 'flac' 'wavpack' 'libpng' 'libjpeg')
pkgdesc="GStreamer Multimedia Framework Good Plugins (gst-plugins-good) with reduced and no GNOME dependecies"
groups=('gstreamer0.10-plugins')
provides=("gstreamer0.10-good-plugins=${pkgver}")
conflicts=('gstreamer0-10-good-plugins')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --disable-schemas-install \
	--disable-hal \
    --disable-esd \
    --disable-oss \
    --disable-aalib \
    --disable-gconf \
    --disable-libcaca \
    --with-package-name="GStreamer Good Plugins Slim (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"

  make
  sed -e 's/gst sys ext/gst/' -i Makefile
}

package() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make -C sys DESTDIR="${pkgdir}" install
  make -C ext DESTDIR="${pkgdir}" install
}

