# Contributor: Sam S. <smls75@gmail.com>
# Contributor: Ben R. <thebenj88 *AT* gmail *DOT* com>

pkgname=eufloria-hib
pkgver=1.0.1
#_hibver=2012-11-08
pkgrel=1
pkgdesc='A unique real time strategy game (Humble Bundle version)'
url='http://www.eufloria-game.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('libgl' 'mesa' 'sdl' 'sdl_mixer' 'sdl_image')
optdepends=('catalyst: AMD Proprietary 3D driver'
			'nvidia: NVIDIA Proprietary 3D driver')
source=('eufloria-hib.desktop')
sha256sums=('637790aa33b43d2d2ef3178c07ff882f7c5f85f0b89ad46b971ceed5133c8783')
options=("!upx")
PKGEXT='.pkg.tar'

_gamepkg="EufloriaHD-Linux-${pkgver}.sh"


package() {
  cd $srcdir

  # Get installer
  _get_humblebundle_source "${_gamepkg}" || {
    error "Unable to find the game archive. Please download it from your Humble
           Bundle page, and place it into one of the above locations."
    exit 1; }

  # Execute installer
  msg "Starting setup..."
  [[ -d "./temp" ]] && rm -r "./temp"
  sh "./${_gamepkg}" --unattended --no-register --keep --overwrite \
                     --target  "${srcdir}/temp" \
                     --bindir  "${srcdir}/bin" \
                     --datadir "${pkgdir}/opt"

  # Remove bundled libraries & helper binaries (use system packages instead)
  rm -rf "${pkgdir}/opt/EufloriaHD/xdg-"*

  # Fix permissions
  find "${pkgdir}" -type f -exec chmod 644 "{}" +

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "${pkgdir}/opt/EufloriaHD/EufloriaHD.png" \
                 "${pkgdir}/usr/share/pixmaps/eufloria.png"

  # Install launcher symlink
  case $CARCH in
      i686) _arch=x86;;
      x86_64) _arch=x86_64;;
  esac
  chmod 755 "${pkgdir}/opt/EufloriaHD/EufloriaHD.bin.${_arch}"
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/EufloriaHD/EufloriaHD.bin.${_arch}" "${pkgdir}/usr/bin/eufloria"

  # Install license file
  install -Dm644 "temp/config/license" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}


# Locate a game archive from one of the Humble Bundles, and symlink it into $srcdir
_get_humblebundle_source() {
  _get_local_source "$1" || return 1;
  # TODO: Implement automatic downloading from Humble Bundle site
}

# Locate a file or folder provided by the user, and symlink it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    if [ -z "$_dir" ]; then _dir="<undefined>"; fi
    echo -n "    - in $_key ['$_dir'] ... ";
    if [ -e "$_dir/$1" ]; then
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
    echo "NOT FOUND"
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}
