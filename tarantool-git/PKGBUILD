# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=tarantool-git
pkgver=1.5.1.42.g10a08e7
pkgrel=1
pkgdesc="persistent, transactional in-memory database"
arch=(i686 x86_64)
url="http://www.tarantool.org"
license=(BSD)
depends=()
# gcc has problems with HEAD tarantool
makedepends=(cmake clang)
source=('tarantool::git://github.com/mailru/tarantool#branch=stable'
        'lua-cjson::git://github.com/mpx/lua-cjson.git'
        'luajit::git://repo.or.cz/luajit-2.0.git'
        'tarantool.systemd')
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'ad36cc26a66a50e8537570bfed5b620c775a9f85')

pkgver() {
  cd $srcdir/tarantool
  git describe | sed 's/^v//;s/-/./g'
}

build() {
  cd $srcdir/tarantool

  # tarantool uses git submodule, handle this
  git submodule init
  git config submodule.lua-cjson.url $srcdir/third_party/lua-cjson
  git config submodule.luajit.url $srcdir/third_party/luajit
  git submodule update

  CC=clang CXX=clang++ cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_SYSCONF_DIR=/etc/tarantool \
    -DCMAKE_LOCALSTATE_DIR=/var \
    -DENABLE_CLIENT=ON \
    .

  make
}

package() {
  cd $srcdir/tarantool
  make install DESTDIR=$pkgdir
  install -D -m644 "${srcdir}/tarantool.systemd" "${pkgdir}/usr/lib/systemd/system/tarantool.service"
}
