# Maintainer: Fabien Dubosson <fabien.dubosson@gmail.com>

pkgname="knime-desktop"
_pkgname="knime"
pkgver="2.9.2"
pkgrel="1"
pkgdesc="A user-friendly graphical workbench for the entire data analysis process"
url="http://www.knime.org/"
license=('custom')
arch=('i686' 'x86_64')
depends=('java-environment' 'cairo' 'python2')
optdepends=('bash: Required for bash-scriptable nodes'
            'r: Required for R-scriptable nodes')
options=('!emptydirs')
changelog="ChangeLog"
_CARCH="${CARCH}"
[ "${CARCH}" = 'i686' ] && _CARCH='x86'
source=("http://www.knime.org/knime_downloads/linux/${_pkgname}_${pkgver}.linux.gtk.${_CARCH}.tar.gz"
        'knime.sh'
        'LICENSE')
md5sums=('c38143ffbd78dc08c189a2df532fccd0'
         'b31f2633136df9ead308f8d1ef9da601'
         '9e93e4def16f04f7808bddb48da3c009')
[ "${CARCH}" = 'x86_64' ] && md5sums[0]='f0337f2b6534d3d302e2b67781bea6f4'

package() {
    # Remove unnecessary files
    rm -r "${srcdir}/${_pkgname}_${pkgver}/jre/"             # Provided by `java-environment`
    rm    "${srcdir}/${_pkgname}_${pkgver}/libcairo-swt.so"  #Â Provided by `cairo`

    # Install files
    install -d -m755 "${pkgdir}/usr/share/"
    mv "${_pkgname}_${pkgver}" "${pkgdir}/usr/share/${pkgname}"

    # Link `libcairo` to the existing one
    ln -s "/usr/lib/libcairo.so" "${pkgdir}/usr/share/${pkgname}/libcairo-swt.so"

    # Correct python script bang syntax
    sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|" "${pkgdir}/usr/share/${pkgname}/plugins/org.apache.ant_1.8.2.v20120109-1030/bin/runant.py"

    # Install the executable
    install -D -m755 "${srcdir}/knime.sh" "${pkgdir}/usr/bin/${pkgname}"

    # Install the license
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
