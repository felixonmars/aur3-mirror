# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributer: Ionut Biru <ibiru@archlinux.org>
# Contributer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=virtualbox-ck-modules
true && pkgname=(virtualbox-ck-host-modules virtualbox-ck-guest-modules)
pkgver=4.2.2
pkgrel=3
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-ck-headers' "virtualbox-host-source>=$pkgver" "virtualbox-guest-source>=$pkgver" 'dkms')

_kernver=3.6.3-2-ck
_extraver=extramodules-3.6-ck

build() {
	# dkms need modification to be run as user
	cp -r /var/lib/dkms .
	echo "dkms_tree='$srcdir/dkms'" > dkms.conf
	# build host modules
	msg2 'Host modules'
	dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
	# build guest modules
	msg2 'Guest modules'
	dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-ck-host-modules(){
	_Hpkgdesc='Host kernel modules for VirtualBox running under Linux-ck.'
	pkgdesc="${_Hpkgdesc}"
	provides=("virtualbox-host-modules")
	depends=('linux-ck>=3.6' 'linux-ck<3.7')
	#conflicts=()
	#groups=('ck-generic')
	install=host.install

	install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
	cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
	install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
	sed -ie "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/host.install"
}

package_virtualbox-ck-guest-modules(){
	_Gpkgdesc='Guest kernel modules for VirtualBox running under Linux-ck.'
	pkgdesc="${_Gpkgdesc}"
	license=('GPL')
	provides=("virtualbox-guest-modules")
	depends=('linux-ck>=3.6' 'linux-ck<3.7')
	#conflicts=('')
	#groups=('ck-generic')
	install=guest.install

	install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
	cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
	install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
	sed -ie "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/guest.install"
}
pkgdesc='Kernel modules for VirtualBox running under Linux-ck. No need for dkms with this package!'
