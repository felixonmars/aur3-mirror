# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributer: Ionut Biru <ibiru@archlinux.org>
# Contributer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=virtualbox-ck-modules
true && pkgname=(virtualbox-ck-host-modules virtualbox-ck-guest-modules)
pkgver=4.2.4
pkgrel=10
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-ck-headers' "virtualbox-host-source>=$pkgver" "virtualbox-guest-source>=$pkgver" 'dkms')

_kernver=3.6.10-1-ck
_extraver=extramodules-3.6-ck

build() {
	# dkms need modification to be run as user
	cp -r /var/lib/dkms .
	echo "dkms_tree='$srcdir/dkms'" > dkms.conf
	# build host modules
	msg2 'Host modules'
	dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
	# build guest modules
	msg2 'Guest modules'
	dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-ck-host-modules() {
	_Hpkgdesc='Host kernel modules for VirtualBox running under Linux-ck.'
	pkgdesc="${_Hpkgdesc}"
	provides=("virtualbox-host-modules")
	depends=('linux-ck>=3.6' 'linux-ck<3.7')
	conflicts=('virtualbox-ck-host-modules-atom' 'virtualbox-ck-host-modules-barcelona' 'virtualbox-ck-host-modules-bulldozer' 'virtualbox-ck-host-modules-corex' 'virtualbox-ck-host-modules-core2' 'virtualbox-ck-host-modules-haswell' 'virtualbox-ck-host-modules-ivybridge' 'virtualbox-ck-host-modules-kx' 'virtualbox-ck-host-modules-nehalem' 'virtualbox-ck-host-modules-p4' 'virtualbox-ck-host-modules-piledriver' 'virtualbox-ck-host-modules-pentm' 'virtualbox-ck-host-modules-sandybridge')
	#replaces=('virtualbox-ck-host-modules-corex')
	#groups=('ck-generic')
	install=host.install

	install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
	cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
	install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
	sed -ie "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/host.install"
}

package_virtualbox-ck-guest-modules() {
	_Gpkgdesc='Guest kernel modules for VirtualBox running under Linux-ck.'
	pkgdesc="${_Gpkgdesc}"
	license=('GPL')
	provides=("virtualbox-guest-modules")
	depends=('linux-ck>=3.6' 'linux-ck<3.7')
	conflicts=('virtualbox-ck-guest-modules-atom' 'virtualbox-ck-guest-modules-barcelona' 'virtualbox-ck-guest-modules-bulldozer' 'virtualbox-ck-guest-modules-corex' 'virtualbox-ck-guest-modules-core2' 'virtualbox-ck-guest-modules-haswell' 'virtualbox-ck-guest-modules-ivybridge' 'virtualbox-ck-guest-modules-kx' 'virtualbox-ck-guest-modules-nehalem' 'virtualbox-ck-guest-modules-p4' 'virtualbox-ck-guest-modules-piledriver' 'virtualbox-ck-guest-modules-pentm' 'virtualbox-ck-guest-modules-sandybridge')
	#replaces=('virtualbox-ck-guest-modules-corex')
	#groups=('ck-generic')
	install=guest.install

	install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
	cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
	install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
	sed -ie "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/guest.install"
}
pkgdesc='Kernel modules for VirtualBox running under Linux-ck. No need for dkms with this package!'
