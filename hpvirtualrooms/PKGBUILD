# Maintainer: Viliam Pucik <viliam.pucik[at]gmail[dot]com>
pkgname=hpvirtualrooms
pkgver=8.0.0.4300
pkgrel=1
pkgdesc="Online meeting client with desktop sharing support"
url="https://www.rooms.hp.com/"
arch=('x86_64' 'i686')
license=('unknown')
depends=('fontconfig' 'alsa-lib' 'mesa' 'glib2' 'libxi' 'libpng12' 'libxrandr')
[ "$CARCH" = "x86_64" ] && depends=('lib32-fontconfig' 'lib32-alsa-lib' 'lib32-mesa' 'lib32-glib2' 'lib32-libxi' 'lib32-libpng12' 'lib32-libxrandr')
makedepends=('sed')
source=("https://www.rooms.hp.com/vRoom_Cab/${pkgname}-install64-F4-${pkgver}.tar.gz" "${pkgname}")
md5sums=('5b1fc3bb291c2e2faa5a6095d7c4a54b'
         '5cbe25942bc5b657da9ab32bbcfa085c')

package() {
  cd "${srcdir}"
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  cd "${srcdir}/${pkgname}-install"
  sed -i 's/\.hpvirtualrooms\///;/^Encoding/d' HP-hpvr.desktop
  echo "Icon=/usr/share/${pkgname}/resources/hpvricon.png" >> HP-hpvr.desktop
  mkdir -p "${pkgdir}/usr/share/${pkgname}/resources/"
  install -m755 ${pkgname}            "${pkgdir}/usr/share/${pkgname}/${pkgname}"
  install -m644 resources/*           "${pkgdir}/usr/share/${pkgname}/resources/"
  install -Dm755 ${pkgname}-plugin.so "${pkgdir}/usr/lib/mozilla/plugins/${pkgname}-plugin.so"
  install -Dm644 HP-hpvr.desktop      "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
