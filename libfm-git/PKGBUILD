# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_pkgname=libfm
pkgname=$_pkgname-git
pkgver=1.1.0.779.g6f9ad96
pkgrel=1
pkgdesc="A glib/gio-based library to develop file managers. Does not include the GTK components."
arch=('i686' 'x86_64')
url="http://www.lxde.org"
license=('GPL2')
depends=('menu-cache')
optdepends=('libexif')
makedepends=('git' 'libexif' 'vala' 'gtk-doc' 'intltool')
provides=($_pkgname)
conflicts=($_pkgname)
source=("git://github.com/lxde/$_pkgname.git")
sha256sums=('SKIP')
_gitname=$_pkgname

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/$_gitname"
	./autogen.sh
	./configure --without-gtk --disable-gtk-doc --disable-udisks --enable-actions --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
