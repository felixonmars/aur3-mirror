# Contributor: Miguel Aguilar <zodiac_es@yahoo.es>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=arpon
_realpkgname=ArpON
pkgver=2.7
pkgrel=3
pkgdesc="Arp handler inspectiON - a portable Arp handler"
arch=('i686' 'x86_64')
url="http://arpon.sourceforge.net/"
license=('custom')
depends=('bash' 'libdnet' 'libnet' 'libpcap')
makedepends=('cmake' 'libdnet')
backup=('etc/conf.d/arpon')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${_realpkgname}-${pkgver}.tar.gz"
        "arpon"
        "arpon.conf.d"
        "arpon.service")
md5sums=('3c3230f6852b9ffd5618a35c2f25221c'
         '86f43d0040e3947c0509de1ed0902775'
         '9b5ee4ac66f42a10efc2768bbd999a18'
         '4ad3b8e72673ae20d119205f442162c5')

build() {
  cd "${srcdir}/${_realpkgname}-${pkgver}"
  [ -d build ] || mkdir build
  cd build
  cmake -DPCAP_LIB_DIR=/usr/lib -DPCAP_LIBRARY=/usr/lib/libpcap.so ..
  make
}

package() {
  cd "${srcdir}/${_realpkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "../LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m755 "${srcdir}/arpon" "${pkgdir}/etc/rc.d/arpon"
  install -D -m644 "${srcdir}/arpon.conf.d" "${pkgdir}/etc/conf.d/arpon"
  install -d -m755  "${pkgdir}/usr/lib/systemd/system/"
  install -D -m644 "${srcdir}/arpon.service" "${pkgdir}/usr/lib/systemd/system/"
}

# vim:set ts=2 sw=2 et:
