# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>

_hkgname=file-embed
_licensefile=LICENSE

pkgname=haskell-file-embed
pkgver=0.0.4.9
pkgrel=1
pkgdesc="Use Template Haskell to read a file or all the files in a directory"
url="https://github.com/snoyberg/file-embed"
license=("MIT")
arch=('i686' 'x86_64')
makedepends=()
depends=("ghc"
    "haskell-base>=4.0"
    "haskell-bytestring>=0.9.1.4"
    "haskell-directory>=1.0.0.3"
    "haskell-filepath"
    "haskell-template-haskell")
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('62632b7d267be7f130745258949f5ba8abfabaaaa74b97671ba119dbb91c1f89')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}

    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}
