# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=syslog-ng-systemd
_pkgname=syslog-ng
pkgver=3.2.2
pkgrel=4
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'openssl')
optdepends=('logrotate: rotate system logs automatically'
            'awk: conversion script from syslog.conf to syslog-ng.conf format')
provides=('logger' 'syslog-ng')
conflicts=('syslog-ng')
options=('!libtool')
backup=('etc/syslog-ng/modules.conf' 'etc/syslog-ng/syslog-ng.conf' 'etc/logrotate.d/syslog-ng')
source=("http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/${_pkgname}_$pkgver.tar.gz"
        systemd-integration.patch
        cap_syslog.patch
        syslog-ng.conf
        syslog-ng.logrotate)
md5sums=('ed8ebe559d52a63fb61e3e2db566643f'
         '7196792f8709f03181371bcfc927dca3'
         '46e5dcff71f820d497898331a3f608fe'
         'a4547d6a711cbb54d4277aaeb8fb0c11'
         '735636090be4582885974542d2a75855')

build() {
    cd $srcdir/$_pkgname-$pkgver

    # suse's systemd integration patchwork
    patch -Np1 < "$srcdir"/systemd-integration.patch 

    # support for 2.6.38
    patch -Np1 < "$srcdir"/cap_syslog.patch

    ./configure --prefix=/usr \
                --sysconfdir=/etc/syslog-ng \
                --localstatedir=/var/lib/syslog-ng \
                --disable-tcp-wrapper \
                --with-pidfile-dir=/var/run \
                --disable-spoof-source
    make
}

package() {
    cd $srcdir/$_pkgname-$pkgver
    make DESTDIR="$pkgdir" install
    install -dm755 "$pkgdir"/var/lib/syslog-ng
    install -Dm644 "$srcdir"/syslog-ng.conf "$pkgdir"/etc/syslog-ng/syslog-ng.conf
    install -Dm644 "$srcdir"/syslog-ng.logrotate "$pkgdir"/etc/logrotate.d/syslog-ng

    # remove libexec
    rmdir "$pkgdir"/usr/libexec
}
