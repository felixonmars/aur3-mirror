# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-enable
pkgver=4.2.0
_githubtag=9088656
pkgrel=2
pkgdesc="Low-level drawing and interaction packages"
arch=('i686' 'x86_64')
url="https://github.com/enthought/enable"
license=('BSD')
depends=('python2-reportlab' 'python2-traitsui' 'mesa')
makedepends=('python2-distribute' 'cython2' 'swig')
conflicts=('python-enthought-enable' 'python2-enable-git' 'python-ets-enable-svn')
options=(!emptydirs)

source=("https://github.com/enthought/enable/tarball/${pkgver}")
md5sums=('9c79da994f4a9b57d2aee3f1fee7c3f8')

build() {
  cd "$srcdir/enthought-enable-${_githubtag}"

  python2 setup.py build
}

package() {
  cd "$srcdir/enthought-enable-${_githubtag}"

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find "${pkgdir}" -name '*.py')

  install -D "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

