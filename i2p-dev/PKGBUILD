# Maintainer: skydrome <irc.freenode.net>
# Contributor: veox <veox at wre dot ath dot cx>
# Contributor: Rorschach <r0rschach@lavabit.com>
# Contributor: Cedric Chabanois <cchabanois@gmail.com>

########[ OPTIONS ]########################################
## Clone i2p from github
__gitroot="git://github.com/i2p/i2p.i2p.git"

## Clone i2p through i2p
# Add and Start a new Standard client tunnel with the following settings:
#   Name: pull.git.repo.i2p
#   Port: 9450  or some other unused port
#   Tunnel Destination: 3so7htzxzz6h46qvjm3fbd735zl3lrblerlj2xxybhobublcv67q.b32.i2p
#__gitroot="git://127.0.0.1:9450/i2p.i2p.git"

####[ BUILD OPTIONS ]####
# set to 0 to build multilanguage translations
_lang_only_en=1
###########################################################

pkgname=i2p-dev
pkgver=0.9.1
pkgrel=3
pkgdesc="A distributed anonymous network (daily mtn->git sync)"
url="http://www.i2p2.de"
license=('GPLv2')
arch=('x86_64' 'i686')
depends=('java-runtime')
makedepends=('apache-ant' 'java-environment' 'git')
conflicts=('i2p' 'i2p-bin' 'i2p-portable' 'i2p-portable-source')
provides=('i2p')
backup=('opt/i2p/wrapper.config'
        'opt/i2p/clients.config'
        'opt/i2p/i2psnark.config'
        'opt/i2p/i2ptunnel.config')
install='i2p.install'
source=('i2prouter' 'i2prouter.service')
sha256sums=('f40c0996b4b0d83b443f1f616bc54bb18735872020e33a50b0f1fbad59c97d7d'
            'dee85e9a856b18bac257cd06e70f42e88edce3f48b92762410359a69d5c4284f')

[[ "$_lang_only_en" = 0 ]] && depends+=('gettext')

build() {
    cd "$srcdir"

    if [ -d "i2p.i2p/.git" ]; then
        msg "Cleaning repo..."
        cd "i2p.i2p"
        git clean -xdf && git pull --depth 1 origin
        msg "The local repository was updated."
    else
        msg "Connecting to GIT server..."
        git clone --depth 1 ${__gitroot}
        cd "i2p.i2p"
        msg "The remote repository was cloned."
    fi

    if [[ "$_lang_only_en" = 1 ]]; then
        sed "s:require.gettext=true:require.gettext=false:" -i build.properties
        # this hack will be fixed in a future release
        for f in ./{apps/{desktopgui,i2psnark/java,i2ptunnel/java,routerconsole/java,susidns/src,susimail},installer/resources/locale}/bundle-messages.sh; do
            sed -i $f \
                -e 's:xgettext -f:false:' \
                -e 's:msgmerge -U:false:' \
                -e 's:msgfmt .* --statistics:false:'
        done
    fi

    source /etc/profile.d/apache-ant.sh
    ant installer-linux
}

package() {
    cd "$srcdir/i2p.i2p"
    echo "INSTALL_PATH=${pkgdir}/opt/i2p" > install.properties
    java -jar i2pinstall_${pkgver}*.jar -options install.properties
    
    sed -i $pkgdir/opt/i2p/{i2prouter,wrapper.config,runplain.sh,eepget} \
        -e "s|$pkgdir/opt/i2p|/opt/i2p|g"
    sed -i $pkgdir/opt/i2p/i2prouter \
        -e "s|#RUN_AS_USER=.*|RUN_AS_USER=i2p|" \
        -e "s|I2P_CONFIG_DIR=.*|I2P_CONFIG_DIR=\"\$I2P/.i2p\"|" \
        -e "s|I2PTEMP=.*|I2PTEMP=\"\$I2P/.tmp\"|"

    install -d "${pkgdir}/opt/i2p/.tmp" "${pkgdir}/usr/share/licenses/i2p/"
    install -Dm755 ${srcdir}/i2prouter               "${pkgdir}/etc/rc.d/i2prouter"
    install -Dm644 ${srcdir}/i2prouter.service       "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1    "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/man/i2prouter.1 "${pkgdir}/usr/share/man/man1/i2prouter.1"
    cp LICENSE.txt                                   "${pkgdir}/usr/share/licenses/i2p/"
    mv ${pkgdir}/opt/i2p/licenses/*                  "${pkgdir}/usr/share/licenses/i2p/"
    rm -r $pkgdir/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,licenses,man}
    chmod -R 750 ${pkgdir}/opt/i2p
}
