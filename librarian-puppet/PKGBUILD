# Maintainer: Jochen Schalanda <jochen+aur AT schalanda.aname>
pkgname=librarian-puppet
pkgver=1.2.0
_gemname='librarian-puppet'
pkgrel=1
pkgdesc='A bundler for your Puppet infrastructure.'
arch=(any)
url='http://librarian-puppet.com/'
license=('MIT')
depends=('ruby' 'ruby-librarian>=0.1.2' 'ruby-puppet_forge')
noextract=(${_gemname}-${pkgver}.gem)
source=(http://rubygems.org/downloads/${_gemname}-${pkgver}.gem)
sha256sums=('53bbb70a2bed76f8e3847aadff1da2d00de148656f80dad07852e067b1422211')

package() {
  cd "${srcdir}"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies \
    -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  # Remove vendored gems
  rm -rf "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/vendor/gems"
  rm -rf "${pkgdir}${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim:set ts=2 sw=2 et:
