# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
pkgname=freeciv-sdl
_realname=freeciv
pkgver=2.3.0
pkgrel=1
pkgdesc="A multiuser clone of the famous Microprose game of Civilization - SDL Client"
url="http://www.freeciv.org/"
arch=('i686' 'x86_64')
depends=(bzip2 sdl sdl_image sdl_mixer readline freetype2)
options=('!libtool')
provides=("freeciv=$pkgver")
conflicts=('freeciv' 'freeciv-gtk')
license=('GPL')
install="freeciv.install"
source=("http://downloads.sourceforge.net/project/$_realname/Freeciv%20${pkgver:0:3}/$pkgver/$_realname-$pkgver.tar.bz2")

build() {
  cd $srcdir/$_realname-$pkgver || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-client=sdl --enable-shared || return 1
  make || return 1
  sed -e 's/gtk2/sdl/g' client/freeciv.desktop > client/freeciv-sdl.desktop || return 1
}

package() {
  cd $srcdir/$_realname-$pkgver || return 1
  make DESTDIR=$pkgdir install || return 1
  install -m644 client/freeciv-sdl.desktop "$pkgdir/usr/share/applications/freeciv.desktop" || return 1
}

md5sums=('70fd4285f2da7d41bdadac43458e62c9')
