# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=allegro
pkgname=lib32-${_pkgbase}
pkgver=5.0.8
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming (32 bit)'
arch=('x86_64')
url='http://alleg.sourceforge.net/'
license=('custom')
depends=('lib32-alsa-lib' 'lib32-libgl' 'lib32-physfs' 'lib32-gtk2' 'lib32-libpulse' "${_pkgbase}")
makedepends=('cmake' 'gcc-multilib' 'lib32-mesa' 'lib32-jack' 'lib32-libxxf86vm' 'lib32-libxxf86dga' 'lib32-libxpm' 'lib32-glu')
options=('!makeflags')
source=(http://downloads.sourceforge.net/alleg/${_pkgbase}-${pkgver}.tar.gz)
sha256sums=('272e77d0ca0cef7586d7207887f87bf271e1d0db5f8f208dc7dd9a5e3bb6140b')

build() {
  cd "${srcdir}"
  
  rm -rf build
  mkdir build && cd build

  cmake "../${_pkgbase}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS=OFF \
    -DLIB_SUFFIX="32" \
    -DCMAKE_C_FLAGS="$CFLAGS -m32" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -m32" \
    -DWANT_OPENAL=OFF

  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/include"

  install -d "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}

