# Maintainer: Jerome Rose <jrose.pub at gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=stackless-python
pkgver=3.2
pkgrel=3
pkgdesc="Python is a high-level scripting language. Stackless is an experimental implementation that supports continuations, generators, microthreads, and coroutines."
arch=('i686' 'x86_64')
url="http://www.stackless.com"
makedepends=('sed' 'python')
depends=('bzip2' 'expat' 'gdbm' 'libffi' 'openssl' 'zlib')
optdepents=('tk: for tkinter')
conflicts=('python>=3')
provides=('python=3.2')
license=('python')
source="http://www.stackless.com/binaries/stackless-${pkgver//./}-export.tar.bz2"
md5sums=('baabe0895b993ffa198bc18189f60dd1')

build() {
	cd "$srcdir/python-$pkgver-stackless"
	./configure \
		--prefix=/usr \
		--with-threads \
		--enable-shared

	#temp hack to fix configure SLPFLAGS
	mv Makefile Makefile.old
	sed 's/@SLPFLAGS@/-fno-omit-frame-pointer -O2 -fPIC/' <Makefile.old >Makefile

	make
}

package() {
	cd "$srcdir/python-$pkgver-stackless"
	make DESTDIR="$pkgdir/" install
	cd "$pkgdir/usr/bin"
	rm -f 2to3
	ln -s python3 python
	ln -s python3-config python-config
	ln -s pydoc3 pydoc
	ln -s idle3 idle
}
