# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
pkgname='bsnes'
# Fool AUR since it doesn't support split PKGBUILDS (shamelessly stolen from kernel26-git PKGBUILD).
(( 1 )) && pkgname=('bsnes' 'snespurify')
pkgver=084
pkgrel=1
pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt')
makedepends=('pkgconfig' 'mesa')
source=('http://bsnes.googlecode.com/files/bsnes_v084-source.tar.bz2' 'add-usr-share-fallback.patch')
md5sums=('66d8b0ef553a8cc29f31fef6a0b24982' '032e01298f3034ad621522e771fd9fa9')

# Build the accuracy profile (you can also choose 'performance' or 'compatibility')
__profile='accuracy'

build() {
  cd "${srcdir}/${pkgname}_v084-source/${pkgname}"

  # Makefile hacks:
  # Disable pulseaudio.
  sed -e 's|audio.pulseaudio ||' \
      -e 's|audio.pulseaudiosimple ||' \
      -i 'ui/Makefile'
  # Don't copy the cheat file.
  sed -e '/mkdir -p ~\/.config\/$(name)/{d}' \
      -e '/cp data\/cheats.bml/{N;d}' \
      -i 'ui/Makefile'

  # Apply patch to make bsnes look in /usr/share/bsnes for filters/shaders.
  patch -p0 < "${srcdir}/add-usr-share-fallback.patch"

  # Compile bsnes.
  make compiler=gcc platform=x profile=${__profile} phoenix=qt
  # Use the make command below if you want ti include your custom gcc flags.
  #make flags="$CXXFLAGS -I. -DPROFILE_${__profile^^}" compiler=gcc platform=x profile=${__profile} phoenix=qt

  # Compile the filters.
  cd "${srcdir}/${pkgname}_v084-source/snesfilter"
  make compiler=gcc platform=x

  # Compile snespurify.
  cd "${srcdir}/${pkgname}_v084-source/snespurify"
  sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
  ./cc-gtk.sh
}

package_snespurify() {
  pkgver=011
  pkgrel=1
  pkgdesc='Utility to clean game files so they are compatible with bsnes.'
  depends=('gtk2')

  cd "${srcdir}/bsnes_v084-source/snespurify"
  install -D --mode 755 snespurify-gtk "${pkgdir}/usr/bin/snespurify-gtk"
}

package_bsnes() {
  pkgver=084
  pkgrel=1
  pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
  changelog='bsnes.changelog'
  depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt')

  cd "${srcdir}/${pkgname}_v084-source/${pkgname}"
  make install profile=${__profile} DESTDIR="${pkgdir}" prefix=/usr

  # Install the filters/shaders
  install --directory "${pkgdir}/usr/share/${pkgname}/filters"
  install -D --mode=644 "${srcdir}"/${pkgname}_v084-source/snesfilter/out/*.filter "${pkgdir}/usr/share/${pkgname}/filters"
  install --directory "${pkgdir}/usr/share/${pkgname}/shaders"
  install -D --mode=644 "${srcdir}"/${pkgname}_v084-source/snesshader/*.shader "${pkgdir}/usr/share/${pkgname}/shaders"
}
