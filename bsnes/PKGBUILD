# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
pkgname='bsnes'
# Fool AUR since it doesn't support split PKGBUILDS (shamelessly stolen from kernel26-git PKGBUILD).
(( 1 )) && pkgname=('bsnes' 'snespurify')
pkgver=0.079
pkgrel=2
pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL2')
depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt')
makedepends=('pkgconfig' 'mesa')
source=("http://bsnes.googlecode.com/files/${pkgname}_v079-source.tar.bz2"
        "gcc_4.6.0_build_fix.patch")
md5sums=('22513f713e29824a85a0fe193298f9f2' '65e2da93a998c4e8f61023e85d332894')

# Build the accuracy profile (you can also choose 'performance' or 'compatibility')
__profile='accuracy'

build() {
  cd "${srcdir}/${pkgname}_v079-source/${pkgname}"

  # Build fixes for GCC 4.6.0 (prerelease 20110603)
  patch -p0 < "${srcdir}/gcc_4.6.0_build_fix.patch"

  # Makefile hacks
  # Disable pulseaudio.
  sed -e 's|audio.pulseaudio ||' \
      -e 's|audio.pulseaudiosimple ||' \
      -i "ui/Makefile"
  # Don't copy the cheat file.
  sed '99,101d' -i "ui/Makefile"

  # Compile bsnes
  make compiler=gcc platform=x profile=${__profile}

  # Compile snespurify
  cd "${srcdir}/${pkgname}_v079-source/snespurify"
  sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
  ./cc-gtk.sh
}

package_snespurify() {
  pkgver=0.010
  pkgrel=1
  pkgdesc='Utility to clean game files so they are compatible with bsnes.'
  depends=('gtk2')

  cd "${srcdir}/bsnes_v079-source/snespurify"
  install -D --mode 755 snespurify-gtk "${pkgdir}/usr/bin/snespurify-gtk"
}

package_bsnes() {
  pkgver=0.079
  pkgrel=2
  pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
  depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt')

  cd "${srcdir}/${pkgname}_v079-source/${pkgname}"
  make install profile=${__profile} DESTDIR=${pkgdir} prefix=/usr
}
