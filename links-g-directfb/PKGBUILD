# Maintainer: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: sqwishy <sqwishybon@gmail.com>
# Contributor: Duologic

pkgname=links-g-directfb
_pkgname=links
pkgver=2.7
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx, with framebuffer and graphics enabled (also with directfb)"
arch=('i686' 'x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'xz' 'zlib' 'openssl' 'gpm' 'libtiff' 'libpng' 'directfb')
conflicts=('links')
provides=('links')
source=("http://links.twibright.com/download/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('1e362a7e27078d630edf5868145a46247cda8cc3')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  (cd intl; ./gen-intl; ./synclang)
  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --enable-javascript \
      --enable-graphics \
      --with-ssl \
      --with-fb \
      --with-directfb
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
