# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Maintainer: TheBenj88 <thebenj88 *AT* gmail *DOT* com>

pkgname=openxcom
pkgver=0.4.5
pkgrel=1
pkgdesc="An open-source reimplementation of the famous X-COM game"
arch=('i686' 'x86_64')
url="http://openxcom.org/"
license=('GPL3')
depends=('sdl' 'gcc-libs' 'sdl_gfx' 'sdl_mixer' 'timidity++' 'yaml-cpp')
install="${pkgname}.install"
source=("https://github.com/SupSuper/OpenXcom/tarball/v${pkgver}"
		"${pkgname}.install")
sha256sums=('8a165b7db8556dd18b53bac7c2f8447efeebb51af3a4b69f26f8c8f499e64a98'
            '102150a0124ffe1fe1ab11f05d2e7a8c8e20decba9cef78fa831fc2429cc8e46')

build() {
  cd "${srcdir}/SupSuper-OpenXcom-ee4d466/src"
  mv Makefile.simple Makefile
  # Fix a problem with yaml-cpp and pkg-config
  sed -i "s_--cflags sdl yaml-cpp\`_--cflags sdl\` -I/usr/include/yaml-cpp_" Makefile
  # Fix a compilation error
  sed -i "s_?=_+=_" Makefile

  make || return 1
}

package() {
  cd "${srcdir}/SupSuper-OpenXcom-ee4d466"

  # Install binary
  install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install data files
  mkdir -p "${pkgdir}/usr/share/${pkgname}/"{Language,SoldierName}
  install -m644 bin/data/Language/* "${pkgdir}/usr/share/${pkgname}/Language"
  install -m644 bin/data/SoldierName/* "${pkgdir}/usr/share/${pkgname}/SoldierName"
  install -m644 bin/data/README.txt "${pkgdir}/usr/share/${pkgname}"

  # Install readme
  install -Dm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
}
