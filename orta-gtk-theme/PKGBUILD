# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: twa022 <twa022@gmail.com>
# Contributor: Adria Arrufat <swiftscythe@gmail.com>

pkgname=orta-gtk-theme
pkgver=1.4.1
pkgrel=7
pkgdesc="Orta GTK+, Metacity, Xfwm4 and more themes"
arch=(any)
url="http://skiesofazel.deviantart.com/art/Orta-184118297"
license=(GPL)
depends=(gtk-engines gksu)
install=orta.install
DLAGENTS=('http::/usr/bin/wget -c -t 3 --waitretry=3 -H -U Mozilla -O %o %u')
source=(
    'http://www.deviantart.com/download/184118297/orta_by_skiesofazel-d31mal5.zip'
    'orta.desktop'
    'orta-install-chrome-themes')
sha256sums=(
    'f2c0709475ccaf057c0c3252e88b0c57e45735eb557014d2e729fc7aaa85f619'
    '0c048e234eb737433d8fd68e66ded5946f9c2de5ea5d298e4d7a9e96705612d0'
    'efcb60392fb10d7b9e37a2f9110a7c428c03c3b474ceb0901929d9ba8eece1e9')
sha512sums=(
    '2f5ea88c399acc56b0130118c7cb87be92cb6311979335578abb7fe6dd87845f51f280b1264a097a75dfb46837450e5072045df76b8201af753564521147fcbb'
    'f8ac44b9072de3d5f08fee53c2bda129ba35b2302fe2d2411d9908656bb3aaea5b53e2361e6d17b5e947bab80a5a7c0f4b17b4a1d41b7343d73b7ed48a5b493f'
    '61583aa3b15cd0394873bb131a74d8849e1bd247106e40a8966ac56459780bb599e6a895be0e537f25e9279dcb781b4566a116ed262ba9d8b2b3ea5ecf46f61f')

build() {
    cd $srcdir
    tar xf extractme.tar.gz
}

package() {
    ## AWN
    install -d $pkgdir/usr/share/avant-window-navigator/themes/
    tar xf Awn/OrtaAWN-0.2.0.tgz -C $pkgdir/usr/share/avant-window-navigator/themes/

    ## Emerald
    cd Emerald/
    for i in *.emerald; do 
	_folder=$pkgdir/usr/share/emerald/themes/$(echo $i | sed 's:\.emerald$::')
	install -d $_folder
	tar xf $i -C $_folder
    done
    cd ../

    ## Chromium
    find Chromium/ -name '*.crx' -execdir install -Dm644 '{}' $pkgdir/usr/share/themes/orta-chromium-themes/'{}' \;
    install -Dm755 orta-install-chrome-themes $pkgdir/usr/bin/orta-install-chrome-themes

    ## Manager
    sed -i 's:#!/usr/bin/python:&2:' OrtaSettingsManager.py
    install -Dm755 OrtaSettingsManager.py $pkgdir/usr/lib/orta/OrtaSettingsManager.py
    cp -r orta/ $pkgdir/usr/lib/orta/
    file_perms $pkgdir/usr/lib/
    ln -s /usr/lib/orta/OrtaSettingsManager.py $pkgdir/usr/bin/orta-settings-manager

    ## Themes
    install -d $pkgdir/usr/share/themes/
    for i in Orta{,-Old,-Old-Squared,-Squared}.tar.gz; do
	tar xf $i -C $pkgdir/usr/share/themes/
    done
    file_perms $pkgdir/usr/share/themes

    ## Desktop file (shortcut)
    install -d $pkgdir/usr/share/applications/
    desktop-file-install orta.desktop --dir=$pkgdir/usr/share/applications

    ## Removing empty directories
    rmdir $pkgdir/usr/share/themes/Orta/gtk-2.0/Styles/{Inkscape,Rhythmbox}

    ## Fixing incorrect file ownerships
    chown -R root:root $pkgdir/usr/
}


file_perms() {
    find $1 -type d -exec chmod 755 '{}' \;
    find $1 -type f -exec chmod 644 '{}' \;
}
