# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Stefan Karner <stefan.karner at student.tuwien.ac.at>

pkgname=openxcom-git
_gitname=OpenXcom
pkgver=0.9.2078.g342b7d6
pkgrel=1
pkgdesc="An open-source reimplementation of the famous X-COM game (git-version)"
arch=('i686' 'x86_64')
url="http://openxcom.org/"
license=('GPL3')
depends=('sdl' 'sdl_gfx' 'sdl_mixer' 'sdl_image' 'yaml-cpp' 'mesa')
makedepends=('git' 'boost' 'glu')
install="${pkgname}.install"
source=(git+"https://github.com/SupSuper/${_gitname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --tags | sed -e 's:v::' -e 's:-:.:g'
}

build() {
  cd "$srcdir/$_gitname"
  ./autogen.sh
  ./configure --with-man=no --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}
