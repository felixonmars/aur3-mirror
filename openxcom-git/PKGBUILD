# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Stefan Karner <stefan.karner at student.tuwien.ac.at>

pkgname=openxcom-git
_gitname=OpenXcom
pkgver=1.0_7_gab4fad6
pkgrel=2
pkgdesc="An open-source reimplementation of the famous X-COM game (git-version)"
arch=('i686' 'x86_64')
url="http://openxcom.org/"
license=('GPL3')
depends=('libdrm' 'libgl' 'libx11' 'libxau' 'libxcb' 'libxdamage'
         'libxdmcp' 'libxext' 'libxfixes' 'libxshmfence' 'libxxf86vm'
         'mesa' 'sdl' 'sdl_gfx' 'sdl_image' 'sdl_mixer' 'yaml-cpp')
makedepends=('git' 'boost' 'glu' 'xmlto' 'docbook-xml' 'docbook-xsl')
provides=('openxcom')
conflicts=('openxcom')
install="${pkgname}.install"
source=(git+"https://github.com/SupSuper/${_gitname}.git")
sha256sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --tags | sed -e 's:v::' -e 's:-:_:g'
}

prepare() {
  cd $_gitname
  ./autogen.sh
}

build() {
  cd $_gitname
  ./configure --prefix=/usr
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
}
