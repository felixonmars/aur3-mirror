# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-wallpapers
_actual_ver=13.04
_extra_ver=daily12.12.13.1
_ubuntu_rel=0ubuntu1
# AUR is broken once again. How is ${_actual_ver}.${_extra_ver} greater than
# 32 characters?
pkgver=13.04
pkgrel=1
pkgdesc="The default wallpapers for Ubuntu (from 9.10 Karmic Koala to 13.04 Raring Ringtail)"
arch=('any')
url="https://launchpad.net/ubuntu-wallpapers"
license=('GPL')
groups=('unity-extra')
makedepends=('libxslt' 'python2-distutils-extra')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('c47f9b5fee5a98d36f76e3c5620f39f1bfe2958487a1a33f181a4f4c585ab24731b2712137a78ecd08530b1f8a158691d77dc3db045c20238b5c7d36a03821ee'
            '4a70a26db145533318289d81007be24c958cbd7ab7d0e17b22c595cebe925d9a9d63ae1dc8bc7afca05c026a9d917a6d25a73e2572a5911f05c46622100d6462')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # The setup.py script reads the package version from debian/*
  patch -p1 -i "${srcdir}/${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

# vim:set ts=2 sw=2 et:
