# Maintainer: artoo <flower_of_life@gmx.net>

_pkgname=ConsoleKit2

pkgname=consolekit
pkgver=0.9.2
pkgrel=2
pkgdesc="A framework for defining and tracking users, login sessions, and seats"
arch=('i686' 'x86_64')
url="https://github.com/ConsoleKit2/ConsoleKit2"
license=('GPL')
provides=('logind')
depends=('dbus' 'dbus-glib' 'libx11' 'polkit' 'udev' 'zlib')
optdepends=('consolekit-openrc: consolekit openrc initscript')
makedepends=('xmlto' 'docbook-xsl')
options=(libtool)
source=("$url/releases/download/$pkgver/$_pkgname-$pkgver.tar.bz2"
	'consolekit.tmpfiles.conf'
	'75-consolekit.rules'
	'consolekit.logrotate')


build(){
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure  \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/ConsoleKit \
    --localstatedir=/var \
    --enable-polkit \
    --enable-pam-module \
    --enable-udev-acl \
    --enable-docbook-docs \
    --with-dbus-services=/usr/share/dbus-1/services \
    --with-logrotate-dir=/etc/logrotate.d \
    --with-xinitrc-dir=/etc/X11/xinit/xinitrc.d \
    --with-pam-module-dir=/usr/lib/security \
    --without-systemdsystemunitdir

    make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  #install -D -m644 data/consolekit.logrotate "$pkgdir"/etc/logrotate.d/consolekit
  install -D -m644 "$srcdir"/consolekit.logrotate "$pkgdir"/etc/logrotate.d/consolekit
  install -D -m644 data/90-consolekit "$pkgdir"/etc/X11/xinit/xinitrc.d/90-consolekit

  install -D -m644 "$srcdir"/consolekit.tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/consolekit.conf

  rm -rf "${pkgdir}"/var/run

  install -d -m700 $pkgdir/etc/polkit-1/rules.d/
  install -D -m644 ${srcdir}/75-consolekit.rules $pkgdir/etc/polkit-1/rules.d/
  chown 102 $pkgdir/etc/polkit-1/rules.d
}
sha256sums=('102aa07e0960e56740e235bba06a546c8e0e88f47546ef252ecda917935af41f'
            '778552dc12b3c235bde200e476d4262da0c135f3f6f8b3e975a87881d1f154d1'
            'dc65bd6a02da46b38595076158ab21e7640a95c525ba81993d9488ca5bdfbe7f'
            '79735ddd37b8ce6216c78542c2a4e32692dce757c86ecd5771ea8a895a1e8e4d')
