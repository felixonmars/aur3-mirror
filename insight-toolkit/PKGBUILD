# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: joel schaerer <joel.schaerer@laposte.net>

pkgname=insight-toolkit
pkgver=3.20.0
pkgrel=3
pkgdesc='Insight Segmentation and Registration Toolkit (ITK).'
arch=('i686' 'x86_64')
url='http://www.itk.org/'
license=('BSD')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/project/itk/itk/${pkgver:0:4}/InsightToolkit-${pkgver}.tar.gz")
md5sums=('5d6b8e6e641624c6a8c06c7f95f05c9e')

build() {
  cd InsightToolkit-${pkgver}

  sed \
    -e '1i#include <cstddef>' \
    -i Utilities/MetaIO/metaUtils.cxx Code/IO/itkImageIORegion.h

  cmake \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DBUILD_TESTING:BOOL=OFF \
    -DBUILD_EXAMPLES:BOOL=OFF \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DITK_USE_REVIEW:BOOL=ON

  make
}

package() {
  cd InsightToolkit-${pkgver}

  make DESTDIR=${pkgdir} install

  # install BSD license
  install -d ${pkgdir}/usr/share/licenses/insight-toolkit
  install -m 644 Copyright.txt \
    ${pkgdir}/usr/share/licenses/insight-toolkit
}
