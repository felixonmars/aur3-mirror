# Maintainer: skydrome <irc.freenode.net>
# -*- coding: utf-8 -*-

#######################
#[ 3rd Party Plugins ]#
_pausewebui=1
_ratiocolor=1
#_instantsearch=1
#_logoff=1
#_filemanager=1
#_fileshare=1
#_fileupload=1
#_mediastream=1
#_chat=1
#_titlebar=1
#######################

pkgname=rutorrent-svn
true && pkgname=('rutorrent-svn' 'rutorrent-plugins-svn')
pkgver=2100
pkgrel=1
arch=('any')
url="http://code.google.com/p/rutorrent"
license=('GPL')
makedepends=('subversion')
source=('plugins.ini')
md5sums=('67d1cde5183196085fd311f3d7aa650d')
options=(!strip !emptydirs)

_svntrunk="http://rutorrent.googlecode.com/svn/trunk"
_webdir="usr/share/webapps"

build() {
    cd "$srcdir"

    msg "Connecting to SVN server..."
    [[ -d "trunk/.svn" ]] && svn up trunk || svn co "$_svntrunk"

    cd trunk
    sed -i rutorrent/conf/config.php \
		-e "s:\$topDirectory .*:\$topDirectory = '/home';:" \
		-e "s:\$XMLRPCMountPoint .*:\$XMLRPCMountPoint = \"/rutorrent/RPC1\";:"

    # Uncomment to use a nicer dark theme
    #sed -i "s:\$defaultTheme .*:\$defaultTheme = \"Oblivion\";:" plugins/theme/conf.php
    
    cd plugins
    [[ $_pausewebui ]] &&
        svn co "https://rutorrent-pausewebui.googlecode.com/svn/trunk"      pausewebui
    [[ $_logoff ]] &&
        svn co "https://rutorrent-logoff.googlecode.com/svn/trunk"          logoff
    [[ $_instantsearch ]] &&
        svn co "https://rutorrent-instantsearch.googlecode.com/svn/trunk"   instantsearch
    [[ $_filemanager ]] &&
        svn co "http://svn.rutorrent.org/svn/filemanager/trunk/filemanager" filemanager
    [[ $_fileshare ]] &&
        svn co "http://svn.rutorrent.org/svn/filemanager/trunk/fileshare"   fileshare
    [[ $_fileupload ]] &&
        svn co "http://svn.rutorrent.org/svn/filemanager/trunk/fileupload"  fileupload
    [[ $_mediastream ]] &&
        svn co "http://svn.rutorrent.org/svn/filemanager/trunk/mediastream" mediastream
    [[ $_chat ]] &&
        svn co "https://rutorrent-chat.googlecode.com/svn/trunk"            chat
    [[ $_ratiocolor ]] &&
        [[ ! -d "ratiocolor" ]] && git clone "git://github.com/Gyran/rutorrent-ratiocolor.git" ratiocolor
    [[ $_titlebar ]] &&
        [[ ! -d "titlebar" ]] && git clone "git://github.com/SanKen/rutorrent-titlebar.git"    titlebar

    wget -q https://raw.github.com/weixiyen/jquery-filedrop/master/jquery.filedrop.js -O filedrop/jquery.filedrop.js
}

package_rutorrent-svn() {
    depends=('rtorrent' 'php>=5.3.0')
    provides=('rutorrent')
    conflicts=('rutorrent')
    install=rutorrent.install
    backup=("${_webdir}/rutorrent/conf/config.php"
            "${_webdir}/rutorrent/conf/access.ini")

    cd "$srcdir/trunk"
    install -dm755  "$pkgdir/$_webdir"
    cp -r rutorrent "$pkgdir/$_webdir"
    rm -r $pkgdir/$_webdir/rutorrent/{plugins,conf/plugins.ini}
}

package_rutorrent-plugins-svn() {
    pkgdesc="Plugins for ruTorrent"
    depends=('rutorrent-svn' 'curl')
    optdepends=('mktorrent: create torrent files with mktorrent'
                'buildtorrent: create torrent files with buildtorrent'
                'transmission-cli: create torrent files with transmission'
                'php-geoip: enable geoip plugin'
                'geoip: enable geoip plugin'
                'unrar: enable unpack plugin'
                'unzip: enable unpack plugin')
    install=rutorrent-plugins.install
    conflicts=('rutorrent-plugins')
    provides=('rutorrent-plugins')
    replaces=('rutorrent-plugins-svn')
    backup=("${_webdir}/rutorrent/conf/plugins.ini")
    
    cd "$srcdir/trunk"
    install -dm755 "$pkgdir/$_webdir/rutorrent/conf"
    install -Dm755 "$srcdir/plugins.ini" "$pkgdir/$_webdir/rutorrent/conf/plugins.ini"
    cp -r plugins  "$pkgdir/$_webdir/rutorrent"
}
pkgdesc="Web frontend to rTorrent in PHP designed to resemble ÂµTorrent"
