# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: lh <jarryson#gmail.com>

pkgname=plymouth-git
pkgdesc="A Linux boot screen implementation"
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
arch=('i686' 'x86_64')
pkgver=20100621
pkgrel=1
depends=('initscripts' 'libdrm' 'gtk2')
makedepends=('git')
options=('!libtool' '!emptydirs')
install=plymouth.install
provides=('plymouth')
conflicts=('plymouth')
source=(plymouth.functions
	plymouth.initcpio_hook
	plymouth.initcpio_install
	plymouth-encrypt.initcpio_hook
	plymouth-encrypt.initcpio_install
	plymouth-update-initrd.patch
	arch-logo.png
        system-release)
md5sums=('916fb026be911d4cf9c51d44bfee0791'
         '86c85dcf88dfdb6b895f72bdca1babd1'
         '3f7e1a50984788082e60a82931e1a21a'
         'f9418c98f84f948ccc860c08cf4177ed'
         'f218e7e4c6b27c5c2135a3de076d8b1a'
         '6b6312a2f12c2b182e24ee91b8590f70'
         '65fa2763d5c9bb9f80973ea5e6e3db3e'
         'ab8a557d10f74bec5c94ed6cae34729c')


_gitroot='git://git.freedesktop.org/git/plymouth'
_gitname='plymouth'
_gitbranch='master'
         
build() {
	cd "$srcdir"
	
	msg "Connecting to git.freedesktop.org GIT server...."

	if [ -d ${srcdir}/${_gitname} ] ; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot $_gitname
	fi
	msg "GIT checkout done or server timeout. Preparing sources..."

	rm -rf ${srcdir}/${_gitname}-build
	cp -r ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build

	msg "Starting make..."
	cd ${srcdir}/${_gitname}-build
	
	sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' -i src/libply-splash-graphics/ply-image.c
	
	./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
		--enable-tracing \
		--with-system-root-install \
		--disable-tests \
		--enable-gdm-transition \
		--without-rhgb-compat-link \
		--with-gdm-autostart-file=no \
		--with-logo=/usr/share/plymouth/arch-logo.png \
		--with-background-start-color-stop=0x000000 \
		--with-background-end-color-stop=0x4D4D4D
	
	make || return 1
	make DESTDIR="$pkgdir" install || return 1
	
	cd "$pkgdir"
	
	patch -Np0 -i "$srcdir/plymouth-update-initrd.patch" || return 1

	#rm -rf `find . -name "*.a"`
	rm -rf `find . -name "*.orig"`

	install -D -m644 $srcdir/plymouth.initcpio_install ${pkgdir}/lib/initcpio/install/plymouth
	install -D -m644 $srcdir/plymouth.initcpio_hook ${pkgdir}/lib/initcpio/hooks/plymouth
	install -D -m644 $srcdir/plymouth-encrypt.initcpio_install ${pkgdir}/lib/initcpio/install/plymouth-encrypt
	install -D -m644 $srcdir/plymouth-encrypt.initcpio_hook ${pkgdir}/lib/initcpio/hooks/plymouth-encrypt
	install -D -m644 $srcdir/plymouth.functions ${pkgdir}/etc/rc.d/functions.d/plymouth.functions
	install -D -m644 "$srcdir/arch-logo.png" ${pkgdir}/usr/share/plymouth/arch-logo.png
	install -D -m644 "$srcdir/system-release" ${pkgdir}/etc/system-release
}

