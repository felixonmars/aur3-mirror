# Maintainer: -
# Contributors: Zephyr, Det, Christian Autermann, Biginoz, Martin Lee, Ricardo Funke, PirateJonno, lh, Cilyan Olowen, Shaffer, Brcha, Lyle Putnam

pkgname=plymouth-git
pkgver=20120715
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support (Git version)"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('systemd' 'libdrm' 'pango')
makedepends=('git')
options=('!libtool' '!emptydirs')
install=$pkgname.install
provides=('plymouth')
conflicts=('plymouth')
backup=('etc/plymouth/plymouthd.conf')
source=('arch-logo.png'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-$CARCH/encrypt_hook"
        'encrypt_hook.patch'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-$CARCH/encrypt_install"
        'encrypt_install.patch'
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'lxdm-plymouth.service'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-set-default-theme.in.patch'
        'plymouth-start.service.in.patch'
        'plymouth-update-initrd.patch'
        'plymouthd.conf'
        'systemd-ask-password-plymouth.path.in.patch'
        'systemd-ask-password-plymouth.service.in.patch'
        'system-release'
        'systemd-unit-dir.patch')
sha1sums=('3bc168dd06ff3e2790f26c131209a7fb4a5ccbd7'  # arch-logo.png
          'f39177bcfe423d03b362888025d48903f6dea75c'  # encrypt_hook
          '7443934006f3f45a6eac163f9f14bce611565d5b'  # encrypt_hook.patch
          '26fb380d56e64cc777ac32e2c124c41ac0769ff5'  # encrypt_install
          '705f818a342c97681abdfb05e2e5c63fbf8743da'  # encrypt_install.patch
          '92b2ea373991af921d8e2c8a8d06c31b4f9e7313'  # gdm-plymouth.service
          '44c03d1cc71f7b9fa6ef7022584f0f81c9c17faf'  # kdm-plymouth.service
          '2f4bab61cbd32d3cca3157de35098f813f485fef'  # kdm-unpatched-plymouth.service
          '31eb516da0e78d9c185dfb9ee5b94468f713ff1c'  # lxdm-plymouth.service
          '9b689741e7358ecb8b354d8f0df9d11cc4205110'  # plymouth.functions
          '0a9549e38e1ca4fef12b3d81460b112dda6cd5e7'  # plymouth.initcpio_hook
          'c2e567178401ad9787f6c6a792e8816d63416148'  # plymouth.initcpio_install
          'be3c17b74c5bb9243a9aeba0b70b1451563cc1a2'  # plymouth-set-default-theme.in.patch
          '31a1b489125b474cacf25f279a39107e8fb8e480'  # plymouth-start.service.in.patch
          '019663ae0070163e8d6cf268e7efeaaef474e725'  # plymouth-update-initrd.patch
          '01235a179d8f991a37035c15cdcc0733b581afd4'  # plymouthd.conf
          'db41300b151d1774ec34168e94fb1a20cc1d4ba7'  # systemd-ask-password-plymouth.path.in.patch
          'cd684055ad4f726b743c1c8fd55576c9faba8ee3'  # systemd-ask-password-plymouth.service.in.patch
          '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c'  # system-release
          '9e8ad4d68588a7445e764740c1dcdfe81a482ca5') # systemd-unit-dir.patch

_gitroot='git://git.freedesktop.org/git/plymouth'
_gitname='plymouth'

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 $_gitroot $_gitname
  fi
  msg "GIT checkout done. Preparing sources..."

  cd "$srcdir"
  rm -rf $_gitname-build
  cp -r $_gitname $_gitname-build

  msg "Applying Patches..."

  msg2 "Fixing encrypt HOOK script for Plymouth"
  patch -p0 -i encrypt_hook.patch

  msg2 "Adding necessary files for encrypt HOOK"
  patch -p0 -i encrypt_install.patch

  cd $_gitname-build
  msg2 "Setting a human-readable error for '# plymouth-set-default-theme -R'"
  patch -p0 -i "$srcdir/plymouth-set-default-theme.in.patch"

  msg2 "Fixing updating initrd for Arch"
  patch -p0 -i "$srcdir/plymouth-update-initrd.patch"

  msg2 "Changing PID file to /run/plymouth.pid"
  patch -p0 -i "$srcdir/plymouth-start.service.in.patch"
  patch -p0 -i "$srcdir/systemd-ask-password-plymouth.path.in.patch"
  patch -p0 -i "$srcdir/systemd-ask-password-plymouth.service.in.patch"

  msg2 "Changing SYSTEMD_UNIT_DIR to /usr/lib/systemd/system/"
  patch -p0 -i "$srcdir/systemd-unit-dir.patch"

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  msg "Starting make..."

  ./autogen.sh --prefix=/usr --exec-prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib --libexecdir=/usr/lib \
    --enable-systemd-integration \
    --enable-tracing \
    --with-gdm-autostart-file=yes \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D \
    --without-rhgb-compat-link \
    --without-system-root-install

  make
}

package() {
  cd $_gitname-build

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/arch-logo.png" "$pkgdir/usr/share/plymouth/arch-logo.png"
  install -Dm644 "$srcdir/encrypt_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "$srcdir/encrypt_install" "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "$srcdir/plymouth.functions" "$pkgdir/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "$srcdir/plymouth.initcpio_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
  install -Dm644 "$srcdir/plymouthd.conf" "$pkgdir/etc/plymouth/plymouthd.conf"
  install -Dm644 "$srcdir/system-release" "$pkgdir/etc/system-release"

  # Plymouth<->systemd integration, units for DMs
  install -Dm644 "$srcdir/gdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/gdm-plymouth.service"
  install -Dm644 "$srcdir/kdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/kdm-plymouth.service"
  install -Dm644 "$srcdir/kdm-unpatched-plymouth.service" "$pkgdir/usr/lib/systemd/system/kdm-unpatched-plymouth.service"
  install -Dm644 "$srcdir/lxdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/lxdm-plymouth.service"
}

# vim: set ts=2 sw=2 ft=sh noet: