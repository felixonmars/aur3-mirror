# Maintainer: Det
# Contributors: Abbradar, Zephyr, Christian Autermann, Biginoz, Martin Lee, Ricardo Funke, PirateJonno, lh, Cilyan Olowen, Shaffer, Brcha, Lyle Putnam

pkgname=plymouth-git
pkgver=20120725
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support (Git version)"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/cgit/plymouth/"
license=('GPL')
depends=('libdrm' 'pango' 'systemd')
makedepends=('git')
options=('!libtool' '!emptydirs')
install=$pkgname.install
provides=('plymouth')
conflicts=('plymouth')
backup=('etc/plymouth/plymouthd.conf')
source=('arch-logo.png'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-i686/encrypt_hook"
        'encrypt_hook.patch'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-i686/encrypt_install"
        'encrypt_install.patch'
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'lxdm-plymouth.service'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-pid.patch'
        'plymouth-quit.service'
        'plymouth-set-default-theme.in.patch'
        'plymouth-update-initrd.patch'
        'plymouthd.conf'
        'system-release'
        'systemd-unit-dir.patch')
sha1sums=('3bc168dd06ff3e2790f26c131209a7fb4a5ccbd7'  # arch-logo.png
          'f39177bcfe423d03b362888025d48903f6dea75c'  # encrypt_hook
          '7443934006f3f45a6eac163f9f14bce611565d5b'  # encrypt_hook.patch
          '26fb380d56e64cc777ac32e2c124c41ac0769ff5'  # encrypt_install
          '705f818a342c97681abdfb05e2e5c63fbf8743da'  # encrypt_install.patch
          'cc11bfc475a3f169c3edb2c6b21da4adc4f852db'  # gdm-plymouth.service
          'f09c63dfc49c57d89405602b7610f6b0375824ed'  # kdm-plymouth.service
          'd4984db7740c4926139ee575cefbac1a32a38231'  # kdm-unpatched-plymouth.service
          'ca252b549cc2f64c2f5146d9388901503906d01a'  # lxdm-plymouth.service
          '9b689741e7358ecb8b354d8f0df9d11cc4205110'  # plymouth.functions
          'e98c5812891b319f15a4785d0accda4cccc33f56'  # plymouth.initcpio_hook
          'c2e567178401ad9787f6c6a792e8816d63416148'  # plymouth.initcpio_install
          '14fa75d0bcd622c1c5ffc6142442ad582c9fb24b'  # plymouth-pid.patch
          'd57015f8a31aef40c9ad3086c3542768170b79cd'  # plymouth-quit.service
          'be3c17b74c5bb9243a9aeba0b70b1451563cc1a2'  # plymouth-set-default-theme.in.patch
          '019663ae0070163e8d6cf268e7efeaaef474e725'  # plymouth-update-initrd.patch
          '01235a179d8f991a37035c15cdcc0733b581afd4'  # plymouthd.conf
          '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c'  # system-release
          '9e8ad4d68588a7445e764740c1dcdfe81a482ca5') # systemd-unit-dir.patch

_gitroot='git://anongit.freedesktop.org/plymouth'
_gitname='plymouth'

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $_gitname ]; then
    cd $_gitname && time git pull --depth 1 origin
    msg "The local files are updated."
  else
    git clone --depth 1 $_gitroot
  fi
  msg "GIT checkout done. Preparing sources..."

  cd "$srcdir"
  rm -rf $_gitname-build
  cp -r $_gitname $_gitname-build

  msg "Applying Patches..."

  msg2 "Fixing encrypt HOOK"
  patch -p0 -i encrypt_hook.patch
  patch -p0 -i encrypt_install.patch

  cd $_gitname-build
  msg2 "Setting a human-readable error for '# plymouth-set-default-theme -R'"
  patch -p0 -i "$srcdir/plymouth-set-default-theme.in.patch"

  msg2 "Fixing updating initrd"
  patch -p0 -i "$srcdir/plymouth-update-initrd.patch"

  msg2 "Changing PID file to /run/plymouth.pid"
  patch -p0 -i "$srcdir/plymouth-pid.patch"

  msg2 "Changing SYSTEMD_UNIT_DIR to /usr/lib/systemd/system/"
  patch -p0 -i "$srcdir/systemd-unit-dir.patch"

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  msg "Starting make..."

  ./autogen.sh --prefix=/usr --exec-prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib --libexecdir=/usr/lib \
    --enable-systemd-integration \
    --enable-tracing \
    --with-gdm-autostart-file=yes \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D \
    --without-rhgb-compat-link \
    --without-system-root-install

  make
}

package() {
  cd $_gitname-build

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/arch-logo.png" "$pkgdir/usr/share/plymouth/arch-logo.png"
  install -Dm644 "$srcdir/encrypt_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "$srcdir/encrypt_install" "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "$srcdir/plymouth.functions" "$pkgdir/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "$srcdir/plymouth.initcpio_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
  install -Dm644 "$srcdir/plymouthd.conf" "$pkgdir/etc/plymouth/plymouthd.conf"
  install -Dm644 "$srcdir/system-release" "$pkgdir/etc/system-release"

  # Plymouth<->systemd integration, including units for DMs
  for i in {{gdm,kdm{,-unpatched},lxdm}-plymouth,plymouth-quit}.service; do
    install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
  done
}

# vim: set ts=2 sw=2 ft=sh noet: