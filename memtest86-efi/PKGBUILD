# Maintainer: X0rg

_pkgbasename=memtest86
pkgname=$_pkgbasename-efi
pkgver=5b7
pkgrel=4
pkgdesc="A free, thorough, stand alone memory test as an EFI application."
arch=('i686' 'x86_64')
url="http://www.memtest86.com"
license=('GPL2')
makedepends=('p7zip')
install=$pkgname.install
source=("$_pkgbasename-$pkgver".iso.gz::"$url"/downloads/beta/"$_pkgbasename"V"$pkgver"-iso.gz
	"$pkgname"
	"$pkgname.conf")
md5sums=('2b35eb35ff27d5e24ef74efb237ec663'
         'd4e15fe577ff95bc3317212cb719bdde'
         '488ef0f904318a7fce4b7a292597019b')

prepare() {
	7z x -y $_pkgbasename-$pkgver.iso

	cd "$srcdir/EFI/BOOT"
	for file in *; do
		newfile=$(echo ${file##*/} | tr 'A-Z' 'a-z');
		mv -T "$file" "$newfile";
	done

	rm thumbs.db
	[[ "${CARCH}" == "i686" ]]   && rm bootx64.efi
	[[ "${CARCH}" == "x86_64" ]] && rm bootia32.efi
}

package() {
	install -dv "$pkgdir/usr/share/$pkgname"
	cp -v "$srcdir/EFI/BOOT/"* "$pkgdir/usr/share/$pkgname"
	install -dv "$pkgdir/usr/bin"
	install -Dvm755 "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
	install -dv "$pkgdir/etc"
	install -Dvm644 "$srcdir/$pkgname.conf" "$pkgdir/etc/$pkgname.conf"
}
