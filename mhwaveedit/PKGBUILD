# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Loui Chang <louipc.ist@gmail.com>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=mhwaveedit
pkgver=1.4.22
pkgrel=1
pkgdesc="A graphical program for editing, playing and recording sound files"
arch=('i686' 'x86_64')
url="http://gna.org/projects/mhwaveedit"
license=('GPL')
depends=('gtk2' 'jack' 'libpulse' 'desktop-file-utils')
makedepends=('ladspa')
optdepends=('ladspa: ladspa effects')
install="$pkgname.install"
source=("http://download.gna.org/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('920108844abfdc2613cc1d3e188a7833')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --without-sdl \
              --without-esound
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
