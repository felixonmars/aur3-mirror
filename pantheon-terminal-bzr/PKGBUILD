# Maintainer: Ner0

pkgname=pantheon-terminal-bzr
pkgver=309
pkgrel=1
pkgdesc="A super lightweight, beautiful, and simple terminal from the Elementary project"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-terminal"
license=('GPL3')
depends=('gtk3' 'glib2' 'vte3' 'granite-bzr' 'gconf' 'dconf' 'libnotify' 'clutter-gtk')
makedepends=('bzr' 'cmake' 'vala' 'pkg-config')
conflicts=('pantheon-terminal')
provides=('pantheon-terminal')
install=$pkgname.install

_bzrtrunk=lp:pantheon-terminal
_bzrmod=pantheon-terminal

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  sed -i 's/follow_last_tabs/follow_last_tab/' src/Settings.vala

  [ -d build ] && rm -rf build
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DGSETTINGS_LOCALINSTALL=OFF
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
