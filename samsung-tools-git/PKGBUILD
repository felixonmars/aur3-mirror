# Maintainer: Fortunato Ventre (voRia) <vorione AT gmail DOT com>

pkgname=samsung-tools-git
_gitname=samsung-tools
pkgver=20130718
pkgrel=1
pkgdesc="Tools for Samsung laptops - Development version"
arch=('any')
url="https://launchpad.net/samsung-tools"
license=('GPL3')
depends=('pm-utils' 'xbindkeys' 'rfkill' 'python2-dbus' 'python2-notify' 'vbetool')
makedepends=('git')
provides=('samsung-tools')
conflicts=('samsung-tools')
optdepends=('phc-intel: for CPU undervolting')
source=('git://github.com/voria/samsung-tools.git')
md5sums=('SKIP')
install=samsung-tools.install
backup=(etc/samsung-tools/system.conf
        etc/samsung-tools/session.conf
        etc/samsung-tools/scripts/bluetooth-on
        etc/samsung-tools/scripts/bluetooth-off
        etc/samsung-tools/scripts/wireless-on
        etc/samsung-tools/scripts/wireless-off)

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {
  cd "$srcdir/$_gitname"

  # Append current git version to version string
  sed -i "s:\(APP_VERSION = \".*\)\":\1~git$pkgver\":" backends/globals.py
}

package() {
  cd "$srcdir/$_gitname"

  make DESTDIR="$pkgdir/" install || return 1

  # Use samsung-tools.install from git sources
  cat archlinux/samsung-tools.install > "$startdir/samsung-tools.install"
}

# vim:set ts=2 sw=2 et:
