# Maintainer: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: simo <simo@archlinux.org>

pkgname=tor-unstable
pkgver=0.2.3.15_alpha
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('i686' 'x86_64')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'tsocks' 'bash')
conflicts=(tor)
provides=(tor)
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf'
        'etc/conf.d/tor')
install='tor.install'
source=("https://www.torproject.org/dist/${pkgname/-unstable/}-${pkgver/_/-}.tar.gz"
        "https://www.torproject.org/dist/${pkgname/-unstable/}-${pkgver/_/-}.tar.gz.asc"
        'torrc'
        'tor'
        'tor.conf.d')
md5sums=('80c6019b6cfa5106282e46ae81c9b59c'
         '37fe873f37c10cd61688d5d581411177'
         '56c75d4e8a66f34167d31e38c43793dd'
         'f8e6868a389877346e7eebaacd1078bb'
         '5c7c6834064b3530c442def6079ac3aa')

build() {
  cd "${srcdir}/${pkgname/-unstable/}-${pkgver/_/-}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname/-unstable/}-${pkgver/_/-}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"
  install -Dm0755 "${srcdir}/tor" "${pkgdir}/etc/rc.d/tor"
  install -Dm0644 "${srcdir}/tor.conf.d" "${pkgdir}/etc/conf.d/tor"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
