# Maintainer: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: simo <simo@archlinux.org>

pkgname=tor-unstable
pkgver=0.2.3.20_rc
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('i686' 'x86_64')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'tsocks' 'bash')
makedepends=('ca-certificates')
conflicts=(tor)
provides=(tor)
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf'
        'etc/conf.d/tor')
install='tor.install'
source=("https://www.torproject.org/dist/${pkgname/-unstable/}-${pkgver/_/-}.tar.gz"{,.asc}
        'torrc'
        'tor'
        'tor.conf.d'
        'tor.service')
md5sums=('59354d68321a8b617a91d8bf0fa7aa78'
         '16cf4030ef4bdb1f66b715fae762efad'
         '56c75d4e8a66f34167d31e38c43793dd'
         'f8e6868a389877346e7eebaacd1078bb'
         '5c7c6834064b3530c442def6079ac3aa'
         'cf23b97a1da09670214da6229a3ecb09')

build() {
  cd "${srcdir}/${pkgname/-unstable/}-${pkgver/_/-}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname/-unstable/}-${pkgver/_/-}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"
  install -Dm0755 "${srcdir}/tor" "${pkgdir}/etc/rc.d/tor"
  install -Dm0644 "${srcdir}/tor.conf.d" "${pkgdir}/etc/conf.d/tor"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"
}
