# Maintainer: Colin Shea <colin@shea.at>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

_pkgbasename=tcp_wrappers
pkgname=lib32-tcp_wrappers
pkgver=7.6
pkgrel=15
pkgdesc="libwrap for 32-bit systems; Monitors and Controls incoming TCP connections"
arch=(x86_64)
url="ftp://ftp.porcupine.org/pub/security/index.html"
license=('custom')
groups=('base')
depends=('bash' 'lib32-glibc' 'tcp_wrappers')
source=(ftp://ftp.porcupine.org/pub/security/tcp_wrappers_${pkgver}.tar.gz
        http://archlinux-stuff.googlecode.com/files/tcp-wrappers-${pkgver}%2B.patch.gz)
md5sums=('e6fa25f71226d090f34de3f6b122fb5a'
         '3e786669e16b78ba726f948ddb73c9db')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/${_pkgbasename}_${pkgver}

  patch -p1 <$srcdir/tcp-wrappers-${pkgver}%2B.patch

  make REAL_DAEMON_DIR=/usr/sbin STYLE=-DPROCESS_OPTIONS linux
  make DESTDIR=$pkgdir install

  # It's a hack, but it's also quick and easy: Just clean up & delete various
  # things from $pkgdir, leaving the rest of the actual package to the 'big
  # brother' version
  rm -rf $pkgdir/etc $pkgdir/usr/sbin $pkgdir/usr/share $pkgdir/usr/include
  mv $pkgdir/usr/lib $pkgdir/usr/lib32
}
