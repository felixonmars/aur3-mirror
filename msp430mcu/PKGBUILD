# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=msp430mcu
pkgver=20110613
pkgrel=5
pkgdesc="C headers and binutils linker scripts for microcontrollers in the MSP430 family"
arch=(any)
url="http://mspgcc4.sourceforge.net/"
license=('GPL')
makedepends=('python2')
options=(!strip)

_mspgcc_ver=20110716
_gnu_mirror="http://ftpmirror.gnu.org"
_sf_base="http://sourceforge.net/projects/mspgcc/files"
_patches_base="${_sf_base}/Patches/LTS/${_mspgcc_ver}"

_patches=("${pkgname}-${pkgver}-sf3379189.patch"
          "${pkgname}-${pkgver}-sf3384550.patch"
          "${pkgname}-${pkgver}-sf3400714.patch")

source=("http://sourceforge.net/projects/mspgcc/files/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        "${_patches[0]}::${_patches_base}/${_patches[0]}/download"
        "${_patches[1]}::${_patches_base}/${_patches[1]}/download"
        "${_patches[2]}::${_patches_base}/${_patches[2]}/download")
sha1sums=('79904ac0c1852c6846cadbb21a0cb77a47adcdcd'
          '87f3d9a759e3dd67f8001775a479243ad2ea61fd'
          '0efb29a413059603b4d2c44643351299238c662d'
          '80c3bc7ea1cb40ac51400437e8e41d6a0b731ecb')

_builddir="${srcdir}/build"

build() {
  cp -r "${srcdir}/msp430mcu-${pkgver}" ${_builddir} && cd ${_builddir}
  sed -i -e "s|python |python2 |" $(find ./scripts/ -name '*.sh')

  (cd "${_builddir}" &&
    for patch in ${_patches[@]} ; do
      msg "Applying ${patch}"
      patch -p1 < "${srcdir}/${patch}"
    done)

  export MSP430MCU_ROOT=$(pwd)
  ./scripts/build.sh
}

package() {
  cd ${_builddir}
  ./scripts/install.sh "${pkgdir}/usr"
}

# vim:set sts=2 ts=2 sw=2 et:
