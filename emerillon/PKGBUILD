# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=emerillon
pkgver=0.1.90
pkgrel=1
pkgdesc="Map viewer for OpenStreetMap"
arch=(i686 x86_64)
url="http://www.novopia.com/emerillon/"
license=(GPL)
depends=('libchamplain>=0.11.0' 'geoclue>=0.11.1' 'libpeas>=1.0' 'rest>=0.6' dconf)
makedepends=(gobject-introspection gtk-doc intltool vala)
options=(!libtool)
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.1/$pkgname-$pkgver.tar.xz)
sha256sums=(0648c70bc19d3bd334a5403938aba3855eb644ee337c29aeecd5ce57ea0bf085)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
