# Maintainer: John Williams <jwilliams4200 liamg reverse&remove moc>
pkgname=snapraid
pkgver=5.0
pkgrel=1
pkgdesc="tool for Snapshot RAID: generate parity files, maintain checksums on data, restore lost data"
arch=('x86_64' 'i686')
url="http://snapraid.sourceforge.net/"
license=('GPL3')
depends=('openssl')
conflicts=('snapraid-git')
source=("http://sourceforge.net/projects/snapraid/files/snapraid-${pkgver}.tar.gz")
sha256sums=('125aff9d5dc606ee9d78d57494df1cafb35376f042375f19cca04e940fd83087')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # patch to not print excluded files and directories 
  # printf("Excluding file '%s'\n", path_next);
  #sed -i -r -e '/printf\("Excluding file '\''%s'\''\\n", path_next\);/d' scan.c 
  # printf("Excluding directory '%s'\n", path_next);
  #sed -i -r -e '/printf\("Excluding directory '\''%s'\''\\n", path_next\);/d' scan.c 

  ./configure --prefix="/usr"
  make 
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" prefix="/usr" mandir="/usr/share/man" install 

  # documentation
  install -D -m644 snapraid.conf.example ${pkgdir}/usr/share/${pkgname}/snapraid.conf.example
  install -D -m644 AUTHORS ${pkgdir}/usr/share/doc/${pkgname}/AUTHORS
  install -D -m644 HISTORY ${pkgdir}/usr/share/doc/${pkgname}/HISTORY
  install -D -m644 README ${pkgdir}/usr/share/doc/${pkgname}/README
  install -D -m644 TODO ${pkgdir}/usr/share/doc/${pkgname}/TODO
}

