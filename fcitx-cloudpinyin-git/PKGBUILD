# Maintainer: Yichao Yu <yyc1992 AT gmail.com>
# Contributor: poplarch <poplarch@gmail.com>

_pkgname=fcitx-cloudpinyin
pkgname=$_pkgname-git
pkgver=0.3.1.64.g213911b
pkgrel=1
epoch=1
pkgdesc="This is a standalone module for fcitx, it can use pinyin API on the internet to input."
arch=('i686' 'x86_64')
url="https://github.com/fcitx/fcitx-cloudpinyin"
license=('GPL')
depends=('fcitx-git' 'fcitx>=4.2.7' 'curl')
makedepends=('git' 'gettext' 'cmake' 'coreutils' 'sh')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
options=('strip' 'debug')

_gitname="fcitx-cloudpinyin"

_gitroot="git://github.com/fcitx/fcitx-cloudpinyin.git"
_gitref="master"

# _gitroot="git://github.com/yuyichao/fcitx-cloudpinyin.git"
# _gitref="master"

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  git describe | sed -e 's/-/./g'
}

build(){
  (_fetch_git)

  msg "Creating make environment..."
  mkdir -p "$srcdir/$_gitname-build"

  msg "Starting make..."
  cd "$srcdir/$_gitname-build"
  cmake ../$_gitname -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
