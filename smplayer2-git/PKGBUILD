# Maintainer: loonyphoenix <loonyphoenix at gmail>
# This is an SMPlayer fork named SMPlayer2 by lachs0r
# that fixes several issues with mplayer2 integration.

pkgname=smplayer2-git
pkgver=20121118
pkgrel=1
pkgdesc="A fork of SMPlayer, targeted at mplayer2 users"
url="https://github.com/lachs0r/SMPlayer2"
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt' 'mplayer2')
makedepends=('git' 'cmake')
optdepends=('quazip: rebuild with this AUR library installed for Subtitle Downloader support')

_gitroot="git://github.com/lachs0r/SMPlayer2.git"
_gitname="smplayer2"

build()

{
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() { 
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
