# Maintainer: Florian Bruhin <archlinux.org@the-compiler.org>

pkgname=yourls-svn
pkgver=750
pkgrel=3
pkgdesc="your own URL shortener"
arch=('any')
url="http://code.google.com/p/yourls/"
license=('GPL3')
depends=('php>=4.3' 'mysql>=4.1')
makedepends=('subversion')
provides=('yourls')
conflicts=('yourls')
backup=('etc/webapps/yourls/config.php')
options=('!strip')
install=$pkgname.install

_svntrunk='http://yourls.googlecode.com/svn/trunk/'
_svnmod='yourls-read-only'

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
}

package() {
  cd "$srcdir/$_svnmod-build"
  _instdir="$pkgdir/usr/share/webapps/yourls"
  _cfgdir="$pkgdir/etc/webapps/yourls"
  mkdir -p "$_instdir" "$_cfgdir"

  cp -ra */ *.php "$_instdir"
  cp -ra sample-robots.txt "$_instdir/robots.txt"
  mv "$_instdir/user/config-sample.php" "$_cfgdir/config.php"
  ln -s /etc/webapps/yourls/config.php "$_instdir/user/config.php"
}

# vim:set ts=2 sw=2 et:
