# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=xiphos-svn
pkgver=4638 
pkgrel=1
pkgdesc="A Bible study tool -- svn version"
arch=('i686' 'x86_64')
url="http://xiphos.org"
license=('GPL')
depends=('webkitgtk' 'libgsf' 'gconf' 'sword-svn' 'gtkhtml4')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'subversion')
provides=('gnomesword' 'xiphos')
conflicts=('gnomesword' 'xiphos')
install=xiphos-svn.install
source=('xiphos::svn://svn.code.sf.net/p/gnomesword/code/trunk')
md5sums=('SKIP')
_svnmod=xiphos

pkgver() {
  cd "$srcdir/${_svnmod}"
  local ver="$(svnversion)"
  printf "%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$srcdir/$_svnmod"
  sed -i 's+/usr/bin/env python+/usr/bin/env python2+' waf

  CXX=g++ ./waf --prefix=/usr --gtk=3 configure 
  ./waf --prefix=/usr build 
}

package() {
  cd "$srcdir/$_svnmod"
  ./waf --destdir=$pkgdir --no-post-install install 
}
