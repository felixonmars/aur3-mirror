# AUR workaround
pkgname=linux-usermode
#pkgname=(linux-usermode linux-usermode-modules)

pkgbase=linux-usermode
_kernelname=-usermodelinux
_srcname=linux-3.6
pkgver=3.6.7
pkgrel=1
pkgdesc="User mode Linux kernel and modules"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://user-mode-linux.sourceforge.net/"
depends=('coreutils')
makedepends=('vde2')
source=("http://www.kernel.org/pub/linux/kernel/v3.x/${_srcname}.tar.xz"
        "http://www.kernel.org/pub/linux/kernel/v3.x/patch-${pkgver}.xz"
	config)
md5sums=('1a1760420eac802c541a20ab51a093d1'
         '134936c362d8812b5cafcf3c67afdce0'
         'd95ebd5ea4aa1783c20053d9521474fa')

build() {
  cd "${srcdir}/${_srcname}"

  patch -p1 -i "${srcdir}/patch-${pkgver}"

  unset LDFLAGS
  sed -i '1,1i#include <sys/resource.h>' arch/um/os-Linux/start_up.c
  sed -i 's|siginfo_t \*|struct siginfo *|' arch/um/os-Linux/signal.c

  cp $srcdir/config .config
  if [ "${_kernelname}" != "" ]; then
    sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"${_kernelname}\"|g" ./.config
    sed -i "s|CONFIG_LOCALVERSION_AUTO=.*|CONFIG_LOCALVERSION_AUTO=n|g" ./.config
  fi
  make ARCH=um vmlinux modules
}

package_linux-usermode() {
  cd "${srcdir}/${_srcname}"
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/kernel26-usermode"
  cp System.map ${pkgdir}/usr/share/kernel26-usermode/System.map
  cp vmlinux ${pkgdir}/usr/bin/
}

package_linux-usermode-modules() {
  install=modules.install

  cd "${srcdir}/${_srcname}"
#  make ARCH=um INSTALL_MOD_PATH="${pkgdir}/usr" modules_install
  make ARCH=um INSTALL_MOD_PATH="${pkgdir}/usr" _modinst_
  rm -f $pkgdir/usr/lib/modules/${pkgver}${_kernelname}/{source,build}
  sed \
    -e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=$pkgver${_kernelname}/g" \
    -i "${startdir}/modules.install"
}
