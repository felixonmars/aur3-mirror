# Maintainer : Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>
# Contributor: Charly <charlyghislain at gmail dot com>
# Contributor: Nick B <Shirakawasuna at gmail dot com>
# Contributor: Julien Humbert <julroy67 at gmail dot com>

_pkgname="konversation"
pkgname="${_pkgname}-git"

pkgver=1.5.61.g2f194da
pkgrel=1
pkgdesc="A user friendly IRC client for KDE - GIT Version"
arch=('x86_64' 'i686')
url="http://konversation.kde.org"

makedepends=('git' 'automoc4' 'cmake' 'pkgconfig' 'docbook-xml' 'optipng')
depends=('kdebase-runtime' 'kdepimlibs')
optdepends=('python: python scripting support'
            'kdebase-konsole: open a terminal in konversation'
            'qca-ossl: Diffie-Hellman key exchange and Blowfish ECB/CBC support')

license=('GPL2' 'FDL')
install="${_pkgname}.install"
options=('!makeflags' '!emptydirs')

conflicts=("${_pkgname}")
provides=("${_pkgname}=${pkgver}")

source=("konversation::git+git://anongit.kde.org/konversation#branch=master")
sha1sums=('SKIP')

pkgver() {
	
	cd "${srcdir}/${_pkgname}/"
	echo "$(git describe --tags)" | sed -e 's|^v||' -e 's|-|.|g'
	
}

prepare() {
	
	cp -r "${srcdir}/${_pkgname}" "${srcdir}/${_pkgname}_build"
	cd "${srcdir}/${_pkgname}_build/"
	
	## libpng16 fix
	find -name '*.png' -exec optipng -quiet -force -fix {} +
	
}

build() {
	
	mkdir -p "${srcdir}/${_pkgname}_build/BUILD"
	cd "${srcdir}/${_pkgname}_build/BUILD/"
	
	cmake "${srcdir}/${_pkgname}_build" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	echo
	
	make
	echo
	
}

package() {
	
	cd "${srcdir}/${_pkgname}_build/BUILD/"
	
	make DESTDIR="${pkgdir}/" install
	echo
	
	## Remove a bunch of symlinks pointing to non-existent files
	for _lang_ in pt_BR uk sv et de en nl; do
		rm "${pkgdir}/usr/share/doc/kde/html/${_lang_}/konversation/common" || true
	done
	
}
