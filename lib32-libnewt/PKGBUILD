# Maintainer: oi_wtf <brainpower at gulli dot com>
# Original-Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lib32-libnewt
_pkgname=libnewt
pkgver=0.52.17
pkgrel=3
_tclver=8.6
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=('x86_64')
url='https://fedorahosted.org/newt/'
license=('GPL')
depends=('lib32-slang' 'lib32-popt' 'gpm')
makedepends=('lib32-tcl')
optdepends=('lib32-tcl')
options=('!makeflags')
source=("https://fedorahosted.org/releases/n/e/newt/newt-$pkgver.tar.gz")
sha256sums=('69837973ef2ee2fa644426f1c3e48d2b18785ebcd382ef7fd01eb2e67d2d632b')

prepare() {
  cd "newt-$pkgver"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  sed -i "s:tcl8.4:tcl$_tclver:" Makefile.in
  echo '#define USE_INTERP_RESULT 1' >> config.h
}

build() {
  cd "newt-$pkgver"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr --libdir=/usr/lib32 --with-gpm-support --without-python
  make
}

package() {
  make -C "newt-$pkgver" DESTDIR="$pkgdir" install

  # lib32 cleanup
  rm -rf "$pkgdir"/usr/{bin,lib,include,share} "$pkgdir/etc"
}

# vim:set ts=2 sw=2 et:
