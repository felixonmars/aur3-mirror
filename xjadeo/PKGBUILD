# Maintainer: N30N <archlinux@alunamation.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=xjadeo
pkgver=0.6.0_rc6
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport."
arch=('i686' 'x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL')
depends=('ffmpeg' 'imlib2' 'jack' 'liblo' 'libxpm' 'portmidi')
makedepends=('imlib' 'qt')
optdepends=('qt: for the GUI (qjadeo)')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver/_/-}.tar.gz" \
	"qjadeo.desktop")
install="$pkgname.install"
md5sums=('f9b26c2c09720124bef0a5fc1c2f43f4'
         '315e2ab44ce3edf4068ff6db48942908')

build() {
  cd "${srcdir}/${pkgname}-${pkgver/_/-}"

  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver/_/-}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 doc/xjadeorc "${pkgdir}/etc/xjadeorc"
  install -D -m644 doc/xjadeo.png "${pkgdir}/usr/share/pixmaps/qjadeo.png"
  install -D -m644 ../qjadeo.desktop "${pkgdir}/usr/share/applications/qjadeo.desktop"
}

# vim: set noet ff=unix: