# Maintainer: Florian Friesdorf <archlinux@chaoflow.net>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: simo <simo@archlinux.org>

pkgname=python24-pil
pkgver=1.1.6
pkgrel=7
pkgdesc="Python imaging library"
arch=('i686' 'x86_64')
url="http://www.pythonware.com/products/pil/index.htm"
license=('custom:"pil"')
depends=('python24' 'libjpeg' 'freetype2') 
makedepends=('tk>=8.5.0')
optdepends=('tk>=8.5.0')
conflicts=('pil-py24')
replaces=('pil-py24')
source=(http://effbot.org/downloads/Imaging-${pkgver}.tar.gz)
md5sums=('3a9b5c20ca52f0a9900512d2c7347622')

build() {
  cd ${srcdir}/Imaging-${pkgver}
  python2.4 setup.py build_ext 
  python2.4 setup.py install --root=${pkgdir}
  mkdir -p ${pkgdir}/usr/include/python2.4/
  cp libImaging/*.h ${pkgdir}/usr/include/python2.4/

  # Install license
  install -m644 -D ${srcdir}/Imaging-${pkgver}/README \
                   ${pkgdir}/usr/share/licenses/${pkgname}/README

  # rename files colliding with non-suffixed version
  echo "Suffixing executables with -py24"
  for x in \
      pilconvert \
      pildriver \
      pilfile \
      pilfont \
      pilprint 
  do
      mv ${pkgdir}/usr/bin/${x}.py ${pkgdir}/usr/bin/${x}-py24.py
  done
}

