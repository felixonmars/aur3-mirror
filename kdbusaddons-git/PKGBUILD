# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdbusaddons-git
pkgver=r51.55b1437
pkgrel=1
pkgdesc='KDBusAddons'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdbusaddons'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules-git' 'git' 'qt5-tools')
groups=('kf5')
conflicts=(kdbusaddons)
provides=(kdbusaddons)
source=('git://anongit.kde.org/kdbusaddons.git')
md5sums=('SKIP')

pkgver() {
  cd kdbusaddons
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdbusaddons \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
