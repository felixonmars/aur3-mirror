# Contributor: Jed Brown <jed@59A2.org>
pkgname=mpich2
pkgver=1.4
pkgrel=1
pkgdesc="An improved implementation of the Message Passing Interface."
url="http://www.mcs.anl.gov/research/projects/mpich2/"
arch=('i686' 'x86_64')
license="custom"
depends=(python2 gcc-fortran libxml2)
makedepends=(libtool make coreutils)
conflicts=()
replaces=()
backup=()
install=install.mpich2
source=(http://www.mcs.anl.gov/research/projects/mpich2/downloads/tarballs/$pkgver/$pkgname-$pkgver.tar.gz
	install.mpich2
	mpich2.profile)
md5sums=('cf7f8c12161b0af3f111e33c6d15f5c6'
         '1eca30c50965e42b15434818a940d6c5'
         'cbda0c129bf3127e3f0c42fcfda2fb86')
options=(!libtool)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  unset MAKEFLAGS # -j2 causes trouble, http://trac.mcs.anl.gov/projects/mpich2/ticket/955

  # CFLAGS etc are normally written into the wrapper compilers.  This
  # gives surprising results, e.g. when the user wants to compile their
  # program without optimization.
  export MPICH2LIB_CFLAGS="$CFLAGS";      unset CFLAGS
  export MPICH2LIB_CXXFLAGS="$CXXFLAGS";  unset CXXFLAGS
  export MPICH2LIB_FFLAGS="$FFLAGS";      unset FFLAGS
  export MPICH2LIB_F90FLAGS="$F90FLAGS";  unset F90FLAGS
  export MPICH2LIB_LDFLAGS="$LDFLAGS";    unset LDFLAGS

  mkdir -p build
  cd build
  ../configure --prefix=/opt/mpich2 --enable-shared --enable-sharedlibs=gcc \
    --enable-error-checking=runtime --enable-error-messages=all \
    --enable-timer-type=clock_gettime \
    --disable-rpath \
    --with-python=python2
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/build
  make DESTDIR=${pkgdir} install

  ln -sf mpiexec.hydra "${pkgdir}/opt/mpich2/bin/mpiexec"
  ln -sf mpiexec "${pkgdir}/opt/mpich2/bin/mpirun"
  ln -sf "mpiexec.hydra" "${pkgdir}/opt/mpich2/bin/mpirun.hydra"
  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/COPYRIGHT ${pkgdir}/usr/share/licenses/mpich2/license.txt
  install -D -m755 ${srcdir}/${pkgname}.profile ${pkgdir}/etc/profile.d/mpich2.sh
  mkdir -p ${pkgdir}/etc/ld.so.conf.d
  echo /opt/mpich2/lib > ${pkgdir}/etc/ld.so.conf.d/mpich2.conf
}
