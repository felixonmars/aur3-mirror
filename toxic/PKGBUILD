# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Florian Hahn <flo@fhahn.com>

pkgname=toxic
pkgver=0.5.2
pkgrel=1

pkgdesc='ncurses Tox client'
arch=('i686' 'x86_64')
url=('http://tox.im')
license=('GPL3')

depends=('tox' 'freealut' 'libnotify')

provides=('toxic')
conflicts=('toxic')

source=("https://github.com/Tox/toxic/archive/v${pkgver}.tar.gz"
        "tox_is_save_encrypted.patch"
        "makefile_git.patch")
md5sums=('6a45cd93d3a64b0b580361a959616140'
         'e9f2213998704800cee7b7f8f016aa1c'
         '9ed36d90b668bceb2ead7493fad0e7cb')
sha256sums=('f2ebf550fbd9b9ee5062740239cdcd9549465caa8080f0e88e797590231db0cc'
            '01e54c39bf865dfd79c218e81fbf943c33a14cc44b92be222f7b0f5b47dd0b17'
            'e31989352c95b28d9d09d8b4d4e36a3a1e8e230c40b9e8a0e0d9ebf46f08dff9')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/tox_is_save_encrypted.patch
  patch -p1 -i ${srcdir}/makefile_git.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

