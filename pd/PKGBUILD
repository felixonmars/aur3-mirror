# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>

pkgname=pd
pkgver=0.42_6
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('i686' 'x86_64')
url="http://crca.ucsd.edu/~msp/software.html"
license=('BSD')
depends=('jack' 'tk')
source=(http://crca.ucsd.edu/~msp/Software/pd-${pkgver/_/-}.src.tar.gz)
md5sums=('27f98731613c7b08208fd2c5fdbc8e3c')

build() {
  cd "$srcdir/pd-${pkgver/_/-}/src"
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-alsa --enable-jack
  make
}

package() {
  cd "$srcdir/pd-${pkgver/_/-}/src"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/pd/LICENSE"
}
