# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>

pkgname=pd
pkgver=0.43_0
pkgrel=2
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('i686' 'x86_64')
url="http://crca.ucsd.edu/~msp/software.html"
license=('BSD')
depends=('jack' 'tk')
source=(http://crca.ucsd.edu/~msp/Software/pd-${pkgver/_/-}.src.tar.gz)
md5sums=('b7e7bab14220146748c2ed596e1c18f2')

build() {
  cd "$srcdir/pd-${pkgver/_/-}/src"
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-alsa --enable-jack
  make
}

package() {
  cd "$srcdir/pd-${pkgver/_/-}/src"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/pd/LICENSE"

  # @bug http://comments.gmane.org/gmane.comp.multimedia.puredata.devel/9482
  install -dm755 "$pkgdir/usr/lib/pd/tcl"
  install -m644 ../tcl/* "$pkgdir/usr/lib/pd/tcl"
}
