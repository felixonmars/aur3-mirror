# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Maintainer: Jachym Barvinek <jachymb@gmail.com>

pkgname=swi-prolog
pkgver=6.2.3
pkgrel=1
pkgdesc='Prolog environment'
arch=('i686' 'x86_64')
url='http://www.swi-prolog.org/'
license=('GPL' 'LGPL')
depends=('gmp' 'readline' 'openssl')
makedepends=('libxft' 'libjpeg' 'unixodbc' 'libxpm' 'libxinerama')
optdepends=('unixodbc: for using the odbc4pl library'
            'libjpeg: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft: for using the pl2xpce library')
options=('!makeflags')
source=("http://www.swi-prolog.org/download/stable/src/pl-${pkgver}.tar.gz")
md5sums=('d1f9dcdab8e16ea58b1a3b7a5a824159')
build() {
  cd pl-${pkgver}

  # enable shared because x86 doesn't build with shared libs, upstream made it intentional...
  ./configure \
    --prefix=/usr \
    --with-world \
    --enable-shared

  make
}

check() {
  cd pl-${pkgver}

  make check || true
}

package() {
  cd pl-${pkgver}

  make DESTDIR=${pkgdir} install

  # fix https://bugs.archlinux.org/task/20873
  chmod +x ${pkgdir}/usr/lib/swipl-${pkgver}/library/dialect/sicstus/swipl-lfr.pl
}
