# Maintainer: TDY <tdy@gmx.com>

pkgname=mount-gtk-git
pkgver=20121230
pkgrel=1
pkgdesc="A graphical front end for udisks and mount"
arch=('i686' 'x86_64')
url="http://mount-gtk.sourceforge.net/"
license=('GPL')
depends=('c++-gtk-utils>=1.2.7' 'gtk3>=2.99.0' 'udisks')
makedepends=('git')
optdepends=('libnotify: for error notifications')
options=('!emptydirs')
provides=('mount-gtk')
conflicts=('mount-gtk')

_gitroot=git://mount-gtk.git.sourceforge.net/gitroot/mount-gtk/mount-gtk
_gitname=mount-gtk

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  aclocal -I m4
  autoheader
  automake
  autoconf -o configure
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
