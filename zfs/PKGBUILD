pkgname=zfs
pkgver=0.6.0_rc10
pkgrel=0
arch=('i686' 'x86_64')
pkgdesc="Native ZFS for Linux"
url="http://zfsonlinux.org/"
depends=("linux>=3.5.3" "zfs-utils>=$pkgver" "util-linux" "spl>=$pkgver")
makedepends=("linux-headers>=3.5.3")
source=(http://github.com/downloads/zfsonlinux/zfs/zfs-0.6.0-rc10.tar.gz
        preempt.patch)
md5sums=('2d90a04c421e6897152cdccb76eb09ba'
         '89104cfe1d087f045b6f3ff9a679d448')
license=('CDDL')
install=zfs.install

build() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  patch -p1 -i $srcdir/preempt.patch
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc --with-config=kernel
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install

  # move module tree /lib -> /usr/lib
  cp -r "$pkgdir/lib" "$pkgdir/usr"
  rm -r "$pkgdir/lib"
}
