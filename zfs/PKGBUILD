# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

# There is an easier and faster way to install, use the split package available here:
# https://github.com/demizer/aur-zfs/blob/master/sources/split

pkgname=('zfs')
# When changing versions, search and replace 0.6.0-rc11, using package
# variables breaks AUR parsing.
pkgver=0.6.0_rc11
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="Native ZFS for Linux"
url="http://zfsonlinux.org/"
depends=('linux>=3.5' "spl>=${pkgver}" "zfs-utils>=${pkgver}")
makedepends=('linux-headers>=3.5')
source=(http://github.com/downloads/zfsonlinux/zfs/zfs-0.6.0-rc11.tar.gz
        zfs_preempt.patch
	)
md5sums=('85435c100d9c1dadad7b961c3cb965f6'
         '13d63bbc0497ccc03d982817b6319512')
license=('CDDL')
install=zfs.install

build() {
  cd ${srcdir}/zfs-0.6.0-rc11
  patch -p0 -i ${srcdir}/zfs_preempt.patch
  ./autogen.sh
  ./configure --with-config=kernel \
              --prefix=/usr \
	      --sysconfdir=/etc \
	      --sbindir=/usr/sbin \
	      --libdir=/usr/lib \
	      --datadir=/usr/share \
	      --includedir=/usr/include \
	      --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-0.6.0-rc11
  make
}

package() {
  cd ${srcdir}/zfs-0.6.0-rc11
  make DESTDIR=${pkgdir} install

  # move module tree /lib -> /usr/lib
  cp -r ${pkgdir}/{lib,usr}
  rm -r ${pkgdir}/lib
}
