# -*- shell-script -*-
# Maintainer: Keith Hughitt <khughitt@umd.edu>
# Contributor:  Felipe Spychalski <spychalski@gmail.com>
# Contributor: Neil Perry <nperry at gmail.com>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=byobu
pkgver=5.41
pkgrel=2
pkgdesc="Enhanced profile and useful notifications for tmux/screen"
arch=("any")
url="https://launchpad.net/byobu"
license=("GPL3")
depends=("libnewt" "python" "tmux")
makedepends=("gettext")
optdepends=("screen: alternative back-end for byobu (default: tmux)"
            "python2: needed for byobu-config")
source=("${url}/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        "statusrc")
md5sums=('aa966c51a403b400ea440f1c1a782a2f'
         '114937599258f95d608fc6b459c0aaff')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # arch-specific screens dir
  sed -re 's;^SOCKETDIR=.+$;SOCKETDIR="/tmp/screens";' -i etc/byobu/socketdir

  # use python2 for byobu-config until python3 bindings for newt are available
  sed -i "s:env python:env python2:" usr/bin/byobu-config

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir"

  # Install custom configuration for arch
  install -D -m644 ${srcdir}/statusrc "${pkgdir}/usr/share/${pkgname}/status/"
}
