# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Franz Burgmann <f dot burgmann at gmail dot com>

pkgname=vdr-xineliboutput
_pluginname=xineliboutput
pkgver=1.0.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin that allows VDR to be used with budget cards (alternative to vdr-xine)"
url="http://sourceforge.net/projects/xineliboutput/"
license=('GPL2')
depends=('dbus-glib' 'libextractor' 'vdr' 'xine-lib')
makedepends=('pkg-config')
optdepends=('xine-ui: An alternative frontend')
install=${pkgname}.install
source=(http://prdownloads.sourceforge.net/sourceforge/xineliboutput/${pkgname}-${pkgver}.tar.bz2
        remote.conf.LIRC.devinput
        remote.conf.LIRC.mceusb
        vdr-sxfe.desktop
        vdr-xine-ui.desktop)
md5sums=('ad31072bb2b9da1de9e20dcb92934f15'
         '8781115ebac9dc500d4096c376c8d605'
         '8a1d026743620a3f5fd2e9c31155c69d'
         'de4b514c2a5e6d84dc4b87ea3b9fde9e'
         '2d5cef1e40a5c7b920ea75c3d129c61f')

build() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  # Assumed VDR directory environment (VDRDIR/Make.config):
  # MANDIR       = /usr/share/man
  # BINDIR       = /usr/bin
  # LOCDIR       = /usr/share/locale
  # PLUGINLIBDIR = /usr/lib/vdr
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr
  
  install -d -m755 "${pkgdir}/usr/lib/vdr"
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -d -m755 "${pkgdir}/usr/share/locale"
  
  make VDRDIR="/usr/include/vdr" \
       LIBDIR="${pkgdir}/usr/lib/vdr" \
       LOCALEDIR="${pkgdir}/usr/share/locale" \
       DESTDIR="${pkgdir}" install
  
  # Install documents and example remote.conf
  for _file in HISTORY README examples/remote.conf.example "${srcdir}/remote.conf.LIRC.devinput" "${srcdir}/remote.conf.LIRC.mceusb"; do
    install -m644 "${_file}" "${pkgdir}/usr/share/doc/${pkgname}/${_file##*/}"
  done
  
  # Install homebrew desktop files
  install -D -m644 "${srcdir}/vdr-sxfe.desktop" "${pkgdir}/usr/share/applications/vdr-sxfe.desktop"
  install -m644 "${srcdir}/vdr-xine-ui.desktop" "${pkgdir}/usr/share/applications/vdr-xine-ui.desktop"
}
