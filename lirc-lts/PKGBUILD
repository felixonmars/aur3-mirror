# Maintainer: Bjoern Franke <bjo@nord-west.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=lirc-lts
pkgver=0.9.0
pkgrel=18
_extramodules=extramodules-3.0-lts
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
pkgdesc="Linux Infrared Remote Control kernel modules for lts arch kernel"
license=('GPL')
depends=('lirc-utils' 'linux-lts>=3.0.36-2' 'linux-lts<3.1')
makedepends=('help2man' 'linux-lts-headers>=3.0' 
'linux-lts-headers<3.1' 'alsa-lib' 'libx11' 'libftdi' 'libirman' 'python2')
provides=('lirc')
options=('!makeflags' '!strip')
install=lirc-lts.install
source=(http://prdownloads.sourceforge.net/lirc/lirc-${pkgver}.tar.bz2
        lirc_wpc8769l.patch
        lirc_atiusb-kfifo.patch
        kernel-2.6.39.patch)
md5sums=('b232aef26f23fe33ea8305d276637086'
         '1cce37e18e3f6f46044abab29016d18f'
         '1f8b104a2365d9f93404b143f499059b'
         '087a7d102e1c96bf1179f38db2b0b237')

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "${srcdir}/lirc-${pkgver}"
  patch -Np1 -i "${srcdir}/lirc_wpc8769l.patch"
  patch -Np1 -i "${srcdir}/lirc_atiusb-kfifo.patch"
  patch -Np1 -i "${srcdir}/kernel-2.6.39.patch"

  sed -i '/AC_PATH_XTRA/d' configure.ac
  sed -e 's/@X_CFLAGS@//g' \
      -e 's/@X_LIBS@//g' \
      -e 's/@X_PRE_LIBS@//g' \
      -e 's/@X_EXTRA_LIBS@//g' -i Makefile.am tools/Makefile.am
  libtoolize
  autoreconf

  PYTHON=python2 ./configure --enable-sandboxed --prefix=/usr \
      --with-driver=all --with-kerneldir=/usr/src/linux-${_kernver}/ \
      --with-moduledir=/usr/lib/modules/${_kernver}/kernel/drivers/misc \
      --with-transmitter

  # Remove drivers already in kernel
  sed -e "s:lirc_dev::" -e "s:lirc_bt829::" -e "s:lirc_igorplugusb::" \
      -e "s:lirc_imon::" -e "s:lirc_parallel::" -e "s:lirc_sasem::" \
      -e "s:lirc_serial::" -e "s:lirc_sir::" -e "s:lirc_ttusbir::" \
      -i Makefile drivers/Makefile drivers/*/Makefile tools/Makefile 
  make
}

package() {
  cd "${srcdir}/lirc-${pkgver}/drivers"
  make DESTDIR="${pkgdir}" moduledir="/usr/lib/modules/${_extramodules}" install

  # set the kernel we've built for inside the install script
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/lirc-lts.install"
  # gzip -9 modules
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}

# vim:set ts=2 sw=2 et:
