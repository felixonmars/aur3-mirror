# Maintainer: Joerg Schuck <joerg_schuck at web dot de>
# Contributor: Tyler <tjb0607 at gmail dot com>
# Contributor: Austin <doorknob60 at gmail dot com>
pkgname=apng-utils
pkgver=2.8
_apngdis_pkgver=2.7
_apngasm_pkgver=2.8
_gif2apng_pkgver=1.9
_apng2gif_pkgver=1.6
pkgrel=5
pkgdesc="apngasm for assembling apng files, apngdis for disassembling apng files, and gif2apng/apng2gif for converting."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/apngdis/"
# apngasm: http://sourceforge.net/projects/apngasm/
# gif2apng: http://sourceforge.net/projects/gif2apng/
# apng2gif: http://sourceforge.net/projects/apng2gif/
provides=( apngasm apngdis gif2apng apng2gif)
license=('LGPL')
depends=( 'libpng' 'gcc-libs' )
source=("http://sourceforge.net/projects/apngdis/files/${_apngdis_pkgver}/apngdis-${_apngdis_pkgver}-src.zip"
        "http://sourceforge.net/projects/apngasm/files/${_apngasm_pkgver}/apngasm-${_apngasm_pkgver}-src.zip"
        "http://sourceforge.net/projects/gif2apng/files/${_gif2apng_pkgver}/gif2apng-${_gif2apng_pkgver}-src.zip"
        "http://sourceforge.net/projects/apng2gif/files/${_apng2gif_pkgver}/apng2gif-${_apng2gif_pkgver}-src.zip"
	apng2gif.cpp.patch
	apngdis.cpp.patch
)

noextract=( 
	"gif2apng-${_gif2apng_pkgver}-src.zip" 
	"apngasm-${_apngasm_pkgver}-src.zip" 
	"apng2gif-${_apng2gif_pkgver}-src.zip" 
	"apngdis-${_apngdis_pkgver}-src.zip" 
)

build() {
  cd "${srcdir}"
  
  # Several packages now use a different build approach than before.
  for _pack in "${noextract[@]}"; do
        _builddir=${_pack%%-*}
	mkdir $_builddir
  	bsdtar xf "${_pack}" --directory $_builddir
  
  	cd $_builddir
	if [ "$_pack" == "${noextract[2]}" ]; then
		patch -p1 apng2gif.cpp ../apng2gif.cpp.patch
	fi
	if [ "$_pack" == "${noextract[3]}" ]; then
		patch -p1 apngdis.cpp ../apngdis.cpp.patch
	fi
  	make ${MAKEFLAGS}
	
	cd "${srcdir}"
  done	
}

package() {
  cd "${srcdir}"
  install -d "$pkgdir/usr/bin/"
  for _pack in "${noextract[@]}"; do
	_builddir=${_pack%%-*}
  	install -Dm755 "$_builddir/$_builddir" "$pkgdir/usr/bin/"
  done
}
md5sums=('b917d829dc8579da40faf1cc19f5bca2'
         'c25094450aefdf98cebc7fc6fd5bf967'
         '4e7336fe6b71c18ac830bb2977204819'
         '1391a0f0705db1f27dd9fb1b0b072e5f'
         '8defe5f400b70a0f3aa327f728f3dc18'
         'd1618f159f077e3ff0da293d5a4460d9')
sha256sums=('cce7470d1ec0c518655d63133931e16d01050033405b67220e94ca7b2b97920d'
            '47bee31bc149efc5932a87819cc0daea668cf7322eeaed9378c43edbdc9803bc'
            '3b21308e935d799b3ffb4a86c6e00ffa4cb9b3f72f52d58d51c66eb0574ae7d2'
            'cd46d7d6de34fed11822ec7f2257b296ec5e45ba1cfa53f70c7974fdae456ffe'
            '790eda4a8b850f4a17ccb44c8f7f135c0ca38ee6acfc1b8b9904fc81ef013b0d'
            'aa55ae45f94ae752a44581a7c49181c8c4985e2ce14b768aa6892fad63b57dd7')
