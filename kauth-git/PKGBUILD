# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kauth-git
pkgver=r23.71b20a3
pkgrel=1
pkgdesc='KAuth'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kauth'
license=('LGPL')
depends=('kcoreaddons-git' 'kf5-polkit-git')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
conflicts=(kauth)
provides=(kauth)
source=('git://anongit.kde.org/kauth.git')
md5sums=('SKIP')

pkgver() {
  cd kauth
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kauth \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
