# Maintainer: Raziel23 <venom23 at runbox dot com>

pkgname=vcmi
pkgver=0.93
pkgrel=1
pkgdesc="Heroes of Might and Magic 3 game engine"
arch=('i686' 'x86_64')
url="http://forum.vcmi.eu/portal.php"
license=('GPL')
depends=('boost-libs' 'ffmpeg' 'sdl_image' 'sdl_mixer' 'sdl_ttf' 'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme')
makedepends=('boost' 'cmake')
optdepends=('innoextract: required by vcmibuilder'
            'unshield: required by vcmibuilder'
            'unzip: required by vcmibuilder'
            'wget: required by vcmibuilder')
install=vcmi.install
source=("http://download.vcmi.eu/$pkgname-$pkgver.tar.gz")
md5sums=('e84c6174f3b45a27fb291034169aa98b')

prepare() {
  cd "$srcdir"

  if [[ -d "$pkgname-$pkgver-build" ]]; then
    msg 'Removing the last build...'
    rm -rf "$pkgname-$pkgver-build"
  fi

  mkdir "$pkgname-$pkgver-build"
}

build() {
  cd "$srcdir/$pkgname-$pkgver-build"

  cmake "../$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_SKIP_RPATH='OFF' \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver-build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
