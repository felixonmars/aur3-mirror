# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban, jjacky

pkgname=jdk
_major=7
_minor=4
_build=b20
_pkg=${_major}u$_minor
pkgver=$_major.$_minor
pkgrel=1
pkgdesc="Java 7 Development Kit"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=('custom')
depends=('jre' 'libx11')
provides=('java-environment=7')
conflicts=("$provides=[@]")
backup=('etc/conf.d/derby-network-server'
        'etc/profile.d/jdk.csh'
        'etc/profile.d/jdk.sh')
install=jdk.install
_arch=i386; [ "$CARCH" == 'x86_64' ] && _arch=amd64
source=("http://download.oracle.com/otn-pub/java/jdk/$_pkg-$_build/jdk-$_pkg-linux-i586.tar.gz"
        'java-monitoring-and-management-console.desktop'
        'java-visualvm.desktop'
        'jdk.sh'
        'jdk.csh'
        'derby-network-server'
        'derby-network-server.conf'
        'javaws-launcher')
md5sums=('8e271abb32ac6ce199ba15ee5beb758b'  # jdk-$_pkg-linux-i586.tar.gz
         'da10de5e6507c392fc9871076ef53ec6'  # java-monitoring-and-management-console.desktop
         '35fd89c5c170021d2183593335703703'  # java-visualvm.desktop
         'b83ab5742651b4234b7d2e20785c6693'  # jdk.sh
         '6f4cbf332816d2c4e9578ecd1d0dce7f'  # jdk.csh
         'a279e195e249000646895d93e199860d'  # derby-network-server
         '4bdff6982c66d24a879c424aaac3d04d'  # derby-network-server.conf
         '45c15a6b4767288f2f745598455ea2bf') # javaws-launcher
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O --header "Cookie: gpw_e24=h"')
[ "$CARCH" == 'x86_64' ] && source[0]="http://download.oracle.com/otn-pub/java/jdk/$_pkg-$_build/jdk-$_pkg-linux-x64.tar.gz" && md5sums[0]='a5ebe416c83b64a68c463a4a65f9e882'

package() {
  msg2 "Creating required dirs"
  cd $(ls -1d jdk1.7.0_*/ | tail -1)
  mkdir -p "$pkgdir"/{opt/java/{,jre/lib/$_arch},etc/{profile,rc}.d,usr/share/{applications,licenses}}

  msg2 "Fetching missing libraries from jre/ to fix jconsole"
  mv jre/lib/$_arch/lib{saproc,attach}.so "$pkgdir"/opt/java/jre/lib/$_arch/

  msg2 "Removing empty and redundant dirs, licenses and .bat scripts"
  rm -r jre/ COPYRIGHT LICENSE THIRDPARTYLICENSEREADME.txt db/bin/*.bat # lib/visualvm/platform/docs/ lib/desktop/

  msg2 "Moving stuff in place"
  mv * "$pkgdir"/opt/java/

  msg2 "Installing scripts, .desktop files, confs and symlinking licenses"
  cd "$srcdir"
  install -m755 jdk.{c,}sh "$pkgdir"/etc/profile.d/
  install -Dm644 derby-network-server.conf "$pkgdir"/etc/conf.d/derby-network-server
  install -m755 derby-network-server "$pkgdir"/etc/rc.d/
  install -m644 java-{visualvm,monitoring-and-management-console}.desktop "$pkgdir"/usr/share/applications/
  ln -s jre "$pkgdir"/usr/share/licenses/jdk
}