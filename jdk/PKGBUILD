# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban, jjacky

pkgname=jdk
_major=7
_minor=9
_build=b05
_pkg=${_major}u$_minor
pkgver=$_major.$_minor
pkgrel=1
pkgdesc="Java $_major Development Kit"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=('custom')
depends=('jre' 'libx11')
provides=("java-environment=$_major")
conflicts=("${provides[@]}")
backup=('etc/conf.d/derby-network-server'
        'etc/profile.d/jdk.csh'
        'etc/profile.d/jdk.sh')
install=jdk.install
_arch=i386; [ "$CARCH" == 'x86_64' ] && _arch=amd64
source=("http://download.oracle.com/otn-pub/java/jdk/$_pkg-$_build/jdk-$_pkg-linux-i586.tar.gz"
        'derby-network-server'
        'derby-network-server.conf'
        'java-monitoring-and-management-console.desktop'
        'java-visualvm.desktop'
        'jdk.csh'
        'jdk.sh')
md5sums=('f66c309ab38d6ba6651f7d98cd58d9d5'  # jdk-$_pkg-linux-i586.tar.gz
         'a279e195e249000646895d93e199860d'  # derby-network-server
         '4bdff6982c66d24a879c424aaac3d04d'  # derby-network-server.conf
         'da10de5e6507c392fc9871076ef53ec6'  # java-monitoring-and-management-console.desktop
         '35fd89c5c170021d2183593335703703'  # java-visualvm.desktop
         '6f4cbf332816d2c4e9578ecd1d0dce7f'  # jdk.csh
         'b83ab5742651b4234b7d2e20785c6693') # jdk.sh
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O --header "Cookie: gpw_e24=h"')
[ "$CARCH" == 'x86_64' ] && source[0]="http://download.oracle.com/otn-pub/java/jdk/$_pkg-$_build/jdk-$_pkg-linux-x64.tar.gz" && md5sums[0]='372b9dcde93230522672837e1820f939'

package() {
  msg2 "Creating required dirs"
  cd jdk1.$_major.0_0$_minor
  mkdir -p "$pkgdir"/{opt/java/{,jre/lib/$_arch},etc/{profile,rc}.d,usr/share/{applications,licenses}}

  msg2 "Fetching missing libraries from jre/ to fix jconsole"
  mv jre/lib/$_arch/lib{saproc,attach}.so "$pkgdir"/opt/java/jre/lib/$_arch/

  msg2 "Removing redundant dirs, licenses and .bat scripts"
  rm man/{ja,{,ja_JP.UTF-8/}man1/{java{,ws},keytool,orbd,{un,}pack200,policytool,rmid,rmiregistry,servertool,tnameserv}.1}
  rm -r jre/ db/bin/*.bat COPYRIGHT LICENSE README.html release *.txt # lib/desktop/ lib/visualvm/platform/docs/

  msg2 "Moving stuff in place"
  mv man "$pkgdir"/usr/share/
  mv * "$pkgdir"/opt/java/

  msg2 "Installing scripts, .desktop files, confs and symlinking licenses"
  cd "$srcdir"
  install -m755 jdk.{c,}sh "$pkgdir"/etc/profile.d/
  install -Dm644 derby-network-server.conf "$pkgdir"/etc/conf.d/derby-network-server
  install -m755 derby-network-server "$pkgdir"/etc/rc.d/
  install -m644 *.desktop "$pkgdir"/usr/share/applications/
  ln -s jre "$pkgdir"/usr/share/licenses/jdk
}