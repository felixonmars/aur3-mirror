# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=gvfs
pkgname=$_pkgname-git
pkgver=20120922
pkgrel=1
pkgdesc="Virtual filesystem implementation for gio"
arch=(i686 x86_64)
url=http://gnome.org/
license=(LGPL)
depends=(bluez dbus-glib dconf fuse glib2-git gtk3 libarchive libbluray
    libcdio libimobiledevice libsoup-gnome obex-data-server smbclient udisks2)
makedepends=(git gtk-doc)
provides=($_pkgname=1.12.3 $_pkgname-{afc,afp,gphoto2,obexftp,smb})
conflicts=($_pkgname $_pkgname-{afc,afp,gphoto2,obexftp,smb})
options=(!libtool)
install=$_pkgname.install

_gitroot=http://git.gnome.org/browse/gvfs
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to git.gnome.org GIT server...."
    if [[ -d $_gitname/.git ]] ; then
        pushd $_gitname && git pull origin
        msg "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Starting build..."
    ./autogen.sh --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/gvfs \
        --with-bash-completion-dir=/usr/share/bash-completion/completions
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
