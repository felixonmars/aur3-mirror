# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=fsl
pkgver=4.1.9
pkgrel=1
pkgdesc="A comprehensive library of analysis tools for FMRI, MRI and DTI brain imaging data"
arch=("i686" "x86_64")
url="http://www.fmrib.ox.ac.uk/fsl/"
license=(custom)
depends=()
makedepends=()
md5sums=('1559f6a86d95c93e5719340f20eab125' 'd71fbd40e44f98b709eb84fa5bcf853a')

source=("http://www.fmrib.ox.ac.uk/fsldownloads/fsl-${pkgver}-sources.tar.gz" "fsl-${pkgver}.patch")

build() {
	
	export FSLDIR="$srcdir/fsl"
	. "${FSLDIR}/etc/fslconf/fsl.sh"
	if [ ! -e ${FSLDIR}/config/${FSLMACHTYPE} ]; then
	cp -r "${FSLDIR}/config/linux_64-gcc4.2" "${FSLDIR}/config/${FSLMACHTYPE}"
	fi
	sed -i "s^ginstall^install^g" "${FSLDIR}/config/${FSLMACHTYPE}/systemvars.mk"

	# Patch for Tcl/Tk Mousewheel causing errors
	# Fix found here: http://www.tek-tips.com/viewthread.cfm?qid=1619488
	cd "${srcdir}"
	patch -Np1 -i ../fsl-${pkgver}.patch

	# fix for odd bug in melodic Makefile
	sed -i "s^OPTFLAGS_alphaev6-dec-osf^#OPTFLAGS_alphaev6-dec-osf^g" ${FSLDIR}/src/melodic/Makefile

	cd "${FSLDIR}"
	./build
}

package() {

	rm -rf $srcdir/fsl/extras/src
	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/opt/fsl

	cp $srcdir/fsl/bin/*		$pkgdir/usr/bin/
	cp -r $srcdir/fsl/data		$pkgdir/opt/fsl/
	cp -r $srcdir/fsl/doc		$pkgdir/opt/fsl/
	cp -r $srcdir/fsl/etc		$pkgdir/opt/fsl/
	cp -r $srcdir/fsl/extras	$pkgdir/opt/fsl/
	cp -r $srcdir/fsl/include	$pkgdir/opt/fsl/
	cp -r $srcdir/fsl/lib		$pkgdir/opt/fsl/
	cp -r $srcdir/fsl/refdoc	$pkgdir/opt/fsl/
	cp -r $srcdir/fsl/tcl		$pkgdir/opt/fsl/

	mkdir -p $pkgdir/etc/profile.d
	echo 'FSLDIR=/opt/fsl' >			$pkgdir/etc/profile.d/fsl.sh
	echo '. ${FSLDIR}/etc/fslconf/fsl.sh' >>	$pkgdir/etc/profile.d/fsl.sh
	echo 'export FSLDIR' >>				$pkgdir/etc/profile.d/fsl.sh

	mkdir -p $pkgdir/usr/share/licenses/fsl
	grep -v \< $srcdir/fsl/doc/fsl/licence.html | cat -s > $pkgdir/usr/share/licenses/fsl/LICENSE

	find $pkgdir -type f -exec chmod 644 {} \;
	find $pkgdir -type d -exec chmod 755 {} \;
	find $pkgdir/usr/bin -exec chmod 755 {} \;
	find $pkgdir/opt/fsl/etc/fslconf -exec chmod 755 {} \;
	find $pkgdir/opt/fsl/extras/bin -exec chmod 755 {} \;
	chmod 755 $pkgdir/etc/profile.d/fsl.sh

}
