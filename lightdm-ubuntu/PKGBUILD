# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Some of this was taken from Morfeo's lightdm PKDBUILD

pkgname=lightdm-ubuntu
_ubuntu_rel=0ubuntu2
pkgver=1.4.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="A lightweight display manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('gnome-themes-standard' 'gnome-backgrounds' 'gnome-icon-theme' 'libwebkit' 'dbus-glib' 'gtk3' 'libxklavier' 'intltool' 'accountsservice')
makedepends=('gnome-doc-utils' 'gobject-introspection' 'pkg-config' 'gtk-doc' 'gnome-common' 'yelp-tools')
optdepends=('xorg-server-xephyr: run lightdm in test mode' 
            'lightdm-webkit-greeter-bzr'
            'lightdm-unity-greeter: default Ubuntu 12.10 greeter')
provides=("lightdm=${pkgver}")
conflicts=('lightdm')
options=('!libtool' '!emptydirs')
backup=('etc/apparmor.d/lightdm-guest-session'
        'etc/dbus-1/system.d/org.freedesktop.DisplayManager.conf'
        'etc/lightdm.conf'
        'etc/lightdm/keys.conf'
        'etc/lightdm/lightdm.conf'
        'etc/lightdm/lightdm-gtk-greeter.conf'
        'etc/lightdm/lightdm-gtk-greeter-ubuntu.conf'
        'etc/lightdm/users.conf')
install=lightdm.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver%.*}-${_ubuntu_rel}.diff.gz"
        'lightdm'
        'lightdm.service'
        'lightdm-plymouth.service'
        'lightdm-gtk-greeter.conf'
        'lightdm.tmpfiles.d'
        'lightdm.pam'
        'lightdm-autologin.pam'
        'guest-session-cross-distro.patch')
sha512sums=('dedea48dbf2dde1a84c63691474b6452340b7ba5abd9793afb02f3ffcdb4d664432e3e4bbd6151bfb5861424c1063b42ca99989642b970b4a804ed8bfea6fb1e'
            '1b3664f5dc604975c56d28548000b6d18a849be7f318dfdbd1f51b1cb9e194b51c317973d3efa800a29c82c9cbed25d1e42b6022cef2dcfcdce1570ccdde471c'
            'c4813ae27d81ece8bf66e89c1adbb215443076cb6a2b1da28a5ea9528b25052715331a1d76711a2d06cbf9d41f46afa9a868b404112035db2295c177e7f99cc0'
            '485f9eb570de47960479ecf708bb5a29c0af2ee6ca4b122f4a9dd38a9d2b319bc3fdb562879dd2e6d5d3f65d5f8ca97e545c805b3030d925533db8c569ff687a'
            '36c5a6607b1fb3563e24a68378d8321f386616c0d9434f9e17150f74e2d8038bec6bb1ed784bfb959c902eb79deb70866277cca5e09f18178f4cb87d9a15dec9'
            'fa35ece114255abfc409f1c9da1eb7129055d8669aad11fe3d69084bf2216e93bf09864ac4e8874e88f166be9735fc55ed899056eb3bd94c5b33d3b2cbd55f4d'
            '98e45127db7615da4955fc359b31c1b05fa87cf99f872487961e03c138c8ddb85346d626e094a8e596c8f544f5d6d00b66db8fc22fa130bbb77d927384e15760'
            '7c97c028302d37235f550593f90cbe502abc9a1995d77aefedffa7e5635f043b4b61c7f8343db0084c561b3f46bbfc57db16a5623cba8d7aaec60e0e6af21853'
            '925016b85d34b219df64f0fce883dd542d0dbbcb865d5034a1aa6402c2d554b0f6fc00dcf05408e63126785923c6d05c93865055b93ab424aae56c5f27c65444'
            'f3c62d14fc4ad735b3ba92d9d39bfb1cb3d40fdf4b60ab872fa044bb35a41293022bad33b2d7ade644638163ca8f7742f2be7076ccfb26f0bd3353586baf293e')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/lightdm_${pkgver%.*}-${_ubuntu_rel}.diff"

  # Disable patches
    # Do not use Ubuntu's language-tools
      sed -i '/04_language_options.patch/d' debian/patches/series

  for i in $(grep -v '#' debian/patches/series); do
    patch -Np1 -i "debian/patches/${i}"
  done

  # Do not depend on Debian/Ubuntu specific adduser package
  patch -Np1 -i "${srcdir}/guest-session-cross-distro.patch"

  # Cannot find gmodule-2.0 with newer glib
  CFLAGS="${CFLAGS} $(pkg-config --cflags --libs gmodule-2.0)"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --libexecdir=/usr/lib/lightdm \
    --localstatedir=/var \
    --with-user-session=ubuntu \
    --with-greeter-user=lightdm \
    --disable-tests # Tests fail with GCC 4.7 \
    #--with-greeter-session=lightdm-gtk-greeter

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/etc/rc.d/"
  install -dm755 "${pkgdir}/usr/lib/systemd/system/"

  # Move files (from debian/rules)
  mv "${pkgdir}/usr/lib/lightdm/lightdm/lightdm-set-defaults" "${pkgdir}/usr/lib/lightdm/"

  # Install PAM service
  install -dm755  "${pkgdir}/etc/pam.d/"
  install -m644 "${srcdir}/lightdm.pam" "${pkgdir}/etc/pam.d/lightdm"
  install -m644 "${srcdir}/lightdm-autologin.pam" "${pkgdir}/etc/pam.d/lightdm-autologin"

  # Install configuration files
  install -dm755 "${pkgdir}/usr/share/doc/lightdm/"
  install -dm755 "${pkgdir}/etc/lightdm/"
  install -m644 "${pkgdir}/etc/lightdm/lightdm.conf" "${pkgdir}/usr/share/doc/lightdm/"
  install -m644 "${pkgdir}/etc/lightdm/keys.conf" "${pkgdir}/usr/share/doc/lightdm/"
  install -m644 "${srcdir}/lightdm-gtk-greeter.conf" "${pkgdir}/etc/lightdm/"

  # Install binaries and scripts
  install -dm755 "${pkgdir}/usr/sbin/"
  install -dm755 "${pkgdir}/usr/lib/lightdm/"
  install -m755 "debian/lightdm-session" "${pkgdir}/usr/sbin/"
  install -m755 "debian/guest-account" "${pkgdir}/usr/sbin/"
  install -m755 "debian/lightdm-greeter-session" "${pkgdir}/usr/lib/lightdm/"
  chmod +x "${pkgdir}/usr/lib/lightdm/lightdm-greeter-session"

  # Install init script and SystemD service
  install -m755 "${srcdir}/lightdm" "${pkgdir}/etc/rc.d"
  install -m644 "${srcdir}/lightdm.service" "${pkgdir}/usr/lib/systemd/system/"
  install -m644 "${srcdir}/lightdm-plymouth.service" "${pkgdir}/usr/lib/systemd/system/"

  # Install systemd tmpfiles.d file
  install -dm755 "${pkgdir}/usr/lib/tmpfiles.d/"
  install -m644 "${srcdir}/lightdm.tmpfiles.d" \
    "${pkgdir}/usr/lib/tmpfiles.d/lightdm.conf"

  # Configuration settings that differ from Ubuntu
  sed -i \
    -e 's/^\(minimum-uid=\).*$/\11000/g' \
    -e 's@/usr\(/sbin/nologin\)$@\1@g' \
    "${pkgdir}/etc/lightdm/users.conf"

  # Configuration files specific to Ubuntu
  rm -rvf "${pkgdir}/etc/init/"
}

# vim:set ts=2 sw=2 et:
