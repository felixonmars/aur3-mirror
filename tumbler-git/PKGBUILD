# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from tumbler package)
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan Jezek <honzin.jezek@gmail.com>

_pkgname=tumbler
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="Thumbnail generator for Thunar GIO branches"
arch=(i686 x86_64)
url=http://git.xfce.org/apps/$_pkgname/tree/README
license=(GPL2)
groups=(xfce4-git)
depends=(dbus-glib gdk-pixbuf2)
makedepends=(ffmpegthumbnailer freetype2 git gstreamer0.10 libgsf libopenraw poppler-glib xfce4-dev-tools)
optdepends=('ffmpegthumbnailer: for video thumbnails'
    'gstreamer0.10: for video thumbnails'
    'poppler-glib: for PDF thumbnails'
    'libgsf: for ODF thumbnails'
    'libopenraw: for RAW thumbnails'
    'freetype2: for font thumbnails')
provides=($_pkgname=0.4.8)
conflicts=($_pkgname)
options=(!libtool)

_gitroot=git://git.xfce.org/xfce/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/xfce4 \
        --disable-static \
        --disable-debug
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
