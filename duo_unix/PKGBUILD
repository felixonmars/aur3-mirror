# Maintainer: Josh <moc tod liamg ta suoecal>

pkgname=duo_unix
pkgver=1.9
pkgrel=1
pkgdesc='Login utility and PAM module for Duo Security two-factor authentication'
arch=('i686' 'x86_64')
url='http://www.duosecurity.com/docs/duounix'
license=('custom:GPL2')
depends=('openssl' 'pam')
install=$pkgname.install
backup=('etc/duo/login_duo.conf' 'etc/duo/pam_duo.conf')
source=(https://github.com/downloads/duosecurity/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('92121c2f0d06babc17a70a71aa272d24c035522136304758f2357c79b67b3061')
options=('!libtool')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # By default, login_duo is installed setuid root with system-wide config files
  # By default, login_duo tries to use the sshd user for privilege separation (--with-privsep-user=USER)
  # If this user does not exist, then the nobody user is used instead
  # More config options here: https://github.com/duosecurity/duo_unix
  ./configure --with-pam=/usr/lib/security --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # The license is GPL, but with a couple clarifications/exceptions (copy the LICENSE file)
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
