# Maintainer: Josh <moc tod liamg ta suoecal>

pkgname=duo_unix
pkgver=1.9.6
pkgrel=1
pkgdesc='Login utility and PAM module for Duo Security two-factor authentication'
arch=('i686' 'x86_64')
url='https://www.duosecurity.com/docs/duounix'
license=('custom:GPL2')
depends=('openssl' 'pam')
install="${pkgname}.install"
backup=('etc/duo/login_duo.conf' 'etc/duo/pam_duo.conf')
source=("https://dl.duosecurity.com/${pkgname}-latest.tar.gz")

# https://www.duosecurity.com/docs/checksums#duounix
sha1sums=('02f2d28af55872c278df8b99d2e8503098504583')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # By default, login_duo is installed setuid root with system-wide config files
  # By default, login_duo tries to use the sshd user for privilege separation (--with-privsep-user=USER)
  # If this user does not exist then the nobody user is used instead (which should be the Arch Linux default)
  # More config options here: https://github.com/duosecurity/duo_unix
  ./configure --prefix=/usr --sbindir=/usr/bin --with-pam=/usr/lib/security
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # The license is GPL, but with a couple clarifications/exceptions (link the LICENSE file to /usr/share/licenses)
  # This link won't point to anything if the !docs option is specified
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/doc/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
