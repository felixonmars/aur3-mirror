# Contributor: Tom < reztho at archlinux dot us >
# Contributor: Nicolas Qui√©not < niQo at aur > 

pkgname=forked-daapd-git
pkgver=20110327
pkgrel=1
pkgdesc='A rewrite of mt-daapd/Firefly Media Server'
arch=('i686' 'x86_64')
url='http://git.debian.org/?p=users/jblache/forked-daapd.git'
license=('GPL')
makedepends=('git' 'mxml' 'antlr3')
depends=('confuse' 'libantlr3c' 'avahi' 'libplist' 'ffmpeg' 'taglib' 'flac' 'libevent<2.0' 'sqlite3' 'libgcrypt' 'libavl-for-forked-daapd' 'libunistring') 
install=${pkgname}.install
backup=('etc/forked-daapd.conf')
source=('forked-daapd.rc')
provides=('mt-daapd' 'forked-daapd')
options=('!libtool')

_gitroot="git://git.debian.org/users/jblache/forked-daapd.git"
_gitname="forked-daapd"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # Compiling and installing forked-daapd
  cd "$srcdir/$_gitname-build"
  /usr/bin/autoreconf -i
  ./configure  --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-flac --enable-musepack --enable-itunes

#  sed -i -e 's|#include <event-config.h>|#include <event2/event-config.h>|' src/evhttp/http.c
#  sed -i -e 's|#include <event-config.h>|#include <event2/event-config.h>|' src/evrtsp/rtsp.c

  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install

  cd ${pkgdir} ; rmdir -p var/cache/forked-daapd ; cd -

  install -Dm644 README ${pkgdir}/usr/share/doc/${pkgname/-git/}/README || return 1
  install -Dm755 ${srcdir}/forked-daapd.rc ${pkgdir}/etc/rc.d/forked-daapd || return 1
}

md5sums=('04bfd583f7cd73c9c3fd166c3bc49cf6')
