# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Mihai Militaru <mihai.militaru@gmx.com>
# Contributor: scippio <scippio@berounet.cz>
# Contributor: Jesse Jaara <jesse.jaara@gmail.com>
# Contributor: Dan Vratil <dan@progdan.cz>
# Maintainer: Daovrin Uƒçakar <davorin.ucakar@gmail.com>

pkgname=lib32-freeimage
pkgver=3.15.3
pkgrel=2
pkgdesc="Library project for developers who would like to support popular graphics image formats"
arch=('x86_64')
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net"
groups=('lib32')
depends=('freeimage' 'lib32-gcc-libs')
source=("http://downloads.sourceforge.net/project/freeimage/Source%20Distribution/${pkgver}/FreeImage${pkgver//./}.zip")
md5sums=('6c305b4086500ff8541c596ea6fc7cd7')

build()
{
  export CC="gcc -march=i686 -m32"
  export CXX="g++ -march=i686 -m32"
  export CFLAGS="${CFLAGS/-march=x86-64} -fPIC -fexceptions -fvisibility=hidden -DNO_LCMS"
  export CXXFLAGS="${CXXFLAGS/-march=x86-64} -fPIC -fexceptions -fvisibility=hidden -DNO_LCMS"

  cp -r FreeImage FreeImagefip

  cd FreeImage        
  make
	
  cd ${srcdir}/FreeImagefip
  make -f Makefile.fip 
}

package() 
{
  cd FreeImage
  make DESTDIR=${pkgdir} install || true

  cd ${srcdir}/FreeImagefip
  make -f Makefile.fip DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/include
  mv ${pkgdir}/usr/lib ${pkgdir}/usr/lib32
}
