# Maintainer: Ner0

pkgname=botanicula
_gamepkg="Botanicula.air"
pkgver=1.0
pkgrel=3
pkgdesc="A point'n'click exploration game from the creators of Machinarium (requires full copy of the game)"
arch=('any')
url="http://www.botanicula.net/"
license=('custom')
depends=('adobe-air-sdk' 'hicolor-icon-theme')
makedepends=('unzip')
install=$pkgname.install
source=($pkgname.desktop
        $pkgname.sh)
md5sums=('617bf525a3ec3de3a69ec10888c56669'
         '2ff0da71d4cec829eed7b7af0a69b55a')

# Uncomment this to reduce the compression time (doesn't work with yaourt)
# PKGEXT='.pkg.tar'

package () {
   msg "You need a full copy of this game in order to install it"
   msg "Searching for ${_gamepkg} in dir: \"$startdir\""
	 pkgpath=$startdir
   if [[ ! -f "$startdir/${_gamepkg}" ]]; then
       error "Game package not found, please type absolute path to ${_gamepkg} (/home/joe):"
       read pkgpath
       if [[ ! -f "${pkgpath}/${_gamepkg}" ]]; then
           error "Unable to find game package." && return 1
       fi
    fi
    msg "Found game package, installing..."


  # Creating folders
  install -dm755 "$pkgdir"/usr/{bin,share/{applications,botanicula}}

  # Extracting the game files
  unzip "$pkgpath/$_gamepkg" -d "$pkgdir/usr/share/$pkgname"

  # Installing launch script and desktop file
  install -m755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -m644 $pkgname.desktop "$pkgdir/usr/share/applications"

  # Installing icons
  cd "$pkgdir/usr/share/$pkgname"

  for i in 16 32 36 48 72 128 256 512; do
    install -Dm644 "data/icons/b$i.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done
}
