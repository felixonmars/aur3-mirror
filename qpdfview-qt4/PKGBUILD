# Maintainer: Boris Vasilyev <bobartmail@gmail.com>

pkgname=qpdfview-qt4
_pkgname=${pkgname%-qt4}
pkgver=0.4.8
pkgrel=1
pkgdesc='A tabbed PDF viewer using the poppler-qt4 library.'
arch=('i686' 'x86_64')
url='https://launchpad.net/qpdfview'
license=('GPL2')
depends=('libcups' 'poppler-qt4' 'qt4')
optdepends=('libspectre: for PostScript support' 'djvulibre: for DjVu support')
makedepends=('automoc4')
provides=('qpdfview')
replace=('qpdfview')
conflicts=('qpdfview')
install='qpdfview-qt4.install'
source=("http://launchpad.net/$_pkgname/trunk/$pkgver/+download/$_pkgname-$pkgver.tar.gz")
md5sums=('63bf12682ecc31c478820a43bb96f5c0')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  if ! pkg-config --exists libspectre; then
    local config="$config without_ps"
  fi
  
  if ! pkg-config --exists ddjvuapi; then
    local config="$config without_djvu"
  fi

  lrelease-qt4 qpdfview.pro
  qmake-qt4 "CONFIG+=$config" qpdfview.pro
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make "INSTALL_ROOT=$pkgdir" install
}


