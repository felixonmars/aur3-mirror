# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=python2-midi
pkgname=$_pkgname-git
pkgver=0.2.3.52.c66e93b
pkgrel=1
pkgdesc="A feature-rich, hardware-independent MIDI toolkit for Python"
arch=(i686 x86_64)
url=https://github.com/vishnubob/python-midi
license=(MIT)
depends=(alsa-lib python2)
makedepends=(python2-distribute swig)
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
source=($pkgname::git://github.com/vishnubob/python-midi.git)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $pkgname/
    echo $(git describe --tags | sed 's/v//;s/-/./g').$(git shortlog | grep -c '\s\+').$(git describe --always)
}

build() {
    cd $pkgname/
    python2 setup.py build    
}

package() {
    cd $pkgname/
    python2 setup.py install --root="$pkgdir" --optimize=1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
