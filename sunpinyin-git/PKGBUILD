# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jekyll Wu <adaptee [at] gmail [dot] com>
# Contributor: Liu Chang <goduck777@gmail.com>
 
pkgname=sunpinyin-git
pkgver=20120820
pkgrel=1
pkgdesc='A statistical language model based pinyin IME by Sun.'
url='http://code.google.com/p/sunpinyin/'
arch=('i686' 'x86_64')
license=('LGPL' 'APACHE')
depends=('sqlite3')
makedepends=('git' 'scons' 'intltool')
provides=('sunpinyin')
conflicts=('sunpinyin')
source=('http://open-gram.googlecode.com/files/lm_sc.t3g.arpa.tar.bz2'
	'http://open-gram.googlecode.com/files/dict.utf8.tar.bz2')
noextract=('lm_sc.t3g.arpa.tar.bz2'
           'dict.utf8.tar.bz2')

_gitroot="git://github.com/sunpinyin/sunpinyin.git"
_gitname="sunpinyin"

build() {
  cd $srcdir
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."

  rm -rf $srcdir/$_gitname-build
  git clone $srcdir/$_gitname $srcdir/$_gitname-build
  cd $srcdir/$_gitname-build
  
  ln -sf ${srcdir}/lm_sc.t3g.arpa.tar.bz2 raw/lm_sc.t3g.arpa.tar.bz2
  ln -sf ${srcdir}/dict.utf8.tar.bz2 raw/dict.utf8.tar.bz2

  sed -i -e "1s|python|python2|" python/*.py python/importer/*.py
  scons --prefix=/usr

}
package ()
{
  cd $srcdir/$_gitname-build
  scons install --prefix=/usr --install-sandbox=$pkgdir 
}

md5sums=('31dd2667d9a5cae9d2ecdacf2201ab13'
         '98b1dc61d3ef3fd4be3c0a9fac410da2')
