# Contributor: Jerome Berger <jeberger@free.fr>
pkgname=gdc2-hg
pkgver=566
_gccver=4.5.1
pkgrel=1
pkgdesc="GDC, Digital Mars D Programing Language (DMD) frontend for GCC (D2 version)"
arch=(i686 x86_64)
url="http://bitbucket.org/goshawk/gdc/wiki/Home"
license="GPL"
provides=('gdc')
depends=("gcc>=$_gccver")
makedepends=('mercurial')
conflicts=('gdc' 'gdc-svn' 'gdc1-hg')
options=('!libtool' '!emptydirs')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${_gccver}/gcc-core-${_gccver}.tar.bz2)
md5sums=('dc8959e31b01a65ce10d269614815054')

_hgroot=http://bitbucket.org/goshawk
_hgrepo=gdc

build() {
   cd $srcdir

   rm -rf $srcdir/gcc-$_gccver-build $srcdir/gcc-build
   mv $srcdir/gcc-$_gccver $srcdir/gcc-$_gccver-build
   ln -s $srcdir/gdc/d $srcdir/gcc-$_gccver-build/gcc

   cd gcc-$_gccver-build
   ./gcc/d/setup-gcc.sh -v2

   export MAKEFLAGS="-j1"

   # Don't install libiberty 
   sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

   # Don't run fixincludes
   sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

   mkdir ../gcc-build
   cd ../gcc-build
   ../gcc-$_gccver-build/configure --prefix=/usr \
       --enable-languages=d --enable-threads  --enable-__cxa_atexit \
       --disable-multilib --libdir=/usr/lib --libexecdir=/usr/lib \
       --disable-shared --disable-bootstrap
   make
}

package() {
   cd $srcdir/gcc-build

   DESTDIR=$pkgdir make install-target-libgcc install-target-libphobos
   DESTDIR=$pkgdir make -C gcc d.install-common d.install-man d.install-normal
   install -Dm 755 gcc/cc1d $pkgdir/usr/bin/cc1d
}
