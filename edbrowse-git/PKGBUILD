# Maintainer: Chris Brannon <cmbrannon79 (at) gmail.com>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>
pkgname=edbrowse-git
pkgver=20130426
pkgrel=1
pkgdesc="Editor, browser, and mail client based on /bin/ed"
arch=('i686' 'x86_64')
url="http://the-brannons.com/edbrowse/"
license=('GPL' 'openssl')
depends=('js185' 'openssl' 'pcre' 'curl>=7.17.0')
makedepends=('git')
provides=(edbrowse)
conflicts=(edbrowse)
replaces=(edbrowse-cvs)
source=()

_gitroot="git://github.com/CMB/edbrowse.git"
_gitname="edbrowse"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build/src"
  make STRIP=''
}

package() {
  cd "$srcdir/$_gitname-build/src"
  make DESTDIR="$pkgdir" prefix=/usr install

  cd ../doc

  # setup.ebrc is a shell script that builds a config file for you.  It's
  # in doc/.
  install -m755 setup.ebrc "${pkgdir}/usr/bin"

  # How much of the documentation should be installed?
  install -dm755 "${pkgdir}/usr/share/doc/edbrowse"
  install -m644 usersguide.html philosophy.html sample.ebrc "$pkgdir/usr/share/doc/edbrowse"
}
