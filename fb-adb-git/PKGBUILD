# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=fb-adb
pkgname=${_pkgname}-git
pkgver=20141024.r73.c99d355
pkgrel=1
pkgdesc="A better shell to use in place of adb when connecting to Android devices"
url="https://github.com/facebook/fb-adb"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('android-tools')
makedepends=('git' 'prelink' 'zip')
options=('!strip' '!buildflags')
install=${pkgname}.install

_ndkver=10b
_source=("git+${url}.git#branch=master")
_sha512sums=('SKIP')
if [ -z "$ANDROID_NDK" ]; then
    if [[ "$CARCH" = "i686" ]]; then
        _source+=("http://dl.google.com/android/ndk/android-ndk32-r${_ndkver}-linux-x86.tar.bz2")
        _sha512sums+=('ecd270244c9f00b9bf883014f65a04459a5cd422e65239b13a463c363b9e82d7907495792bbe1287dee082117c03356b1f8d869339a4cf1966f2a56ae664aeb9')
    else
        _source+=("http://dl.google.com/android/ndk/android-ndk32-r${_ndkver}-linux-${CARCH}.tar.bz2")
        _sha512sums+=('6b7ec25a7c2d6cef94ba61403274276a912c5fa757dda5206548619fb155e97bd6eab0eb3354f51f703b82300a754ec8da352aeb20cdcdf80884aaa4bcd97a6f')
    fi
fi
source=(${_source[@]})
sha512sums=(${_sha512sums[@]})

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    [[ -z "$ANDROID_NDK" ]] && export ANDROID_NDK="$srcdir"/android-ndk-r${_ndkver}

    # configure
    cd $_pkgname
    ./autogen.sh

    # build
    install -d build
    cd build
    ../configure
    make

    # compress android deployment binary
    cd stub-arm
    zip ${_pkgname}-android-arm.zip ${_pkgname}
}

package() {
    install -Dm755 ${_pkgname}/build/${_pkgname} "$pkgdir"/usr/bin/${_pkgname}
    install -Dm755 ${_pkgname}/build/stub-arm/${_pkgname}-android-arm.zip "$pkgdir"/usr/share/${_pkgname}/${_pkgname}-android-arm-${pkgver}.zip
    echo "Extract '${_pkgname}-android-arm-${pkgver}.zip' and copy 'fb-adb' to '/system/bin/fb-adb' on your Android device" > "$pkgdir"/usr/share/${_pkgname}/README
}
