# Maintainer: Robert Knauer <robert@privatdemail.net>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-pae
pkgver=340.32
_extramodules=extramodules-3.15-pae
pkgrel=1
pkgdesc="NVIDIA drivers for linux-pae"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-pae>=3.15' 'linux-pae<3.16' 'nvidia-libgl' "nvidia-utils=${pkgver}")
makedepends=('linux-pae-headers>=3.15' 'linux-pae-headers<3.16')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)
source=()
sha256sums=()

if [ "$CARCH" = "i686" ]; then
  _arch='x86'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
  source+=(
    "http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
  )
  sha256sums+=(
    '1d0489c35c5b332c9b949d9de3c3cbab4ac3a94385aa41e7a7b62ef23f4395f5'
  )
elif [ "$CARCH" = "x86_64" ]; then
  _arch='x86_64'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
  source+=(
    "http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
  )
  sha256sums+=(
    '00efb4717f224e54710d0025588003b99827bb0010e3c9c10ffe04344ebfd9d9'
  )
fi

prepare() {
  cd "${srcdir}"
  rm -rf "${_pkg}"
  sh "${_pkg}.run" --extract-only
  cd "${_pkg}"
}

build() {
  _kernver="$(cat /lib/modules/${_extramodules}/version)"
  cd "${srcdir}/${_pkg}/kernel"
  make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
  install -d -m755 "${pkgdir}/etc/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-pae.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
