# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from xfce4-appfinder package)
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

_pkgname=xfce4-appfinder
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="Xfce application finder"
arch=(i686 x86_64)
url=http://www.xfce.org/projects/$_pkgname
license=(GPL2)
groups=(xfce4-git)
depends=(garcon-git libxfce4ui-git)
makedepends=(git)
provides=($_pkgname=4.10.0)
conflicts=($_pkgname)
options=(!libtool)
install=$_pkgname.install

_gitroot=git://git.xfce.org/xfce/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh\
    --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static \
        --disable-debug
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
