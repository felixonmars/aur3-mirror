# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-qt-git
pkgver=20120807
pkgrel=1
pkgdesc="A library for Qt-based Telepathy clients"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
license=('LGPL')
depends=('qt' 'telepathy-farstream')
makedepends=('libxslt' 'python2' 'cmake' 'git' 'doxygen')
conflicts=('telepathy-qt' 'telepathy-qt4-git')
provides=('telepathy-qt' 'telepathy-qt4-git')
options=('!libtool')

_gitroot="git://git.collabora.co.uk/git/freedesktop.org-mirror/telepathy/telepathy-qt.git"
_gitname="telepathy-qt"

build() {
  cd ${srcdir}

  msg "Connecting to the GIT server...."
  
  if [[ -d ${srcdir}/$_gitname ]] ; then
    cd $_gitname
    git checkout -- .
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  
  msg "GIT checkout done"
  msg "Patching..."
  cd ${srcdir}/${_gitname}

  msg "Starting configure..."

  rm -rf ${srcdir}/build && mkdir ${srcdir}/build
  cd ${srcdir}/build

  cmake ../$_gitname \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DENABLE_TESTS=false
  
  msg "Starting make..."
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR="$pkgdir" install
}
