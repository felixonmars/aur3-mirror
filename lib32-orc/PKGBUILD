# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=orc
pkgname=lib32-$_name
pkgver=0.4.16
pkgrel=1
pkgdesc="The Oild Runtime Compiler. Multilib"
arch=('x86_64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('lib32-glibc' "$_name")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://code.entropywave.com/download/$_name/$_name-$pkgver.tar.gz")
md5sums=('e482932e544c847761449b106ecbc483')

build() {
  cd "$srcdir/$_name-$pkgver"

  # multilib
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Removing unneeded files
  rm -rf ${pkgdir}/usr/{bin,include,share}

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
