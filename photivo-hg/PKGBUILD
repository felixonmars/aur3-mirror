# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
pkgname=photivo-hg
pkgver=787
pkgrel=1
pkgdesc="Free and open source photo processor"
arch=('i686' 'x86_64')
url="http://photivo.org/"
license=('GPL3')
depends=('exiv2' 'qt' 'lcms2' 'fftw' 'graphicsmagick-qd16' 'lensfun' 'liblqr' 'shared-mime-info')
optdepends=('gimp: Gimp plugins' 'python2: Gimp to Photivo plugin')
makedepends=('mercurial' 'gimp')
provides=('photivo')
conflicts=('photivo')
install=photivo.install
source=()
md5sums=()

_hgroot="https://photivo.googlecode.com/hg/"
_hgrepo="photivo"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u || return 1
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir/$_hgrepo-build" ]; then
    rm -rf "$srcdir/$_hgrepo-build"
  fi
  cp -a "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  # remove the need for ccache
  find . -type f -name "*.pro" -exec sed -i 's|ccache||' '{}' ';'

  qmake PREFIX=/usr
  make

  # fix the "Gimp to Photivo" plugin
  sed -i -e 's|\x0D$||' -e 's|#!.*|#!/usr/bin/python2|' "mm extern photivo.py"
}

package() {
  cd "$srcdir/$_hgrepo-build"

  make INSTALL_ROOT="$pkgdir" install
  rm -rf "$pkgdir"/home

  # GIMP integration
  install -D -m755 ptGimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/ptGimp"
  install -D -m755 "mm extern photivo.py" "$pkgdir/usr/lib/gimp/2.0/plug-ins/mm-extern-photivo.py"
}
