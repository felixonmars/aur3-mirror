
pkgname=mingw-w64-celt
pkgver=0.11.3
pkgrel=1
pkgdesc="Low-latency audio communication codec (mingw-w64)"
arch=(any)
url="http://www.celt-codec.org/"
license=(BSD)
depends=('mingw-w64-crt' 'mingw-w64-libogg')
makedepends=('mingw-w64-gcc')
options=('staticlibs' '!buildflags' '!strip')
source=(http://downloads.us.xiph.org/releases/celt/celt-$pkgver.tar.gz)
md5sums=('5511732a426cc42bf986ca79b5cdd02f')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/celt-$pkgver"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --enable-custom-modes \
                 --disable-shared # FIXME: x86_64 dll is not created
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/celt-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
#    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
