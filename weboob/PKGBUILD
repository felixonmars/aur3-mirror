# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=weboob
pkgver=0.8.2
pkgrel=1
pkgdesc="Web Out Of Browsers provides several applications to interact with a lot of websites."
arch=('any')
url="http://weboob.org/"
license=('GPL')
depends=('python2-qt' 'python2-elementtidy' 'python2-prettytable' 'python2-webob'
         'python-clientform' 'python-dateutil' 'python2-feedparser' 'python-gdata'
         'python-html2text' 'python-html5lib' 'python-imaging' 'python-lxml'
         'python-mako' 'python-mechanize' 'python2-nose' 'python-pysqlite' 'pygtk'
         'python-routes' 'python-yaml' 'pywebkitgtk' 'pyxdg')
optdepends=('python-simplejson: simplejson backend')
conflicts=('weboob-git')
install="$pkgname.install"
source=("http://weboob.org/attachments/download/129/$pkgname-$pkgver.tar.gz")
md5sums=('9cb9d61dee49fc2b5ceb1dbe36c63576')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fixes
  export PYTHON="python2"
  sed -i "s_env python_&2_" `grep -rl "env python" .`

  # remove forced white background property
  sed -i "48,50d" weboob/applications/qvideoob/ui/video.ui
  sed -i "146,150d" weboob/applications/qvideoob/ui/main_window.ui
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir"
}
