# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: lang2 <wenzhi.liang@gmail.com>

pkgname=python-pycparser
pkgver=2.10
pkgrel=1
pkgdesc='C parser and AST generator written in Python'
url='https://github.com/eliben/pycparser'
makedepends=('git')
depends=('python' 'python-ply')
arch='any'
license=('BSD')
source=('https://github.com/eliben/pycparser/archive/release_v2.10.zip')
sha256sums=('1217244f882c330782f4762a1fb37cef21a929ce0123ac388e7e367c35ddbae3')

prepare() {
  cd "${srcdir}/pycparser-release_v${pkgver}"

  # generate lextab.py and yacctab.py
  python -c 'import pycparser; pycparser.CParser()'
  mv lextab.py yacctab.py pycparser
}

build() {
  cd "${srcdir}/pycparser-release_v${pkgver}"

  python setup.py build
}

package() {
  cd "${srcdir}/pycparser-release_v${pkgver}"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
