# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=mosml-git
pkgver=2.00.10.r226.gc25e10e
pkgrel=1
pkgdesc='Moscow ML is a light-weight implementation of Standard ML (SML)'
url="http://www.itu.dk/~sestoft/mosml.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('git')
provides=('mosml')
conflicts=('mosml')

source=('git+https://github.com/kfl/mosml')
md5sums=('SKIP')

pkgver() {
    cd mosml
    git describe --tags | sed -E 's/^r//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
    cd mosml/src
    make BINDIR=/usr/bin LIBDIR=/usr/lib/mosml world
}

package() {
    cd mosml/src
    make PREFIX=/usr DESTDIR="$pkgdir" install

    # This isn't done by the Makefile
    cd ../man
    for manual in *.1; do
        install -Dm644 "$manual" "$pkgdir/usr/share/man/man1/$manual"
    done
}

