# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname='profile-cleaner'
pkgver=2.32
pkgrel=1
pkgdesc='Reduces profile size by cleaning their sqlite databases.'
arch=('any')
url='https://github.com/graysky2/profile-cleaner'
license=('MIT')
depends=('bash' 'bc' 'coreutils' 'findutils' 'grep' 'ncurses' 'parallel' 'sed'
'sqlite')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz")
install=readme.install
sha256sums=('a506633d83f841e548e8737f5c5f154d9b85165b6adc462f0757d0db556a453e')

build() {
	cd "$pkgname-$pkgver"
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
