# Maintainer: Duncan Townsend <duncant@mit.edu>
pkgname=electrum-server-git
pkgver=20121017
pkgrel=1
pkgdesc="Server to support Electrum python-based Bitcoin client"
arch=('x86_64') # not tested on i686
url="http://electrum-desktop.com/"
license=('AGPL3')
groups=('electrum')
depends=('python2' 'python2-jsonrpclib-git' 'python2-bitcoin-abe-git' 'python2-pyopenssl')
makedepends=('git')
optdepends=('electrum-git: client')
provides=('electrum-server')
conflicts=('electrum-server')
replaces=()
backup=('etc/electrum.conf')
options=()
install="${pkgname}.install"
source=()
noextract=()

_gitroot='git://github.com/spesmilo/electrum-server.git'
_gitname='electrum-server'

# change this if you'd like to build a different branch
_gitbranch='master'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin $_gitbranch
    msg "The local files are updated."
  else
    git clone -b $_gitbranch "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  sed -i -e 's#/usr/bin/env python#/usr/bin/env python2#' server.py transports/stratum_http.py
}

package() {
  cd "$srcdir/$_gitname-build"

  DOC_DIR="${pkgdir}/usr/share/doc/${pkgname%-git}"
  install -d "${DOC_DIR}"
  install -m644 *.md "${DOC_DIR}/"

  DATA_DIR="${pkgdir}/usr/share/${pkgname%-git}"
  install -d "${DATA_DIR}"
  install -m644 processor.py version.py "${DATA_DIR}/"
  install -m755 server.py "${DATA_DIR}/"
  cp -r transports "${DATA_DIR}/"
  install -d "${DATA_DIR}/backends/"
  cp -r backends/abe "${DATA_DIR}/backends/"
  cp -r backends/irc "${DATA_DIR}/backends/"
  install -m644 backends/__init__.py "${DATA_DIR}/backends/"
  # ignore backends/libbitcoin/

  CONF_DIR="${pkgdir}/etc"
  install -d "${CONF_DIR}"
  install -m640 electrum.conf.sample "${CONF_DIR}/electrum.conf"

  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/share/${pkgname%-git}/server.py" "${pkgdir}/usr/bin/electrum-server"

  # ignore patches/
  # ignore start
  # ignore stop
}
