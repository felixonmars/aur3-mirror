# Maintainer: peace4all <markspost at rocketmail dot com>
# Contributer: Alucryd <alucryd at gmail dot com> 
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>

pkgname=gufw
pkgver=12.10.0
pkgrel=4
pkgdesc="An easy, intuitive way to manage your Linux firewall"
arch=('any')
url="http://gufw.tuxfamily.org/index.html"
license=('GPL3')
depends=('dbus' 'dbus-python' 'gksu' 'gtk3' 'hicolor-icon-theme' 'notification-daemon' 'polkit' 'python2-gobject' 'ufw')
makedepends=('python2-distutils-extra')
install=$pkgname.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/gui-ufw_${pkgver}.orig.tar.gz" 
	"$pkgname-$pkgver-execute.patch")
md5sums=('2d6cc0f1b26af81c39a27236bb247454'
	 '3c71725122155b44802ceabfa56923ba')

package() {
  cd "${srcdir}/gui-ufw-${pkgver}"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
      sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
      sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py build install --root="${pkgdir}"

  # apply archlinux fixes
  cd "${pkgdir}"
  patch -Np0 -i "${srcdir}/${pkgname}-${pkgver}-execute.patch"

  # fix permissions
  find "${pkgdir}" -type d | xargs chmod 755
  find "${pkgdir}" -type f | xargs chmod 644
  chmod -R 755 "${pkgdir}/usr/bin"
  chmod 755 "${pkgdir}/usr/share/gufw/daemon/gufw_daemon.py"
}
