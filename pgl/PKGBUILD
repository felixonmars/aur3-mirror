# Maintainer: Pierre Buard <pierre.buard+aur gmail com>
pkgname=pgl
pkgver=2.2.1
pkgrel=2
pkgdesc='PeerGuardian is a privacy oriented firewall application (GUI).'
url='http://sourceforge.net/projects/peerguardian/'
arch=('i686' 'x86_64')
depends=('cron' 'logrotate' 'wget' 'libnetfilter_queue' 'iptables' 'qt' 'polkit-qt')
optdepends=('unzip: for zipped blocklists'
	    'p7zip: for 7z blocklists'
	    'net-tools: to whitelist local IP addresses'
	    'networkmanager: to whitelist newly started network interfaces'
	    'smtp-forwarder: to send reports')
license=('GPL')
replace=('pgl-nogui')
conflicts=('blockcontrol' 'moblock' 'pgl-cli')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf'
	'etc/logrotate.d/pglcmd'
	'etc/logrotate.d/pgld')
source=(http://downloads.sourceforge.net/project/peerguardian/PeerGuardian%20Linux/${pkgver}/${pkgname}-${pkgver}.tar.gz
	pgl.service
	pgl-update.service
	pgl-update.timer)
md5sums=('ee2b8d69f0966b7b79e16864efe115cb'
	 '2d9ff2bfeb45f5282eaf9bccffc66016'
	 '864845d43b0b15e9545cd772dbd655dc'
	 '36106bc30b272057f2502486a7e7a05f')
install=pgl.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}/

    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
		--with-initddir=/etc/rc.d --with-piddir=/run
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}/
    make DESTDIR=${pkgdir} install

    install -Dm644 ${srcdir}/pgl.service ${pkgdir}/usr/lib/systemd/system/pgl.service
    install -Dm644 ${srcdir}/pgl-update.service ${pkgdir}/usr/lib/systemd/system/pgl-update.service
    install -Dm644 ${srcdir}/pgl-update.timer ${pkgdir}/usr/lib/systemd/system/pgl-update.timer
}
