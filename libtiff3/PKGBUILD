# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=libtiff3
pkgver=3.9.5
pkgrel=2
pkgdesc="Legacy version of the library for manipulation of TIFF images"
arch=('i686' 'x86_64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('libjpeg' 'zlib' 'libtiff')
makedepends=('mesa-libgl' 'freeglut' 'libxmu' 'libxi' )
optdepends=('freeglut: for using tiffgt')
options=('!libtool')
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz)
md5sums=('8fc7ce3b4e1d0cc8a319336967815084')
sha1sums=('f40aab20fb2f609b5cbc1171c40b66a1445e3773')

build() {
  cd "${srcdir}/tiff-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYRIGHT "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  # We want only version3. Avoid conflict with 'libtiff' package
  rm -rf "${pkgdir}/usr/lib/libtiff".{a,so}
  rm -rf "${pkgdir}/usr/lib/libtiffxx".{a,so}
  # We want only libraries
  rm -rf "${pkgdir}/usr"/{bin,include,share/doc,share/man}
}
