# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Panagiotis Papadopoulos (pano_90 <at> gmx <dot> net)

pkgname=kwave
pkgver=0.8.5
pkgrel=4
pkgdesc="A sound editor for KDE"
arch=('i686' 'x86_64')
url="http://kwave.sourceforge.net/"
license=('GPL')
depends=('audiofile' 'flac' 'fftw' 'kdelibs' 'libvorbis' 'libsamplerate')
makedepends=('cmake' 'gettext' 'imagemagick' 'kdesdk-poxml' 'automoc4')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-1.tar.bz2
        ${pkgname}-qt47.patch)
md5sums=('6328cf26b9e81b159c8fbf7d694c9b8a'
         '3ec612fdf03a723d724adfe39320a99a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #patch to build with Qt 4.7
  patch -Np1 -i "${srcdir}/${pkgname}-qt47.patch"
  mkdir ../build
  cd ../build

  cmake ../${pkgname}-${pkgver} -DCMAKE_INSTALL_PREFIX=/usr \
  -DLIB_INSTALL_DIR=/usr/lib \
  -DCMAKE_BUILD_TYPE=Release \
  -DWITH_PULSEAUDIO=OFF \
  -DWITH_PHONON=OFF \
  -DWITH_MP3=OFF \
  -DWITH_DOC=OFF
  #disable Phonon support, since it currently does not work, and only makes Kwave crash 
  #disable MP3 support, since it doesn't compile although dependency id3lib is installed
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
