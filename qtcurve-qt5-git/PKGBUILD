# Maintainer: Yichao Yu <yyc1992@gmail.com>

pkgbase=qtcurve-qt5
pkgname=${pkgbase}-git
_realver=1.8.15
pkgver=2113.87db511
pkgrel=1
epoch=1
pkgdesc='A configurable set of widget styles for KDE and Gtk. Qt5 experimental port version. Not finished, with known bugs. Patch/help welcome.'
arch=('i686' 'x86_64')
url="http://craigd.wikispaces.com/"
license=('GPL')
groups=('qtcurve')
depends=('qt5-base' 'qt5-svg' 'libxcb')
makedepends=('cmake')
provides=("${pkgbase}=${_realver}")
conflicts=("${pkgbase}")
options=('debug' 'strip')

_gitname="qtcurve-qt5"

_gitroot="git://github.com/yuyichao/qtcurve-qt5.git"
_gitref=master

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  local outfile=/dev/null
  [[ -e /dev/tty ]] && outfile=/dev/tty
  (_fetch_git &> ${outfile})
  cd "$srcdir/$_gitname"

  echo "$(git rev-list --count "${_gitref}").$(git rev-parse --short "${_gitref}")"
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  mkdir -p build
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_gitname/build"

  make DESTDIR="$pkgdir/" install
}
