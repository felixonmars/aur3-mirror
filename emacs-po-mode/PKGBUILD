# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgname=emacs-po-mode
_pkgname=gettext
pkgver=0.18.1.1
pkgrel=5
pkgdesc="Emacs po-mode for editing gettext .po files (gettext-tools)"
url="http://www.gnu.org/software/gettext/"
license=("GPL")
arch=('any')
depends=('gettext' 'libcroco' 'expat')
makedepends=('emacs')
install=${pkgname}.install
changelog=ChangeLog
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${_pkgname}-${pkgver}.tar.gz
        po-mode-2.21.patch)
md5sums=('3dd55b952826d2b32f51308f2f91aa89'
         'f5750e6bf1d4b76c0394959516afadcd')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/gettext-tools/misc/"

  patch -p3 -i "${srcdir}/po-mode-2.21.patch"
  emacs -batch -f batch-byte-compile po-compat.el po-mode.el
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/gettext-tools/misc/"

  install -d "${pkgdir}/usr/share/emacs/site-lisp"
  install -m644 po-mode.el{,c} po-compat.el{,c} \
             -t "${pkgdir}/usr/share/emacs/site-lisp"
}
