# Based on linux-firmware by Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: xduugu
pkgname=linux-firmware-git
pkgver=20130409
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
makedepends=('git')
conflicts=('linux-firmware'
           'kernel26-firmware'
           'carl9170-fw')
provides=("linux-firmware=$pkgver")
options=(!strip)
source=("git://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git")
md5sums=('SKIP')

pkgver() {
  date -u +%Y%m%d
}

package() {
  cd "$srcdir/${pkgname%-*}"
  install -dm755 "$pkgdir/usr/lib/firmware" "$pkgdir/usr/share/licenses/$pkgname"
  cp -a * "$pkgdir/usr/lib/firmware/"

  rm -rf "$pkgdir/usr/lib/firmware/"{carl9170fw,configure,GPL*}
  mv "$pkgdir/usr/lib/firmware"/{LICEN*,WHENCE} "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
