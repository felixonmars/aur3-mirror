# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributer: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=nessus
pkgver=5.0.2
pkgrel=2
_bigver=Nessus-$pkgver-fc16
pkgdesc="Vulnerability scanner"
arch=('i686' 'x86_64')
depends=('openssl' 'gnupg')
license=('custom')
url="http://www.nessus.org"
install=nessus.install

# Download the rpm from <http://tenable.com/products/nessus/nessus-download-agreement>
# 32-bit: Nessus-5.0.2-fc16.i386.rpm
# 64-bit: Nessus-5.0.2-fc16.x86_64.rpm
source=($_bigver.$CARCH.rpm
        nessus.sh
        LICENSE.NESSUS
        nessus.install)

md5sums=('dee07ed774aaa8b77726bbcb25951cb5'
         '8c5772ac63f97d94475fe03e80d6ba5c'
         '8ff98bc9488304fcb66753d3cfb5f30e'
         'd9d25d4075acc50c0050c7a4f244c7cb')

if [[ $CARCH = i686 ]]; then
  md5sums[0]='4991e918fe258cc325aec20ecdeddbc0'
fi

package() {
  install -Dm755 $pkgname.sh "$pkgdir/etc/profile.d/$pkgname.sh"

  mkdir -p "$pkgdir/etc/ld.so.conf.d" "$pkgdir/usr/share" "$pkgdir/opt/nessus" \
           "$pkgdir/usr/lib"

  cp -a lib "$pkgdir/usr"

  # path to libraries
  echo /opt/nessus/lib > "$pkgdir/etc/ld.so.conf.d/nessus.conf"

  # man pages
  cp -a opt/nessus/man "$pkgdir/usr/share/man"

  # main files
  cp -a opt/nessus/{bin,com,etc,lib,sbin,var} "$pkgdir/opt/nessus"

  # license
  install -Dm644 LICENSE.NESSUS "$pkgdir/usr/share/licenses/nessus/LICENSE.NESSUS"
}
