# Maintainer: TDY <tdy@archlinux.info>

pkgname=chapel-svn
pkgver=20956
pkgrel=1
pkgdesc="An emerging parallel programming language from Cray Inc designed for high-end computing"
arch=('i686' 'x86_64')
url="http://chapel.cray.com/"
license=('BSD' 'GPL' 'custom')
depends=('bc' 'python2')
makedepends=('subversion')
provides=('chapel')
conflicts=('chapel')
options=('!emptydirs')
install=chpl.install
source=(chplenv.sh)
sha256sums=('cf63d9d656a96acde00151391b61623ae57c8864134ca70ff9fad2c3f78bcf33')

_svntrunk=svn://svn.code.sf.net/p/chapel/code/trunk
_svnmod=chapel

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d $_svnmod/.svn ]]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf $_svnmod-build
  svn export $_svnmod $_svnmod-build
  cd $_svnmod-build

  find -type f -exec sed -i '1s/python/&2/' '{}' \;
  make depend all
}

package() {
  cd "$srcdir/$_svnmod-build"

  # docs
  install -dm755 "$pkgdir/usr/share/doc/$_svnmod/technotes/"
  install -m644 doc/release/technotes/* "$pkgdir/usr/share/doc/$_svnmod/technotes/"
  install -m644 doc/release/chapelLanguageSpec.pdf "$pkgdir/usr/share/doc/$_svnmod/"
  install -m644 doc/release/quickReference.pdf "$pkgdir/usr/share/doc/$_svnmod/"
  install -m644 doc/release/README* "$pkgdir/usr/share/doc/$_svnmod/"
  install -Dm644 man/man1/chpl.1 "$pkgdir/usr/share/man/man1/chpl.1"

  # dist
  install -dm755 "$pkgdir/opt/$_svnmod/"
  cp -a bin lib make modules runtime third-party util "$pkgdir/opt/$_svnmod/"
  install -Dm755 ../chplenv.sh "$pkgdir/etc/profile.d/chplenv.sh"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_svnmod/LICENSE"

  # vim
  install -Dm644 etc/vim/ftdetect/chpl.vim "$pkgdir/usr/share/vim/vimfiles/ftdetect/chpl.vim"
  install -Dm644 etc/vim/indent/chpl.vim "$pkgdir/usr/share/vim/vimfiles/indent/chpl.vim"
  install -Dm644 etc/vim/syntax/chpl.vim "$pkgdir/usr/share/vim/vimfiles/syntax/chpl.vim"
  install -Dm644 etc/vim/README "$pkgdir/usr/share/doc/$_svnmod/vim/README"
  install -Dm644 etc/vim/sample-vimrc "$pkgdir/usr/share/doc/$_svnmod/vim/vimrc"
  install -Dm644 etc/vim/LICENSE "$pkgdir/usr/share/licenses/$_svnmod/LICENSE.vim"

  # emacs
  install -Dm644 etc/emacs/22/chpl-mode.el "$pkgdir/usr/share/emacs/chpl-mode.el"
  install -Dm644 etc/emacs/COPYING "$pkgdir/usr/share/licenses/$_svnmod/LICENSE.emacs"

  # cleanup
  cd "$pkgdir/opt/$_svnmod"
  find -type d -name docs -o -name man -exec rm -r '{}' +

  msg2 "Enforcing text file permissions..."
  for _file in $(find -type f); do
    (head -n1 "$_file" | grep '^#!' &>/dev/null) && chmod 755 "$_file" || chmod 644 "$_file"
  done

  msg2 "Enforcing directory and ELF permissions..."
  chmod 755 $(find ! -name '*.o' -exec file '{}' \; | grep -E 'ELF|directory' | cut -d: -f1)
}

# vim:set ts=2 sw=2 et:
