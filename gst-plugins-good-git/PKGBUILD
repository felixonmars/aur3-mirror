# Original Package: Jan de Groot <jgc@archlinux.org>
# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

pkgname=gst-plugins-good-git
pkgver=1.0.2.627.g6714d8c
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('i686' 'x86_64')
license=('LGPL')
provides=('gst-plugins-good=99')
conflicts=('gst-plugins-good')
url="http://gstreamer.freedesktop.org/"
depends=('libpulse' 'jack' 'libsoup' 'gst-plugins-base-git' 'wavpack' 'aalib' 'taglib' 'libdv' 'libshout' 'libvpx' 'gdk-pixbuf2' 'libcaca' 'libavc1394' 'libiec61883' 'libxdamage' 'v4l-utils' 'cairo')
makedepends=('gstreamer' 'speex' 'flac' 'libraw1394')
options=(!libtool !emptydirs)

source='git://anongit.freedesktop.org/gstreamer/gst-plugins-good'
sha256sums=('SKIP')

_gitname='gst-plugins-good'

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname
  export CFLAGS="-Wno-error"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental
  make
}

package() {
  cd $_gitname
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}
