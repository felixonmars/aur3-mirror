# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=uuterm-svn
pkgver=80
pkgrel=2
pkgdesc="A portable VT100-like terminal emulator with minimal code and memory footprint and maximal Unicode support."
arch=('i686' 'x86_64')
url="http://www.etalabs.net/uuterm.html"
license=('GPL2')
depends=('libx11' 'ncurses')
makedepends=('subversion' 'sh')
provides=('uuterm')
conflicts=('uuterm')
install=uuterm.install

_svntrunk=svn://svn.mplayerhq.hu/uuterm/trunk/
_svnmod=uuterm

build() {
  cd "$srcdir"

  if [[ -d $_svnmod/.svn ]]; then
    cd $_svnmod && svn up -r $pkgver
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # honor build flags
  for flags in {C,LD}FLAGS; do
    [[ ${!flags} ]] && printf '%s = %s\n' "$flags" "${!flags}"
  done > config.mak

  # default target only builds uuterm-x11
  make all uuterm-fb
}

package() {
  cd "$srcdir/$_svnmod-build"

  # compile terminfo
  install -dm755 "$pkgdir/usr/share/terminfo"
  tic -o "$pkgdir/usr/share/terminfo" uuterm.ti

  # ucf is the new hotness
  install -Dm644 ytty/ytty.ucf "$pkgdir/usr/share/fonts/ucf/ytty.ucf"

  install -Dm755 uuterm-x11 "$pkgdir/usr/bin/uuterm-x11"
  install -Dm755 uuterm-fb "$pkgdir/usr/bin/uuterm-fb"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/uuterm/COPYING"
}
