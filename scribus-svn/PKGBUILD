# Maintainer: Simo Huhtiranta <simo_huhtirantaATpp_inet_fi>  
# Contributor: Tilmann Becker <tilmann.becker@web.de> 

pkgname=scribus-svn
pkgver=17756
pkgrel=1
pkgdesc="A desktop publishing program - Version from SVN"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.scribus.net"
install=${pkgname}.install
depends=( 'qtwebkit'      \
  'libcups>=1.2.10'       \
  'lcms2'                 \
  'ghostscript>=8.60'     \
  'libart-lgpl>=2.3.19'   \
  'aspell'                \
  'poppler'               \
  'python2'               \
  'graphicsmagick'        \
  'shared-mime-info')
makedepends=('subversion' 'cmake')
conflicts=('scribus')
provides=('scribus')
source=(${pkgname}.desktop)
md5sums=('aec1cd630e78fe47df07b0ce4a725f7d')
options=('!emptydirs')
_svntrunk='svn://scribus.net/trunk'
_svnmod='Scribus'

build() {
  cd $srcdir

  if [ -d $_svnmod/.svn ]; then
	  (cd $_svnmod && svn up -r $pkgver)
  else
	  svn co $_svntrunk --config-dir ./ $_svnmod -r $pkgver
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  
  [ -d $srcdir/$_svnmod-build ] && rm -rf $srcdir/$_svnmod-build
  mkdir $_svnmod-build
  cd $_svnmod-build

  cmake ../$_svnmod/Scribus -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DWANT_SYSTEM_CAIRO=1 -DCMAKE_SKIP_RPATH:BOOL=YES \
    -DWANT_GRAPHICSMAGICK=1 -DCMAKE_INCLUDE_PATH=/usr/include/python2.7
  
  sed -i 's|-Wl,--as-needed||' scribus/CMakeFiles/scribus.dir/link.txt
  make
}

package () {
  cd $srcdir/$_svnmod-build
  make DESTDIR=$pkgdir install
  for _i in $pkgdir/usr/share/scribus/{scripts,samples}/*.py
  do
    sed -i '1s+python+python2+' ${_i}
  done
  install -Dm644 $srcdir/scribus-svn.desktop \
    $pkgdir/usr/share/applications/scribus-svn.desktop
}


