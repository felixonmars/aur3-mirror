# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=gdebugger
pkgver=6.2.438
pkgrel=1
pkgdesc="An advanced OpenGL and OpenCL debugger, profiler and memory analyzer"
arch=('i686' 'x86_64')
url="http://developer.amd.com/tools/heterogeneous-computing/amd-gdebugger/"
license=('custom')
depends=('libpng12' 'libgl' 'libcl')
options=(!strip)

# the sources must be manually downloaded from:
#   http://developer.amd.com/tools/heterogeneous-computing/amd-gdebugger/

if [ "$CARCH" = "i686" ]; then
  _arch=x86
  source=(gDEBugger_LNX_6.2_32.tar.gz \
          gDEBugger.sh gdebugger.png gdebugger.desktop)
  md5sums=('61542aa500d93f4389ab1eaaeb0cb3de'
           '4484ba47685f41d3ce465388e6c583bc'
           '7236dccfd1dc47aec9812fb449b243d2'
           'e0f9de6f04a29be8ff8b5deae6bb964f')
else
  _arch=x86_64
  source=(gDEBugger_LNX6.2_64.tar.gz \
          gDEBugger.sh gdebugger.png gdebugger.desktop)
  md5sums=('9c2e9f7ee425cc9279b7f99d19e80be6'
           '4484ba47685f41d3ce465388e6c583bc'
           '7236dccfd1dc47aec9812fb449b243d2'
           'e0f9de6f04a29be8ff8b5deae6bb964f')
fi

package() {
  install -d -m755 "$pkgdir/opt"
  cp -a gDEBugger$pkgver-$_arch  "$pkgdir/opt/gDEBugger"

  install -D -m755 "gDEBugger.sh" "$pkgdir/usr/bin/gDEBugger"
  install -D -m755 "gdebugger.png" "$pkgdir/usr/share/pixmaps/gdebugger.png"
  install -D -m755 "gdebugger.desktop" "$pkgdir/usr/share/applications/gdebugger.desktop"
  install -D -m644 "gDEBugger$pkgver-$_arch/Legal/EndUserLicenseAgreement.htm" "$pkgdir/usr/share/licenses/$pkgname/EULA.htm"

  # add symlink to libOpenCL.so.1 on 64bit, otherwise gDEBugger won't work
  # WARNING: if you built 64bit OpenCL applications while having this package installed, 
  # be aware that you may run into problems with them after uninstalling this package.
  # They may be linked to the symlink created by this package instead of /usr/lib/libOpenCL.so.1
  install -d -m755 "$pkgdir/usr/lib64"
  ln -sf /usr/lib/libOpenCL.so.1 "$pkgdir/usr/lib64/libOpenCL.so.1"
}

# vim:set ts=2 sw=2 et:
