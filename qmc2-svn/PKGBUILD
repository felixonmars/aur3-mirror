# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

## OPTIONS: 0=off 1=on ##

_use_sdlmame=0
_use_sdlmess=0
_use_sdlume=0
_use_arcade=0

#########################

[ "${_use_sdlmame}" = "1" -o "${_use_sdlmess}" = "1" -o "${_use_sdlume}" = "1" -o "${_use_arcade}" = "1" ] &&  _use+="qmc2-core-svn "
[ "${_use_sdlmame}" = "1" ] && _use+="qmc2-sdlmame-svn "
[ "${_use_sdlmess}" = "1" ] && _use+="qmc2-sdlmess-svn "
[ "${_use_sdlume}" = "1" ] && _use+="qmc2-sdlume-svn "
[ "${_use_arcade}" = "1" ] && _use+="qmc2-arcade-svn "

if [ "${_use_sdlmame}" = "1" -o "${_use_sdlmess}" = "1" -o "${_use_sdlume}" = "1" -o "${_use_arcade}" = "1" ]; then
  makedepends=('subversion' 'rsync' 'mesa' 'qtwebkit' 'phonon-qt4' 'sdl')
  _svnmod="qmc2"
else
  warning "Need choice, at least, one Option, Please edit OPTIONS in the PKGBUILD before build, if not build a EMPTY package"
fi

pkgbase=qmc2-svn
pkgname=('qmc2-svn' $_use)
pkgver=0.44.5947
pkgrel=1
pkgdesc="Qt 4 based UNIX MAME frontend supporting SDLMAME, SDLMESS or SDLUME. (SVN version) (SPLIT PKGBUILD)"
url="http://qmc2.arcadehits.net/wordpress/"
license=("GPL")
arch=('i686' 'x86_64')
source=("qmc2::svn://svn.code.sf.net/p/qmc2/code/trunk")
md5sums=('SKIP')

_buildopts="CTIME=0 PREFIX=/usr DATADIR=/usr/share SYSCONFDIR=/etc QTDIR=/usr QMAKE=qmake-qt4 LRELEASE=lrelease-qt4 LUPDATE=lupdate-qt4"

pkgver() {
  if [ "${_use_sdlmame}" = "1" -o "${_use_sdlmess}" = "1" -o "${_use_sdlume}" = "1" -o "${_use_arcade}" = "1" ]; then
    cd "${_svnmod}"
    _vmajor="$(grep -e "VERSION_MAJOR" Makefile -num 1 | cut -d " " -f3)"
    _vminor="$(grep -e "VERSION_MINOR" Makefile -num 1 | cut -d " " -f3)"
    echo "${_vmajor}.${_vminor}.$(svnversion)"
  else
    echo "EMPTY"
  fi
}

prepare() {
  if [ "${_use_sdlmame}" = "1" -o "${_use_sdlmess}" = "1" -o "${_use_sdlume}" = "1" -o "${_use_arcade}" = "1" ]; then
    cd "${srcdir}"
    rm -fr "${srcdir}/${_svnmod}/runonce/"runonce{,.o}
    rm -fr "${_svnmod}_sdlmame-build"
    rm -fr "${_svnmod}_sdlmess-build"
    rm -fr "${_svnmod}_sdlume-build"
    [ "${_use_sdlmame}" = "1" ] && cp -R ${_svnmod} "${_svnmod}_sdlmame-build"
    [ "${_use_sdlmess}" = "1" ] && cp -R ${_svnmod} "${_svnmod}_sdlmess-build"
    [ "${_use_sdlume}" = "1" ] && cp -R ${_svnmod} "${_svnmod}_sdlume-build"
    true
  fi
}

build() {
  if [ "${_use_sdlmame}" = "1" -o "${_use_sdlmess}" = "1" -o "${_use_sdlume}" = "1" ]; then
    pushd "${srcdir}/${_svnmod}/runonce/" &> /dev/null
    qmake-qt4
    make -f Makefile.qmake
    popd &> /dev/null
    if [ "${_use_sdlmame}" = "1" ]; then
      msg2 "Build QMC2 with SDLMAME compat"
      make -C "${srcdir}/${_svnmod}_sdlmame-build" ${_buildopts} EMULATOR=SDLMAME
    fi
    if [ "${_use_sdlmess}" = "1" ]; then
      msg2 "Build QMC2 with SDLMESS compat"
      make -C "${srcdir}/${_svnmod}_sdlmess-build" ${_buildopts} EMULATOR=SDLMESS
    fi
    if [ "${_use_sdlume}" = "1" ]; then
      msg2 "Build QMC2 with SDLUME compat"
      make -C "${srcdir}/${_svnmod}_sdlume-build" ${_buildopts} EMULATOR=SDLUME
    fi
    if [ "${_use_arcade}" = "1" ]; then
      msg2 "build Arcade Front End"
      make -C "${srcdir}/${_svnmod}" arcade ${_buildopts}
    fi
    msg2 "build CHDman Qt GUI"
    make -C "${srcdir}/${_svnmod}" qchdman ${_buildopts}
    msg2 "build manuals"
    make -C "${srcdir}/${_svnmod}" man ${_buildopts}
  fi
}

package_qmc2-svn() {
  arch=('any')
}

package_qmc2-core-svn() {
  packagedec=('Qt 4 based UNIX MAME frontend. Core files, included qCHDman GUI and Arcade Front End. (SVN version)')
  arch=('i686' 'x86_64')
  depends=('qt4')
  conflicts=('qmc2')
  provides=("qmc2-core-svn=${pkgver}")
  optdepends=('qmc2-sdlmame-svn: Frontend for SDLMAME (install from "qmc2-svn" package)'
              'qmc2-sdlmess-svn: Frontend for SDLMESS (install from "qmc2-svn" package)'
              'qmc2-sdlume-svn: Frontend for SDLUME (install from "qmc2-svn" package)'
              'qmc2-arcade-svn: Arcade frontend for SDLMAME/SDLMESS/SDLUME (install from "qmc2-svn" package)')

  pushd "${srcdir}/${_svnmod}/data/" &> /dev/null
  find . -type f -exec install -Dm644 "{}" "${pkgdir}/usr/share/qmc2/{}" \;
  ln -s /usr/share/qmc2/img/classic/*  "${pkgdir}/usr/share/qmc2/img/"
  popd &> /dev/null
  install -Dm644 "${srcdir}/${_svnmod}/inst/qmc2.ini.template" "${pkgdir}/etc/qmc2/qmc2.ini"
  sed 's|DATADIR|/usr/share|' -i "${pkgdir}/etc/qmc2/qmc2.ini"
  install -Dm755 "${srcdir}/${_svnmod}/runonce/runonce" "${pkgdir}/usr/bin/runonce"
  make -C "${srcdir}/${_svnmod}" ${_buildopts} DESTDIR="${pkgdir}" MAN_DIR="${pkgdir}/usr/share/man" qchdman-install man-install
  find "${pkgdir}/usr/share/man/man6" ! -type d ! -name "qchdman.6.gz" -a ! -name "runonce.6.gz" -a ! -name "qmc2-main-gui.6.gz" -a ! -name "qmc2.6.gz" -delete
}

package_qmc2-sdlmame-svn() {
  packagedec=('Qt 4 based UNIX MAME frontend for SDLMAME. (SVN version)')
  depends=("qmc2-core-svn=${pkgver}" 'qtwebkit' 'phonon-qt4')
  optdepends=('sdlmame: A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support')
  arch=('i686' 'x86_64')

  make -C "${srcdir}/${_svnmod}_sdlmame-build" ${_buildopts} EMULATOR=SDLMAME DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_svnmod}" ${_buildopts} DESTDIR="${pkgdir}" MAN_DIR="${pkgdir}/usr/share/man" man-install
  rm -fr "${pkgdir}/usr/bin/"{qmc2,runonce}
  rm -fr "${pkgdir}/etc"
  rm -fr "${pkgdir}/usr/share/qmc2"
  find "${pkgdir}/usr/share/man/man6" ! -type d ! -name "qmc2-sdlmame.6.gz" -delete
}

package_qmc2-sdlmess-svn() {
  packagedec=('Qt 4 based UNIX MAME frontend for SDLMESS. (SVN version)')
  depends=("qmc2-core-svn=${pkgver}" 'qtwebkit' 'phonon-qt4')
  optdepends=('sdlmess: Multiple Emulator Super System, computer and game console emulator using SDL with OpenGL support')
  arch=('i686' 'x86_64')

  make -C "${srcdir}/${_svnmod}_sdlmess-build" ${_buildopts} EMULATOR=SDLMESS DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_svnmod}" ${_buildopts} DESTDIR="${pkgdir}" MAN_DIR="${pkgdir}/usr/share/man" man-install
  rm -fr "${pkgdir}/usr/bin/"{qmc2,runonce}
  rm -fr "${pkgdir}/etc"
  rm -fr "${pkgdir}/usr/share/qmc2"
  find "${pkgdir}/usr/share/man/man6" ! -type d ! -name "qmc2-sdlmess.6.gz" -delete
}

package_qmc2-sdlume-svn() {
  packagedec=('Qt 4 based UNIX MAME frontend for SDLUME. (SVN version)')
  depends=("qmc2-core-svn=${pkgver}" 'qtwebkit' 'phonon-qt4')
  optdepends=('sdlume: Universal Machine Emulator combines the features of MAME and MESS into a single multi-purpose emulator')
  arch=('i686' 'x86_64')

  make -C "${srcdir}/${_svnmod}_sdlume-build" ${_buildopts} EMULATOR=SDLUME DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_svnmod}" ${_buildopts} DESTDIR="${pkgdir}" MAN_DIR="${pkgdir}/usr/share/man" man-install
  rm -fr "${pkgdir}/usr/bin/"{qmc2,runonce}
  rm -fr "${pkgdir}/etc"
  rm -fr "${pkgdir}/usr/share/qmc2"
  find "${pkgdir}/usr/share/man/man6" ! -type d ! -name "qmc2-sdlume.6.gz" -delete
}

package_qmc2-arcade-svn() {
  packagedec=('Qt 4 based UNIX MAME Arcade frontend for SDLMAME/SDLMESS/SDLUME. (SVN version)')
  depends=("qmc2-core-svn=${pkgver}" 'sdl' 'qt4')
  optdepends=('sdlmame: A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support'
              'sdlmess: Multiple Emulator Super System, computer and game console emulator using SDL with OpenGL support'
              'sdlume: Universal Machine Emulator combines the features of MAME and MESS into a single multi-purpose emulator')
  arch=('i686' 'x86_64')

  make -C "${srcdir}/${_svnmod}" ${_buildopts} DESTDIR="${pkgdir}" MAN_DIR="${pkgdir}/usr/share/man" arcade-install man-install
  rm -fr "${pkgdir}/usr/bin/"{qmc2,runonce}
  rm -fr "${pkgdir}/etc"
  rm -fr "${pkgdir}/usr/share/qmc2"
  find "${pkgdir}/usr/share/man/man6" ! -type d ! -name "qmc2-arcade.6.gz" -delete
}