# Maintainer: jarda-wien <xstej70@gmail.com>
# Former maintainer: Jason Melton <jason.melton@gmail.com>
# Contributor: Atilla ÖNTAŞ <tarakbumba@gmail.com>

pkgname=dkms-nvidia
_pkgname=nvidia
pkgver=295.53
pkgrel=1
pkgdesc="NVIDIA dynamic kernel module (DKMS) drivers for kernel26."
arch=(i686 x86_64)
url="http://www.nvidia.com/"
license=('custom')
depends=('dkms')
optdepends=("nvidia-utils=${pkgver}")
provides=("nvidia=${pkgver}")
conflicts=("nvidia")
install=${_pkgname}.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
  source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('af353274ab078cb90aba428ca1f00f0e')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
  source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('c974ee20fc016c9fa9ef39fc2e298f8a')
fi

source[1]="dkms.conf"
md5sums[1]='555223e467dbcc8f74481046719c2d04'

build() {
	cd $srcdir
	sh ${_pkg}.run --extract-only
}

package() 
{
 	mkdir -p                                ${pkgdir}/usr/src/${_pkgname}-${pkgver}
 	cp -a       ${srcdir}/${_pkg}/kernel/*  ${pkgdir}/usr/src/${_pkgname}-${pkgver}
	cp          ${srcdir}/dkms.conf         ${pkgdir}/usr/src/${_pkgname}-${pkgver}

  install -d -m755 $pkgdir/etc/modprobe.d
  echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
}
