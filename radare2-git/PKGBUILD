pkgname="radare2-git"
pkgver=20120723
pkgrel=1
pkgdesc="A collection of tools with the aim to create a complete, portable, multi-architecture, unix-like toolchain for reverse engineering."
arch=('i686' 'x86_64')
url="http://radare.org"
license=('GPL3')
depends=('vala' 'valabind-git')
makedepends=('mercurial')
provides=('radare2' 'r2-bindings')
conflicts=('radare2' 'r2-bindings' 'radare2-hg')

_gitroot="git://github.com/radare/radare2.git"
_gitname="radare2"

build() {
  cd ${srcdir}

  if [ -d ${_gitname}-build ] ; then
    msg "Removing leftovers..."
    rm -rf ${_gitname}-build
  fi

  if [ -d ${_gitname} ]; then
    cd ${_gitname}
    git pull --rebase
  else
    git clone ${_gitroot} ${_gitname}
  fi

  cd ${srcdir}
  git clone ${_gitname} ${_gitname}-build
  cd ${_gitname}-build

  msg "Building radare2..."
  ./configure --prefix=/usr
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
  export CFLAGS="${CFLAGS//-fPIE -pie}"
  make

  msg "Building language bindings..."
  cd r2-bindings
  make clean
  PKG_CONFIG_PATH="$PKG_CONFIG_PATH:${srcdir}/${_gitname}-build/pkgcfg" ./configure --prefix=/usr --enable-devel --enable=python
  make python2
}

package() {
  cd ${srcdir}/${_gitname}-build

  make DESTDIR=${pkgdir} install
  install -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/* "${pkgdir}/usr/share/man/man1"

  cd r2-bindings
  make DESTDIR=${pkgdir} install-vapi
  make DESTDIR=${pkgdir} PYTHON=python2.7 install-python

  cd ${srcdir}
  rm -rf ${_gitname}-build
}
