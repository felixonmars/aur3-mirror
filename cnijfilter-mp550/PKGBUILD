# Maintainer: Stefan Hackl <stef.hackl@gmail.com>

pkgbase=('cnijfilter-mp550')
pkgname=('cnijfilter-mp550')
true && pkgname=('cnijfilter-common' 'cnijfilter-mp550')
_pkgname=cnijfilter
pkgver=3.20
pkgrel=4
url="http://software.canon-europe.com/products/0010757.asp"
arch=('i686' 'x86_64')
license=('custom')
makedepends=('rpmextract')
source=("http://files.canon-europe.com/files/soft37275/software/MP550_rpm_driver_pack.tar")
md5sums=('5d806a0b626cc6aff6f172b95e11e105')

build() {
  mkdir ${srcdir}/${_pkgname}-common
  mkdir ${srcdir}/${_pkgname}-mp550
  cd ${srcdir}
  tar -xf MP550_rpm_driver_pack.tar
  tar -xf ${_pkgname}-mp550series-${pkgver}-1-i386-rpm.tar.gz
  mv ${srcdir}/${_pkgname}-mp550series-${pkgver}-1-i386-rpm/packages/${_pkgname}-mp550series-${pkgver}-1.i386.rpm  ${srcdir}/${_pkgname}-mp550
  mv ${srcdir}/${_pkgname}-mp550series-${pkgver}-1-i386-rpm/packages/${_pkgname}-common-${pkgver}-1.i386.rpm ${srcdir}/${_pkgname}-common
  cd ${srcdir}/${_pkgname}-common
  rpmextract.sh ${_pkgname}-common-${pkgver}-1.i386.rpm
  cd ${srcdir}/cnijfilter-mp550
  rpmextract.sh ${_pkgname}-mp550series-${pkgver}-1.i386.rpm
}

package_cnijfilter-common() {
  pkgdesc="Canon IJ Printer Driver (Common package)"
  if [ "${CARCH}" = 'x86_64' ]; then
    depends=('lib32-libcups' 'lib32-popt')
  elif [ "${CARCH}" = 'i686' ]; then
    depends=('libcups' 'popt')
  fi
  install -d ${pkgdir}/usr/lib/cups/backend
  install -d ${pkgdir}/usr/local/bin
  install -d ${pkgdir}/usr/share/licenses/${_pkgname}
  install -Dm 755 ${srcdir}/${pkgname}/usr/lib/libcnnet.so.1.1.0 ${pkgdir}/usr/lib/libcnnet.so.1.1.0
  install -Dm 644 ${srcdir}/${pkgname}/usr/lib/bjlib/cnnet.ini ${pkgdir}/usr/lib/bjlib/cnnet.ini
  install -Dm 755 -t ${pkgdir}/usr/lib/cups/backend ${srcdir}/${pkgname}/usr/lib/cups/backend/*
  install -Dm 755 ${srcdir}/${pkgname}/usr/lib/cups/filter/pstocanonij ${pkgdir}/usr/lib/cups/filter/pstocanonij
  install -Dm 755 -t ${pkgdir}/usr/local/bin ${srcdir}/${pkgname}/usr/local/bin/*
  install -Dm 644 -t ${pkgdir}/usr/share/licenses/${_pkgname} ${srcdir}/${pkgname}/usr/share/doc/${_pkgname}-common-3.20/*
}

package_cnijfilter-mp550() {
  pkgdesc="Canon Printer Driver (For Multifunction MP550 series)"
  if [ "${CARCH}" = 'x86_64' ]; then
    depends=('lib32-libxml2' 'lib32-gtk2' 'lib32-popt' 'lib32-libpng12' 'cnijfilter-common=3.20')
  elif [ "${CARCH}" = 'i686' ]; then
    depends=('libxml2' 'gtk2' 'libpng12' 'popt' 'cnijfilter-common=3.20')
  fi
  install -d ${pkgdir}/usr/lib/bjlib
  install -d ${pkgdir}/usr/local/bin
  install -d ${pkgdir}/usr/local/share/cngpijmonmp550/pixmaps
  install -d ${pkgdir}/usr/local/share/printuimp550
  install -Dm 755 -t ${pkgdir}/usr/lib/ ${srcdir}/${pkgname}/usr/lib/*.*
  install -Dm 755 -t ${pkgdir}/usr/lib//bjlib ${srcdir}/${pkgname}/usr/lib/bjlib/*
  install -Dm 755 -t ${pkgdir}/usr/local/bin ${srcdir}/${pkgname}/usr/local/bin/*
  install -Dm 644 -t ${pkgdir}/usr/local/share/cngpijmonmp550/pixmaps/ ${srcdir}/${pkgname}/usr/local/share/cngpijmonmp550/pixmaps/*
  for locale in $(ls ${srcdir}/${pkgname}/usr/local/share/locale); do
    install -d ${pkgdir}/usr/local/share/locale/${locale}/LC_MESSAGES
    install -Dm 644 -t ${pkgdir}/usr/local/share/locale/${locale}/LC_MESSAGES ${srcdir}/${pkgname}/usr/local/share/locale/${locale}/LC_MESSAGES/*
  done
  install -Dm 644 -t ${pkgdir}/usr/local/share/printuimp550 ${srcdir}/${pkgname}/usr/local/share/printuimp550/*
  install -Dm 644 ${srcdir}/${pkgname}/usr/share/cups/model/canonmp550.ppd ${pkgdir}/usr/share/cups/model/canonmp550.ppd
}

pkgdesc="Canon Printer Driver (For Multifunction MP550 series)"
if [ "${CARCH}" = 'x86_64' ]; then
  depends=('lib32-libxml2' 'lib32-gtk2' 'lib32-popt' 'lib32-libpng12')
elif [ "${CARCH}" = 'i686' ]; then
   depends=('libxml2' 'gtk2' 'libpng12' 'popt')
fi