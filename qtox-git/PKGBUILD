# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=qtox
pkgname=${_pkgname}-git
pkgver=r811.525552a
pkgrel=1
pkgdesc="Powerful QT Tox client that tries to follow the Tox UI guidlines"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://github.com/tux3/qTox"
license=('GPL3')
depends=('tox-git>=r2823.83e31c1' 'qt5-base' 'desktop-file-utils' 'opencv' 'openal')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("$_pkgname")
install="${pkgname}.install"

source=("${_pkgname}::git+https://github.com/tux3/qTox.git"
        "${_pkgname}.desktop")
sha512sums=('SKIP'
            '844aeb51d3af84bb0a484cf3a6e63ff7cc5698d9ebceff53c485feea70f5b06ec89e0590d364b5e5431ec29539453c8e4e9b06fc7bf1ca41bbb2325a2c1274dc')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd $_pkgname
    install -d libs
    ln -s /usr/lib libs/
    ln -s /usr/include libs/
    sed -i 's|SMILEYPACK_DEFAULT_PATH "./smileys"|SMILEYPACK_DEFAULT_PATH "/usr/share/qtox/smileys"|' misc/smileypack.h
}

build() {
    cd $_pkgname
    install -d build
    cd build
    qmake-qt5 ..
    make
}

package() {
    install -Dm644 ${_pkgname}.desktop "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
    install -Dm644 ${_pkgname}/img/icon.png "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
    install -Dm755 ${_pkgname}/build/${_pkgname} "${pkgdir}/usr/bin/${_pkgname}"
    install -d "${pkgdir}/usr/share/${_pkgname}"
    cp -R ${_pkgname}/smileys "${pkgdir}/usr/share/${_pkgname}"
}
