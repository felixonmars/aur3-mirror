# Contributor: Dave Reisner <d@falconindy.com>

pkgname=pkgfile-git
pkgver=20120718
pkgrel=2
pkgdesc="a pacman .files metadata explorer"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
makedepends=('git' 'perl')
conflicts=('pkgfile' 'nosr')
provides=('pkgfile' 'nosr')
replaces=('nosr-git')
install=pkgfile.install

_gitroot="git://github.com/falconindy/pkgfile.git"
_gitname="pkgfile"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  ./configure
  make
}

package() {
  make -C "$srcdir/$_gitname-build" DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
