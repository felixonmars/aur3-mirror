# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor: totoloco <totoloco en gmail>

pkgname=vimprobable2-git
pkgver=c5936cc
pkgrel=1
pkgdesc="WWW browser that behaves like the Vimperator plugin available for Mozilla Firefox. It is based on the WebKit engine (using GTK bindings). It is a fork of the currently abandoned vimpression (former webkitbrowser)."
arch=('i686' 'x86_64')
url="http://www.yllr.net/vimprobable/"
license=('mit')
depends=('libwebkit>=1.1.11 gtk2 libsoup')
makedepends=('git')
provides=('vimprobable2')
conflicts=('vimprobable2')
_gitname="vimprobable"
_gitbranch="vimprobable2"
source=("$_gitname::git://git.code.sf.net/p/$_gitname/code")
md5sums=('SKIP')
epoch=1

pkgver() {
  cd "$_gitname"
  git describe --always
}

prepare() {
  cd "$srcdir/$_gitname"
  sed -i 's:LIBS = gtk+-2.0 webkit-1.0 libsoup-2.4:LIBS = gtk+-2.0 webkit-1.0 libsoup-2.4 javascriptcoregtk-1.0:' Makefile
  [[ -f "$SRCDEST"/config.h ]] && msg "Using your config.h" && cp "$SRCDEST"/config.h .
  [[ -f "$SRCDEST"/keymap.h ]] && msg "Using your keymap.h" && cp "$SRCDEST"/keymap.h .
}

build() {
  cd "$srcdir/$_gitname"
  make
}

package() {
  cd "$srcdir/$_gitname"
  make PREFIX=/usr MANDIR=/usr/share/man DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/share/licenses/"$_gitbranch"
  chmod 0755  "$pkgdir"/usr/share/licenses/"$_gitbranch"/
  install -m 644 LICENSE "$pkgdir"/usr/share/licenses/"$_gitbranch"/LICENSE
}
