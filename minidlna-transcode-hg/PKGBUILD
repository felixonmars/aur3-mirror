# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Biginoz < biginoz AT free point fr>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>
# Contributor: Matthias Sobczyk <matthias.sobczyk@googlemail.com>
# Contributor: Kamil Kaminski <kyle@kkaminsk.com>
pkgname=minidlna-transcode-hg
pkgver=16
pkgrel=2
pkgdesc="A DLNA/UPnP-AV Media server with transcode support"
arch=('i686' 'x86_64')
url="https://bitbucket.org/stativ/minidlna-transcode"
license=('GPL')
depends=('libexif' 'libjpeg' 'libid3tag' 'ffmpeg' 'sqlite3')
makedepends=('mercurial')
conflicts=('minidlna')
provides=('minidlna')
backup=('etc/minidlna.conf'
        'etc/conf.d/minidlna')
source=(minidlna.rc minidlna.conf minidlna.service)
md5sums=('4556202816114c5142bf1e47e0ab1e40'
         'af1b8c0fef9902f797b0324c255da5a1'
         '8ad78fc17e3d95218bcfea76dfbb369f')

_hgroot="https://stativ@bitbucket.org/stativ/minidlna-transcode"
_hgrepo="minidlna-transcode"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [[ -d "$_hgrepo" ]]; then
    cd "$_hgrepo"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "$_hgroot" "$_hgrepo"
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_hgrepo-build"

  make DESTDIR="$pkgdir" install
  install -Dm644 minidlna.conf "$pkgdir"/etc/minidlna.conf

  install -Dm755 "$srcdir"/minidlna.rc "$pkgdir"/etc/rc.d/minidlna
  mkdir -p "$pkgdir"/etc/conf.d
  echo 'MINIDLNA_USER=nobody' > "$pkgdir"/etc/conf.d/minidlna
  echo 'MINIDLNA_OPTS=""' >> "$pkgdir"/etc/conf.d/minidlna
  
  install -Dm0644 "$srcdir"/minidlna.conf "$pkgdir"/usr/lib/tmpfiles.d/minidlna.conf

  # systemd files
  install -Dm0644 "$srcdir"/minidlna.conf "$pkgdir"/usr/lib/tmpfiles.d/minidlna.conf
  install -Dm0644 "$srcdir"/minidlna.service "$pkgdir"/usr/lib/systemd/system/minidlna.service
}
