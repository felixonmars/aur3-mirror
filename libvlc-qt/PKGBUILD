pkgname=libvlc-qt
pkgver=0.6.1
pkgrel=1
pkgdesc="A free library used to connect Qt and libvlc libraries"
arch=('i686' 'x86_64')
url="http://projects.tano.si/en/library"
license=('GPL')
depends=('vlc>=1.1' 'qt>=4.6')
makedepends=('cmake>=2.6')
source=("https://github.com/ntadej/vlc-qt/tarball/$pkgver")
md5sums=('b704e048161dc9c6bbb0924f2bb21708')
_gitcommit=41086cb

_optimal_make_jobs() {
	if [ -r /proc/cpuinfo ]; then
		local core_count=$(grep -Fc processor /proc/cpuinfo)
	else
		local core_count=0
	fi

	if [ $core_count -gt 1 ]; then
		echo -n $[$core_count-1]
	else
		echo -n 1
	fi
}

build() {
	cd "$srcdir/ntadej-vlc-qt-$_gitcommit"

	msg 'Running cmake...'
	cmake . -DCMAKE_INSTALL_PREFIX=/usr

	msg 'Running make...'
	make -j$(_optimal_make_jobs)
}

package() {
	cd "$srcdir/ntadej-vlc-qt-$_gitcommit"

	make DESTDIR="$pkgdir" install
}
