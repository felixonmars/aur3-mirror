# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=avidemux
pkgname=$_pkgname-svn
pkgver=7908
pkgrel=1
pkgdesc="A graphical tool to edit video (filter/re-encode/split)"
arch=(i686 x86_64)
url=http://www.avidemux.org/
license=(GPL2)
depends=(libxml2 gtk2)
makedepends=(subversion yasm)
optdepends=(libvorbis libdca faad2 faac x264 sdl alsa-lib lame xvidcore libxv)
makedepends+=($optdepends)
provides=($_pkgname)
conflicts=($_pkgname)
source=($_pkgname.desktop)
sha256sums=('a82f1b3e7b0801c1a3b1072c85e1e2d9b1cf3bcf15520f9ec03f11612daba787')
sha512sums=('fad15f4b66a7561e10b34bcd25559d935ecb94fdfe66aaa7e1e05d200d0112d138290e38c8339ace81a86fc3cc138f9a6d7f5ca09b57dbcba473b9d11fc94ba4')

_svntrunk=svn://svn.berlios.de/avidemux/branches/avidemux_2.5_branch_gruntster/
_svnmod=$_pkgname

build() {
    cd "$srcdir"

    msg "Starting SVN checkout..."
    if [[ -d $_svnmod/.svn ]]; then
        pushd $_svnmod && svn up -r $pkgver
        msg2 "The local files have been updated."
        popd
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi
    msg2 "SVN checkout done or server timeout"

    rm -rf $_svnmod-build
    install -d $_svnmod-build
    cd $_svnmod-build

    msg "Compiling..."
    cmake ../$_svnmod/
    make
}

package() {
    cd "$srcdir"/$_svnmod-build/
    make prefix="$pkgdir/usr" install

    # freedesktop thingies
    install -Dm644 ${_pkgname}_icon.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png
    desktop-file-install ../$_pkgname.desktop --dir "$pkgdir"/usr/share/applications/$_pkgname.desktop
}
