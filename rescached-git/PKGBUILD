# Maintainer: sulhan <ms@kilabit.info>
_pkgname=rescached
pkgname=rescached-git
pkgver=r104.d750114
pkgrel=1
pkgdesc="Resolver/DNS cache daemon"
arch=('i686' 'x86_64')
url="https://github.com/shuLhan/rescached"
license=('custom:BSD')
makedepends=('git' 'asciidoc')
backup=('etc/rescached/rescached.cfg')
source=("$_pkgname::git+https://github.com/shuLhan/rescached.git"
	"libvos::git+https://github.com/shuLhan/libvos.git")
sha1sums=('SKIP' 'SKIP')

pkgver() {
	cd "$srcdir/$_pkgname/src"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgname"
	git submodule init
	git config submodule.libvos.url $srcdir/libvos
	git submodule update
	cd "src"
}

build() {
	cd "$srcdir/$_pkgname/src"
	echo ">>"
	echo ">> cleaning ..."
	echo ">>"
	make distclean
	echo ">>"
	echo ">> make ..."
	echo ">>"
	unset CXXFLAGS
	make || return 1
}

package() {
	cd "$srcdir/$_pkgname/src"

	make DESTDIR="$pkgdir/" install
}
