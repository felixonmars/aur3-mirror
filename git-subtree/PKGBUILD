# $Id: PKGBUILD 165345 2012-08-16 02:47:11Z dan $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=git-subtree
pkgver=1.7.11.5
pkgrel=1
pkgdesc="Merges and splits subtrees from your git project into subprojects and back"
arch=(i686 x86_64)
url="http://git-scm.com/"
license=('GPL2')
depends=('git')
makedepends=('asciidoc' 'xmlto')
source=("http://git-core.googlecode.com/files/git-$pkgver.tar.gz" "makefile.patch")

build() {
  cd "$srcdir/git-$pkgver/contrib/subtree"
  patch -p0 -i "$srcdir/makefile.patch"
  make prefix=/usr gitexecdir=/usr/lib/git-core DESTDIR="$pkgdir"
}

package() {
  cd "$srcdir/git-$pkgver/contrib/subtree"
  make prefix=/usr gitexecdir=/usr/lib/git-core DESTDIR="$pkgdir" install
}

md5sums=('9985d35c11531d546426ebefb327c847'
         '99d1395da2e7964634f0d84cf70ae069')
