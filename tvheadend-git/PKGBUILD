# Maintainer: Markus Opitz <mastero23 at gmail dot com>
# Based on package hts-tvheadend-svn by azleifel <azleifel at googlemail dot com>

pkgname=tvheadend-git
pkgver=20120812
pkgrel=1
pkgdesc="TV streaming server for Linux"
arch=('i686' 'x86_64')
url="http://www.lonelycoder.com/hts/tvheadend_overview.html"
license=('GPL3')
depends=('avahi' 'openssl')
makedepends=('git')
optdepends=('xmltv: For an alternative source of programme listings')
provides=('tvheadend')
conflicts=('hts-tvheadend' 'hts-tvheadend-svn')
backup=('etc/conf.d/tvheadend')
install=tvheadend.install
source=('tvheadend.conf.d' 'tvheadend.rc.d')
md5sums=('854845963c364cdcefcbd473f713179b'
         'a8d7b66c6592aabde7556b253f5e0d54')

_gitroot="git://github.com/tvheadend/tvheadend.git"
_gitname="tvheadend"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./configure --prefix=/usr --release
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  sed -i 's|/share/man1|/share/man/man1|' support/posix.mk
  make DESTDIR="$pkgdir/" install

  install -D -m 755 "$srcdir/tvheadend.rc.d" "$pkgdir/etc/rc.d/tvheadend"
  install -D -m 644 "$srcdir/tvheadend.conf.d" "$pkgdir/etc/conf.d/tvheadend"
}
