# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=python2-pyelftools
_pkgname=pyelftools
pkgver=0.22
pkgrel=1
pkgdesc="Python library for analyzing ELF files and DWARF debugging information"
url="https://github.com/eliben/pyelftools"
arch=('any')
license=('custom:Public Domain')
depends=('python2')
source=(https://github.com/eliben/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=("ebbc9e3429d471bd7e57fcc840b4a1998090bb8929e706422f5bba06507fed931b71f22add45c13748f3db77134e4cc54b7c5d6c37d11fed1de12b209151ad6c")

prepare() {
  cd ${_pkgname}-${pkgver}
  mv scripts/readelf.py scripts/readelf.py2
  sed -ri 's/readelf.py/\02/g' setup.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python2 test/run_all_unittests.py
}

package() {
  cd ${_pkgname}-${pkgver}

  python2 setup.py install -O1 --root="${pkgdir}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
