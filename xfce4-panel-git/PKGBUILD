# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Silvio Knizek <killermoehre@gmx.net>
# Contributor: lh <jarryson@gmail.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

_pkgname=xfce4-panel
pkgname=$_pkgname-git
pkgver=4.10.0.139.gf82a0b0
pkgrel=1
pkgdesc="Panel for the Xfce desktop environment"
arch=(i686 x86_64)
license=(GPL2)
url=http://git.xfce.org/xfce/$_pkgname/tree/README
groups=(xfce4-git)
depends=(desktop-file-utils exo-git garcon-git libwnck libxfce4ui-git)
makedepends=(git xfce4-dev-tools)
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
options=(!libtool)
install=$_pkgname.install
source=($pkgname::git://git.xfce.org/xfce/$_pkgname)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $pkgname/
    git describe | sed 's/^xfce4-panel-//;s/-/./g'
}

build() {
    cd $pkgname/
    ./autogen.sh --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --enable-gio-unix \
        --disable-static
    make
}

package() {
    make -C $pkgname DESTDIR="$pkgdir" install
}
