# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-bsnes-git
pkgver=416.9fad49b
pkgrel=1
pkgdesc="libretro implementation of bSNES/higan. (Super Nintendo Entertainment System)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="http://gitorious.org/bsnes"
license=('GPL3')
makedepends=('git')

_gitname=bsnes
source=("git+https://git.gitorious.org/bsnes/${_gitname}.git#branch=libretro"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/bsnes_accuracy_libretro.info"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/bsnes_performance_libretro.info"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/bsnes_balanced_libretro.info")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  git clone . perf
  git clone . balanced

  echo "=== Building bSNES accuracy ==="
  make ui=target-libretro profile=accuracy

  cd "perf"
  echo "=== Building bSNES performance ==="
  make ui=target-libretro profile=performance

  cd "../balanced"
  echo "=== Building bSNES balanced ==="
  make ui=target-libretro profile=balanced
}

package() {
  install -Dm644 "${_gitname}/out/bsnes_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-bsnes-accuracy.so"
  install -Dm644 "${_gitname}/perf/out/bsnes_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-bsnes-performance.so"
  install -Dm644 "${_gitname}/balanced/out/bsnes_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-bsnes-balanced.so"
  install -Dm644 "bsnes_accuracy_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-bsnes-accuracy.info"
  install -Dm644 "bsnes_performance_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-bsnes-performance.info"
  install -Dm644 "bsnes_balanced_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-bsnes-balanced.info"
}
