# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: ryooichi <ryooichi+arch AT gmail DOT com>

# change this to true if you want to use msfupdate
# this will include the svn repository in the package
_msfupdate=false

pkgname=metasploit-svn
pkgver=15257
pkgrel=1
pkgdesc="The Metasploit Framework is a development platform for creating security tools and exploits."
arch=('any')
url='http://metasploit.com/'
license=('BSD')
depends=('ruby' 'ruby-msgpack')
makedepends=('subversion')
provides=('metasploit')
conflicts=('metasploit')
options=('!strip')

optdepends=('java-runtime: msfgui' 'ruby-pg: database support')
if $_msfupdate; then
  optdepends+=('subversion: msfupdate')
fi

_svntrunk=https://www.metasploit.com/svn/framework3/trunk
_svnmod=metasploit

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d $_svnmod/.svn ]]; then
    cd $_svnmod && svn up -r $pkgver
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"  
}

package() {
  cd "$pkgdir"
  mkdir -p usr/{bin,src}

  if $_msfupdate; then
    cp -R "$srcdir/$_svnmod" usr/src/$_svnmod
  else
    svn export $srcdir/$_svnmod usr/src/$_svnmod
    rm usr/src/$_svnmod/msfupdate
  fi

  install -Dm644 usr/src/$_svnmod/README usr/share/licenses/$pkgname/COPYING

  cd usr/bin

  for f in ../src/metasploit/msf*; do
    ln -s $f .
  done
}
