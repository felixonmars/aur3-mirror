# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname='profile-sync-daemon'
_pkgname_short='psd'
pkgver=3.16
pkgrel=1
pkgdesc='Symlinks and syncs browser profile dirs to RAM thus reducing HDD/SDD calls and speeding-up browsers.'
arch=('any')
url='https://wiki.archlinux.org/index.php/Profile-sync-daemon'
license=('GPL')
depends=('rsync')
optdepends=('cron: allow hourly (default) or periodic syncs of tmpfs <--> HDD/SDD storage')
conflicts=('firefox-sync' 'firefox-tmpfs-daemon' 'chromium-tmpfs' 'tmpfs-store')
replaces=('sync-browsers-daemon')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz")
backup=("etc/$_pkgname_short.conf")
install=readme.install
sha256sums=('25e77a3340fd7fe8dd139a1baa0177ab34ad45878d99ec7bee565a56138efe21')

package() {
	cd "$srcdir/$pkgname-$pkgver"
	install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
	install -Dm644 $_pkgname_short.conf "$pkgdir/etc/$_pkgname_short.conf"
	install -Dm755 rc.$_pkgname_short "$pkgdir/etc/rc.d/$_pkgname_short"
	install -Dm755 $_pkgname_short.cron.hourly "$pkgdir/etc/cron.hourly/$_pkgname_short-update"
	install -Dm644 "$_pkgname_short.service" "$pkgdir/usr/lib/systemd/system/$_pkgname_short.service"
	
	gzip -9 $_pkgname_short.manpage
	install -g 0 -o 0 -Dm 0644 $_pkgname_short.manpage.gz "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
	ln -s "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$_pkgname_short.1.gz"
}
