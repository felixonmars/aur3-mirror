# Mantainer: Ivan Pulido <ijpulidos[AT]riseup.net>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=maxima-clisp
pkgver=5.29.0
pkgrel=1
pkgdesc="Maxima - a sophisticated computer algebra system. Clisp compiled version."
arch=('i686' 'x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('clisp' 'texinfo' 'sh')
makedepends=('python2')
optdepends=('gnuplot: plotting capabilities' 'rlwrap: readline support via /usr/bin/rmaxima' 'tk: graphical xmaxima interface')
# needs rebuild when bash changes version
# needs a rebuild when sbcl changes version
options=('!makeflags' '!zipman') # don't zip info pages or they won't work inside maxima
install=maxima.install
source=("http://downloads.sourceforge.net/sourceforge/maxima/maxima-$pkgver.tar.gz" "maxima.desktop")
sha1sums=('7d330c3296f2da9893803b3fcdb636cc6ab8ecf7'
          '4398ebb1ec85ccfa12f37516a56d60c26f74b18b')

build() {
  cd ${srcdir}/maxima-${pkgver}

  # set correct python executable to create docs
  sed -i "s|${PYTHONBIN:-python}|python2|" doc/info/extract_categories.sh

  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
	--libexecdir=/usr/lib --enable-clisp --with-default-lisp=clisp
  make
}

package() {
  cd ${srcdir}/maxima-${pkgver}
  make DESTDIR=${pkgdir} install

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/maxima.desktop \
  	${pkgdir}/usr/share/applications/maxima.desktop

  # make sure, we have a nice icon for the desktop file at the right place ;)
  install -d ${pkgdir}/usr/share/pixmaps/
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png \
	${pkgdir}/usr/share/pixmaps/maxima.png
}
