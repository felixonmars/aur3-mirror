# $Id$
# Maintainer: JSpaces <jspaces -aT- incentre -d0T- net>

_srcname=soxr
pkgname="lib$_srcname"
pkgver=0.1.1
pkgrel=1
pkgdesc="The SoX Resampler library that aims to give fast and high quality results for any constant resampling ratio."
arch=('i686' 'x86_64')
url="http://sourceforge.net/p/soxr/wiki/Home/"
license=('GPL')
depends=()
source=(http://downloads.sourceforge.net/project/${_srcname}/${_srcname}-${pkgver}-Source.tar.xz)
sha256sums=('dcc16868d1a157079316f84233afcc2b52dd0bd541dd8439dc25bceb306faac2')

build() {
	cd "$srcdir/$_srcname-$pkgver-Source"
	[[ -d build ]] && rm -rf build
	mkdir build && cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
				-DCMAKE_BUILD_TYPE=Release \
				-DCMAKE_USE_RELATIVE_PATHS=1 \
				-DCMAKE_SKIP_RPATH=1 \
				..
	make
}

package() {
	cd "$srcdir/$_srcname-$pkgver-Source/build"
	
	make DESTDIR="$pkgdir" install

	install -Dm644 "$srcdir/$_srcname-$pkgver-Source/LICENCE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# Delete duplicate license in doc folder."
	rm -f "$pkgdir/usr/share/doc/$pkgname/LICENCE"
}

# vim:set ts=2 sw=2 et:
