# $Id$
# Maintainer: JSpaces <xpace -aT- incentre -d0T- net>

_srcname=soxr
pkgname="lib$_srcname"
pkgver=0.1.1
pkgrel=1
pkgdesc="The SoX Resampler library that aims to give fast and high quality results for any constant resampling ratio."
arch=('i686' 'x86_64')
url="http://sourceforge.net/p/soxr/wiki/Home/"
license=('GPL')
groups=('jspaces')
depends=()
makedepends=('cmake')
source=(http://downloads.sourceforge.net/project/${_srcname}/${_srcname}-${pkgver}-Source.tar.xz)
sha256sums=('dcc16868d1a157079316f84233afcc2b52dd0bd541dd8439dc25bceb306faac2')

build() {
	cd ${_srcname}-${pkgver}-Source
	cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DBUILD_EXAMPLES:BOOL=OFF \
        -DBUILD_SHARED_LIBS:BOOL=ON \
        -DCMAKE_BUILD_TYPE:STRING=Release \
        -DVISIBILITY_HIDDEN:BOOL=ON \
        -DWITH_AVFFT:BOOL=ON \
        -DWITH_DOUBLE_PRECISION:BOOL=ON \
        -DWITH_LSR_BINDINGS:BOOL=ON \
        -DWITH_OPENMP:BOOL=ON \
        -DWITH_PFFFT:BOOL=ON \
        -DWITH_SIMD:BOOL=ON \
        -DCMAKE_USE_RELATIVE_PATHS:BOOL=ON \
        -DCMAKE_SKIP_RPATH:BOOL=ON \
				${srcdir}/${_srcname}-${pkgver}-Source
	make
}

package() {
	cd $_srcname-$pkgver-Source
	
	make DESTDIR="$pkgdir" install

	install -Dm644 "$srcdir/$_srcname-$pkgver-Source/LICENCE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# Delete duplicate license in doc folder."
	rm -f "$pkgdir/usr/share/doc/$pkgname/LICENCE"
}

# vim:set ts=2 sw=2 et:
