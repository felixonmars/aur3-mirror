# Maintainer: Glorfindel <glorfindelATsentDOTcom>
# Contributor : Brizou <brizou @ gmail.com>
# Contributor : Cedric MATHIEU <me.xenom @ gmail.com>
# Contributor : Det <nimetonmaili @ gmail.com>
# Contributor: coderoar <coderoar @ gmail.com>
# Contributor: kang <kang @ mozilla.com>

pkgname=firefox-nightly-fr
pkgdesc='Standalone web browser from mozilla.org, nightly build'
url='http://www.mozilla.org/projects/firefox'
pkgver=35.0a1
pkgrel=3
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
source=('firefox-nightly.desktop' 'firefox-nightly-safe.desktop' 'vendor.js')
sha512sums=('d2d836b07288d2a13d01d668399df8a2a15884a58a7051303938bf74a30bdfc23a8bc57395494345727ae24e1dd9cab09c67a640a5c9e7a8df895f987e009dcd' '00caf982c072c7499433c494ecf2096542c4ddf368c4b97fb22672fd669683911d009a65a498b8a03da61ae9aa5a4e8bdaa6e58b148c2a5d7c008910b2af26a5' 'bae5a952d9b92e7a0ccc82f2caac3578e0368ea6676f0a4bc69d3ce276ef4f70802888f882dda53f9eb8e52911fb31e09ef497188bcd630762e1c0f5293cc010')
depends=('alsa-lib' 'libxt' 'libnotify' 'mime-types' 'nss' 'gtk2' 'sqlite' 'dbus-glib')
conflicts=('firefox-nightly')

package() {
  FX_BASE_SRC="firefox-${pkgver}.fr.linux-${CARCH}"
  FX_TARBALL="${FX_BASE_SRC}.tar.bz2"
  FX_CHKSUM="${FX_BASE_SRC}.checksums"
  FX_GPG="${FX_BASE_SRC}.checksums.asc"
  FX_BASE_URI="https://ftp.mozilla.org/pub/mozilla.org/firefox/nightly/latest-mozilla-central-l10n/"
  FX_SRC_URI="${FX_BASE_URI}/${FX_TARBALL}"
  FX_CHKSUM_URI="${FX_BASE_URI}/${FX_CHKSUM}"
  FX_GPG_URI="${FX_BASE_URI}/${FX_GPG}"


  msg "Downloading..."
  curl -OR ${FX_SRC_URI}
  curl -OR ${FX_CHKSUM_URI}
  curl -OR ${FX_GPG_URI}
  msg "Verifying SHA512 checksum..."
  grep ${FX_TARBALL} ${FX_CHKSUM}|grep sha512 |sed 's/sha512\ [0-9]*\ //' > sha512
  sha512sum -c sha512
  # uncomment this line to enable GnuPG signature verification. You'll need Firefox's GnuPG release key.
  # Their current fingerprint is 9D03 193D 6BDC 541B D796  C4E4 7F4D 6645 1EBC AB3A shortid 0x1EBCAB3A
  #  msg "Verifying GnuPG signature..."
  #  gpg --verify ${FX_GPG}
  msg "Extracting..."
  bsdtar -x -f ${FX_TARBALL}
  msg "Packaging..."
  #  uncomment this line to remove these
  #  rm -rf firefox/{extensions,plugins,searchplugins}
  mkdir -p "${pkgdir}"/{usr/{bin,share/{applications,pixmaps}},opt}
  cp -r firefox "${pkgdir}/opt/firefox-${pkgver}"

  ln -s /opt/firefox-${pkgver}/firefox "${pkgdir}/usr/bin/firefox-nightly"
  install -m644 "${srcdir}"/{firefox-nightly.desktop,firefox-nightly-safe.desktop} "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/firefox/browser/icons/mozicon128.png" "${pkgdir}/usr/share/pixmaps/firefox-nightly-icon.png"
  install -Dm644 "${srcdir}/vendor.js" "${pkgdir}/opt/firefox-${pkgver}/browser/defaults/preferences/vendor.js"
}
