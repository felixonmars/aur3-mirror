# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-session
pkgver=0.3.7.1
pkgrel=1
pkgdesc="Quick! Change your status. Switch users. Close your session. All provided by the The Session Menu these tasks are conviniently placed in the upper-right corner of the desktop to make them available and easy to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('libindicator>=0.3.19' 'libdbusmenu>=0.4.92' 'dbus-glib' 'dconf')
true && depends=('libindicator3>=0.3.19' 'libdbusmenu-gtk3>=0.4.92' 'dbus-glib' 'dconf')
makedepends=('intltool>=0.35.0')
optdepends=('gnome-control-center: open system settings from User and Device menus'
            'gnome-session: shutdown and logout from Device menu'
            'gnome-screensaver: lock the screen from Device menu'
            'system-config-printer-gnome: launch printer config tool from Device menu'
            'simple-scan: launch scanner app from Device menu'
            'cheese: launch webcam app from Device menu')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/0.3/$pkgver/+download/$pkgname-$pkgver.tar.gz
        fix-location.patch
        make-gtklogouthelper-optional.patch
        make-apt-optional.patch)
md5sums=('3c18fa9d4489a29c3ad091379b4b3fcb'
         '72fbf076fea7ba48f5cca38c9169a0ad'
         '8e66be1cb4d51a48d028d5dfc5f2f6e0'
         '4cdfa559a9cc59703fba5608243d0568')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i "$srcdir/fix-location.patch"
  patch -Np1 -i "$srcdir/make-gtklogouthelper-optional.patch"
  patch -Np1 -i "$srcdir/make-apt-optional.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --disable-gtklogouthelper --disable-apt
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
