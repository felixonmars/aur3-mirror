# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-session
pkgver=0.2.10
pkgrel=4
pkgdesc="Quick! Change your status. Switch users. Close your session. All provided by the The Session Menu these tasks are conviniently placed in the upper-right corner of the desktop to make them available and easy to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
groups=('ayatana')
depends=('libindicator>=0.3.5' 'libdbusmenu>=0.2.2' 'gconf>=2.0' 'gnome-session-ubuntu')
makedepends=('intltool')
optdepends=('gnome-screensaver-ubuntu: fixed for user switch')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('aa3437b6da02aed457deb2231aeef048'
         '321f2544221d2fc89a99d6b387af4099')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
