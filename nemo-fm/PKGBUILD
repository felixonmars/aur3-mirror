# Maintainer: Ner0

pkgname=nemo-fm
pkgver=1.0.4
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork), stable version"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('dconf' 'desktop-file-utils' 'exempi' 'gnome-desktop' 'gnome-icon-theme' 'hicolor-icon-theme' 'libnotify' 'libtracker-sparql')
makedepends=('gtk-doc' 'gobject-introspection' 'intltool')
optdepends=('gksu: Open as Root')
conflicts=('nemo-git')
options=('!emptydirs' '!libtool')
install=nemo.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver")
md5sums=('638fc73493ef354cdf8438d847b8fb66')

build() {
  cd linuxmint-nemo-*

  # Get default terminal value
  _terminal=$(gsettings get org.gnome.desktop.default-applications.terminal exec | tr -d "'")

  # Set "Open in Terminal" to default terminal (Arch doesn't use x-terminal-emulator)
  sed -i "s/x-terminal-emulator/$_terminal/" src/nemo-view.c

  # Fix "Open as Root"
  sed -i "s/gksu/gksudo/" src/nemo-view.c

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-gtk-doc-html \
      --disable-nwp-extension \
      --disable-schemas-compile
  make
}

package() {
  cd linuxmint-nemo-*
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
