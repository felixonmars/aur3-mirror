# Maintainer: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Ricardo Band <me [at] xengi [dot] de>

pkgname=popcorntime
pkgver=0.2.8
pkgrel=5
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('x86_64' 'i686')
url="http://popcorn-time.tv/"
license=('GPL3')
makedepends=('zip')
depends=('ttf-ms-fonts')
conflicts=('popcorntime-beta' 'popcorntime-git')
options=('!strip')
install="popcorntime.install"
md5sums=('SKIPPED'
         '5725b4c466ca3726e7a6df52730dacd2'
         'efe872f41ed74a706308b4015e4fd82f'
         '9608f35f4cc083a0c36b0f4ef275ab10')

if [ "$CARCH" = 'i686' ]; then
  _platform=Linux-32
  md5sums[0]='9f6d00dfe49096e454081c1fa6594ec3'
elif [ "$CARCH" = 'x86_64' ]; then
  _platform=Linux-64
  md5sums[0]='f62b7952c228ffbb7f5b269913102903'
fi

source=("http://dl.bintray.com/popcorntime/Release/Popcorn-Time-${pkgver}-${_platform}.tar.gz"
        "https://raw.githubusercontent.com/popcorn-org/popcorn-app/v${pkgver}/images/icon.png"
        "popcorntime.install"
        "popcorntime.desktop")

build() {
  cd "${srcdir}"
  sed -i 's/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x30/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x31/g' "Popcorn-Time"
}

package() {
  cd "${srcdir}"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  # Program
  install -Dm755 Popcorn-Time "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 nw.pak "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 libffmpegsumo.so "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 package.nw "${pkgdir}/usr/share/${pkgname}/"

  # Link to program
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/usr/share/${pkgname}/Popcorn-Time" "${pkgdir}/usr/bin/${pkgname}"

  # Desktop file
  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  # Icon
  install -Dm644 "${srcdir}/icon.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"
}
