# Maintainer: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: xantares <xantares09@hotmail.com>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Ricardo Band <me [at] xengi [dot] de>

pkgname=popcorntime
pkgver=0.3.5.2
pkgrel=9
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('i686' 'x86_64')
url="http://popcorntime.io/"
license=('GPL3')
makedepends=('nodejs-grunt-cli' 'nodejs-bower')
depends=('ttf-ms-fonts' 'gconf')
options=('!strip')
install="popcorntime.install"
_gitname=popcorn-app
_nw_ver=0.9.2
md5sums=('efe872f41ed74a706308b4015e4fd82f'
         '9608f35f4cc083a0c36b0f4ef275ab10'
         '1f1cc562fa752ef1b20916727cf46f51'
         'SKIP'
         'SKIP'
         'SKIP')

if [ "$CARCH" = 'i686' ]; then
  _platform=linux32
  _nw_platform=linux-ia32
  md5sums[5]='dca779c67be155aa53458a146cda41ee'
elif [ "$CARCH" = 'x86_64' ]; then
  _platform=linux64
  _nw_platform=linux-x64
  md5sums[5]='fc25eb312257b8c7e04e8e59c973566e'
fi
_nw_file=node-webkit-v${_nw_ver}-${_nw_platform}.tar.gz

source=("popcorntime.install"
        "popcorntime.desktop"
        "popcorn-app-v${pkgver}.zip::https://git.popcorntime.io/stash/plugins/servlet/archive/projects/PT/repos/popcorn-app?at=refs%2Ftags%2Fv${pkgver}"
        "popcorn-i18n-master.zip::https://git.popcorntime.io/stash/plugins/servlet/archive/projects/PT/repos/popcorn-i18n?at=refs%2Fheads%2Fmaster"
        "popcorn-themes-master.zip::https://git.popcorntime.io/stash/plugins/servlet/archive/projects/PT/repos/popcorn-themes?at=refs%2Fheads%2Fmaster"
        "http://cdn.popcorntime.io/nw/v${_nw_ver}/${_nw_file}")
noextract=("popcorn-app-v${pkgver}.zip"
           "popcorn-i18n-master.zip"
           "popcorn-themes-master.zip")

prepare() {
  mkdir -p "${srcdir}/${_gitname}"/src/app/language
  bsdtar -xf popcorn-app-v${pkgver}.zip -C "${srcdir}/${_gitname}"
  bsdtar -xf popcorn-i18n-master.zip -C "${srcdir}/${_gitname}"/src/app/language
  bsdtar -xf popcorn-themes-master.zip -C "${srcdir}/${_gitname}"/src/app/styl/third_party

  cd "${srcdir}/${_gitname}"

  # see http://stackoverflow.com/questions/24706416/bower-error-when-installing-package-arguments-to-path-join-must-be-strings
  sed -i 's|"bower": "1.3.6"|"bower": "1.3.8"|g' package.json

  mkdir -p "${srcdir}/${_gitname}/build/cache/${_platform}/${_nw_ver}/"
  mv "${srcdir}/node-webkit-v${_nw_ver}-${_nw_platform}/"* \
    "${srcdir}/${_gitname}/build/cache/${_platform}/${_nw_ver}/"

  # for gyp
  export PYTHON=/usr/bin/python2

  # Get dependencies
  npm install
}

build() {
  cd "${srcdir}/${_gitname}"

  # The grunt 'build' task served as a reference for this:
  grunt css
  grunt stylus:third_party
  grunt bower_clean
  grunt nodewebkit

  # Thanks to Revelation60 for pointing it out
  # https://github.com/rogerwang/node-webkit/wiki/The-solution-of-lacking-libudev.so.0
  msg2 "Patching program to fix libudev.so.0 problem"
  cd "${srcdir}/${_gitname}/build/releases/Popcorn-Time/${_platform}/Popcorn-Time"
  sed -i 's/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x30/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x31/g' Popcorn-Time
}

package() {
  cd "${srcdir}"

  _bpath="${srcdir}/${_gitname}/build/releases/Popcorn-Time/${_platform}/Popcorn-Time"

  install -dm755 "${pkgdir}/usr/lib/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  # Program
  install -Dm755 "${_bpath}/Popcorn-Time" "${pkgdir}/usr/lib/${pkgname}/"
  install -Dm644 "${_bpath}/nw.pak" "${pkgdir}/usr/lib/${pkgname}/"
  install -Dm644 "${_bpath}/libffmpegsumo.so" "${pkgdir}/usr/lib/${pkgname}/"

  # Link to program
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${pkgname}/Popcorn-Time" "${pkgdir}/usr/bin/${pkgname}"

  # Desktop file
  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  # Icon
  install -Dm644 "${srcdir}/${_gitname}/src/app/images/icon.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"
}
