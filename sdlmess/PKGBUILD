# Contributor: robb_force <robb_force@holybuffalo.net>
# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=sdlmess
pkgver=0.146u5
pkgrel=1

_basever=0146

pkgdesc='A port of the popular Multiple Emulator Super System using SDL.'
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat' 'gtk2')
makedepends=('mesa')
[ "$CARCH" = 'i686' ] && makedepends=('mesa' 'nasm')
optdepends=('ttf-liberation: recommended UI font')

source=(sdlmess.sh
        "http://ompldr.org/vZHV6ag/mess${_basever}s.zip"
        "http://ompldr.org/vZWNpaw/mess${_basever}u1_diff.zip"
        "http://ompldr.org/vZWx2cA/mess${_basever}u2_diff.zip"
        "http://ompldr.org/vZXJhYw/mess${_basever}u3_diff.zip"
        "http://ompldr.org/vZXg1cQ/mess${_basever}u4_diff.zip"
        "http://ompldr.org/vZjYxeA/mess${_basever}u5_diff.zip")
md5sums=('141069e7019da5e33414dc8d4c421150'
         '3ad4e780d1d7d3b66edf985417018c5b'
         '4627e675aa221894bed36111e2e9be25'
         '46c443604debccc1b7b60f2704acdeea'
         'd1f40ac334357b415d9624e0df505e00'
         '2d921dde61dcbba9194e80df57099fd4'
         '772f52d9dbff9e14ee4c2350858380d7')
install=sdlmess.install

build() {
  cd "$srcdir"
  
  find . -type f -not -name '*.png' -exec perl -pi -e 's/\r\n?/\n/g' '{}' \;

  for i in 1 2 3 4 5; do
    msg2 "Applying patch: mess${_basever}u${i}.diff"
    patch --silent -p0 -E < mess${_basever}u${i}.diff
  done

  # Adjusting make options according to target architecture
  if [ "$CARCH" == 'x86_64' ]; then
    echo 'Compiling for AMD64...'
    make TARGET=mess AMD64=1 PTR64=1 SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  elif [ "$CARCH" == 'i686' ]; then
    echo 'Compiling for i686...'
    make TARGET=mess I686=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  else
    echo 'Compiling for i386...'
    make TARGET=mess PM=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  fi
}

package() {
  cd "$srcdir"
    
  # Installing the wrapper script
  install -Dm755 "$srcdir/sdlmess.sh" "$pkgdir/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 mess "$pkgdir/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "$pkgdir/usr/share/sdlmess/artwork"
  install -m644 artwork/* "$pkgdir/usr/share/sdlmess/artwork/"
  install -d "$pkgdir/usr/share/sdlmess/hash"
  install -m644 hash/* "$pkgdir/usr/share/sdlmess/hash/"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/sdlmess/license.txt"
}
