# Contributor: robb_force <robb_force@holybuffalo.net>
# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=sdlmess
pkgver=0.143u8
pkgrel=1

_basever=0143
_patchlevel=8

pkgdesc='A port of the popular Multiple Emulator Super System using SDL.'
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat' 'gtk2')
makedepends=('mesa')
[ "$CARCH" = 'i686' ] && makedepends=('mesa' 'nasm')
optdepends=('ttf-liberation: recommended UI font')

for i in `seq 1 ${_patchlevel}`; do
  _patches="${_patches} mess${_basever}u${i}_diff.zip::http://mess.redump.net/_media/downloads:mess${_basever}u${i}_diff.zip"
done

source=("mess${_basever}s.zip::http://mess.redump.net/_media/downloads:mess${_basever}s.zip" 
        sdlmess.sh ${_patches})
md5sums=('29256f4244daea3e239151d94c0d9969'
         '141069e7019da5e33414dc8d4c421150'
         'a918b689490fb0994e4ab0292228779c'
         '461f4ae715463d9251b2207537de3e72'
         '5176a192308d8610b2f223aced64bcfe'
         'de4c74c43669c76239acc1a37c975b03'
         '0c07c5933239629df26ab95e5f209736'
         '80e0ce364b0dc7db370fd257ac010588'
         '80f62763243163cfcf30ed320de11fe9'
         '41c464697fd3474cf5793243ff990e66')
install=sdlmess.install

build() {
  cd "$srcdir"
  
  find . -type f -not -name '*.png' -exec perl -pi -e 's/\r\n?/\n/g' '{}' \;

  # malformed patch
  sed -e '863 d' -i mess0143u6.diff
  sed -e '274 d' -i mess0143u7.diff

  for i in `seq 1 ${_patchlevel}`; do
    msg2 "Applying patch: mess${_basever}u${i}.diff"
    patch --silent -p0 -E < mess${_basever}u${i}.diff
  done

  # Adjusting make options according to target architecture
  if [ "$CARCH" == 'x86_64' ]; then
    echo 'Compiling for AMD64...'
    make TARGET=mess AMD64=1 PTR64=1 SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  elif [ "$CARCH" == 'i686' ]; then
    echo 'Compiling for i686...'
    make TARGET=mess I686=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  else
    echo 'Compiling for i386...'
    make TARGET=mess PM=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  fi
}

package() {
  cd "$srcdir"
    
  # Installing the wrapper script
  install -Dm755 "$srcdir/sdlmess.sh" "$pkgdir/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 mess "$pkgdir/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "$pkgdir/usr/share/sdlmess/artwork"
  install -m644 artwork/* "$pkgdir/usr/share/sdlmess/artwork/"
  install -d "$pkgdir/usr/share/sdlmess/hash"
  install -m644 hash/* "$pkgdir/usr/share/sdlmess/hash/"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/sdlmess/license.txt"
}
