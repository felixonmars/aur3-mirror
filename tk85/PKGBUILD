# Maintainer: b00rt00s ( bomby dot zrzuc at gmail dot com ) 
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tk85
pkgver=8.5.15
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl, 8.5 series"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('tcl85' 'libxss' 'libxft')
conflicts=('tk' 'tk84')
provides=("tk" "tk=${pkgver}")
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
md5sums=('55b8e33f903210a4e1c8bce0f820657f')
options=('staticlibs')

build() {
	cd ${srcdir}/tk${pkgver}/unix
	
	[[ $CARCH == "x86_64" ]] && BIT="--enable-64bit"
	./configure --prefix=/usr --mandir=/usr/share/man --enable-threads $BIT
	
	make
}

package() {
	cd ${srcdir}/tk${pkgver}/unix
	
	make INSTALL_ROOT=${pkgdir} install install-private-headers
	ln -sf wish8.5 ${pkgdir}/usr/bin/wish
	
	# install license
	install -Dm644 license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	
	# remove buildroot traces
	sed -i "s#${srcdir}#/usr/src#" ${pkgdir}/usr/lib/tkConfig.sh

}
