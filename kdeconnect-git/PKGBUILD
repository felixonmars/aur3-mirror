# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.

_buildtype='RelWithDebInfo'

##############################################################

pkgname=kdeconnect-git
pkgver=517.5bf93af
pkgrel=1
pkgdesc='KDE Connect kded and kcm - development version'
url='https://albertvaka.wordpress.com/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdebase-runtime' 'qjson' 'qca-ossl' 'libfakekey')
makedepends=('automoc4' 'git' 'cmake')
optdepends=('sshfs: remote file browsing')
provides=('kdeconnect')
conflicts=('kdeconnect')
install=kdeconnect.install
source=("${pkgname}::git://anongit.kde.org/kdeconnect-kde")
md5sums=('SKIP')

if [[ ! ${_buildtype} == 'Release' ]] && [[ ! ${_buildtype} == 'release' ]]; then
  options=('debug')
fi

pkgver() {
  cd ${pkgname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  if [[ -e ${pkgname}-build ]]; then rm -rf ${pkgname}-build; fi
  mkdir ${pkgname}-build
}

build() {
  cd ${pkgname}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        ../${pkgname}
  make
}

package() {
  cd ${pkgname}-build
  make DESTDIR=${pkgdir} install
}
