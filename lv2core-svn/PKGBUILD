# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=lv2core
pkgname=$_pkg-svn
pkgver=98
pkgrel=1
pkgdesc="A standard for plugins and matching host applications, mainly targeted at audio processing and generation."
arch=('any')
url="http://lv2plug.in/"
license=('GPL')
groups=('lv2')
makedepends=('subversion' 'rdflib')
provides=("$_pkg")
conflicts=("$_pkg")

_svntrunk=http://lv2plug.in/repo/trunk
_svnmod=$_pkg

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #
  sed -e "s|\(env python\).*|\12|" \
      -e "s|'/usr|'$pkgdir/usr|g" \
      -e "s|link(bundle|&[bundle.index(\"/usr\"):]|" \
      -i core.lv2/lv2config

  python2 ./waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$_svnmod-build"

  DESTDIR="$pkgdir" python2 waf install

  # Create header simlinks
  python2 core.lv2/lv2config

  # Delete generator
  rm -rf "$pkgdir/usr/bin"
}
