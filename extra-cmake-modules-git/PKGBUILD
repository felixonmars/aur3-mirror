# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=extra-cmake-modules-git
pkgver=2508.d336179
pkgrel=1
pkgdesc='Extra CMake modules'
arch=('any')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL')
depends=('cmake')
makedepends=('git')
conflicts=('extra-cmake-modules')
provides=('extra-cmake-modules')
source=('git://anongit.kde.org/extra-cmake-modules.git')
md5sums=('SKIP')

pkgver() {
  cd extra-cmake-modules
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../extra-cmake-modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
