# Maintainer:  John Schoenick <john@pointysoftware.net>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=wine-multimedia-git
_basename=wine
pkgver=20110909
pkgrel=1

pkgdesc="A compatibility layer for running Windows programs"
url="http://www.winehq.com"
arch=(i686 x86_64)
license=(LGPL)
install=wine.install

depends=(
  fontconfig       lib32-fontconfig
  mesa             lib32-mesa 
  libxcursor       lib32-libxcursor
  libxrandr        lib32-libxrandr
  libxdamage       lib32-libxdamage
  libxxf86dga      lib32-libxxf86dga
  libxi            lib32-libxi
  alsa-lib         lib32-alsa-lib
  desktop-file-utils
  
  # Required for new openalSOFT api calls that arn't in release yet.
  # Sound wont work without this!
  openal-git       lib32-openal-git
  # The pulse driver wont work without this, but you can remove it if you don't expect to use it.
  alsa-plugins-git lib32-alsa-plugins-git
)

makedepends=(autoconf ncurses bison perl fontforge flex prelink git
  'gcc>=4.5.0-2'  'gcc-multilib>=4.5.0-2'
  giflib          lib32-giflib
  libxpm          lib32-libxpm
  libpng          lib32-libpng
  libxinerama     lib32-libxinerama
  libxcomposite   lib32-libxcomposite
  libxmu          lib32-libxmu
  libxxf86vm      lib32-libxxf86vm
  libxml2         lib32-libxml2
  libxslt         lib32-libxslt
  libldap         lib32-libldap
  lcms            lib32-lcms
  mpg123          lib32-mpg123
  openal          lib32-openal
  libcups         lib32-libcups
  gnutls          lib32-gnutls
  v4l-utils       lib32-v4l-utils
  
  # This is required if you want the build to support OSS,
  # but since this is an AUR package and just installing OSS can devestate your alsa sound system,
  # I'm leaving it out by default.
  #oss
)
  
optdepends=(
  giflib          lib32-giflib
  libpng          lib32-libpng
  libldap         lib32-libldap
  lcms            lib32-lcms
  libxml2         lib32-libxml2
  mpg123          lib32-mpg123
  openal          lib32-openal
  libcups         lib32-libcups
  gnutls          lib32-gnutls
  v4l-utils       lib32-v4l-utils
  oss
)

# Set to 1 to auto-rebase multimedia-git onto the latest wine-git
# Set to 0 if you just want to use the raw multimedia-git, which doesn't
# rebase itself all that often.
_rebase=1

# Wine gitweb:             http://source.winehq.org/git/wine.git
# Multimedia tree gitweb:  http://repo.or.cz/w/wine/multimedia.git
_gitroot=git://repo.or.cz/wine/multimedia.git
_gitname=wine-multimedia
_upstream=git://source.winehq.org/git/wine.git

# This is used when _rebase=1, see comment in build()
source=(winemultimedia_add_winepulse_getpriority.patch)
sha256sums=('209b22d5fe05a59ddb5108507261641f733256ba218bfe932df867fb0757aeb9')

if [[ $CARCH == i686 ]]; then
  # Strip lib32 etc. on i686
  depends=(${depends[@]/*32-*/})
  makedepends=(${makedepends[@]/*32-*/})
  makedepends=(${makedepends[@]/*-multilib*/})
  optdepends=(${optdepends[@]/*32-*/})
  provides=("wine=$pkgver")
  conflicts=('wine')
else
  provides=("wine=$pkgver" "bin32-wine=$pkgver" "wine-wow64=$pkgver")
  conflicts=('wine' 'bin32-wine' 'wine-wow64')
  replaces=('bin32-wine')
fi

build() {
  cd "$srcdir"
  
  # Get source
  if [ ! -d "$_gitname" ]; then
      (
          # The reason we clone upstream and add a remote
          # to multimedia is mainly because repo.or.cz
          # is much slower - it could be done either way.
          msg2 "Cloning wine git"
          git clone "$_upstream" "$_gitname"
          cd "$_gitname"
          
          msg2 "Fetching multimedia git"
          git remote add multimedia "$_gitroot"
          git fetch multimedia
          git checkout -b multimedia multimedia/master
      )
  fi
  (
      cd "$_gitname"
      msg2 "Updating multimedia git"
      git checkout -f multimedia
      # The multimedia branch sometimes rebases itself, so 
      # we fetch and hard reset. If you want to apply your
      # own patches to your local repository, replace this
      # line with just "git pull" - But be aware that
      # you're responsible for fixing desync in the future.
      git fetch multimedia && git reset --hard multimedia/master && git clean -xfd
      
      if [ "$_rebase" -gt 0 ]; then
        msg2 "Pulling upstream patches onto multimedia git"
        git fetch origin
        # Make a 'temp' branch to rebase on upstream
        git branch -D temp || true
        git checkout -b temp

        git merge --squash -Xours origin/master || true
        # -Xours resolves conflicts by just keeping the multimedia branch versions,
        # this might return errors due to deleted files, which don't present a problem
        # to the build
        
        # The following commit added GetPriority() to all the drivers:
        # http://source.winehq.org/git/wine.git/commitdiff/c4b94b1ba4ee1664c0a14515d6fbc6d648e9175b
        # So this patch adds the same thing to winepulse, fixing it refusing to load when rebasing
        # on upstream.
        # If this is failing to apply, its possible the multimedia folks added it, in which case
        # it can be dropped here.
        patch -Np1 -i "${srcdir}/winemultimedia_add_winepulse_getpriority.patch"
      fi
  )
  
  # Get rid of old build dirs
  rm -rf $_basename-{32,64}-build
  mkdir $_basename-32-build

  if [[ $CARCH == x86_64 ]]; then
    msg2 "Building Wine-64..."

    mkdir $_basename-64-build
    cd "$srcdir/$_basename-64-build"
    ../$_gitname/configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --libdir=/usr/lib \
      --with-x \
      --enable-win64

    make

    _wine32opts=(
      --libdir=/usr/lib32
      --with-wine64="$srcdir/$_basename-64-build"
    )

    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  msg2 "Building Wine-32..."
  cd "$srcdir/$_basename-32-build"
  ../$_gitname/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-x \
    "${_wine32opts[@]}"

  make
}

package() {
  msg2 "Packaging Wine-32..."
  cd "$srcdir/$_basename-32-build"

  if [[ $CARCH == i686 ]]; then
    make prefix="$pkgdir/usr" install
  else
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib32" \
      dlldir="$pkgdir/usr/lib32/wine" install

    msg2 "Packaging Wine-64..."
    cd "$srcdir/$_basename-64-build"
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib" \
      dlldir="$pkgdir/usr/lib/wine" install
  fi
}

# vim:set ts=8 sts=2 sw=2 et:
