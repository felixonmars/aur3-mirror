# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Emiliano Vavassori <syntaxerrormmm(at)gmail.com>

pkgname=ruby-minitest
pkgver=5.5.1
pkgrel=1
pkgdesc='A complete suite of testing facilities supporting TDD, BDD, mocking, and benchmarking'
arch=(any)
url='https://github.com/seattlerb/minitest'
license=('MIT')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/minitest-${pkgver}.gem"
        'LICENSE')
noextract=("minitest-${pkgver}.gem")
sha256sums=('d63b3136cc48295a1e80b6a3fb3638c3024e632ce53107ad3cc3f3fa32b012c1'
            'af538134326c884b9c274b0987777be79d168d950a6acc6a0091fe8031761f7c')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install \
    -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "minitest-${pkgver}.gem"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
