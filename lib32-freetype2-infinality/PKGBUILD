# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Contributor: Andre Fettouhi <A.Fettouhi@gmail.com>

_pkgbasename=freetype2-infinality
pkgname=lib32-$_pkgbasename
pkgver=2.4.9
pkgrel=2
_pkgdate=20120403
_pkgrel=02
pkgdesc="TrueType font rendering library (32-bit) with infinality patch"
arch=(x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('lib32-zlib' 'lib32-bzip2' $_pkgbasename)
optdepends=(
	'lib32-libxft-lcd: LibXft with custom LCD filter'
)
makedepends=(gcc-multilib)
conflicts=('lib32-freetype2')
provides=("lib32-freetype2=$pkgver")
options=('!libtool')
source=(
	"http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2"
	"http://www.infinality.net/fedora/linux/zips/freetype-infinality-${pkgver}-${_pkgdate}_${_pkgrel}-x86_64.tar.bz2"
	"freetype-2.2.1-enable-valid.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/freetype-2.2.1-enable-valid.patch?h=packages/freetype2"
        "bug35833.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/bug35833.patch?h=packages/freetype2"
        "bug35847.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/bug35847.patch?h=packages/freetype2"
)
md5sums=('77a893dae81fd5b896632715ca041179'
         '86aee24471768459e961e78fa31f3b88'
         '214119610444c9b02766ccee5e220680'
         '2721e68041d343f445910fdd254dbecd'
         '7779d2c170e211d50f32130e7b493920')

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	rm -rf "${srcdir}/freetype-${pkgver}-build"
	cp -a "${srcdir}/freetype-${pkgver}" "${srcdir}/freetype-${pkgver}-build"
	cd "${srcdir}/freetype-${pkgver}-build"
	cat "$srcdir/"*.patch | patch -Np1
	./configure --prefix=/usr --libdir=/usr/lib32
	make
}

package() {
	cd "${srcdir}/freetype-${pkgver}-build"
	make DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}
