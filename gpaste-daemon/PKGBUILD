# Maintainer: Ner0

pkgname=gpaste-daemon
pkgver=2.5
pkgrel=1
pkgdesc="A clipboard management daemon"
arch=('i686' 'x86_64')
url="https://github.com/Keruspe/GPaste"
license=('GPL3')
depends=('dbus-core' 'libxml2' 'gtk3')
makedepends=('dbus-core' 'libxml2' 'gtk3' 'intltool')
conflicts=('cinnamon-extension-gpaste' 'gpaste')
provides=('gpaste')
options=('!emptydirs')
install=$pkgname.install
source=("https://github.com/downloads/Keruspe/GPaste/${pkgname%-*}-${pkgver}.tar.xz")
md5sums=('44ddc08c5c03151867623da3cce06846')

build () {
  cd ${pkgname%-*}-${pkgver}

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --disable-applet \
              --disable-gnome-shell-extension \
              --disable-schemas-compile
  make ${MAKEFLAGS}
}

package () {
  cd "$srcdir/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
