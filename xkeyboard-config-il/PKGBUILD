# Maintainer: Jan de Groot <jgc@archlinux.org>
# il - new SI1452: Yoel Lion <yoel3ster@gmail.com>
pkgname=xkeyboard-config-il
pkgbasename=xkeyboard-config
pkgver=2.6
pkgrel=1
pkgdesc="X keyboard configuration files with new SI1452 layout patch"
arch=(any)
license=('custom')
url="http://www.freedesktop.org/wiki/Software/XKeyboardConfig"
depends=('xorg-xkbcomp')
makedepends=('intltool')
provides=('xkeyboard-config' 'xkbdata')
replaces=('xkeyboard-config' 'xkbdata')
conflicts=('xkeyboard-config' 'xkbdata')
source=(http://xorg.freedesktop.org/archive/individual/data/${pkgbasename}/${pkgbasename}-${pkgver}.tar.bz2 
il.patch extra.patch)
sha256sums=('22a1594116bf0ee3165f48487bdca4e5f25b2a1436e35e188b3ee38c543dabda' 
'8724e737f381464eccb7f7bd07464249ae3b81396f8b6437c7e8088bc1b58856' 
'a3910232a45b9bca6a4239d431a109ff83e7d1d21719f8295ab6f4ffff9b79ed')

build() {
  cd "${srcdir}/${pkgbasename}-${pkgver}"
  patch -p1 < ${srcdir}/il.patch
  patch -p1 < ${srcdir}/extra.patch
  ./configure --prefix=/usr \
      --with-xkb-base=/usr/share/X11/xkb \
      --with-xkb-rules-symlink=xorg \
      --enable-compat-rules=yes
  make
}
package() {
  cd "${srcdir}/${pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/X11/xkb/compiled"
  install -m755 -d "${pkgdir}/var/lib/xkb"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgbasename}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgbasename}/"
}
