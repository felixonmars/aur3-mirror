# Maintainer: PyroPeter <googlemail.com@abi1789>

pkgname=hplip-plugin
pkgver=3.12.6
pkgrel=1
pkgdesc="Binary plugin for HPs hplip printer driver library"
arch=('i686' 'x86_64')
url="http://hplipopensource.com/"
license=('unknown: proprietary')
makedepends=(python2 pygobject dbus-python)
depends=('hplip='$pkgver)
source=(
	"http://www.openprinting.org/download/printdriver/auxfiles/HP/plugins/hplip-$pkgver-plugin.run"
)
md5sums=('7563b7601f9c382a9d87490135eec6d0')

# http://hplip.sourceforge.net/plugin.conf

package() {
  cd "$srcdir/"

  if [ -w /etc/hp/hplip.conf ]; then
    echo 'You should not make this package as root!!111!1'
    return 1
  fi

  # untargziping the makeself selfextracting archive
  sh hplip-$pkgver-plugin.run --tar xvf

  sed -i 's!$HOMEDIR!'$pkgdir'/usr/share/hplip!g' plugin.spec
  sed -i 's!$RULESDIR!'$pkgdir'/etc/udev/rules.d!g' plugin.spec

  sed -i 's!if mode == INTERACTIVE_MODE:!installPlugin()\nsys.exit(0)\n\0!' \
      plugin_install.py
  sed -i 's!sys.exit(1)!raise!' \
      plugin_install.py

  python2 plugin_install.py

  while IFS= read -r -d '' link; do
    linkdir="`dirname $link`"
    linkdir="`readlink -e $linkdir`"
    linktarget="`readlink -e $link`"
    relpath="`python2 -c 'import sys,os.path; print os.path.relpath(sys.argv[1],sys.argv[2])' $linktarget $linkdir`"
    ln -sf "$relpath" "$link"
  done < <(find "$pkgdir" -type l -print0)

  mkdir -p "$pkgdir"/lib/udev
  mv "$pkgdir"/etc/udev/rules.d "$pkgdir"/lib/udev/
  chmod 0644 "$pkgdir"/lib/udev/rules.d/*
  rm -r "$pkgdir"/etc

  sed -i  -e "s|SYSFS|ATTR|g" \
	  -e "s|sysfs|attr|g" \
	  -e 's|"bin/sh|"/bin/sh|g' "$pkgdir"/lib/udev/rules.d/*.rules
}
