# Maintainer: t3ddy  <t3ddy1988 "at" gmail {dot} com>
# Contributor: Lex Rivera aka x-demon <aur@x-demon.org>
# Contributor: Det <nimetonmaili at gmail a-dot com>

pkgname=google-chrome-beta
pkgver=15.0.874.106   # Check for new Linux releases in: http://googlechromereleases.blogspot.com/search/label/Beta%20updates
pkgrel=1
pkgdesc="An attempt at creating a safer, faster, and more stable browser (Beta Channel)"
arch=('i686' 'x86_64')
url="http://www.google.com/chrome"
license=('custom:chrome')
depends=('alsa-lib' 'cairo' 'freetype2' 'gcc-libs' 'gconf' 'gtk2' 'hicolor-icon-theme' 'libpng12' 'libxslt' 'libxss' 'nss')
optdepends=('gecko-mediaplayer')
provides=("google-chrome=$pkgver")
conflicts=('google-chrome')
install=${pkgname}.install
_channel='beta'

if [ "$CARCH" = "i686" ]; then
    _arch='i386'    
    md5sums=('e4260e44de50732ef9ac08f6ba09afae')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='amd64'
    optdepends+=('flashplugin')
    md5sums=('ed690d01c7ff6fa2930a0b4863a3b74e')
fi

source=("http://dl.google.com/linux/direct/google-chrome-${_channel}_current_${_arch}.deb")


package() {
    msg "Extracting the Google Chrome package"
    ar -xv google-chrome-${_channel}_current_${_arch}.deb
    tar -xvf data.tar.lzma
    msg2 "Done extracting!"

    msg "Preparing install"
    install -d "$pkgdir"/{opt,usr/{bin,share/applications}}
    mv -v opt/google "$pkgdir"/opt
    msg2 "Done preparing!"

    msg "Actual installation"
    ln -sv /opt/google/chrome/google-chrome "$pkgdir/usr/bin/"
    ln -sv /usr/lib/mozilla/plugins "$pkgdir/opt/google/chrome"
    mv -v "$pkgdir/opt/google/chrome/google-chrome.desktop" "$pkgdir/usr/share/applications"

    mkdir "$pkgdir/opt/google/chrome/lib"
    cd "$pkgdir/opt/google/chrome/lib"
    ln -sv /usr/lib/libnspr4.so libnspr4.so.0d
    ln -sv /usr/lib/libnss3.so libnss3.so.1d
    ln -sv /usr/lib/libnssutil3.so libnssutil3.so.1d
    ln -sv /usr/lib/libplc4.so libplc4.so.0d
    ln -sv /usr/lib/libplds4.so libplds4.so.0d
    ln -sv /usr/lib/libsmime3.so libsmime3.so.1d
    ln -sv /usr/lib/libssl3.so libssl3.so.1d

    # Installing icons to /usr/share/icons/hicolor/
    for i in 16 32 48 256;
    do
        install -D "$pkgdir/opt/google/chrome/product_logo_${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/google-chrome.png"
    done
        
    msg2 "Installation finished!"
}
