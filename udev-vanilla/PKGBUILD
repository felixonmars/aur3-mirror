# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=udev-vanilla
_pkgname=udev
pkgdesc="The userspace dev tools (udev). Less annoying Arch junk."
pkgver=168
pkgrel=4
arch=('i686' 'x86_64')
url="http://www.kernel.org/pub/linux/utils/kernel/hotplug/udev.html"
license=('GPL')
depends=('libusb-compat' 'glib2' 'module-init-tools>=3.11' 'pciutils')
conflicts=('udev')
provides=("udev=$pkgver")
options=(!libtool)
makedepends=('pciutils' 'libusb-compat' 'glib2' 'gperf' 'libxslt' 'gobject-introspection')
backup=(etc/udev/udev.conf etc/modprobe.d/framebuffer_blacklist.conf)
install=udev.install
source=(http://www.kernel.org/pub/linux/utils/kernel/hotplug/$_pkgname-$pkgver.tar.bz2
        81-arch.rules)
md5sums=('4a466078532ab5dd5c35acc3ea2ec9a1'
         '72242dad9de6ed88faee459519ee7d53')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --sysconfdir=/etc \
              --with-rootlibdir=/lib \
              --libexecdir=/lib/udev \
              --sbindir=/sbin \
              --with-systemdsystemunitdir=/lib/systemd/system
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # fix file conflict in bluez
  rm -f "$pkgdir/lib/udev/hid2hci"

  # Install arch rules for grouping/perms
  install -D -m644 "$srcdir/81-arch.rules" "$pkgdir/lib/udev/rules.d/81-arch.rules"

  # /dev/loop0
  mknod -m 0660 "$pkgdir/lib/udev/devices/loop0" b 7 0
  chgrp disk "$pkgdir/lib/udev/devices/loop0"

  # create framebuffer blacklist
  mkdir -p "$pkgdir/etc/modprobe.d/"
  mapfile -t modules < <(find /lib/modules/*/kernel/drivers/video -name '*fb.ko.gz' -exec basename {} .ko.gz \; | sort -u)
  (( ${#modules[*]} )) && printf 'blacklist %s\n' "${modules[@]}" > "$pkgdir/etc/modprobe.d/framebuffer_blacklist.conf"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/lib/udev/rules.d/*.rules
}

