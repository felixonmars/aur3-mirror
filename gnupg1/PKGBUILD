# $Id: PKGBUILD 148992 2012-02-05 12:21:23Z andyrtr $
# Contributor: Andreas Radke <andyrtr at archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>
pkgname=gnupg1
pkgname_=gnupg
pkgver=1.4.12
pkgrel=2
pkgdesc="GNU Privacy Guard - a PGP replacement tool"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('zlib' 'bzip2' 'libldap>=2.4.18' 'libusb-compat' 'curl>=7.16.2' 'readline>=6.0.00')
source=(http://mirrors.dotsrc.org/gcrypt/gnupg/$pkgname_-$pkgver.tar.bz2)
install=gnupg.install
url="http://www.gnupg.org/"
md5sums=('ce3742e5c7912559cab7894ad8ba7f6b')

build() {
  cd "${srcdir}/${pkgname_}-${pkgver}"
  ./configure --prefix=/usr \
	--libexecdir=/usr/lib \
	--enable-noexecstack
  make
  #ln -s ${pkgname}-${pkgver}/scripts .. # seems obsolete now
}

check() {
  cd "$srcdir/$pkgname_-$pkgver"
  make -k check #All 27 tests passed
}

package () {
  cd "${srcdir}/${pkgname_}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # fix fileconflict with gnupg2 pkg
  rename 'gpg' 'gpg1' "${pkgdir}"/usr/share/man/man1/gpg* "${pkgdir}"/usr/bin/gpg*
}
