# Maintainer: Michael Lass <bevan@bi-co.net>
# Contributor: Szymon Jakubczak <szym-at-mit-dot-edu>

pkgname=openafs
pkgver=1.6.1
pkgrel=6
pkgdesc="An open source client for the AFS distributed file system"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('krb5')
makedepends=('autoconf' 'automake' 'bison' 'flex' 'gzip' 'linux-headers')
conflicts=('openafs-features' 'openafs-features-libafs')
backup=(etc/openafs/ThisCell
        etc/openafs/cacheinfo
        etc/openafs/afs.conf
        etc/openafs/afs.conf.client
        etc/openafs/CellServDB)
install=openafs.install
source=(http://openafs.org/dl/openafs/${pkgver}/${pkgname}-${pkgver}-src.tar.bz2
        openafs.rc
	afs.conf.client
	cacheinfo
	ThisCell
	fix-includes.patch)
md5sums=('7215609990a2b253f8980fc1e1da0530'
         '85fa92bdd773085c850ced56fbb76021'
	 'f3dc7efa634460f3d54584f409003369'
	 '4a1b86e3dbf8bd44f715e5c96b8e49c7'
	 'ee7512dad150430cd4f0b299c4e11b89'
	 '25fa4b96f30234372608e8703c27b2c2')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Fix missing include of sys/resources.h
  # Tis solves the error "‘PRIO_PROCESS’ undeclared"
  patch -p1 < ${srcdir}/fix-includes.patch

  ./regen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --disable-fuse-client
  make
}

package() {
  _kernelver=$(uname -r)
  _extramodules=$(readlink -fn /usr/lib/modules/${_kernelver}/extramodules)

  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # rename kpasswd which is already provided by krb5
  mv ${pkgdir}/usr/bin/kpasswd ${pkgdir}/usr/bin/kpasswd-openafs

  # create cache directory
  install -dm700 ${pkgdir}/var/cache/openafs

  # install kernel module
  install -dm755 ${pkgdir}${_extramodules}
  mv ${pkgdir}/usr/lib/openafs/libafs-${_kernelver}.mp.ko ${pkgdir}${_extramodules}/libafs.ko || \
  mv ${pkgdir}/usr/lib/openafs/libafs-${_kernelver}.ko    ${pkgdir}${_extramodules}/libafs.ko
  gzip -9 ${pkgdir}${_extramodules}/libafs.ko
  rmdir ${pkgdir}/usr/lib/openafs

  # move PAM libs
  install -dm755 ${pkgdir}/usr/lib/security
  mv ${pkgdir}/usr/lib/pam_afs.krb.so.1 ${pkgdir}/usr/lib/pam_afs.so.1 ${pkgdir}/usr/lib/security/

  # install init script
  install -Dm755 ${srcdir}/openafs.rc ${pkgdir}/etc/rc.d/${pkgname}

  # install default CellServDB and afs.conf
  install -D ${srcdir}/${pkgname}-${pkgver}/src/WINNT/install/wix/CellServDB ${pkgdir}/etc/${pkgname}/CellServDB
  install -D ${srcdir}/${pkgname}-${pkgver}/src/afsd/afs.conf.linux ${pkgdir}/etc/${pkgname}/afs.conf

  # install necessary configs
  install -D ${srcdir}/afs.conf.client ${pkgdir}/etc/${pkgname}/afs.conf.client
  install -D ${srcdir}/cacheinfo ${pkgdir}/etc/${pkgname}/cacheinfo
  install -D ${srcdir}/ThisCell ${pkgdir}/etc/${pkgname}/ThisCell

  # install license
  install -D ${srcdir}/${pkgname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
