# Maintainer: Michael Lass <bevan@bi-co.net>
# Contributor: Szymon Jakubczak <szym-at-mit-dot-edu>

pkgname=openafs
pkgver=1.6.1
pkgrel=11
pkgdesc="An open source client for the AFS distributed file system"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('krb5')
makedepends=('autoconf' 'automake' 'bison' 'flex' 'gzip' 'linux-headers')
conflicts=('openafs-features' 'openafs-features-libafs')
backup=(etc/conf.d/openafs
	etc/openafs/ThisCell
        etc/openafs/cacheinfo
        etc/openafs/CellServDB)
install=openafs.install
source=(http://openafs.org/dl/openafs/${pkgver}/${pkgname}-${pkgver}-src.tar.bz2
        http://openafs.org/dl/openafs/${pkgver}/${pkgname}-${pkgver}-doc.tar.bz2
        adjust-configs.patch
        openafs-client.rc
	fix-linux-3_5.patch
	fix-glibc-2_16.patch
	make_dir_dentry_aliases_act_like_symlinks.patch
	fix-linux-3_6.patch
	fix-linux-3_7.patch
	do_not_skip_flushing_pages_for_dv-0_files.patch)
md5sums=('7215609990a2b253f8980fc1e1da0530'
         '73d9a86038e827f95de90cf56c940bf7'
         '39bdbbae5d66a23da12bd438edee7bd3'
         '2c7ec958f3dd4526c39b1794a450119a'
         'ea52f1ca23710f00d3848092a6ebf420'
         '67b5cf636ba8724c760ee638efcf4cc7'
         '225c5c7bbb3ca29e5cd224066d6e1c94'
         '82319c26968954fa4d8fbaa563ce4476'
         '844724f04006422e863ec9b551e26b02'
         'c8ffb5b3457a57bdb02bf65a9a535b5e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Adjust RedHat config and serice file to our needs
  patch -p1 < ${srcdir}/adjust-configs.patch

  # Fix compatibility with linux 3.5
  # Picked from: - http://gerrit.openafs.org/7578
  #              - http://gerrit.openafs.org/7579
  patch -p1 < ${srcdir}/fix-linux-3_5.patch

  # Fix compatibility with glibc 2.16
  # Picked from: - http://gerrit.openafs.org/8155
  patch -p1 < ${srcdir}/fix-glibc-2_16.patch

  # Make dir dentry aliases act like symlinks
  # Picked from: - http://gerrit.openafs.org/7951
  patch -p1 < ${srcdir}/make_dir_dentry_aliases_act_like_symlinks.patch

  # Fix compatibility with linux 3.6
  # Picked from: - http://gerrit.openafs.org/8077
  #              - http://gerrit.openafs.org/8078
  #              - http://gerrit.openafs.org/8079
  #              - http://gerrit.openafs.org/8080
  #              - http://gerrit.openafs.org/8081
  #              - http://gerrit.openafs.org/8082
  #              - http://gerrit.openafs.org/8083
  #              - http://gerrit.openafs.org/8084
  patch -p1 < ${srcdir}/fix-linux-3_6.patch

  # Fix compatibility with linux 3.7
  # Picked from: - http://gerrit.openafs.org/8336
  #              - http://gerrit.openafs.org/8337
  #              - http://gerrit.openafs.org/8338
  patch -p1 < ${srcdir}/fix-linux-3_7.patch

  # Do not skip flushing pages for dv-0 files
  # Picked from: - http://gerrit.openafs.org/8465
  patch -p1 < ${srcdir}/do_not_skip_flushing_pages_for_dv-0_files.patch

  ./regen.sh -q
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --disable-fuse-client
  make
}


package() {
  _kernelver=$(uname -r)
  _extramodules=$(readlink -fn /usr/lib/modules/${_kernelver}/extramodules)

  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # rename kpasswd which is already provided by krb5
  mv ${pkgdir}/usr/bin/kpasswd ${pkgdir}/usr/bin/kpasswd-openafs
  mv ${pkgdir}/usr/share/man/man1/kpasswd.1 ${pkgdir}/usr/share/man/man1/kpasswd-openafs.1

  # create cache directory
  install -dm700 ${pkgdir}/var/cache/openafs

  # move kernel module
  install -dm755 ${pkgdir}${_extramodules}
  mv ${pkgdir}/usr/lib/openafs/libafs-${_kernelver}.mp.ko ${pkgdir}${_extramodules}/libafs.ko || \
  mv ${pkgdir}/usr/lib/openafs/libafs-${_kernelver}.ko    ${pkgdir}${_extramodules}/libafs.ko
  gzip -9 ${pkgdir}${_extramodules}/libafs.ko

  # move PAM libs
  install -dm755 ${pkgdir}/usr/lib/security
  mv ${pkgdir}/usr/lib/pam_afs.krb.so.1 ${pkgdir}/usr/lib/pam_afs.so.1 ${pkgdir}/usr/lib/security/

  # install init script and systemd service files
  install -Dm755 ${srcdir}/openafs-client.rc ${pkgdir}/etc/rc.d/openafs-client
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/packaging/RedHat/openafs-client.service ${pkgdir}/usr/lib/systemd/system/openafs-client.service
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/packaging/RedHat/openafs-server.service ${pkgdir}/usr/lib/systemd/system/openafs-server.service

  # install default configs
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/afsd/CellServDB ${pkgdir}/etc/${pkgname}/CellServDB
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/packaging/RedHat/openafs.sysconfig ${pkgdir}/etc/conf.d/openafs
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/packaging/RedHat/openafs-ThisCell ${pkgdir}/etc/${pkgname}/ThisCell
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/packaging/RedHat/openafs-cacheinfo ${pkgdir}/etc/${pkgname}/cacheinfo

  # install license
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
