# Maintainer: garion < garion @ mailoo.org >

pkgname=vobsub2srt-git
pkgver=20111121
pkgrel=1
pkgdesc="Convert IDX/SUB subtitles into SRT text subtitles"
arch=('i686' 'x86_64')
url="https://github.com/ruediger/VobSub2SRT/blob/master/README.org"
license=('GPL')
depends=('ffmpeg' 'libtiff' 'mplayer' 'tesseract')
makedepends=('cmake' 'git')

_gitroot="git://github.com/ruediger/VobSub2SRT.git"
_gitname="VobSub2SRT"

build() {
  cd $srcdir

  msg "Connecting to GIT server..."

  if [ -d "$srcdir/$_gitname" ] ; then
      cd $_gitname
      git config remote.origin.url $_gitroot
      git pull origin
      msg "The local files are updated."
  else
      git clone $_gitroot
      cd $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  #gillux's patch for tesseract 3.01
  sed -i -e 's!find_library(Tesseract_LIBRARIES NAMES tesseract_full tesseract_api!find_library(Tesseract_LIBRARIES NAMES tesseract!' $srcdir/$_gitname/CMakeModules/FindTesseract.cmake

  if [ -d "build" ]; then rm -rf build; fi
  ./configure || return 1
  make || return 1
}

package(){
  cd $srcdir/$_gitname
  mkdir -p build/install

  make DESTDIR="$srcdir/$_gitname/build/install" install || return 1
  cd build/install

  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/share/man/man1
  install -m 755 usr/local/bin/vobsub2srt $pkgdir/usr/bin/
  install -m 644 usr/local/share/man/man1/vobsub2srt.1 $pkgdir/usr/share/man/man1/
}
