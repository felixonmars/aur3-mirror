# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=sudokuki
pkgver=1.1.5
pkgrel=2
pkgdesc="A free graphical sudoku game written in Java"
arch=('i686' 'x86_64')
url="http://sudokuki.sourceforge.net/"
license=('GPL3')
depends=('java-runtime')
makedepends=('java-environment')
options=('!emptydirs' '!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop")
md5sums=('41553d6656f8b366b280012ddf984536'
         '98b5813d35496eda6faa943abad3010b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" noinst_libdir="" install

  # Fix path to java in the launcher
  sed -i 's|java|"$JAVA_HOME/bin/java"|' "${pkgdir}/usr/bin/${pkgname}"

  # Install a desktop entry
  install -Dm644 src/resources/files/net/jankenpoi/sudokuki/resources/images/logo.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install a readme file
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim:set ts=2 sw=2 et:
