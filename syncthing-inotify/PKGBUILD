# Maintainer: Stefan Tatschner <stefan@sevenbyte.org>

pkgname=syncthing-inotify
pkgver=0.5
pkgrel=3
pkgdesc="Inotify file watcher for Syncthing"
url="https://github.com/syncthing/syncthing-inotify"
license=('MPLv2')
arch=('i686' 'x86_64')
depends=('syncthing')
makedepends=('git' 'go' 'godep')
source=("$pkgname-$pkgver::git+https://github.com/syncthing/syncthing-inotify.git#commit=c9e038421e6f516a1264c8d6e58439592de7a4c8"
        "33.patch")
sha256sums=('SKIP'
            '6d5f0ad1863f3d58436439c4d568331d9709c7579d887668d3c0efe52727560e')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 -i "${srcdir}/33.patch"
    
    cd "${srcdir}"
    mkdir -p "src/github.com/syncthing"
    mv "${pkgname}-${pkgver}" "src/github.com/syncthing/${pkgname}"
}

build() {
    export GOPATH="${srcdir}"
    cd "${srcdir}/src/github.com/syncthing/${pkgname}"
    go get
    go build
}

check() {
    export GOPATH="${srcdir}"
    cd "${srcdir}/src/github.com/syncthing/${pkgname}"
    go test
}

package() {
    cd "${srcdir}/src/github.com/syncthing/${pkgname}"
    install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 "etc/linux-systemd/user/syncthing-inotify.service" "${pkgdir}/usr/lib/systemd/user/syncthing-inotify.service"
    install -Dm644 "etc/linux-systemd/system/syncthing-inotify@.service" "${pkgdir}/usr/lib/systemd/system/syncthing-inotify@.service"
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
