# Contributor: Naoya Inada <inaoya@kushinada.org>
#     Updated: Yuki Chiba <chiba@jaist.ac.jp>

pkgname=smlsharp
pkgver=1.0.0
pkgrel=1
pkgdesc="A new programming language in the Standard ML family"
arch=('i686' 'x86_64')
url="http://www.pllab.riec.tohoku.ac.jp/smlsharp/"
license=('custom')
depends=('gmp')
[ "$CARCH" == "x86_64" ] && depends=('lib32-gmp')
makedepends=()
source=(http://www.pllab.riec.tohoku.ac.jp/smlsharp/download/$pkgname-$pkgver.tar.gz)
md5sums=('7e932b2cdcec479352507a06b5348f82')

build() {
  cd $srcdir/$pkgname-$pkgver
  if [ "$CARCH" == "i686" ]; then
    ./configure --prefix=/usr --enable-thread
  else
    ./configure --prefix=/usr --enable-thread CC='gcc -m32' LD='ld -m elf_i386'
  fi

  make
}

package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  for file in LICENSE SMLNJ_LICENSE; do
    install -D -m 644 $file $pkgdir/usr/share/licenses/$pkgname/$file
  done
}
