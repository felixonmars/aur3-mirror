# Maintaniner: OmeGa <omega at mailoo dot org>

pkgname=firefox-spell-es-es
_addons_file=119323
pkgver=1.5
pkgrel=2
pkgdesc="Spanish (Spain) spellchecking dictionary for Firefox."
arch=('any')
url="https://addons.mozilla.org/addon/3554/"
license=('MPL')
depends=('firefox')
makedepends=('unzip')
source=(${pkgname}.xpi::https://addons.mozilla.org/firefox/downloads/file/${_addons_file}/diccionario_de_espanolespana-${pkgver}.xpi)
noextract=(${pkgname}.xpi)
sha1sums=('6446cdf657f9e32eca28063416be57598c9a8813')

build() {
  cd ${srcdir}
  [[ -d ${pkgname}.extract ]] || \
    mkdir ${pkgname}.extract
  cd ${pkgname}.extract
  unzip ../${pkgname}.xpi
}

package() {
  cd ${srcdir}/${pkgname}.extract 
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf) || return 1
  local dstdir=${pkgdir}/usr/lib/firefox/extensions/${emid}
  install -d ${dstdir}
  sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>20.*</em:maxVersion>#' install.rdf
  cp -R * ${dstdir}
  find ${pkgdir} -type d -exec chmod 0755 {} \;
  find ${pkgdir} -type f -exec chmod 0644 {} \;
}

# vim:set ts=2 sw=2 et:
