# $Id: PKGBUILD 112951 2014-06-10 07:50:39Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=autofs-sssd
pkgver=5.1.0
pkgrel=2
pkgdesc='A kernel-based automounter for Linux. Compiled with sssd support.'
arch=('i686' 'x86_64')
url='http://freshmeat.net/projects/autofs'
license=('GPL2')
depends=('libxml2')
makedepends=('libldap' 'krb5' 'kmod' 'sssd')
optdepends=('krb5: for LDAP support' 'sssd: for SSS support')
conflicts=('autofs')
replaces=('autofs')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc'
        'etc/autofs/auto.net'
        'etc/autofs/auto.smb'
        'etc/autofs/autofs_ldap_auth.conf'
        'etc/default/autofs')
options=(!makeflags)
source=("http://www.kernel.org/pub/linux/daemons/${pkgname%-*}/v5/${pkgname%-*}-${pkgver}.tar.xz")
sha256sums=('f4b343797e6df03627a9fd41bda6fd85acb84aa3fdd15694eec03ec90a52bc27')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  sed -i -e 's|/etc/auto.misc|/etc/autofs/auto.misc|' \
         -e 's|/etc/auto.master.d|/etc/autofs/auto.master.d|' samples/auto.master
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc/autofs --sbindir=/usr/bin \
    --with-mapdir=/etc/autofs --without-hesiod \
    --enable-ignore-busy --with-systemd
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  make INSTALLROOT="${pkgdir}" install
  rm -r "$pkgdir/run"
  install -dm755 "$pkgdir/etc/autofs/auto.master.d"
}

# vim:set ts=2 sw=2 et:
