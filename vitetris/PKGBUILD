# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=vitetris
pkgver=0.57
pkgrel=1
pkgdesc="A terminal-based Tetris clone with 2-player mode."
arch=('i686' 'x86_64')
url="http://victornils.net/tetris"
license=('BSD')
depends=('ncurses')
makedepends=('patch')
options=(!makeflags)
source=(http://victornils.net/tetris/$pkgname-$pkgver.tar.gz \
        $pkgname-makefile.diff)
md5sums=('07d02ee03e2edd66a8741729e237f21f'
         '5510adbb8ff3153466aa58c90d5765d8')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -Np0 -i ${srcdir}/$pkgname-makefile.diff || return 1

#can be built without ncurses if you like
  ./configure --prefix=${pkgdir}/usr --docdir=${pkgdir}/usr/share/$pkgname --without-x --with-ncurses || return 1
  make || return 1
  make gameserver || return 1
  make install || return 1
  install -D -m755 gameserver ${pkgdir}/usr/bin/$pkgname-gameserver || return 1

#add a high score file:
  install -d -m775 ${pkgdir}/var/games/
  install -D -g games -m664 /dev/null ${pkgdir}/var/games/vitetris/hiscores || return 1

#license
  install -D -m644 licence.txt ${pkgdir}/usr/share/licenses/$pkgname/LICENSE || return 1
}
