# Maintainer: Harms <thotro at lyse dot net>
# Contributor Pascal Gross√© <pascal.grosse@gmail.com>

pkgname=tome4
pkgver='1.2.5'
pkgrel='2'
pkgdesc="An open-source, single-player, role-playing roguelike game set in the world of Eyal."
arch=('i686' 'x86_64')
url="http://tome.te4.org/"
license=('custom' 'GPL3')
depends=('glu' 'openal' 'libvorbis' 'sdl2_ttf>=2.0.12' 'sdl2_image>=2.0.0' )
makedepends=('premake' 'zip' 'unzip')
options=(!makeflags emptydirs)
source=("http://te4.org/dl/t-engine/t-engine4-src-${pkgver}.tar.bz2"
	aur-${pkgname}-${pkgver}-${pkgrel}.patch
	tome4
	tome4.desktop)
conflicts=('tome4-beta' 'tome4-git' 'tome4-nomusic')
sha256sums=(
	'71d8129fcae5d2c0f0404a6aa9bc64954838518a36f5e6ad564883f9bddbd716'
	'9e4a657f4d6825b7d0419e63d28de427c3e91c48faf71538dee8d928132724ee'
	'3a1e4901337f91697bcfaf095f488c447c27ccc2484a543e31f024d5aefb61a0'
	'80e663d9eb2d41c6d9c0a6b4b5b52eb33b0872c3412a617530777fd137c0baa1'
)

###############################################################################
# 1. Patch the game.
###############################################################################
prepare() {
	cd "$srcdir"/t-engine4-src-${pkgver}
	patch -p1 < "${srcdir}/aur-${pkgname}-${pkgver}-${pkgrel}.patch"
}

###############################################################################
# 2. Build the game engine from source.
###############################################################################
build() {
	cd t-engine4-src-${pkgver}
	premake4 gmake
	make config=release
}

###############################################################################
# 3. Create the package. Copy all that shall be installed into the package dir.
###############################################################################
package() {
	# Extract and install the icon.
	unzip -oj -qq "${srcdir}/t-engine4-src-${pkgver}/game/engines/te4-${pkgver}.teae" \
		"data/gfx/te4-icon.png" -d "${srcdir}"
	install -Dm644 "${srcdir}/te4-icon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	
	# Install the documents.
	install -Dm644 "${srcdir}/t-engine4-src-${pkgver}/CONTRIBUTING" "${pkgdir}/usr/share/doc/${pkgname}/CONTRIBUTING"
	install -Dm644 "${srcdir}/t-engine4-src-${pkgver}/CREDITS" "${pkgdir}/usr/share/doc/${pkgname}/CREDITS"
	
	# Install the custom license into standard location.
	install -Dm644 "${srcdir}/t-engine4-src-${pkgver}/COPYING-TILES" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING-TILES"

	# Install the game executable, and the launcher.
	install -Dm755 "${srcdir}/t-engine4-src-${pkgver}/t-engine" "${pkgdir}/opt/${pkgname}/t-engine"
	install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	
	# Install the game data.
	cp -r "${srcdir}/t-engine4-src-${pkgver}/bootstrap" "${pkgdir}/opt/${pkgname}/"
	cp -r "${srcdir}/t-engine4-src-${pkgver}/game" "${pkgdir}/opt/${pkgname}/"
	
	# Delete built-in fonts from the game.
	zip -d -qq "${pkgdir}/opt/${pkgname}/game/engines/te4-${pkgver}.teae" \
		data/font/Vera.ttf \
		data/font/VeraBd.ttf \
		data/font/VeraIt.ttf \
		data/font/VeraMono.ttf \
		data/font/VeraMoBd.ttf \
		data/font/DroidSans.ttf \
		data/font/DroidSansMono.ttf \
		data/font/DroidSans-Bold.ttf \
		data/font/DroidSerif.ttf \
		data/font/DroidSerif-Bold.ttf \
		data/font/DroidSerif-Italic.ttf \
		data/font/DroidSerif-BoldItalic.ttf
}
