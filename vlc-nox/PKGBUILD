# Maintainer: Erus <erus.iluvatar@gmail.com>

_pkgbase=vlc
pkgname=vlc-nox
pkgver=1.1.12
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A enlighted version of the multi-platform MPEG, VCD/DVD, and DivX player"
url="http://www.videolan.org/vlc/"
license=('GPL')
provides=('vlc')
makedepends=('lirc-utils' 'libxpm' 'pkgconfig' 'live-media'
             'flac' 'libtheora' 'alsa-lib' 'jack' 'udev'
             'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'chrpath')
depends=('a52dec' 'libmtp' 'libdvbpsi' 'libcdio'
         'libdca' 'sdl_image' 'libdvdnav' 'mesa'
         'lua' 'libv4l' 'libcddb' 'smbclient' 'libmatroska' 'zvbi'
         'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2'
         'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug'
         'ttf-freefont' 'libxv' 'libass' 'xdg-utils')
optdepends=('ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'vdpau-video: VDPAU backend for VA API (for GPU acceleration on Nvidia cards)')
options=('!libtool')
install=vlc.install
source=("http://download.videolan.org/pub/videolan/${_pkgbase}/${pkgver}/${_pkgbase}-${pkgver}.tar.bz2")

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/misc/freetype.c
  ./configure --prefix=/usr --disable-qt4 --disable-skins2 \
              --disable-rpath \
              --disable-dbus \
              --enable-faad \
              --enable-v4l \
              --enable-snapshot \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --with-live555-tree=/usr/lib/live \
              --enable-realrtsp

  make
  make DESTDIR=${pkgdir}/ install
  for res in 16 32 48 128; do
      install -D -m644 ${srcdir}/vlc-${pkgver}/share/icons/${res}x${res}/vlc.png \
          ${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png
  done
  rm -rf ${pkgdir}/usr/lib/mozilla
}
md5sums=('91de1ad308c947e35380f9d747ff5713')
sha512sums=('62b6036fe47dd453e6ce5f2244495b11e70359790d6c56d908395b98ebb159f5dc1dcae78edb2cf1b79d2745cfacc76d8db2f3ffea7d52f6523f3c2ddafc0f4d')
