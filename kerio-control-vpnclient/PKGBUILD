# Maintainer: Muflone  <muflone(at)vbsimple(dot)net>
# Contributor: ssv1982 <ssv1982(at)gmail(dot)com>

pkgname=kerio-control-vpnclient
pkgver=7.4.0.4986
_pkgmainver=7.4.0
_pkgsubver=4986
pkgrel=1
pkgdesc="Kerio Control VPN client for corporate networks."
arch=('i686' 'x86_64')
url="http://www.kerio.com/control"
license=('custom:kerio-control-vpnclient')
options=(!strip)
install=${pkgname}.install
depends=(glibc zlib util-linux gcc-libs procps openssl dialog)
changelog=${pkgname}.changelog
_libdir=lib

if [ "$CARCH" = "x86_64" ]; then
  # Add some dependencies from multilib for 32 bit compatibility
  depends=(lib32-glibc lib32-zlib lib32-util-linux lib32-gcc-libs procps lib32-openssl dialog)
  _libdir=lib32
fi

source=("http://download.kerio.com/dwn/control/control-${_pkgmainver}-${_pkgsubver}/kerio-control-vpnclient-${_pkgmainver}-${_pkgsubver}-linux.deb"
        "kvpnc"
        "kvpnc.conf"
        "kvpnc.service"
        "kvpnc.init")
md5sums=('12fb3b822d851c00ed380e588d949545'
         'c053d47d7e7d35457cfcdd42ff44128f'
         'c11f487e65cef6432dc28969ff220583'
         '8ed74d4bf2191bef2e26439bb237d812'
         '0da68368f0c230c10d96b2f12489c2c1')

build() {
    # Get binary sources.
    bsdtar -xvf "$srcdir/data.tar.gz"
    rm -f "$srcdir/data.tar.gz"
    rm -f "$srcdir/control.tar.gz"
    rm -f "$srcdir/debian-binary"

    # Clean Debian package source files
    rm -r "$srcdir/etc"
    rm -r "$srcdir/usr/share/lintian"
    rm -f "$srcdir/data.tar.gz"

    # Removed unneeded files in package specific per Debian
    cd "$srcdir/usr/share/doc/$pkgname"
    rm -f "changelog.Debian.gz"
    rm -f "README"
    gzip -dfc "EULA.txt.gz" > "EULA.txt"
}

package() {
    # Create directories for package
    install -d "$pkgdir/usr/${_libdir}"
    install -d "$pkgdir/usr/"{sbin,share/licenses/$pkgname,share/doc/$pkgname}
    install -d "$pkgdir/usr/lib/$pkgname"
    
    # Install files in the package
    install -m 755 -t "$pkgdir/usr/sbin" kvpnc
    install -m 755 -t "$pkgdir/usr/lib/$pkgname" usr/sbin/kvpncsvc
    install -m 644 -t "$pkgdir/usr/${_libdir}" $srcdir/usr/lib/*
    install -m 644 -t "$pkgdir/usr/share/doc/$pkgname" $srcdir/usr/share/doc/$pkgname/{Acknowledgments.gz,copyright}
    install -m 644 -t "$pkgdir/usr/share/licenses/$pkgname" $srcdir/usr/share/doc/$pkgname/EULA.txt

    # Install common configuration file
    install -d "$pkgdir/etc/conf.d"
    install -m 644 -t "$pkgdir/etc/conf.d" kvpnc.conf

    # Install systemv service
    install -d "$pkgdir/etc/rc.d"
    install -m 755 kvpnc.init "$pkgdir/etc/rc.d/kvpnc"

    # Install systemd service
    install -d "$pkgdir/usr/lib/systemd/system"
    install -m 644 -t "$pkgdir/usr/lib/systemd/system" kvpnc.service
}
