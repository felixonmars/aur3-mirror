# Maintainer: György Balló <ballogy@freestart.hu>
_pkgname=seahorse-plugins
pkgname=$_pkgname-git
pkgver=20120101
pkgrel=1
pkgdesc="Plugins and extensions to third party software to provide encryption capability"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Seahorse/Plugins"
license=('GPL')
depends=('gconf' 'libcryptui' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('git' 'gnome-common' 'intltool' 'gnome-doc-utils')
options=('!emptydirs' '!libtool')
install=$_pkgname.install

_gitroot="git://git.gnome.org/seahorse-plugins"
_gitname="seahorse-plugins"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static --disable-schemas-install --disable-scrollkeeper \
               --disable-update-mime-database --disable-applet --disable-nautilus \
               --with-gconf-schema-file-dir=/usr/share/gconf/schemas --with-gtk=3.0
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install

  # Remove seahorse-tool (now it's in seahorse-nautilus package)
  rm "$pkgdir"/usr/bin/seahorse-tool \
     "$pkgdir"/usr/share/applications/seahorse-pgp-{encrypted,keys,signature}.desktop \
     "$pkgdir"/usr/share/man/man1/seahorse-tool.1 \
     "$pkgdir"/usr/share/seahorse-plugins/ui/seahorse-{multi-encrypt,notify,progress}.xml

}
