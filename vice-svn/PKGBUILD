# Maintainer:Lobotomius <lobotomius at gmail dot com>
# Contributor:grimi <grimi at poczta dot fm>
# Contributor:Sebastian Wolf <fatmike303 at gmail dot com>
# Last modified: 22/06/2013 23:41:20 +0100 PKGBUILD

pkgname=vice-svn
pkgver=27413
pkgrel=2
pkgdesc="A Versatile Commodore Emulator, development version, GTK3 and FFMPEG enabled"
arch=('i686' 'x86_64')
license=('GPL')
url="http://vice-emu.sourceforge.net"
depends=('vte3' 'giflib' 'ffmpeg')
makedepends=('xorg-bdftopcf' 'xorg-mkfontdir' 'subversion')
provides=("vice=$pkgver")
conflicts=('vice' 'vice-gnomeui' 'vice-gtkglext' 'vice-sdl')
source=('svn+https://vice-emu.svn.sourceforge.net/svnroot/vice-emu/trunk'
	'http://upload.wikimedia.org/wikipedia/en/5/5c/CBM_Logo.svg'
	'filechooser.c.patch' 'vice.texi.patch' 'configure.ac.patch'
	'x64.desktop')
md5sums=('SKIP'
         '7b3c3d3a220b747a00d9f507adb7ef0d'
         '2a0f30763044e6e686518dc5f06ab1d0'
         '85bd35e9b07bc83d5d046dd4a1de7032'
         '3b932a527397b0357f8a1bab0163e39d'
         '92381be443259ed06ef264596d0f09e4')

pkgver() {
	cd trunk; svnversion | tr -d [A-z]
}

build() {
	cd "$srcdir/trunk/vice"

	./autogen.sh

	patch -p1 src/arch/unix/x11/gnome/uifilechooser.c < "$srcdir/filechooser.c.patch"
	patch -p1 doc/vice.texi < "$srcdir/vice.texi.patch"
	patch -p1 configure.ac < "$srcdir/configure.ac.patch"

	./configure \
		--enable-arch=unix \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-fullscreen \
		--without-oss \
		--without-arts \
		--with-png \
		--without-x \
		--enable-ffmpeg \
		--enable-gnomeui3

	make
}

package() {
	cd "$srcdir/trunk/vice"
	make DESTDIR="$pkgdir" install
	mkdir -p "$pkgdir/usr/share/icons"
	mkdir -p "$pkgdir/usr/share/applications"
	install -m744 "$srcdir/CBM_Logo.svg" "$pkgdir/usr/share/icons/CBM_Logo.svg"
	install -m744 "$srcdir/x64.desktop" "$pkgdir/usr/share/applications/x64.desktop"
}

