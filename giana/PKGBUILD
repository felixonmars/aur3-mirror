# Maintainer: carstene1ns <arch carsten-teibes de>
# Contributor: megadriver <megadriver at gmx dot com>

pkgname=giana
pkgver=1.001
pkgrel=3
pkgdesc="Giana's Return, unofficial sequel to the (in)famous Mario clone Great Giana Sisters"
arch=('i686' 'x86_64')
url="http://www.gianas-return.de"
license=('custom: Freeware')
depends=('sdl_mixer' 'zlib' 'glut' 'glu')
optdepends=('libmodplug: better music decoder')
install=$pkgname.install
options=(!strip)
source=(http://www.gianas-return.de/gr-v1001-ubuntu.zip
        $pkgname.sh
        $pkgname.desktop
        $pkgname-fs.desktop)
md5sums=('9b6f8459ecd2483f0756e170908c780d'
         'SKIP'
         'SKIP'
         'SKIP')
sha256sums=('cf932707776c25444cb9ff9feb91489886efdc3764aa6aeeb53845f7c1816970'
            'SKIP'
            'SKIP'
            'SKIP')

package() {
  # create folders
  install -d $pkgdir/opt/$pkgname/data $pkgdir/usr/share/doc/$pkgname \
             $pkgdir/usr/share/licenses/$pkgname

  # install architecture dependent executeable
  case $CARCH in
    i686)
      install -m755 ${pkgname}_ubuntu32 $pkgdir/opt/$pkgname/$pkgname;;
    x86_64)
      install -m755 ${pkgname}_ubuntu64 $pkgdir/opt/$pkgname/$pkgname;;
  esac

  # install data files and create license file
  install -m644 data/*.zda $pkgdir/opt/$pkgname/data
  install -m644 *.txt $pkgdir/usr/share/doc/$pkgname
  tail -n +45 readme.txt | head -n 8 > $pkgdir/usr/share/licenses/$pkgname/FREEWARE

  # set group permission of folder, so we can save highscores and config later
  chgrp games $pkgdir/opt/$pkgname
  chmod g+w $pkgdir/opt/$pkgname

  # install launcher and desktop file
  install -Dm755 $pkgname.sh $pkgdir/usr/bin/$pkgname
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 $pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
