# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>

pkgname=wayland-mesa-git
pkgver=20101101
pkgrel=1
pkgdesc='Mesa OpenGL Library from GIT repository.'
url='http://www.mesa3d.org'
license=('custom')
arch=('x86_64' 'i686')
depends=('libgl-git' 'glproto-git' 'libdrm-git' 'dri2proto-git' 'libxxf86vm>=1.1.0' 'libxdamage>=1.1.2' 'expat>=2.0.1' 'libxmu>=1.0.5' 'libx11>=1.3.2' 'libxt>=1.0.7' 'libxext>=1.1.1' 'gcc-libs>=4.4.2')
makedepends=('git' 'pkgconfig' 'makedepend')
optdepends=('mesa-demos-git: for mesa demos (glxinfo, glxgears, ...)')
conflicts=('mesa-apps' 'mesa')
provides=('mesa=7.8.2' 'mesa-git')
options=('!makeflags')
source=('ftp://ftp.archlinux.org/other/mesa/gl-manpages-1.0.1.tar.bz2'
	'LICENSE')
md5sums=('6ae05158e678f4594343f32c2ca50515'
         '5c65a0fe315dd347e09b1f2826a1df5a')

_gitroot='git://anongit.freedesktop.org/mesa/mesa'
_gitname='mesa'

build() {
  msg 'Connecting to git.freedesktop.org GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
  else
    git clone ${_gitroot}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  # Work around a problem in LDFLAGS
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  cd ${srcdir}

  # Cleanup and prepare the build dir
  [ -d build ] && rm -rf build
  cp -r ${_gitname} build
  cd build

  msg 'Applying patches'
  # Revert "intel: sync to vblank by default"
  # This reverts commit e9bf3e4cc9a7e4bcd4c45bd707541d26ecdf0409.
  sed -i -e 's/DRI_CONF_VBLANK_ALWAYS_SYNC/DRI_CONF_VBLANK_DEF_INTERVAL_0/g' src/mesa/drivers/dri/intel/intel_screen.c

  ./autogen.sh \
    --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=swrast,radeon,r200,r300,r600,i810,i915,i965,unichrome,mach64,mga,r128,savage,sis,tdfx \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glut \
    --disable-gallium \
    --enable-egl \
    --enable-gles2

  msg 'Starting actual compile. Go get some coffee...'

  make

  msg 'Building man pages'

  cd ${srcdir}/gl-manpages-1.0.1

  ./configure --prefix=/usr

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  rm -f ${pkgdir}/usr/lib/libGL.so*
  rm -rf ${pkgdir}/usr/lib/xorg
  rm -f ${pkgdir}/usr/include/GL/glut*

  cd ${srcdir}/gl-manpages-1.0.1
  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
