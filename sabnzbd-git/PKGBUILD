# Maintainer: dryes <joswiseman@gmail>
# Contributor: Revelation60, Lucky <https://aur.archlinux.org/packages.php?ID=13691>
# Contributor: Gordin <9ordin @t gmail dot com>
pkgname='sabnzbd-git'
pkgver=20120228
pkgrel=1
pkgdesc='A web-interface based binary newsgrabber with NZB file support.'
arch=('any')
url='http://www.sabnzbd.org/'
license=('GPL')
depends=('par2cmdline' 'python2' 'python2-cheetah' 'python2-feedparser' 'python2-yenc' 'python2-pyopenssl' 'python2-gntp-git' 'unrar' 'unzip' 'sqlite' 'curl' 'zip')
makedepends=('git')
backup=('etc/conf.d/sabnzbd')
install='sabnzbd.install'
conflicts=('sabnzbd' 'sabnzbd-bzr' 'sabnzbd-develop-git')
provides=('sabnzbd')
replaces=('sabnzbd-bzr')
source=('sabnzbd.sh' 'sabnzbd.init' 'sabnzbd.confd' 'sabnzbd.desktop' 'addnzb.sh' 'nzb-2.png' 'sab2_64.png' 'x-nzb.xml')
md5sums=('4c4eed70f072687f448006eac9405c78'
         'cebd70ff1dc59414a811fa85b6d87094'
         '08c37de85c98008fd04cf7f187729c79'
         'f02b45e3030504b3f3b30836d406752f'
         'c0d52ba8d47f731790cd717fde46b829'
         '2a49c07b1e3e6448eabe92644315f983'
         'fdc878dd0f6f25617e627b04409abbbd'
         '11fb2cd1451e3725b08bfc2bd045be54')

_gitroot='git://github.com/sabnzbd/sabnzbd.git'
_gitname='sabnzbd'

build() {
  cd "${srcdir}"
  msg 'Connecting to GIT server....'

  if [ -d "${_gitname}" ] ; then
    cd "${_gitname}" && git pull origin
    msg 'The local files are updated.'
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg 'GIT checkout done or server timeout'
  
  cd "${srcdir}/${_gitname}"
  cat package.py | sed -r 's/^(PatchVersion\(release\))/\#\1/gi' package.py > package_.py
  python2 package_.py source
  rm package_.py
}

package() {
  mkdir -p "${pkgdir}/usr/share/doc/sabnzbd/examples/"
  cd "${srcdir}/${_gitname}/srcdist/"
  rm -rf win PKG-INFO Sample-PostProc.cmd
  mv licenses/ *.txt "${pkgdir}/usr/share/doc/sabnzbd/"
  mv Sample-PostProc.sh "${pkgdir}/usr/share/doc/sabnzbd/examples/"
  mv "${PWD}" "${pkgdir}/usr/share/sabnzbd/"

  install -Dm755 "${srcdir}/sabnzbd.sh" "${pkgdir}/usr/bin/sabnzbd"
  install -Dm755 "${srcdir}/sabnzbd.init" "${pkgdir}/etc/rc.d/sabnzbd"
  install -Dm644 "${srcdir}/sabnzbd.confd" "${pkgdir}/etc/conf.d/sabnzbd"
  install -Dm755 "${srcdir}/sabnzbd.desktop" "${pkgdir}/usr/share/applications/sabnzbd.desktop"
  install -Dm755 "${srcdir}/addnzb.sh" "${pkgdir}/usr/share/sabnzbd/addnzb.sh"
  install -Dm644 "${srcdir}/nzb-2.png" "${pkgdir}/usr/share/sabnzbd/nzb-2.png"
  install -Dm644 "${srcdir}/sab2_64.png" "${pkgdir}/usr/share/sabnzbd/sab2_64.png"
  install -Dm770 "${srcdir}/x-nzb.xml" "${pkgdir}/usr/share/sabnzbd/x-nzb.xml"
}
