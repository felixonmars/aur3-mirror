# $Id: PKGBUILD 80202 2012-11-17 15:45:28Z eric $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=pmount
pkgver=0.9.23
pkgrel=8
pkgdesc="mount removable devices as normal user"
arch=(i686 x86_64)
license=('GPL2')
url="http://pmount.alioth.debian.org/"
backup=('etc/pmount.allow')
depends=('sysfsutils>=2.0.0-1' 'util-linux')
makedepends=('intltool')
source=(
  https://alioth.debian.org/frs/download.php/file/3310/${pkgname}-${pkgver}.tar.bz2
  'http://anonscm.debian.org/gitweb/?p=pmount/pmount-debian.git;a=blob_plain;f=debian/completion/pmount'
  https://alioth.debian.org/tracker/download.php/31157/412979/313576/4753/fix-pmount-segfault.diff
)

build() {
  export CFLAGS="${CFLAGS} -fPIE -pie"
  export CXXFLAGS="${CXXFLAGS} -fPIE -pie"
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/fix-pmount-segfault.diff
  ./configure --prefix=/usr --disable-hal \
    --with-cryptsetup-prog=/usr/sbin/cryptsetup
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/pmount ${pkgdir}/usr/share/bash-completion/completions/pmount
  mkdir -p ${pkgdir}/media
}
md5sums=('db19f5bf3151b1b41705ec7bafa439d3'
         '224f24127f8ab3605de8b9bcb433882f'
         '5b223bce1fb262bbb98c541725f5e985')
