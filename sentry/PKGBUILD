# Maintainer: Benjamin A. Shelton <zancarius@gmail.com>
# Source: https://github.com/zancarius/archlinux-pkgbuilds

pkgname=sentry
pkgver=5.0.21
pkgrel=1
pkgdesc="Python-based realtime logging and aggregation server."
arch=(any)
url="http://pypi.python.org/pypi/sentry"
license=(BSD)
conflicts=('python2-sentry')
depends=(
    'django-crispy-forms>=1.1.4'
    'django-indexer>=0.3.0'
    'django-paging>=0.2.4'
    'django-picklefield>=0.2.0'
    'django-social-auth>=0.7.1'
    'django-social-auth-trello>=1.0.2'
    'django-templatetag-sugar>=0.1'
    'gunicorn>=0.14.6'
    python2
    'python-simplejson>=2.1.6'
    'python2-amqplib>=1.0.2'
    'python2-anyjson>=0.3.3'
    'python2-beautifulsoup3>=3.2.1'
    'python2-billiard>=2.7.3.12'
    'python2-dateutil>=1.5'
    'python2-django-celery>=2.5.5'
    'python2-httpagentparser>=1.0.5'
    'python2-kombu>=2.4.0'
    'python2-logan>=0.5.1'
    'python2-oauth2>=1.5.167'
    'python2-openid>=2.2'
    'python2-pynliner>=0.4.0'
    'python2-raven>=2.0.6'
    'python2-south>=0.7.6'
)
makedepends=(python2-distribute)
optdepends=(
    'mysql-python: MySQL backend support'
    'python2-psycopg2: PostgreSQL backend support'
)
install="${pkgname}.install"
source=(
    "http://pypi.python.org/packages/source/s/${pkgname}/${pkgname}-${pkgver}.tar.gz"
    "${pkgname}.install"
    "sentry.service"
)
md5sums=(
    3b91bdb21089c0174510f4d40e907fce # sentry tarball
    285bd39734a1b3d9f64e0b2f376f47b3 # install file
    5735e776b1842c686ebf074b1904e7bd # systemd service
)

build () {
    
    cd "${srcdir}/${pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}/" --optimize=1

    # Remove Desktop Services Store from package. This has been removed
    # from django-paging's Github repo but still persists in the PIP
    # package. Harmless, but I don't like them. :) More information:
    # http://en.wikipedia.org/wiki/.DS_Store
    find "${pkgdir}" -name '.DS_Store' -delete

}

package () {

    install -Dm0644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm0644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/sentry.service"

}