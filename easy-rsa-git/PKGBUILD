# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=easy-rsa-git
pkgver=20120502
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network) - git checkout"
arch=('i686' 'x86_64')
url="https://github.com/OpenVPN/easy-rsa"
depends=('openssl')
conflicts=('easy-rsa' 'openvpn<2.3.0')
provides=('easy-rsa')
license=('custom')

_gitroot="git://github.com/OpenVPN/easy-rsa.git"
_gitname="easy-rsa"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Build easy-rsa
  autoreconf -vi
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${_gitname}-build

  # Install easy-rsa
  mkdir -p ${pkgdir}/usr/share/easy-rsa/
  make DESTDIR=${pkgdir} install

  # remove old openssl config
  rm ${pkgdir}/usr/share/easy-rsa/openssl-0.9.{6,8}.cnf
}
