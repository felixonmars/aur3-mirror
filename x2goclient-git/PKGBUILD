# Maintainer: Markus Opitz <mastero23 at gmail dot com>

pkgname=x2goclient-git
pkgver=20120312
pkgrel=1
pkgdesc="Graphical client (qt4) for x2go"
arch=('i686' 'x86_64')
url="http://www.x2go.org/"
license=('GPL')
depends=('libcups' 'libssh' 'nxproxy-x2go-git' 'qt')
makedepends=('man2html')
optdepends=('pulseaudio: sound support')
conflicts=(x2goclient)
install=x2goclient-git.install

_gitname="${pkgname%-git}"
_gitroot="git://code.x2go.org/$_gitname.git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i 's/qmake-qt4/qmake/' Makefile
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
