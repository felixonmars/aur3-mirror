# Contributor: Blaž Tomažič <blaz.tomazic@gmail.com>

pkgname=clamtk
pkgver=4.43
pkgrel=1
pkgdesc="GUI front-end for ClamAV using gtk2-perl. It is designed to be an easy-to-use, lightweight, point-and-click desktop virus scanner for Linux."
arch=('any')
url="http://clamtk.sourceforge.net/"
license=('GPL')
depends=('clamav' 'perl' 'gtk2-perl' 'udev' 'perl-date-calc' 'perl-locale-gettext' 'perl-file-find-rule' 'perl-libwww' 'perl-net-dns' 'perl-http-message')
install='clamtk.install'
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('1c569e133c3dd5441f2eba8c8f0dc8574080143ee279ab82ad8a01585bb59010')

build() {
  return 0
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # executable
  install -Dm755  clamtk $pkgdir/usr/bin/clamtk
  
  # libs
  for n in lib/* ; do
	  install -Dm644 $n $pkgdir/usr/lib/perl5/vendor_perl/ClamTk/`basename $n`
  done
  
  #localization
  for n in po/*.mo ; do
	  install -Dm644 $n $pkgdir/usr/share/locale/`basename $n .mo`/LC_MESSAGES/clamtk.mo
  done
  
  # man pages
  install -Dm644 clamtk.1.gz $pkgdir/usr/share/man/man1/clamtk.1.gz
  
  # pixmaps
  install -Dm644 images/clamtk.xpm $pkgdir/usr/share/pixmaps/clamtk.xpm
  install -Dm644 images/clamtk.png $pkgdir/usr/share/pixmaps/clamtk.png
  install -Dm644 images/clamtk-loader.gif $pkgdir/usr/share/pixmaps/clamtk-loader.gif
  
  # menu
  install -Dm644  clamtk.desktop $pkgdir/usr/share/applications/clamtk.desktop
  # install -Dm644 clamtk-kde.desktop $pkgdir/usr/share/apps/konqueror/servicemenus/clamtk-kde.desktop
}

# vim:set ts=2 sw=2 et:
