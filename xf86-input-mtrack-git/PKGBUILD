pkgname=xf86-input-mtrack-git
pkgver=20121103
pkgrel=1
pkgdesc="A multitouch X driver using the kernel MT protocol"
arch=('i686' 'x86_64')
url="http://github.com/BlueDragonX/xf86-input-mtrack"
license=('GPL')
depends=('mtdev' 'libxss')
makedepends=('git' 'xorg-server-devel' 'resourceproto' 'glproto' 'autoconf' 'automake' 'libtool' 'pkg-config')
provides=('xf86-input-mtrack')
conflicts=('xf86-input-mtrack')
backup=('etc/X11/xorg.conf.d/10-mtrack.conf')
source=('10-mtrack.conf')
md5sums=('ffb540330d92957e0da28af5a005136e')

_gitroot="https://github.com/BlueDragonX/xf86-input-mtrack.git"
_gitname="mtrack"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    ( cd $_gitname && git pull origin; )
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  autoreconf --install
  ./configure --prefix=/usr
  make Makefile mtrack_drv.la config.h
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install-data
  install -Dm644 "$srcdir/10-mtrack.conf" "$pkgdir/etc/X11/xorg.conf.d/10-mtrack.conf"
}
