# Contributor: tobias [tobias [at] archlinux.org]
# Maintainer: Tom Vincent <http://www.tlvince.com/contact/>

pkgname=mutt-kiss
pkgver=1.5.21
pkgrel=2
pkgdesc='Mutt sans POP/IMAP/SMTP/SSL, with attachment patches'
url='http://www.mutt.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gpgme' 'ncurses' 'libidn')
conflicts=('mutt')
provides=('mutt')
source=("ftp://ftp.mutt.org/mutt/devel/mutt-${pkgver}.tar.gz"
        "patch-${pkgver}-r1.vk.pgp_verbose_mime"
        "mutt-attach.patch")
sha1sums=('a8475f2618ce5d5d33bff85c0affdf21ab1d76b9'
          '573699f4c4c038b9674714ab7849f9c88aa62059'
          '94da52d50508d8951aa78ca4b073023414866be1')

build() {
    cd "${srcdir}/mutt-${pkgver}"

    # Named PGP signature patch: http://dev.mutt.org/trac/ticket/3478
    patch -p1 < ${srcdir}/patch-${pkgver}-r1.vk.pgp_verbose_mime || return 1

    # Forgotten attachment detector
    patch -p1 < ${srcdir}/mutt-attach.patch || return 1

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-gpgme \
        --enable-hcache \
        --with-curses=/usr \
        --with-regex \
        --with-idn \

    make
}

package() {
    cd "${srcdir}/mutt-${pkgver}"
    make DESTDIR="${pkgdir}" install

    rm "${pkgdir}"/usr/bin/{flea,muttbug}
    rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
    rm "${pkgdir}"/etc/mime.types{,.dist}
    install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
