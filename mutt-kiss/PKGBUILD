# Contributor: tobias [tobias [at] archlinux.org]
# Maintainer: Tom Vincent <http://www.tlvince.com/contact/>

_pkgname=mutt
pkgname=$_pkgname-kiss
pkgver=1.5.21
pkgrel=4
pkgdesc='Mutt sans POP/IMAP/SMTP/SSL, with pgp/attachment patches'
url='http://www.mutt.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gpgme' 'ncurses' 'libidn')
conflicts=('mutt')
provides=('mutt')
source=("ftp://ftp.mutt.org/mutt/devel/${_pkgname}-${pkgver}.tar.gz"
        "pgp-verbose-mime.patch"
        "mutt-attach.patch"
        "confirm-crypt-hook.patch")
sha1sums=('a8475f2618ce5d5d33bff85c0affdf21ab1d76b9'
          '573699f4c4c038b9674714ab7849f9c88aa62059'
          '94da52d50508d8951aa78ca4b073023414866be1'
          'd3d40d8994702663042e876f91507c92a759a544')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # Named PGP signature patch: http://dev.mutt.org/trac/ticket/3478
  patch -p1 < ${srcdir}/pgp-verbose-mime.patch
  # Forgotten attachment detector
  patch -p1 < ${srcdir}/mutt-attach.patch
  # http://www.woolridge.ca/mutt/confirm-crypt-hook.html
  patch -p1 < ${srcdir}/confirm-crypt-hook.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gpgme \
    --enable-hcache \
    --with-curses=/usr \
    --with-regex \
    --with-idn \

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/bin/{flea,muttbug}
  rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
  rm "${pkgdir}"/etc/mime.types{,.dist}
  install -Dm644 contrib/gpg.rc "${pkgdir}/etc/Muttrc.gpg.dist"
}
