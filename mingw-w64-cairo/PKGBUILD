pkgname=mingw-w64-cairo
pkgver=1.12.2
pkgrel=2
pkgdesc="Cairo vector graphics library (mingw-w64)"
arch=(any)
url="http://cairographics.org"
license=("LGPL" "MPL")
makedepends=(mingw-w64-gcc patch)
depends=(mingw-w64-crt mingw-w64-glib2 mingw-w64-libpng mingw-w64-fontconfig mingw-w64-pixman)
options=(!libtool !strip !buildflags)
source=("http://cairographics.org/releases/cairo-${pkgver}.tar.xz"
"cairo-1.12.2-ignore-charset-for-non-cid-fonts.patch"
"cairo-1.12.2-reduce-broken-stopped-edge-continuation.patch")
md5sums=('87649eb75789739d517c743e94879e51'
         '0c9f4ff476fbf4af075cbf3ef8cbeeda'
         'befa1cc99b7039171fa6d8d247f0fead')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  export CFLAGS="$CFLAGS -mms-bitfields"
  cd "$srcdir/cairo-$pkgver"
  patch -Np1 -i ${srcdir}/cairo-1.12.2-reduce-broken-stopped-edge-continuation.patch
  patch -Np1 -i ${srcdir}/cairo-1.12.2-ignore-charset-for-non-cid-fonts.patch
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-xlib=no \
      --enable-xlib-xrender=no \
      --enable-win32=yes \
      --enable-win32-font=yes \
      --enable-png=yes \
      --enable-shared \
      --enable-static
    sed -i "s/deplibs_check_method=.*/deplibs_check_method=pass_all/g" libtool
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}