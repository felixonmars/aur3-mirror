# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=totem-gst
_pkgbase=totem
pkgname=totem-gst
true && pkgname=('totem-gst' 'totem-plugin-gst')
pkgver=3.0.1
pkgrel=1
pkgdesc="A GNOME3 integrated movie player based on GStreamer (Uses the old GStreamer backend without Clutter)"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('GPL2' 'custom')
depends=('gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'totem-plparser' 'libxxf86vm'
         'libxtst' 'desktop-file-utils' 'iso-codes' 'python2' 'libpeas' 'hicolor-icon-theme'
         'gnome-icon-theme-symbolic' 'gsettings-desktop-schemas' 'dconf')
makedepends=('gnome-common' 'intltool' 'gtk-doc' 'python2-gobject' 'nautilus' 'libgdata' 'xulrunner' 'lirc-utils'
             'libepc' 'bluez' 'vala' 'grilo')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/${pkgver%.*}/${_pkgbase}-${pkgver}.tar.bz2
        totem-3.2.patch)
sha256sums=('013b6cfedac6ff264aa27de7346597d0b8da7f4ed1f91b2c966a13d3fdff679a'
            '4e634d44fddf3f34693e6707c2b94fae760d09bebdcd0b23ffa6f22627b90e8d')
build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -Np1 -i "${srcdir}/totem-3.2.patch"
  chmod 755 data/uri-scheme-include.sh
  autoreconf -fi
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/totem \
    --localstatedir=/var \
    --disable-static \
    --enable-python \
    --enable-nautilus
  make
}

package_totem-gst() {
  groups=('gnome-extra')
  install=totem.install
  optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
              'gstreamer0.10-bad-plugins: Extra media codecs'
              'gstreamer0.10-ffmpeg: Extra media codecs'
              'lirc-utils: Infrared Remote Control plugin'
              'libepc: Publish Playlist plugin'
              'libgdata: YouTube Browser plugin'
              'bluez: Bemused plugin'
              'grilo-plugins: Browse sidebar (remote media)'
              'pyxdg: opensubtitles plugin')
  conflicts=(totem)
  provides=("totem=$pkgver")

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}/usr/lib/mozilla"
  rm "${pkgdir}/usr/lib/totem/totem-plugin-viewer"

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
    $pkgdir/usr/lib/totem/{totem/totem-bugreport.py,plugins/iplayer/iplayer2.py}

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/totem-gst/COPYING"
}

package_totem-plugin-gst() {
  pkgdesc="Totem plugin for web browsers"
  depends=("totem=${pkgver}")
  conflicts=(totem-plugin)
  provides=("totem-plugin=$pkgver")

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make -C browser-plugin \
    plugindir=/usr/lib/mozilla/plugins \
    xptdir=/usr/lib/mozilla/plugins \
    DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/totem-plugin-gst/COPYING"
}

# AUR workaround
pkgdesc="A GNOME3 integrated movie player based on GStreamer (Uses the old GStreamer backend without Clutter)"
depends=('gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'totem-plparser' 'libxxf86vm'
         'libxtst' 'desktop-file-utils' 'iso-codes' 'python2' 'libpeas' 'hicolor-icon-theme'
         'gnome-icon-theme-symbolic' 'gsettings-desktop-schemas' 'dconf')
