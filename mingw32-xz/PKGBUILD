pkgname=mingw32-xz
pkgver=5.0.4
pkgrel=1
pkgdesc="Library and command line tools for XZ and LZMA compressed files (mingw32)"
arch=(any)
url="http://tukaani.org/xz/"
license=("custom, GPL, LGPL")
depends=(mingw32-runtime)
makedepends=(mingw32-gcc)
options=(!libtool !buildflags !strip)
source=("http://tukaani.org/xz/xz-${pkgver}.tar.gz")
md5sums=('df3df690aef18384e1e031be7ec3a964')

build() {
  cd "${srcdir}/xz-${pkgver}"

  ./configure \
  --prefix=/usr/i486-mingw32 \
  --host=i486-mingw32
  make
}

package() {
  cd "${srcdir}/xz-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  i486-mingw32-strip "$pkgdir/usr/i486-mingw32/bin/"*.exe
  i486-mingw32-strip -x -g "$pkgdir/usr/i486-mingw32/bin/"*.dll
  i486-mingw32-strip -g "$pkgdir/usr/i486-mingw32/lib/"*.a
  rm -rf "$pkgdir/usr/i486-mingw32/share"
}
