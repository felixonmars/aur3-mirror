# Maintainer: Christoph Haag <haagch@studi,informatik.uni-stuttgart.de>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
pkgname=steamos-compositor
pkgver=1.14
pkgrel=1
pkgdesc="Compositor used by SteamOS \"based on xcompmgr by Keith Packard et al.\""
arch=('i686' 'x86_64')
url="http://repo.steampowered.com/steamos/pool/main/s/steamos-compositor/"
license=('GPL')
depends=('libxfixes' 'libxext' 'libxcomposite' 'libxdamage' 'libxrender' 'libxxf86vm' 'sdl_image' 'libgl')
makedepends=('mesa' 'libxt' 'libxpm' 'lesstif')
source=("http://repo.steampowered.com/steamos/pool/main/s/steamos-compositor/steamos-compositor_${pkgver}.tar.gz")
md5sums=('ba95b8241849bed2d0dde296b1ccd801')

prepare() {
  cd "$srcdir/$pkgname"
  rm -rf autom4te.cache aclocal.m4 configure
  autoreconf -i
}

build() {
  export LIBS="-lXm -lXext -lXt -lXpm -lXdamage -lXfixes"
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname"
  make -k check
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}
