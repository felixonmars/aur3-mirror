# Maintainer: 謝致邦<Yeking@Red54.com>

_doc_root=/srv/http
_server_root=/etc/tengine
_conf_path=${_server_root}/conf
_tmp_path=/var/spool/tengine
_log_path=/var/log/tengine
_user=http
_group=http

pkgname=tengine
pkgver=1.4.0
pkgrel=1
pkgdesc="hacked-up version of Nginx at Taobao"
arch=('i686' 'x86_64')
depends=('pcre' 'zlib' 'openssl')
url="http://tengine.taobao.org"
license=('2-clause BSD')
backup=("etc/tengine/conf/fastcgi.conf"
	"etc/tengine/conf/fastcgi_params"
	"etc/tengine/conf/koi-win"
	"etc/tengine/conf/koi-utf"
	"etc/tengine/conf/mime.types"
	"etc/tengine/conf/tengine.conf"
	"etc/tengine/conf/scgi_params"
	"etc/tengine/conf/uwsgi_params"
	"etc/tengine/conf/win-utf"
	"etc/logrotate.d/tengine"
	"etc/conf.d/tengine"
	"srv/http/50x.html")
source=(http://tengine.taobao.org/download/tengine-$pkgver.tar.gz
	tengine.rc
	tengine.patch)
md5sums=('ae9393bf58c5a38cd9913ce35e4e1baa'
         '98a9fd2318785b69fb2a22ecbe227909'
         'ff44cad3ed747ee36397262bd9c6c60d')

build() {
	cd $srcdir/tengine-$pkgver
	patch -Np1 -i $srcdir/tengine.patch
	./configure \
		--prefix=${_server_root} \
		--sbin-path=/usr/sbin/tengine \
		--conf-path=${_conf_path}/tengine.conf \
		--pid-path=/var/run/tengine.pid \
		--lock-path=/var/lock/tengine.lock \
		--http-client-body-temp-path=${_tmp_path}/client_body_temp \
		--http-proxy-temp-path=${_tmp_path}/proxy_temp \
		--http-fastcgi-temp-path=${_tmp_path}/fastcgi_temp \
		--http-log-path=${_log_path}/access.log \
		--error-log-path=${_log_path}/error.log \
		--user=${_user} --group=${_group} \
		--with-imap --with-imap_ssl_module --with-http_ssl_module \
		--with-http_stub_status_module \
		--with-http_dav_module \
		--with-http_gzip_static_module \
		--with-ipv6 \
		--http-scgi-temp-path=${_tmp_path} \
		--http-uwsgi-temp-path=${_tmp_path}
		#--add-module=/usr/lib/passenger/ext/tengine \
		#--with-http_mp4_module \
		#--with-http_realip_module \
		#--with-http_addition_module \
		#--with-http_xslt_module \
		#--with-http_image_filter_module \
		#--with-http_geoip_module \
		#--with-http_sub_module \
		#--with-http_flv_module \
		#--with-http_gzip_static_module \
		#--with-http_random_index_module \
		#--with-http_secure_link_module \
		#--with-http_degradation_module \
		#--with-http_perl_module \

	make
}

package() {
	cd $srcdir/tengine-$pkgver
	make DESTDIR=$pkgdir install

	install -d $pkgdir/etc/logrotate.d/
	cat <<EOF > $pkgdir/etc/logrotate.d/tengine
	$_log_path/*log {
		create 640 http log
		compress
		postrotate
			/bin/kill -USR1 \`cat /var/run/tengine.pid 2>/dev/null\` 2> /dev/null || true
		endscript
	}
EOF

	sed -i -e "s/\<user\s\+\w\+;/user $_user;/g" $pkgdir/$_conf_path/tengine.conf

	install -d $pkgdir/$_tmp_path

	# move default document root outside server root
	install -d $pkgdir/$_doc_root
	mv $pkgdir/$_server_root/html/* $pkgdir/$_doc_root/
	rm -rf $pkgdir/$_server_root/html
	rm -f $pkgdir/$_doc_root/index.html

	# let's create links for relative paths in config file
	ln -s $_log_path $pkgdir/$_server_root/logs
	ln -s $_doc_root $pkgdir/$_server_root/html

	install -D -m755 $srcdir/tengine.rc $pkgdir/etc/rc.d/tengine
	install -D -m644 LICENSE $pkgdir/usr/share/licenses/tengine/LICENSE
	mkdir -p $pkgdir/etc/conf.d
	echo "TENGINE_CONFIG=/etc/tengine/conf/tengine.conf" >$pkgdir/etc/conf.d/tengine
	rm -rf $pkgdir/var/run
}
