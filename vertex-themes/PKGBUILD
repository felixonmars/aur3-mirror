# Maintainer: CanalGuada <dguadal at free dot fr>
# Author: horst3180 @ deviantart

pkgname=vertex-themes
pkgver=20141110
pkgrel=1
pkgdesc='Vertex Gtk2, Gtk3, Metacity, Xfwm, Cinnamon and GNOME Shell themes (GNOME 3.14 version)'
_gnomever=3.14
arch=('any')
url='http://horst3180.deviantart.com/art/Vertex-Theme-470663601'
license=('GPL3')
depends=('gtk-engine-murrine')
provides=('vertex-themes')
conflicts=('vertex-themes-git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/horst3180/Vertex-theme/archive/${pkgver}.tar.gz")
sha256sums=('d498ece91b35f250a20409d35e102da2fdc40da989a86a97fceb833ce252490a')

build() {
  cd Vertex-theme-${pkgver}

  sed -i '125,126d' ./build.sh
  chmod +x ./build.sh
  ./build.sh
}
package() {
  cd Vertex-theme-${pkgver}

  for theme in "Vertex-Gnome-Shell" "Vertex_alt_metacity" "Vertex-Cinnamon"; do
    find "$theme" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done

  cd Gnome-${_gnomever}
  for theme in "Vertex" "Vertex-Dark" "Vertex-Light"; do
    find "$theme" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done

  cd ../Chrome
  mv Vertex-light.crx Vertex-Light.crx
  for theme in "Vertex" "Vertex-Light"; do
    mkdir -p "$pkgdir/usr/share/themes/$theme/Chrome/" 
    cp -dr --no-preserve='ownership' "$theme.crx" "$pkgdir/usr/share/themes/$theme/Chrome/"
  done

  cd ../Thunderbird
  for theme in "Vertex" "Vertex-Dark"; do
    cd "$theme"
    find "chrome" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/$theme/Thunderbird/{}" \;
    cd -
  done

  cd ../Firefox
  for theme in "Vertex" "Vertex-Dark" "Vertex-Light"; do
    cd "$theme"
    find "chrome" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/$theme/Firefox/{}" \;
    cd -
  done

  cd ..
  cp -dr --no-preserve='ownership' {LICENSE,README} "$pkgdir"/usr/share/themes/Vertex/
}

# vim: ts=2 sw=2 et:
