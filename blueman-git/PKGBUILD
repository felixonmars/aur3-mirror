# Contributor: twa022 <twa022 at gmail dot com>

_pkgname=blueman
pkgname=${_pkgname}-git
pkgver=5.764be4d
pkgrel=1
pkgdesc="GTK+ bluetooth manager adopted by LinuxMint"
arch=('i686' 'x86_64')
license=('GPL3')
url="https://github.com/linuxmint/blueman"
depends=('bluez4' 'bluez-libs' 'gnome-icon-theme' 'libpulse' 'obex-data-server'
         'python2-dbus' 'python2-notify' 'startup-notification' 'xdg-utils')
makedepends=('pyrex' 'intltool')
optdepends=('dnsmasq: For creating PAN network'
	    'modemmanager: For modem management service'
	    'notification-daemon: For desktop notifications'
	    'polkit-gnome: For privileged actions'
	    'pulseaudio: To use the pulseaudio plugin'
	    'python2-gconf: For gconf configuration support')
options=('!libtool')
install=$_pkgname.install
source=(https://launchpad.net/${_pkgname}/1.2/${pkgver}/+download/${_pkgname}-${pkgver}.tar.gz)
source=(${_pkgname}::git+https://github.com/linuxmint/blueman.git)
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_pkgname}
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${srcdir}/${_pkgname}"

  export PYTHON=python2
  for file in apps/blueman*; do
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done
  sed -i 's|"blueman-tray"|"blueman"|' blueman/plugins/applet/StatusIcon.py

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/blueman \
	--with-no-runtime-deps-check
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
  echo "NotShowIn=KDE;" >> ${pkgdir}/etc/xdg/autostart/blueman.desktop
}
