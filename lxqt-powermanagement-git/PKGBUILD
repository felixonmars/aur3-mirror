# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-powermanagement
pkgname=$_pkgname-git
pkgver=0.7.0
pkgrel=1
pkgdesc="LXQt power management daemon"
arch=("i686" "x86_64" "armv6h")
url="http://www.lxde.org"
license=("GPL2")
depends=("qt4" "liblxqt-git" "libxcb" "upower")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git://git.lxde.org/git/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
