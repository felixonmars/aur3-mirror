# Maintainer: Filip Brcic <brcha@gna.org>
# OldMaintainer: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
pkgname=ninja
pkgver=1.3.4
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=('i686' 'x86_64')
url='http://martine.github.io/ninja/'
license=(Apache)
depends=('gcc-libs')
makedepends=('asciidoc' 'python2' 're2c' 'git')
checkdepends=('gtest')
optdepends=('emacs: for Emacs mode')
install=ninja.install
source=("git://github.com/martine/ninja.git#tag=v${pkgver}")
md5sums=('SKIP')

_python="python2"

build() {
    cd "${srcdir}/ninja"
    ${_python} bootstrap.py
    if [ -x /usr/bin/emacs ] ; then
        emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
    fi
    asciidoc doc/manual.asciidoc
}

package() {
    cd "${srcdir}/ninja"

    # Main binary
    install -m755 -D ninja \
                     "${pkgdir}/usr/bin/ninja"

    # Manual
    install -m644 -D doc/manual.asciidoc \
                     "${pkgdir}/usr/share/doc/ninja/manual.asciidoc"
    install -m644 -D doc/manual.html \
                     "${pkgdir}/usr/share/doc/ninja/manual.html"

    # Emacs mode
    install -m644 -D misc/ninja-mode.el \
                     "${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.el"
    if [ -x /usr/bin/emacs ] ; then
        install -m644 -D misc/ninja-mode.elc \
                         "${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.elc"
    fi

    # Bash completion
    install -m644 -D misc/bash-completion ${pkgdir}/usr/share/bash-completion/completions/ninja

    # Zsh completion
    install -m644 -D misc/zsh-completion ${pkgdir}/usr/share/zsh/site-functions/_ninja
}

check() {
  cd ${srcdir}/ninja

  ${_python} configure.py --with-gtest=/usr/src/gtest
  ./ninja ninja_test
  ./ninja_test --gtest_filter=-SubprocessTest.SetWithLots
}
