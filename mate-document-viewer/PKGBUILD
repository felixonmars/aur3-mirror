# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-document-viewer
pkgver=1.6.1
pkgrel=3
pkgdesc="Simply a document viewer"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'djvulibre' 'gobject-introspection' 'gtk2'
         'libgxps' 'libmatekeyring' 'libsm' 'libspectre' 'mate-file-manager'
         'mate-icon-theme' 'poppler-glib' 't1lib' 'texlive-bin' 'zlib')
makedepends=('gtk-update-icon-cache' 'mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-extras')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('5b3d1643b7945d56214ec13dc38cb641dfb688e1')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-gtk-doc \
        --enable-djvu \
        --enable-dvi \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --disable-introspection \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    ln -s atril "${pkgdir}/usr/bin/mate-document-viewer"
}
