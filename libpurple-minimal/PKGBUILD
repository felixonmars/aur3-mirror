# Maintainer: Magicking <syllaur@gmail.com>
# Contributors: wzff BluePeppers n0nsense
pkgname=libpurple-minimal
pkgver=2.10.1
pkgrel=1
pkgdesc="minimal build of the pidgin IM library (no gstreamer, perl and X11 dependencies)"
arch=('i686' 'x86_64')
url="http://developer.pidgin.im/wiki/WhatIsLibpurple"
license=('GPL')
depends=('glib2' 'gnutls' 'libxml2' 'libidn' 'nss')
makedepends=('make' 'intltool')
conflicts=('gaim' 'pidgin' 'libpurple')
provides=("libpurple=${pkgver}")
source="http://downloads.sourceforge.net/pidgin/pidgin-${pkgver}.tar.gz"
md5sums=('0e2ee8f44d6621e5adb3a7e50281d14b')

build(){
	cd $srcdir/pidgin-${pkgver}
	./configure --prefix=/usr \
		    --sysconfdir=/etc \
		    --disable-perl \
		    --disable-gtkui \
		    --disable-consoleui \
		    --disable-sm \
		    --disable-gestures \
		    --disable-screensaver \
		    --disable-startup-notification \
		    --disable-schemas-install \
		    --disable-tcl \
		    --disable-tk \
		    --disable-nls \
		    --without-x \
		    --disable-vv \
		    --disable-gstreamer \
		    --disable-meanwhile \
		    --disable-avahi \
		    --disable-dbus \
		    --disable-doxygen || return 1
	make -C libpurple
}

package() {
	cd $srcdir/pidgin-${pkgver}
	make -C libpurple DESTDIR=$pkgdir install
}

