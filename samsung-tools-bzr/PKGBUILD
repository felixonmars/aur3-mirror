# Contributor: Fortunato Ventre (voRia) <vorione@gmail.com>

pkgname=samsung-tools-bzr
pkgver=506
pkgrel=1
pkgdesc="Tools for Samsung netbooks - Development version"
arch=('any')
url="https://launchpad.net/samsung-tools"
license=('GPLv3')
depends=('bzr' 'pm-utils' 'xbindkeys' 'rfkill' 'dbus-python' 'python-notify')
conflicts=('samsung-tools' 'samsung-tools-dev')
provides=('samsung-tools')
optdepends=('phc-intel: for CPU undervolting')
source=()
md5sums=()
sha1sums=()
backup=(etc/samsung-tools/system.conf
        etc/samsung-tools/session.conf
        etc/samsung-tools/scripts/bluetooth-on
        etc/samsung-tools/scripts/bluetooth-off
        etc/samsung-tools/scripts/wireless-on
        etc/samsung-tools/scripts/wireless-off)
install=samsung-tools.install

_bzrtrunk=lp:samsung-tools
_bzrmod=samsung-tools

build() {

  cd "$srcdir"

  msg "Connecting to the server...."
  if [ ! -d ./"$_bzrmod" ]; then
    bzr branch "$_bzrtrunk" "$_bzrmod"
  else
    cd "$_bzrmod"
    bzr revert
    cd ..
  fi
  msg "BZR checkout done or server timeout"

  cd "$srcdir/$_bzrmod"

  # Fix shebangs in all .py files in order to use python2
  find . -name \*.py | while read FILE; do
    sed -i "1s:#!/usr/bin/env python:#!/usr/bin/env python2:" "$FILE"
  done

  msg "Starting make..."
  make DESTDIR="$pkgdir/" install || return 1

  # Install /etc/rc.d/ script from bzr sources
  install -D -m755 "archlinux/samsung-tools.rcd" "$pkgdir/etc/rc.d/samsung-tools" || return 1
  
  # Use samsung-tools.install from bzr sources
  cat archlinux/samsung-tools.install > "$startdir/samsung-tools.install"
}
