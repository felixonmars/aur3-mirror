# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=kivy
pkgver=1.3.0
pkgrel=1
pkgdesc="A python module for developing multi-touch enabled media rich applications."
arch=(i686 x86_64)
url="http://kivy.org/"
license=('LGPL')
depends=('python-pygame' 'python-opengl' 'python-imaging'
         'gstreamer0.10-python' 'cython2' 'mtdev')
optdepends=('twisted: networking framework integration')
source=("http://pypi.python.org/packages/source/K/Kivy/Kivy-$pkgver.tar.gz")
md5sums=('6f3aa6c75fc2f249d941b0ed01a44e05')

build() {
  cd "$srcdir/Kivy-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/Kivy-$pkgver"
  python2 setup.py install --root="$pkgdir"

  # python2 fixes
  sed -i "s/\#\!.*python/&2/" \
    `grep -rl "\#\!.*python" "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
