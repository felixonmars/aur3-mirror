# A temporary package based on aur:owncloud-client
# Original PKGBUILD Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.

_buildtype='Release'

##############################################################

_name=mirall
pkgname=owncloud-client-qt5
pkgver=1.7.1
pkgrel=2
pkgdesc='ownCloud client based on mirall'
arch=('i686' 'x86_64' 'armv7h')
url='http://owncloud.org/'
license=('GPL2')
depends=('qtkeychain-qt5' 'qtwebkit' 'neon')
makedepends=('cmake' 'python-sphinx')
optdepends=('python2-nautilus: integration with Nautilus')
provides=('mirall' 'ocsync')
conflicts=('mirall-git' 'ocsync')
install=owncloud-client.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2"
        "https://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2.asc")
md5sums=('5355f5dee2beb2d2dc39c8ad77511c0b'
         'SKIP')

if [[ ! ${_buildtype} == 'Release' ]] && [[ ! ${_buildtype} == 'release' ]]; then
  options=('debug')
fi

prepare() {
  if [[ -e ${srcdir}/${_name}-${pkgver}-build ]]; then rm -rf ${srcdir}/${_name}-${pkgver}-build; fi
  mkdir ${srcdir}/${_name}-${pkgver}-build
}

build() {
  cd ${srcdir}/${_name}-${pkgver}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        ../${_name}-${pkgver}
  make
  make doc-man
}

package() {
  cd ${srcdir}/${_name}-${pkgver}-build
  make DESTDIR=${pkgdir} install
}
