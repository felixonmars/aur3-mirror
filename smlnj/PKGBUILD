# Maintainer: Thomas Wei√üschuh <thomas t-8ch de>

pkgname=smlnj
pkgver=110.75
pkgrel=1
pkgdesc="Standard ML of New Jersey, a compiler for the Standard ML '97 programming language"
url="http://www.smlnj.org/"
license=(BSD)
arch=(i686 x86_64)
provides=(sml)

if [ "$CARCH" = "x86_64" ]; then
  makedepends=('gcc-multilib')
  depends=('lib32-glibc')
else
  # everything in base{,-devel}
  makedepends=()
  depends=()
fi

_url="http://smlnj.cs.uchicago.edu/dist/working/${pkgver}/"

source=(
  "urlgetter.sh"

  "smlnj-${pkgver}-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"

  "smlnj-${pkgver}-config.tgz::${_url}config.tgz"

  "smlnj-${pkgver}-cm.tgz::${_url}cm.tgz"
  "smlnj-${pkgver}-compiler.tgz::${_url}compiler.tgz"
  "smlnj-${pkgver}-runtime.tgz::${_url}runtime.tgz"
  "smlnj-${pkgver}-system.tgz::${_url}system.tgz"
  "smlnj-${pkgver}-MLRISC.tgz::${_url}MLRISC.tgz"
  "smlnj-${pkgver}-smlnj-lib.tgz::${_url}smlnj-lib.tgz"

  "smlnj-${pkgver}-ckit.tgz::${_url}ckit.tgz"
  "smlnj-${pkgver}-nlffi.tgz::${_url}nlffi.tgz"

  "smlnj-${pkgver}-cml.tgz::${_url}cml.tgz"

  "smlnj-${pkgver}-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "smlnj-${pkgver}-ml-lex.tgz::${_url}ml-lex.tgz"
  "smlnj-${pkgver}-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "smlnj-${pkgver}-ml-burg.tgz::${_url}ml-burg.tgz"

  "smlnj-${pkgver}-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
)

_unused=(
  "smlnj-${pkgver}-eXene.tgz::${_url}eXene.tgz"

  "smlnj-${pkgver}-pgraph.tgz::${_url}pgraph.tgz"
  "smlnj-${pkgver}-heap2asm.gz::${_url}heap.asm.tgz"
)

build() {

  cd "$srcdir"

  # The build system uses this env variable
  URLGETTER="$srcdir/urlgetter.sh"

  # urlgetter.sh uses these variables
  export srcdir
  export pkgver

  # Parallel builds won't work
  unset MAKEFLAGS

  # Confuses install.sh
  unset SMLNJ_HOME

  config/install.sh
}

package() {

  cd "$srcdir"

  mkdir -p "$pkgdir/usr/lib/smlnj/" "$pkgdir/usr/bin/"
  cp -R "$srcdir/lib/"* "$pkgdir/usr/lib/smlnj/"
  cp -R "$srcdir/bin" "$pkgdir/usr/"
}

sha256sums=('4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            '99f986c12474d663984156df9784b2bc8578ce89927fe5c8ba9e68ac09b4469d'
            '3c452eadc0da4eda4f1de1fe5c842eb42b88e1ea1fefe3212f82827052cfe1b0'
            '4604fac13f3810673233ab7b31649035256f78d0ad86b6a6b43f37ffc5ddb12b'
            'c72c25fa9f844e085448ef07ec5c7f44ebaec44a4ecfbafab38e64193b49bf21'
            '30fe442f42414908df3377a06c8e79af569ea13a43c9a7d3bbf052debbdec412'
            '973f5ebd3b9823365b9dadbf370c0aa131df94d4d80ada660bb05a3010776a2e'
            '4daf94fb6bc8ff836a677616fa3aa6d001ad42829af20e662cbed8720b3e7237'
            '2f8196ce2f7509c0ac0a0a69107c1d681554d1c58ba1bee92ef833816341caed'
            'b5849f96b3cde1d8d33ba94e41c9093fc2f9f35db3499086c69a8552630f9477'
            '0daea7803edb7e836d45676d00e8c2cef1439daff89eaccda608144d23ad82d6'
            '0bb3ae6feda0f2b4dc86a3e386462319761733c71aeff36aadcd625790d562c6'
            '2a8138c219dd5f42970c1a8ed9fb1e00dcf45d450c5f129bfb184819c36a2c06'
            '307628841271f942df4db0d782e164f25a9fb07ce12b2083e4d6eef898c07266'
            'bd9b35557c520af1251a19b096ec0106bc6c03b7ebab2cf1391649c5c6b222f3'
            'd65e37281de97d3501356e6f5e842e3b247fb1a2f5580cc419d44d4f3f060e5f'
            'e04a6740166d7f2da41923400fae3b9661d1e52dce4f64efff3a3780c9a6a43c')
