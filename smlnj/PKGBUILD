# Maintainer: Thomas Wei√üschuh <thomas t-8ch de>

pkgname=smlnj
pkgver=110.77
pkgrel=2
pkgdesc="Standard ML of New Jersey, a compiler for the Standard ML '97 programming language"
url="http://www.smlnj.org/"
license=(BSD)
arch=(i686 x86_64)
provides=(sml)

if [ "$CARCH" = "x86_64" ]; then
  makedepends=('gcc-multilib')
  depends=('lib32-glibc')
else
  # everything in base{,-devel}
  makedepends=()
  depends=()
fi

_url="http://smlnj.cs.uchicago.edu/dist/working/${pkgver}/"

source=(
  "urlgetter.sh"
  "profile.d-smlnj.sh"

  "smlnj-${pkgver}-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"

  "smlnj-${pkgver}-config.tgz::${_url}config.tgz"

  "smlnj-${pkgver}-cm.tgz::${_url}cm.tgz"
  "smlnj-${pkgver}-compiler.tgz::${_url}compiler.tgz"
  "smlnj-${pkgver}-runtime.tgz::${_url}runtime.tgz"
  "smlnj-${pkgver}-system.tgz::${_url}system.tgz"
  "smlnj-${pkgver}-MLRISC.tgz::${_url}MLRISC.tgz"
  "smlnj-${pkgver}-smlnj-lib.tgz::${_url}smlnj-lib.tgz"

  "smlnj-${pkgver}-ckit.tgz::${_url}ckit.tgz"
  "smlnj-${pkgver}-nlffi.tgz::${_url}nlffi.tgz"

  "smlnj-${pkgver}-cml.tgz::${_url}cml.tgz"

  "smlnj-${pkgver}-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "smlnj-${pkgver}-ml-lex.tgz::${_url}ml-lex.tgz"
  "smlnj-${pkgver}-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "smlnj-${pkgver}-ml-burg.tgz::${_url}ml-burg.tgz"

  "smlnj-${pkgver}-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
)

_unused=(
  "smlnj-${pkgver}-eXene.tgz::${_url}eXene.tgz"

  "smlnj-${pkgver}-pgraph.tgz::${_url}pgraph.tgz"
  "smlnj-${pkgver}-heap2asm.gz::${_url}heap.asm.tgz"
)

build() {

  cd "$srcdir"

  # The build system uses this env variable
  URLGETTER="$srcdir/urlgetter.sh"

  # urlgetter.sh uses these variables
  export srcdir
  export pkgver

  # Parallel builds won't work
  unset MAKEFLAGS

  # Confuses install.sh
  unset SMLNJ_HOME

  config/install.sh
}

package() {

  cd "$srcdir"

  mkdir -p "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/"{bin,lib} "$pkgdir/usr/lib/smlnj"

  mkdir -p "$pkgdir/etc/profile.d"
  cp "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"

  for bin in ml-build heap2exec ml-makedepend; do
    echo "s#$srcdir#/usr/lib/smlnj#g" "$pkgdir/usr/lib/smlnj/bin/$bin"
    sed -i -e "s#$srcdir#/usr/lib/smlnj#g" "$pkgdir/usr/lib/smlnj/bin/$bin"
  done
}

sha256sums=('4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '9982a36d9fb3d2d15590d0d8bb15a30d3e684ad49f56d69eef8684f4eaf3bf02'
            '6bed2026e05367f247e2fcfa918edb835bdab24182f02f3253c1baf2f301b552'
            '1c60316b3f16ae1a93cb87e281edcf2a84a554ac0415f010b8d565c2ae3a0a82'
            'fcc7388167ee2d0048644bb2147bbca3e16f60be326dd724407e0f51e254e52f'
            '8e40aa7d39c806d36360174d36bc376fa53662b5ac4f0a4add120c8c2fe67f2b'
            '367cce5e0ab848d28bc019862a30c2b5b774fd4733301bb72fec811d8e9d7f54'
            '4b443ee9e27bc8d2010bdce5724302c7d338e8eba0051d45cb4e384204488a76'
            '9232f7cf61a9a7a859dc9249a47a77b954a746137e8d8f6e57236f8c030677e7'
            '29145b1739d46173cbaa87456b2bed35977c4ae7e83c48134a591459939b3966'
            '8729d2aa203329fa76f9a7dccafe6fe88d0f397f1238727fea8fa1c67ae38cf0'
            'bc4548eda9531c52029ed68a7988985b62ad17e95841c04dbbe32b4b30df4ef7'
            'eff30e6a3c4afb7f8265650e3e1cc83a8ff252ed19f22b30c2c431c9beca126e'
            'c4c0e108bf166f77e083692a27f9231d730c5b2a73a2f9f5409e55928c679553'
            '24a2b1d955e990c6f2a64d6be4df0c7d9bf430f56501587ecf95d30732cf266c'
            '9bc7bb014d34a63cb3ac8c42104bd579e8990ab2803c32a6a4410fcf8cfcbd4c'
            'e6c7a4ed96de076b8d27af44eb8bfc1121e32f915d9a0b8fc356b5a27c761908')
