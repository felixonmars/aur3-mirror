# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
pkgname=dockmanager
pkgver=0.1.0
pkgrel=2
pkgdesc="Dock-independent helper scripts for compatible docks"
url="https://launchpad.net/dockmanager"
arch=(i686 x86_64)
license=(GPL)
depends=('libdesktop-agnostic>=0.3.90-2' gconf dbus-glib python2)
makedepends=('vala>=0.10' pkgconfig intltool autoconf)
conflicts=(dockmanager-bzr)
options=('!libtool' '!emptydirs')
install=dockmanager.install
source=("http://launchpad.net/dockmanager/trunk/0.1.0/+download/dockmanager-0.1.0.tar.gz" dockmanager.install)
md5sums=('52a1c833a8f3599156dc00cd52ca624a'
         '462b3dfcd4c87e83b023680acbabd084')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON=python2

  ./configure --prefix=/usr --enable-release

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  # Fix for python2
  find $pkgdir -type f -exec sed -i '1s|#!/usr/bin/env python$|&2|' {} +

  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/dockmanager.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -rf "$pkgdir/etc/gconf"
}
