# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Lucas De Marchi <lucas.de.marchi@gmail.com>
# Contributor: Matthias Meulien orontee@gmail.com

pkgname=global
pkgver=6.2.12
pkgrel=1
pkgdesc="A source code tag system"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/global/"
license=('GPL')
depends=('libltdl' 'bash' 'perl')
optdepends=('idutils' 'ctags')
options=(!emptydirs !libtool)
install=global.install
source=(http://tamacom.com/global/${pkgname}-${pkgver}.tar.gz)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --with-exuberant-ctags=/usr/bin/ctags
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make -k check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/emacs/site-lisp"
	ln -s ../../gtags/gtags.el "${pkgdir}/usr/share/emacs/site-lisp/gtags.el"
}

md5sums=('a526e9542b95ee5549b9aa40704bb7bd')
sha256sums=('caf1cfa83a6e35423a3c581ff53ccebdcfa64ce9855be426c4de681694875316')
