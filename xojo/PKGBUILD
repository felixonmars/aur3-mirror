# Contributor: Schala <schalaalexiazeal@gmail.com>
# Maintainer: Ng Oon-Ee <n g  o o n  e e dot t a l k AT g m a i l DOT c o m>
pkgname=xojo
pkgver=2014r1
pkgrel=1
pkgdesc="A RAD environment based on BASIC that compiles native applications for Windows, Mac, Linux, and the web."
arch=(i686 x86_64)
url="http://www.xojo.com"
license=("custom")
source=("http://41160df63757fc043cfd-66287f38a83954e31a54d1dbe33e0650.r4.cf2.rackcdn.com/Xojo$pkgver/xojo$pkgver.tgz"
"xojo.desktop"
"lingua.desktop"
"RemoteDebuggerDesktop.desktop")
[ $CARCH = i686 ] && depends=(gtk2 expat glib2 mesa)
[ $CARCH = x86_64 ] && depends=(lib32-gtk2 lib32-expat lib32-glib2 lib32-mesa)
md5sums=('7dc0b90d42bfbcecaca4b324910df1c7'
         '9092b4d5b10a3f4c2aad2479319874c4'
         '75b76a955017807cc43a05a3a8186828'
         '1d97e0e9df4ee477fe1cbb1a57973615')

package() {
	cd $srcdir
	find . -name '.DS_Store' -exec rm {} \;
	find "./xojo$pkgver/Extras" -type d \( -name 'OS X' -o -name 'Windows' -o -name 'Visual Studio*' -o -name 'Xcode' \) |
    xargs -rtl1 -I {} rm -r {}
	mkdir -p "$pkgdir/usr/bin" "$pkgdir/opt" "$pkgdir/usr/share/licenses/xojo"
	cp -r "xojo$pkgver" "$pkgdir/opt/xojo"
	ln -s "/opt/xojo/Xojo" "$pkgdir/usr/bin/Xojo"
	ln -s "/opt/xojo/Extras/Lingua/Lingua Linux" "$pkgdir/usr/bin/Lingua"
	ln -s "/opt/xojo/Extras/Remote Debugger Desktop/Linux/Remote Debugger Desktop" "$pkgdir/usr/bin/RemoteDebuggerDesktop"
	ln -s "/opt/xojo/Extras/RemoteDebugger Console/Linux/RemoteDebuggerConsole" "$pkgdir/usr/bin/RemoteDebuggerConsole"
	install -Dm644 "$srcdir/xojo.desktop" "$pkgdir/usr/share/applications/xojo.desktop"
	install -m644 "$srcdir/lingua.desktop" "$pkgdir/usr/share/applications/"
	install -m644 "$srcdir/RemoteDebuggerDesktop.desktop" "$pkgdir/usr/share/applications/"
	install -Dm644 "$pkgdir/opt/xojo/xojo.xpm" "$pkgdir/usr/share/pixmaps/xojo.xpm"
	ln -s "/opt/xojo/Read Mes/License Agreement.txt" "$pkgdir/usr/share/licenses/xojo/License Agreement.txt"
	ln -s "/opt/xojo/Extras/Plugin SDK/Plug-In SDK License.txt" "$pkgdir/usr/share/licenses/xojo/Plug-In SDK License.txt"
	ln -s "/opt/xojo/Extras/Plugin SDK/Plugin Converter/License.txt" "$pkgdir/usr/share/licenses/xojo/Plugin Converter License.txt"
	ln -s "/opt/xojo/Read Mes/Third Party Licenses and Notices.txt" "$pkgdir/usr/share/licenses/xojo/Third Party Licenses and Notices.txt"
}
