# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-libxslt
pkgver=1.1.28
pkgrel=4
pkgdesc="XML stylesheet transformation library (mingw-w64)"
arch=(any)
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=('mingw-w64-crt' 'mingw-w64-libxml2' 'mingw-w64-libgcrypt')
makedepends=('mingw-w64-gcc')
options=('!strip' 'staticlibs' '!buildflags')
source=("http://xmlsoft.org/sources/libxslt-${pkgver}.tar.gz"
	'mingw32-libxslt-use-correct-mkdir.patch')
md5sums=('9667bf6f9310b957254fdcf6596600b7'
         'd9a2f525f8f703d8e2000000dcaa943e')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/libxslt-${pkgver}"
  patch -p0 -i "${srcdir}/mingw32-libxslt-use-correct-mkdir.patch"
}

build()
{
  cd "$srcdir/libxslt-${pkgver}"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --enable-shared \
      --with-libxml-prefix=/usr/${_arch} \
      --without-python
    make
    popd
  done
}

package()
{
  for _arch in ${_architectures}; do
    cd "$srcdir/libxslt-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm -r "$pkgdir"/usr/${_arch}/share
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-all "$pkgdir"/usr/${_arch}/bin/*.exe
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

