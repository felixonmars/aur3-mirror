# Maintainer: Yichao Yu <yyc1992 at gmail dot com>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=hotot-qt5
pkgname=hotot-qt5-git
pkgver=20130124
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
url="http://www.hotot.org/"
license=('LGPL3')
arch=('i686' 'x86_64')
depends=('hotot-data-git' 'qt5')
provides=("${pkgbase}")
conflicts=("${pkgbase}")
makedepends=('git' 'cmake' 'intltool')

_gitname="hotot"
_gitroot="https://github.com/lyricat/Hotot.git"
_gitbranch=master

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch"
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch" --
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  msg "Creating make environment..."
  git clean -fdx
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DWITH_GTK=Off -DWITH_CHROME=Off -DWITH_QT=Off -DWITH_QT5=On
  make
  make install DESTDIR="${PWD}/pkg"
}

package() {
  cd "$srcdir/$_gitname/build/pkg"

  install -Dm755 usr/bin/hotot-qt5 "$pkgdir/usr/bin/hotot-qt5"
  install -Dm644 usr/share/applications/hotot-qt5.desktop \
    "$pkgdir/usr/share/applications/hotot-qt5.desktop"
}
