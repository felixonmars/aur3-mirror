# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=gcc-python-plugin
pkgver=0.10
_gccver=4.7.1
pkgrel=2
pkgdesc="Allows the invoking of arbitrary Python scripts from inside the compiler"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/gcc-python-plugin/"
license=('GPL3')
depends=("gcc=${_gccver}" 'python-six')
makedepends=('python2' 'python-sphinx' 'docutils')
optdepends=('python2-six: for the Python-2 plugin')
source=(https://fedorahosted.org/releases/g/c/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('d61ee8594d1c3151419cbf82f5e0ac82')

_plugindir=/usr/lib/gcc/${CHOST}/${_gccver}/plugin

build() {
  cd $srcdir/$pkgname-$pkgver

  make clean
  # build python3 plugin
  make PLUGIN_PYTHONPATH=${_plugindir}/${pkgname} plugin
  mv python.so python3.so
  rm -f *.o

  # build python2 plugin
  make PYTHON=python2 PYTHON_CONFIG=python2-config PLUGIN_PYTHONPATH=${_plugindir}/${pkgname} plugin
  mv python.so python2.so

  # adjust gcc-with-python script to use global plugin
  sed -i 's#$(pwd)/python.so#python#' gcc-with-python

  # not working...
  #make -C docs man
}

# TODO - run test-suite

package() {
  cd $srcdir/$pkgname-$pkgver

  install -dm755 $pkgdir/${_plugindir}/$pkgname $pkgdir/usr/bin

  install -m755 python{2,3}.so $pkgdir/${_plugindir}
  ln -s python3.so $pkgdir/${_plugindir}/python.so

  install -m644 gccutils.py $pkgdir/${_plugindir}/${pkgname}

  find libcpychecker -type d \
    -exec install -dm755 $pkgdir/${_plugindir}/$pkgname/{} \;
  find libcpychecker -type f \
    -exec install -m644 {} $pkgdir/${_plugindir}/$pkgname/{} \;

  install -m755 gcc-with-python $pkgdir/usr/bin/
  # install -dm755 $pkgdir/usr/share/man/man1
  # install -m644 docs/_build/man/gcc-python-plugin.1 $pkgdir/usr/share/man/man1/
}
