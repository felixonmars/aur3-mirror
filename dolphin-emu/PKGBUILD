# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Jeremy Newton (Mystro256)
# Special thanks to the contributors of dolphin-emu-svn: for the icon, desktop file and some lines from the PKGBUILD
# Thanks to all that contributed! :)

pkgname=dolphin-emu
pkgver=3.0
pkgrel=9
epoch=1
pkgdesc="A GameCube and Wii emulator (stable git branch)"
arch=('i686' 'x86_64')
url="http://www.dolphin-emulator.com/"
license=('GPL2')

makedepends=('git' 'cmake' 'glproto' 'opencl-headers')
depends=('sdl' 'libxxf86vm' 'libao' 'wxgtk' 'wiiuse' 'nvidia-cg-toolkit' \
         'glew' 'mesa' 'libgl' 'openal' 'portaudio' 'lzo2')

# Cannot be BUILT with sfml later than 1.6
# IF YOU NEED SFML>=1.7 FOR SOMETHING ELSE, TRY REMOVING THE CONFLICT
# BELOW AND ONLY TEMPORARY UNINSTALLING SFML TO BUILD/INSTALL DOLPHIN
conflicts=('dolphin-emu-git' 'sfml>=1.7' 'sfml-git')

source=('dolphin-emu.desktop'
        'unistd.diff'
        'Dolphin_Logo.png')

_gitroot=https://code.google.com/p/dolphin-emu/
_gitname=dolphin-emu-3.0

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin 3.0
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg "Checking out at stable 3.0 branch"
  git checkout 3.0

  msg "GIT checkout done or server timeout"

  patch -Np2 < ${srcdir}/unistd.diff

  mkdir build && cd build
  export CXXFLAGS="$CXXFLAGS -fpermissive"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/dolphin-emu-3.0-build/build"

  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/dolphin-emu.desktop" "${pkgdir}/usr/share/applications/dolphin-emu.desktop"
  install -Dm644 "${srcdir}/Dolphin_Logo.png" "${pkgdir}/usr/share/pixmaps/dolphin-emu.png"
}

# vim:set ts=2 sw=2 et:

md5sums=('feed4580c2e6bfbc7f6c67dad861daae'
         'a463b96d02462b5f6b0894f6ced821ab'
         'd15c51f547b4bd47e510faac40bcc9d6')
