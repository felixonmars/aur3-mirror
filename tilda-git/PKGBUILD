# Maintainer: DerFlob <derflob at derflob dot de>

pkgname=tilda-git
pkgver=20121123
pkgrel=1
pkgdesc="Linux terminal based on classic terminals from first person shooter games"
arch=('i686' 'x86_64')
url="https://github.com/tsloughter/tilda"
license=('GPL')
depends=('vte' 'confuse' 'libglade')
makedepends=('git' 'gawk')
provides=('tilda')
conflicts=('tilda')

source=('tilda-fix.patch')
md5sums=('ae9d20d9ab87848b2eca860c9364349a')

_gitroot='git://github.com/tsloughter/tilda.git'
_gitname='tilda'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

# gdk && desktop file fix FS#25762
  patch -Np0 -i "${srcdir}/tilda-fix.patch"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
