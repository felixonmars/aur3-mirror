# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=brother-hl2270dw
_cupswrappername=brhl2270dwcups_src
_lprname=hl2270dwlpr
pkgver=2.0.4.2
_cupswrapperver=2.0.4-2
_lprver=2.1.0-1
pkgrel=2
pkgdesc='Brother HL-2270DW CUPS driver'
arch=('i686' 'x86_64')
url='http://welcome.solutions.brother.com/bsc/public_s/id/linux/en/index.html'
license=('GPL')
depends=('a2ps' 'cups')
source=("http://www.brother.com/pub/bsc/linux/dlf/${_cupswrappername}-${_cupswrapperver}.tar.gz"
        "http://pub.brother.com/pub/com/bsc/linux/dlf/${_lprname}-${_lprver}.i386.rpm"
        "brcupsconfig.c.patch"
        "brHL2270DWrc.patch"
        "cupswrapperHL2270DW-2.0.4.patch"
        "filterHL2270DW.patch"
        "psconvert2.patch"
        "setupPrintcap2.patch")
sha512sums=('fb97551d4ee5d8200a208f76608afbd91357d0f65d8ced7d74be774b95286cac5b884354174a5e7d2a2ba85c00a398ba3cc1efd305e4349ec32a483064979a2b'
            '5ae554f5e96367b7d4ba1e3d63e37f13a3e600487b09671e733b54548ae44a03cb81f8f7e6e047631a0f6c6dcbb7c4b4a17c63178a597148d41e33eeb4647835'
            'a5203143a88098ea923e89395893f1f3d392888a71e1e0db1f8b055a064f02d61dbfa376be500fba1d96036d1c669322d0a3ed1df69d0422049695fd35257e17'
            'ac945d5d55c3f67f5ec76263ffc9fee3d37106ae2c5e9270639a638b182e3f273ccbf1b51573230da023bbfb4102cbf294ee9aeaedd7fac25461725c021aa6cb'
            'b563a4f7a6ba19ba0ac49b370f18c7156877ec1aeba3ac7d2349d3ae87531bd485f7db9f637f9510771e5f4e6ecedd7f27a4184ac9da9787dedf8c583e1bb17b'
            '57fa86c70a5c7f52bb33fdfcfdda2cc9c47b6767e57ee600f7819958879fb5f86d4566e235fcf296f81cc8ee1f14667e96661042b10547743c9d83620b28a428'
            '0373248d86de7332ec3e11b00a9518f8a0fcf2f2494990b1291048764817252d83398772b11dcf09f348136fdcf3f6e56f6f0806278fd96e66d1b3038db22793'
            'f2fc60c072f458250fd954b59fa9210886cb6adff9a40cc03cf2d7101d4505566193d63a45c4596ef826b8b233935f803bbf2d2a6e59f7877da4817e8c9c67c2')

if [[ -z "$CARCH" ]]; then
  echo ":: PKGBUILD could not detect your architecture. Some dependencies may be missing."
elif [[ "$CARCH" == "x86_64" ]]; then
  depends=('a2ps' 'cups' 'lib32-glibc')
fi

package() {
  cd "$srcdir"
  bsdtar -xf "${_lprname}-${_lprver}.i386.rpm"

  _lprpath="$srcdir/usr/local/Brother/Printer/HL2270DW/"
  pushd "$_lprpath/inf/"
  patch < "$startdir/brHL2270DWrc.patch"
  patch < "$startdir/setupPrintcap2.patch"
  popd

  pushd "$_lprpath/lpd/"
  patch < "$startdir/filterHL2270DW.patch"
  patch < "$startdir/psconvert2.patch"
  popd

  # Install the Brother LPD filter and associated files
  mkdir -p "$pkgdir/usr/share/Brother"
  cp -R "$srcdir/usr/local/Brother" "$pkgdir/usr/share"
  cp -R "$srcdir/var" "$pkgdir/var"
  # These chmod commands are in the cupswrapper installation script. I do not
  # know what their purpose is.
  chmod a+w "$pkgdir/usr/share/Brother/Printer/HL2270DW/inf/brHL2270DWrc"
  chmod a+w "$pkgdir/usr/share/Brother/Printer/HL2270DW/inf"

  # Install the Brother CUPS LPD wrapper filter and the PPD file
  pushd "$srcdir/$_cupswrappername-$_cupswrapperver"
  pushd "$srcdir/$_cupswrappername-$_cupswrapperver/brcupsconfig3/"
  patch < "$startdir/brcupsconfig.c.patch"
  popd
  # These next two lines extract the PPD file and the CUPS LPD wrapper filter
  patch < "$startdir/cupswrapperHL2270DW-2.0.4.patch"
  . cupswrapperHL2270DW-2.0.4
  install -Dm 755 "brlpdwrapperHL2270DW" "$pkgdir/usr/lib/cups/filter/brlpdwrapperHL2270DW"
  install -Dm 644 "HL2270DW.ppd" "$pkgdir/usr/share/cups/model/HL2270DW.ppd"
  popd
}

# vim:set ts=2 sw=2 et:
