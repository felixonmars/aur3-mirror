# Maintainer:  William Giokas <1007380@gmail.com>
# Contributor: Christoph Vigano <mail at cvigano dot de>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Michael Rosset <mrosset@uarch.org>
# Based on [extra]'s git

pkgname=git-git
_pkgname=git
epoch=1
pkgver=1.8.2.1.r478.g1468a58
pkgrel=1
pkgdesc="The fast distributed version control system - Git version"
arch=('i686' 'x86_64')
url="http://git-scm.com/"
license=('GPL2')
depends=('curl' 'expat>=2.0' 'perl-error' 'perl>=5.14.0' 'openssl' 'pcre')
makedepends=('git' 'asciidoc' 'xmlto' 'docbook-xsl' 'python2')
optdepends=('tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-term-readkey: git svn'
            'perl-mime-tools: git send-email'
            'perl-net-smtp-ssl: git send-email TLS support'
            'perl-authen-sasl: git send-email TLS support'
            'python2: various helper scripts'
            'subversion: git svn'
            'cvsps: git cvsimport')
provides=('git')
conflicts=('git')
install=git.install
backup=('etc/conf.d/git-daemon.conf')
source=('git-daemon.conf'
        'git-daemon.service'
        'git-daemon.socket'
        'git://git.kernel.org/pub/scm/git/git.git#branch=master')

md5sums=('2e42bf97779a1c6411d89043334c9e78'
         '579f698a6d49d72ce9894b8d36369261'
         'f67869315c2cc112e076f0c73f248002'
         'SKIP')


pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always --long | sed -E 's/v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/"$_pkgname
	
  export PYTHON_PATH='/usr/bin/python2'
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 all man
}
    
package() {
  export PYTHON_PATH='/usr/bin/python2'

  cd "$srcdir/"$_pkgname
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    USE_LIBPCRE=1 \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    INSTALLDIRS=vendor DESTDIR="$pkgdir" install install-man

  # bash completion
  mkdir -p "$pkgdir"/etc/bash_completion.d/
  install -m644 ./contrib/completion/git-completion.bash "$pkgdir"/etc/bash_completion.d/git 
  # more contrib stuff
  cp -a ./contrib "$pkgdir"/usr/share/git/ 
  # scripts are for python 2.x
  sed -i 's|#![ ]*/usr/bin/env python|#!/usr/bin/env python2|' \
    $(find "$pkgdir" -name '*.py') \
    "$pkgdir"/usr/share/git/gitview/gitview

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf "$pkgdir"/usr/lib/perl5

  # git daemon
  install -D -m644 "$srcdir"/git-daemon.conf "$pkgdir"/etc/conf.d/git-daemon.conf
  install -D -m644 "$srcdir"/git-daemon.service "$pkgdir"/usr/lib/systemd/system/git-daemon.service
  install -D -m644 "$srcdir"/git-daemon.socket "$pkgdir"/usr/lib/systemd/system/git-daemon.socket
}
