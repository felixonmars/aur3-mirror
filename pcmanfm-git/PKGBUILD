# Maintainer: TDY <tdy@gmx.com>

pkgname=pcmanfm-git
pkgver=20130204
pkgrel=1
pkgdesc="An extremely fast, lightweight, yet feature-rich file manager with tabbed browsing"
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')
depends=('libfm-git')
makedepends=('git' 'intltool' 'pkg-config')
optdepends=('gvfs: mounting of local and remote drives'
            'gnome-menus: applications menu and "Open with..." dialog')
provides=('pcmanfm')
conflicts=('pcmanfm')
source=(automake-version.patch obsolete-macro.patch)
install=pcmanfm.install
md5sums=('484c8f3600ac91c9200897db6a392573'
         '878b557f247da501bf6c94f40f0dae25')

_gitroot=git://pcmanfm.git.sourceforge.net/gitroot/pcmanfm/pcmanfm
_gitname=pcmanfm

build() {
  cd "$srcdir"

  msg "Connecting to $_gitroot..."
  if [[ -d $_gitname ]]; then
    cd "${srcdir}/${_gitname}" && git pull origin
    msg "Local files updated"
  else
    git clone $_gitroot 
    msg "Git checkout done"
  fi
  
  [[ -d "${srcdir}/${_gitname}-build" ]] && rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  
  cd "${srcdir}/${_gitname}-build"

  msg "Applying patches..."
  git apply ../automake-version.patch
  git apply ../obsolete-macro.patch
    
  msg "Starting make..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make LDFLAGS="-lm ${LDFLAGS}"
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
}
