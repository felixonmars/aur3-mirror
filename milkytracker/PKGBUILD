# Contributor: Christopher O'Neill <milkytracker at chrisoneill co uk>
pkgname=milkytracker
pkgver=0.90.85
pkgrel=1
pkgdesc="FastTracker 2 inspired music tracker"
arch=('i686' 'x86_64')
url="http://www.milkytracker.org/"
license=('GPL3')
depends=('sdl>=1.2.0' 'alsa-lib' 'zlib')
optdepends=('jack-audio-connection-kit: JACK audio support')
options=(docs)
source=("http://milkytracker.org/files/$pkgname-$pkgver.tar.bz2" "milkytracker.desktop" "64bit_freebsd_fix.patch" "doc_fix.patch" "jack_autoconnect.patch" "no_zziplib_dep.patch")
md5sums=('2e6795b0bff18b087bd7e4c658f512f3'
         'f57943d52fb65d4dbbc40791a6057bd1'
         'dcdb4301bab58f43fe427a4195d467af'
         '5df782036a8215c0c8719319f0d0ca93'
         '80a145913441b077baf5033409269f24'
         '68a5c28f2940cc769050cc4c2bc4085e')
build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i ../64bit_freebsd_fix.patch 
  patch -p1 -i ../doc_fix.patch 
  patch -p1 -i ../jack_autoconnect.patch 
  patch -p1 -i ../no_zziplib_dep.patch

  ./configure --prefix=/usr --with-alsa
  make || return 1
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/pixmaps"
  cp resources/pictures/carton.png "$pkgdir/usr/share/pixmaps/milkytracker.png"
  mkdir "$pkgdir/usr/share/applications"
  cp "$srcdir/milkytracker.desktop" "$pkgdir/usr/share/applications"
  mkdir -p "$pkgdir/usr/share/doc/milkytracker"
  cp docs/ChangeLog.html docs/MilkyTracker.html docs/readme_unix docs/FAQ.html\
    "$pkgdir/usr/share/doc/milkytracker"
}

# vim:set ts=2 sw=2 et:
