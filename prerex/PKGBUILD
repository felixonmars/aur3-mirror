# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=prerex  
pkgver=6.4.0
pkgrel=2
pkgdesc="a LaTeX document style for prerequisite charts"
url="http://www.prerex.com"
arch=('i686' 'x86_64')
license=('GPL')
depends=('texlive-pstricks' 'poppler-qt')

install=prerex.install
source=($pkgname-$pkgver.zip::ftp://cam.ctan.org/tex-archive/graphics/$pkgname.zip)
md5sums=('f93ed0195627c68998422911c0563da5')

build() {
  cd $srcdir/$pkgname
  bsdtar xzf $pkgname-$pkgver.tar.gz
  cd $pkgname-$pkgver
  ./configure --prefix=/usr 
  make 
  cd ..
  bsdtar xzf v$pkgname-$pkgver.tar.gz
  cd v$pkgname-$pkgver
  qmake vprerex.pro
  make
}
package () {
  install -Dm644 $srcdir/$pkgname/$pkgname.sty \
    $pkgdir/usr/share/texmf/tex/latex/$pkgname/$pkgname.sty 
  cd $srcdir/$pkgname/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -d $pkgdir/usr/share/texmf/doc/$pkgname
  cp $srcdir/$pkgname/doc/* $pkgdir/usr/share/texmf/doc/$pkgname
  install -Dm644 $pkgdir/usr/share/texmf/doc/$pkgname/$pkgname.sty.7 \
    $pkgdir/usr/share/man/man7/$pkgname.sty.7
  rm $pkgdir/usr/share/texmf/doc/$pkgname/$pkgname.sty.7
  install -Dm755 $srcdir/$pkgname/v$pkgname-$pkgver/v$pkgname \
    $pkgdir/usr/bin/v$pkgname
  install -Dm544 $srcdir/$pkgname/v$pkgname-$pkgver/v$pkgname.1 \
    $pkgdir/usr/share/man/man1/v$pkgname.1
  rm -r $pkgdir/usr/include
  rm -r $pkgdir/usr/lib
}
