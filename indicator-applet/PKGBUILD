# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-applet
pkgver=0.4.12
pkgrel=1
pkgdesc="A small applet to display information from various applications consistently in the panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-applet"
license=('GPL')
depends=('gnome-panel>=3.0' 'libindicator>=0.3.92' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35.0')
optdepends=('indicator-application: take menus from applications and place them in the panel'
            'indicator-appmenu: host the menus from an application'
            'indicator-datetime: a very, very simple clock'
            'indicator-messages: a place on the users desktop that collects messages that need a response'
            'indicator-power: show the power status of your devices'
            'indicator-session: change your status, switch users'
            'indicator-sound: a unified sound menu')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        gnome3.patch)
md5sums=('43fe3ee2ded69d74cd0ceefd3d4166f3'
         '204316bc32b24684e5e9f658340d16e8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/gnome3.patch"

  # Rename FastUserSwitchApplet to avoid conflict with GDM
  mv data/org.ayatana.panel.FastUserSwitchApplet.panel-applet.in.in data/org.ayatana.panel.IndicatorAppletSession.panel-applet.in.in
  mv data/org.gnome.panel.applet.FastUserSwitchAppletFactory.service.in data/org.gnome.panel.applet.IndicatorAppletSessionFactory.service.in
  find -type f | xargs sed -i 's/FastUserSwitchApplet/IndicatorAppletSession/'

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
