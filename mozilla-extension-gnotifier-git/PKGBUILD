# Maintainer: XZS <d.f.fischer at web dot de>
pkgname=mozilla-extension-gnotifier-git
pkgver=1.8.7
pkgrel=1
pkgdesc='Add-on for Firefox and Thunderbird to enable integration with GNOME native notification system.'
url='https://github.com/mkiol/GNotifier'
arch=('any')
license=('GPL3')
depends=('libnotify')
optdepends=(
  'firefox: for firefox plugin'
  'thunderbird: for thunderbird plugin'
)
provides=('firefox-extension-gnotifier' 'thunderbird-extension-gnotifier')
conflicts=('firefox-extension-gnotifier' 'thunderbird-extension-gnotifier')
options=('!emptydirs')
source=("$pkgname::git+https://github.com/mkiol/GNotifier.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  grep -Pom1 '(?<=<em:version>)[[:digit:]\.]*(?=</em:version>)' install.rdf
}

package() {
  cd $pkgname
  local emid=$(grep -Pom1 '(?<=<em:id>).*(?=</em:id>)' install.rdf)
  local fxpath="/usr/lib/firefox/browser/extensions/$emid"
	local fxdir="$pkgdir$fxpath"
  local tbdir="$pkgdir/usr/lib/thunderbird/extensions"
	install -d "$fxdir" "$tbdir"
  cp -r install.rdf lib locale resources "$fxdir"
  ln -s "$fxpath" "$tbdir/$emid"
}
