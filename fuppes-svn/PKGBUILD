# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=fuppes-svn
pkgver=692
pkgrel=2
pkgdesc="A free, multiplatform Universal Plug and Play (UPnP) A/V MediaServer"
arch=('i686' 'x86_64')
url="http://fuppes.ulrich-voelkel.de/"
license=('GPL2')
backup=('var/lib/fuppes/fuppes.cfg')
depends=('libxml2' 'pcre' 'sqlite')
optdepends=('libvorbis: OGG Vorbis decoding' \
            'libmpcdec: MPC decoding' 'flac: FLAC decoding' \
            'imagemagick: image transcoding' \
            'taglib: audio metadata' 'libmp4v2: audio metadata' \
            'exiv2: EXIF support' \
            'ffmpeg: video metadata' \
            'mysql: mysql database' \
            'curl: blip.tv')
# minimal makedepends
#makedepends=('subversion')
# depends for all possible plugins (only the correctly detected ones are listed)
makedepends=('subversion' \
             'libvorbis' 'libmpcdec' 'flac' 'faad2' 'libmad' \
             'imagemagick' \
             'taglib' 'libmp4v2' \
             'exiv2' \
             'ffmpeg' \
             'mysql')
source=(fix_regexp_crash.diff fix_includes.diff ffmpeg_0.11.diff)
md5sums=('67a7059bcf61d01bf8ac8f3b4429bd53'
         '050071ed96aa1f25802f4ed88334e765'
         'bef3522097ec99b30609770ad3d509d5')

_svnmod="fuppes"
_svntrunk="https://fuppes.svn.sourceforge.net/svnroot/fuppes/trunk"

build()
{
  cd "$srcdir"
  msg "Connecting to SVN server...."
  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  patch -Np0 < "$srcdir/fix_regexp_crash.diff" || true
  patch -Np0 < "$srcdir/fix_includes.diff" || true
  patch -Np0 < "$srcdir/ffmpeg_0.11.diff" || true

  autoreconf -vfi
  ./configure --sysconfdir=/etc --localstatedir=/var --prefix=/usr --disable-rpath
  sed -i 's|$(basedir)/fuppes.cfg|$(DESTDIR)/&|g' config/Makefile
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install
}

