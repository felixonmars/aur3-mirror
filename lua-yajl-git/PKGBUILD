# Maintainer: Justin Davis <jrcd83@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lua-yajl-git
pkgver=20110429
pkgrel=1
pkgdesc="Integrates the yajl JSON library with Lua."
arch=(i686 x86_64)
url='http://github.com/brimworks/lua-yajl'
license=(MIT)
depends=('yajl>=2' 'lua>=5.1')
makedepends=(git cmake)
options=(!libtool)
source=('LICENSE')
md5sums=('27c5699b8602d4d18e139339b96ae5dc')

_gitroot="git://github.com/brimworks/lua-yajl.git"
_gitbranch="master"
_repodir="${srcdir}/lua-yajl"

build() {
  msg "Building ${pkgname}..."

  if [ -d "$_repodir" ] ; then
    warning 'Repository directory already exists!'
    cd "$_repodir"
    git checkout master
    msg2 "Pulling..."
    git pull "$_gitroot" master
  else
    msg2 "Cloning $_gitroot repository..."
    git clone "$_gitroot" "$_repodir"
    cd "$_repodir"
    git checkout "$_gitbranch"
  fi

  cmake .
  make
}

package(){
  msg "Packaging ${pkgname}..."
  cd "$_repodir"
  make DESTDIR="$pkgdir" install
  install -Dm644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mkdir -p "${pkgdir}/usr/lib/lua/5.1"
  mv -t "${pkgdir}/usr/lib/lua/5.1/" "${pkgdir}/usr/local/share/lua/cmod/"* 
  rm -R "${pkgdir}/usr/local"
} 

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
