# Maintainer: Ilmari Repo <ilmari at gmail dot com>
# Contributor: GazJ Gary James <garyjames82 at gmail dot com> (CADuntu PKGBUILD)

pkgname=librecad-svn
pkgver=112
pkgrel=1
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/librecad/"
license=('GPL')
depends=('qt')
makedepends=('subversion')
provides=('librecad')
replaces=('caduntu' 'caduntu-svn')
source=('librecad.desktop')
md5sums=('19dcb83a3fcdb3752439095b118ac6d3')

_svntrunk='https://librecad.svn.sourceforge.net/svnroot/librecad'
_svnmod='librecad'

build() {
	cd $srcdir
	msg "Connecting to the SVN server..."
	if [ -d "$_svnmod/.svn" ]; then
		svn up -r $pkgver $_svnmod
	else
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
	fi
	msg "SVN checkout done or server timeout"
	cd $srcdir/$_svnmod/trunk
	qmake || return 1
	make || return 1
}

package() {
  cd $srcdir/$_svnmod/trunk
  install -D -m 755 unix/librecad $pkgdir/usr/bin/librecad
  install -D -m 644 $srcdir/librecad.desktop $pkgdir/usr/share/applications/librecad.desktop
  install -D -m 644 res/main/librecad.png $pkgdir/usr/share/pixmaps/librecad.png
  mkdir -p $pkgdir/usr/share/librecad/
  cp -r unix/resources/{library,patterns,fonts,qm} $pkgdir/usr/share/librecad/
}