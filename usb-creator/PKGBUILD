# Maintainer : Jekyll Wu  < adaptee at gmail dot com>
# Contributor: Jacco Koning <archlinux@menollo.nl>, with help from qwak

pkgname=usb-creator
pkgver=0.2.40
pkgrel=1
pkgdesc="Create bootable USB from a LiveCD or disc image of Ubuntu. "
arch=('i686' 'x86_64')
url="https://launchpad.net/usb-creator"
license=('GPL3')
depends=(
            'udisks'
            'syslinux'
            'dosfstools'
            'mtools'
            'cdrkit'
            'python2-dbus'
        )
optdepends=(
            'kdebindings-python2: for KDE frontend'
            'pygtk: for GTK frontend'
           )
makedepends=('python2-distutils-extra')
install=usb-creator.install
source=(    "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.tar.gz"
            "0001-change-the-path-of-parted.patch"
            "0002-remove-blackcode-using-lsb-release-and-python-debian.patch"
        )

build() {
    local extractdir="${srcdir}/${pkgname}-${pkgver}"

    cd "${extractdir}"

    # command `parted` is located at /usr/sbin/parted in archlinux
    patch -Np1 -i "${srcdir}/0001-change-the-path-of-parted.patch"

    # archlinux can't provide thoso equivalents; further more, these code
    # are maily for dealing version differences between current ubuntu and
    # the ubuntu iso. So just remove them, a dirty but simple solution.
    patch -Np1 -i "${srcdir}/0002-remove-blackcode-using-lsb-release-and-python-debian.patch"

    # force using python2 as interpreter
    find . -type f | \
        xargs grep -l '^#!/usr/bin/python$' | \
                xargs sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_'

    python2 setup.py install --root="${pkgdir}"

    # FIXME:
    # That setup.py is written with the assumption that /usr/share/kde4/apps is
    # part of $(kde-prefix --path data), which is true for # ubuntu.
    #
    # But this assumption does not hold in archlinux, which will make
    # usb-creator-kde fail to load its ui file.
    #
    # So we must put usbcreator-kde.ui to an appropriate place!
    install --mode=0644 -D "${extractdir}/gui/usbcreator-kde.ui" \
                            "${pkgdir}/usr/share/apps/usb-creator-kde/usbcreator-kde.ui"

    # FIXME: install manpages, manually...
    local MANDIR="${pkgdir}/usr/share/man/man8"
    for manpage in ${pkgname}-{gtk,kde}.8 ; do
        install --mode=0644 -D "${extractdir}/man/${manpage}" "${MANDIR}/${manpage}"
    done

}

md5sums=('63789c53272de4cd86f4be6479d49985'
         'bc80c990f59f7cab2db8cfa76746be61'
         '585ff16f1dd29ada15007c33452deaba')
