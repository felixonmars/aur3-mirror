pkgname=libx86emu-src
pkgver=1.1.21.7
pkgrel=1
pkgdesc='x86 emulation library'
arch=('x86_64' 'i686' 'armv7h')
url='http://download.opensuse.org/source/factory/repo/oss/suse/src/'
license=('BSD')
depends=('glibc')
makedepends=('rpmextract' 'make' 'gcc')
conflicts=('libx86emu')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/libx86emu-${pkgver%.*.*}-${pkgver#*.*.}.src.rpm")
sha256sums=('9e3d83fca96494fc02643c785de1ffb5acf4acc6dd6c9ac49aa0c6548e032f6f')

prepare() {
  rpm2cpio "libx86emu-${pkgver%.*.*}-${pkgver#*.*.}.src.rpm" | bsdtar -xf -
  tar jxf "libx86emu-${pkgver%.*.*}.tar.bz2"
}

build() {
  make -C "libx86emu-${pkgver%.*.*}" LIBDIR=/usr/lib
}

package() {
  make -C "libx86emu-${pkgver%.*.*}" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  install -Dm644 "libx86emu-${pkgver%.*.*}/LICENSE" \
    "$pkgdir/usr/share/licenses/libx86emu/LICENSE"
}
