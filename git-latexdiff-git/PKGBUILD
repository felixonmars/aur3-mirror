# Maintainer: Andre <andre-arch@delorus.de> 

pkgname=git-latexdiff-git
pkgver=r60.49bbd42
pkgrel=1
pkgdesc="git-latexdiff is a wrapper around latexdiff (http://www.ctan.org/pkg/latexdiff) that allows using it to diff two revisions of a LaTeX file."
arch=('i686' 'x86_64')
url="https://gitorious.org/git-latexdiff/git-latexdiff"
license=('custom')
depends=('texlive-core' 'git')
source=("$pkgname"::'git://gitorious.org/git-latexdiff/git-latexdiff')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  sed -n '/# Copying and distribution/,/ any warranty./p' git-latexdiff > LICENSE
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm644 "$srcdir/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 "$srcdir/$pkgname/git-latexdiff" "$pkgdir/usr/lib/git-core/git-latexdiff"
}
