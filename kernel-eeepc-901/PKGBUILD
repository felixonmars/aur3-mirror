# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Smith Dhumbumroong <zodmaner@gmail.com>

pkgname=kernel-eeepc-901
pkgver=2.6.39.3
_kernver=2.6.39
pkgrel=1
pkgdesc="The Linux Kernel and modules for the Asus Eee PC 901"
arch=('i686')
license=('GPL2')
url="http://bitbucket.org/zodmaner/eeepc-901/wiki/Home"
groups=(eeepc-901)
depends=('coreutils' 'module-init-tools' 'linux-firmware')
optdepends=('crda: for wireless regulatory domain support'
            'iw: for wireless configuration support')
replaces=('kernel-eeepc-901-kms')
install=kernel-eeepc-901.install
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$_kernver.tar.bz2
        ftp://ftp.kernel.org/pub/linux/kernel/v2.6/patch-$pkgver.bz2
        kernelconfig
        61-eee-ssd.rules
        CREDITS)

build() {
  # unset since our build machine may differ from eee
  unset CFLAGS CXXFLAGS

  # get into the linux source directory and start some magic
  cd $srcdir/linux-$_kernver

  if [ "$_kernver" != "$pkgver" ]; then
    # add latest kernel stable patch
    patch -Np1 < ../patch-$pkgver || return 1
  fi

  # load configuration and build kernel + modules
  cp ../kernelconfig ./.config
  make silentoldconfig
  #make menuconfig
  make || return 1
}

package() {
  # install our modules
  cd $srcdir/linux-$_kernver
  mkdir -p $pkgdir/{lib/modules,boot}
  make INSTALL_MOD_PATH=$pkgdir modules_install || return 1

  # remove the junk symlinks
  cd $srcdir/linux-$_kernver
  rm $pkgdir/lib/modules/${pkgver}-eeepc-901/{build,source}
  # install the kernel
  cp System.map $pkgdir/boot/System.map.eeepc-901
  cp arch/x86/boot/bzImage $pkgdir/boot/vmlinuz-eeepc-901
  # install a helper file for all install scripts
  mkdir -p $pkgdir/usr/share/kernel-eeepc-901/
  echo "KERNEL_VERSION='${pkgver}-eeepc-901'" > $pkgdir/usr/share/kernel-eeepc-901/currver

  # udev rules for SSD drives
  mkdir -p $pkgdir/lib/udev/rules.d/
  install -m644 $srcdir/61-eee-ssd.rules $pkgdir/lib/udev/rules.d/

  # install credits file
  install -m644 $srcdir/CREDITS $pkgdir/usr/share/kernel-eeepc-901/CREDITS
}

md5sums=('1aab7a741abe08d42e8eccf20de61e05'
         '06b858e8f81600038129afe7bcd4e162'
         'a30908d6e6e46bf6103bfc57ce0ce98e'
         '73a20e8bf2bb29ba342f43460c6291cb'
         'bccc9e60b27d739d0d4383dc9c08ed4a')
