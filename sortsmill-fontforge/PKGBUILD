# Maintainer: Bastien Dejean <baskerville at lavabit.com>

pkgname=sortsmill-fontforge
pkgver=2.0.0_alpha1
ftyver=2.4.11
pkgrel=1
pkgdesc='An outline and bitmap font editor'
arch=('i686' 'x86_64')
url='https://bitbucket.org/sortsmill/sortsmill-fontforge'
license=('GPLv3')
groups=('xorg')
depends=('glib' 'pcre' 'gc' 'gsl' 'gmp' 'libunicodenames' 'libatomic_ops' 'guile-devel' 'python2-cython-git' 'python2-gmpy')
optdepends=('pango' 'cairo' 'libunistring' 'libpng' 'libtiff' 'giflib' 'libjpeg' 'libxml2' 'libxcursor' 'libspiro' 'libiconv' 'freetype2')
conflicts=('fontforge')
provides=('fontforge')
source=('https://bitbucket.org/sortsmill/sortsmill-fontforge/downloads/sortsmill-fontforge-commit-13d73a4-snapshot.tar.xz'
        "http://downloads.sourceforge.net/project/freetype/freetype2/${ftyver}/freetype-${ftyver}.tar.bz2")
md5sums=('2154091f73398ae7175bebcd690dc3b8'
         'b93435488942486c8d0ca22e8f768034')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-freetype-source=../freetype-$ftyver PYTHON=python2 
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
