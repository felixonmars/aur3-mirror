# Maintainer: N30N <archlinux@alunamation.com>

pkgname="appleseed-git"
provides=("appleseed")
pkgver=20120507
pkgrel=1
pkgdesc="An open source renderer with a focus on quality and correctness."
url="http://appleseedhq.net/"
license=("MIT")
arch=("i686" "x86_64")
depends=("boost-libs" "qt")
makedepends=("git" "cmake" "boost" "libx11") # "openexr" "hdf5" "alembic" "xerces-c"
source=("appleseed.desktop" "appleseed.install")
md5sums=("f34363dc929737a0c25e8c68b1faab04" "22c1b8bea3b61b4697031b7aaf7c46db")
install="appleseed.install"

_gitroot="https://github.com/jupiter-jazz/appleseed.git"
_gitname="appleseed"

build() {
	if [ -d ${_gitname} ]; then
		rm -rf build
		cd "${_gitname}"
		git clean -dfx -e bf-translations
		git reset --hard
		git pull origin
	else
		git clone "${_gitroot}"
		cd "${_gitname}"
	fi


	cd src/openexr
	./configure --with-pic --prefix="$(pwd)"
	make
	make install
	mkdir -p ../../build/linux-gcc4/openexr/
	cp lib/*.a ../../build/linux-gcc4/openexr/
	make clean
	cd ../..

	cd src/hdf5
	mkdir build; cd build
	cmake -DCMAKE_C_FLAGS="-fPIC" -DHDF5_BUILD_HL_LIB=1 \
		-DCMAKE_ARCHIVE_OUTPUT_DIRECTORY=../../../build/linux-gcc4/hdf5 ..
	make
	cd ../../..

	cd src/alembic
	mkdir build; cd build
	cmake -DCMAKE_CXX_FLAGS="-fPIC" ..
	make
	cd ../../..

	cd src/xerces-c
	./configure --disable-network --with-pic
	make
	mkdir -p ../../build/linux-gcc4/xerces-c/
	cp src/.libs/libxerces-c.a ../../build/linux-gcc4/xerces-c/
	make clean
	cd ../..


	sed -r "s/(Boost_USE_STATIC_LIBS )TRUE/\1FALSE/" -i src/CMakeLists.txt
	sed -r \
		-e "/-Werror/d" \
		-e "s/\\$.+libz.a$/$(pkg-config --libs zlib)/" \
		-e "s/\\$.+libpng.a$/$(pkg-config --libs libpng)/" \
		-i src/cmake/config/linux-gcc4.txt

	cd build
	cmake "../src"
	make
}

check() {
	"${_gitname}/sandbox/bin/Release/appleseed.cli" --run-unit-tests
}

package() {
	cd "${_gitname}"
	install -Dm644 LICENSE.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 resources/logo/appleseed-seeds-gray.svg \
		"${pkgdir}/usr/share/pixmaps/appleseed.svg"
	install -m644 resources/logo/appleseed-seeds-gray-256.png \
		"${pkgdir}/usr/share/pixmaps/appleseed.png"
	install -Dm644 "${srcdir}/appleseed.desktop" \
		"${pkgdir}/usr/share/applications/appleseed.desktop"

	install -d "${pkgdir}/opt/appleseed/images"
	ln -s /tmp "${pkgdir}/opt/appleseed/images/autosave"

	cd sandbox
	install -Dm644 extras/blender/blenderseed.py \
		"${pkgdir}/usr/share/blender/2.63/scripts/addons/blenderseed.py"

	find schemas settings stylesheets \
		-type f -not -name ".gitignore" -exec \
		install -Dm644 "{}" "${pkgdir}/opt/appleseed/{}" \;

	cd bin/Release
	find -not -name "*.so" -exec \
		install -Dm755 "{}" "${pkgdir}/opt/appleseed/bin/{}" \;
	install -d "${pkgdir}/usr/lib"
	install -m644 *.so "${pkgdir}/usr/lib"
}

# vim: set noet ff=unix:
