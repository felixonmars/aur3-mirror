#################################################
# Based on exim
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: judd <jvinet@zeroflux.org>
#################################################
# exim-mysql
# Maintainer: Ian K Armstrong <ian.k.armstrong@openmail.cc>
#
# dependencies added: libmysqlclient, libspf2, sqlite3
#
################################################# 
pkgname=exim-mysql
pkgver=4.82
pkgrel=1
pkgdesc="A Message Transfer Agent with MySQL and sqlite3 support"
arch=('i686' 'x86_64')
url="http://www.exim.org/"
license=('GPL')
backup=(etc/mail/aliases etc/mail/exim.conf etc/logrotate.d/exim etc/conf.d/exim)
install=exim.install
depends=('db' 'pcre' 'pam' 'tcp_wrappers' 'openssl' 'libmysqlclient' 'sqlite3')
makedepends=('sudo')
provides=('smtp-server')
conflicts=('smtp-server')
options=('!makeflags')
source=(http://exim.mirror.fr/exim/exim4/exim-${pkgver}.tar.gz
        aliases 
        newaliases 
        exim 
        exim.logrotate 
        exim.conf.d)
md5sums=('7f7b50394f8f3d4141e7dca9392f62f0'
         '4874006f0585253ddab027d441009757'
         'ea39f58bffc16f5e3bbe59dffcf09449'
         '9aed772e87223213e8da9ca5e7376869'
         'e18a535218718c5eb394ed5c9296fe06'
         'b75fe4c6e960a59a25b5f51e8f61ba3a')

build() {
  # An exim group is required to build this
  if [ ! `egrep '^exim' /etc/group` ]; then
    echo "==> Adding group exim"
    sudo groupadd -g 79 exim
  fi

  # An exim user is required to build this
  if [ ! `egrep '^exim' /etc/passwd` ]; then
    echo "==> Adding user exim"
    sudo useradd -u 79 -g exim -d /var/spool/exim -s /bin/false exim
  fi

  cd ${srcdir}/exim-${pkgver}
  echo "
BIN_DIRECTORY=/usr/bin
CONFIGURE_FILE=/etc/mail/exim.conf
EXIM_USER=exim
EXIM_GROUP=exim
SPOOL_DIRECTORY=/var/spool/exim
INCLUDE=-I/usr/include
ROUTER_ACCEPT=yes
ROUTER_DNSLOOKUP=yes
ROUTER_IPLITERAL=yes
ROUTER_MANUALROUTE=yes
ROUTER_QUERYPROGRAM=yes
ROUTER_REDIRECT=yes
TRANSPORT_APPENDFILE=yes
TRANSPORT_AUTOREPLY=yes
TRANSPORT_PIPE=yes
TRANSPORT_SMTP=yes
SUPPORT_MAILDIR=yes
LOOKUP_DBM=yes
LOOKUP_LSEARCH=yes
LOOKUP_MYSQL=yes
LOOKUP_SQLITE=yes
PCRE_LIBS=-lpcre
LOOKUP_INCLUDE=-I /usr/include/mysql
LOOKUP_LIBS=-L/usr/lib -lmysqlclient -lsqlite3
WITH_CONTENT_SCAN=yes
FIXED_NEVER_USERS=root
AUTH_CRAM_MD5=yes
AUTH_DOVECOT=yes
AUTH_PLAINTEXT=yes
AUTH_SPA=yes
HEADERS_CHARSET=\"UTF-8\"
SUPPORT_TLS=yes
TLS_LIBS=-L/usr/lib -lssl -lcrypto
TLS_INCLUDE=-I/usr/include/openssl
LOG_FILE_PATH=/var/log/exim/%slog
COMPRESS_COMMAND=/bin/gzip
COMPRESS_SUFFIX=gz
ZCAT_COMMAND=/bin/zcat
SUPPORT_PAM=yes
CFLAGS=-O -I/usr/include
EXTRALIBS_EXIM=-L/usr/lib -lwrap -lpam
SYSTEM_ALIASES_FILE=/etc/mail/aliases
CHOWN_COMMAND=/bin/chown
CHGRP_COMMAND=/bin/chgrp
CHMOD_COMMAND=/bin/chmod
MV_COMMAND=/bin/mv
RM_COMMAND=/bin/rm
TOUCH_COMMAND=/bin/touch
PERL_COMMAND=/usr/bin/perl
TMPDIR=\"/tmp\"
PID_FILE_PATH=/var/run/exim.pid
MAX_NAMED_LIST=16
SUPPORT_MOVE_FROZEN_MESSAGES=yes">"Local/Makefile"
make
}

package() {
  cd $srcdir/exim-$pkgver
  install -D -m644 ../exim.logrotate ${pkgdir}/etc/logrotate.d/exim
  install -D -m644 ../exim.conf.d ${pkgdir}/etc/conf.d/exim
  install -D -m644 doc/exim.8 ${pkgdir}/usr/share/man/man8/exim.8
  install -D -m755 ../exim ${pkgdir}/etc/rc.d/exim
  mkdir -p ${pkgdir}/var/spool/exim/db ${pkgdir}/etc/mail \
    ${pkgdir}/var/log/exim ${pkgdir}/usr/{lib,bin}
  chown root.exim ${pkgdir}/var/spool/exim ${pkgdir}/var/log/exim
  chown exim.exim ${pkgdir}/var/spool/exim/db
  chmod 770 ${pkgdir}/var/spool/exim ${pkgdir}/var/spool/exim/db ${pkgdir}/var/log/exim
  cd build-Linux-*
  inst_dest=${pkgdir}/usr/bin inst_conf=${pkgdir}/etc/mail/exim.conf ../scripts/exim_install
  cd ${srcdir}/exim-${pkgver}/src
  sed "s|/etc/aliases|/etc/mail/aliases|g" configure.default | \
    sed "s|SYSTEM_ALIASES_FILE|/etc/mail/aliases|g" \
    >${pkgdir}/etc/mail/exim.conf

  cp ${srcdir}/aliases ${pkgdir}/etc/mail
  cp ${srcdir}/newaliases ${pkgdir}/usr/bin
  cd ${pkgdir}/usr/bin
  ln -s exim mailq
  ln -s exim rmail
  ln -s exim rsmtp
  ln -s exim runq
  ln -s exim sendmail
  # fhs compliancy
  ln -s ../bin/exim ../lib/sendmail

  mkdir -p ${pkgdir}/etc/rc.d
  cp ${srcdir}/exim ${pkgdir}/etc/rc.d

  return 0
}
