# Maintainer: GordonGR <gordongr@freemail.gr>

pkgname=singularityviewer
pkgver=1.7.0.2621
pkgrel=1
pkgdesc="Singularity Viewer is an exciting client for Second Life (secondlife) and OpenSim (opensimulator), which combines the look and feel of Viewer 1.23 with the latest and greatest of available technology."
url="http://www.singularityviewer.org/"
license=('custom')
depends=('apr-util' 'gtk2' 'libpng' 'libgl' 'libjpeg6' 'libidn' 'sdl' 'mesa')
optdepends=('nvidia-utils: GL support for NVIDIA drivers' 'libpulse: for PulseAudio support' 'alsa-lib: for ALSA support' 'gstreamer0.10: For video support, may need good, bad and ugly plugins' 'freealut: For OpenAL support')
[ "$CARCH" = "x86_64" ] && depends=('lib32-gtk2' 'lib32-libpng' 'lib32-libidn' 'lib32-sdl' 'lib32-mesa')
[ "$CARCH" = "x86_64" ] && optdepends=('lib32-nvidia-utils: GL support for NVIDIA drivers' 'lib32-libpulse: for PulseAudio support' 'lib32-alsa-lib: for ALSA support' 'lib32-freealut: For OpenAL support')
arch=('i686' 'x86_64')
#install=singularityviewer.install
source=(	"https://github.com/downloads/singularity-viewer/SingularityViewer/Singularity-i686-$pkgver.tar.bz2"
		"singularityviewer.desktop"
		"singularityviewer.launcher")

md5sums=('129c741067a3fb899e545efe4d099878'
         'dfe312441d443f2e1c955b194d4f35dd'
         'eb596f5cf7b6f2d0c55c0082fb99a905')

        
package() {
  cd $srcdir
  
  # Rename Data Directory
  mv Singularity-i686-${pkgver} singularityviewer
  
  # Install Desktop File
  install -D -m644 $srcdir/singularityviewer.desktop \
    $pkgdir/usr/share/applications/singularityviewer.desktop
  
  # Install Icon File
  install -D -m644 $srcdir/singularityviewer/singularity_icon.png \
    $pkgdir/usr/share/pixmaps/singularityviewer.png
  
  # Install Launcher
  install -D -m755 $srcdir/singularityviewer.launcher \
    $pkgdir/usr/bin/singularityviewer

  # Install License file
  install -D -m755 $srcdir/singularityviewer/licenses.txt \
    $pkgdir/usr/share/licenses/$pkgname/LICENSE
    
  # Move Data to Destination Directory
  install -d $pkgdir/opt
  mv singularityviewer $pkgdir/opt/
} 
