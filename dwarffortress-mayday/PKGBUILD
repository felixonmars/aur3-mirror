# Maintainer: Patrick Chilton <chpatrick _at_ gmail _dot_ com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>
pkgname=dwarffortress-mayday
pkgver=34_10
_dfver=34_10
_dfgver=34_10
pkgrel=1
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world. Mayday tileset."
arch=(i686 x86_64)
# WIP Thread: http://www.bay12forums.com/smf/index.php?topic=66142.0
url="http://mayday.w.staszic.waw.pl/df.php"
install="$pkgname.install"
license=('custom:dwarffortress')
depends=(gtk2 mesa sdl_image libsndfile openal sdl_ttf)
makedepends=(unzip)
if [[ $CARCH == 'x86_64' ]]; then
  depends=(lib32-gtk2 lib32-mesa lib32-sdl_image lib32-libsndfile lib32-openal
  lib32-libxdamage lib32-ncurses lib32-sdl_ttf)
  optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
              'lib32-catalyst-utils: If you have ATI graphics')
fi
conflicts=(dwarffortress)
source=(http://www.bay12games.com/dwarves/df_${_dfver}_linux.tar.bz2
        http://mayday.w.staszic.waw.pl/~mayday/upload/DFG/dfg_${_dfgver}_win.zip
        dwarffortress dwarffortress.desktop dwarffortress.png)
noextract=(dfg_${_dfgver}_win.zip df_${_dfver}_linux.tar.bz2)

build() {
  cd "${srcdir}"
  tar -f "df_${_dfver}_linux.tar.bz2" -x \
  --exclude "df_linux/raw" \
  --exclude "df_linux/data/art" \
  --exclude "df_linux/data/init/colors.txt" \
  --exclude "df_linux/data/init/init.txt"

  unzip -qod df_linux "dfg_${_dfgver}_win.zip" \
    "raw/*" \
    "data/art/*" \
    "data/init/colors.txt" \
    "data/init/init.txt"

  cd $srcdir/df_linux
  install -dm755 $pkgdir/opt/
  cp -r $srcdir/df_linux/ $pkgdir/opt/

  # Yay for precompiled stuff with junk permissions! :D
  find $pkgdir/opt/df_linux -type d -exec chmod 755 {} +
  find $pkgdir/opt/df_linux -type f -exec chmod 644 {} +

  install -Dm755 $srcdir/dwarffortress $pkgdir/usr/bin/dwarffortress

  chmod 755 $pkgdir/opt/df_linux/libs/Dwarf_Fortress

  install -d -m775 -o root -g games $pkgdir/opt/df_linux/data/save

  # This probably is overkill, but I don't know what specific files df needs permission for in here.
  chmod 775 $pkgdir/opt/df_linux
  chown root:games $pkgdir/opt/df_linux
  chown -R root:games $pkgdir/opt/df_linux/data
  find $pkgdir/opt/df_linux/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/df_linux/data -type f -exec chmod 664 {} +

  # Desktop launcher with icon
  install -Dm644 $srcdir/dwarffortress.desktop $pkgdir/usr/share/applications/dwarffortress.desktop
  install -Dm644 $srcdir/dwarffortress.png     $pkgdir/usr/share/pixmaps/dwarffortress.png

  install -Dm644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress/readme.txt
}

md5sums=('81ef446e3701839211d9b0ff9cd06ffc'
         '02eeace7e069b9c2790cbd4c799bdc26'
         'c19aacc31e8df354827db352fecfd200'
         'c8984d1eea6e409ecf339d6ee9e91e42'
         'b1d51f82400073af9bb179e34a9209d0')

# vim:set ts=2 sw=2 et:
