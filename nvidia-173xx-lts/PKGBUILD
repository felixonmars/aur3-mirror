# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-173xx-lts
pkgver=173.14.39
_extramodules=extramodules-3.10-lts
pkgrel=1
pkgdesc="NVIDIA drivers for linux-lts, 173xx branch."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=('linux-lts>=3.10.0' "nvidia-173xx-utils=${pkgver}")
makedepends=('linux-lts-headers>=3.10.0')
conflicts=('nvidia-96xx-lts' 'nvidia-lts')
license=('custom')
install=nvidia.install
source=("ftp://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run")
options=(!strip)
md5sums=('5b423543428554ef33a200fbbe3cb9fc')
[ "$CARCH" = "x86_64" ] && md5sums=('0799f194869e40141c7bac8a71762db6')

build() {
        _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	cd $srcdir
	sh NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run --extract-only
	cd NVIDIA-Linux-$ARCH-${pkgver}-pkg0
	cd usr/src/nv/
	ln -s Makefile.kbuild Makefile
	make SYSSRC=/usr/lib/modules/${_kernver}/build module
}

package() {
	cd $srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/
	mkdir -p $pkgdir/usr/lib/modules/${_extramodules}/
	install -m644 nvidia.ko $pkgdir/usr/lib/modules/${_extramodules}/

        mkdir -p $pkgdir/usr/lib/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/usr/lib/modprobe.d/nvidia-lts.conf
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" $startdir/nvidia.install
        # gzip -9 module
        gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
        install -D -m644 "${srcdir}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/LICENSE" "${pkgdir}/usr/share/licenses/nvidia-173xx-lts/LICENSE"
}
