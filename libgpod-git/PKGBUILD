# Maintainer: Jonas Abrahamsson <JKAbrams(at)aur>
# Contributor: Nicolas Quienot   <niQo(at)aur>
# Contributor: Dustin Mays       <dork.fish.wat(at)gmail(dot)com>
# Contributor: Kenny Stocoff     <sogenfallen(at)hotmail(dot)com>
# Contributor: Prurigro          <prurigro(at)gmail(dot)com>
# Contributor: oke3              <Sekereg [at] gmx [dot] com>
  
pkgname=libgpod-git
pkgver=20120624
pkgrel=1
pkgdesc="A shared library used to access the contents of an iPod"
url="http://www.gtkpod.org/wiki/Libgpod"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gdk-pixbuf2' 'mutagen' 'sg3_utils' 'libimobiledevice' 'libusbx')
makedepends=('intltool' 'swig' 'docbook-xsl' 'pygobject2-devel' 'gtk-sharp-2')
optdepends=('gtk-sharp-2: Mono bindings')
options=('!libtool' '!emptydirs')
conflicts=('libgpod')
provides=('libgpod=0.8.2')
source=('libgpod-git-archlinux_automake.patch')
md5sums=('f95f08cb72e3e987abb0318e538701e0')
  
_gitroot=git://gtkpod.git.sourceforge.net/gitroot/gtkpod/libgpod
_gitname=libgpod


build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."
 
    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin && cd "$srcdir"
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi
 
    msg "GIT checkout done or server timeout"
    msg "Starting build..."
 
    rm -rf "$_gitname-build"
    git clone "$_gitname" "$_gitname-build"
    ls
    cd "$_gitname-build"
    patch -Np1 -i ../libgpod-git-archlinux_automake.patch
 
    ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
                 --enable-udev
 
    make
}
 
package() {
    cd "$srcdir/$_gitname-build"
 
    make DESTDIR="$pkgdir" install
}
