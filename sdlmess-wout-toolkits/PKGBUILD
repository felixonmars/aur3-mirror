# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based on: Anton Shestakov <engored*ya.ru>

_patchlevel=3
_basever=0.148

pkgname=sdlmess-wout-toolkits
pkgver="${_basever}.u${_patchlevel}"
pkgrel=1
pkgdesc="Multiple Emulator Super System, computer and game console emulator using SDL with OpenGL support. Without toolkits (gtk and qt)"
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
provides=('sdlmess')
conflicts=('sdlmess')
depends=('sdl>=1.2.11' 'fontconfig' 'sdl_ttf' 'alsa-lib')
makedepends=('nasm' 'mesa' 'glu' 'wget' 'python2')
optdepends=('sdlmamefamily-tools: Tools for manage MAME/MESS/UME roms')
DLAGENTS=('http::/usr/bin/wget -U "Mozilla/5.0 (X11; U; Linux x86_64; en-US; rv:1.9.1.2) Gecko/20090804 Shiretoko/3.5.2" -c -t 3 --waitretry=3 -O %o %u')
install="sdlmess.install"

for i in $(seq 1 "${_patchlevel}"); do
  _patches="${_patches} sdlmame-${_basever/./}u${i}_diff.zip::http://mamedev.org/updates/${_basever/./}u${i}_diff.zip"
done

source=("mame${_basever/./}s.zip::http://mamedev.org/downloader.php?file=releases/mame${_basever/./}s.zip"
        "sdlmess.sh"
        ${_patches})
md5sums=('38f7727c2961cd31e2ab6aa1814a23ba'
         '141069e7019da5e33414dc8d4c421150'
         '01edd53824784f52448f4128f6d52aac'
         '6970189e5cd593dea59aa87579bb4dca'
         '406dff01777ba08a35489b3081d5f644')

prepare() {
  cd "${srcdir}"
  rm -fr build && mkdir build && cd build
  bsdtar -xf "${srcdir}/mame.zip"
  find . -type f -not -name "*.png" -exec perl -pi -e 's|\r\n?|\n|g' "{}" \;
  for i in $(seq 1 "${_patchlevel}"); do
    msg2 "Patch#${i}"
    patch --silent -p0 -E -i "${srcdir}/${_basever/./}u${i}.diff"
  done

  sed -e 's|# NO_X11|NO_X11|g' -e 's|# NO_USE_QTDEBUG|NO_USE_QTDEBUG|g' -i src/osd/sdl/sdl.mak
}

build() {
  cd "${srcdir}/build"

  make PYTHON=python2 OPTIMIZE=2 NOWERROR=1 ARCHOPTS=-march=native FULLNAME=mess TARGET=mess
}

package() {
  cd "${srcdir}/build"
  # Installing the wrapper script
  install -Dm755 "${srcdir}/sdlmess.sh" "${pkgdir}/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 mess "${pkgdir}/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "${pkgdir}/usr/share/sdlmess/"{artwork,hash}
  install -m644 artwork/* "${pkgdir}/usr/share/sdlmess/artwork/"
  install -m644 hash/* "${pkgdir}/usr/share/sdlmess/hash/"

  # Install man
  install -Dm644 src/osd/sdl/man/mess.6 "${pkgdir}/usr/share/man/man6/sdlmess.6"

  # The license
  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/custom/${pkgname}/license.txt"
}
