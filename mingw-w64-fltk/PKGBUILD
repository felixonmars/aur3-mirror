# Maintainer: Daniel Kirchner <daniel at ekpyron dot org>

pkgname=mingw-w64-fltk
_basename=fltk
pkgver=1.3.1
pkgrel=1
pkgdesc="Graphical user interface toolkit (mingw-w64)"
arch=('any')
license=('custom:LGPL')
url="http://www.fltk.org/"
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
source=(http://ftp.easysw.com/pub/$_basename/$pkgver/$_basename-$pkgver-source.tar.gz)
options=(!strip !buildflags)
md5sums=('b5b84ed63751ab3f76aa8d36cccf59e7')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS

  sed -i -e "s/DIRS = \$(IMAGEDIRS) src \$(CAIRODIR) fluid test documentation/DIRS = \$(IMAGEDIRS) src \$(CAIRODIR) fluid documentation/" "${srcdir}/${_basename}-${pkgver}/Makefile"

  for _arch in ${_architectures}; do
	  rm -rf "${srcdir}/build-${_arch}"
	  cp -r "${srcdir}/${_basename}-${pkgver}" "${srcdir}/build-${_arch}"
      cd "${srcdir}/build-${_arch}"
      ./configure --prefix=/usr/${_arch} --host=${_arch}
      make
  done
}

package () {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm -f "${pkgdir}/usr/${_arch}/bin/fluid.exe"
    rm -rf "${pkgdir}/usr/${_arch}/share"
    ${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/*.a
  done
}
