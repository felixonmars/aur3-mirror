# Contributor: Lex Rivera aka x-demon <aur@x-demon.org>                
# Maintainer: Lex Rivera aka x-demon <aur@x-demon.org>
# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=binkd
pkgver=0.9.11
pkgrel=6
pkgdesc="Binkley protocol daemon for transferring files between Fidonet systems"
arch=('i686' 'x86_64')
url="http://2f.ru/binkd/"
license=('GPL')
backup=("etc/binkd/binkd.conf")
source=("ftp://happy.kiev.ua/pub/fidosoft/mailer/$pkgname/$pkgname-$pkgver.tar.gz"
        "binkd.conf.patch"
        "binkd.service"
        "binkd@.service"
        "binkd.socket"
        "binkd.xinetd"
        "binkd.rcd")
sha1sums=('b0e2b222c4f786c787066a7d2e70bd92ac586d95'
          'a8f9c0e1eaf714ae38f167e915e440fff2fbe10c'
          'e7e815a9605b83d5247b30ed602c211d5f95a653'
          '0e4bb918f9416ec9582a410fcca58ec2fb693878'
          '469aa00b9f14e7439f310e25f7e9321e80ea7e18'
          '82066ab5585bb8a069da141b525ad620d3ac99b5'
          '69cdfc94c8f16a5a593c3bd5f0bd94266fb8558e')

build() {
  cd "$srcdir/$pkgname"
  cp mkfls/unix/{Makefile*,configure*,install-sh,mkinstalldirs} .
  ./configure \
    --prefix=/usr           \
    --sysconfdir=/etc       \
    --localstatedir=/var    \
    --with-https            \
    --with-ntlm             \
    ;
  touch banner # shut up post-make text
  make
}

package() {
  cd "$srcdir/$pkgname"
  make PREFIX="$pkgdir/usr" install

  # fix absolute symlink
  ln -sf "binkd-$pkgver" "$pkgdir/usr/sbin/binkd"

  # fix manpage location
  mkdir -p "$pkgdir/usr/share"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share/man"

  # fix /usr/etc and strip -dist suffix
  install -Dm0644 "$pkgdir/usr/etc/binkd.conf-dist" "$pkgdir/etc/binkd/binkd.conf"
  rm -rf "$pkgdir/usr/etc"

  # adjust config to have Unix paths
  cd "$pkgdir"
  patch -p1 < "$srcdir/binkd.conf.patch"
  for dir in inbound{,/fidonet,/incomplete} outbound{,/unknown} {tmail,brake}/boxes; do
    mkdir -p "$pkgdir/var/spool/fido/$dir"
  done

  cd "$srcdir"
  install -Dm0644 binkd.service   "$pkgdir/usr/lib/systemd/system/binkd.service"
  install -Dm0644 binkd@.service  "$pkgdir/usr/lib/systemd/system/binkd@.service"
  install -Dm0644 binkd.socket    "$pkgdir/usr/lib/systemd/system/binkd.socket"
  install -Dm0644 binkd.xinetd    "$pkgdir/etc/xinetd.d/binkd"
  install -Dm0755 binkd.rcd       "$pkgdir/etc/rc.d/binkd"
}

# vim: ts=2:sw=2:et
