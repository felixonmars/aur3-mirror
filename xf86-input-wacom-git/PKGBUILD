# Contributor: Egor Pomortsev <egor.pomortsev@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Federico Cinelli <cinelli.federico@gmail.com>

pkgname=xf86-input-wacom-git
pkgver=20120410
pkgrel=1
pkgdesc="X.Org Wacom tablet driver - development version"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net"
license=('GPL')
depends=('xorg-server>=1.9.0' 'libxi')
optdepends=('libxrandr: for xsetwacom tool'
            'libxinerama: for xsetwacom tool')
makedepends=('git' 'xorg-server-devel' 'libxext')
provides=('xf86-input-wacom')
conflicts=('xf86-input-wacom')
options=(!libtool)

_gitroot="git://linuxwacom.git.sourceforge.net/gitroot/linuxwacom/xf86-input-wacom"
_gitname="xf86-input-wacom"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r  "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  make
}
package() {
  make -C "$srcdir/$_gitname-build" PREFIX=/usr  DESTDIR="$pkgdir" install
}

