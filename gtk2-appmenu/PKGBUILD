# $Id: PKGBUILD 81490 2010-05-31 07:23:49Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gtk2-appmenu
pkgname=gtk2-appmenu
#Comment the line above, and uncomment the line below
#pkgname=('gtk2-appmenu' 'gtk-update-icon-cache')
pkgver=2.24.7
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk2.install
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'krb5'
             'gnutls' 'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
options=('!libtool' '!docs')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.bz2
        xid-collision-debug.patch
	043_ubuntu_menu_proxy.patch
        072_indicator_menu_update.patch
        fix.patch
        012_ubuntu-set-grab-add.patch)
sha256sums=('8481c35156ea01ca94c7347d9f28b726ebbe0914d5416a30bab9b007afefe141'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            '83280aa0f3ac1e2456656d3e97d804ec5f891737f93799fcb1cc1f1dd0d9a41a'
            '2e288dd89adf5b940b7284465a8913025f41dfda4d032a1c25fab9d2e9192ed0'
            '8cd5576b28b9fa42da109bdaec2ff253f276d02f9856d3795efb1724037a14c8'
            '32e6765f7e0bebb135eb715561be05ecdc178b1bd24b6ae6526fef8c815e5b00')

build() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}/xid-collision-debug.patch"

  patch -Np0 -i "${srcdir}/fix.patch"
  patch -Np1 -i "${srcdir}/043_ubuntu_menu_proxy.patch"
  patch -Np1 -i "${srcdir}/072_indicator_menu_update.patch"
  patch -Np1 -i "${srcdir}/012_ubuntu-set-grab-add.patch"

  CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-xinput=yes --disable-visibility

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool 

  make $MAKEFLAGS
}

package_gtk2-appmenu() {
    pkgdesc="The GTK+ Toolkit (v2) with Ubuntu AppMenu Patch"
    install=gtk2.install
    depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'krb5' 
             'gnutls' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    backup=(etc/gtk-2.0/gtkrc)
    provides=("gtk2=${pkgver}")
    conflicts=('gtk2')

    cd "${srcdir}/gtk+-${pkgver}"

    make DESTDIR="${pkgdir}" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm ${pkgdir}/usr/bin/gtk-update-icon-cache
}
package_gtk-update-icon-cache() {
    pkgdesc="The GTK+ update icon cache tool"
    depends=('gdk-pixbuf2')

    cd "${srcdir}/gtk+-${pkgver}/gtk"

    install -D -m755 gtk-update-icon-cache ${pkgdir}/usr/bin/gtk-update-icon-cache
}
