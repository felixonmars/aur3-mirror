# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from network-manager package)
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

_pkgname=networkmanager
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="Network Management daemon"
arch=(i686 x86_64)
url=http://www.gnome.org/projects/NetworkManager
license=(GPL)
depends=(dbus-glib dhclient libsoup polkit wpa_supplicant)
makedepends=(git gobject-introspection gtk-doc intltool)
optdepends=('modemmanager: for modem management service'
    'dnsmasq: Connection sharing'
    'bluez: Bluetooth support'
    'openresolv: resolvconf support')
provides=($_pkgname=0.9.4)
conflicts=($_pkgname)
backup=(etc/NetworkManager/NetworkManager.conf)
options=(!emptydirs !libtool)
install=$pkgname.install
source=(NetworkManager.conf {disable_set_hostname,dnsmasq-path,nm-polkit-permissive,repair-signals}.patch)
sha256sums=('44b048804c7c0b8b3b0c29b8632b6ad613c397d0a1635ec918e10c0fbcdadf21'
    '46f3d7cc5fdc449e613b1e6a651879f87f0a9fa65818e7e0ebae20b11d2eef39'
    'e7753d8ae42395289c0294f039a658628086383a56ea03cafe38a4579012879f'
    '91881a6ba962800895901fcc6929955b9d49095b6ed97fd913c99c77ddd630db'
    'c6872283adc50e758a4af65014e5fb4786bb020f411acc4f0a01f2052297a650')
sha512sums=('1f4e6c09da5a8921ee7759861cfa5e808c64cd27e733cd62cfb35776db05a226a73443c071730035480c9ba4abe85fa7daeb14aeffbbdae20ed193d78341f451'
    '8527e8dfe5b46fc44b85f93532e8a0431d6a7934c4f782aca90a451ac3a3576dd15aa28df040f1b114f65e9037bc9895eb908e92bd4e4c621e6a9c7178a3ec34'
    'a2276623bb1d8e7c5f0c647a2bd24aca54a138c353bb9ff137263090af4141d184d943010052836de829158c7f1f0b873c67c05af5e1433efe9611f5e37c9c1a'
    'c8668a85332065f5cd89650951a15725bae1f5a530b61fbc2fe57d51a8924b1f3e8765cd3784d89476db3d2d9c8992156c4986424670d91d8f960425abbacde5'
    '1503b809f2d363db64875f0b0f560ebc2847753910002c444740e466f080877413c270b1f2a41bd90456dd4edc550286324fe1b12f10d0123f0deb88ab15e14f')

_gitroot=git://anongit.freedesktop.org/NetworkManager/NetworkManager.git
_gitname=NetworkManager

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."

    patch -Np1 -i ../disable_set_hostname.patch
    patch -Np1 -i ../dnsmasq-path.patch
    patch -Np1 -i ../nm-polkit-permissive.patch

    gtkdocize
    autopoint --force
    AUTOPOINT="intltoolize -f -c --automake" autoreconf -fi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/networkmanager \
        --with-crypto=nss \
        --with-distro=arch \
        --with-dhclient=/usr/sbin/dhclient \
        --with-dhcpcd=/usr/sbin/dhcpcd \
        --with-iptables=/usr/sbin/iptables \
        --with-systemdsystemunitdir=/usr/lib/systemd/system \
        --with-udev-dir=/usr/lib/udev \
        --with-resolvconf=/usr/sbin/resolvconf \
        --with-session-tracking=systemd \
        --disable-static \
        --enable-more-warnings=no \
        --disable-wimax
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install

    install -m644 ../$_gitname.conf "$pkgdir"/etc/$_gitname/
    rm -r "$pkgdir"/var/run
    # Provide native service for arch-daemons generator
    ln -s $_gitname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
