# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevplatform-git
pkgver=20121027
pkgrel=1
pkgdesc="A C/C++ development platform for KDE - Git build"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdelibs' 'boost-libs' 'subversion' 'qjson')
makedepends=('cmake' 'automoc4' 'boost' 'git')
provides=('kdevplatform')
conflicts=('kdevplatform')
source=('template.patch')
md5sums=('b3a37e25c5af9a27dcee00fbe0e4ea15')

_gitroot="git://anongit.kde.org/kdevplatform"
_gitname="kdevplatform"

build() {
    cd "$srcdir"
    msg "Connecting to Git server..."

    if [ -d $_gitname ] ; then
        cd $_gitname

        # Revert patched changes
        git checkout .

        git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "Git checkout done."

    # Patch for GCC 4.6 build
    cd "$srcdir/$_gitname"
    git apply "$srcdir/template.patch"

    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../${_gitname} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release

    make
}

package() {
    cd "$srcdir/build"

    make DESTDIR="${pkgdir}" install
}
