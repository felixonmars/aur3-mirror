# Contributor: Philipp Schwarz <fi-dschi_archlinux - at - arcor - dot - de>
# Maintainer: SanskritFritz (gmail)

pkgname=areca-beta
pkgver=7.4
pkgrel=1
_prgname=areca
pkgdesc="Personal file backup software supporting compression, encryption and incremental backups."
arch=('i686' 'x86_64')
license=('GPL2')
depends=('java-runtime')
provides=('areca')
conflicts=('areca')
url=('http://www.areca-backup.org')
source=("http://sourceforge.net/projects/areca/files/areca-beta/areca-${pkgver}-beta/areca-${pkgver}-beta-linux-gtk-32.tar.gz"
	"areca.sh"
	"areca_cl.sh"
	"areca_decrypt.sh"
	"areca_dezip.sh"
	"areca.desktop")
md5sums=('9e16115eadee58211764cc6aece1bf4f'
         '024e3fe74c518a9dee889f3ea1ff9f71'
         '662d65e44fe0806cff69850dae55f107'
         '8e0088445454ab80a9f04c84b3363814'
         '2f1bd15c94600065c43366f440cc0c37'
         '5e978edc20d715cefec098e3e77a3721')
# for 64bit
[ "$CARCH" = "x86_64" ] && source[0]="http://sourceforge.net/projects/areca/files/areca-beta/areca-${pkgver}-beta/areca-${pkgver}-beta-linux-gtk-64.tar.gz"
[ "$CARCH" = "x86_64" ] && md5sums[0]='111fd2524039e71797c662a0395294d7'

package() {
	mkdir -p "${pkgdir}/opt/"
	cp -r  "${srcdir}/${_prgname}"  "${pkgdir}/opt/"

	# copy start scripts to /usr/bin for they then will be in $PATH
	# these contain the full fixed path of /opt/areca/bin/areca_run.sh
	for _script in "areca" "areca_cl" "areca_decrypt" "areca_dezip"
	do
		install -D "${startdir}/${_script}.sh" "${pkgdir}/usr/bin/${_script}"
	done

	# symlink menu icon
	mkdir -p "${pkgdir}/usr/share/pixmaps"
	ln -s "/opt/areca/icons/ico_72.png" "${pkgdir}/usr/share/pixmaps/areca_72x72.png"

	# copy .desktop file (menu entry)
	install -D "${startdir}/${_prgname}.desktop" "${pkgdir}/usr/share/applications/${_prgname}.desktop"
} 
