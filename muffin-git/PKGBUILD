# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=muffin-git
pkgver=20120410
pkgrel=1
pkgdesc="A window manager for GNOME"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gconf' 'gobject-introspection' 'libcanberra' 'startup-notification' 'zenity' 'libxxf86vm')
makedepends=('git' 'intltool' 'gnome-doc-utils')
provides=('muffin-wm')
conflicts=('muffin-wm')
options=('!libtool' '!emptydirs')
install=muffin.install

_gitroot='https://github.com/linuxmint/muffin.git'
_gitname='muffin'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Fix build with libx11 >= 1.4.99.1
  CFLAGS="${CFLAGS} -Wno-error=deprecated-declarations"

  autoreconf -vfi
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin --localstatedir=/var --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain muffin "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -v "${pkgdir}"/etc/gconf/schemas/*.schemas
}
