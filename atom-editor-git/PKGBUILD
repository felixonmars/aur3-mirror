# Maintainer: John Reese <john@noswap.com>
# Upstream URL: https://github.com/atom/atom
#
# For improvements/fixes to this package, please send a pull request:
# https://github.com/jreese/arch

pkgname=atom-editor-git
pkgver=20140707
pkgrel=1
pkgdesc="Chrome-based text editor from Github"
arch=('x86_64' 'i686')
url="https://github.com/atom/atom"
license=('MIT')
provides=('atom-editor')
conflicts=('atom-editor')
depends=('alsa-lib' 'gconf' 'gtk2' 'libatomic_ops' 'libgcrypt' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
makedepends=('git')

_gitroot="git://github.com/atom/atom"
_gitname="atom"

source=("$_gitroot" 'atom-python.patch')
sha256sums=('SKIP' 'ecaac2664b33676df2d4f45d24f1754bf801fc1f723ad51d330c69f8c249dcd8')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

package() {
  cd "$srcdir/$_gitname"

  export PYTHON=python2
  export INSTALL_PREFIX="$pkgdir/usr"

  script/build --install-dir=$pkgdir/usr
  script/grunt install

  patch "$pkgdir/usr/bin/atom" < "$srcdir/atom-python.patch"

  sed -e "s/<%= description %>/$pkgdesc/" \
    -e "s|<%= installDir %>/share/atom/atom|/usr/bin/atom|"\
    -e "s|<%= iconName %>|atom|"\
    resources/linux/Atom.desktop.in > resources/linux/Atom.desktop
  install -Dm644 resources/linux/Atom.desktop "$pkgdir/usr/share/applications/Atom.desktop"

  install -Dm644 resources/atom.png "$pkgdir/usr/share/pixmaps/atom.png"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
