# Maintainer: Damjan Georgievski <gdamjan@gmail.com>
pkgname=uwsgi-router-plugins
_pkgbase=uwsgi
_router_plugins=(corerouter fastrouter router_uwsgi router_http router_redirect router_rewrite
                    router_basicauth router_cache)
pkgver=1.4.1
pkgrel=1
epoch=
pkgdesc="All the router plugins for uWSGI (fastrouter, redirect, rewrite, http, uwsgi, basicauth)"
arch=(i686 x86_64)
url="http://projects.unbit.it/uwsgi/"
license=('GPL2')
groups=()
depends=(uwsgi-core pcre)
makedepends=(python2)
checkdepends=()
provides=()
conflicts=()
replaces=(uwsgi)
backup=()
options=()
install=
changelog=
source=(http://projects.unbit.it/downloads/$_pkgbase-$pkgver.tar.gz)
noextract=()
md5sums=('e79cc615a354531ed0c1e06c2aa3286a')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"
  for plugin in "${_router_plugins[@]}"; do
      python2 uwsgiconfig.py --plugin plugins/$plugin core $plugin
  done
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"
  for plugin in "${_router_plugins[@]}"; do
      install -Dm755 ${plugin}_plugin.so "$pkgdir/usr/lib/uwsgi/${plugin}_plugin.so"
  done
}

