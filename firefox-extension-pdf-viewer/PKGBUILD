# Maintainer: OmeGa <omega at mailoo dot org>

pkgname=firefox-extension-pdf-viewer
_addons_file=181716
pkgver=0.7.1
pkgrel=2
pkgdesc="Uses HTML5 to display PDF files directly in Firefox."
arch=('any')
url="https://github.com/mozilla/pdf.js/"
license=('Apache')
depends=('firefox')
makedepends=('unzip')
source=(https://addons.mozilla.org/firefox/downloads/file/${_addons_file}/pdf_viewer-${pkgver}.xpi)
noextract=("pdf_viewer-${pkgver}.xpi")
sha1sums=('b32767375bc1127386d910d8d0f057f078e8117e')

build() {
  cd ${srcdir}
  [[ -d ${pkgname}.extract ]] || \
    mkdir ${pkgname}.extract
  cd ${pkgname}.extract
  unzip ../pdf_viewer-${pkgver}.xpi
}

package() {
  cd ${srcdir}/${pkgname}.extract  
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf) || return 1
  local dstdir=${pkgdir}/usr/lib/firefox/extensions/${emid}
  install -d ${dstdir}
#  sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>20.*</em:maxVersion>#' install.rdf
  cp -R * ${dstdir}
  find ${pkgdir} -type d -exec chmod 0755 {} \;
  find ${pkgdir} -type f -exec chmod 0644 {} \;
}

# vim:set ts=2 sw=2 et:
