# Maintainer: Jordi De Groof <jordi.degroof@gmail.com>
# Contributor: Dogukan Korkmazturk <d.korkmazturk@gmail.com>
# Contributor: Stephane Travostino <stephane.travostino@gmail.com>
# Contributor: Calogero Lo Leggio <kalos@autistici.org>

pkgname=bacula-bat
pkgver=5.2.6
pkgrel=1
pkgdesc="Bacula Administration Tool Console"
arch=(i686 x86_64)
url="http://www.bacula.org"
license=('AGPL3')
depends=('qt')
backup=(etc/bat.conf)
source=("http://downloads.sourceforge.net/sourceforge/bacula/bacula-${pkgver}.tar.gz")
md5sums=('914483b92539598678542bd2ddb28043')

build() {
    cd "${srcdir}/bacula-${pkgver}"

    ./configure --prefix=/usr --sbindir=/usr/bin \
    --enable-client-only --enable-bat \
    --with-x --with-openssl \
    --sysconfdir=/etc/

    make || return 1
}

package() {
    # install only bat files
    mkdir -p ${pkgdir}/usr/bin/ ${pkgdir}/usr/lib/ ${pkgdir}/etc/
    mkdir -p ${pkgdir}/usr/share/man/man1/ ${pkgdir}/usr/share/applications/ ${pkgdir}/usr/share/pixmaps/
    install -m755 ${srcdir}/bacula-${pkgver}/src/qt-console/.libs/bat ${pkgdir}/usr/bin/bat
    install -m755 ${srcdir}/bacula-${pkgver}/src/lib/.libs/*-*.so ${pkgdir}/usr/lib
    install -m755 ${srcdir}/bacula-${pkgver}/src/findlib/.libs/*-*.so ${pkgdir}/usr/lib
    install -m644 ${srcdir}/bacula-${pkgver}/src/qt-console/bat.conf ${pkgdir}/etc/bat.conf
    install -m644 ${srcdir}/bacula-${pkgver}/manpages/bat.1 ${pkgdir}/usr/share/man/man1/bat.1
    install -m644 ${srcdir}/bacula-${pkgver}/scripts/bat.desktop ${pkgdir}/usr/share/applications/bat.desktop
    install -m644 ${srcdir}/bacula-${pkgver}/src/qt-console/images/bat_icon.png ${pkgdir}/usr/share/pixmaps/bat_icon.png
}
