# Maintainer: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
pkgname=openblas
_pkgname=xianyi-OpenBLAS
pkgver=0.2.1
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD "
arch=('i686' 'x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl')
provides=('blas=3.4.0' 'cblas')
conflicts=('blas' 'cblas')
options=(!makeflags)
source=(${_pkgname}-v${pkgver}-0-g551f478.tar.gz::http://github.com/xianyi/OpenBLAS/tarball/v${pkgver})
md5sums=('ab3d39f1f94655e31cd4dcec6001ccbe')

build() {
  cd "$srcdir/$_pkgname-956397f"

  unset CFLAGS
  unset CXXFLAGS
  NPROC=`grep "physical id" /proc/cpuinfo|sort|uniq|wc -l`
  NCORE4PROC=` grep "cores" /proc/cpuinfo|sort|uniq|sed -e 's/cpu cores.*: //'`
  let NCORE=NPROC*NCORE4PROC
  make USE_OPENMP=1 NO_LAPACK=1 NUM_THREADS=$NCORE
}

package() {
  cd "$srcdir/$_pkgname-956397f"

  make PREFIX="$pkgdir/usr" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/lib/"
  ln -sf libopenblas_*-r$pkgver.a libopenblas.a
  ln -sf libopenblas_*-r$pkgver.so libopenblas.so
  ln -sf libopenblas_*-r$pkgver.so libopenblas.so.0
  ln -sf libopenblas.so libblas.so.3
  ln -sf libopenblas.a libblas.a
  ln -sf libblas.so.3 libblas.so
  ln -sf libopenblas.so libcblas.so.3
  ln -sf libopenblas.a libcblas.a
  ln -sf libcblas.so.3 libcblas.so
}

# vim:set ts=2 sw=2 et:
