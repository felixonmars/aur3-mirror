# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=sidplay2-libs
pkgver=2.1.1
pkgrel=5
pkgdesc="The new generation of SID emulation"
arch=('i686' 'x86_64')
url="http://sidplay2.sourceforge.net"
license=('GPL')
depends=('gcc-libs')
makedepends=('pkgconfig' 'libtool' 'autoconf' 'automake')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sidplay2/sidplay-libs-$pkgver.tar.gz
	$pkgname-$pkgver.diff)
md5sums=('7ea0ba5dc1da4604d15eaae001f7d2a7'
         '7350bf67b247d040e64e160aae820711')

build() {
  cd "$srcdir"/sidplay-libs-$pkgver

  patch -Np1 -i "$srcdir"/$pkgname-$pkgver.diff

  export CXXFLAGS+=" -fPIC"

  autoreconf -i

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir"/sidplay-libs-$pkgver

  make DESTDIR="$pkgdir" install
}
