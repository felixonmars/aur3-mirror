# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=sidplay2
pkgver=2.0.9
pkgrel=4
pkgdesc="A command line player for SID music files"
arch=('i686' 'x86_64')
url="http://$pkgname.sourceforge.net"
license=('GPL')
depends=('sidplay2-libs')
makedepends=('pkgconfig' 'autoconf' 'automake')
source=(http://downloads.sourceforge.net/$pkgname/sidplay-$pkgver.tar.gz
        config.guess.diff
        $pkgname-gcc44.patch)
md5sums=('8b0449e501ba8e684f718dce9b77c5a5'
         '7b986675a6721700f86c0eb56a5b33bb'
         'cd6e6e936793f25550bb579fa3f8c99d')

build() {
  cd "$srcdir"/sidplay-$pkgver

  patch -Np1 -i "$srcdir"/config.guess.diff
  patch -Np0 -i "$srcdir"/$pkgname-gcc44.patch

  ./configure --prefix=/usr --disable-static

  make
}

package () {
  cd "$srcdir"/sidplay-$pkgver

  make DESTDIR="$pkgdir" install
}
