# Maintainer: Luis Sarmiento < lgsarmientop-ala-unal.edu.co >

pkgname=scid
_pkgname=Scid
pkgver=4.3
pkgrel=3
pkgdesc="A Free Chess Database Application"
url="http://scid.sourceforge.net"
arch=('x86_64' 'i686')
license=('GPL')
depends=('tk' 'python')
options=('!emptydirs')

source=("$pkgname-$pkgver.tar.bz2::http://prdownloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2?download"
  "configure_tclversion.patch"
  "http://a.fsdn.com/con/icons/sc/scid%40sf.net/scid.png")
md5sums=('66592f91b690458d3aecb582142fc159'
  'e706fdb83d94e3c67ae67324e6ba214a'
  'd1994a0f57295c1d3857129de7ee0abb')

build() {
  _TCL_VER=`pacman -Q tk | cut -f2 -d" " | cut -f1-2 -d"."`
  msg "Compiling ${pkgname} using Tcl version ${_TCL_VER}"
  cd ${srcdir}/${pkgname}
  patch -Np2 < ${srcdir}/configure_tclversion.patch
  ./configure BINDIR=${pkgdir}/usr/bin SHAREDIR=${pkgdir}/usr/share/${pkgname} TCL_VERSION=${_TCL_VER} OPTIMIZE="-O2 -funroll-loops"
  make
}

package () {
  cd ${srcdir}/${pkgname}
  make install
  msg "Creating Desktop file"
  install -Dm644 ${srcdir}/scid.png ${pkgdir}/usr/share/scid/scid.png
  echo "
[Desktop Entry]
Version=1.0
Name=${_pkgname}
Comment=${pkgdesc}
Exec=${pkgname}
Icon=/usr/share/${pkgname}/scid.png
Categories=Game;BoardGame;
Type=Application
Terminal=false
StartupNotify=false
" > $srcdir/$pkgname.desktop
  
  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
