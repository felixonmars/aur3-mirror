# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=openfst
pkgver=1.2.10
pkgrel=1
pkgdesc="Library for constructing, combining, optimizing, and searching weighted finite-state transducers (FSTs)"
arch=('i686' 'x86_64')
url="http://www.openfst.org/"
license=('APACHE')
depends=()
optdepends=('icu: for enabling unicode support')
options=(!libtool)
source=("http://openfst.cs.nyu.edu/twiki/pub/FST/FstDownload/${pkgname}-${pkgver}.tar.gz")
md5sums=('2c73dca4cbfe3850b9b7f6988249c870')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# Options according to http://openfst.cs.nyu.edu/twiki/bin/view/FST/ReadMe
	# using "--enable-far" together with "--with-icu" produces an error
	OPTIONS="--prefix=/usr"
	OPTIONS+=" --enable-bin"            # Enable fst::script and command-line binaries; Default: yes
	OPTIONS+=" --enable-compact-fsts"   # Enable all CompactFst classes;                Default: no
	OPTIONS+=" --enable-const-fsts"     # Enable all ConstFst classes;                  Default: no
	OPTIONS+=" --enable-far"            # Enable FAR (FST Archive) extension;           Default: no
	OPTIONS+=" --enable-lookahead-fsts" # Enable LookAheadFst classes;                  Default: no
	OPTIONS+=" --enable-pdt"            # Experimental push-down transducer extensions; Default: no
#	OPTIONS+=" --with-icu"              # Use ICU to provide unicode support;           Default: no
	LIBS="-ldl" ./configure $OPTIONS
	make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
