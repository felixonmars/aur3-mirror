# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=openfst
pkgver=1.3.1
pkgrel=1
pkgdesc="Library for constructing, combining, optimizing, and searching weighted finite-state transducers (FSTs)"
arch=('i686' 'x86_64')
url="http://www.openfst.org/"
license=('APACHE')
depends=('gcc-libs' 'glibc')
options=(!libtool)
source=("http://openfst.cs.nyu.edu/twiki/pub/FST/FstDownload/${pkgname}-${pkgver}.tar.gz")
md5sums=('9d1ce908eac610da805dfd4fdc337b82')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# Options according to http://openfst.cs.nyu.edu/twiki/bin/view/FST/ReadMe
	OPTIONS="--prefix=/usr --disable-dependency-tracking"
	OPTIONS+=" --enable-bin"            # Enable fst::script and command-line binaries; Default: yes
	OPTIONS+=" --enable-compact-fsts"   # Enable all CompactFst classes;                Default: no
	OPTIONS+=" --enable-const-fsts"     # Enable all ConstFst classes;                  Default: no
	OPTIONS+=" --enable-far"            # Enable FAR (FST Archive) extension;           Default: no
	OPTIONS+=" --enable-lookahead-fsts" # Enable LookAheadFst classes;                  Default: no
	OPTIONS+=" --enable-pdt"            # Experimental push-down transducer extensions; Default: no
	./configure $OPTIONS
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
