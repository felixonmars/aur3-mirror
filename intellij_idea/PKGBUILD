# Maintainer: Alexsandr Pavlov <kidoz at mail dot ru>
pkgname=intellij_idea
_pkgname=idea-IU
pkgver=12.1.1
_pkgver=129.239
pkgrel=1
pkgdesc="The most intelligent Java IDE."
arch=('i686' 'x86_64')
options=('!strip')
url="http://www.jetbrains.com/idea"
license=('custom')
depends=('java-environment')
source=(http://download.jetbrains.com/idea/ideaIU-${pkgver}.tar.gz
         intellij_idea
         intellij_idea.desktop)

sha256sums=('2e9b2c2a6e6d52bfe5786e83adda25655b88a2c8b51ca307f1e0ed3ad81da318'
            '71facc43aedfe34d56093ee33a2856511fee6762287eb128c7bbe4c4c8a4d07c'
            '38dcbc472588050c3dd742a053f7651e10de59a5db8151c8c4459a0bfc4f83ce')

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -r ${srcdir}/${_pkgname}-${_pkgver}/* "${pkgdir}/opt/${pkgname}"
  if [[ $CARCH = 'i686' ]]; then
    rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen64.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
  fi
  if [[ $CARCH = 'x86_64' ]]; then
    rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen.so"
    rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier"
  fi

  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/applications"
  mkdir -p "${pkgdir}/usr/share/pixmaps"
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "${startdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications"
  install -m 644 "${pkgdir}/opt/${pkgname}/bin/idea.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -m 644 "${srcdir}/${_pkgname}-${_pkgver}/license/IDEA_license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/${_pkgname}_license.txt"
  install -m 755 "${startdir}/${pkgname}" "${pkgdir}/usr/bin"
}

