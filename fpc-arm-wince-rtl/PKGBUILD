pkgname=fpc-arm-wince-rtl
pkgver=2.6.0
pkgrel=4
pkgdesc="Free Pascal runtime library for Windows CE."
arch=(any)
url="http://www.freepascal.org"
license=("GPL" "LGPL" "custom")
depends=("fpc=2.6.0" arm-wince-binutils "ppcrossarm=2.6.0")
makedepends=(arm-wince-binutils "ppcrossarm=2.6.0")
options=(!buildflags !strip)
source=("ftp://ftp.freepascal.org/pub/fpc/dist/$pkgver/source/fpcbuild-$pkgver.tar.gz")
md5sums=('17375e665a4e1311f85812fe2754f609')

build() {
  cd "${srcdir}/fpcbuild-$pkgver/fpcsrc/compiler"
  fpcmake -Tall
  cd ../..
  make NOGDB=1 build OS_TARGET=wince CPU_TARGET=arm
}

package() {
  cd "${srcdir}/fpcbuild-$pkgver"
  make NOGDB=1 PREFIX="${pkgdir}/usr" crossinstall OS_TARGET=wince CPU_TARGET=arm
  rm -r "${pkgdir}/usr/bin"
  rm "$pkgdir/usr/lib/fpc/$pkgver/ppcrossarm"
  
  find $pkgdir -name '*.o' -o -name '*.a' | xargs -rtl1 arm-wince-strip -g
  
  cat << 'EOM'
  ==> PLEASE NOTE:
  ==> Please be sure to copy fpcbuild-x.x.x.tar.gz to the extracted package folders
  ==> of any RTLs or other FPC cross compilers you intend to build, as Free Pascal's 
  ==> FTP server is very low bandwidth. Thanks!    
EOM
}