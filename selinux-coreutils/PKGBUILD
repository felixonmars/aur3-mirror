# $Id: PKGBUILD 140429 2011-10-14 06:04:28Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Nicky726 <Nicky726@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=selinux-coreutils
_origname=coreutils
pkgver=8.14
pkgrel=1
pkgdesc="SELinux aware basic file, shell and text manipulation utilities of the GNU operating system"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('selinux' 'selinux-system-utilities')
depends=('glibc' 'selinux-shadow' 'selinux-pam' 'acl' 'gmp' 'libcap' 'selinux-usr-libselinux')
conflicts=("${_origname}")
replaces=('mktemp')
provides=("${_origname}=${pkgver}-${pkgrel}")
backup=('etc/pam.d/su')
install=${_origname}.install
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$_origname/$_origname-$pkgver.tar.xz{,.sig}
	coreutils-uname.patch
	coreutils-pam.patch
	coreutils-8.14-datetime.patch
	su.pam)
md5sums=('bcb135ce553493a45aba01b39eb3920a'
         '279712f9afc954beaff0d99194c8f462'
         'c4fcca138b6abf6d443d48a6f0cd8833'
         'aad79a2aa6d566c375d7bdd1b0767278'
         '70bf5b87c0df78189880782c1135a00c'
         'fa85e5cce5d723275b14365ba71a8aad')

build() {
  cd ${srcdir}/${_origname}-${pkgver}

  # added su wheel group pam patch (from fedora git)
  patch -Np1 -i ${srcdir}/coreutils-pam.patch

  # linux specific uname improvement (from gentoo portage)
  patch -Np1 -i ${srcdir}/coreutils-uname.patch

  # fixing failing datetime test (thanx to chenxiaolong)
  patch -Np1 -i ${srcdir}/coreutils-8.14-datetime.patch

  autoreconf -v
  ./configure --prefix=/usr --libexecdir=/usr/lib/coreutils \
              --enable-install-program=su \
              --enable-no-install-program=groups,hostname,kill,uptime \
              --enable-pam
  make
}

check() {
  cd ${srcdir}/${_origname}-${pkgver}
  make RUN_EXPENSIVE_TESTS=yes check
}

package() {
  cd ${srcdir}/${_origname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  cd ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/{bin,usr/sbin}
  
  # binaries required by FHS
  _fhs="cat chgrp chmod chown cp date dd df echo false ln ls \
        mkdir mknod mv pwd rm rmdir stty su sync true uname"
  mv ${_fhs} ${pkgdir}/bin

  # binaries required by various Arch scripts
  _bin="cut dir dircolors du install mkfifo readlink shred \
        sleep touch tr vdir"
  mv ${_bin} ${pkgdir}/bin
  ln -sf /bin/sleep ${pkgdir}/usr/bin/sleep

  mv chroot ${pkgdir}/usr/sbin
  install -Dm644 ${srcdir}/su.pam ${pkgdir}/etc/pam.d/su
}
