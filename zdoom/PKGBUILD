# Contributor: Jan Cholasta <grubber@grubber.cz>
# Maintainer: Jan Cholasta <grubber@grubber.cz>

pkgname=zdoom
pkgver=2.5.0
pkgrel=2
pkgdesc="An enhanced Doom port with additional support for Heretic, Hexen and Strife."
arch=('i686' 'x86_64')
url="http://www.zdoom.org/"
license=('custom')
depends=('bzip2' 'gtk2' 'sdl')
makedepends=('nasm' 'cmake' 'p7zip')
optdepends=('doom1-wad: Doom shareware IWAD (game data)'
            'harmony-wad: Harmony IWAD (game data)'
            'heretic1-wad: Heretic shareware IWAD (game data)'
            'hexen1-wad: Hexen 1 Demo IWAD (game data)'
            'strife0-wad: Strife shareware IWAD (game data)'
            'urbanbrawl-wad: Urban Brawl: Action Doom 2 IWAD (game data)')
source=(http://zdoom.org/files/zdoom/2.5/zdoom-${pkgver}-src.7z \
        http://www.fmod.org/index.php/release/version/fmodapi42816linux.tar.gz \
        ${pkgname}-${pkgver}-sharedir.patch \
        zdoom.desktop \
        zdoom.png)
noextract=(zdoom-${pkgver}-src.7z)
md5sums=('26afe95fb9fd28d91662563674bbb86e'
         'e6d1a9c8565051a983af7bcc5c4d9cfb'
         '967d8639f90678f13e1e2886b2c8d7e6'
         '83e47fdae2768da78cd4ac151ec92ad1'
         '7e1518eeda9bab34b7222a04c690697d')

_fmodver=4.28.16
_fmod64=

if [ "$CARCH" = "x86_64" ]; then
  makedepends[0]='yasm'
  source[1]=http://www.fmod.org/index.php/release/version/fmodapi42816linux64.tar.gz
  md5sums[1]='b6953f9331c72f9dfa95b31a41aa77b2'
  _fmod64=64
fi

build() {
  msg2 "Extracting ${pkgname}-${pkgver}-src.7z with 7z"
  7z x -y -o"$srcdir"/${pkgname}-${pkgver} "$srcdir"/${pkgname}-${pkgver}-src.7z &> /dev/null

  cd "$srcdir"/${pkgname}-${pkgver}

  patch -p1 < ../${pkgname}-${pkgver}-sharedir.patch || return 1

  cp ../fmodapi${_fmodver//./}linux${_fmod64}/api/lib/libfmodex${_fmod64}-${_fmodver}.so libfmodex-${pkgname}.so || return 1

  cmake -DFMOD_INCLUDE_DIR=../fmodapi${_fmodver//./}linux${_fmod64}/api/inc -DFMOD_LIBRARY=libfmodex-${pkgname}.so -DCMAKE_SKIP_RPATH=TRUE . || return 1
  make || return 1
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  install -m755 -D zdoom "$pkgdir"/usr/bin/zdoom
  install -m644 -D zdoom.pk3 "$pkgdir"/usr/share/games/zdoom/zdoom.pk3
  install -m644 -D docs/BUILDLIC.TXT "$pkgdir"/usr/share/licenses/${pkgname}/BUILDLIC.TXT
  install -m644 -D docs/doomlic.txt "$pkgdir"/usr/share/licenses/${pkgname}/doomlic.txt

  install -m755 -D libfmodex-${pkgname}.so "$pkgdir"/usr/lib/libfmodex-${pkgname}.so

  install -m644 -D ../zdoom.png "$pkgdir"/usr/share/pixmaps/zdoom.png
  install -m644 -D ../zdoom.desktop "$pkgdir"/usr/share/applications/zdoom.desktop
}
