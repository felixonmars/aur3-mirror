#Maintainer: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor: Steven She <mintcoffee@gmail.com>
# Contributor: vbPadre <vbPadre@gmail.com>
pkgname=cndrvcups-lb
pkgver=2.40
pkgrel=1
pkgdesc="Canon UFR II / LIPSLX Printer Driver for Linux with support for LBP/iR/MF printers"
arch=('x86_64')
url="http://support-au.canon.com.au/contents/AU/EN/0100270808.html"
license=('custom')
install=${pkgname}.install
_MachineType=`uname -m`
if [[ ${_MachineType} == i686 ]]; then
  echo "Package is x86_64 only for now, aborting makepkg"
  return 1
else
  depends=('lib32-libxml2' 'cndrvcups-common=2.40')
fi
makedepends=('autoconf' 'automake')
source=('Linux_UFRII_PrinterDriver_V240_uk_EN.tar.gz::http://pdisp01.c-wss.com/gdl/WWUFORedirectTarget.do?id=MDEwMDAwMjcwODA1&cmp=ABS&lang=EN'
        'how-to.txt')
options=('!emptydirs' '!strip')
md5sums=('ee79eb933763f981e584bc0098ad44d7'
         'd0d2c2a04f49195181cbb8a5e564b94d')

build() {
    cd $srcdir/Linux_UFRII_PrinterDriver_V240_uk_EN/Sources
    tar xf $pkgname-${pkgver}-2.tar.gz
    #prepare build directory
    rm -rf $pkgname-build
    cp -r "$pkgname-$pkgver" "$srcdir/$pkgname-build"
    cd "$srcdir/$pkgname-build"
    
    cd ppd
    ./autogen.sh --prefix=/usr
    cd ../pstoufr2cpca
    ./autogen.sh --prefix=/usr --libdir=/usr/lib
    cd ../backend
    ./autogen.sh --prefix=/usr --libdir=/usr/lib
    cd ../cpca
    ./autogen.sh --prefix=/usr --enable-progpath=/usr/bin --libdir=/usr/lib --disable-static
    cd ../cngplp
    ./autogen.sh --prefix=/usr --libdir=/usr/lib
    cd files
    ./autogen.sh --prefix=/usr
    cd ..
    make
}

package() {
  
    cd "${srcdir}/${pkgname}-build"
    mkdir -p ${pkgdir}/usr/{bin,include,lib32,lib}
    mkdir -p ${pkgdir}/usr/lib/cups/{filter,backend}
    mkdir -p ${pkgdir}/usr/share/{cups/model,caepcm,cngplp,cnpkbidi,ufr2filter}
    cd "${srcdir}/${pkgname}-build"
    make install DESTDIR=${pkgdir}

install -m 4755 libs/cnpkmoduleufr2  ${pkgdir}/usr/bin

install -m 755 libs/libcanonufr2.la  ${pkgdir}/usr/lib32
install -s -m 755 libs/libcanonufr2.so.1.0.0  ${pkgdir}/usr/lib32
install -s -m 755 libs/libufr2filter.so.1.0.0   ${pkgdir}/usr/lib32
install -s -m 755 libs/libEnoJBIG.so.1.0.0   ${pkgdir}/usr/lib32
install -s -m 755 libs/cnpkbidi   ${pkgdir}/usr/bin
install -s -m 755 libs/libcaiocnpkbidi.so.1.0.0   ${pkgdir}/usr/lib32

install -m 644 data/CNC* ${pkgdir}/usr/share/caepcm
install -m 644 data/CNL6*  ${pkgdir}/usr/share/caepcm
install -m 644 data/CNL8*  ${pkgdir}/usr/share/caepcm
install -m 644 data/CNL9*  ${pkgdir}/usr/share/caepcm
install -m 644 data/CNLC*  ${pkgdir}/usr/share/caepcm
install -m 644 data/CNLA*  ${pkgdir}/usr/share/caepcm
install -m 644 data/CNLB*  ${pkgdir}/usr/share/caepcm
install -m 644 data/CNLD*  ${pkgdir}/usr/share/caepcm
install -m 644 data/CnLB*  ${pkgdir}/usr/share/caepcm
install -m 644 libs/cnpkbidi_info* ${pkgdir}/usr/share/cnpkbidi
install -m 644 libs/ThLB*  ${pkgdir}/usr/share/ufr2filter

install -m 755 libs/libcnlbcm.so.1.0     ${pkgdir}/usr/lib32

cd ${pkgdir}/usr/lib32
ln -sf libcanonufr2.so.1.0.0 libcanonufr2.so
ln -sf libcanonufr2.so.1.0.0 libcanonufr2.so.1
ln -sf libufr2filter.so.1.0.0 libufr2filter.so
ln -sf libufr2filter.so.1.0.0 libufr2filter.so.1
ln -sf libEnoJBIG.so.1.0.0 libEnoJBIG.so
ln -sf libEnoJBIG.so.1.0.0 libEnoJBIG.so.1
ln -sf libcaiocnpkbidi.so.1.0.0 libcaiocnpkbidi.so
ln -sf libcaiocnpkbidi.so.1.0.0 libcaiocnpkbidi.so.1
ln -sf libcnlbcm.so.1.0     libcnlbcm.so.1
ln -sf libcnlbcm.so.1.0     libcnlbcm.so
cd -

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 L*-ufr2-2.4* "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -m644 LICENSE-lipslx-2.40.txt "${pkgdir}/usr/share/licenses/${pkgname}/"
}
