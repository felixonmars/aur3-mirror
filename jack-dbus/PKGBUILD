#Maintainer: Daniele Paolella <danielepaolella@email.it>
#Contributor: YacK <brayan@yack.com.br>
pkgname=jack-dbus
pkgver=0.121.3
pkgrel=1
pkgdesc="A low-latency audio server, with D-Bus interface"
arch=('i686' 'x86_64')
url="http://nedko.arnaudov.name/soft/jack/dbus/"
license=('GPL' 'LGPL')
depends=('libsamplerate' 'dbus-core')
optdepends=('libffado: FireWire support'
            'celt-0.7: jack_netsource'
            'python2: jack_control')
provides=("jack=$pkgver" "jack-audio-connection-kit=$pkgver" 'jackdbus')
conflicts=('jack')
replaces=('jack-audio-connection-kit-dbus')
options=(!libtool)
changelog=$pkgname.changelog
source=(http://nedko.arnaudov.name/soft/jack/dbus/jack-audio-connection-kit-dbus-$pkgver.tar.gz)
md5sums=('a7b2867b4021fa1dd86f7ba67245311d')

build() {
  cd "$srcdir/jack-audio-connection-kit-$pkgver"

  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd "$srcdir/jack-audio-connection-kit-$pkgver"

  make DESTDIR="$pkgdir/" install

  sed -i 's:bin/env python:bin/env python2:' \
    "$pkgdir/usr/bin/jack_control" 
}

# vim:set ts=2 sw=2 et:
