# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>

pkgname=coq
pkgver=8.4
pkgrel=1
pkgdesc='Formal proof management system.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
options=('!emptydirs')
depends=('gtk2' 'ocaml')
makedepends=('lablgtk2' 'camlp5-transitional' 'netpbm' 'hevea' 'texlive-latexextra' 'texlive-pictures')
source=("http://coq.inria.fr/distrib/V$pkgver/files/coq-$pkgver.tar.gz")
md5sums=('f28662cd687f66ed3c372ca3d35ea928')

build() {
  cd coq-$pkgver

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -configdir '/etc/xdg/coq/' \
    -opt \
    -with-doc yes

  make doc

  make
}

package() {
  cd coq-$pkgver

  make COQINSTALLPREFIX="$pkgdir" install

  make COQINSTALLPREFIX="$pkgdir" install-doc
}
