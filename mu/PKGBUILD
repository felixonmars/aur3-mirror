# Maintainer: Tom Willemsen <tom at ryuslash dot org>
# Contributor: Uwe <moc liamg skcuneelu <-- look what I did there>
# Contributor: Nicolas Pouillard <nicolas dot pouillard at gmail dot com>
pkgname=mu
pkgver=0.9.8.3
pkgrel=1
pkgdesc="collection of utilities for indexing and searching Maildirs"
arch=('i686' 'x86_64')
url="http://www.djcbsoftware.nl/code/mu"
install=${pkgname}.install
license=('GPL3')
depends=('xapian-core' 'gmime' 'sqlite3' 'gtk2')
optdepends=('cronie: updating index regularly')
source=("http://mu0.googlecode.com/files/mu-${pkgver}.tar.gz")
md5sums=('cc072c14e54062c83809c1d2403e48f7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
      "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
