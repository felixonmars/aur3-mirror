# Maintainer: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/
pkgname="spacefm-git"
pkgver=20120414
pkgrel=1
pkgdesc="A multi-panel tabbed file manager - git next branch"
arch=('i686' 'x86_64')
url=("http://ignorantguru.github.com/spacefm/")
license=('GPL2')

makedepends=('intltool' 'gettext')
depends=('gtk2' 'shared-mime-info' 'desktop-file-utils' 'startup-notification' 'udev')
optdepends=('lsof: device processes'
            'eject: eject media'
            'wget: plugin download'
            'ktsuss: perform as root functionality'
            'gksu: perform as root functionality'
            'pmount: mount as non-root user'
            'udisks: mount as non-root user'
            'udisks2: mount as non-root user')
conflicts=('spacefm')
provides=('spacefm')
install="spacefm.install"
_gitroot="git://github.com/IgnorantGuru/spacefm.git"
_gitname="spacefm"

# Set USE_GDB=1 to build for gdb debugging, otherwise set USE_GDB=0
USE_GDB=1

if (( USE_GDB == 1 )); then
    options=('!strip')
fi

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin next
    msg "The local files are updated."
  else
    git clone -b next $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

    # NOTE: To enable hal support add --enable-hal to
    # configure line below. See README for details.
    # NOTE: To disable inotify and use fam or gamin instead, add:
    # --disable-inotify to configure line below.
    # NOTE: To add a custom su program (mysu in this example), add:
    # --with-preferable-sudo="mysu"  to configure line below.

    if (( USE_GDB == 1 )); then
        CFLAGS+="-g" ./configure --prefix=/usr
        echo
        CFLAGS+="-g" make
    else
        ./configure --prefix=/usr
        echo
        make
    fi
    echo
}

package() {
    cd "${srcdir}/$_gitname-build"
    echo
    make DESTDIR="${pkgdir}/" install
    echo
}

