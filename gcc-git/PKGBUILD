# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-git
pkgver=4.9.124128.885cf45
pkgrel=1
pkgdesc="The GNU Compiler Collection developmental snapshot"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL' 'custom')
url="http://gcc.gnu.org"
depends=('glibc' 'binutils' 'gmp' 'mpfr' 'libmpc' 'isl' 'cloog')
makedepends=('flex' 'bison' 'git')
checkdepends=('dejagnu')
options=('!libtool' '!strip')
source=('git://gcc.gnu.org/git/gcc.git')
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/gcc

  _ver=$(cat ${srcdir}/gcc/gcc/BASE-VER)
  _ver=${_ver%.*}
  
  echo ${_ver}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd ${srcdir}/gcc

  # Do not run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # Arch Linux installs x86_64 libraries /lib
  [[ $CARCH == "x86_64" ]] && sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  # XXX - avoid issues with docs built with texinfo-5.0 and old revisions
  #sed -i 's/BUILD_INFO=info/BUILD_INFO=/' gcc/configure

  mkdir ${srcdir}/gcc-build
}

build() {
  cd ${srcdir}/gcc-build
  
  _ver=$(cat ${srcdir}/gcc/gcc/BASE-VER)
  _ver=${_ver%.*}

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}

  # XXX - avoid unstoppable -Werror in some old revisions
  #unset CPPFLAGS

  # add ada,fortran,go,objc,obj-c++ as wanted
  # additional languages not tested...
  _languages="c,c++,lto"

  ${srcdir}/gcc/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --datadir=/usr/share/gcc-${_ver} \
      --with-bugurl=http://aur.archlinux.org/packages.php?ID=16045 \
      --enable-languages=${_languages} \
      --enable-shared --enable-threads=posix \
      --with-system-zlib --enable-__cxa_atexit \
      --disable-libunwind-exceptions --enable-clocale=gnu \
      --disable-libstdcxx-pch \
      --enable-gnu-unique-object --enable-linker-build-id \
      --enable-cloog-backend=isl --disable-cloog-version-check \
      --enable-lto --enable-gold --enable-ld=default \
      --enable-plugin --with-plugin-ld=ld.gold \
      --with-linker-hash-style=gnu --disable-install-libiberty \
      --disable-multilib --disable-libssp --disable-werror \
      --enable-checking=release \
      --program-suffix=-${_ver} --enable-version-specific-runtime-libs
  make
}

check() {
  cd gcc-build

  # increase stack size to prevent test failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=31827
  ulimit -s 32768

  # do not abort on error as some are "expected"
  make -k check || true
  ${srcdir}/gcc/contrib/test_summary
}

package() {
  cd ${srcdir}/gcc-build

  _ver=$(cat ${srcdir}/gcc/gcc/BASE-VER)
  _ver=${_ver%.*}

  make DESTDIR=${pkgdir} install

  rm $pkgdir/usr/lib/gcc/$CHOST/*/libstdc++.so.*-gdb.py

  # XXX - disable-install-libiberty does not work in some old revisions
  #rm $pkgdir/usr/lib/libiberty.a

  # deal with conflicting man and info pages
  rm ${pkgdir}/usr/share/man/man7/{fsf-funding,gfdl,gpl}.7
  for i in $pkgdir/usr/share/info/*; do
    mv $i ${i//.info}-${_ver}.info
  done

  install -Dm644 ${srcdir}/gcc/COPYING.RUNTIME \
    ${pkgdir}/usr/share/licenses/$pkgname/RUNTIME.LIBRARY.EXCEPTION
}
