# Maintainer: graysky <graysky AT archlnux.us>
# Contributor: A.J. Korf <jacobkorf at gmail dot com>
# Contrubutor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-ck
pkgver=275.09.07
_kernver='2.6.39-ck'
pkgrel=2
_pkgdesc="NVIDIA drivers for kernel26-ck."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-ck>=2.6.39' 'kernel26-ck<2.6.40' "nvidia-utils=$pkgver")
makedepends=('kernel26-ck-headers>=2.6.39' 'kernel26-ck-headers<2.6.40')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-ck-stable' 'nvidia-beta-ck')
#groups=('ck-generic')
license=('custom')
options=(!strip)
install=nvidia.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-$_arch-$pkgver"
	source=("ftp://download.nvidia.com/XFree86/Linux-$_arch/$pkgver/$_pkg.run")
	sha256sums=('27d1b2073c11bd56e423ed80bd22773f1f282c39ab3b76773144cfa680a1ff0c')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-$_arch-$pkgver-no-compat32"
	source=("ftp://download.nvidia.com/XFree86/Linux-$_arch/$pkgver/$_pkg.run")
	sha256sums=('69a80fb99e740efe48830ba631ea621ca4f51f4ba3212448820ff878c8f946cc')
fi

build() {
	cd "$srcdir"
	sh $_pkg.run --extract-only
	cd $_pkg/kernel
 	make SYSSRC=/lib/modules/$_kernver/build module
}

package() {
	install -D -m644 "$srcdir"/$_pkg/kernel/nvidia.ko \
		"$pkgdir"/lib/modules/$_kernver/kernel/drivers/video/nvidia.ko
        install -d -m755 "$pkgdir"/etc/modprobe.d
        echo "blacklist nouveau" >> "$pkgdir"/etc/modprobe.d/nouveau_blacklist_ck.conf
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='$_kernver'/" $startdir/nvidia.install
        # gzip -9 all modules to save 100MB of space
        find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
