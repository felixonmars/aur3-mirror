# Maintainer: graysky <graysky AT archlnux.us>
# Contributor: A.J. Korf <jacobkorf at gmail dot com>
# Contrubutor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-ck
pkgver=310.19
_extramodules=extramodules-3.6-ck
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=4
_pkgdesc="NVIDIA drivers for linux-ck."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-ck>=3.6' 'linux-ck<3.7' "nvidia-utils=${pkgver}")
makedepends=('linux-ck-headers>=3.6' 'linux-ck-headers<3.7')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-275xx-ck' 'nvidia-ck-stable' 'nvidia-beta-ck' 'nvidia-ck-atom' 'nvidia-ck-corex' 'nvidia-ck-kx' 'nvidia-ck-p4' 'nvidia-ck-pentm' 'nvidia-304xx-ck' 'nvidia-304xx-ck-atom' 'nvidia-304xx-ck-corex' 'nvidia-304xx-ck-kx' 'nvidia-304xx-ck-p4' 'nvidia-304xx-ck-pentm')
#groups=('ck-generic')
#replaces=()
license=('custom')
options=(!strip)
install=nvidia-ck.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('2adbdd38540b3a8955714760e05f575d')	
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('0ba08d32852e442ebba5ba22c7abed36')
fi

build() {
	cd "${srcdir}"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-ck.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-ck.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
