# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=librdmacm
pkgver=1.0.17
pkgrel=1
pkgdesc="Userspace RDMA Connection Manager"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibverbs')
optdepends=('ibacm: assistant communication manager')
source=("http://www.openfabrics.org/downloads/rdmacm/${pkgname}-${pkgver}.tar.gz")
# cksum start
md5sums=('da6fad887e9c24cb01b74b75f8449cb1')
sha512sums=('4cda346dfc32eca9b98ee7b3d85a29a00076daa5a40f93470d5ba0ef5d418f584980f9758e616f16a69a2f3548c1f600b1ab6ea69a95a17b1ccd668657c2de58')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}