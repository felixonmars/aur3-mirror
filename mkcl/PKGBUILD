# Maintainer: DavidK <david_king at softhome dot net>
# Contributor: Alexej Magura <agm2819*gmail*>
# 
# 
pkgname=mkcl
pkgver=1.1.8
pkgrel=1
pkgdesc="ManKai Common Lisp"
arch=('i686' 'x86_64')
url="http://common-lisp.net/project/mkcl/"
license=('LGPL3' 'custom')
depends=('gmp' 'gawk')
provides=('common-lisp' 'cl-asdf')
source=("http://common-lisp.net/project/mkcl/releases/mkcl-${pkgver}.tar.gz")
md5sums=('b48d0744e9620fda57d63d6d3b594d0d')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make prefix="$pkgdir/usr" install
    install -m 644 -D "Copyright" "$pkgdir/usr/share/licenses/$pkgname/Copyright"
    install -m 644 -D src/doc/mkcl.man "$pkgdir/usr/share/man/man1/mkcl.1"
    install -m 644 -D src/doc/mkcl-config.man "$pkgdir/usr/share/man/man1/mkcl-config.1"
    cd "$pkgdir/usr/lib/$pkgname-$pkgver"
    chmod 644 lsp.a
    chmod 644 mkclgc.a
    chmod 644 mkclmin.a
    chmod 644 mkcltop.a
}
