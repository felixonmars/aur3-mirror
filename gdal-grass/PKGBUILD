# $Id: PKGBUILD 2741 2009-09-21 23:46:46Z ibiru $
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.org>
# Maintainer: Emiliano Vavassori <syntaxerrormmm(at)gmail.com>

pkgname=gdal-grass
pkgver=1.4.3
pkgrel=4
pkgdesc="Standalone drivers for GRASS raster and vector files"
arch=('i686' 'x86_64')
url="http://www.gdal.org/"
license=('GPL')
depends=('gdal' 'grass')
makedepends=('fftw')
source=(http://download.osgeo.org/gdal/${pkgname}-${pkgver}.tar.gz
	'gdal-grass-destdir.patch')
md5sums=('2bfb7640e03b4d9d012f1a5d62c0351d'
         '78f74b862040e47f2c94279692d5cba6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr  --with-gdal=/usr/bin/gdal-config --with-grass=/opt/grass
  patch -Np 0 -i "${srcdir}/gdal-grass-destdir.patch"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}/usr/share/gdal/grass/etc"
  install -d "${pkgdir}/usr/share/gdal/grass/driver/db"
  make DESTDIR="${pkgdir}" install
}
