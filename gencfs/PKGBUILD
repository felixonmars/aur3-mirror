# Maintainer: Raphael Scholer <rscholer@gmx.de>
# Contributor: Andre Klitzing <andre () incubo () de>
pkgname=gencfs
pkgver=1.1
pkgrel=3
pkgdesc="EncFS GUI for encrypted directory mounting"
arch=("i686" "x86_64")
url="http://sallu.tuxfamily.org"
license=("custom")
depends=("encfs" "gtk2")
makedepends=("cmake")
source=("http://sallu.tuxfamily.org/IMG/bz2/${pkgname}-${pkgver//./-}-tar.bz2"
        "license")
md5sums=('1cedb4f74808394ca0488006fda52995'
         '2d740db2d9a00ce8e4d54d3ea8789b5a')

build() {
  cd ${srcdir}
  cmake -D CMAKE_INSTALL_PREFIX=/usr "${pkgname}-${pkgver}"
  make
}

package() {
  cd ${srcdir}
  make DESTDIR="${pkgdir}" install
  install -m 644 -D "license" "${pkgdir}/usr/share/licenses/${pkgname}/license"

  cd "${pkgdir}/usr"
  rm -r "doc"
}

# vim:set ts=2 sw=2 et:
