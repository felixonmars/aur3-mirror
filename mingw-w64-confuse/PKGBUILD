# Maintainer: Julien Schueller <julien.schueller@gmail.com>

pkgname=mingw-w64-confuse
pkgver=2.7
pkgrel=1
pkgdesc="C-library for parsing configuration files (mingw-w64)"
arch=(any)
url="http://www.nongnu.org/confuse"
license=('LGPL')
depends=('mingw-w64-crt')
source=("http://savannah.nongnu.org/download/confuse/confuse-${pkgver}.tar.gz")
options=('!libtool' '!emptydirs' '!buildflags' '!strip')
md5sums=('45932fdeeccbb9ef4228f1c1a25e9c8f')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/confuse-${pkgver}"
  for _arch in ${_architectures}; do
    unset LDFLAGS CPPFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --enable-shared \
                 --disable-examples
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/confuse-${pkgver}/build-${_arch}
    make install DESTDIR=${pkgdir}
#     ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a    
  done
}
