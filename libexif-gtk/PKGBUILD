# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>

pkgname=libexif-gtk
pkgver=0.3.5
pkgrel=9
pkgdesc="GTK frontend to the libexif library"
arch=('i686' 'x86_64')
url="http://libexif.sourceforge.net"
license=('GPL')
depends=('libexif' 'gtk2')
options=('!libtool')
source=(http://downloads.sourceforge.net/libexif/$pkgname-$pkgver.tar.bz2
	'gtk2-2.12.patch')
md5sums=('0ecdba41f3e0f20a11b8555bd2dd2a07'
         'f8d53413c6291a0dca5da99016b1bf47')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # Patch for gtk2-2.12
  patch -p1 -i $srcdir/gtk2-2.12.patch
  # Patch for libexif
  sed -i 's|exif-mem.h|libexif/exif-mem.h|g' configure
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
