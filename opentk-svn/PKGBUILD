pkgname=opentk-svn
pkgver=3125
pkgrel=2
pkgdesc="An advanced, cross-platform, C-sharp OpenGL, OpenAL and OpenCL wrapper for Mono/.Net (svn)"
arch=(any)
license=("MIT")
depends=(mono)
makedepends=(mono subversion)
options=(!strip)
url="http://www.opentk.com"
conflicts=(opentk)
provides=(opentk)
_svntrunk="https://opentk.svn.sourceforge.net/svnroot/opentk/trunk"
_svnmod="opentk"

build() {
  cd "${srcdir}"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver $_svnmod
  fi

  msg 'SVN checkout done or server timeout'

  rm -rf "${_svnmod}-build"
  cp -r "$_svnmod" "${_svnmod}-build"
  cd "${_svnmod}-build"

  xbuild OpenTK.sln
}

package() {
  cd "${srcdir}/${_svnmod}-build/Binaries/OpenTK/Debug/"
  find . -name '*.config' -o -name '*.mdb' -o -name '*.dll' -o -name '*.xml' |
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/opentk/"{}
  cp -r Data "$pkgdir/usr/lib/opentk/"
  rm "$pkgdir/usr/lib/opentk/"{Bind,Build.UpdateVersion,Convert}.exe.mdb
  install -m755 Examples.exe "$pkgdir/usr/lib/opentk/"
  install -Dm644 "$srcdir/${_svnmod}-build/Documentation/License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"
  find . -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
}
