# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Maintainer : Trevor Turner <turn3r.tr3v0r at gmail dot com>

pkgname=virtualbox-modules-uksm
pkgver=4.3.6
pkgrel=6
pkgdesc='Host linux-uksm kernel modules for VirtualBox'
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
depends=("linux-uksm=3.13.5-1")
makedepends=("linux-uksm-headers=3.13.5-1"
             "virtualbox-host-dkms=$pkgver")
provides=("virtualbox-host-modules=$pkgver")
conflicts=("virtualbox>$pkgver" "virtualbox<$pkgver")
install=virtualbox-modules-uksm.install

_extramodules=extramodules-3.13-uksm
_kernver="3.13.5-1-uksm"

build() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
}

package(){
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
