# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=kmscon-git
pkgver=20121206
pkgrel=1
pkgdesc="A simple terminal emulator based on linux kernel mode setting (KMS)."
arch=(i686 x86_64)
url="https://github.com/dvdhrm/kmscon"
license=('MIT')
depends=('libdrm' 'mesa' 'libgbm' 'libgles' 'udev' 'pango' 'libxkbcommon-git')
makedepends=('git' 'xproto' 'gtk-doc')
provides=(kmscon)
conflicts=(kmscon)

_gitroot=https://github.com/dvdhrm/kmscon.git
_gitname=kmscon

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  ./configure --prefix=/usr --disable-wlterm
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 docs/kmscon.service "$pkgdir/usr/lib/systemd/system/kmscon.service"
  install -Dm644 docs/kmscon@.service "$pkgdir/usr/lib/systemd/system/kmscon@.service"
}
