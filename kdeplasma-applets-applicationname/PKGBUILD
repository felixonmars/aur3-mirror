# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-applets-applicationname
pkgver=1.7
pkgrel=1
pkgdesc="A QML plasmoid to display the application name of the focused window"
arch=('any')
url="https://github.com/ascarpino/applicationname-plasmoid"
license=('GPL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
replaces=('kdeplasma-applets-windowtitle')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/ascarpino/applicationname-plasmoid/tarball/${pkgver}")
md5sums=('48aae9c14ad03d3247b109d84831694d')

build() {
  mkdir build
  cd build
  cmake ../ascarpino-applicationname-plasmoid-18437d0 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
