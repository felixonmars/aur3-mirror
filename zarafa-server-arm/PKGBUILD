# Maintainer: MartiMcFly martimcfly@autorisation.de
# Contributor: C Anthony Risinger

pkgname=zarafa-server-arm
pkgver=7.1.7
pkgrel=42779
pkgdesc="Open Source Groupware Solution"
arch=('any')
url="http://www.zarafa.com/"
license=('AGPL3')
depends=('mysql'
         'curl'
         'libxml2'
         'openssl'
         'openldap'
         'krb5'
	 'boost>=1.52.0'
	 'bison'
	 'python2'
	 'swig'
	 'kyotocabinet'
	 'zarafa-libical'
         'zarafa-libvmime=0.9.2')
makedepends=('php>=5.2.0'
             'e2fsprogs'
             'openldap')
optdepends=('zarafa-webaccess-ajax: AJAX based web-interface')
backup=('etc/zarafa/server.cfg'
	'etc/zarafa/ical.cfg')
install=${pkgname}.install
source=("zarafa-${pkgver}.tar.gz::http://download.zarafa.com/community/final/7.1/${pkgver}-${pkgrel}/sourcecode/zcp-${pkgver}.tar.gz"
        "rc.zarafa-ical"
        "rc.zarafa-server"
	"swig.patch"
	"arm.patch"
	"zarafa-server.service"
	"zarafa-ical.service"
	"zarafa-dagent.service"
	"zarafa-spooler.service"
	"zarafa-gateway.service"
	"mariadb.patch")
noextract=()
md5sums=('7a47d6cfd35c6d2963a10b1920f08577'
         '44d98735e4806e0ed1b8e62931d42eee'
         '4a2337e721e6579d012243f990a06252'
         'fe0c5afaba65cdd9f43a552552add2b4'
         'ce5e4808ed1d30435f4825ea1a8351fa'
         '0e2728f1e35b25ca679427fcb57315d8'
         'c25f3982217fe390d68ed9003a5988ed'
         '9666bf713645af11dd65b3ac5cbb42d9'
         '846b76e5cb0239a488c81e11d74ad08b'
         '705ada3a8c4b904696e0c461c131b4f7'
         '1241edb4eac987b81a0700447eea1781')
build() {
  cd ${srcdir}/zarafa-${pkgver}

  # Patch
  patch -p0 <${srcdir}/swig.patch
  
  # http://gentoo-overlays.zugaina.org/betagarden/portage/net-mail/zarafa/
  patch -p1 <${srcdir}/mariadb.patch
  
  # https://forums.zarafa.com/showthread.php?8651-Raspberry-P
  patch -p1 <${srcdir}/arm.patch

  msg "Starting build..."
  CPPFLAGS=-I/usr/include/python2.7 ./configure --prefix=/usr \
    --enable-oss \
    --enable-release \
    --enable-python \
    --with-python=/usr/bin/python2 \
    --disable-debug \
    --disable-testtools \
    --disable-static \
    --with-userscript-prefix=/etc/zarafa/userscripts \
    --with-quotatemplate-prefix=/etc/zarafa/quotamails \
    --with-sysconfdir=/etc

  # make/g++ doesnt pick up -luuid properly without this... even though configure
  # finds it, and -luuid is present in the actual command that fails...
  make LDFLAGS="-luuid" || return 1
}


package() {
  cd ${srcdir}/zarafa-${pkgver}

  make DESTDIR=${pkgdir} install || return 1
  # This isn't really a PEAR package... but Arch's default PHP config has open_basedir restrictions
  # and I doubt there will ever be a MAPI PEAR package to conflict, so one less thing user will have to do
  mv ${pkgdir}/usr/share/php ${pkgdir}/usr/share/pear

  # Finalize
  install -m 755 -o root -g root -d ${pkgdir}/var/log/zarafa
  install -m 755 -o root -g root -d ${pkgdir}/var/lib/zarafa
  install -m 755 -o root -g root -d ${pkgdir}/var/lib/zarafa-webaccess

  install -m 755 -o root -g root -D ${srcdir}/rc.zarafa-ical ${pkgdir}/etc/rc.d/zarafa-ical
  install -m 755 -o root -g root -D ${srcdir}/rc.zarafa-server ${pkgdir}/etc/rc.d/zarafa-server

  mkdir -p  ${pkgdir}/etc/systemd/system
  install -m 755 $startdir/zarafa-server.service ${pkgdir}/etc/systemd/system
  install -m 755 $startdir/zarafa-ical.service ${pkgdir}/etc/systemd/system
  install -m 755 $startdir/zarafa-dagent.service ${pkgdir}/etc/systemd/system
  install -m 755 $startdir/zarafa-gateway.service ${pkgdir}/etc/systemd/system
  install -m 755 $startdir/zarafa-spooler.service ${pkgdir}/etc/systemd/system

  # copy example configs to their active locations
  for cfg in ${pkgdir}/usr/share/doc/zarafa/example-config/*.cfg; do
    install -m 755 -o root -g root -D ${cfg} ${pkgdir}/etc/zarafa
  done

  mkdir -p ${pkgdir}/etc/httpd/conf/extra/
  install -m 755 ${srcdir}/zarafa-${pkgver}/php-webclient-ajax/zarafa-webaccess.conf ${pkgdir}/etc/httpd/conf/extra/zarafa-webaccess.conf

  mkdir -p ${pkgdir}/usr/share/zarafa-webaccess/
  install -m 755 -d ${pkgdir}/usr/share/zarafa-webaccess/
  cp -r ${srcdir}/zarafa-${pkgver}/php-webclient-ajax/* ${pkgdir}/usr/share/zarafa-webaccess/
  mv ${pkgdir}/usr/share/zarafa-webaccess/config.php.dist ${pkgdir}/usr/share/zarafa-webaccess/config.php
  sed -i 's$/usr/share/php/$/usr/share/pear/$' ${pkgdir}/usr/share/zarafa-webaccess/config.php
}
