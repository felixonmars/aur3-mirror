# Maintainer: talavis <aur at talavis eu>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=seaview
pkgver=4.4
pkgrel=1
pkgdesc="A multiplatform GUI for multiple sequence alignment and molecular phylogeny."
arch=('i686' 'x86_64')
url="http://pbil.univ-lyon1.fr/software/seaview.html"
license=('custom')
depends=('shared-mime-info' 'fltk' 'libjpeg-turbo')
install='seaview.install'
source=('http://pbil.univ-lyon1.fr/software/seaview_data/seaview.tar.gz'
	'seaview.install'
        'seaview.desktop'
        'seaview.svg'
        'seaview.sharedmimeinfo'
        'copyright')
md5sums=('c304054697b6025d323af328bb29938a'
	 'fd6607168def7db9cf4775cacdbb9f02'
         '94f13509470dd69f053ffc178884e292'
         '70ceabd9ae2f398612948c66c564857e'
         '9592a71ee55fa6edfbaddbce2d5469b9'
         '6634a8269933e9fa93da6fc33878a575')

build() {
    cd $srcdir/seaview
    make
    gzip -f seaview.1
}

package() {

    install -d $pkgdir/usr/share/seaview
    install -d $pkgdir/usr/bin/
    install -D -m755 $srcdir/seaview/seaview $pkgdir/usr/bin/seaview
    install -D -m644 $srcdir/seaview/seaview.xpm $pkgdir/usr/share/pixmaps/seaview.xpm
    install -D -m644 $srcdir/seaview/seaview.html $pkgdir/usr/share/seaview/seaview.html
    install -D -m644 $srcdir/seaview/seaview.1.gz $pkgdir/usr/share/man/man1/seaview.1.gz
    
    install -D -m644 $srcdir/seaview.desktop $pkgdir/usr/share/applications/seaview.desktop
    install -D -m644 $srcdir/seaview.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/seaview.svg
    install -D -m644 $srcdir/seaview.sharedmimeinfo $pkgdir/usr/share/mime/packages/seaview.xml
    install -D -m644 $srcdir/copyright $pkgdir/usr/share/licenses/seaview/copyright
}

