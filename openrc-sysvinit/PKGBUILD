# Contributor: Eric Belanger <eric@archlinux.org>
# Maintainer: Andrew Gregory <andrew.gregory.8@gmail.com>

pkgname=openrc-sysvinit
pkgdesc="Linux System V Init patched to use alternate inittab for openrc"
depends=('sysvinit')
install=sysvinit.install
pkgver=2.88
pkgrel=8
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/sysvinit"
license=('GPL')
groups=('base')
source=(http://download.savannah.gnu.org/releases/sysvinit/sysvinit-${pkgver}dsf.tar.bz2
        "0001-simplify-writelog.patch"
        "0002-remove-ansi-escape-codes-from-log-file.patch"
        "openrc-init.patch")
sha1sums=('f2ca149df1314a91f3007cccd7a0aa47d990de26'
          '8ce9543f4ec31f3d3c66c45048f9a81e8907f5c2'
          '33e3fff0ca240896a3cd83ef9734c7bcf6ef6be7'
          'aaabc62ce14646c6fe21c27f60ee5c9933dfc0fb')

build() {
  cd "${srcdir}/sysvinit-${pkgver}dsf"

  # FS#30005
  patch -p1 -d "src" -i "${srcdir}/0001-simplify-writelog.patch"
  patch -p1 -d "src" -i "${srcdir}/0002-remove-ansi-escape-codes-from-log-file.patch"

  # use /etc/openrc/inittab instead of /etc/inittab
  patch -p1 -d "src" -i "${srcdir}/openrc-init.patch"

  make 
}

package() {
  cd "${srcdir}/sysvinit-${pkgver}dsf"
  install -d ${pkgdir}/sbin
  install -m 755 src/init ${pkgdir}/sbin/init-openrc
}
