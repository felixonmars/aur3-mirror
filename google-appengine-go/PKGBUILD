# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Richard Hoffman <coverslide@gmail.com>
# Contributor: Max <fuzymonkey@gmail.com>

pkgname=google-appengine-go
pkgver=1.7.1
pkgrel=1
pkgdesc="Google App Engine SDK for Go"
arch=('x86_64' 'i686')
depends=("python2>=2.7")
url="http://code.google.com/appengine/downloads.html#Google_App_Engine_SDK_for_Go"
license=('APACHE')
options=('!strip')
noextract=("go_appengine_sdk_linux_amd64-$pkgver.zip" "go_appengine_sdk_linux_386-$pkgver.zip")
makedepends=('unzip')

if [ $CARCH != "i686" ]; then
  source=("http://googleappengine.googlecode.com/files/go_appengine_sdk_linux_amd64-$pkgver.zip")
  sha256sums=('4b42bee9750e6d8eab0907e7124d4a0dfca152def35ebc05f5823216e9278691')
else
  source=("http://googleappengine.googlecode.com/files/go_appengine_sdk_linux_386-$pkgver.zip")
  sha256sums=('553a12b2fbe4559ec3d0a4bbcf67d2a21d6ff7b6d1ce10b62953fecafa91faac')
fi

build() {
  cd "$srcdir"

  unzip -q "go_appengine_sdk_linux_"*"-$pkgver.zip"
  cd "google_appengine"

  msg2 "Correcting file permissions..."
  # Only make the .py files executable
  find . -type f -exec chmod 644 '{}' \;
  chmod +x *.py goroot/bin/*

  for f in *.py
  do
    if [ "$f" != "dev_appserver.py" ]; then
      msg2 "Creating wrapperscript for $f [`basename $f .py`-go]..."
      echo -e "#!/bin/sh\npython2 /opt/google-appengine-go/$f \$*" \
        > "$srcdir/`basename $f .py`-go"
    else
      msg2 "Creating wrapperscript for $f [appserver]..."
      echo -e "#!/bin/sh\npython2 /opt/google-appengine-go/$f \$*" \
        > "$srcdir/appserver"
    fi
    # Modifying script to use Python 2
	  sed -i '0,/on/s//on2/' $f
  done
}

package() {
  cd "$srcdir/google_appengine"

  msg2 "Packaging files..."
  mkdir "$pkgdir/opt"
  cp -R "./" "$pkgdir/opt/$pkgname"

  msg2 "Creating a symbolic link for the python modules..."
  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages"
  ln -s "/opt/$pkgname/google" "$pkgdir/usr/lib/python2.7/site-packages/google" 
  
  msg2 "Packaging wrapperscripts..."
  mkdir -p "$pkgdir/usr/bin"
  install -Dm755 "$srcdir/appserver" "$pkgdir/usr/bin/appserver"
  install -Dm755 $srcdir/*-go "$pkgdir/usr/bin/"

  msg2 "Cleaning up deprecated files..."
  rm -r "$pkgdir/opt/$pkgname/tools"

  msg2 "Packaging license..."
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg2 "Fixing permissions..."
  find "$pkgdir/opt/$pkgname" -type d -exec chmod 755 {} \;
  chmod +x "$pkgdir/opt/$pkgname/goroot/pkg/tool/linux_amd64/"*
}

# vim:set ts=2 sw=2 et:
