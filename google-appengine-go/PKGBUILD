# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Richard Hoffman <coverslide@gmail.com>
# Contributor: Max <fuzymonkey@gmail.com>

pkgname=google-appengine-go
pkgver=1.8.9
pkgrel=1
pkgdesc='Google App Engine SDK for Go'
arch=('x86_64' 'i686')
depends=('python2')
url='http://code.google.com/appengine/downloads.html#Google_App_Engine_SDK_for_Go'
license=('APACHE')
options=('!strip')
noextract=("go_appengine_sdk_linux_amd64-$pkgver.zip"
           "go_appengine_sdk_linux_386-$pkgver.zip")
makedepends=('unzip')
optdepends=('mysql-python: MySQL support for Python 2'
            'python2-imaging: Image processing capabilities for Python')
if [[ $CARCH != 'i686' ]]; then
  _zipname="go_appengine_sdk_linux_amd64-$pkgver.zip"
  source=("http://googleappengine.googlecode.com/files/$_zipname")
  sha256sums=('c5fba32dc586d1c5613da6f1ed8710f12c0274b4ab2a8e51d88344624b9d683c')
else
  _zipname="go_appengine_sdk_linux_386-$pkgver.zip"
  source=("http://googleappengine.googlecode.com/files/$_zipname")
  sha256sums=('a1d15cdd46f76a2cda23c49024f85b2867d7e308398207927ae5888cde35cb5e')
fi
noextract=("$_zipname")

prepare() {
  unzip -q "$_zipname"
}

build() {
  cd 'go_appengine'

  # Only make the .py files executable
  find . -type f -exec chmod 644 '{}' \;
  chmod +x *.py goroot/bin/*

  # This is to avoid name conflicts in /usr/bin with other appengine packages
  for f in *.py
  do
    msg2 "Creating wrapperscript for $f [`basename $f .py`-go]..."
    echo -e "#!/bin/sh\npython2 /opt/google-appengine-go/$f \$*" \
      > "$srcdir/`basename $f .py`-go"
    if [ "$f" == "dev_appserver.py" ]; then
      msg2 "Creating wrapperscript for $f [appserver]..."
      echo -e "#!/bin/sh\npython2 /opt/google-appengine-go/$f \$*" \
        > "$srcdir/appserver"
    fi
    # Modifying script to use Python 2
	  sed -i '0,/on/s//on2/' $f
  done
}

package() {
  cd 'go_appengine'

  install -d "$pkgdir/opt"
  cp -R "./" "$pkgdir/opt/$pkgname"

  msg2 'Fixing permissions...'
  if [ $CARCH != "i686" ]; then
    chmod +x "$pkgdir/opt/$pkgname/goroot/pkg/tool/linux_amd64/"*
  else
    chmod +x "$pkgdir/opt/$pkgname/goroot/pkg/tool/linux_386/"*
  fi
  find "$pkgdir/opt/$pkgname" -type d -exec chmod 755 {} \;
  chmod +x "$pkgdir/opt/$pkgname/"go*

  msg2 'Creating a symbolic link for the python modules...'
  install -d "$pkgdir/usr/lib/python2.7/site-packages"
  ln -s "/opt/$pkgname/google" "$pkgdir/usr/lib/python2.7/site-packages/google" 
  
  install -d "$pkgdir/usr/bin"
  install -Dm755 "$srcdir/appserver" "$pkgdir/usr/bin/appserver"
  install -Dm755 $srcdir/*-go "$pkgdir/usr/bin/"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg2 'Cleaning up deprecated files...'
  rm -r "$pkgdir/opt/$pkgname/tools"
}

# vim:set ts=2 sw=2 et:
