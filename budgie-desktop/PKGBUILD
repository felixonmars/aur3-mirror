# Maintainer: Carlos Silva <r3pek@r3pek.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Josh Klar <j@iv597.com>

pkgname=budgie-desktop
pkgver=8
pkgrel=3
pkgdesc="Simple GTK3 desktop experience"
arch=('i686' 'x86_64')
url="http://evolve-os.com/"
license=('GPL2')
depends=('libwnck3' 'gnome-menus' 'gnome-settings-daemon' 'libpeas' 'mutter' 'desktop-file-utils' 'libgee' 'gnome-themes-standard')
makedepends=('git' 'python2' 'vala')
install=budgie-desktop.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/evolve-os/budgie-desktop/archive/v${pkgver}.tar.gz"
        'git+https://git.gnome.org/browse/libgnome-volume-control#commit=e14dbe8')
sha256sums=('7325ab97cdcd7b896503c04cd4844ae62d50bee7bbf4f6a777196ec27a1a1079'
            'SKIP')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	rm -r gvc
	mv ../libgnome-volume-control gvc
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	autoreconf --force --install
	./configure --prefix=/usr
	make
}

check() {
	cd "$srcdir"/$pkgname-$pkgver
	make -k check
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
