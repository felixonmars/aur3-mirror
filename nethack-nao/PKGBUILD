# Maintainer: AkiraYB <brunoyb!outlook,com>

pkgname=nethack-nao
_pkgname=nethack
pkgver=3.4.3_20121204
_pkgver=3.4.3
pkgrel=1
pkgdesc='A single player dungeon exploration game (with patches from NAO)'
arch=('i686' 'x86_64')
url='http://www.nethack.org/'
license=('custom')
depends=('bash')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
backup=('var/games/nethack/logfile'
        'var/games/nethack/perm'
        'var/games/nethack/record'
        'var/games/nethack/xlogfile')
changelog="${_pkgname}.changelog"
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${_pkgver//.}-src.tgz"
        "http://alt.org/nethack/nh343-nao.diff" # http://alt.org/nethack/naonh.php
        "${_pkgname}.patch")
sha256sums=('bb39c3d2a9ee2df4a0c8fdde708fbc63740853a7608d2f4c560b488124866fe4'
            '9e27a2a348757315630a5918f2f06810abc744390c3c2bf1676110dc9629f3af'
            'd20adbfbf78dc811530f32b39ee15bb30a04bb11a3a5d34dc47d1206eadc3784')

build()
{
	cd "${srcdir}/${_pkgname}-${_pkgver}"

	patch -Np1 -i "${srcdir}/nh343-nao.diff"
	patch -Np1 -i "${srcdir}/${_pkgname}.patch"

	sh sys/unix/setup.sh
	make
}

package()
{
	cd "${srcdir}/${_pkgname}-${_pkgver}"

	local _libdir="${pkgdir}/usr/lib/nethack"
	local _docdir="${pkgdir}/usr/share/doc/${_pkgname}"
	local _mandir="${pkgdir}/usr/share/man/man6"
	local _licensesdir="${pkgdir}/usr/share/licenses/${_pkgname}"
	local _gamesdir="${pkgdir}/var/games/nethack"

	install -d "$_docdir" "$_mandir" "$_licensesdir"

	make PREFIX="$pkgdir" MANDIR="$_mandir" install manpages

	mv "${_libdir}/license" "${_licensesdir}/LICENSE"
	install -m 644 doc/Guidebook.txt "${_docdir}"

	sed -e "s|^HACKDIR=${pkgdir}|HACKDIR=|" -i "${pkgdir}/usr/bin/nethack"

	chown -R root:games "${_gamesdir%/nethack}" "${_libdir}/nethack"
	chmod 775 "${_gamesdir%/nethack}"
	chmod 775 "${_gamesdir}"/{,save,extrainfo,userdata}
	chmod 664 "${_gamesdir}"/{logfile,perm,record,xlogfile}
}
