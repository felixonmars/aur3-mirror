# $Id: PKGBUILD 170725 2012-11-10 05:22:37Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=gdk-pixbuf2
pkgname=${pkgbase}-git
_realver=2.27.2
pkgver=20130202
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2-git' 'libpng' 'libtiff' 'libjpeg' 'libx11')
makedepends=('gtk-doc' 'gobject-introspection-git')
options=('!libtool')
install=gdk-pixbuf2.install
provides=("$pkgbase=$_realver")
conflicts=("$pkgbase")

_gitname=gdk-pixbuf

_gitroot='git://git.gnome.org/gdk-pixbuf'
_gitbranch=master


build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch" --
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch" --
    git reset --hard "$_gitbranch"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  ./autogen.sh --prefix=/usr \
    --without-libjasper \
    --with-x11 \
    --with-included-loaders=png
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}
