# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=osquery
pkgver=1.0.5
pkgrel=1
pkgdesc='Tool makes low-level operating system analytics and monitoring both performant and intuitive'
arch=(i686 x86_64)
url='https://github.com/facebook/osquery/'
license=(BSD)
depends=(rocksdb google-glog thrift procps-ng gflags boost-libs)
makedepends=(cmake doxygen boost git python-jinja libunwind python)
source=(https://github.com/facebook/osquery/archive/$pkgver.zip
        git://github.com/osquery/third-party
        compile_fixes.patch)
sha1sums=('e470d0f40d45124b6970be7e7c9d63879035cf4e'
          'SKIP'
          'cd4d78d0e8244a0f9c38efaceb059e35f6aeb5aa')

prepare() {
  cd osquery-$pkgver
  patch -p1 < ../compile_fixes.patch

  rm -r third-party
  ln -sf ../third-party
}

build() {
  cd osquery-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=ON
  make
}

check() {
  cd osquery-$pkgver
  make test
}

package () {
  cd osquery-$pkgver
  make DESTDIR="$pkgdir" install
}
