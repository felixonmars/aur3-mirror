# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
pkgname=perl-class-insideout
pkgver=1.10
pkgrel=1
pkgdesc="Perl module for safe and simple inside-out objects construction"
arch=("any")
url="http://search.cpan.org/dist/Class-InsideOut/"
license=("Apache")
depends=(
  "perl" # provides 'perl-config', 'perl-exporter', 'perl-scalar-list-utils>=1.09', 'perl-test-simple>=0.45', 'perl-base'
  "perl-class-isa")
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/D/DA/DAGOLDEN/Class-InsideOut-$pkgver.tar.gz)
md5sums=("942347c9fe1d36da470bf89de1753571")

build() {
  cd "$srcdir/Class-InsideOut-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Class-InsideOut-$pkgver"
  make test
}

package() {
  cd "$srcdir/Class-InsideOut-$pkgver"
  make install DESTDIR="$pkgdir"
}
