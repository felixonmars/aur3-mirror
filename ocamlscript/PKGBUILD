# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Dwight Schauer <dschauer@gmail.com>

pkgname=ocamlscript
pkgver=2.0.2
pkgrel=3
pkgdesc="compiles scripts, i.e. one-file programs, into natively-compiled binaries, and executes them."
arch=(i686 x86_64)
url="http://martin.jambon.free.fr/ocamlscript.html"
license=('custom:boost')
depends=('ocaml')
makedepends=('ocaml-findlib')
source=(http://martin.jambon.free.fr/$pkgname-$pkgver.tar.gz)
md5sums=('e3e511d7537d2c5172603efec02073ce')
options=(!strip !makeflags)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Note(Sergei): we only install `native` versions of the library and
  # `ocamlscript` binary.
  make common optexe || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  export PREFIX=$pkgdir/usr
  mkdir -p $OCAMLFIND_DESTDIR
  mkdir -p $PREFIX/bin

  make install
}
