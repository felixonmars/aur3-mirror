# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: mutlu_inek <mutlu_inek@yahoo.de>

pkgname=iscan
pkgver=2.26.4
pkgrel=1
pkgdesc="A front-end for EPSON scanner/all-in-ones"
arch=('i686' 'x86_64')
url="http://avasys.jp/eng/linux_driver/"
license=('GPL2' 'custom:AVASYSPL')
depends=('gtk2' 'iscan-data' 'sane')
optdepends=('gimp: to use iscan as a GIMP plugin')
conflicts=(${pkgname}-legacy)
install=${pkgname}.install
options=('!libtool')
source=(http://linux.avasys.jp/drivers/${pkgname}/${pkgver}/${pkgname}_${pkgver}-2.tar.gz)
md5sums=('0867e02ad254f083ab853633e62abaf6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-dependency-reduction \
              --enable-frontend \
              --enable-gimp \
              --enable-jpeg \
              --enable-tiff \
              --enable-png
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install sane file config
  install -d -m755 "${pkgdir}/etc/sane.d"
  install -m644 backend/epkowa.conf "${pkgdir}/etc/sane.d/"

  # install desktop file
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/"

  # install custom license
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/${pkgname}-${pkgver}/non-free/AVASYSPL.en.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/AVASYSPL"
}

