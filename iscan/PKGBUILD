# Maintainer: garion < garion @ mailoo.org >
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=iscan
pkgver=2.29.1
pkgrel=4
pkgdesc="A front-end for EPSON scanner/all-in-ones"
arch=('i686' 'x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('GPL2' 'custom:AVASYSPL')
depends=('gtk2' 'sane')
optdepends=('iscan-data: Image Scan! for Linux data files')
makedepends=('gettext')
conflicts=(${pkgname}-legacy)
install=${pkgname}.install
backup=(etc/sane.d/epkowa.conf)
options=('!libtool')
source=("http://a1227.g.akamai.net/f/1227/40484/1d/download.ebz.epson.net/dsc/f/01/00/02/02/33/5e8cbea4df158119cd56bba0b435ce740ea708e7/iscan_2.29.1-5.tar.gz"
        'iscan-2.28.1-libpng15.patch')
md5sums=('e2db4b409f0b6a4f20d325b4d54d1dec'
         'fc7db347c301683c01376056898f2792')
build() {
  cd "$srcdir/$pkgname-$pkgver"
  export LDFLAGS="${LDFLAGS} -ldl -lpng15"

  # patch for building iscan against libpng15 by giovanni
  patch -Np0 -i "${srcdir}/iscan-2.28.1-libpng15.patch"

  ./configure --prefix=/usr \
              --enable-dependency-reduction \
              --enable-frontend \
              --enable-jpeg \
              --enable-tiff \
              --enable-png #--enable-gimp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  # install sane file config
  install -d -m755 "$pkgdir/etc/sane.d"
  install -m644 backend/epkowa.conf "$pkgdir/etc/sane.d/epkowa.conf"

  # install desktop file
  install -d -m755 "$pkgdir/usr/share/applications"
  install -m644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  # install license
  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "non-free/AVASYSPL.en.txt" "$pkgdir/usr/share/licenses/$pkgname/AVASYSPL"
}

