# Maintainer: GordonGR <gordongr@freemail.gr>
# Contributor: Splex

pkgname=dolphinviewer
pkgver=3.3.21.24786
_pngver=1.5.1
pkgrel=1
pkgdesc="An Open Source third party viewers for Second LifeÂ® (secondlife) and OpenSim (opensimulator) grids."
url="http://dolphinviewer.eregion.de"
license=('LGPL')

[ "$CARCH" = "i686"   ] && depends=('apr-util' 'gtk2' 'libgl' 'libidn' 'libjpeg-turbo' 'mesa' 'nss' 'sdl')
[ "$CARCH" = "i686"   ] && optdepends=('libpulse: for PulseAudio support' 'alsa-lib: for ALSA support' 'nvidia-utils: for NVIDIA support' 'flashplugin: for inworld Flash support' 'gstreamer0.10: for video support, may need good, bad and ugly plugins' 'lib32-freealut: for OpenAL support')
[ "$CARCH" = "x86_64" ] && depends=('apr-util' 'lib32-gtk2' 'lib32-libgl' 'lib32-libidn' 'lib32-libjpeg-turbo' 'lib32-mesa' 'lib32-nss' 'lib32-sdl')
[ "$CARCH" = "x86_64"   ] && optdepends=('lib32-libpulse: for PulseAudio support' 'lib32-alsa-lib: for ALSA support' 'lib32-nvidia-utils: for NVIDIA support' 'lib32-flashplugin: for inworld Flash support' 'lib32-gstreamer0.10: for video support, may need good, bad and ugly plugins' 'lib32-freealut: for OpenAL support')

arch=('i686' 'x86_64')
#install=dolphinviewer.install
source=('dolphinviewer.desktop' 'dolphinviewer.launcher'
        "http://dolphinviewer.eregion.de/files/installers/DolphinViewer3-i686-linux-${pkgver}.tar.bz2"
        "http://downloads.sourceforge.net/sourceforge/libpng/libpng-${_pngver}.tar.xz"
        'donotregister.patch')

md5sums=('ab3ac1b7edb7991a622309369ac10457'
         '0879128702933080ad018898a003c699'
         'f05b26d0054fac10d9f7e5f1c2066359'
         '35455234375f1adff8083f408a099e3a'
         '1b4db712c2db43c0ae4f69b6b5410c43')



build() {
  # build compatible lib32-libpng
  msg "Building libpng..."

  if [ "$CARCH" = "x86_64" ] ; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  cd "${srcdir}/libpng-${_pngver}"

  ./configure
  make
}

package() {
  cd $srcdir
  
  # Rename Data Directory
  mv DolphinViewer3-i686-linux-${pkgver} dolphinviewer

  # Do not register the application (saves from badly-installed desktop files and icons)
  patch dolphinviewer/secondlife donotregister.patch
  rm dolphinviewer/etc/refresh_desktop_app_entry.sh
  
  # Install Desktop File
  install -D -m644 $srcdir/dolphinviewer.desktop \
    $pkgdir/usr/share/applications/dolphinviewer.desktop
  
  # Install Icon File
  install -D -m644 $srcdir/dolphinviewer/secondlife_icon.png \
    $pkgdir/usr/share/pixmaps/dolphinviewer.png
  
  # Install Launcher
  install -D -m755 $srcdir/dolphinviewer.launcher \
    $pkgdir/usr/bin/dolphinviewer

  # Move Data to Destination Directory
  install -d $pkgdir/opt
  mv dolphinviewer $pkgdir/opt/

  # Install libpng we built
  install -D -m755 $srcdir/libpng-${_pngver}/.libs/libpng15.so.15.1.0 \
    $pkgdir/opt/dolphinviewer/lib

  cd $pkgdir/opt/dolphinviewer/lib
  ln -s libpng15.so.15.1.0 libpng15.so.15
  ln -s libpng15.so.15.1.0 libpng15.so
}
