# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=ibus-table
pkgname=$_pkgname-git
pkgver=20120922
pkgrel=1
pkgdesc="The IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on."
arch=(any)
url=http://code.google.com/p/ibus/
license=(LGPL)
depends=(python2-pyenchant ibus aspell-en)
makedepends=(git)
provides=($_pkgname=1.3.9)
conflicts=($_pkgname)
options=(!libtool)

_gitroot=https://github.com/acevery/$_pkgname.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]] ; then
        pushd $_gitname && git pull origin master
        msg "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg "GIT checkout done or server timeout"

    rm -rf $_gitname-build
    cp -r $_gitname $_gitname-build
    cd $_gitname-build

    # python2 fix
    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    sed -ri 's:(exec python)([^2]):\12\2:' engine/ibus-{engine-table,table-createdb}.in

    msg "Building..."
    ./autogen.sh PYTHON=/usr/bin/python2 --prefix=/usr --libexecdir=/usr/lib/ibus
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
