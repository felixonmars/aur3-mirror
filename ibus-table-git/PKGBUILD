# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: riverscn <riverscn@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=ibus-table-git
pkgver=20120825
pkgrel=1
pkgdesc="The IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on."
arch=(any)
url="http://code.google.com/p/ibus/"
license=(LGPL)
depends=(python2-pyenchant ibus aspell-en)
makedepends=(git)
provides=(ibus-table=1.3.9)
conflicts=(ibus-table)
options=(!libtool)

_gitroot="https://github.com/acevery/ibus-table.git"
_gitname="ibus-table"

build() {
    cd "$srcdir"/
    msg "Connecting to GIT server...."

    if [[ -d $_gitname ]] ; then
	cd $_gitname && git pull origin master
	msg "The local files are updated."
	cd ../
    else
	git clone $_gitroot
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf $_gitname-build
    cp -r $_gitname $_gitname-build
    cd $_gitname-build

    # python2 fix
    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python:&2:' '{}' \;
    sed -i 's:exec python:&2:' engine/ibus-{engine-table,table-createdb}.in

    ./autogen.sh PYTHON=/usr/bin/python2 --prefix=/usr --libexecdir=/usr/lib/ibus
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
} 
