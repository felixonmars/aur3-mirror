# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: riverscn <riverscn@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=ibus-table-git
pkgver=20120821
pkgrel=1
pkgdesc="The IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on."
arch=(any)
url="http://code.google.com/p/ibus/"
license=(LGPL)
depends=(python2-pyenchant ibus aspell-en)
provides=(ibus-table=1.3.9)
conflicts=(ibus-table)
makedepends=(git cvs)
options=(!libtool)

_gitroot="git://github.com/acevery/ibus-table.git"
_gitname="ibus-table"

build() {
    cd $srcdir
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
	cd $_gitname && git pull origin master
	msg "The local files are updated."
    else
	git clone $_gitroot
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf $srcdir/$_gitname-build
    cp -r $srcdir/$_gitname $srcdir/$_gitname-build
    cd $srcdir/$_gitname-build

    # python2 fix
    sed -ri 's:^#!/usr/bin/(env )?python:&2:' $(find -name '*.py' -type f)
    sed -i 's:exec python:&2:' engine/ibus-{engine-table,table-createdb}.in

    ./autogen.sh PYTHON=/usr/bin/python2 --prefix=/usr --libexecdir=/usr/lib/ibus
    _corecount=$(grep -i processor /proc/cpuinfo | wc -l)
    make -j$_corecount
}

package() {
    cd $srcdir/$_gitname-build
    make DESTDIR=$pkgdir install
} 
