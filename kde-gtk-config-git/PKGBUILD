# Contributor: vicky91 <vickypaiers@gmail.com>
# Maintainer: vicky91 <vickypaiers@gmail.com>
pkgname=kde-gtk-config-git
pkgver=20121202
pkgrel=1
pkgdesc="GTK2 and GTK3 Configurator for KDE"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/kde-gtk-config"
license=('GPL')
depends=('kdebase-runtime' )
makedepends=('git' 'automoc4' 'cmake' 'gettext' 'gtk2' 'gtk3')
conflicts=('chakra-gtk-config' 'chakra-gtk-config-git' )
options=(!strip)
install=${pkgname}.install

_gitroot='git://anongit.kde.org/kde-gtk-config'
_gitname='kde-gtk-config'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}/" install
}

