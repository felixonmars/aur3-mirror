# Maintainer: naelstrof <naelstrof@gmail.com>

pkgname=mingw-w64-openal
pkgver=1.14
pkgrel=1
pkgdesc="OpenAL audio library for use with opengl (mingw-w64)"
arch=(any)
url="http://connect.creativelabs.com/openal/"
license=('GPL2')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'cmake')
options=(!strip !buildflags)
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-$pkgver.tar.bz2)
md5sums=('3d8b86c21a2f87a2a5e60f78f3b3f03d')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"
build() {
  unset LDFLAGS

  for _arch in ${_architectures}; do
      mkdir -p ${srcdir}/build-${_arch} && cd ${srcdir}/build-${_arch}
      #-D__MINGW64__ required for the 32 bit version to compile correctly.
      #Toolchain required for the compiler to pass sanity checks done by cmake correctly.
      echo "SET(CMAKE_SYSTEM_NAME Windows)
            SET(CMAKE_C_COMPILER ${_arch}-gcc)
            SET(CMAKE_CXX_COMPILER ${_arch}-g++)
            SET(CMAKE_RC_COMPILER ${_arch}-windres)
            SET(CMAKE_FIND_ROOT_PATH /usr/${_arch})
            SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
            SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
            SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
            add_definitions(-D__MINGW64__)" > temp.cmake
      #FIXME: -DMMDEVAPI=OFF is required for it to compile correctly on mingw-w64's version of winapi. If the api gets patched this should be enabled!
      cmake -DCMAKE_TOOLCHAIN_FILE=temp.cmake \
            -DCMAKE_INSTALL_PREFIX=/usr/${_arch} \
            -DEXAMPLES=OFF \
            -DMMDEVAPI=OFF \
            ../openal-soft-${pkgver}
      make
  done
}

package() {
  for _arch in ${_architectures}; do
      cd ${srcdir}/build-${_arch}
      make DESTDIR=${pkgdir} install
  done
}
