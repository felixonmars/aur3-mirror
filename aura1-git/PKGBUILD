# Maintainer: Colin Woodbury <colingw@gmail.com>
# Contributor: Bob Valantin <valantin89 [at] gmail [dot] com>

pkgname=aura1-git
pkgver=20150111
pkgrel=1 
pkgdesc="A secure package manager for Arch Linux and the AUR written in Haskell."
url="https://github.com/fosskers/aura"
arch=('i686' 'x86_64')
license=('GPL-3')
depends=('git' 'gmp' 'pacman' 'pcre' 'abs')
makedepends=('ghc'
  'haskell-aur'
  'haskell-lens'
  'haskell-mtl'
  'haskell-parsec'
  'haskell-regex-base'
  'haskell-regex-pcre-builtin'
  'haskell-split'
  'haskell-temporary'
  'haskell-text'
  'haskell-transformers'
  'haskell-wreq')
provides=('aura')
conflicts=('aura' 'aura-bin' 'aura-git')
source=("${pkgname}::git+https://github.com/aurapm/aura.git")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"
  git checkout aura1
  runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O
  runhaskell Setup build
}

package() {
  cd "${srcdir}/${pkgname}"
  runhaskell Setup copy --destdir=${pkgdir}

  # Installing man page
  mkdir -p "$pkgdir/usr/share/man/man8/"
  install -m 644 doc/aura.8 "$pkgdir/usr/share/man/man8/aura.8"
  
  # Installing bash completions
  mkdir -p "$pkgdir/usr/share/bash-completion/completions/"
  install -m 644 doc/completions/bashcompletion.sh "$pkgdir/usr/share/bash-completion/completions/aura"
  
  # Installing zsh completions
  mkdir -p "$pkgdir/usr/share/zsh/site-functions/"
  install -m 644 doc/completions/_aura "$pkgdir/usr/share/zsh/site-functions/_aura"

  # Directory for storing PKGBUILDs
  mkdir -p "$pkgdir/var/cache/aura/pkgbuilds"

  # Directory for storing installed package states
  mkdir -p "$pkgdir/var/cache/aura/states"
}
