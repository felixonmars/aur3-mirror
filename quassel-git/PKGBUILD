# Contributor: Daniel Albers <daniel@lbe.rs>

#########################################
# This is actually a splitted PKGBUILD. #
# To use it as such, set $_aur=0.       #
#########################################
_aur=1

pkgbase='quassel-git'
pkgname=("$pkgbase")
package() {
  package_$pkgname
}

[[ $_aur ]] || pkgbase='quassel'
[[ $_aur ]] || pkgname=('quassel-git'
                  'quassel-light-git'
                  'quassel-client-git'
                  'quassel-client-light-git'
                  'quassel-core-git'
	          #TODO: quassel-common
         )

pkgver=0.10.pre.13.g8ef0278
pkgrel=1
pkgdesc='KDE/Qt-based distributed IRC client'
url='http://quassel-irc.org'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qt4')
makedepends=('git' 'cmake')
provides=('quassel')
conflicts=('quassel')
source=(
  'git+https://github.com/quassel/quassel.git'
 #'git://git.quassel-irc.org/quassel.git'
)
md5sums=('SKIP')

_gitdir="${startdir}/quassel"
_srcdir="${startdir}/src/quassel"
[[ $_aur ]] || _gitdir="${startdir}/${pkgbase}"
[[ $_aur ]] || _srcdir="${startdir}/src/${pkgbase}"

_builddir="${startdir}/build"
_cmakecache="CMakeCache.txt"

_debug=0
if [[ $_debug == 0 ]]; then
  exec 3> /dev/null
else
  exec 3>& 1
fi

package_quassel-git() {
  pkgdesc='KDE-based IRC client (monolithic version)'
  depends='kdelibs'
  makedepends=('automoc4' 'cmake')

  _prepare_build

  pushd "$_builddir" >& 3

  msg 'Configuring build options...'
  cmake -DWANT_MONO=ON              \
        -DWANT_CORE=OFF             \
        -DWANT_QTCLIENT=OFF         \
        -DWITH_OPENSSL=ON           \
        -DWITH_KDE=ON               \
        -DWITH_WEBKIT=ON            \
	-DCMAKE_INSTALL_PREFIX=/usr \
	"$_srcdir"

  popd >& 3
  _build
}

package_quassel-light-git() {
  pkgdesc='Qt-based IRC client (monolithic version)'

  _prepare_build

  pushd "$_builddir" >& 3

  msg 'Configuring build options...'
  cmake -DWANT_MONO=ON              \
        -DWANT_CORE=OFF             \
        -DWANT_QTCLIENT=OFF         \
        -DWITH_OPENSSL=ON           \
        -DWITH_KDE=OFF              \
        -DWITH_WEBKIT=OFF           \
	-DCMAKE_INSTALL_PREFIX=/usr \
	"$_srcdir"

  popd >& 3
  _build
}

package_quassel-client-git() {
  pkgdesc='KDE-based distributed IRC client (client only)'
  depends='kdelibs'
  makedepends=('automoc4' 'cmake')
  provides=('quassel-client')
  conflicts=('quassel-client')

  _prepare_build

  pushd "$_builddir" >& 3

  msg 'Configuring build options...'
  cmake -DWANT_MONO=OFF             \
        -DWANT_CORE=OFF             \
        -DWANT_QTCLIENT=ON          \
        -DWITH_OPENSSL=ON           \
        -DWITH_KDE=ON               \
        -DWITH_WEBKIT=ON            \
	-DCMAKE_INSTALL_PREFIX=/usr \
	"$_srcdir"

  popd >& 3
  _build
}

package_quassel-client-light-git() {
  pkgdesc='Qt-based distributed IRC client (client only)'
  provides=('quassel-client')
  conflicts=('quassel-client')

  _prepare_build

  pushd "$_builddir" >& 3

  msg 'Configuring build options...'
  cmake -DWANT_MONO=OFF             \
        -DWANT_CORE=OFF             \
        -DWANT_QTCLIENT=ON          \
        -DWITH_OPENSSL=ON           \
        -DWITH_KDE=OFF              \
        -DWITH_WEBKIT=OFF           \
	-DCMAKE_INSTALL_PREFIX=/usr \
	"$_srcdir"

  popd >& 3
  _build
}

package_quassel-core-git() {
  pkgdesc='KDE/Qt-based distributed IRC client (core only)'
  provides=('quassel-core')
  conflicts=('quassel-core')

  _prepare_build

  pushd "$_builddir" >& 3

  msg 'Configuring build options...'
  cmake -DWANT_MONO=OFF             \
        -DWANT_CORE=ON              \
        -DWANT_QTCLIENT=OFF         \
        -DWITH_OPENSSL=ON           \
	-DCMAKE_INSTALL_PREFIX=/usr \
	"$_srcdir"

  popd >& 3
  _build
}

pkgver() {
  pushd "$_gitdir" >& 3
  git describe --always | sed 's/-/./g;'
  popd >& 3
}

_prepare_build() {
  msg 'Preparing build directory...'
  mkdir -p "$_builddir"
  pushd "$_builddir" >& 3
  rm -f "$_cmakecache"
  popd >& 3
}

_build() {
  pushd "$_builddir" >& 3

  msg 'Compiling...'
  make

  make DESTDIR="$pkgdir" install
}
