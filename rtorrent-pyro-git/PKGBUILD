# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

## This is tha v0.9.2 tag if you wish to use whats in the official tarball instead of git head
#_gitrev='4dd7582240d3293f23dc73c2fc8c3de09c237578'
_use_stable_rev='n'

## Compile in debugging symbols cause this is a git version and you are most likely looking for bugs
#_debug="--disable-debug"
_debug="--enable-debug"

pkgname=rtorrent-pyro-git
_pkgname='rtorrent'
pkgver=20120930
pkgrel=1
pkgdesc="rTorrent-git with Pyroscope patches"
url="https://code.google.com/p/pyroscope"
license=('GPL')
arch=('i686' 'x86_64')
depends=('libtorrent-pyro-git=20120930' 'curl' 'xmlrpc-c')
makedepends=('git' 'gcc>=4.7.0' 'cppunit')
options=('!strip')
backup=()
conflicts=("$_pkgname")
provides=("$_pkgname")
install='pyroscope.install'

_url="https://pyroscope.googlecode.com/svn/trunk/pyrocore/docs/rtorrent-extended/patches"
_gitname="${_pkgname}"
_gitroot="git://github.com/rakshasa/${_pkgname}.git"
[[ $_use_stable_rev = 'n' ]] && _gitoptions="--depth 1"

source=("${_url}/ps-ui_pyroscope_0.8.8.patch"
		"${_url}/pyroscope.patch"
		"${_url}/ui_pyroscope.patch"
		"${_url}/command_pyroscope.cc"
		"${_url}/ui_pyroscope.cc"
		"${_url}/ui_pyroscope.h")

md5sums=('7a88f8ab5d41242fdf1428de0e2ca182'
         'bd04a0699b80c8042e1cf63a7e0e4222'
         '0a2bbaf74c7160ba33876dcc2f050f14'
         'ca7feaa0f6fad892d10734bb5b11167b'
         'fb90aae4c4d8b252ae5b2d65492a1138'
         '1258acfc82c50a8f452ace87fef0b416')

build() {
    cd $srcdir

    msg "Connecting to GIT server..."
    if [ -d "${_gitname}/.git" ]; then
        cd $_gitname && git pull origin
        msg "The local repository was updated."
    else
        git clone ${_gitoptions} ${_gitroot} ${_gitname}
        cd $_gitname
        [[ $_use_stable_rev = 'y' ]] && git checkout $_gitrev
        msg "The remote repository was cloned."
    fi

    msg "Patching rtorrent..."
    # Used by the the pyroscope patch to determine version we are patching to
    sed -i "s:\\(AC_DEFINE(HAVE_CONFIG_H.*\\):\1\\nAC_DEFINE(RT_HEX_VERSION, 0x000902, for CPP if checks):" configure.ac
    for i in ${srcdir}/*.patch; do
        patch -uNp1 -i "$i"
    done
    for i in ${srcdir}/*.{cc,h}; do
        ln -s $i src
    done
    sed -i 's:rTorrent \" VERSION:rTorrent-pyro " VERSION:' src/ui/download_list.cc

    msg "Starting build.."
    #export CFLAGS="-march=native -O3 -pipe -fstack-protector-all -fno-strict-overflow -fno-strict-aliasing -D_FORTIFY_SOURCE=2 --param=ssp-buffer-size=4 -Wall"
    #export CXXFLAGS="$CFLAGS -Wno-conversion-null -std=c++11"
    #export LDFLAGS="-Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now"
    export CXXFLAGS+=" -fno-strict-aliasing -std=c++11"

    ./autogen.sh
    ./configure --prefix=/usr --with-xmlrpc-c $_debug
    make
}

package() {
    cd ${srcdir}/${_pkgname}
    make DESTDIR="${pkgdir}" install

    install -Dm644 doc/faq.xml        "${pkgdir}/usr/share/doc/rtorrent/faq.xml"
    install -Dm644 doc/rtorrent.rc    "${pkgdir}/usr/share/doc/rtorrent/rtorrent.rc"
    install -Dm644 doc/old/rtorrent.1 "${pkgdir}/usr/share/man/man1/rtorrent.1"
}
