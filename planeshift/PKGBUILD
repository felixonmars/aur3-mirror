# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Marcelo Cavalcante / Kalib <kalib@archlinux-br.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Gimmeapill <gimmeapill@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=planeshift
pkgver=0.5.9.2
pkgrel=1
pkgdesc='Open source and cross-platform 3D Fantasy MMORPG'
url='http://www.planeshift.it/'
arch=('x86_64' 'i686')
license=('GPL' 'custom:PlaneShift Content License')
depends=('libgl' 'libsm' 'libxmu' 'libxpm' 'openal')
makedepends=('aria2' 'gendesk')
options=('emptydirs')
install=planeshift.install
source=("planeshift_x86_64.meta4"
        "planeshift_i686.meta4"
        "planeshiftsetup.desktop"
        "license.txt"
        "scriptfix.patch")
sha256sums=('1d25dccd53d69e863f6ea774fb94badacbc687e876c7e8f02ab72267a17c9ccf'
            '4dbebe2fdcf5f1812ea1012d89e8654dbf133ac773b3b84233efea5280a71deb'
            '51cb76cfa35109412bf12f67ca1d1fde99804226de0fab29ecd882f67e7c336f'
            '51ba4271c77c00efe087f2f456ae777853f85b97767cf085d2fb8f9b82c082fc'
            'e37585331598531d44a2381d8ac96289fd2e6d6ae70321035cf22654f76d2a0a')
_name=('PlaneShift Client')
_genericname=('MMORPG')

build() {
  cd "$srcdir"

  gendesk -n
  msg2 "Downloading PlaneShift $pkgver (may seed for up to a minute)..."
  msg2 "Be patient and don't panic!"
  aria2c -M planeshift_$CARCH.meta4 -V -c --allow-overwrite=true \
    --seed-ratio=5.0 --seed-time=1 --no-conf=true
  if [ `find . -maxdepth 1 -name "PlaneShift-*.run" | wc -l` == 0 ]; then
    msg2 'Download failed!'
    echo
    echo 'Make sure you have enough disk space in'
    echo "$srcdir"
    echo
    echo 'You need at least 800-900 MB free for the download,'
    echo 'and quite a bit more for extracting the file.'
    echo
    echo "Here's the currently available disk space:"
    echo
    df -h "$srcdir"
    echo
    return 1
  fi
}

package() {
  cd "$srcdir"

  msg2 "Extracting the (large) file..."
  chmod +x PlaneShift-*.run
  ./PlaneShift-*.run \
    --mode unattended \
    --syswide yes \
    --prefix "$pkgdir/opt"

  msg2 "Setting permissions..."
  find "$pkgdir/opt/PlaneShift" -type f -exec chmod 660 {} +
  find "$pkgdir/opt/PlaneShift" -type d -exec chmod 770 {} +
  cd "$pkgdir/opt/PlaneShift"
  chmod 770 psclient.bin pslaunch.bin psclient pslaunch

  msg2 "Packaging scripts..."
  install -Dm755 psclient "$pkgdir/usr/bin/$pkgname"
  install -Dm755 pslaunch "$pkgdir/usr/bin/$pkgname-setup"

  msg2 "Cleaning up..."
  rm -f uninstall unscript.sh planeshift

  msg2 "Patching..."
  cd "$pkgdir/usr/bin/"
  patch -Np0 < "$srcdir/scriptfix.patch"

  msg2 "Packaging shortcuts..."
  mkdir -p "$pkgdir/usr/share/applications"
  install -Dm644 "$srcdir"/*.desktop "$pkgdir/usr/share/applications/"
  install -Dm644 "$pkgdir/opt/PlaneShift/support/icons/psicon.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 "Packaging license..."
  install -Dm644 "$srcdir/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:


