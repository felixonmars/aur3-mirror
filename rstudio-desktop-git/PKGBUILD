#
# Maintainer: JT Wilkinson <jowilkin at tu dot archserver dot org>
#
# Note: Apache ant and java must be on the path for make to succeed.
#       If either was installed as a dependency during the build, you
#       will need to log out and then log back in for the changes to
#       your path and JAVA_HOME environment variable to take effect.
#
pkgname=rstudio-desktop-git
pkgver=20120908
pkgrel=1
pkgdesc="A new integrated development environment (IDE) for R programming language"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r>=2.11.1' 'qt>=4.8' 'qtwebkit' 'boost-libs>=1.5' 'util-linux' 'shared-mime-info' 'gcc-libs')
makedepends=('git' 'cmake>=2.8' 'boost>=1.5' 'openjdk6' 'apache-ant' 'unzip' 'openssl' 'bzip2' 'pango' 'pam' 'zlib')
provides=('rstudio-desktop' 'rstudio-desktop-bin ')
conflicts=('rstudio-desktop' 'rstudio-desktop-bin ')
install="${pkgname}.install"

_gitroot="git://github.com/rstudio/rstudio.git"
_gitname="rstudio"

build() {
  cd "${srcdir}"
  echo "Entering ${srcdir}"
  if [ -d "$_gitname" ] ; then
    msg "Updating repository from server...."
    cd "$_gitname"
    git pull
  else
    msg "Cloning from GIT server...."
    git clone "$_gitroot" "$_gitname"
  fi

  cd "${srcdir}/$_gitname"
  msg "GIT checkout done or server timeout"

  # Install gwt - this creates files inside the src package that are needed for the build
  cd "${srcdir}/$_gitname/dependencies/common"
  msg "Downloading and installing gwt"
  ./install-gwt
  msg "Downloading and installing dictionaries"
  ./install-dictionaries
  msg "Downloading and installing mathjax"
  ./install-mathjax

  if [ -d "${srcdir}/$_gitname/build" ]; then
    msg "Clean previous build directory..."
    rm -rf "${srcdir}/$_gitname/build"
  fi


  # Configure with cmake in a new buld directory as recommended in the rstudio INSTALL file
  msg "Configuring the makefile with cmake"
  mkdir "${srcdir}/$_gitname/build"
  cd "${srcdir}/$_gitname/build"

  # Configure cmake to install to /opt/rstudio
  cmake -DRSTUDIO_TARGET=Desktop -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio ..
}

package() {
  msg "Starting make and install...."
  cd "${srcdir}/$_gitname/build"
  make DESTDIR="$pkgdir" install

  msg "Installing license...."
  install -D -m644 "${srcdir}/$_gitname/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

