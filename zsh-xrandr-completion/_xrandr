#compdef xrandr
local context state line
typeset -A opt_args
local outputs modes expl

_xrandr_outputs() {
  local vals
  vals=( ${(f)"$(xrandr | awk '/connected/ { print $1 }')"} )
  _describe -t outputs 'outputs' vals && return 0
}

_xrandr_modes() {
  local vals
  vals=( ${(f)"$(xrandr | grep -E -o '[0-9]+x[0-9]+' | sort -n | uniq)"} )
  _describe -t outputs 'modes' vals && return 0
}

_arguments \
  '(-d -display)'{-d,-display}':X display:_x_display' \
  '-help[display help]' \
  '(-o --orientation)'{-o,--orientation}':rotation:(normal inverted left right 0 1 2 3)' \
  '(-q --query)'{-q,--query}'[display current state]' \
  '(-s --size)'{-s,--size}':size:' \
  '(-r --rate --refresh)'{*-r,*--rate,*--refresh}':target refresh rate:' \
  '(-v --version)'{-v,--version}'[display version]' \
  '-x[reflect across X axis]' \
  '-y[reflect across Y axis]' \
  '--screen:X screen number' \
  '--verbose[be more verbose]' \
  '--dryrun' \
  '--nograb' \
  '(--prop --properties)'{--prop,--properties}'[display the contents of properties for each output]' \
  '--fb:size:' \
  '--fbmm:size:' \
  '--dpi:dpi:' \
  "*--output:output to reconfigure:{_xrandr_outputs}" \
  '*--auto' \
  "*--mode:mode:{_xrandr_modes}" \
  '*--preferred' \
  '*--pos:position:' \
  '*--reflect:axes:(normal x y xy)' \
  '*--rotate:rotation:(normal inverted left right)' \
  "*--left-of:relative position to:{_xrandr_outputs}" \
  "*--right-of:relative position to:{_xrandr_outputs}" \
  "*--above:relative position to:{_xrandr_outputs}" \
  "*--below:relative position to:{_xrandr_outputs}" \
  "*--same-as:relative position to:{_xrandr_outputs}" \
  '*--set:property:(Backlight scaling\ mode):value:->value' \
  '*--scale:output scaling:' \
  '*--transform:transformation matrix:' \
  '*--off[disable the output]' \
  '*--crtc:crtc to use:' \
  '*--panning:panning:' \
  '*--gamma:r\:g\:b:' \
  '*--primary' \
  '--noprimary' \
  '*--newmode:name: :clock MHz: :hdisp: :hsync-start: :hsync-end: :htotal: :vdisp: :vsync-start: :vsync-end: :vtotal:' \
  '*--rmmode:Mode name:' \
  "*--addmode:output:{_xrandr_outputs}:name:" \
  "*--delmode:output:{_xrandr_outputs}:name:" \
  && return 0

if [[ $state == value ]]; then
  case $words[CURRENT-1] in
    (scaling* mode)
      _description value expl "output property 'scaling mode'"
      compadd "$@" "$expl[@]" None Full Center Full\ aspect && return 0
      ;;
  esac
fi
