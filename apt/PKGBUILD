# Contributor: Johannes Dewender  arch at JonnyJD dot net
pkgname=apt
pkgver=0.9.7.8
pkgrel=1
pkgdesc="commandline package manager"
arch=('i686' 'x86_64')
url="http://packages.debian.org/sid/apt"
license=('GPL2')
depends=('gnupg' 'curl')
makedepends=('dpkg' 'docbook-xsl' 'po4a')
checkdepends=()
optdepends=()
provides=('libapt-inst' 'libapt-pkg' 'libapt-pkg-dev' "apt-utils")
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
source=(http://ftp.debian.org/debian/pool/main/a/$pkgname/${pkgname}_$pkgver.tar.gz)
md5sums=('d44f459d59d8fa7fc5f455f1f982f08c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  rm -f buildlib/config.{guess,sub}
  cp /usr/share/automake-*/config.guess buildlib/
  cp /usr/share/automake-*/config.sub buildlib/
  _stylesheet=`ls -d /usr/share/xml/docbook/xsl-stylesheets-* | xargs basename`
  sed -i -e "s|stylesheet/nwalsh|$_stylesheet|" doc/manpage-style.xsl
  # disable debiandoc, this saves us from sgml problems
  sed -i -e 's|-C doc $@|-C doc manpages|' Makefile
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p $pkgdir/usr/bin
  # apt
  for file in {cache,cdrom,config,get,key,mark}; do
    install bin/apt-$file $pkgdir/usr/bin/
  done
  # apt-utils
  for file in {extracttemplates,ftparchive,sortpkgs}; do
    install bin/apt-$file $pkgdir/usr/bin/
  done

  mkdir -p $pkgdir/usr/lib
  libdir=$pkgdir/usr/lib
  # libapt-inst
  install bin/libapt-inst.so.1.5.0 $libdir/
  ln -s -r $libdir/libapt-inst.so.1.5.0 $libdir/libapt-inst.so.1.5
  ln -s -r $libdir/libapt-inst.so.1.5.0 $libdir/libapt-inst.so
  # libapt-pkg
  install bin/libapt-pkg.so.4.12.0 $libdir/
  ln -s -r $libdir/libapt-pkg.so.4.12.0 $libdir/libapt-pkg.so.4.12
  ln -s -r $libdir/libapt-pkg.so.4.12.0 $libdir/libapt-pkg.so
  # apt-utils
  mkdir -p $pkgdir/usr/lib/apt/solvers
  install bin/apt-internal-solver $pkgdir/usr/lib/apt/solvers/apt
  install bin/apt-dump-solver $pkgdir/usr/lib/apt/solvers/dump
  # apt
  mkdir -p $pkgdir/usr/lib/apt/methods
  install bin/methods/* $pkgdir/usr/lib/apt/methods/
  mkdir -p $pkgdir/usr/lib/dpkg/methods/apt/
  install dselect/{install,setup,update} $pkgdir/usr/lib/dpkg/methods/apt/
  install -m 644 dselect/{desc.apt,names} $pkgdir/usr/lib/dpkg/methods/apt/

  # ALL manpages
  mkdir -p $pkgdir/usr/share/man
  for part in {1,5,8}; do
    mkdir -p $pkgdir/usr/share/man/man$part
    gzip -f doc/*.$part
    install -m 644 doc/*.$part.gz $pkgdir/usr/share/man/man$part/
    for lang in {de,es,fr,ja,pl,pt}; do
      mkdir -p $pkgdir/usr/share/man/$lang/man$part
      gzip -f doc/$lang/*.$part || true
      install -m 644 doc/$lang/*.$part.gz $pkgdir/usr/share/man/$lang/man$part/\
                                                                        || true
    done
  done

  # ALL example configs
  mkdir -p $pkgdir/usr/share/doc/apt/examples
  install -m 644 doc/examples/* $pkgdir/usr/share/doc/apt/examples

  # ALL locales
  mkdir -p $pkgdir/usr/share/locale
  for lang in locale/*; do
    mkdir -p $pkgdir/usr/share/$lang/LC_MESSAGES
    install -m 644 $lang/LC_MESSAGES/* $pkgdir/usr/share/$lang/LC_MESSAGES/
  done

  # libapt-pkg-dev
  mkdir -p $pkgdir/usr/include/apt-pkg
  install -m 644 include/apt-pkg/* $pkgdir/usr/include/apt-pkg/
}

# vim:set ts=2 sw=2 et:
