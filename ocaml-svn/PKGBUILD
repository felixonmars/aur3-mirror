# $Id$
# Maintainer: Nicolas Hureau <kalenz@lse.epita.fr>

pkgname=ocaml-svn
pkgver=11015
pkgrel=1
pkgdesc="A functional language with OO extensions - SVN trunk"
arch=('i686' 'x86_64')
url="http://caml.inria.fr/"
license=('LGPL2' 'custom: QPL-1.0')
groups=()
depends=('gdbm')
makedepends=('tk' 'ncurses>=5.6-7' 'libx11' 'gcc' 'subversion')
optdepends=('ncurses: advanced ncurses features' 'tk: advanced tk features')
provides=('ocaml')
conflicts=('ocaml')
replaces=()
backup=()
options=()
install=
source=()
noextract=()
md5sums=()

_svntrunk="http://caml.inria.fr/svn/ocaml/trunk"
_svnmod="ocaml-svn"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # Build
  ./configure -prefix /usr
  make world.opt || return 1
}

package() {
  cd "$srcdir/$_svnmod-build"

  install -D -m 644 LICENSE $pkgdir/usr/share/licenses/ocaml/LICENSE
  make PREFIX=$pkgdir/usr MANDIR=$pkgdir/usr/share/man install || return 1

  # Save >10MB with this one, makepkg only strips debug symbols.
  find $pkgdir/usr/lib -type f -name '*.so.*' -exec strip --strip-unneeded {} \;
}
