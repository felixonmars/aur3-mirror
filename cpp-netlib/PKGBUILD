# $Id$
# Maintainer: imotec <info(at)imotec(dot)nl>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

# for customization
_pkgname=cpp-netlib
_ext=
_basedir=/usr
_boost=boost${_ext}
# note: depends=() needs separate customization

pkgname=${_pkgname}${_ext}
pkgver=0.9.4
pkgrel=1
pkgdesc="A library that provides application layer protocol support using modern C++ techniques"
arch=('i686' 'x86_64')
url="http://cpp-netlib.github.com/"
license=('custom')
depends=('boost>=1.45.0')
optdepends=('openssl: for https')
makedepends=('cmake')
source=(https://github.com/downloads/cpp-netlib/cpp-netlib/$_pkgname-$pkgver.tar.gz)
md5sums=('884bb416b63a0c1bd0c8d5a95913136e')

build() {
  install -d $srcdir/build
  cd $srcdir/build
  cmake -DBOOST_ROOT=$_basedir ../"$_pkgname-$pkgver"
  make
}

package() {
  install -d ${pkgdir}${_basedir}/{include/${_pkgname},lib}
  install -d ${pkgdir}/usr/share/licenses/$pkgname

  #Install library files
  cd "$srcdir/$_pkgname-$pkgver"
  cp -r boost/ ${pkgdir}${_basedir}/include/${_pkgname}/
  install -Dm644 LICENSE_1_0.txt ${pkgdir}/usr/share/licenses/$pkgname/LICENSE_1_0.txt

  cd $srcdir/build
  cp -r libs/network/src/*.a ${pkgdir}${_basedir}/lib/
}
