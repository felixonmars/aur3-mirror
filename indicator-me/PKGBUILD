# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-me
pkgver=0.2.10
pkgrel=3
pkgdesc="A menu in the menu bar that represents you, the user, and the things that represent you in the computer"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-me"
license=('GPL')
groups=('ayatana')
depends=('libindicator>=0.3.0' 'libgwibber>=0.0.4' 'libdbusmenu>=0.2.2' 'telepathy-glib>=0.9.0' 'gconf>=2.0.0' 'ido>=0.1')
makedepends=('intltool')
options=(!libtool)
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        launchpad-export.tar.gz)
md5sums=('e1cad91b1d70764f3d2ac2ab35e3b172'
         '2ca93046cf88d4a937ce5c556ff537e8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  echo 'ace af am an ar ast az be bg bn br bs ca crh cv cy cs csb da de dv el en_AU en_CA en_GB eo es et eu fa fi fr fy ga gl gu gv he hi hr hu hy id is it ja ka kk km kn ko ku lb lt lv mi mk ml mn mr ms nb ne nl nn ny oc pa pl pt pt_BR ro ru sc sd si sk sl sn sq sr sv sw ta te th tl tr tt ug uk ur vi zh_CN zh_HK zh_TW' >po/LINGUAS

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
