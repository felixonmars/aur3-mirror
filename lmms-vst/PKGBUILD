# $Id: PKGBUILD 40769 2011-02-26 18:40:13Z mherych $
# Maintainer: mar77i <mysatyre at gmail dot com>
# Maintainer: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

pkgname=lmms-vst
_pkgbasename="${pkgname%-vst}"
pkgver=0.4.12
pkgrel=2
pkgdesc="The Linux MultiMedia Studio, with VeSTige support enabled"
arch=('i686' 'x86_64')
url="http://lmms.sourceforge.net/"
license=('GPL')
depends=('sdl' 'glib2' 'fluidsynth' 'libpng' 'libvorbis' 'libxft' 'libxinerama' 'qt' 'sdl_sound' 'libsamplerate' 'shared-mime-info' 'wine')
optdepends=('fftw: SpectrumAnalyzer plugin'
            'stk: STK instruments plugins')
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2')
provides=('lmms-extras' 'lmms')
conflicts=('lmms-extras' 'lmms')
options=('!libtool' '!makeflags')
install=lmms.install
source=("http://downloads.sourceforge.net/sourceforge/lmms/${_pkgbasename}-${pkgver}.tar.bz2")
md5sums=('0642aba9b6b71d2ed41b44cfde4004f6')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -USE=vst
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="${pkgdir}" install
}

