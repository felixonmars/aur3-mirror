pkgname=droidcam
pkgver=3.5
pkgrel=4
pkgdesc='A tool for using your android device as a wireless/usb webcam'
arch=('i686' 'x86_64')
url='http://www.dev47apps.com/'
license=('custom')
depends=('linux-headers' 'glibc' 'glib2' 'pcre' 'freetype2' 'gdk-pixbuf2' 'gtk2' 'pango')
options=('!strip')
optdepends=('v4l-utils: Userspace tools and conversion library for Video 4 Linux'
             'xf86-video-v4l: X.org v4l video driver')
install='droidcam.install'
source=("droidcam.rc"
        "droidcam.desktop"
        "install.patch")
        
sha1sums=('3887b0dcde9add66441b420956c276533726fb82'
          'f85b3f34f98908bd1327df0ea0e650dcbdcb07e9'
          'e5930f5926358386ce22f990e786d002e04c5d24')

[[ "${CARCH}" = "i686" ]] && _arch="x86" && sha1sums+=('5cec41421d60eb5c32e5eab9399dd80b38a2337d')
[[ "${CARCH}" = "x86_64" ]] && _arch="x64" && sha1sums+=('770f9645fcd4fd3273fede19df6cc075e4607835')

source+=("http://www.dev47apps.com/files/droidcam-${_arch}.tar.gz")

build() {
  cd $srcdir
  patch -p0 < ${srcdir}/install.patch
}

package() {
  # Install droidcam binary file
  cd $pkgdir
  mkdir -p usr/bin
  install -m755 ${srcdir}/${pkgname} $pkgdir/usr/bin/${pkgname}

  # Install droidcam initscript
  install -Dm0755 "${srcdir}/${pkgname}.rc" "${pkgdir}/etc/rc.d/${pkgname}"
  
  # Install the desktop icon and ".desktop" files
  install -dm0755 "${pkgdir}/usr/"{share/applications,share/pixmaps,share/$pkgname}
  install -m0644 "${srcdir}/icon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -m0644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install doc
  install -dm0755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -m0644 "${srcdir}/README" "${pkgdir}/usr/share/doc/$pkgname/README"

  # Install module sources
  install -dm0755 "${pkgdir}/usr/src/${pkgname}-${pkgver}"
  install -m0755 "${srcdir}/install" "${pkgdir}/usr/src/${pkgname}-${pkgver}/install"
  cp -a "${srcdir}/webcam" "${pkgdir}/usr/src/${pkgname}-${pkgver}/"

  # Write the build module file 
  echo "# Droidcam installation directory" > "${pkgdir}/usr/share/${pkgname}/build_tmp"
  echo "cd /usr/src/${pkgname}-${pkgver}" >> "${pkgdir}/usr/share/${pkgname}/build_tmp"
  echo "./install" >> "${pkgdir}/usr/share/${pkgname}/build_tmp"
  echo "cd -" >> "${pkgdir}/usr/share/${pkgname}/build_tmp"
  chmod 0755 "${pkgdir}/usr/share/${pkgname}/build_tmp"
}
