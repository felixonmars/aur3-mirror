# Maintainer: Raphael Scholer <rscholer@gmx.de>
pkgname=elementary-xfce-icons-git
_pkgname=elementary-xfce
pkgver=0.4_86_ge5a9319
pkgrel=4
pkgdesc='Elementary icon-theme with improved Xfce support'
arch=('any')
url='https://github.com/shimmerproject/elementary-xfce'
license=('GPL2')
makedepends=('git')
depends=('gnome-icon-theme')
conflicts=('elementary-xfce-icons')
install="${pkgname}.install"
source=("git+${url}")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  echo "$(git describe --always |sed 's#-#_#g;s#v##')"
}

package() {
  mkdir -p "${pkgdir}/usr/share/icons"

  cd "${_pkgname}"
  find ./ -type d -name "elementary-xfce*" -execdir cp -r {} "${pkgdir}/usr/share/icons/" \;

  cd "${pkgdir}/usr/share/icons"
  find ./ -type f \( \
      -name "AUTHORS" \
      -o -name "CONTRIBUTORS" \
      -o -name "LICENSE" \
    \) \
    -delete
}
# vim:set ts=2 sw=2 et:
