# Mantainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
pkgname=perl-pod2-it
pkgver=0.15
pkgrel=1
__pname=POD2-IT
pkgdesc='Italian Perldoc Translation Project'
arch=('any')
url='http://pod2it.sourceforge.net'
license=('GPL' 'PerlArtistic')
depends=('perl')
options=(!emptydirs !purge)
source=(http://sourceforge.net/projects/pod2it/files/pod2it/$__pname-$pkgver.tar.gz/download)
md5sums=('bfca4652e8a80589df252ca6e111b340')

build(){
    chmod -R 755 $__pname-$pkgver
    cd $__pname-$pkgver
    perl Makefile.PL INSTALLDIRS=vendor 
    make
}
check(){
    cd $__pname-$pkgver
    make test
}
package(){
    cd $__pname-$pkgver
    make install DESTDIR=$pkgdir 
    find ${pkgdir} -name perllocal.pod -delete
    find ${pkgdir} -name .packlist -delete
}
