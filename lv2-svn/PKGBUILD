# Maintainer : SpepS <dreamspepser at yahoo dot it>

_name=lv2
pkgname=lv2-svn
pkgver=656
pkgrel=1
pkgdesc="A standard for plugins and matching host applications, mainly targeted at audio processing and generation."
arch=('i686' 'x86_64')
url="http://lv2plug.in/"
license=('custom:ISC')
makedepends=('subversion' 'python2-rdflib' 'libsndfile' 'gtk2')
optdepends=('libsndfile: lv2-eg-sampler extension'
            'gtk2: lv2-eg-sampler extension')
conflicts=('lv2')
replaces=('lv2core')

_svntrunk=http://lv2plug.in/repo/trunk
_svnmod=lv2

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  python2 waf configure --prefix=/usr
  python2 waf build
}

package() {
  cd "$srcdir/$_svnmod-build"

  python2 waf install --destdir="$pkgdir/"

  # provides and conflicts
  _lv2ver=`grep "^VERSION" wscript | cut -d"'" -f2`
  provides=("lv2=$_lv2ver")
  for _ext in `find "$pkgdir" -name manifest.ttl | sort`; do
    _name=`basename $(dirname "$_ext") .lv2 | sed "/^lv2/! s/.*/lv2-&/"`
    _minv=`grep minorVersion "$_ext" | sed "s/.* \([0-9]\) .*/\1/"`
    _micv=`grep microVersion "$_ext" | sed "s/.* \([0-9]\) .*/\1/"`
    [[ $_minv ]] && [[ $_micv ]] && _vers=$_minv.$_micv || \
    _vers=`grep "^VERSION" plugins/${_name/lv2-}.lv2/wscript | cut -d"'" -f2`
    provides+=("$_name=$_vers")
    conflicts+=("$_name")
  done

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
