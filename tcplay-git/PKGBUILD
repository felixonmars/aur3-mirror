# Maintainer: James Reed <supplantr at archlinux dot info>

_basename=tcplay
pkgname=${_basename}-git
pkgver=20120910
pkgrel=1
pkgdesc="A free and simple TrueCrypt implementation based on dm-crypt."
arch=('i686' 'x86_64')
url="https://github.com/bwalex/tc-play"
license=('BSD')
depends=('libgcrypt' 'util-linux' 'device-mapper')
makedepends=('git')
provides=(${_basename})
conflicts=(${_basename})
options=(!buildflags)

_gitroot="git://github.com/bwalex/tc-play.git"
_gitname="tc-play"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout."
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make PBKDF_BACKEND=gcrypt
}

package() {
  cd "$srcdir/$_gitname-build"
  install -Dm755 ${_basename} "${pkgdir}/usr/bin/${_basename}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_basename}/LICENSE"
  install -Dm644 ${_basename}.3 "${pkgdir}/usr/share/man/man3/${_basename}.3"
  install -Dm644 ${_basename}.8 "${pkgdir}/usr/share/man/man8/${_basename}.8"
}
