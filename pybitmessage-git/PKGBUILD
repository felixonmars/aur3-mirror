# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=pybitmessage-git
_gitname='PyBitmessage'
_realname='pybitmessage'
pkgver=ae22234
pkgrel=1
pkgdesc="Bitcoin inspired P2P encrypted messenger"
arch=('any')
depends=('hicolor-icon-theme' 'python2' 'python2-pyqt' 'qt4')
makedepends=('git')
url="https://github.com/Bitmessage/PyBitmessage"
license=('MIT')
source=(git://github.com/Bitmessage/PyBitmessage.git
        pybm.sh)
sha256sums=('SKIP'
            'c20eeeffd4931d8bcd50de5e6e1a974323ebbd3f5ad1d34483c6adeb3b5f3ae1')
provides=('pybitmessage' 'pybm')
conflicts=('pybitmessage')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd $_gitname

  msg 'Installing shared files...'
  mkdir -p "$pkgdir"/usr/share/${_realname}
  cp -R src/* "$pkgdir"/usr/share/${_realname}

  msg 'Installing desktop icons...'
  install -D -m644 desktop/${_realname}.desktop "${pkgdir}/usr/share/applications/${_realname}.desktop"
  install -D -m644 src/images/can-icon-24px.png "${pkgdir}/usr/share/icons/hicolor/24x24/apps/${_realname}.png"
  install -D -m644 desktop/can-icon.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${_realname}.svg"
  install -D -m644 desktop/can-icon.svg "${pkgdir}/usr/share/pixmaps/${_realname}.svg"

  msg 'Installing executable...'
  install -D -m755 ${srcdir}/pybm.sh "${pkgdir}/usr/bin/pybm"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
