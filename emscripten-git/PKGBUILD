# Maintainer: Benoit Favre <benoit.favre@gmail.com>
# Contributor: Sam Bliss <m13253@hotmail.com> -- new VCS guidelines for pacman 4.1 
pkgname=emscripten-git
pkgver=20130528
pkgrel=1
pkgdesc="an LLVM-to-JavaScript compiler"
arch=(i686 x86_64)
url="http://emscripten.org"
license=('MIT')
depends=('clang' 'llvm' 'nodejs>=0.5.5' 'python2>=2.7')
makedepends=('git')
conflicts=('emscripten')
provides=('emscripten')
source=('git://github.com/kripken/emscripten.git'
        emscripten-wrapper.sh emscripten-config)
md5sums=('SKIP'
         'ac5c217faf52c6ec1c4a9c6f44e8d95d'
         '1ba5ed180447443da6d0df7a4fe1fa60')
install=$pkgname.install

pkgver() {
  cd "$srcdir/emscripten"
  git log -1 --format="%cd" --date=short | tr -d -
}

build() {
  cd "$srcdir/emscripten"
  sed -i 's/\<python\>/python2/g' $(find . -name \*.py) emcc em++ emar em-config emlibtool emconfigure emmake
}

package() {
  cd "$srcdir/emscripten"
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/lib/emscripten"
  mkdir -p "$pkgdir/usr/lib/emscripten/tests"
  cp -r -L emcc em++ emar em-config emlibtool emconfigure emmake emranlib emscripten.py src/ system/ tools/ third_party/ tools/ "$pkgdir/usr/lib/emscripten"
  cp -r -L tests/hello* "$pkgdir/usr/lib/emscripten/tests"
  install -m 0755 $srcdir/emscripten-wrapper.sh "$pkgdir/usr/bin"
  install -m 0644 $srcdir/emscripten-config "$pkgdir/usr/lib/emscripten"
  cd "$pkgdir/usr/bin"
  for i in emcc em++ emar em-config emlibtool emconfigure emmake emranlib
  do
    ln -s emscripten-wrapper.sh $i
  done
}

# vim:set ts=2 sw=2 et:
