# Maintainer: Vlad Kolotvin <vlad.kolotvin@gmail.com>
pkgname=emscripten-git
pkgver=1.15.0
pkgrel=1
pkgdesc="an LLVM-to-JavaScript compiler"
arch=(i686 x86_64)
url="http://emscripten.org"
license=('MIT')
depends=('nodejs' 'python2')
makedepends=('git')
conflicts=('emscripten')
provides=('emscripten')
source=('git://github.com/kripken/emscripten.git#branch=incoming'
        'git://github.com/kripken/emscripten-fastcomp.git#branch=incoming'
        'git://github.com/kripken/emscripten-fastcomp-clang.git#branch=incoming'
        'emscripten.sh'
        'emscripten.config')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         '543c109cf8753f5d834d1b206312f049'
         'c8b7db20a44edb41c8675812e17b5c42')

pkgver() {
    cd $srcdir/emscripten
    cat emscripten-version.txt
}

prepare() {
    ln -s $srcdir/emscripten-fastcomp-clang $srcdir/emscripten-fastcomp/tools/clang
}

build() {
    cd $srcdir/emscripten-fastcomp
    mkdir build
    cd build
    cmake .. -DPYTHON_EXECUTABLE=/usr/bin/python2 \
             -DCMAKE_BUILD_TYPE=Release \
             -DLLVM_TARGETS_TO_BUILD="X86;JSBackend" \
             -DLLVM_BUILD_RUNTIME=OFF \
             -DLLVM_INCLUDE_EXAMPLES=OFF \
             -DLLVM_INCLUDE_TESTS=OFF
    make
}

package() {
    mkdir -p $pkgdir/opt/emscripten-fastcomp
    mkdir -p $pkgdir/usr/lib/emscripten
    mkdir -p $pkgdir/etc/profile.d
    mkdir -p $pkgdir/usr/bin

    cp -R $srcdir/emscripten-fastcomp/build/bin/* $pkgdir/opt/emscripten-fastcomp
    install -m 0755 $srcdir/emscripten-fastcomp/emscripten-version.txt $pkgdir/opt/emscripten-fastcomp
    install -m 0755 $srcdir/emscripten.sh $pkgdir/etc/profile.d/
    for i in em++ emar emcc em-config emconfigure emmake emranlib emrun emscons
    do
        ln -s /usr/lib/emscripten/$i "$pkgdir/usr/bin/$i"
    done

    cd "$srcdir/emscripten"

    sed -i 's/\<python\>/python2/g' $(find . -name \*.py) em++ emar emcc em-config emconfigure emmake emranlib emrun emscons
    
    cp -R em* cmake/ scons-tools/ src/ system/ third_party/ tools/ $pkgdir/usr/lib/emscripten
    install -m 0755 $srcdir/emscripten.config $pkgdir/usr/lib/emscripten/tools/settings_template_readonly.py
}

# vim:set ts=2 sw=2 et:
