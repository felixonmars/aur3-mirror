pkgname=mingw32-openssl
pkgver=1.0.1a
pkgrel=1
arch=('any')
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (mingw32)"
depends=('mingw32-runtime' 'mingw32-zlib')
options=(!strip !buildflags)
license=('BSD')
url="http://www.openssl.org"
source=("http://openssl.org/source/openssl-$pkgver.tar.gz")
md5sums=('a0104320c0997cd33e18b8ea798609d1')

build()
{
  cd "$srcdir/openssl-$pkgver"
  unset LDFLAGS
  ./Configure \
    --cross-compile-prefix=i486-mingw32- \
    -DOPENSSL_NO_CAPIENG \
    --prefix=/usr/i486-mingw32 \
    mingw \
    shared \
    zlib-dynamic
  make
}

package() {
  cd "$srcdir/openssl-$pkgver"
  make INSTALL_PREFIX=$pkgdir install
  i486-mingw32-strip $pkgdir/usr/i486-mingw32/bin/*.exe
  i486-mingw32-strip -x -g $pkgdir/usr/i486-mingw32/bin/*.dll
  i486-mingw32-strip -g $pkgdir/usr/i486-mingw32/lib/*.a
  i486-mingw32-strip -x -g $pkgdir/usr/i486-mingw32/lib/engines/*.dll
  rm -rf $pkgdir/usr/i486-mingw32/{bin/c_rehash,ssl/misc,ssl/man}
}
