# Maintainer: mickele <mimocciola at yahoo dot com>
# Contributor: Ilmari Repo <ilmari at gmail dot com> (librecad-svn PKGBUILD)
# Contributor: GazJ Gary James <garyjames82 at gmail dot com> (CADuntu PKGBUILD)

pkgname=librecad-git
pkgver=20110621
pkgrel=1
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/librecad/"
license=('GPL')
depends=('qt')
makedepends=('git')
conflicts=('librecad')
provides=('librecad')
replaces=('librecad-svn' 'caduntu' 'caduntu-svn')
source=('librecad.desktop')
md5sums=('19dcb83a3fcdb3752439095b118ac6d3')

_gitroot="https://github.com/LibreCAD/LibreCAD.git"
_gitname="LibreCAD"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname}/.git ] ; then
    cd "${_gitname}"

    # Change remote url to anongit
    if [ -z $( git branch -v | grep anongit ) ] ; then
        git remote set-url origin ${_gitroot}
    fi
    
    git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  # patch to solve an issue caused by gcc-4.6
  sed -e "s|LiteralMask<Value_t, n>::mask;|LiteralMask<Value_t, static_cast<unsigned int>(n)>::mask;|" \
      -e "s|SimpleSpaceMask<n>::mask;|SimpleSpaceMask<static_cast<unsigned int>(n)>::mask;|" \
      -i "${srcdir}/LibreCAD/fparser/fparser.cc"

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd "${srcdir}/${_gitname}"
  qmake
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  install -D -m 755 unix/librecad $pkgdir/usr/bin/librecad
  install -D -m 644 "${srcdir}/librecad.desktop" "${pkgdir}/usr/share/applications/librecad.desktop"
  install -D -m 644 res/main/librecad.png "${pkgdir}/usr/share/pixmaps/librecad.png"
  mkdir -p "${pkgdir}/usr/share/librecad/"
  cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/librecad/"
}
