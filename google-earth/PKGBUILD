# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributors: 458italia <svenskaparadox@gmail.com>, Madek <gonzaloseguel@gmail.com>, Berseker <berseker86@gmail.com>

pkgname=google-earth
pkgver=6.2.2.6613
pkgrel=1
pkgdesc="A 3D interface to view satellite images of Earth and other objects"
arch=('i686' 'x86_64')
url="http://earth.google.com"
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'ld-lsb' 'lib32-fontconfig' 'lib32-gcc-libs' 'lib32-libsm' 'lib32-libxrender' 'lib32-mesa' 'shared-mime-info')
optdepends=('lib32-ati-dri: For the open source ATI driver users'
            'lib32-catalyst-utils: For AMD Catalyst users'
            'lib32-gtk2: SCIM support'
            'lib32-intel-dri: For the open source Intel driver users'
            'lib32-nouveau-dri: For the open source Nouveau driver users'
            'lib32-nss-mdns: In case the application fails to contact the servers'
            'lib32-nvidia-utils: For the NVIDIA proprietary driver users'
            'lib32-nvidia-utils-bumblebee: For the NVIDIA proprietary driver + Bumblebee users'
            'qt: For changing the font size with qtconfig'
            'ttf-ms-fonts: Fonts')
options=('!emptydirs')
install=$pkgname.install
_arch=i386  # Workaround for the AUR Web interface Source parser
[ "$CARCH" = 'x86_64' ] && _arch=amd64
source=("http://packages.linuxmint.com/pool/import/g/googleearth/googleearth_$pkgver-r0_$_arch.deb"
        "$url/intl/en/license.html"
        "$pkgname-mimetypes.xml")
md5sums=('bd1960eb3e2d061b319c3f38835840d7'
         '7363c6144ebb298b1d7aec713ca8a82a'
         'e3c67b8d05c3de50535bd7e45eee728e')

if [[ "$CARCH" == "i686" ]]; then
  depends=('desktop-file-utils' 'fontconfig' 'hicolor-icon-theme' 'ld-lsb' 'libsm' 'libxrender' 'mesa' 'shared-mime-info')
  optdepends=('ati-dri: For the open source ATI driver users'
              'catalyst-utils: For AMD Catalyst users'
              'gtk2: SCIM support'
              'intel-dri: For the open source Intel driver users'
              'nouveau-dri: For the open source Nouveau driver users'
              'nss-mdns: In case the application fails to contact the servers'
              'nvidia-utils: For the NVIDIA proprietary driver users'
              'nvidia-utils-bumblebee: For the NVIDIA proprietary driver + Bumblebee users'
              'qt: For changing the font size with qtconfig'
              'ttf-ms-fonts: Fonts')
  md5sums[0]='e38c0d806cf1d581213fb6f4f204ec9c'
fi

_instdir=/opt/google/earth/free/
PKGEXT=".pkg.tar"

package() {
  msg2 "Extracting the data.tar.gz"
  bsdtar -xf data.tar.gz -C "$pkgdir/"

  msg2 "Installing the .desktop file, the shared MIME info package, icons and the license"
  mv "$pkgdir/$_instdir/$pkgname.desktop" "$pkgdir/usr/share/applications/"
  install -Dm644 $pkgname-mimetypes.xml "$pkgdir/usr/share/mime/packages/$pkgname-mimetypes.xml"

  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 "$pkgdir/$_instdir/product_logo_${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done

  install -Dm644 license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"

  msg2 "Removing duplicated images"
  rm "$pkgdir"/$_instdir/product_logo_*.png
}