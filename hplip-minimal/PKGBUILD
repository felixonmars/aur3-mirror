# Maintainer: megadriver <megadriver at gmx dot com>
# Based on hplip from [extra]

pkgname=hplip-minimal
_pkgname=hplip
pkgver=3.12.6
pkgrel=1
pkgdesc="The HP printer drivers, and not much else"
arch=('i686' 'x86_64')
url="http://hplipopensource.com"
license=('GPL')
depends=('ghostscript>=8.64-6')
makedepends=('cups' 'libusbx')
optdepends=('cups: for printing support'
            'libusbx: for advanced usb support')
conflicts=('hplip')
options=('!docs' '!libtool')
source="http://downloads.sourceforge.net/${_pkgname}/$_pkgname-$pkgver.tar.gz"
md5sums=('5303938e8630775ea6fb383af85775e5')

build() {
 cd "$srcdir/$_pkgname-$pkgver"

 export AUTOMAKE='automake --foreign'
 autoreconf --force --install

 ./configure --prefix=/usr \
             --disable-qt4 \
             --disable-doc-build \
             --disable-dbus-build \
             --disable-network-build \
             --disable-scan-build \
             --disable-fax-build \
             --disable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-only-build \
             --enable-new-hpcups \
             --disable-cups-drv-install \
             --enable-cups-ppd-install \
             --enable-pp-build \

 make
}

package() {
 cd "$srcdir/$_pkgname-$pkgver"
 make DESTDIR="$pkgdir/" install
 # remove config provided by sane and autostart of hp-daemon
 rm -rf "$pkgdir"/etc/{sane.d,xdg}
 # remove HAL .fdi file because HAL is no longer used
 rm -rf "$pkgdir"/usr/share/hal
}
