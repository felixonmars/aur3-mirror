# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>
pkgname=ocaml-zip
pkgver=1.04
pkgrel=6
pkgdesc="zip and gzip library for ocaml"
arch=('i686' 'x86_64')
url="http://cristal.inria.fr/~xleroy/software.html#camlzip"
license=('GPL')
depends=('ocaml' 'zlib')
makedepends=('ocaml-findlib')
options=(!strip)
source=(http://forge.ocamlcore.org/frs/download.php/328/camlzip-$pkgver.tar.gz
        http://sources.gentoo.org/viewcvs.py/*checkout*/gentoo-x86/dev-ml/camlzip/files/camlzip-1.03-Makefile-findlib.patch?rev=1.2
        http://sources.gentoo.org/viewcvs.py/*checkout*/gentoo-x86/dev-ml/camlzip/files/META)
md5sums=('b3930f4f2e2563b9a5e1b17aa455e635'
         '0e0f9c6860c23f4efef72563969188dd'
         '357f403e3152810089ea2f7bb86bf77d')

build() {
  cd "$srcdir/camlzip-$pkgver"

  cp ../camlzip-1.03-Makefile-findlib.patch?rev=1.2 makefile.patch
  sed -ie 's|$(OCAMLFIND) install camlzip|$(OCAMLFIND) install $(OCAMLFIND_INSTFLAGS) zip|' makefile.patch || return 1
  patch -i makefile.patch || return 1

  ln -s $startdir/META META
  sed -ie 's|version=.*|version="'$pkgver'"|' META || return 1

  make all allopt || return 1
}

package() {
  cd "$srcdir/camlzip-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  export OCAMLFIND_LDCONF="ignore"

  mkdir -p "$OCAMLFIND_DESTDIR/stublibs"
  make install || return 1
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
