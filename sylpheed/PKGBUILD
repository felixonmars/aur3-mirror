# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sylpheed
pkgver=3.1.3
pkgrel=1
pkgdesc="Lightweight and user-friendly e-mail client"
arch=('i686' 'x86_64')
url="http://sylpheed.sraoss.jp/en/"
license=('GPL')
depends=('gpgme' 'gtkspell')
makedepends=('compface' 'openssl')
options=('!libtool')
install="$pkgname.install"
source=("http://sylpheed.sraoss.jp/$pkgname/v3.1/$pkgname-$pkgver.tar.bz2")
md5sums=('2ad17c0f822b266d83dfba11c3088659')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --enable-ldap
  make

  # Build Attachment-Tool Plug-in
  cd plugin/attachment_tool && make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Install Attachment-Tool Plug-in
  cd plugin/attachment_tool && make DESTDIR="$pkgdir/" install-plugin
}
