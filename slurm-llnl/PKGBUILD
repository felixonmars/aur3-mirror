# Maintainer: Fredrik Tegenfeldt <fredrik.tegenfeldt AT unige DOT ch>
pkgname=slurm-llnl
pkgver=2.4.4
pkgrel=1
pkgdesc="Simple Linux Utility for Resource Management"
arch=('i686' 'x86_64')
license=('GPL')
url="https://computing.llnl.gov/linux/slurm/"
groups=()
depends=('python2' 'libmysqlclient>=5.0.0' 'postgresql-libs' 'munge' 'gtk2' 'lua' 'hwloc')
makedepends=('openssl>=0.9.6' 'readline' 'lua')
provides=(slurm)
conflicts=()
replaces=()
backup=()
options=()
install=${pkgname}.install
changelog=
source=("slurm-llnl-tmpfiles.conf" "slurmdbd-llnl.service" "slurmd-llnl.service" "slurmctld-llnl.service" "arch_rc.d.slurm" "arch_rc.d.slurmdbd" "shtml2html.py.patch" "http://www.schedmd.com/download/latest/slurm-${pkgver}.tar.bz2")
noextract=()

md5sums=('51e4ae2c51edf7c145d1b87bec4c344e'
         '06dc8133b1819e88a4cbfd4c1988663d'
         '075fb291b08f8b3331436ccea17b9aa4'
         'dfeb751b1eba6039a44b74f376a1e0d7'
         'f5944208daf02cac82016d80a5619daa'
         '35e726a55591db9734110d6e09bbaadd'
         '9d8f907b340cedc28f6148fb8665f0c2'
         '0439d711488f38a1ab363d048e363818')

build() {
  cd "$srcdir/slurm-$pkgver"

  patch -p0 -i ../shtml2html.py.patch

  ./configure --prefix=/usr           \
              --sysconfdir=/etc/slurm \
	      --localstatedir=/       \
              --with-mysql            \
              --with-ssl              \
              --with-munge            \
              --with-readline
  make
}

package() {
  cd "$srcdir/slurm-$pkgver"

  make DESTDIR="$pkgdir/" install

  # slurm configs and license
  install -D -m644 etc/slurm.conf.example    "${pkgdir}/etc/slurm/slurm.conf.example"
  install -D -m644 etc/slurmdbd.conf.example "${pkgdir}/etc/slurm/slurmdbd.conf.example"
  install -D -m644 LICENSE.OpenSSL           "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.OpenSSL"
  install -D -m644 COPYING                   "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # init related install
  install -D -m755 ../arch_rc.d.slurm    "${pkgdir}/etc/rc.d/slurm"
  install -D -m755 ../arch_rc.d.slurmdbd "${pkgdir}/etc/rc.d/slurmdbd"

  # SystemD related install
  install -D -m644 ../slurmd-llnl.service        "${pkgdir}/usr/lib/systemd/system/slurmd-llnl.service"
  install -D -m644 ../slurmctld-llnl.service     "${pkgdir}/usr/lib/systemd/system/slurmctld-llnl.service"
  install -D -m644 ../slurmdbd-llnl.service      "${pkgdir}/usr/lib/systemd/system/slurmdbd-llnl.service"
  install -D -m644 ../slurm-llnl-tmpfiles.conf   "${pkgdir}/etc/tmpfiles.d/slurm-llnl-tmpfiles.conf"
}
