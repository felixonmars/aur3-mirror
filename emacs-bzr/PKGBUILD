# Maintainer: Neil Santos <nsantos16+aur@gmail.com>

pkgname=emacs-bzr
pkgver=103710
pkgrel=1
pkgdesc='GNU Emacs from official Bzr repository'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/emacs/'
license=('GPL3')
depends=('dbus-core' 'libpng' 'libtiff' 'librsvg' 'giflib' 'gtk2' 'libxpm' 'libjpeg>=7' 'hicolor-icon-theme')
makedepends=('bzr' 'pkgconfig' 'texinfo' 'gpm')
provides=('emacs=$pkgver')
conflicts=('emacs' 'emacs-nox' 'emacs-otf' 'emacs-cvs' 'emacs-git')
install=$pkgname.install

_bzrtrunk='http://bzr.savannah.gnu.org/r/emacs/trunk'
_bzrmod='emacs'

build() {
  cd $srcdir
  msg "Connecting to Savannah..."

  if [[ -d $_bzrmod/.bzr ]]; then
    (cd $_bzrmod && bzr update -v && cd ..)
    msg "Local checkout updated or server timeout"
  else
    bzr co --lightweight -v $_bzrtrunk $_bzrmod
    msg "Checkout done or server timeout"
  fi

  cp -urT $_bzrmod/ ${_bzrmod}-build
  cd ${_bzrmod}-build

  #  msg "Trying to ensure building from a clean(-ish) state..."
  #  make clean

  mandir=/usr/share/man
  msg "Starting make..."
  ./autogen.sh && ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --mandir=${mandir} \
    --without-sound \
    --with-x-toolkit=gtk || return 1
  make bootstrap || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  msg "Cleaning up..."
  mv $pkgdir/usr/bin/{ctags,ctags.emacs} || return 1
  mv $pkgdir/usr/bin/{etags,etags.emacs} || return 1
  mv $pkgdir${mandir}/man1/{etags.1,etags.emacs.1}.gz || return 1
  mv $pkgdir${mandir}/man1/{ctags.1,ctags.emacs.1}.gz || return 1

  find $pkgdir/usr/share/emacs -type d -exec chmod 755 {} \;
  find $pkgdir/usr/share/emacs -exec chown root.root {} \;
  chmod 775 $pkgdir/var/games
  chmod 775 $pkgdir/var/games/emacs
  chmod 664 $pkgdir/var/games/emacs/*
  chown -R root:50 $pkgdir/var/games

  rm $pkgdir/usr/share/info/dir || return 1
}
