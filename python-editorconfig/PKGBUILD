# Maintainer: Hong Xu <hong@topbug.net>

pkgname=python-editorconfig
pkgver=0.12.0
pkgrel=1
pkgdesc="EditorConfig Core Library written in Python"
license=('PSF')
arch=('i686' 'x86_64')
url="http://editorconfig.org/"
depends=('python')
provides=(${pkgname})
source=("https://pypi.python.org/packages/source/E/EditorConfig/EditorConfig-${pkgver}.tar.gz"
        "remove-console.patch")
md5sums=('c7220a5deab54c56b67d3c2f9069138a'
         '9590b7df856e6e206465503cda825303')

prepare() {
  cd "${srcdir}/EditorConfig-${pkgver}"
  # Remove the console script since a faster executable is available in editorconfig-core-c
  patch -p0 setup.py <${srcdir}/remove-console.patch
}

build() {
  cd "${srcdir}/EditorConfig-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/EditorConfig-${pkgver}"
  /usr/bin/python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"

  cat << EEOOFF
  WARNING: This package has removed the executable /usr/bin/editorconfig file.
  If you need this executable, please install editorconfig-core-c
  (https://aur.archlinux.org/packages/editorconfig-core-c), which provides a
  faster executable.
EEOOFF
}

# TODO: Add check()
