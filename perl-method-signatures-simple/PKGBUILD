# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=perl-method-signatures-simple
_cpanname="Method-Signatures-Simple"
pkgver=1.02
pkgrel=2
pkgdesc="Basic method declarations with signatures, without source filters"
arch=('any')
url="http://search.cpan.org/~ruz/$_cpanname"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.5.0' 'perl-devel-declare')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/R/RH/RHESA/$_cpanname-$pkgver.tar.gz")
md5sums=('92444064d2b369d4427cb4ec6b652855')

# Function to change to the working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
	cd "$srcdir/$_cpanname-$pkgver"
	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment
	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment
	make test
}

package() {
	prepareEnvironment
	make install

	# Remove "perllocal.pod" and ".packlist".
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
