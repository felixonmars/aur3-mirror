# Maintainer: David Strobach <lalochcz@gmail.com>
# Contributor: Paolo Stivanin <admin AT polslinux DOT it>

pkgname=rpm
pkgver=5.3.11
pkgrel=1
_pkgrel=0
_pkgreldate=20110602
pkgdesc="The RedHat Package Manager"
arch=('i686' 'x86_64')
url="http://rpm5.org/"
license=('GPL')
groups=('system')
depends=('perl' 'python2' 'zlib' 'beecrypt' 'pcre' 'file' 'neon' 'xz' 'popt' 'db5.1')
conflicts=("${pkgname}-org" 'rpmextract')
options=('!emptydirs')
source=("http://rpm5.org/files/$pkgname/$pkgname-5.3/$pkgname-$pkgver-$_pkgrel.$_pkgreldate.src.rpm")
md5sums=('5187a796c2fed092544c3c6c0f3db55a')

build() {
  msg "Preparing sources"
  bsdtar -xf $pkgname-$pkgver.tar.gz
  msg "Starting build"
  cd $pkgname-$pkgver
  LDFLAGS="${LDFLAGS} -Wl,--no-as-needed"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-python \
              --with-python-inc-dir=/usr/include/python2.7 \
              --with-python-lib-dir=/usr/lib/python2.7 \
              --with-ruby=none \
              --without-selinux \
              --with-popt=/usr/lib \
              --with-pcre=/usr/lib \
              --with-bzip2=/usr/lib \
              --with-xz=/usr/lib \
              --with-file=/usr/lib \
              --with-neon=external \
              --with-beecrypt=external \
              --disable-openmp \
              --disable-build-lafiles
  make DESTDIR="$pkgdir"
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
