# Maintainer: Taylor Venable <taylor@metasyntax.net>

_pkgname='bigloo'
pkgname="${_pkgname}-devel"
_pkgver='3.9b-alpha'
_pkgsuffix='27Nov12'
pkgver="${_pkgver/-/_}"
pkgrel=2
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=('openssl' 'sqlite3')
makedepends=('java-environment' 'zip')
optdepends=('emacs')
provides=('bigloo')
conflicts=('bigloo')
options=('!makeflags')
install=bigloo.install
source=("ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${_pkgname}${_pkgver}${_pkgsuffix}.tar.gz"
        'satisfy-ldconfig.sh')
md5sums=('3f24ca0d264d33dd8d27c59306794658'
         'c253eb5651c81204f6c16b89c3c2cb6a')

build() {
  cd "${srcdir}/${_pkgname}${_pkgver%-*}"
  ./configure --prefix=/usr \
    --enable-ssl \
    --enable-sqlite \
    --disable-gstreamer \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --jvm=yes
  make build
}

package() {
  cd "${srcdir}/${_pkgname}${_pkgver%-*}"
  make DESTDIR="${pkgdir}" install
  make -C manuals DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}/usr/lib/bigloo/${_pkgver%-*}"/*.a

  # Slake ldconfig's thirst for symlinks.
  find "${pkgdir}/usr/lib/bigloo/${_pkgver%-*}" -type f -name '*_es-*.so' \
    -exec "${startdir}/satisfy-ldconfig.sh" '{}' \;
}
