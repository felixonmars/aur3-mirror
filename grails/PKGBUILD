# Contributor: Alexander Rødseth <rodseth@gmail.com>
pkgname=grails
pkgver=1.3.7
pkgrel=3
pkgdesc="Groovy on rails"
arch=('any')
url="http://grails.org/"
depends=('java-environment' 'junit' 'bash' 'sh')
makedepends=('apache-ant')
optdepends=('groovy')
license=('Apache')
source=("http://dist.springframework.org.s3.amazonaws.com/release/GRAILS/$pkgname-$pkgver.zip")
md5sums=('ea7cbfecdcf30c861f8faa8552ce3b46')

build() {
  srcdir="$srcdir/$pkgname-$pkgver"
  cd "$srcdir"

  #msg "Building..."
  #ANT_OPTS=" -Xms512m -Xmx1g" ant

  msg2 "Configuring paths..."
  sed -i 's:DIRNAME=`dirname "$0"`:DIRNAME=\/usr\/share\/grails:' \
    bin/grails
  sed -i 's:DIRNAME=`dirname "$0"`:DIRNAME=\/usr\/share\/grails:' \
    bin/grails-debug

  msg2 "Creating wrapperscript..."
  echo '#!/bin/sh' > grails
  echo 'if [ ! -f $GRAILS_HOME/startGrails ]; then' >> grails
  echo '  GRAILS_HOME=/usr/share/grails' >> grails
  echo 'fi' >> grails
  echo 'DIRNAME=$GRAILS_HOME' >> grails
  echo '. "$DIRNAME/startGrails"' >> grails
  echo 'startGrails org.codehaus.groovy.grails.cli.GrailsScriptRunner "$@"' \
    >> grails
}

package() {
  srcdir="$srcdir/$pkgname-$pkgver"
  cd "$srcdir"

  msg2 "Packaging executables..."
  install -Dm755 bin/grails-debug \
    "$pkgdir/usr/share/$pkgname/bin/grails-debug"
  install -Dm755 bin/startGrails \
    "$pkgdir/usr/share/$pkgname/startGrails"

  install -Dm755 grails "$pkgdir/usr/bin/$pkgname"
  install -Dm755 grails "$pkgdir/usr/share/$pkgname/bin/$pkgname"

  msg2 "Packaging jars..."
  mkdir -p "$pkgdir/usr/share/$pkgname/lib/"
  cp $srcdir/lib/*.jar "$pkgdir/usr/share/$pkgname/lib/"
  mkdir -p "$pkgdir/usr/share/$pkgname/dist/"
  cp $srcdir/dist/*.jar "$pkgdir/usr/share/$pkgname/dist/"

  msg2 "Packaging extensible stylesheet language files..."
  cp $srcdir/lib/*.xsl "$pkgdir/usr/share/$pkgname/lib/"

  msg2 "Packaging class and sourcefiles..."
  mkdir -p "$pkgdir/usr/share/$pkgname/target/"
  cp -r "$srcdir/src" "$pkgdir/usr/share/$pkgname/"

  msg2 "Packaging icons..."
  mkdir -p "$pkgdir/usr/share/pixmaps/"
  cp $srcdir/media/icons/*.png "$pkgdir/usr/share/pixmaps/"

  msg2 "Packaging plugins..."
  mkdir -p "$pkgdir/usr/share/$pkgname/plugins/"
  cp $srcdir/plugins/*.zip "$pkgdir/usr/share/$pkgname/plugins/"

  msg2 "Packaging scripts..."
  mkdir -p "$pkgdir/usr/share/$pkgname/scripts/"
  cp $srcdir/scripts/*.groovy "$pkgdir/usr/share/$pkgname/scripts/"
  cp "$srcdir/scripts/log4j.properties" "$pkgdir/usr/share/$pkgname/scripts/"

  msg2 "Packaging documentation..."
  mkdir -p "$pkgdir/usr/share/doc/$pkgname-$pkgver/"
  cp README "$pkgdir/usr/share/doc/$pkgname-$pkgver/"
  cp -r "$srcdir/doc/api" "$pkgdir/usr/share/doc/$pkgname-$pkgver/"
  
  msg2 "Packaging settings and configuration files..."
  mkdir -p "$pkgdir/usr/share/$pkgname/conf/"
  cp $srcdir/conf/* \
    "$pkgdir/usr/share/$pkgname/conf/"
  echo "export GRAILS_HOME=/usr/share/grails" > "$srcdir/grails.sh"
  install -Dm755 "$srcdir/grails.sh" \
    "$pkgdir/etc/profile.d/grails.sh"
  install -Dm644 "$srcdir/build.properties" \
    "$pkgdir/usr/share/$pkgname/build.properties"
  install -Dm644 "$srcdir/src/grails/ant/build.xml" \
    "$pkgdir/usr/share/$pkgname/build.xml"

  msg2 "Packaging license..."
  install -Dm644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
