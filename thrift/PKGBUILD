# Contributor: Byron Clark <byron@theclarkfamily.name>
# based on thrift-git PKGBUILD
pkgname=thrift
pkgver=0.8.0
pkgrel=5
pkgdesc="Scalable cross-language services framework for IPC/RPC"
arch=(i686 x86_64)
url="http://thrift.apache.org/"
license=(APACHE)
depends=(boost-libs)
makedepends=(boost java-environment apache-ant python2 php perl perl-bit-vector perl-class-accessor)
optdepends=('python2: to use Python bindings'
            'java-environment: to use Java bindings'
            'php: to use PHP bindings'
            'perl: to use Perl bindings'
            'perl-bit-vector: to use Perl bindings'
            'perl-class-accessor: to use Perl bindings')
options=(!emptydirs)
source=(http://www.apache.org/dist/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
        maven-repo-path.patch
        thrift-phpext.patch)
md5sums=('d29dfcd38d476cbc420b6f4d80ab966c'
         '65bbe2a4260d9f7e3a8e385919788ff0'
         '22ac4d4d06f2d61d6aa21607dd3c9743')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -p1 -i $srcdir/maven-repo-path.patch
  patch -p1 -i $srcdir/thrift-phpext.patch

  # apache-ant is not installed in a normal path location
  . /etc/profile.d/apache-ant.sh

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --without-ruby
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  # ViM syntax file
  install -d -m 0755 $pkgdir/usr/share/vim/vimfiles/syntax
  install -m 0644 contrib/thrift.vim $pkgdir/usr/share/vim/vimfiles/syntax
}

# vim:set ts=2 sw=2 et:
