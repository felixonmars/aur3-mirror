# Contributor: Byron Clark <byron@theclarkfamily.name>
# based on thrift-git PKGBUILD
pkgname=thrift
pkgver=0.8.0
pkgrel=2
pkgdesc="Scalable cross-language services framework for IPC/RPC"
arch=(i686 x86_64)
url="http://thrift.apache.org/"
license=(APACHE)
depends=(boost)
makedepends=(java-environment apache-ant python2 php perl perl-bit-vector perl-class-accessor)
optdepends=('python2: to use Python bindings'
            'java-environment: to use Java bindings'
            'php: to use PHP bindings'
            'perl: to use Perl bindings'
            'perl-bit-vector: to use Perl bindings'
            'perl-class-accessor: to use Perl bindings')
options=(!emptydirs)
source=(http://www.apache.org/dist/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
        maven-repo-path.patch)
md5sums=('d29dfcd38d476cbc420b6f4d80ab966c'
         '65bbe2a4260d9f7e3a8e385919788ff0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i $srcdir/maven-repo-path.patch

  # apache-ant is not installed in a normal path location
  . /etc/profile.d/apache-ant.sh

  PYTHON=/usr/bin/python2 /bin/sh ./configure --prefix=/usr

  # compiler
  make -C compiler/cpp
  install -d -m 0755 $pkgdir/usr/bin
  install -m 0755 compiler/cpp/thrift $pkgdir/usr/bin

  # C++ library
  make -C lib/cpp
  make -C lib/cpp DESTDIR=$pkgdir install

  # Python library
  pushd lib/py
  python2 setup.py build
  python2 setup.py install --no-compile --root=$pkgdir
  popd

  # Java library
  pushd lib/java
  ant
  install -d -m 0755 $pkgdir/usr/share/java/thrift
  install -D -m 0644 build/libthrift-${pkgver}.jar $pkgdir/usr/share/java/thrift
  popd

  # PHP library
  pushd lib/php/src
  install -d -m 0755 $pkgdir/usr/share/php/thrift
  for ITEM in *; do
    if [ "$ITEM" != ext ]; then
      cp -R "$ITEM" $pkgdir/usr/share/php/thrift
    fi
  done
  find $pkgdir/usr/share/php/thrift -type d | xargs chmod 0755
  find $pkgdir/usr/share/php/thrift -type f | xargs chmod 0644
  pushd ext/thrift_protocol
  phpize
  /bin/sh ./configure
  make
  make INSTALL_ROOT=$pkgdir install
  popd
  popd

  # Perl library
  pushd lib/perl
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
  make install DESTDIR=$pkgdir
  find $pkgdir -name perllocal.pod -delete
  find $pkgdir -name .packlist -delete
  popd

  # ViM syntax file
  install -d -m 0755 $pkgdir/usr/share/vim/vimfiles/syntax
  install -m 0644 contrib/thrift.vim $pkgdir/usr/share/vim/vimfiles/syntax
}

# vim:set ts=2 sw=2 et:
