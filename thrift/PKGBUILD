# Contributor: Byron Clark <byron@theclarkfamily.name>
# based on thrift-git PKGBUILD
pkgname=thrift
pkgver=0.6.1
pkgrel=1
pkgdesc="Scalable cross-language services framework for IPC/RPC between C++, Java, Ruby, PHP, Python, C#, Haskell, Erlang, etc."
arch=(i686 x86_64)
url="http://incubator.apache.org/thrift/"
license=(APACHE)
depends=(boost)
makedepends=(java-environment apache-ant python2 ruby php perl perl-bit-vector perl-class-accessor)
optdepends=('python2: to use Python bindings'
            'ruby: to use Ruby bindings'
            'java-environment: to use Java bindings'
            'php: to use PHP bindings'
            'perl: to use Perl bindings'
            'perl-bit-vector: to use Perl bindings'
            'perl-class-accessor: to use Perl bindings')
options=(!emptydirs)
source=(http://www.apache.org/dist/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
        use-ruby-strlcpy.patch
        maven-repo-path.patch)
md5sums=('e1ec722d5f38077a23a32c4de4d4ce94'
         '0b3365d831d97c92a6ee6ca12e08655c'
         '8f5ccb4dcab4b31bdfcd443284a37ece')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i $srcdir/use-ruby-strlcpy.patch
  patch -p1 -i $srcdir/maven-repo-path.patch

  # apache-ant is not installed in a normal path location
  . /etc/profile.d/apache-ant.sh

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr

  # compiler
  make -C compiler/cpp
  install -d -m 0755 $pkgdir/usr/bin
  install -m 0755 compiler/cpp/thrift $pkgdir/usr/bin

  # C++ library
  make -C lib/cpp
  make -C lib/cpp DESTDIR=$pkgdir install

  # Python library
  pushd lib/py
  python2 setup.py build
  python2 setup.py install --no-compile --root=$pkgdir
  popd

  # Ruby library
  pushd lib/rb
  ruby setup.rb config --installdirs=std
  ruby setup.rb setup
  ruby setup.rb install --prefix=$pkgdir
  popd

  # Java library
  pushd lib/java
  ant
  install -d -m 0755 $pkgdir/usr/share/java/thrift
  install -D -m 0644 build/libthrift-${pkgver}-snapshot.jar $pkgdir/usr/share/java/thrift
  popd

  # PHP library
  pushd lib/php/src
  install -d -m 0755 $pkgdir/usr/share/php/thrift
  for ITEM in *; do
    if [ "$ITEM" != ext ]; then
      cp -R "$ITEM" $pkgdir/usr/share/php/thrift
    fi
  done
  find $pkgdir/usr/share/php/thrift -type d | xargs chmod 0755
  find $pkgdir/usr/share/php/thrift -type f | xargs chmod 0644
  pushd ext/thrift_protocol
  phpize
  ./configure
  make
  make INSTALL_ROOT=$pkgdir install
  popd
  popd

  # Perl library
  pushd lib/perl
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
  make install DESTDIR=$pkgdir
  find $pkgdir -name perllocal.pod -delete
  find $pkgdir -name .packlist -delete
  popd
}

# vim:set ts=2 sw=2 et:
