# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=gloobus-preview-bzr
_pkgname=gloobus-preview
pkgver=290
pkgrel=1
pkgdesc="A quicklook for GNU/Linux"
arch=('i686' 'x86_64')
url="https://launchpad.net/gloobus-preview"
license=('GPL3')
depends=('desktop-file-utils' 'djvulibre' 'gstreamer0.10-base' \
         'gtksourceview3' 'libspectre' 'poppler-glib' \
         'python2-gconf' 'taglib')
makedepends=('boost' 'bzr' 'intltool')
optdepends=('gdk-pixbuf-psd: PSD images support'
            'gdk-pixbuf-xcf: XCF images support'
            'gloobus-sushi-bzr: nautilus 3.x support'
            'libicns: ICNS files support'
            'marlin-bzr: file manager integration support'
            'nautilus-actions: nautilus menu integration'
            'nautilus-elementary-bzr: file manager integration support'
            'unoconv: LibO files support')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool')
install=$pkgname.install

_bzrtrunk="https://code.launchpad.net/~gloobus-dev/$_pkgname/last_working_branch"
_bzrmod="$pkgname"

build() {
  cd "$srcdir"

  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr --no-plugins pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  # no psd and xcf loaders
  sed -i '/loaders/d' src/Makefile.am
  sed -i '/src\/loaders/d' configure.ac

  # python2 fix
  sed -i 's_python_&2_' src/"$_pkgname"-configuration

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
