# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname=graylog2-web-interface
pkgver=0.9.5p2
pkgrel=2
pkgdesc="Graylog2 is an open source syslog implementation that stores your logs in MongoDB (web interface)"
arch=(any)
url="http://graylog2.org"
license=('GPL')
depends=(ruby-enterprise-bundler)
source=(https://github.com/downloads/Graylog2/$pkgname/$pkgname-$pkgver.tar.gz Gemfile.lock.patch mongoid.yml graylog2.cron)
install=graylog2.install
backup=(etc/webapps/graylog2/{email,general,mongoid}.yml)
options=(!strip)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p0 -i "$srcdir/Gemfile.lock.patch"
	/opt/ruby-enterprise/bin/bundle install --deployment --binstubs --without test development
	find bin script -type f -exec sed -i 's|^#!/usr/bin/env ruby|#!/opt/ruby-enterprise/bin/ruby|' {} \;
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	install -d -m0755 "$pkgdir/usr/share/webapps/graylog2"
	install -d -m0755 "$pkgdir/etc/webapps/graylog2"

	cp -a * "$pkgdir/usr/share/webapps/graylog2"

	for _CONF in {email,general,mongoid}.yml; do
		mv "$pkgdir/usr/share/webapps/graylog2/config/$_CONF" "$pkgdir/etc/webapps/graylog2/$_CONF"
		ln -s "/etc/webapps/graylog2/$_CONF" "$pkgdir/usr/share/webapps/graylog2/config/$_CONF"
	done

	cp "$srcdir/mongoid.yml" "$pkgdir/etc/webapps/graylog2/mongoid.yml"

	install -D -m0755 "$srcdir/graylog2.cron" "$pkgdir/etc/cron.d/graylog2"
}

md5sums=('f4fe65b6d894f872d47e814da4709fba'
         '265ce6ac5b7826054908d4bbd9672913'
         'f73e045d203a958c4082a84a2e4496b8'
         '1dbd9485c43b5981377d1fb3d9e251b3')
