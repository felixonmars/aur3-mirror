# Maintainer: Lukas K. <lu@0x83.eu>

pkgname=gtk3-scroll
pkgver=3.8.1
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libcups libxcursor libxinerama libxrandr libxi libxcomposite libxdamage pango shared-mime-info colord at-spi2-atk wayland libxkbcommon)
makedepends=(gobject-introspection)
options=('!libtool')
backup=(etc/gtk-3.0/settings.ini)
provides=("gtk3=3.8.1")
conflicts=('gtk3')
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
        scroller.patch
        switch.patch)
       
sha256sums=('f43e7c7d7a0e2417225509e88f67799534c69abfbb6f615fc7a895bf15e31255'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            'e93a3c06974d9ef82a6f77dc3da8c2c1d143e941de12e3a757a5746ac45dfc9e'
            '84b4e74b1d98bef69b9feb369aabed585a7dde99d05178c30e8e534f66d0d089')

build() {
    cd "gtk+-$pkgver"

	patch gtk/gtknotebook.c < ../../scroller.patch
    patch gtk/gtkswitch.c < ../../switch.patch
	
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --enable-x11-backend \
        --enable-broadway-backend \
        --enable-wayland-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
