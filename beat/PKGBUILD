# Maintainer : Alucryd <alucryd at gmail dot com>

pkgbase=beat
pkgname=beat
true && pkgname=('beat-gtk' 'beat-qt')
pkgver=01
pkgrel=4
arch=('i686' 'x86_64')
url="http://byuu.org/programming/beat/"
license=('GPL3')
source=("http://byuu.org/files/beat_v$pkgver.tar.xz" "beat-gtk.desktop" 
"beat-qt.desktop" )
sha256sums=('0ba71b22fa405929df3b300c3b80537bc8851854b27b291d3ad812ea1c45873d'
            '48a3822e9613695f1b41db88539f968ff48edf3e0f01baa0ea30aa73a86a3a12'
            'b83354f759556df3fc660ec42f53b6c6a3dd2ba3b41698d0460bf6b8c63a37d8')

build() {
  cd "${srcdir}/beat"

# QT 4.8.0 fix
  moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp

# Linker fix
  sed -i 's|gtk+-2.0|gtk+-2.0 x11|g' phoenix/Makefile
  sed -i 's|QtCore QtGui|QtCore QtGui x11|g' phoenix/Makefile

# Compile GTK GUI
  make compiler=gcc platform=x phoenix=gtk
  mv beat beat-gtk
  make clean

# Compile QT GUI
  make compiler=gcc platform=x phoenix=qt
  mv beat beat-qt
  make clean
}

package_beat-gtk() {
  pkgdesc="Delta patcher for the higan emulator - GTK GUI"
  depends=('gtk2')
  provides=('beat')

  cd "${srcdir}/beat"
  install -Dm 755 beat-gtk "${pkgdir}/usr/bin/beat-gtk"
  install -dm 755 "${pkgdir}/usr/share/applications"
  install -m 644 "../beat-gtk.desktop" "${pkgdir}/usr/share/applications/beat-gtk.desktop"
}

package_beat-qt() {
  pkgdesc="Delta patcher for the higan emulator - QT GUI"
  depends=('qt>=4.7.0')
  provides=('beat')

  cd "${srcdir}/beat"
  install -Dm 755 beat-qt "${pkgdir}/usr/bin/beat-qt"
  install -dm 755 "${pkgdir}/usr/share/applications"
  install -m 644 "../beat-qt.desktop" "${pkgdir}/usr/share/applications/beat-qt.desktop"
}

pkgdesc="Delta patcher for the higan emulator"
depends=()
