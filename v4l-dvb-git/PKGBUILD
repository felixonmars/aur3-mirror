# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)

pkgname=v4l-dvb-git
pkgver=20120719
pkgrel=1
pkgdesc="V4L-DVB device drivers git version"
arch=('i686' 'x86_64')
url="http://linuxtv.org/"
license=('GPL')
depends=('linux-headers' 'perl-proc-processtable' 'patchutils')
makedepends=('git')
options=(!makeflags)
install=v4l-dvb.install
_gitroot="git://linuxtv.org/media_build.git"
_gitname="media_build"
source=(config)
sha1sums=('7bd95c8f07bdaeb3cec925c4db5c8f6bfbcc6cb5')


build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d "${srcdir}/${_gitname}" ] ; then
    cd ${_gitname} && git pull --rebase
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd "${srcdir}/${_gitname}/linux"
  make download untar
  cd ..
  # Build with 3.0-ARCH kernel
  _kernver=`uname -r`
  sed -i -e "s/KERNELRELEASE\ :=/KERNELRELEASE\ :=\ ${_kernver}/" \
    -e "s/\$(KERNELRELEASE)/${_kernver}/" \
    "${srcdir}/${_gitname}"/v4l/Makefile{,.sound}
  # /lib => /usr/lib
  sed -i "s/\/lib/\/usr\/lib/g" "${srcdir}/${_gitname}/v4l/Makefile"
  # Use alternate config, because cx23885 module won't build
  cp "${srcdir}/config" "${srcdir}/${_gitname}/v4l/.config"
  make
}

package() {
  install -dm755 "${srcdir}/usr/lib/modules/$(uname -r)/updates"
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}/usr/lib/modules/$(uname -r)/updates" install
}
