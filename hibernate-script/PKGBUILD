# Maintainer: Ng Oon-Ee <ngoonee.talk@gmail.com>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume at alaux dot net>

pkgname=hibernate-script
pkgver=2.0
pkgrel=7
pkgdesc="Set of scripts for managing tux on ice, hibernation and suspend to RAM"
arch=('any')
url="http://www.tuxonice.net"
license=('GPL')
depends=('sh')
options=(!strip)
backup=('etc/hibernate/hibernate.conf' 'etc/hibernate/tuxonice.conf' \
        'etc/hibernate/disk.conf' 'etc/hibernate/ram.conf' \
        'etc/hibernate/common.conf' 'etc/hibernate/blacklisted-modules' \
        'etc/hibernate/ususpend-both.conf' 'etc/hibernate/sysfs-ram.conf' \
        'etc/hibernate/ususpend-ram.conf' 'etc/hibernate/sysfs-disk.conf' \
        'etc/hibernate/ususpend-disk.conf')
source=(http://tuxonice.net/files/${pkgname}-${pkgver}.tar.gz
        hibernate-script-${pkgver}-arch.patch
        hibernate.rc)
md5sums=('5c21770afbae503450e3c4a5502bf29d'
         '2e6dba2c90e90952b8b1f167045c38e6'
         '0c033f583d4fa2e2d249b788fd92a0ce')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Fix scriptlets to work properly with Archlinux
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-arch.patch
  sed -i 's|#!\/bin\/sh|#!\/bin\/bash|' "${srcdir}/hibernate-script-2.0/hibernate.sh"
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export BASE_DIR=${pkgdir}
  export PREFIX=/usr
  export MAN_DIR=$BASE_DIR$PREFIX/share/man
  install -dm755 ${pkgdir}/etc/{rc,logrotate}.d
  ./install.sh || return 1
  install -m 755 $srcdir/hibernate.rc $pkgdir/etc/rc.d/hibernate-cleanup
  # Allow for behaviour as in man-page (calling by hibernate-* uses
  # /etc/hibernate/*.conf file.
  ln -s /usr/sbin/hibernate ${pkgdir}/usr/sbin/hibernate-ram
  ln -s /usr/sbin/hibernate ${pkgdir}/usr/sbin/hibernate-disk
}
