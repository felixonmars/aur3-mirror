# Maintainer: Christophe Gueret <christophe.gueret@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=sugar
pkgver=0.92.3
pkgrel=1
pkgdesc="Sugar is the core component of a worldwide effort to provide every child with equal opportunity for a quality education"
arch=('i686' 'x86_64')
url="http://www.sugarlabs.org/"
license=('GPL')
groups=('sugar-desktop' 'glucose')
depends=('python2' 'gtk2' 'python-simplejson' 'sugar-datastore' 'sugar-artwork' 'sugar-toolkit')
makedepends=('gcc' 'perl>=5.8.1')
optdepends=('xorg-server-xephyr: Needed for sugar-emulator')
source=(http://download.sugarlabs.org/sources/sucrose/glucose/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('12e4ca69e5b05588a7a726d9521543fa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2 ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # python2 fix (taken from pyrss PKGBUILD, modified a bit)
  for file in $(find ${pkgdir}/usr/bin -name 'sugar*'); do
        sed -i 's_^python_python2_' $file
        sed -i 's_^#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
}



