# Maintainer: xduugu
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>
# Contributor: Mathias Burén <mathias . buren @ gmail . com>

_pkgname=tp_smapi
pkgname=$_pkgname-ck
#pkgname=$_pkgname$(uname -r | sed -r 's/^[^-]+//')
pkgver=0.40
pkgrel=5
pkgdesc="Modules for ThinkPad's SMAPI functionality, for -ck"
arch=('i686' 'x86_64')
url="http://tpctl.sourceforge.net/"
license=('GPL')
depends=('mkinitcpio>=0.6.8' 'module-init-tools>=3.12-2')
makedepends=('linux-ck-headers')
install=tp_smapi.install
source=(http://downloads.sourceforge.net/sourceforge/tpctl/$_pkgname-$pkgver.tgz
        fix_for_2.6.37.patch
        tp_smapi.patch
	tp_smapi-x200.patch)

build() {
  # update kernel version in install file
  sed -ri "s/^(kernver=).*$/\1$(uname -r)/" "$startdir/$_pkgname.install"

  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 < "$srcdir/tp_smapi.patch"
  patch -Np1 < "$srcdir/fix_for_2.6.37.patch"
  patch -Np1 < "$srcdir/tp_smapi-x200.patch"
  make HDAPS=1
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C "/lib/modules/$(uname -r)/build" INSTALL_MOD_PATH="$pkgdir" M="$PWD" modules_install

  # move hdaps.ko into updates to override in-tree kernel module
  cd "$pkgdir/lib/modules/$(uname -r)/"
  mkdir updates
  mv extra/hdaps.ko updates

  # compress kernel modules
  find "$pkgdir" -name "*.ko" -exec gzip -9 {} +
}
md5sums=('f4eb8bb4d4413a5ae65aa7d77f4112c0'
         '80aa0a5b5a485551c1c9fae3083f110d'
         '90894542082f4b27da979aea36111cb4'
         '085f56cb5e6d338b694fce27b508949f')
