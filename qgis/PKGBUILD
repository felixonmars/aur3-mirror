# Maintainer: Lantald < lantald at gmx.com
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Eric Forgeot < http://esclinux.tk >

pkgname=qgis
pkgver=1.8.0
pkgrel=1
pkgdesc='Quantum GIS is a Geographic Information System (GIS) that supports vector, raster & database formats'
url='http://qgis.org/'
license=('GPL')
arch=('i686' 'x86_64')
# update to http://www.qgis.org/wiki/Building_QGIS_from_Source#Overview
depends=('libmysqlclient' 'postgresql-libs' 'sqlite3' 'jasper' 'curl' 'qt' 'python2' 'python2-qt' 'giflib'  'cfitsio' 'qwt5' 'gdal' 'flex' 'bison' 'libspatialite' 'spatialindex')
makedepends=('cmake' 'grass' 'gsl' 'postgis' 'netcdf' 'fcgi' 'python2-sip')
optdepends=('postgis: postgis support and SPIT plugin'
            'fcgi: qgis mapserver'
            'python2-sip: python-support'
            'grass: grass plugin'
            'gsl: georeferencer')
options=('!makeflags')
source=("http://qgis.org/downloads/qgis-${pkgver}.tar.bz2"
        'qgis.desktop')

md5sums=('1d60520f81d7763c026d0af887ac9a05'
         '8ab66039f2aba519b92f52272ec3c13e')

build() {
  # Fix insecure RPATH is weird, but just works ;)
  # echo "os.system(\"sed -i '/^LFLAGS/s|-Wl,-rpath,.\+ ||g' gui/Makefile core/Makefile\")" >> python/configure.py.in
  #cd qgis-${pkgver}

  rm -rf build
  mkdir build
  cd build

  cmake ../qgis-${pkgver} \
    -DENABLE_TESTS=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGRASS_PREFIX=/opt/grass \
    -DQGIS_MANUAL_SUBDIR=share/man \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DPYTHON_SITE_PACKAGES_DIR=/usr/lib/python2.7/site-packages \
    -DPYTHON_INCLUDE_PATH=/usr/include/python2.7/

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
  
  # create a more user-friendly application name link
  ln -s /usr/bin/qgis $pkgdir/usr/bin/quantum-gis
  
  # install some freedesktop.org compatibility
  install -D -m644 ${srcdir}/qgis.desktop \
    ${pkgdir}/usr/share/applications/qgis.desktop
}
