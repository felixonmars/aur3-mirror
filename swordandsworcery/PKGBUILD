# Maintainer: Eric Anderson <ejona86 at gmail dot com>

pkgname=swordandsworcery
pkgver=1.02
pkgrel=1
pkgdesc="An exploratory action adventure videogame with an emphasis on audiovisual style (game purchased separately)"
arch=('i686' 'x86_64')
url="http://www.swordandsworcery.com/"
license=('custom')
if [ "$CARCH" == "x86_64" ]; then
    depends=('lib32-mesa' 'lib32-curl' 'lib32-sdl')
else
    depends=('mesa' 'curl' 'sdl')
fi
options=('!strip')
_gamepkg="${pkgname}_${pkgver}.tar.gz"
source=("${pkgname}.desktop"
        'http://www.swordandsworcery.com/storage/icon_512.png'
        "${_gamepkg}")
noextract=("${_gamepkg}")
md5sums=('cc5a308dbd2d19c3b183fab755f2d396'
         '071d74203e593188ee28c0913d57d4ff'
         '2ede02c0552baeb12979bf9a68fdf399')
PKGEXT='.pkg.tar'

if [ ! -f "${startdir}/${_gamepkg}" ]; then
  warning ""
  warning "Requires purchased game download placed at ${startdir}/${_gamepkg}"
  warning ""
fi

build(){
    cd "${srcdir}"
    mkdir "${pkgname}"
    bsdtar -x -C "${pkgname}" -f "${_gamepkg}"
}

package(){
    cd ${srcdir}

    mkdir -p "${pkgdir}/opt/"
    cp -R "${srcdir}/${pkgname}" "${pkgdir}/opt/"

    # Use system-provided versions
    rm "${pkgdir}/opt/${pkgname}/lib/libstdc++.so.6.0.16"
    rm "${pkgdir}/opt/${pkgname}/lib/libstdc++.so.6"
    rm "${pkgdir}/opt/${pkgname}/lib/libcurl.so"
    rm "${pkgdir}/opt/${pkgname}/lib/libSDL-1.2.so.0"

    install -D -m644 "${srcdir}/icon_512.png" \
        "${pkgdir}/opt/${pkgname}/icon_512.png"

    install -D -m644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    install -D -m644 "${srcdir}/${pkgname}/eula.txt" \
        "${pkgdir}/usr/share/licenses/${pkgname}/eula.txt"

    mkdir -p "${pkgdir}/usr/bin"
    ln -s "/opt/${pkgname}/run.sh" "${pkgdir}/usr/bin/${pkgname}"
}
