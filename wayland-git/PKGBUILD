# Maintainer: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Scimmia

pkgname=wayland-git
pkgver=1.4.0.r26.g3adcf6f
pkgrel=1
pkgdesc='A computer display server protocol.'
arch=(i686 x86_64)
url='http://wayland.freedesktop.org'
provides=("wayland=${pkgver}")
license=('MIT')
depends=('libffi')
makedepends=('git' 'expat')
conflicts=('wayland')
source=(git://anongit.freedesktop.org/wayland/wayland)
sha1sums=('SKIP')

pkgver() {
    cd wayland
	git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'

}

build() {
    cd wayland

    ./autogen.sh --prefix=/usr \
				 --disable-documentation \
				 --disable-static
    make
}

package() {
    cd wayland
    make DESTDIR="${pkgdir}" install
    install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
