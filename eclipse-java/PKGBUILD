# $Id$
# Maintainer: Chris Fordham <chris [at] fordham [hyphon] nagy [dot] id [dot] au> aka flaccid
# Contributor: Vitaliy Berdinskikh <ur6lad@archlinux.org.ua> aka UR6LAD
# Contributor: Jack Zhang <moonboots08@gmail.com>
# Package Build Source: https://github.com/flaccid/archlinux-packages/blob/master/eclipse-java/PKGBUILD

_eclipse_name=luna
_eclipse_release=R
_eclipse_timestamp=201202080800

# mirror path:
# /technology/epp/downloads/release/luna/R/eclipse-java-luna-R-linux-gtk-x86_64.tar.gz

pkgname=eclipse-java
pkgver=4.4.0
pkgrel=1
pkgdesc="Eclipse IDE for Java Developers (present release)."
arch=('i686' 'x86_64')
url="http://www.eclipse.org/"
license=("EPL")
depends=('java-environment>=6' 'gtk2>=2.16.1' 'xulrunner>=1.9.0.10')
provides=('eclipse')
conflicts=('eclipse' 'eclipse-java-future')
replaces=('eclipse' 'eclipse-java-future')
changelog=${pkgname}.ChangeLog.markdown
install=${pkgname}.install

# defaults to x86_64 as AUR site does not eval $CARCH
source=(
  http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk-x86_64.tar.gz
  eclipse.sh
  eclipse.desktop
  eclipse.svg
)

sha256sums=('a1e5a861f57f2d5479e3752d19c99e88e40eb69dbe178890f7029eee402b5e6b'
            '4cca2873697a3af39a96449021d7fdc2fc2b01abc9f7883946081f6be7a5ed48'
            '4eb2189c96fcfa340886b049b34dc3636d7b2bfa865140dc72edb61455d900c3'
            '6fe3ab198af244f9c8c2463b6837855506e811f61e5fd8ac7c9d5fe348830a14')

# override source for 32-bit
[ "$CARCH" = "i686" ] && source[0]="http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk.tar.gz"
[ "$CARCH" = "i686" ] && sha256sums[0]='007f3118c3e5a2c4fd37e6bcbeaa4b9b6d616a8876d96ff7e6d61056b1a8e069'

package() {
	local _icon_path=/usr/share/eclipse/plugins/org.eclipse.platform_${pkgver}.v${_eclipse_timestamp}

	install -m755 -d $pkgdir/usr/{bin,share/applications}
	install -m755 -d $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,scalable}/apps

	cd "$srcdir"
  mv eclipse "$pkgdir/usr/share"

	install -D -m755 eclipse.sh $pkgdir/usr/bin/eclipse
	install -D -m644 eclipse.desktop $pkgdir/usr/share/applications
	install -D -m644 eclipse.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse.svg

  ln -s ${_icon_path}/eclipse.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png
	ln -s ${_icon_path}/eclipse32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png
	ln -s ${_icon_path}/eclipse48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png
}

# vim:set ts= 2 sw=2 Et:
