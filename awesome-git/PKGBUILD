# Contributor: noonov <noonov@gmail.com>
# Contributor: wtchappell <wtchappell@gmail.com>

pkgname=awesome-git
pkgver=20110514
pkgrel=1
pkgdesc="A highly configurable, next generation framework window manager for X"
arch=('i686' 'x86_64')
url="http://awesome.naquadah.org/"
license=('GPL2')
depends=('startup-notification' 'libev' 'lua' 'dbus' 'imlib2' 'libxdg-basedir'
  'pango' 'cairo-xcb' 'lua-oopango-git' 'lua-oocairo-git'
  'xcb-util>=0.3.8' 'xcb-util-image' 'xcb-util-keysyms' 'xcb-util-wm')
makedepends=('git' 'cmake' 'gperf' 'asciidoc' 'xmlto' 'luadoc' 'imagemagick')
provides=('awesome')
conflicts=('awesome')
options=('docs')
source=(awesome.desktop)

_gitroot="git://git.naquadah.org/awesome.git"
_gitname="awesome"

build() {
  cd ${srcdir}

  msg "Connecting to GIT server..."
  if [[ -d ${_gitname} ]]; then
    (cd ${_gitname} && git pull origin)
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_gitname}-build
  git clone ${_gitname} ${_gitname}-build

  cd ${srcdir}/${_gitname}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc
  make
}

package() {
  cd ${srcdir}/${_gitname}-build

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/awesome.desktop \
    ${pkgdir}/usr/share/xsessions/awesome.desktop
}

md5sums=('be32bd22c6279603b7f81a3184109c80')
