# Contributor: noonov <noonov@gmail.com>
# Contributor: wtchappell <wtchappell@gmail.com>

pkgname=awesome-git
pkgver=20121017
pkgrel=1
pkgdesc="A highly configurable, next generation framework window manager for X"
arch=('i686' 'x86_64')
url="http://awesome.naquadah.org/"
license=('GPL2')
depends=('startup-notification' 'libxdg-basedir' 'libev' 'dbus' 'lua'
  'cairo>=1.12.2' 'pango' 'gdk-pixbuf2' 'lgi-git'
  'xcb-util'{,-image,-keysyms,-wm}'>=0.3.8')
makedepends=('git' 'cmake' 'asciidoc' 'xmlto' 'luadoc' 'imagemagick' 'doxygen')
optdepends=('rlwrap: readline support for awesome-client')
provides=('awesome')
conflicts=('awesome')
options=('docs')
source=(awesome.desktop)
md5sums=('2763cab6a20d4b0f2676329d57ed3a45')

_gitroot="http://git.naquadah.org/git/awesome.git"
# _gitroot="git://git.naquadah.org/awesome.git"
_gitname="awesome"

build() {
  cd ${srcdir}

  msg "Connecting to GIT server..."
  if [[ -d ${_gitname} ]]; then
    (cd ${_gitname} && git pull origin)
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_gitname}-build
  git clone ${_gitname} ${_gitname}-build

  cd ${srcdir}/${_gitname}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc
  make
}

package() {
  cd ${srcdir}/${_gitname}-build

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/awesome.desktop \
    ${pkgdir}/usr/share/xsessions/awesome.desktop
}
