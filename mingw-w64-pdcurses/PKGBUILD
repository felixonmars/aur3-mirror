# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-pdcurses
pkgver=3.4
pkgrel=1
pkgdesc="Curses library for MinGW (mingw-w64)"
arch=('any')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!libtool' '!strip')
license=('custom')
url="http://pdcurses.sourceforge.net/"
source=("http://downloads.sourceforge.net/pdcurses/PDCurses-${pkgver}.tar.gz"
        "mingw-pdcurses-3.4-build.patch")
md5sums=('4e04e4412d1b1392a7f9a489b95b331a'
         'd1a36327155f51a1073b930e3647e4aa')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/PDCurses-${pkgver}"

  patch -Np1 -i ${srcdir}/mingw-pdcurses-3.4-build.patch

  unset LDFLAGS
  unset CFLAGS

  cp -a win32 win64
  ln -s win32 i686-w64-mingw32
  ln -s win64 x86_64-w64-mingw32

  for _arch in ${_architectures}; do
    pushd ${_arch}
    make -f mingwin32.mak \
      CC=${_arch}-gcc \
      LINK=${_arch}-gcc \
      STRIP=${_arch}-strip \
      WIDE=Y UTF8=Y DLL=Y
    popd
  done
}

package() {
  cd "${srcdir}/PDCurses-${pkgver}"

  for _arch in ${_architectures} ; do
    mkdir -p ${pkgdir}/usr/${_arch}/{bin,lib,include}

    install ${_arch}/*.exe ${pkgdir}/usr/${_arch}/bin/
    install ${_arch}/pdcurses.dll ${pkgdir}/usr/${_arch}/bin/
    install ${_arch}/pdcurses.a ${pkgdir}/usr/${_arch}/lib/libpdcurses.dll.a

    install -m 0644 curses.h panel.h term.h ${pkgdir}/usr/${_arch}/include/

    ${_arch}-strip --strip-all ${pkgdir}/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/lib/*.dll.a
  done
}

# vim:set ts=2 sw=2 et:
