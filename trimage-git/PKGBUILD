# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=trimage-git
pkgver=20110620
pkgrel=1
pkgdesc="A GUI based lossless image compressor."
url="http://trimage.org"
arch=('any')
license=('MIT')
depends=("python2-pyqt" "optipng" "advancecomp" "jpegoptim" "pngcrush")
conflicts=("trimage")
provides=("trimage")
source=()
md5sums=()

_gitroot="git://github.com/Kilian/Trimage.git"
_gitname="Trimage"

build() {
  cd "$srcdir"
  msg "Connecting to github..."

  if [ -d "$srcdir"/${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Running setup.py..."

  cd "$srcdir/$_gitname"

  find ./ -type f -name '*.py' | xargs -n 1 sed -i 's|/usr/bin/env python|/usr/bin/env python2|'
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' trimage

  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
}
