# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: hybraries <macwolf@archlinux.de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Navi <navitwo.at.gmail.dot.com>
# Contributor: rabyte <rabyte.at.gmail.dot.com>
# Contributor: Johannes Krampf <wuischke.at.amule.dot.org>
pkgname=midori-git
pkgver=20110528
pkgrel=1
pkgdesc="A lightweight web browser based on WebKit. Git development version."
arch=('x86_64' 'i686')
url="http://twotoasts.de/index.php?/pages/midori_summary.html"
license=('LGPL')
depends=('libwebkit' 'libnotify' 'libxss' 'libunique' 'hicolor-icon-theme')
makedepends=('pkg-config' 'git' 'python2' 'libxml2' 'gtk2' 'intltool' 'docutils' 'libsoup' 'vala')
provides=('midori')
conflicts=('midori')
install=midori.install
_gitroot="git://git.xfce.org/apps/midori"
_gitname="midori"

build() {
  cd "$srcdir"

  msg "Connecting to the midori git repository..."
  if [ -d "$srcdir/$_gitname" ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  msg "GIT checkout done or server timeout"
  cd "$srcdir"
  rm -rf $_gitname-build
  git clone $_gitname $_gitname-build
  cd "$_gitname-build"

  msg2 "Configuring..."
  sed -i 's:python:python2:' waf
  sed -i 's:python:python2:' wscript
  ./waf configure --prefix=/usr 

  msg2 "Compiling..."
  ./waf build 
}

package() {
  cd "$_gitname-build"

  msg2 "Packaging..."
  DESTDIR="$pkgdir" ./waf install
}

# vim:set ts=2 sw=2 et:
