# Maintainer: Robert La Spina <rkidlaspina at gmail dot com>
# Based on the skulltag package by Mikael Eriksson <mikael_eriksson@miffe.org>
# and Sean Streeter <anubis2591 at gmail dot com>
pkgname=zandronum
pkgver=1.2.1
pkgrel=1
pkgdesc="OpenGL ZDoom port with Client/Server multiplayer."
arch=('i686' 'x86_64')
url="http://zandronum.com/"
license=('custom')
conflicts=('zandronum-hg')
#makedepends=('cmake' 'mercurial' 'yasm' 'mesa') # Not sure if it needs yasm.
depends=('sdl' 'gtk2' 'libjpeg6' 'glu')
optdepends=('timidity++: midi support'
            'freedoom: free IWAD')
install=zandronum.install

if [ "$CARCH" == "i686" ];then
    source=(
        "http://zandronum.com/downloads/${pkgname}${pkgver}-linux-x86.tar.bz2"
        "http://www.fmod.org/download/fmodex/api/Linux/fmodapi42416linux.tar.gz"
        "LICENSE.txt"
    )
    md5sums=(
        '8b900802568bc57bef9c0cd10d2957d0'
        '00b157e8d09d539b0a7023c48b71422a'
        '58c2c8b15c886b838dc2ed5186361507'
    )
    _64=
fi

if [ "$CARCH" == "x86_64" ]; then
    source=(
        "http://zandronum.com/downloads/${pkgname}${pkgver}-linux-x86_64.tar.bz2"
        "http://www.fmod.org/download/fmodex/api/Linux/fmodapi42416linux64.tar.gz"
        "LICENSE.txt"
    )
    md5sums=(
        '722f67d9dcecae66db6eff5401ee37e2'
        '70b2a6a2618ee9823ab564b4e945c885'
        '58c2c8b15c886b838dc2ed5186361507'
    )
    _64=64
fi


build() {
  cd $srcdir
 
  cat > zandronum.sh << EOF
#!/bin/sh
export LD_LIBRARY_PATH=/usr/share/zandronum/lib
exec /usr/share/zandronum/zandronum "\$@"
EOF

  cat > zandronum-server.sh << EOF
#!/bin/sh
export LD_LIBRARY_PATH=/usr/share/zandronum/lib
exec /usr/share/zandronum/zandronum-server "\$@"
EOF

}

package(){ 
  cd $srcdir
  
  install -Dm644 "zandronum.pk3" "$pkgdir/usr/share/zandronum/zandronum.pk3"
  #install -Dm644 "brightmaps.pk3" "$pkgdir/usr/share/zandronum/brightmaps.pk3"
  install -Dm644 "skulltag_actors.pk3" "$pkgdir/usr/share/zandronum/skulltag_actors.pk3"
  install -Dm755 "liboutput_sdl.so" "$pkgdir/usr/share/zandronum/lib/liboutput_sdl.so"
  install -Dm755 "zandronum" "$pkgdir/usr/share/zandronum/zandronum"
  install -Dm755 "zandronum-server" "$pkgdir/usr/share/zandronum/zandronum-server"
  install -Dm755 "zandronum.sh" "$pkgdir/usr/bin/zandronum"
  install -Dm755 "zandronum-server.sh" "$pkgdir/usr/bin/zandronum-server"
  install -Dm755 "fmodapi42416linux${_64}/api/lib/libfmodex${_64}-4.24.16.so" "$pkgdir/usr/share/zandronum/lib/libfmodex${_64}-4.24.16.so"

  install -Dm644 "LICENSE.txt" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
