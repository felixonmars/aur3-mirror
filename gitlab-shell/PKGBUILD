# Maintainer: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com> 
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Lee Watson <aur@revthefox.co.uk>
# Contributor: Tobias Hunger <tobias DOT hunger AT gmail DOT com>
# Contributor: Stefan Tatschner <stefan@sevenbyte.org>

pkgname=gitlab-shell
pkgver=2.2.0
pkgrel=3
pkgdesc="Self hosted Git management software. (shell daemon)"
arch=('any')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby>=1.9' 'redis' 'git')
options=('!strip')

install=gitlab-shell.install
backup=('etc/webapps/gitlab-shell/config.yml')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gitlabhq/gitlab-shell/archive/v${pkgver}.tar.gz"
	gitlab-shell.tmpfiles.d
	)
sha512sums=('846576ecb60dba3fb7e87d551d64742c0a25f16b8b37c4acbdd14dc323a7b269fc53103e5c70bc4beced46aafbf20a66949a5b03521bcb418bffcef1921dc3a5'
	'f5d82441b1bb4992dcde5b95830496a1511299de5e2e85ac434021d00d20f88af5d206da2cb43a7d264a5c3d922559f205749bfa76d945ce9eae420de392c5b9')
_homedir='/var/lib/gitlab'
_datadir="/usr/share/webapps/${pkgname}"
_srcdir="gitlab-shell-${pkgver}"
_logdir='/var/log/gitlab'
_etcdir="/etc/webapps/${pkgname}"

prepare() {
	cd "${srcdir}/${_srcdir}"

	sed -e 's|user: git|user: gitlab|' \
		-e "s|/home/git|${_homedir}|" \
		-e "s|# host: |host: |" \
		-e "s|# port: |port: |" \
		-e "s|socket: |# socket: |" \
		-e "s|# log_file: .*|log_file: \"${_logdir}/gitlab-shell.log\"|" \
		config.yml.example > config.yml
	sed -i "s|<HOMEDIR>|${_homedir}|g" "${srcdir}/gitlab-shell.tmpfiles.d"
	sed -i "s|<LOGDIR>|${_logdir}|g" "${srcdir}/gitlab-shell.tmpfiles.d"
}

package() {
	install -d \
		"${pkgdir}${_datadir}" \
		"${pkgdir}${_homedir}" \
		"${pkgdir}${_etcdir}"
	install -dm0750 "${pkgdir}${_logdir}"

	cd "${srcdir}/${_srcdir}"

	mv config.yml "${pkgdir}${_etcdir}"
	ln -fs "${_etcdir}/config.yml" "${pkgdir}${_datadir}/config.yml"

	cp -a VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
	ln -fs "${_datadir}" "${pkgdir}/${_homedir}/"

	install -dm0700 "${pkgdir}${_homedir}/.ssh"
	install -dm2770 "${pkgdir}${_homedir}/repositories"
	install -dm0750 "${pkgdir}${_homedir}/satellites"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"

	install -Dm644 "${srcdir}/gitlab-shell.tmpfiles.d" "${pkgdir}/usr/lib/tmpfiles.d/gitlab-shell.conf"
}

# vim:set ts=4 sw=4 et:
