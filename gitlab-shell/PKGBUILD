# Maintainer: Lee Watson <aur@revthefox.co.uk>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Pavol Hluchy <lopo@losys.eu>

pkgname=gitlab-shell
pkgver=1.9.7
pkgrel=1
pkgdesc="Self hosted Git management software. (shell daemon)"
arch=('any')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby>=1.9' 'redis' 'git')
options=('!strip')

install=gitlab-shell.install
backup=('etc/webapps/gitlab/shell.yml')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gitlabhq/gitlab-shell/archive/v${pkgver}.tar.gz")
sha512sums=('b1382864af5e63d57b44d41f90c7e5daf9c91b7fc100584e1fcc1bbc6088e2c104099260c6eacf2a9d4c07e6832e9107f73f46567d3077404373a960c543841e')
_homedir=/var/lib/gitlab
_datadir=/usr/share/gitlab-shell
_srcdir="gitlab-shell-${pkgver}"

package() {
    install -d \
        "${pkgdir}${_datadir}" \
        "${pkgdir}${_homedir}" \
        "${pkgdir}/etc/webapps/gitlab"

    cd "${srcdir}/${_srcdir}"

    sed -e 's|user: git|user: gitlab|' \
        -e "s|/home/git/repositories|${_homedir}/repositories|" \
        -e "s|/home/git|${_homedir}|" \
        config.yml.example > "${pkgdir}/etc/webapps/gitlab/shell.yml"
    ln -fs /etc/webapps/gitlab/shell.yml "${pkgdir}${_datadir}/config.yml"

    cp -a VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
    ln -fs /usr/share/gitlab-shell ${pkgdir}/${_homedir}/

    install -dm700 "${pkgdir}${_homedir}/.ssh"
    install -dm770 "${pkgdir}${_homedir}/"{repositories,satellites}

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
