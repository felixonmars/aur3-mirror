# Contributor: Chris Brannon <cmbrannon79@gmail.com>
pkgname=emacspeak-svn
pkgver=7774
pkgrel=1
pkgdesc="Emacs extension that provides spoken output"
arch=(i686 x86_64)
url="http://emacspeak.sourceforge.net/"
license=('GPL' 'custom')
makedepends=('subversion')
depends=(emacs tcl tclx espeak)
optdepends=(  'eflite: software speech via the FLite TTS engine')
provides=(emacspeak)
conflicts=(emacspeak)
install='emacspeak-svn.install'
source=('verbatim.txt')
md5sums=('d6248e590e7ab9a510356fb38cea1373')

_svntrunk='http://emacspeak.googlecode.com/svn/trunk/'
_svnmod=emacspeak

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  make config
  # Prevent shr-url.elc from being compiled.  It's an optional module,
  # and it won't work with the emacs version distributed in the official repos.
  touch lisp/shr-url.elc
  make

  # This one isn't compiled by default, but a lot of folks use it.
  cd servers/linux-espeak
  make TCL_VERSION=8.5
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install
  # Remember, shr-url.elc is bogus.
  # Fixme: remove these workarounds when Arch's emacs includes shr.el.
  rm "$pkgdir/usr/share/emacs/site-lisp/emacspeak/lisp/shr-url.elc"
  cd servers/linux-espeak
  make DESTDIR="$pkgdir" install
  # Interestingly, the source files are installed under DESTDIR.
  cd "$pkgdir/usr/share/emacs/site-lisp/emacspeak/servers/linux-espeak"
  rm -f tclespeak.cpp Makefile
  # A handful of files have permissions of 750 and 640; fix.
  cd "$pkgdir"
  find . -perm 640 -print0
  find . -perm 750 -print0
  gzip -9nf "${pkgdir}"/usr/share/info/*
  rm -f "$pkgdir/usr/share/info/dir"
}
