# Maintainer: Adam Nielsen <malvineous@shikadi.net>

_pkgname=camoto-studio
pkgname=${_pkgname}-git
pkgver=20120512
pkgrel=1
pkgdesc="Examine and edit DOS games"
arch=('i686' 'x86_64')
url="http://www.shikadi.net/camoto"
license=('GPL3')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=(
	'boost'
	'png++'
	'libgamecommon'
	'libgamearchive'
	'libgamegraphics'
	'libgamemaps'
	'libgamemusic'
	'wxgtk'
)
makedepends=('git' 'autoconf' 'automake' 'pkgconfig')
options=()
_gitname=${_pkgname}
_gitroot="git://github.com/Malvineous/${_gitname}"
source=()
md5sums=()

build() {
	cd "${srcdir}"

	if [ -d "$_gitname" ] ; then
		cd "$_gitname"

		msg "Cleaning up from previous build."
		make distclean

		msg "Updating from GitHub server..."
		git pull origin
		msg "The local files are updated."
	else
		msg "Downloading from GitHub server..."
		git clone --depth=1 -- "$_gitroot" "$_gitname"
		cd "$_gitname"
	fi

	msg "Configuring build environment..."
	./autogen.sh
	./configure --prefix="/usr"

	msg "Beginning compile..."
	make ${MAKEFLAGS}
}

check() {
	cd "$srcdir/$_gitname"
	make ${MAKEFLAGS} check
}

package() {
	cd "$srcdir/$_gitname"
	make install DESTDIR="$pkgdir"
}
