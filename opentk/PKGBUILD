pkgname=opentk
pkgver=2010.10.06
pkgrel=7
pkgdesc="An advanced, cross-platform, C-sharp OpenGL, OpenAL and OpenCL wrapper for Mono/.Net."
arch=(any)
license=("MIT")
depends=(mono)
makedepends=(mono)
options=(!strip)
url="http://www.opentk.com"
conflicts=(opentk-svn)
source=("http://downloads.sourceforge.net/opentk/opentk-${pkgver//./-}.zip")
md5sums=('22534c103fa67fb6403f24f602f1d279')

build() {
  cd "${srcdir}/opentk"
  xbuild OpenTK.sln
}

package() {
  cd "${srcdir}/opentk/Binaries/OpenTK/Debug"
  find . -name '*.config' -o -name '*.mdb' -o -name '*.dll' -o -name '*.xml' |
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/opentk/"{}
  cp -r Data "$pkgdir/usr/lib/opentk/"
  rm "$pkgdir/usr/lib/opentk/"{Bind,Convert}.exe.mdb
  install -m755 Examples.exe "$pkgdir/usr/lib/opentk/"
  install -Dm644 "$srcdir/opentk/Documentation/License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"
  find . -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
}
