# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-openssl
pkgver=1.0.1c
pkgrel=1
arch=('any')
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc')
options=(!strip !buildflags)
license=('BSD')
url="http://www.openssl.org"
source=("http://openssl.org/source/openssl-$pkgver.tar.gz")
md5sums=('ae412727c8c15b67880aef7bd2999b2e')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd ${srcdir}/openssl-${pkgver}

  # No support for out-of-source builds => create two copies
  for _arch in ${_architectures}; do
    mkdir -p $srcdir/build-${_arch}
    cp -a ${srcdir}/openssl-${pkgver}/* ${srcdir}/build-${_arch}
  done

  # Use mingw cflags instead of hardcoded ones
  sed -i -e '/^"mingw"/ s/-fomit-frame-pointer -O3 -march=i486 -Wall/-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4/' ${srcdir}/build-${_arch}/Configure
  sed -i -e '/^"mingw"/ s/-fomit-frame-pointer -O3 -march=i486 -Wall/-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4/' ${srcdir}/build-${_arch}/Configure

  for _arch in ${_architectures}; do
    _mingw=mingw
    if $(echo ${_arch} | grep -q x86_64); then
      _mingw=mingw64
    fi
    cd "$srcdir/build-${_arch}"
    unset LDFLAGS
    ./Configure \
      --cross-compile-prefix=${_arch}- \
      -DOPENSSL_NO_CAPIENG \
      --prefix=/usr/${_arch} \
      ${_mingw} \
      shared \
      zlib-dynamic
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/build-${_arch}"
    make INSTALL_PREFIX=$pkgdir install
    ${_arch}-strip $pkgdir/usr/${_arch}/bin/*.exe
    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/lib/engines/*.dll
    rm -rf $pkgdir/usr/${_arch}/{bin/c_rehash,ssl/misc,ssl/man}
  done
}
