# Maintainer: Sergio Correia <sergio.correia@uece.net>

pkgname=qt5-qtimageformats-git
pkgver=20121004
pkgrel=1
pkgdesc="Qt 5: qtimageformats module"
arch=('i686' 'x86_64')
url="https://qt.gitorious.org/qt/qtimageformats"
license=('LGPL')
depends=('libtiff' 'libmng')
makedepends=('gcc' 'git' 'qt5-qtbase-git')
source=('remove-destdir.patch')
md5sums=('02921b185472859c3f5549ae57345d50')

_gitroot="git://gitorious.org/qt/qtimageformats.git"
_gitname=qt5-qtimageformats

build() {
	if [ ! -d "${_gitname}" ]; then
		git clone ${_gitroot} ${_gitname} && cd ${_gitname}
	else
		cd ${_gitname} && git reset --hard && git pull origin && git clean -dfx
	fi

	msg "GIT checkout done."

	patch -uNp1 -i ../remove-destdir.patch || return 1

	/opt/qt5/bin/qmake
	make
}

package() {
	cd "${_gitname}"

	make INSTALL_ROOT="${pkgdir}" install
}
