# Maintainer: Bernardo Barros <bernardobarros@gmail.com>
# Contributor: Uli Armbruster <uli_armbruster@web.de>

pkgname=ardour3-svn
_realname=ardour3
pkgver=9187
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation. Version 3 with lv2 support (pre-release)."
arch=('i686' 'x86_64')
url="http://www.ardour.org"
license=('GPL')
depends=('util-linux-ng' 'liblrdf>=0.4.0' 'liblo' 'libgnomecanvasmm>=2.12.0' 'slv2>=0.6.4' 'aubio' 'jack2-svn')
makedepends=('boost>=1.39' 'cppunit>=1.12.0' 'soundtouch' 'libvorbis>=1.1.2' 'flac>=1.2.1' "python2>=2.3" 'glib2>=2.2'  'cppunit')
provides=('ardour3')
conflicts=('ardour3')
source=(${pkgname}.desktop)
md5sums=('2736c648e452ae45c703e48cf64f75e7')
install=ardour3-svn.install

_svntrunk=http://subversion.ardour.org/svn/ardour2/branches/3.0
_svnmod=${_realname}

build() {
  cd ${srcdir}
  
  rm -rf ${_svnmod}-build
  
  if [ -d $_svnmod ] ; then
    (cd $_svnmod && svn update)
    msg "The local files are updated."
  else
    svn co ${_svntrunk} ${_svnmod}
  fi
  
  cp -rf ${_svnmod} ${_svnmod}-build
  
  cd ${_svnmod}-build

  python2 ./waf configure \
      --prefix=/usr \
      --configdir=/etc \
      --tranzport \
      --freesound \
      --freedesktop \
      --lv2 \
      --nls \
      -p \
      --debug \
      --extra-warn
  
  python2 ./waf build

  DESTDIR="${pkgdir}" python2 waf install
  
  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${_realname}.desktop"
}

