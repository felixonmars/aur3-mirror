# Maintainer: Bernardo Barros <bernardobarros@gmail.com>
# Contributor: Uli Armbruster <uli_armbruster@web.de>

pkgname=ardour3-svn
_realname=${pkgname%-svn}
pkgver=9484
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('i686' 'x86_64')
url="http://www.ardour.org/"
license=('GPL2')
depends=('libgnomecanvasmm>=2.12.0' 'jack>=0.120.0' 'liblrdf>=0.4.0' 'liblo' 'aubio' 'slv2>=0.6.4' 'desktop-file-utils' 'soundtouch') # can be jack or jack2 
makedepends=('python2' 'boost>=1.39' 'cppunit>=1.12.0'  'doxygen' 'graphviz')
optdepends=()
provides=("${_realname}")
conflicts=("${_realnmae}")
install=ardour3-svn.install
source=("${pkgname}.desktop")
        
_svntrunk=http://subversion.ardour.org/svn/ardour2/branches/3.0
_svnmod=${_realname}

build() {
  cd "$srcdir"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_svnmod}-build"
  cp -r "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  # patch a bug present in (at least) rev9483
  # add default initializer and public functions

  python2 waf configure \
    --prefix=/usr \
    --configdir=/etc \
    --docs \
    --freedesktop \
    --freesound \
    --lv2 \
    --nls \
    --tranzport
  # enabled by default
  #   --debug

  python2 waf build
}

package() {
  cd "${srcdir}/${_svnmod}-build"

  python2 waf --destdir="${pkgdir}" install

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${_realname}.desktop"
}

md5sums=('2736c648e452ae45c703e48cf64f75e7')
