# Contributor: Dave Reisner <d@falconindy.com>

pkgname=util-linux-git
pkgver=20120120
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux - the GIT version"
url="http://userweb.kernel.org/~kzak/util-linux/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('zlib' 'udev' 'ncurses')
makedepends=('git')
provides=('util-linux-ng=2.21' 'util-linux=2.21')
conflicts=('util-linux-ng' 'util-linux')
replaces=('util-linux-ng')
options=('!libtool')
optdepends=('perl: for chkdupexe support')

_gitroot="git://git.kernel.org/pub/scm/utils/util-linux/util-linux.git"
_gitname="util-linux"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname"{,-build}
  cd "$srcdir/$_gitname-build"

  # alter location of adjtime file for FHS
  sed -e 's@etc/adjtime@var/lib/hwclock/adjtime@' -i include/pathnames.h

  msg "Configuring..."
  ./autogen.sh
  ./configure \
      --enable-write \
      --enable-raw \
      --disable-wall \
      --enable-partx \
      --enable-new-mount

  msg "Starting make..."
  make
}

package() {
  make -C "$_gitname-build" DESTDIR="$pkgdir" install

  # create adjtime file's dir
  install -dm755 "$pkgdir/var/lib/hwclock"
}

