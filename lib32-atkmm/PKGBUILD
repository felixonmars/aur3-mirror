# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=atkmm
pkgname=lib32-$_pkgbasename
pkgver=2.22.5
pkgrel=2
pkgdesc="C++ bindings for atk (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-atk' 'lib32-libsigc++' "$_pkgbasename")
makedepends=('gcc-multilib' 'lib32-glibmm')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/2.22/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('61e03e5e67952c8fd9919049fae680bd0f2a1547344394f36b98b05b34b7be6c')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"  
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,lib32/atkmm*}
}
