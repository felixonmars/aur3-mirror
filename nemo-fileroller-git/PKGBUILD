# Maintainer: twa022 <twa022 at gmail dot com>

_pkgname=nemo-fileroller
pkgname=${_pkgname}-git
pkgver=20120920
pkgrel=1
pkgdesc="Nemo file rollerextension"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-fileroller"
license=('GPL')
source=('configure.in_fix.patch')
depends=('libnotify' 'nemo-git' 'file-roller')
options=('!libtool' '!emptydirs')

_gitroot=git://github.com/linuxmint/nemo-fileroller.git
_gitname=nemo-fileroller

build() {
  msg "Connecting to GIT server...."	

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  [ -d "$_gitname-build" ] && rm -rf "$_gitname-build"
  git clone "$_gitname" "$_gitname-build"
  cd "$_gitname-build"

  ## I think they started with the nemo-dropbox source, so there
  ## are some unnecessary python dependencies
  patch -uNp2 -r- -i ../configure.in_fix.patch
  autoreconf -fi
  
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$_gitname-build"
  make DESTDIR="${pkgdir}" install
}
sha256sums=('0f524f04d22135ac9e601f204957832cbb6471831c190a84645765a08e9faec9')
