# $Id: PKGBUILD 161948 2012-06-16 22:01:05Z ibiru $
# Maintainer : wuyingren <keitaroh@gmail.com>
# Contributor : Thomas Baechler <thomas@archlinux.org>
# Adapted for fbcondecor by: Conrad Nelson

pkgname=nvidia-fbcondecor
pkgver=304.60
_extramodules=extramodules-3.5-fbcondecor
pkgrel=1
pkgdesc="NVIDIA drivers for linux kernel patched with fbcondecor."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-fbcondecor>=3.5' 'linux-fbcondecor<3.6' "nvidia-utils=${pkgver}")
makedepends=('linux-headers>=3.5' 'linux-headers<3.6')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia-fbcondecor.install

# doing this to force md5sum generating for i686
#CARCH=i686

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('42b9887076b2ebcf1af5ee13bc332ccb')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('7248399a125808e3bbc9c66da99a098d')
fi

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
        cd "${srcdir}"
        sh "${_pkg}.run" --extract-only
        cd "${_pkg}/kernel"
        make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
        install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
                "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
        install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
	echo "blacklist nvidiafb" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
        sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-fbcondecor.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}

