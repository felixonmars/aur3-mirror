# Contributor: Philipp Überbacher <murks at lavabit dot com>
# Maintainer: David Adler <david dot jo dot adler at gmail dot com>

pkgname=phasex-dev-git
pkgver=20110301
pkgrel=1
pkgdesc="Synthesizer using phase offset modulation. 3rd party development version"
arch=('i686' 'x86_64')
url="http://github.com/disabled/phasex-dev"
license=('GPL2')
depends=('jack' 'gtk2' 'libsamplerate' 'lash')
makedepends=('git')
provides=('phasex')
conflicts=('phasex' 'phasex-git')

_gitroot="http://github.com/disabled/phasex-dev.git"
_gitname="phasex-dev"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"
  

  aclocal
  autoconf
  automake
  autoheader
  ./configure --prefix="/usr" --enable-arch="native"
  make
}
 
package() { 
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}/" install
}
