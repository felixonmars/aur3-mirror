# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mickele <mimocciola at yahoo dot com> (librecad-git PKGBUILD)
# Contributor: Ilmari Repo <ilmari at gmail dot com> (librecad-svn PKGBUILD)
# Contributor: GazJ Gary James <garyjames82 at gmail dot com> (CADuntu PKGBUILD)

pkgname=librecad
pkgver=2.0.0rc1
_pkgtag="e037a0d"
pkgrel=1
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad"
arch=('i686' 'x86_64')
url="http://www.librecad.org/"
license=('GPL')
depends=('qt4' 'muparser')
makedepends=('boost' 'imagemagick')
conflicts=('librecad-git')
replaces=('librecad-svn' 'caduntu' 'caduntu-svn')
install=librecad.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/LibreCAD/LibreCAD/tarball/${pkgver}")

build() {
	cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

	# fix version string
	sed -i "/^SCMREVISION/c SCMREVISION=\"${pkgver}\"" librecad/src/src.pro

	export CPPFLAGS="-std=c++0x"
	export PATH="/usr/lib/qt4/bin:${PATH}"
	qmake-qt4 librecad.pro
	make
}

package() {
	cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

	install -D -m 755 unix/librecad "${pkgdir}/usr/bin/librecad"
	install -D -m 755 unix/ttf2lff "${pkgdir}/usr/bin/ttf2lff"

	install -D -m 644 desktop/librecad.desktop "${pkgdir}/usr/share/applications/librecad.desktop"
	install -D -m 644 desktop/librecad.1 "${pkgdir}/usr/share/man/man1/librecad.1"

	for SIZE in 16 24 32 48 64 96 128; do
		convert -scale ${SIZE} \
			desktop/graphics_icons_and_splash/Icon\ LibreCAD/Icon_Librecad.svg \
			${srcdir}/librecad.png
		install -D -m 644 ${srcdir}/librecad.png "${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/librecad.png"
	done
	install -D -m 644 desktop/graphics_icons_and_splash/Icon\ LibreCAD/Icon_Librecad.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/librecad.svg"

	mkdir -p "${pkgdir}/usr/share/librecad/"
	cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/librecad/"
}

sha256sums=('a69a24cfd93d19246eb2d008bb4946ab4ac1ddde4183d4fd2a436fdfd7830544')
