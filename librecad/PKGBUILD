# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mickele <mimocciola at yahoo dot com> (librecad-git PKGBUILD)
# Contributor: Ilmari Repo <ilmari at gmail dot com> (librecad-svn PKGBUILD)
# Contributor: GazJ Gary James <garyjames82 at gmail dot com> (CADuntu PKGBUILD)

pkgname=librecad
pkgver=2.0.0beta1
_pkgtag=33e0bd4
pkgrel=3
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad"
arch=('i686' 'x86_64')
url="http://www.librecad.org/"
license=('GPL')
depends=('qt' 'boost' 'muparser')
conflicts=('librecad-git')
replaces=('librecad-svn' 'caduntu' 'caduntu-svn')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/LibreCAD/LibreCAD/tarball/${pkgver}"
	'librecad.desktop')

build() {
	cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

	# fix version string
	sed -i "/^SCMREVISION/c SCMREVISION=\"${pkgver}\"" librecad/src/src.pro

	export CPPFLAGS="-std=c++0x"
	qmake librecad.pro
	make
}

package() {
	cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

	install -D -m 755 unix/librecad "${pkgdir}/usr/bin/librecad"
	install -D -m 755 unix/ttf2lff "${pkgdir}/usr/bin/ttf2lff"

	install -D -m 644 "${srcdir}/librecad.desktop" "${pkgdir}/usr/share/applications/librecad.desktop"
	install -D -m 644 librecad/res/main/librecad.png "${pkgdir}/usr/share/pixmaps/librecad.png"

	mkdir -p "${pkgdir}/usr/share/librecad/"
	cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/librecad/"
}

sha256sums=('8bf969b79be115f3b3ff72cc030a4c21fe93164dd0cb19ddfb78a7d66b8bc770'
            'a584d4257e4cc783b8eed49d26e75d4850187027dfaba859611d60393dd27c66')
