# Contributor: schuay <jakob dot gruber at gmail dot com>

pkgname=maitreya
pkgver=6.0.1
pkgrel=1
pkgdesc="A free software for Vedic and western astrology"
arch=(i686 x86_64)
license=('GPL')
url="http://www.saravali.de/maitreya/index.html"
depends=('wxgtk')
source=(http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
install=maitreya.install
md5sums=('ef656025c257b976fd3d0c890e5fa01c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # the default build flags "-Wl, --as-needed" don't work, override them
  ./configure --prefix=/usr LDFLAGS="-Wl,--hash-style=gnu"

  # 'wx-config --libs' also sets --as-needed, so manually remove it from all Makefiles
  find . -name 'Makefile' -print | while read file; do sed -i 's/-Wl,--as-needed//' $file; done

  make
}
package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
