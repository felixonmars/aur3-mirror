# Contributor: chenxing <cxcxcxcx at gmail.com>
# Maintainer: alexmo82 <25396682 at live.it>

pkgname=freefilesync
pkgver=6.0
pkgrel=1
pkgdesc="Visual folder comparison and synchronization"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/freefilesync/"
license=('GPLv3')
depends=(wxgtk2.9 webkitgtk2 boost)
conflicts=(freefilesync-ubuntu)
source=("http://downloads.sourceforge.net/project/freefilesync/freefilesync/v$pkgver/FreeFileSync_${pkgver}_Source.zip" FreeFileSync.desktop ffsicon.png RealtimeSync.desktop rtsicon.png)
md5sums=('8e69bfac44336ad0540345ae3762e616'
         'a7be7841f47f29d8e6210c8ab592a1e3'
         '1f452dff6f970d95839411008d86250b'
         '90152f1021f2d6fb6ab4fa511dc60fa9'
         'ee5587fa0a8d906ad416564e4daf5a06')

build() {
  cd ${srcdir}/FreeFileSync/Source
  sed -i 's/wx-config --/wx-config-2.9 --/' Makefile
  sed -i 's/CXXFLAGS[ \t]*=/CXXFLAGS = -Wno-unused-local-typedefs -Wno-deprecated-declarations -Wno-literal-suffix /' Makefile
  make BUILD=Launchpad

  cd RealtimeSync
  sed -i 's/wx-config --/wx-config-2.9 --/' Makefile
  sed -i 's/CXXFLAGS[ \t]*=/CXXFLAGS = -Wno-unused-local-typedefs -Wno-deprecated-declarations -Wno-literal-suffix /' Makefile
  make BUILD=Launchpad
}

package() {
  cd ${srcdir}/FreeFileSync/Source
  make DESTDIR=${pkgdir} install

  cd RealtimeSync
  make DESTDIR=${pkgdir} install

  cd ${srcdir}
  install -Dm644 FreeFileSync.desktop $pkgdir/usr/share/applications/FreeFileSync.desktop
  install -Dm644 ffsicon.png $pkgdir/usr/share/pixmaps/ffsicon.png
  install -Dm644 RealtimeSync.desktop $pkgdir/usr/share/applications/RealtimeSync.desktop
  install -Dm644 rtsicon.png $pkgdir/usr/share/pixmaps/rtsicon.png
}
