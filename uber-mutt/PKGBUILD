# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Patrick Brisbin <pbrisbin@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=uber-mutt
pkgver=1.5.21
pkgrel=1
pkgdesc="A small but very powerful text-based mail client patched by the best."
arch=('i686' 'x86_64')
url='http://lunar-linux.org/index.php?page=mutt-sidebar'
license=('GPL')
depends=('openssl' 'gdbm' 'mime-types' 'libsasl' 'slang' 'gnupg' 'gpgme')
conflicts=('mutt')
provides=('mutt')
options=('!buildflags')
source=(ftp://ftp.mutt.org/mutt/devel/mutt-${pkgver}.tar.gz
        http://spacehopper.org/mutt/sidebar-5302767aa6aa.gz
        http://dbg.download.sourcemage.org/grimoire/codex/stable/mail/mutt/patches/trash_folder/trash_folder-1.5.18.patch.bz2
        muttrc.example
        mutt-unmailbox.patch
        mutt-attach.patch)
md5sums=('a29db8f1d51e2f10c070bf88e8a553fd'
         '7f987e3c41a42780135f74f6579c457f'
         '9d95c08295f8ec5171f774f776ef8413'
         '336d1d8e290a0595dbe2cd92d720ffc9'
         'fa8e03a49a2fa7b294dc8237d928cdb7'
         '659d26f6dae386d55a58c5311e57632e')

build() {
  cd "$srcdir/mutt-$pkgver"

  # forgotten attatchment detector
  patch -Np1 < "$srcdir/mutt-attach.patch"

  # patch to add sidebar support
  patch -Np1 < "$srcdir/sidebar-5302767aa6aa"

  # patch to add trash folder
  patch -Np1 < "$srcdir/trash_folder-1.5.18.patch"

  sed -i '/^AM_C_PROTOTYPES$/{d;q;}' configure.ac

  autoreconf
  ./configure --prefix=/usr --sysconfdir=/etc --enable-pop \
    --enable-imap --enable-smtp --enable-pgp --enable-hcache \
    --enable-gpgme --with-ssl=/usr --with-sasl --without-idn \
    --with-regex --with-slang

  make
}

package() {
  cd "$srcdir/mutt-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove unneeded or conflicting files
  rm -f "$pkgdir/usr/bin"/{flea,muttbug}
  rm -f "$pkgdir/usr/share/man/man1"/{flea,muttbug}.1
  rm -f "$pkgdir/etc"/mime.types*

  # install example muttrc file
  install -Dm644 "$srcdir/muttrc.example" "$pkgdir/etc/muttrc.example"
}
