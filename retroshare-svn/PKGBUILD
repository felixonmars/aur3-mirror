# Maintainer: stqn

pkgname=retroshare-svn
pkgver=5907
pkgrel=1
pkgdesc="Serverless encrypted instant messenger with filesharing, chatgroups, e-mail."
arch=('i686' 'x86_64')
url="http://retroshare.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('qt' 'libupnp' 'libgnome-keyring' 'libxss' 'speex')
makedepends=('subversion')
provides=('retroshare')
conflicts=('retroshare')
install='retroshare.install'
source=('retroshare.install')
sha256sums=('47c23238cbfabb6f07b6a25666ee5941243176360ca28ec31378d94e87326ec1')

_svntrunk=https://retroshare.svn.sourceforge.net/svnroot/retroshare/trunk
_svnmod=trunk

# Set this to true to build and install retroshare-nogui
_build_nogui=false

# Set this to true to build and install the plugins
_build_plugins=true

build() {
	cd "$srcdir"
	msg "Connecting to SVN server..."

	if [[ -d "$_svnmod/.svn" ]]; then
		(cd "$_svnmod" && svn up -r "$pkgver")
	else
		svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
	fi

	msg "SVN checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_svnmod-build"
	cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
	cd "$srcdir/$_svnmod-build"

	#
	# BUILD HERE
	#
	msg "Compiling OpenPGP-SDK..."
	cd openpgpsdk/src
	qmake
	make

	msg "Compiling libbitdht..."
	cd ../..
	cd libbitdht/src
	qmake
	make

	msg "Compiling libretroshare..."
	cd ../..
	cd libretroshare/src
	qmake
	LANG=C ./version_detail.sh
	make

	msg "Compiling retroshare-gui..."
	cd ../..
	cd retroshare-gui/src
	qmake
	LANG=C ./version_detail.sh
	make

	cd ../..

	if [[ $_build_plugins == "true" ]] ; then
		msg "Compiling plugins..."
		cd "plugins"
		sed -i 's/lessThan.*/true {/' VOIP/VOIP.pro
		qmake
		make
		cd ..
	fi

	if [[ "$_build_nogui" == "true" ]]; then
		msg "Compiling retroshare-nogui..."
		cd retroshare-nogui/src
		qmake
		make
	fi
}

package() {
	cd "$srcdir/$_svnmod-build"

	# Binaries
	install -D -m 755 retroshare-gui/src/RetroShare "$pkgdir/usr/bin/retroshare"
	if [[ "$_build_nogui" == "true" ]]; then
		install -D -m 755 \
			"retroshare-nogui/src/retroshare-nogui" \
			"${pkgdir}/usr/bin/retroshare-nogui"
	fi

	# plugins
	if [[ $_build_plugins == "true" ]] ; then
		install -D -m 755 \
			"plugins/LinksCloud/libLinksCloud.so" \
			"${pkgdir}/usr/lib/retroshare/extensions/libLinksCloud.so"
		install -D -m 755 \
			"plugins/VOIP/libVOIP.so" \
			"${pkgdir}/usr/lib/retroshare/extensions/libVOIP.so"
	fi

	# Applications menu entry
	mkdir -p "$pkgdir/usr/share/applications"
	cat >"$pkgdir/usr/share/applications/retroshare.desktop" <<EOF
[Desktop Entry]
Version=1.0
Type=Application
Name=RetroShare
GenericName=Sharing Network
Comment=Secure communication with your friends
Icon=/usr/share/pixmaps/retroshare_blue.png
Exec=/usr/bin/retroshare
Terminal=false
StartupNotify=true
Categories=Network;InstantMessaging;FileTransfer;Chat;Email;
EOF
	install -D -m 644 \
		"retroshare-gui/src/gui/images/retrosharelogo1.png" \
		"${pkgdir}/usr/share/pixmaps/retroshare_blue.png"

	# bdboot (needed to bootstrap the DHT)
	install -D -m 644 \
		"libbitdht/src/bitdht/bdboot.txt" \
		"${pkgdir}/usr/share/RetroShare/bdboot.txt"

	# Skins
	cp -r "retroshare-gui/src/qss" "${pkgdir}/usr/share/RetroShare/"
}
