# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

#   Adding corresponding optdepends to makedepends array will ensure
# wanted feature is compiled in.

pkgname=evince-gtk
pkgver=3.10.2
_pkgalias=evince
pkgrel=2
pkgdesc="A document viewer, built without GConf and GNOME keyring support"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/evince/"
license=('GPL')
depends=('gtk3' 'libsm' 'poppler-glib' 'gnome-icon-theme'
         'desktop-file-utils' 'gsettings-desktop-schemas')
makedepends=('itstool' 'intltool')
optdepends=('libspectre: PostScript support'
            'djvulibre: DJVU support'
            'texlive-bin: DVI support'
            'libgxps: XPS support'
            'gvfs: bookmark and annotation support')
provides=("$_pkgalias")
conflicts=("$_pkgalias")
install=$_pkgalias.install
source=("http://ftp.gnome.org/pub/GNOME/sources/evince/${pkgver%.*}/$_pkgalias-$pkgver.tar.xz")
sha256sums=('1052ed2d2a0d3a570e75eb84ada1708b2816a588609512511eae9968a71fccd0')

_ods=()

add_ods_pc()
{
    pkg-config $1 && echo "$3" >> "$srcdir/tmp" || _ods=("${_ods[@]}" "$2")
}

add_ods_file()
{
    [[ -e "$1" ]] && echo "$3" >> "$srcdir/tmp" || _ods=("${_ods[@]}" "$2")
}

unset_optdepend()
{
    for _idx in ${!optdepends[@]}
    do
        if [[ "${optdepends[$_idx]}" =~ ^$1:* ]]
        then
            unset optdepends[$_idx]
            break
        fi
    done
}

build()
{
    cd "$srcdir/$_pkgalias-$pkgver"

    sed -i 's/gnome-icon-theme//' configure.ac configure

    rm -rf "$srcdir/tmp"
    add_ods_pc libspectre '--disable-ps' libspectre
    add_ods_pc ddjvuapi '--disable-djvu' djvulibre
    add_ods_file '/usr/include/kpathsea/kpathsea.h' '--disable-dvi' texlive-bin
    add_ods_pc libgxps '--disable-xps' libgxps

    #autoreconf --force --install
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/$_pkgalias \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-maintainer-mode \
        --disable-schemas-compile \
        --disable-debug \
        --disable-tests \
        --disable-nautilus \
        --enable-previewer \
        --disable-introspection \
        --enable-t1lib \
        --enable-comics \
        --disable-gtk-doc \
        --without-keyring \
        --with-smclient=xsmp "${_ods[@]}"

    make
}

package()
{
    cd "$srcdir/$_pkgalias-$pkgver"
    make DESTDIR="$pkgdir" install

    while read _ch
    do
        if [[ 1 -gt 0 ]]; then depends=("${depends[@]}" "$_ch"); fi
        unset_optdepend "$_ch"
    done < "$srcdir/tmp"
    optdepends=("${optdepends[@]}")
    rm -rf "$srcdir/tmp"
}
