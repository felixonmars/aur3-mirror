# Maintainer: rtfreedman  <rob<d0t>til<d0t>freedman< T>googlemail<d0t>com>
#
# This source is now officially GPL but still (software) patent encumbered.
# Using it might be a problem in your country - in most others it isn't ;)
#
# If you're unsure, use it only on your own private systems
# and not inside your company or systems facing the public.
#
# This PKGBUILD will be available as long as the repository is public.
#
# For discussions about the git repo, see:
# http://phoronix.com/forums/showthread.php?81642-Native-Linux-Kernel-Module-Is-Out-For-Microsoft-exFAT
#
# http://opensource.samsung.com/

pkgname=exfat-git
pkgver=69.f02c5b8
pkgrel=2
url='https://github.com/rxrz/exfat-nofuse'
pkgdesc='Native kernel module for extended fat support'
license=('GPL')
#
arch=('i686' 'x86_64')
depends=('linux>=3.8' 'linux<3.11')
makedepends=('linux-headers' 'git')
provides=('exfat')
conflicts=('exfat')
#
install="${pkgname}.install"
options=('!strip')
source=("${pkgname}::git+https://github.com/rxrz/exfat-nofuse.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

# Find all kernels and extramodule directories
KERNELS=`cat /usr/lib/modules/extramodules*/version`
XTRAMOD=`find /usr/lib/modules -name version | sed 's|\/usr\/lib\/modules\/||; s|\/version||'`

build() {
  cd "${pkgname}"
  cp Makefile.module Makefile
  for _kernver in $KERNELS; do
    echo Building module for $_kernver
    make KDIR=/usr/lib/modules/${_kernver}/build
    mkdir -p ${_kernver}
    mv exfat.ko ${_kernver}
    rm *.o .exfat*.cmd
  done
}

package() {
  cd "${pkgname}"
  # Loop through all detected kernels
  for _kernver in $KERNELS; do
	# Loop through all detected extramodules directories
	for _mod_dir in $XTRAMOD; do
	  # Check which extramodules directory corresponds with the built module
	  if [ $(cat "/usr/lib/modules/${_mod_dir}/version") = ${_kernver} ]; then
	    gzip ${_kernver}/exfat.ko
	    install -m755 -d "${pkgdir}/usr/lib/modules/${_mod_dir}/"
	    install -m644 -t "${pkgdir}/usr/lib/modules/${_mod_dir}/" ${_kernver}/exfat.ko.gz
	  fi
	done
  done
}
