# Maintainer: TDY <tdy@gmx.com>

pkgname=maratis-svn
pkgver=155
pkgrel=1
pkgdesc="A portable, simple, and visual game development tool for artists and developers"
arch=('i686' 'x86_64')
url="http://www.maratis3d.org/"
license=('GPL')
depends=('libgl' 'libsndfile' 'openal')
makedepends=('chrpath' 'mesa' 'python2' 'scons' 'subversion')
provides=('maratis')
conflicts=('maratis')
source=(MaratisEditor.sh
        MaratisEditor.desktop
        MaratisPlayer.sh
        MaratisPlayer.desktop)
md5sums=('ca34ea59336bffe2660ca700eea05354'
         '51d2ecdf6aa3b20a3c6728c32bdce8fc'
         'a600295a5b4b3c8f20ee8c53d61f80ec'
         'ba4f9ba9dd24a8dde370f82c6b0f7d12')

_svntrunk=http://maratis.googlecode.com/svn/
_svnmod=maratis

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
   cd "$_svnmod" && svn up -r "$pkgver"
  else
   svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build/trunk/dev"

  python2 scons.py
}

package() {
  cd "$srcdir/$_svnmod-build/trunk/dev/prod/linux2/release/Maratis/Bin"

  # delete insecure rpaths
  chrpath -d libM{Engine,Core}.so Maratis{Editor,Player}

  # libs
  install -Dm755 libMEngine.so "$pkgdir/usr/lib/libMEngine.so"
  install -Dm755 libMCore.so "$pkgdir/usr/lib/libMCore.so"

  # gui
  install -dm755 "$pkgdir/opt/$_svnmod/gui/meshs"
  install -m644 gui/*.* "$pkgdir/opt/$_svnmod/gui"
  install -m644 gui/meshs/*.* "$pkgdir/opt/$_svnmod/gui/meshs"
  install -Dm644 font/default.tga "$pkgdir/opt/$_svnmod/font/default.tga"

  # bins
  install -Dm755 MaratisEditor "$pkgdir/opt/$_svnmod/MaratisEditor"
  install -Dm755 MaratisPlayer "$pkgdir/opt/$_svnmod/MaratisPlayer"
  install -Dm755 "$srcdir/MaratisEditor.sh" "$pkgdir/usr/bin/MaratisEditor"
  install -Dm755 "$srcdir/MaratisPlayer.sh" "$pkgdir/usr/bin/MaratisPlayer"

  # desktop entries
  install -Dm644 "$srcdir/MaratisEditor.desktop" \
    "$pkgdir/usr/share/applications/MaratisEditor.desktop"
  install -Dm644 "$srcdir/MaratisPlayer.desktop" \
    "$pkgdir/usr/share/applications/MaratisPlayer.desktop"
  install -Dm644 gui/Title.png "$pkgdir/usr/share/pixmaps/$_svnmod.png"
}

# vim:set ts=2 sw=2 et:
