pkgname=mate-character-map
pkgver=20120504
pkgrel=1
pkgdesc="MATE Unicode Charmap"
arch=('i686' 'x86_64')
license=('GPL')
depends=('hicolor-icon-theme')
makedepends=('mate-doc-utils' 'pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
url="http://mate-desktop.org/"
groups=('mate-extras')
install='mate-character-map.install'
source=()
sha256sums=()

_gitroot=git://github.com/mate-desktop/mate-character-map.git
_gitname=mate-character-map

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build" --depth=1
	cd "$srcdir/$_gitname-build"

	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-scrollkeeper \
		--disable-mateconf || return 1
	#--with-gtk=3.0
	make || return 1
}

package() {
	cd "$srcdir/$_gitname-build"
	make DESTDIR="${pkgdir}" install || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
