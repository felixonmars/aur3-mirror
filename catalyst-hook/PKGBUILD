# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Shen Miren <dickeny@gmail.com> (build_module/hook idea and code)
# Contributor: aidanlinz
# Contributor: cyberpatrol
# Contributor: Rip-Rip
# Contributor: nob
# Contributor: Krzysztof Kolasa (patch to fix ttys bug)
# Contributor: lano1106 (patch to fix support of intel's iommu)

pkgname=catalyst-hook
pkgver=13.4
pkgrel=4
_amdver=12.104
pkgdesc="AMD/ATI drivers. Auto re-compile fglrx module while shutdown/reboot. Now with Angela - The Slayer Of The Daemons ;P"
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
depends=('catalyst-utils' 'gcc-libs' 'gcc>4.0.0' 'make' 'patch')
optdepends=('linux-headers: to build the fglrx module for the linux kernel'
	  'linux-lts-headers: to build the fglrx module for the linux-lts kernel')
conflicts=('catalyst-test' 'xf86-video-ati' 'xf86-video-radeonhd' 'ati-dri' 'catalyst-daemon' 'catalyst' 'catalyst-generator' 'catalyst-dkms')
provides=("catalyst=${pkgver}")
install=${pkgname}.install

source=(
    http://www2.ati.com/drivers/linux/amd-catalyst-${pkgver}-linux-x86.x86_64.zip
    hook-fglrx
    catalyst_build_module
    ati_make.sh
    makefile_compat.patch
    catalyst-hook.service
    kolasa_fglrx_fixing_ttys.patch
    lano1106_fglrx_intel_iommu.patch)

md5sums=('558bd1b31173b06b25da647a7112e734'
         '9126e1ef0c724f8b57d3ac0fe77efe2f'
         'f75ad119618ed37bc4b344a3b924aab1'
	 '62239156a9656c6f41e89a879578925c'
	 '3e1b82bd69774ea808da69c983d6a43b'
	 'a64e2eae5addc6d670911ccf94b8cda4'
	 '5f28dfc4c9fdb0fbfed69c8cb38659dd'
	 '5184b94a2a40216a67996999481dd9ee')


build() {
     /bin/sh ./amd-catalyst-13.4-linux-x86.x86_64.run --extract archive_files
}

package() {
    # License
      install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
      install -m644 ${srcdir}/archive_files/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}

      cd ${srcdir}/archive_files

    # Patching
      patch -Np1 -i ../makefile_compat.patch
      patch -Np1 -i ../kolasa_fglrx_fixing_ttys.patch
      patch -Np1 -i ../lano1106_fglrx_intel_iommu.patch

    # Prepare modules source files
      _archdir=x86_64
      test "${CARCH}" = "i686" && _archdir=x86
      install -m755 -d ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/*.c \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/*.h \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/2.6.x/Makefile \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a \
                ${pkgdir}/usr/share/ati/build_mod
      install -m755 -d ${pkgdir}/usr/bin
      install -m755 ${srcdir}/catalyst_build_module ${pkgdir}/usr/bin

    # modified ati's make.sh script
      install -m755 ${srcdir}/ati_make.sh ${pkgdir}/usr/share/ati/build_mod

    # hook fglrx
      install -m755 -d ${pkgdir}/usr/lib/initcpio/install
      install -m644 ${srcdir}/hook-fglrx ${pkgdir}/usr/lib/initcpio/install/fglrx

    # systemd service to perform fglrx module build at shutdown
      install -m755 -d ${pkgdir}/usr/lib/systemd/system
      install -m644 ${srcdir}/catalyst-hook.service ${pkgdir}/usr/lib/systemd/system
}