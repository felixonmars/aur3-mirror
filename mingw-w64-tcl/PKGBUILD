# Maintainer: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-tcl
_majorver1=8
_majorver2=6
_majorver="${_majorver1}.${_majorver2}"
pkgver="${_majorver}.1"
pkgrel=2
pkgdesc="The Tcl scripting language (mingw-w64)"
arch=('any')
depends=('mingw-w64-crt' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc')
options=('staticlibs' '!strip' '!buildflags')
license=('custom')
url="http://tcl.sourceforge.net/"
source=("http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz"
        "tcl-8.5.14-autopath.patch"
	"tcl-8.5.14-conf.patch"
	"tcl-8.5.14-hidden.patch"
	"tcl-8.6.1-mingwexcept.patch"
	"tcl-mingw-w64-compatibility.patch")
sha256sums=('16ee769248e64ba1cae6b4834fcc4e4edd7470d881410e8d58f7dd1434343514'
            '3cb435f768052acabe5b1fbef5ae9a8d0967b0f86f7695fb4bae8953ee470357'
            '970a37e5496e7ad9e7cbb3dfba3f3a40630da67f14abd819df82af53faabdf25'
            'a809617939336a0068c5dd567f2c25489b2824ca25ad20f9cbf4e74c81396d2e'
            '77b7509f3677fe659a70994f664444c1f0e99899bd3e73cba71caa4ccb24ebd4'
            '866fe923b7d43339bea8a8484299ba71ebdb3a7213508809f2adae4eff60a382')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/tcl${pkgver}"

  patch -Np1 -i "${srcdir}/tcl-8.5.14-autopath.patch"
  patch -Np1 -i "${srcdir}/tcl-8.5.14-conf.patch"
  patch -Np1 -i "${srcdir}/tcl-8.5.14-hidden.patch"
  patch -Np1 -i "${srcdir}/tcl-8.6.1-mingwexcept.patch"
  patch -Np0 -i "${srcdir}/tcl-mingw-w64-compatibility.patch"

}

build() {
  unset LDFLAGS
  unset CFLAGS

  cd "${srcdir}/tcl${pkgver}/win"
  autoconf
  cd "${srcdir}"
  
  # out of source building failed for me. Did not try fixing it.
  cp -R tcl${pkgver} tcl${pkgver}-i686-w64-mingw32
  mv tcl${pkgver} tcl${pkgver}-x86_64-w64-mingw32

  for _arch in ${_architectures}; do
    pushd tcl${pkgver}-${_arch}/win
    [ "${_arch}" = 'x86_64-w64-mingw32' ] && enable64bit='--enable-64bit'
    ./configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --disable-threads \
      --enable-shared \
      $enable64bit
    make -j1 TCL_LIBRARY=/usr/${_arch}/share/tcl${_majorver}

    popd
  done
}

package() {
  cd "${srcdir}"

  for _arch in ${_architectures}; do
    pushd tcl${pkgver}-${_arch}
    
    make install -j1 -C win INSTALL_ROOT="${pkgdir}" TCL_LIBRARY=/usr/${_arch}/share/tcl${_majorver}

    ln -s tclsh${_majorver1}${_majorver2}.exe "${pkgdir}/usr/${_arch}/bin/tclsh.exe"

    mv "${pkgdir}/usr/${_arch}/lib/libtcl${_majorver1}${_majorver2}.a" "${pkgdir}/usr/${_arch}/lib/libtcl${_majorver1}${_majorver2}.dll.a"
    mv "${pkgdir}/usr/${_arch}/lib/libtclstub${_majorver1}${_majorver2}.a" "${pkgdir}/usr/${_arch}/lib/libtclstub${_majorver1}${_majorver2}.dll.a"
    
    # for linking with -libtcl
    ln -s libtcl${_majorver1}${_majorver2}.dll.a "${pkgdir}/usr/${_arch}/lib/libtcl.dll.a"

    ln -s ../share/tcl${_majorver} "${pkgdir}/usr/${_arch}/lib/tcl${_majorver}"

    # postgresql and maybe other packages too need tclConfig.sh
    # paths don't look at /usr/lib for efficiency, so we symlink into tcl8.5 for now
    ln -s /usr/${_arch}/lib/tclConfig.sh "${pkgdir}/usr/${_arch}/lib/tcl${_majorver}/tclConfig.sh"

    mkdir -p "${pkgdir}/usr/${_arch}/include/tcl-private/"{generic,win}
    find generic win -name "*.h" -exec cp -p '{}' "${pkgdir}"/usr/${_arch}/include/tcl-private/'{}' ';'
    ( cd "${pkgdir}/usr/${_arch}/include"
      for i in *.h ; do
	[ -f "${pkgdir}/usr/${_arch}/include/tcl-private/generic/$i" ] && ln -sf ../../$i "${pkgdir}/usr/${_arch}/include/tcl-private/generic" ;
      done
    ) || true

    # fix executable bits
    chmod a-x "${pkgdir}/usr/${_arch}/share/tcl${_majorver}/encoding/"*.enc
    chmod a-x "${pkgdir}/usr/${_arch}/lib/"*/pkgIndex.tcl

    # remove buildroot traces
    sed -i -e "s|$PWD/win|/usr/${_arch}/lib|; s|$PWD|/usr/${_arch}/include/tcl-private|" "${pkgdir}//usr/${_arch}/lib/tclConfig.sh"
    rm -rf "${pkgdir}/usr/${_arch}/share/tcl${_majorver}/tclAppInit.c"
    rm -rf "${pkgdir}/usr/${_arch}/share/tcl${_majorver}/ldAix"

    # move windows packages to where tcl85.dll will find them
    mv "${pkgdir}/usr/${_arch}/lib/dde"* "${pkgdir}/usr/${_arch}/lib/tcl${_majorver}/"
    mv "${pkgdir}/usr/${_arch}/lib/reg"* "${pkgdir}/usr/${_arch}/lib/tcl${_majorver}/"

    rm "${pkgdir}/usr/${_arch}/bin/"*.exe
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip --strip-debug "${pkgdir}/usr/${_arch}/lib/"*.a
    rm "$pkgdir/usr/${_arch}/bin/zlib1.dll"
    popd
  done
}

# vim:set ts=2 sw=2 et:
