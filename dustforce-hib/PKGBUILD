# Maintainer: Sam S. <smls75@gmail.com>

pkgname=dustforce-hib
pkgver=1.0+20120917
_hibver=1347954459
pkgrel=1
pkgdesc='A fast-paced 2D action platformer (Humble Bundle version)'
url='http://dustforce.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('mesa' 'libgl' 'sdl' 'libxdamage' 'freealut' 'libidn' 'libvorbis'
         'libxft' 'xdg-utils')
PKGEXT='.pkg.tar'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

_installer="dustforce-linux-${_hibver}.sh"
source=("hib://${_installer}"
        'dustforce-hib.desktop')
md5sums=('afaca0c6241904becb66a29821f4dce4'
         'c012863d22d4332c651a272eb5e9cf03')

[ $CARCH == "i686" ] && _arch='x86' || _arch='x86_64'

build() {
  msg2 "Extracting $_installer..."
  rm -rf game temp; mkdir -p game temp; cd temp
  sh ../$_installer --tar xpf
  lzma -d < instarchive_linux_$_arch | tar xpf - -C ../game
  lzma -d < instarchive_all | tar xpf - -C ../game
  lzma -d < subarch | tar xpf -
  lzma -d < deps/cURL/cURL_files_linux_$_arch | tar xpf - -C ../game
}

package() {
  cd $srcdir
  _installdir=/opt/Dustforce; _target="$pkgdir"/$_installdir

  # Install game files
  mkdir -p "$_target"
  cp -TRl game "$_target"

  # Fix permissions
  find "$pkgdir" -type f -exec chmod 644 "{}" +
  chmod 755 "$_target"/Dustforce.bin.$_arch

  # Install icon & desktop entry
  install -Dm644 game/Dustforce.png \
                 "$pkgdir"/usr/share/pixmaps/dustforce.png
  install -Dm644 $pkgname.desktop \
                 "$pkgdir"/usr/share/applications/$pkgname.desktop

  # Install launcher symlink
  install -d "$pkgdir"/usr/bin
  ln -s $_installdir/Dustforce.bin.$_arch "$pkgdir"/usr/bin/dustforce
}
