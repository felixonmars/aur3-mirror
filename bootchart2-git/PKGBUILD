# Contributor: DuGi <dugi@irc.pl>
# Contributor: nTia89 <tia.tif AT fastwebnet DOT it>
# Maintainer: Luca Bennati <lucak3 AT gmail DOT com>

pkgname=bootchart2-git
pkgrel=1
pkgver=20111026
pkgdesc="Boot Process Performance Visualization"
arch=('i686' 'x86_64')
url="http://github.com/mmeeks/bootchart"
license=('GPL')
provides=('bootchart2')
conflicts=('bootchart2' 'bootchart')
depends=('busybox' 'pygtk')
makedepends=('git')
install=bootchart2-git.install

_gitname="bootchart"
_gitroot="git://github.com/mmeeks/${_gitname}.git"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d "${_gitname}" ] ; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."
 
  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  PYTHON=python2 make
  sed -i 's:#!/usr/bin/python:#!/usr/bin/python2:' pybootchartgui.py
}

check() {
  cd "${srcdir}/${_gitname}-build"
  PYTHON=python2 make -k test || return 0
}

package() {
  cd "${srcdir}/${_gitname}-build"
  PYTHON=python2 make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/share/doc{s,}
}
