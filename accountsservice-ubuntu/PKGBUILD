# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice-ubuntu
_ubuntu_rel=1ubuntu4
pkgver=0.6.29.${_ubuntu_rel}
pkgrel=100
pkgdesc="D-Bus interface for user account query and manipulation"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'systemd' 'lightdm-ubuntu')
makedepends=('intltool' 'gobject-introspection' 'vala')
provides=("accountsservice=${pkgver%.*}")
conflicts=('accountsservice')
options=('!libtool')
source=("http://cgit.freedesktop.org/accountsservice/snapshot/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/accountsservice_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('5348aae4fa791e4cbf5ab4a763d5a8aa1dec5d4fb86fed6f6769aca987970d07bd88930c9c6d8cb25d01620e0344a7a025b71603ff0e2d115c8f2e401ac9814e'
            '5beeb371f945b069b76799c3317c7ff589d13641fd982311c90a560a076d9b9bb56281a2be781d3588da8547938b91704b89b61dae655294d584081384d948d4')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
    # Disable patches
      # Arch Linux uses wheel group for system administration
        sed -i '/0002-create-and-manage-groups-like-on-a-debian-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0002-create-and-manage-groups-like-on-a-ubuntu-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Arch Linux uses /etc/gdm as the GDM configuration directory
        sed -i '/0005-gdm_config_file_path.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0005-gdm_config_file_path_ubuntu.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # adduser is Debian-specific
        sed -i '/0006-adduser_instead_of_useradd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Language patches are still pretty specific to Ubuntu (Ubuntu is trying
      # to get them merged upstream)
        sed -i '/0001-formats-locale-property.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0009-language-tools.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0010-set-language.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0012-add-keyboard-layout-support.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # systemd can be used in Arch Linux
        sed -i '/2002-disable_systemd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # This is not the proper way to fix the issue: run "intltoolize -f"
        sed -i '/1001-buildsystem.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Does not apply cleanly
        sed -i '/0013-add-has-message-support.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
    # Fix patches
      sed -i 's,/etc/gdm3/daemon\.conf,/etc/gdm/custom.conf,g' \
        "${srcdir}"/debian/patches/2001-filtering_out_users.patch

  for i in $(cat "${srcdir}/debian/patches/ubuntu.series" | grep -v '#'); do
    echo ${i}
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var \
    --disable-static \
    --enable-systemd

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
