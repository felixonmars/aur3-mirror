# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice-ubuntu
_ubuntu_rel=6ubuntu3
pkgver=0.6.21.${_ubuntu_rel}
pkgrel=100
pkgdesc="D-Bus interface for user account query and manipulation"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'libsystemd' 'lightdm-ubuntu')
makedepends=('intltool' 'gobject-introspection' 'vala')
provides=("accountsservice=${pkgver%.*}")
conflicts=('accountsservice')
options=('!libtool')
source=("http://cgit.freedesktop.org/accountsservice/snapshot/${pkgname%-*}-${pkgver%.*}.tar.xz"
        "http://archive.ubuntu.com/ubuntu/pool/main/a/accountsservice/accountsservice_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('fa23419d9275087644cb69b89e0d0f037407f4bba393ce3c755e43340e6cab5cb531cf19373578317bac2ed9abff5e1801e0cbf0e09660116e64e6987037f501'
            '842849d9f0dca4fa921675bc89f700f5d8da8b0aee2ce78648e1be56b1da8bf7477c786d57bdbc25cdd232ea024b1ef8bf1aaf07bf70e2ce3c1203f2bdcac442')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  # Apply Ubuntu patches
    # Disable patches
      # Arch Linux uses wheel group for system administration
        sed -i '/0002-create-and-manage-groups-like-on-a-debian-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0002-create-and-manage-groups-like-on-a-ubuntu-system.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Arch Linux uses /etc/gdm as the GDM configuration directory
        sed -i '/0005-gdm_config_file_path.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0005-gdm_config_file_path_ubuntu.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # adduser is Debian-specific
        sed -i '/0006-adduser_instead_of_useradd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # Language patches are still pretty specific to Ubuntu (Ubuntu is trying
      # to get them merged upstream)
        sed -i '/0001-formats-locale-property.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0009-language-tools.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
        sed -i '/0010-set-language.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
      # systemd can be used in Arch Linux
        sed -i '/2002-disable_systemd.patch/d' \
          "${srcdir}"/debian/patches/{ubuntu.,}series
    # Fix patches
      sed -i 's,/etc/gdm3/daemon\.conf,/etc/gdm/custom.conf,g' \
        "${srcdir}"/debian/patches/2001-filtering_out_users.patch

  for i in $(cat "${srcdir}/debian/patches/ubuntu.series" | grep -v '#'); do
    echo ${i}
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var \
    --disable-static

  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver%.*}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
