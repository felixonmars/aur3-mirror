# Maintainer: joetw <call dot imera42 at googlemail dot com>
# Contributor: Greg Fitzgerald <greg at gregf dot org>

pkgname=xcape-git
_gitname=xcape
pkgver=1.0.33.g664a292
pkgrel=1
pkgdesc="Use a modifier as another key (chord) when pressed and released on its own"
arch=('i686' 'x86_64')
url="https://github.com/alols/xcape"
license=('GPL3')
depends=('libxtst')
provides=('xcape')
makedepends=('git')
source=("git+${url}")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cd $_gitname
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir/" MANDIR="/share/man/man1" install
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"

  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}

# vim:set ft=PKGBUILD ts=2 sw=2 et:
