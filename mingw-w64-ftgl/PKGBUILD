pkgname=mingw-w64-ftgl
_pkgver=2.1.3-rc5
pkgver=2.1.3rc5
pkgrel=5
pkgdesc="OpenGL library to use arbitrary fonts (mingw-w64)"
arch=(any)
url="http://ftgl.wiki.sourceforge.net"
license=("MIT")
depends=(mingw-w64-crt mingw-w64-freetype)
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
options=(staticlibs !strip !buildflags)
source=("http://downloads.sourceforge.net/ftgl/ftgl-$_pkgver.tar.bz2"
        "m4.gl.m4.patch")
md5sums=('c7879018cde844059495b3029b0b6503'
         '8590beb2dea7e28e7b7214ed562b2472')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd "${srcdir}/ftgl-${_pkgver/-/\~}"
  sed -i "s|SUBDIRS = src test demo docs|SUBDIRS = src|g" Makefile.am
  patch -p0 -i "${srcdir}"/m4.gl.m4.patch
  autoreconf -vis
}

build() {
  cd "${srcdir}/ftgl-${_pkgver/-/\~}"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    # to find freetype-config
    export PATH=$PATH:/usr/${_arch}/bin
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --without-x \
      --with-ft-exec-prefix=/usr/${_arch}
    make ECHO=echo
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/ftgl-${_pkgver/-/\~}/build-${_arch}"
    make DESTDIR="$pkgdir" install ECHO=echo
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
