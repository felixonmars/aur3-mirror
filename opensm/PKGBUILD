# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=opensm
pkgver=3.3.17
pkgrel=1
pkgdesc="InfiniBand subnet manager and administration"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibumad')
source=("http://www.openfabrics.org/downloads/management/${pkgname}-${pkgver}.tar.gz"
        "modules-load.d-opensm.conf"
        "opensm-genconfig@.service"
        "opensm.service"
        "opensm@.service")
# cksum start
md5sums=('9c1b85e47ab495110c1944e0f4d634b7'
         '56c1f0f686c506830c0e18be9a3d7579'
         '515c30a1399c006490ddbe6daf46867c'
         '2d33e2a2ed5645cad1945ff273a13f7c'
         'd4384d3be43db2259e08e7b14d5cadf3')
sha512sums=('6c3b551f4250be4ccc4aeb9f19e46f146f9d4e0ecebc70b2e8615356d7be5d810bb854889e28d3b0a6651388b11edd7adbddfc29fd4e6d0697c5ad2471ef77bd'
            '9c8fcc7e41c5c478afb473b6298164d1576c6fe8bd9f41a3fb8b4f8a830ffad1b80e4eb375bb0c0a3b76f92a5a174d020f92b87c75d2e7ababe5470bad715167'
            '6a6f04c72f3ec8158bf247b53716b6217e5ebe7179efdcde1c6e59da4fe793314287b86a43d2df6ffb370067e0ab6b41937c66f3c8dd7e443401a6067bc86922'
            '8720398335abe87ca0f56fda994e759094cbe009861dadd612b9c5fd19ffbebfbe88ef1eb71f0c1a7d1060019f681129cfed812470c5d76aa0b64dc8b7ed34c2'
            'e0e68ab82320ac07d50acffa40b19dff3862530e371e2e8a5cccf17d78b63064c7334b5530d8b87755972ca4395940c3c7ec785d12aa6f91e224797919d2249c')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --sbindir=/usr/bin
  make
}

package() {
  # Install Systemd files first
  install -Dm644 "${srcdir}/opensm.service" "${pkgdir}/usr/lib/systemd/system/opensm.service"
  install -Dm644 "${srcdir}/opensm@.service" "${pkgdir}/usr/lib/systemd/system/opensm@.service"
  install -Dm644 "${srcdir}/opensm-genconfig@.service" "${pkgdir}/usr/lib/systemd/system/opensm-genconfig@.service"
  install -Dm644 "${srcdir}/modules-load.d-opensm.conf" "${pkgdir}/usr/lib/modules-load.d/opensm.conf"

  # Install software to pkg directory
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  # add files to pkg
  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  mkdir -p "${pkgdir}/var/cache/opensm"
  mkdir -p "${pkgdir}/etc/opensm"
  touch "${pkgdir}/var/cache/opensm/.empty"
  touch "${pkgdir}/etc/opensm/partitions.conf"

  # cleanup files from pkg
  rm -rf "${pkgdir}/etc/init.d"
}
