# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=opensm
pkgver=3.3.16
pkgrel=1
pkgdesc="InfiniBand subnet manager and administration"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibumad')
source=("http://www.openfabrics.org/downloads/management/${pkgname}-${pkgver}.tar.gz"
        "modules-load.d-opensm.conf"
        "opensm-genconfig@.service"
        "opensm.service"
        "opensm@.service")
# cksum start
md5sums=('32b16efbaba69d478f8c05df42ce0462'
         '56c1f0f686c506830c0e18be9a3d7579'
         '515c30a1399c006490ddbe6daf46867c'
         '2d33e2a2ed5645cad1945ff273a13f7c'
         'd4384d3be43db2259e08e7b14d5cadf3')
sha256sums=('db60b5361a2ac1ba67d847e74ba3c1066afddbc92d9cb9b2321a40bd8a109133'
            '32f54b34cfdde40dbc7bd6fd9f2ffdb74b8f7308e55e8615062f3798180fd878'
            '2aece50b4260cfaed764a46c79408066954eb46866a5d260e39c5ef08bce7dd4'
            '595af651df63e3ccd2d7e743ab351c8c3a33538241b84aef6fbae1e95aa8e946'
            '783f4c92fec235b58a88439c198cffac5052f59bd64effe90482feee88670d21')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --sbindir=/usr/bin
  make
}

package() {
  # Install Systemd files first
  install -Dm644 "${srcdir}/opensm.service" "${pkgdir}/usr/lib/systemd/system/opensm.service"
  install -Dm644 "${srcdir}/opensm@.service" "${pkgdir}/usr/lib/systemd/system/opensm@.service"
  install -Dm644 "${srcdir}/opensm-genconfig@.service" "${pkgdir}/usr/lib/systemd/system/opensm-genconfig@.service"
  install -Dm644 "${srcdir}/modules-load.d-opensm.conf" "${pkgdir}/usr/lib/modules-load.d/opensm.conf"

  # Install software to pkg directory
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  # add files to pkg
  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  mkdir -p "${pkgdir}/var/cache/opensm"
  mkdir -p "${pkgdir}/etc/opensm"
  touch "${pkgdir}/var/cache/opensm/.empty"
  touch "${pkgdir}/etc/opensm/partitions.conf"

  # cleanup files from pkg
  rm -rf "${pkgdir}/etc/init.d"
}
