# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=opensm
pkgver=3.3.18
pkgrel=3
pkgdesc="InfiniBand subnet manager and administration"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibumad')
source=("https://www.openfabrics.org/downloads/management/${pkgname}-${pkgver}.tar.gz"
        "modules-load.d-opensm.conf"
        "opensm.service"
        "opensm-launch")
md5sums=('642c2a2fda102a7d11add773a89265af'
         '56c1f0f686c506830c0e18be9a3d7579'
         '3a6e3c02187a1b312f5c5c4938a71b20'
         'cc7ce9da9b7e361a0b784610ae532a03')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --sbindir=/usr/bin
  make
}

package() {
  # Install Systemd files first
  install -Dm644 "${srcdir}/opensm.service" "${pkgdir}/usr/lib/systemd/system/opensm.service"
  install -Dm644 "${srcdir}/modules-load.d-opensm.conf" "${pkgdir}/usr/lib/modules-load.d/opensm.conf"
  install -Dm755 "${srcdir}/opensm-launch" "${pkgdir}/usr/share/opensm/opensm-launch"

  # Install software to pkg directory
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  # add files to pkg
  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  mkdir -p "${pkgdir}/var/cache/opensm"
  mkdir -p "${pkgdir}/etc/opensm"
  touch "${pkgdir}/var/cache/opensm/.empty"
  touch "${pkgdir}/etc/opensm/partitions.conf"

  # cleanup files from pkg
  rm -rf "${pkgdir}/etc/init.d"
}
