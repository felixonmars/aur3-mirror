# Contributor: Kyle Keen <keenerd@gmail.com>
pkgname=saleae-logic
pkgver=1.1.15
pkgrel=3
pkgdesc="High speed USB logic analyzer."
arch=(i686 x86_64)
url="http://www.saleae.com/"
license=('unknown')  # closed source, none given
depends=('gcc-libs' 'gtk2' 'libice')
makedepends=()

case $CARCH in
  'i686')
    source=("http://downloads.saleae.com/Logic%20$pkgver%20(32-bit).zip" \
            "http://downloads.saleae.com/Logic%20Guide.pdf")
    md5sums=('70f5cf72c77d9d54777e419fe84e2c21'
             '836db7ec757b1e6453602ed4831d4765')
    __arch='' ;;
  'x86_64')
    source=("http://downloads.saleae.com/Logic%20$pkgver%20(64-bit).zip" \
            "http://downloads.saleae.com/Logic%20Guide.pdf")
    md5sums=('99f709c340e2e0872e02534e2ec6ca77'
             '836db7ec757b1e6453602ed4831d4765')
    __arch=' (64-bit)' ;;
esac

package() {
  # for now, it needs /opt
  cd "$srcdir/Logic $pkgver$__arch"
  install -d "$pkgdir/opt/saleae-logic"
  install -d "$pkgdir/usr/share/doc/saleae-logic"
  cp -r ./ "$pkgdir/opt/saleae-logic/"
  touch "$pkgdir/opt/saleae-logic/Settings/settings.xml"
  chmod 0666 "$pkgdir/opt/saleae-logic/Settings/settings.xml"
  # docs
  install -Dm644 ../../*.pdf "$pkgdir/usr/share/doc/saleae-logic/"
  # udev
  sed -i 's/SYSFS/ATTRS/g' Drivers/99-SaleaeLogic.rules
  install -Dm644 Drivers/99-SaleaeLogic.rules "$pkgdir/etc/udev/rules.d/99-SaleaeLogic.rules" 
  # launch script
  install -d "$pkgdir/usr/bin"
  touch "$pkgdir/usr/bin/logic"
  echo -e "#!/bin/sh\nexec /opt/saleae-logic/Logic > /dev/null 2>&1" > "$pkgdir/usr/bin/logic"
  chmod +x "$pkgdir/usr/bin/logic"
}



