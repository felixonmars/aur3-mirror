# Contributor: Kyle Keen <keenerd@gmail.com>
pkgname=saleae-logic
pkgver=1.1.15
pkgrel=1
pkgdesc="High speed USB logic analyzer."
arch=(i686 x86_64)
url="http://www.saleae.com/"
license=('unknown')  # closed source, none given
depends=('fontconfig' 'gtk2' 'libpng' 'libsm' 'libxau' 'libxdmcp' 'pixman' 'util-linux-ng' 'xcb-util')  # scary list courtesty of ldd
makedepends=()

case $CARCH in
  'i686')
    source=("http://www.saleae.com/downloads/logic/Logic $pkgver (32-bit).zip" \
            "http://www.saleae.com/downloads/logic/Logic Guide.pdf")
    md5sums=('70f5cf72c77d9d54777e419fe84e2c21'
             'bf4b871b45c18a108c00c5fc710a3a99')
    __arch='' ;;
  'x86_64')
    source=("http://www.saleae.com/downloads/logic/Logic $pkgver (64-bit).zip" \
            "http://www.saleae.com/downloads/logic/Logic Guide.pdf")
    md5sums=('99f709c340e2e0872e02534e2ec6ca77'
             'bf4b871b45c18a108c00c5fc710a3a99')
    __arch=' (64-bit)' ;;
esac

package() {
  # for now, it needs /opt
  cd "$srcdir/Logic $pkgver$__arch"
  install -d "$pkgdir/opt/saleae-logic"
  install -d "$pkgdir/usr/share/doc/saleae-logic"
  cp -r ./ "$pkgdir/opt/saleae-logic/"
  touch "$pkgdir/opt/saleae-logic/Settings/settings.xml"
  chmod 0666 "$pkgdir/opt/saleae-logic/Settings/settings.xml"
  # docs
  install -Dm644 ../../*.pdf "$pkgdir/usr/share/doc/saleae-logic/"
  # udev
  install -Dm644 Drivers/99-SaleaeLogic.rules "$pkgdir/etc/udev/rules.d/99-SaleaeLogic.rules" 
  # launch script
  install -d "$pkgdir/usr/bin"
  touch "$pkgdir/usr/bin/logic"
  echo -e "#!/bin/sh\n/opt/saleae-logic/Logic > /dev/null 2>&1" > "$pkgdir/usr/bin/logic"
  chmod +x "$pkgdir/usr/bin/logic"
}


