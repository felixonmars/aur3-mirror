# Contributor: Benjamin Sick

pkgname=gmt
pkgver=4.5.9
pkgrel=2
pkgdesc="Generic Mapping Tools: Collection of tools for manipulating geographic and Cartesian data sets, and generating EPS maps."
arch=(i686 x86_64)
url="http://gmt.soest.hawaii.edu/"
license=('GPL')
depends=('libxaw' 'gdal' 'octave')
optdepends=('csh: for some of example scripts')
install=gmt.install
source=(ftp://ftp.soest.hawaii.edu/gmt/${pkgname}-${pkgver}.tar.bz2 gmtdigitize.patch)
md5sums=('9f241e97774eadd1fe79f4d7022fcaed'
         'dfdf81474451084e7cb73dd459a7f19a')

build() {
  cd ${startdir}/src/GMT${pkgver}
  patch -p0 -i ${startdir}/src/gmtdigitize.patch
  ./configure --prefix=/usr --bindir=/usr/bin --libdir=/usr/lib \
              --includedir=/usr/include --mandir=/usr/share/man \
              --datarootdir=/usr/share/gmt --docdir=/usr/share/gmt/doc \
              --enable-shared --disable-rpath --with-x \
              --enable-triangle --enable-octave --enable-eps --enable-gdal \
              --enable-netcdf=/usr --with-gshhg_dir=/usr/share/gmt/coast
  export MAKEFLAGS="-j1"
  make || return 1
}

package() {
  cd ${startdir}/src/GMT${pkgver}
  make DESTDIR=${pkgdir} \
       MEX_XDIR=$pkgdir/`octave-config --oct-site-dir` \
       MEX_MDIR=$pkgdir/`octave-config --m-site-dir` \
       install-all || return 1
  mkdir -p ${pkgdir}/etc/profile.d
  echo "# GMT environment variable" > ${pkgdir}/etc/profile.d/gmt.sh
  echo "export GMTHOME=/usr/share/gmt" >> ${pkgdir}/etc/profile.d/gmt.sh
  echo 'export GMT_SHAREDIR=${GMTHOME}' >> ${pkgdir}/etc/profile.d/gmt.sh
  chmod 755 ${pkgdir}/etc/profile.d/gmt.sh
}

# vim:set ts=2 sw=2 et:
