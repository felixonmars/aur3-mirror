# Contributor: John Schoenick <john@pointysoftware.net>
# Contributor: Holger "Skittles" Vogel <holger.vogel [at] gmail [dot] com>

pkgname=kvirc4-svn-nokde
pkgver=6295
pkgrel=1
pkgdesc="Qt4 based IRC-Client, compiled without kde4 support - SVN Version"
arch=('i686' 'x86_64')
url="http://www.kvirc.net"
license=('GPL')
depends=('qt' 'glibc' 'openssl' 'zlib' 'perl')
makedepends=('cmake' 'subversion' 'automoc4' 'gettext')
conflicts=('kvirc4' 'kvirc')
provides=('kvirc' 'kvirc4')

_svntrunk=https://svn.kvirc.de/svn/trunk/kvirc
_svnmod=kvirc

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"

  msg "Removing old build dir"
  rm -rf "${_svnmod}-build"

  msg "Creating build dir"
  svn export "$_svnmod"{,-build}

  cd "${_svnmod}-build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWANT_PHONON=OFF -DWANT_KDE4=OFF
  # Other flags include:
  # WANT_AUDIOFILE WANT_BEER WANT_COEXISTENCE WANT_CRYPT WANT_DCC WANT_DEBUG
  # WANT_DOXYGEN WANT_ENV WANT_ESD WANT_GETTEXT WANT_GSM WANT_GTKSTYLE
  # WANT_IGNORE WANT_IPC WANT_IPV WANT_KDE WANT_MEMORY WANT_OGG WANT_OPENSSL
  # WANT_OSS WANT_PERL WANT_PHONON WANT_PIZZA WANT_PYTHON WANT_QT WANT_QTDBUS
  # WANT_QTSVG WANT_QTWEBKIT WANT_STRIP WANT_TRANSPARENCY WANT_UNIVERSAL
  # WANT_USERDOCS WANT_VERBOSE


  make
}

package() {
  cd "$srcdir/${_svnmod}-build"
  make DESTDIR="$pkgdir" install
}
