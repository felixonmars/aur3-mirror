# Maintainer: B <gotleenucks at gmail dot com>
# Contributor: DonVla <donvla@users.sourceforge.net>
# Contributor: BlackEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Ulf Winkelvos <ulf [at] winkelvos [dot] de>
# Contributor: Ralf Barth <archlinux dot org at haggy dot org>
#
# Original credits go to Edgar Hucek <gimli at dark-green dot com>
# for his xbmc-vdpau-vdr PKGBUILD at https://archvdr.svn.sourceforge.net/svnroot/archvdr/trunk/archvdr/xbmc-vdpau-vdr/PKGBUILD.
# This PKGBUILD is an adaptation of the xbmc-git PKGBUILD in the AUR; credits go to the people who contributed to that.

pkgname=xbmc-git-xvba
_commit=c6331599dc165aed98d59a61c2ad32198a18c588
pkgver=20120804
pkgrel=1
pkgdesc="XBMC Media Center - unofficial development version with XvBA support"
provides=('xbmc')
conflicts=('xbmc' 'xbmc-pulse' 'xbmc-svn' 'xbmc-git')
replaces=('xbmc-svn')
arch=('i686' 'x86_64')
url="https://github.com/FernetMenta/xbmc/tree/xvba"
license=('GPL' 'LGPL')
depends=('afpfs-ng' 'faac' 'faad2' 'hicolor-icon-theme' 'jasper' 'libass' 'libcdio' 'libmad' 'libmicrohttpd' 'libplist'
         'libmodplug' 'libnfs' 'libmpeg2' 'libmysqlclient' 'libsamplerate' 'libxrandr' 'libshairport' 'lsb-release' 'lzo2'
	 'mesa-demos' 'smbclient' 'sdl_image>=1.2.10' 'sdl_mixer' 'unzip' 'xorg-xdpyinfo' 'yajl' 'xvba-sdk')
makedepends=('boost' 'cmake' 'git' 'gperf' 'nasm' 'zip')
optdepends=('avahi: to use zerconf features (remote, etc...)' 
            'gdb: for meaningful backtraces in case of trouble - STRONGLY RECOMMENDED' 
            'libssh: support for sshfs'
            'lirc: remote controller support' 
            'pulseaudio: pulseaudio support'
            'udisks: automount external drives' 
            'upower: used to trigger suspend functionality' 
            'python-simplejson: weather service functionality'
            'unrar: access compressed files without unpacking them')
options=('makeflags')
install="${pkgname}.install"
source=(h264_high@L4.1_out_of_spec.patch
	usleep.patch)

_gitroot="git://github.com/FernetMenta/xbmc.git"
_prefix=/usr

build() {
    cd "${srcdir}"
    msg "Connecting to GIT server...."

    if [ -d "${pkgname%-git-xvba}" ] ; then
        cd "${pkgname%-git-xvba}" && git checkout eden && git pull origin 
	git checkout
        #[[ "${_commit}" ]] && git checkout "${_commit}"
        msg "The local files are updated."
        msg "Running make distclean"
        make distclean || :
    else
        git clone "${_gitroot}" -b eden "${pkgname%-git-xvba}"
        cd "${pkgname%-git-xvba}"
	git checkout
        #[[ "${_commit}" ]] && git checkout "${_commit}"
    fi

    # Configure XBMC
    cd "${srcdir}/${pkgname%-git-xvba}"

    # fix lsb_release dependency
    sed -i -e 's:/usr/bin/lsb_release -d:cat /etc/arch-release:' xbmc/utils/SystemInfo.cpp

    # remove "--as-needed" from LDFLAGS
    unset LDFLAGS; LDFLAGS="-Wl,--hash-style=gnu"

    # Switch to High@L5.1 profile if the High@L4.1 rip is
    # out of spec (happens more than you'd think, unfortunately).
    # For this to work you need to enable High@L5.1 support!
    # Instructions: http://bit.ly/ME5Sba
    patch -Np1 -i $srcdir/${source[0]}

    # Fix usleep error
    msg "Adding missing include"
    patch -Np1 -i $srcdir/${source[1]}

    msg "Bootstrapping XBMC"
    ./bootstrap

    msg "Configuring XBMC" 
    export PYTHON_VERSION=2  # external python v2
    ./configure --prefix="${_prefix}" \
                --enable-external-libraries \
                --disable-external-ffmpeg \
		--enable-xvba \
                --enable-debug 

    # Now build
    msg "Running make" 
    make ${MAKEFLAGS}
}

package() {

    cd "${srcdir}/${pkgname%-git-xvba}"
    msg "Running make install" 
    make prefix="${pkgdir}${_prefix}" install

    # Make sure we use python2
    sed -i -e 's/python/python2/g' ${pkgdir}/${_prefix}/bin/xbmc

    # lsb_release fix
    sed -i -e 's/which lsb_release &> \/dev\/null/\[ -f \/etc\/arch-release ]/g' "${pkgdir}${_prefix}/bin/xbmc"
    sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/arch-release/g" "${pkgdir}${_prefix}/bin/xbmc"

    # .desktop files
    install -D -m 0644 "${srcdir}/${pkgname%-git-xvba}/tools/Linux/xbmc.desktop" "${pkgdir}${_prefix}/share/applications/xbmc.desktop"
    install -D -m 0644 "${srcdir}/${pkgname%-git-xvba}/tools/Linux/xbmc-48x48.png" "${pkgdir}${_prefix}/share/pixmaps/xbmc.png"

    # Tools
    install -D -m 0755 "${srcdir}/${pkgname%-git-xvba}/xbmc-xrandr" "${pkgdir}${_prefix}/share/xbmc/xbmc-xrandr"
    install -D -m 0755 "${srcdir}/${pkgname%-git-xvba}/tools/TexturePacker/TexturePacker" "${pkgdir}${_prefix}/share/xbmc/"

    # Licenses
    install -d -m 0755 "${pkgdir}${_prefix}/share/licenses/${pkgname}" 
    for licensef in LICENSE.GPL copying.txt; do 
        mv "${pkgdir}${_prefix}/share/doc/xbmc/${licensef}" "${pkgdir}${_prefix}/share/licenses/${pkgname}" 
    done 

    # Docs 
    install -d -m 0755 "${pkgdir}${_prefix}/share/doc/${pkgname}" 
    for docsf in keymapping.txt README.linux; do 
        mv "${pkgdir}${_prefix}/share/doc/xbmc/${docsf}" "${pkgdir}${_prefix}/share/doc/${pkgname}" 
     done

    # cleanup some stuff
    msg "Cleanup unneeded files"
    rm -rf "${pkgdir}/usr/share/xsessions"
    rm -f "${pkgdir}/usr/share/icons/hicolor/icon-theme.cache"

    # strip
    msg "Stripping binaries"
    find "${pkgdir}" -type f -exec strip {} \; >/dev/null 2>/dev/null
}

sha1sums=('027a9db73ec577bfa2a3afa4deaa03a5ecd56144'
          '6b4f453d459aa297f4760e2de5ea3a8068d7bcf8')
