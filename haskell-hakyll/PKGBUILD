# Maintainer: Leif Warner <abimelech@gmail.com>
_hkgname=hakyll
pkgname=haskell-hakyll
pkgver=4.4.1.2
pkgrel=1
pkgdesc="A static website compiler library"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('haskell-binary<0.8' 'haskell-blaze-html<0.7' 'haskell-blaze-markup<0.6' 'haskell-bytestring' 'haskell-cmdargs<0.11' 'haskell-containers' 'haskell-cryptohash<0.12' 'haskell-data-default<0.6' 'haskell-deepseq' 'haskell-directory' 'haskell-filepath' 'haskell-fsnotify<0.1' 'haskell-http-conduit'  'haskell-http-types<0.9' 'haskell-lrucache<1.2' 'haskell-mtl' 'haskell-network' 'haskell-old-locale' 'haskell-old-time' 'haskell-pandoc<1.13' 'haskell-pandoc-citeproc<0.2' 'haskell-parsec' 'haskell-process' 'haskell-random' 'haskell-regex-base' 'haskell-regex-tdfa<1.2' 'haskell-snap-core<0.10' 'haskell-snap-server<0.10' 'haskell-system-filepath<=0.5' 'haskell-tagsoup<0.14' 'haskell-text' 'haskell-time')
options=('staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('04226b0e446ab65fd4c5fbc79db64590')
prepare() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    sed -ie '195s/1.10/3.0/' hakyll.cabal
}
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
