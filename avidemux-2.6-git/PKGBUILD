#Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=avidemux-2.6-git
pkgver=20121106
pkgrel=1
pkgdesc="A graphical tool to edit video(filter/re-encode/split). (GIT version)"
arch=('i686' 'x86_64')
url="http://www.avidemux.org/"
license=('GPL2')
depends=('libpulse' 'libdca' 'faac' 'lame' 'xvidcore' 'opencore-amr' 'libvpx' 'faad2' 'jack' 'aften' 'x264' 'mesa' 'libva' 'dcaenc' 'glu')
makedepends=('cmake' 'git' 'yasm')
provides=('avidemux-2.6')
conflicts=('avidemux-2.6-svn')
options=('!makeflags')

_gitroot=git://gitorious.org/avidemux2-6/avidemux2-6.git
_gitname=avidemux

# Enable CORE
_CORE=1
# Enable CLI
_CLI=1
# Enable GTK
_GTK=0
# Enable QT
_QT=1
# Enable Plugins
_PLG=1

if [ "${_CORE}" = "1" ]; then
  _buildCORE="--with-core"
else
  _buildCORE="--without-core"
fi
if [ "${_CLI}" = "1" ]; then
  _buildCLI="--with-cli"
else
  _buildCLI="--without-cli"
fi
if [ "${_GTK}" = "1" ]; then
  depends+=('gtk2')
  _buildGTK="--with-gtk"
else
  _buildGTK="--without-gtk"
fi
if [ "${_QT}" = "1" ]; then
  depends+=('qt')
  _buildQT="--with-qt4"
else
  _buildQT="--without-qt4"
fi
if [ "${_PLG}" = "1" ]; then
  _buildPLG="--with-plugins"
else
  _buildPLG="--without-plugins"
fi

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server..."

  if [ -d "${_gitname}" ] ; then
    cd "${_gitname}" && git pull --depth=1
  else
    git clone --depth=1 "${_gitroot}" "${_gitname}"
  fi

  cd "${srcdir}"

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -fr "${_gitname}-build"
  cp -R "${_gitname}" "${_gitname}-build"
  cd "${_gitname}-build"

  bash bootStrap.bash ${_buildCORE} ${_buildCLI} ${_buildGTK} ${_buildQT} ${_buildPLG}

}

package() {
  cp -R "${srcdir}/${_gitname}-build/install/"* "${pkgdir}"
}
