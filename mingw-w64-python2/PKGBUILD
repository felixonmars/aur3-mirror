# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=python2
pkgname=mingw-w64-$_pkgname
pkgver=2.7.5
pkgrel=1
_pybasever=2.7
pkgdesc="A high-level scripting language (mingw-w64)"
arch=('any')
license=('PSF')
url="http://www.python.org/"
depends=('mingw-w64-crt' 'mingw-w64-expat' 'mingw-w64-bzip2' 'mingw-w64-gdbm' 'mingw-w64-openssl' 'mingw-w64-libffi' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config' 'binfmt-wine')
options=('!makeflags' '!libtool' '!buildflags' '!strip')
source=("http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz"
        'crucifixion-freedom-python.tar.gz')
sha1sums=('b7389791f789625c2ba9d897aa324008ff482daf'
          '94deaebee05d1b2f4435ef67747a9558f484d555')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/Python-${pkgver}"
  rm -f Modules/_ctypes/libffi_msvc/win32.S Python/fileblocks.c Misc/python-config.sh.in
  patch -Np1 -i "${srcdir}"/2.7.5/0005-MINGW.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0006-mingw-removal-of-libffi-patch.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0007-mingw-system-libffi.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0010-mingw-osdefs-DELIM.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0015-mingw-use-posix-getpath.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0020-mingw-w64-test-for-REPARSE_DATA_BUFFER.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0025-mingw-regen-with-stddef-instead.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0030-mingw-add-libraries-for-_msi.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0035-MSYS-add-MSYSVPATH-AC_ARG.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0040-mingw-cygwinccompiler-use-CC-envvars-and-ld-from-print-prog-name.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0045-cross-darwin.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0050-mingw-sysconfig-like-posix.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0055-mingw-pdcurses_ISPAD.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0060-mingw-static-tcltk.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0065-mingw-x86_64-size_t-format-specifier-pid_t.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0070-python-disable-dbm.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0075-add-python-config-sh.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0080-mingw-nt-threads-vs-pthreads.patch 
  patch -Np1 -i "${srcdir}"/2.7.5/0085-cross-dont-add-multiarch-paths-if.patch 
  patch -Np1 -i "${srcdir}"/2.7.5/0090-mingw-reorder-bininstall-ln-symlink-creation.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0095-mingw-use-backslashes-in-compileall-py.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0100-mingw-distutils-MSYS-convert_path-fix-and-root-hack.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0105-mingw-MSYS-no-usr-lib-or-usr-include.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0110-mingw-_PyNode_SizeOf-decl-fix.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0115-mingw-cross-includes-lower-case.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0500-mingw-install-LDLIBRARY-to-LIBPL-dir.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0505-add-build-sysroot-config-option.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0510-cross-PYTHON_FOR_BUILD-gteq-275-and-fullpath-it.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0515-mingw-add-GetModuleFileName-path-to-PATH.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0520-Add-interp-Python-DESTSHARED-to-PYTHONPATH-b4-pybuilddir-txt-dir.patch
  patch -Np1 -i "${srcdir}"/2.7.5/0525-msys-monkeypatch-os-system-via-sh-exe.patch

  autoreconf -vfi
  
  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|" Lib/cgi.py

  sed -i "s/python2.3/python2/g" Lib/distutils/tests/test_build_scripts.py \
     Lib/distutils/tests/test_install_scripts.py Tools/scripts/gprof2html.py
  
  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
  
}

build() {
  cd "${srcdir}/Python-${pkgver}"

#   export OPT="${CFLAGS}"
  
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    export LIBFFI_INCLUDEDIR=`${_arch}-pkg-config libffi --cflags-only-I | sed "s|\-I||g"` 
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --build=$CHOST \
      --enable-shared \
      --with-threads \
      --with-system-expat \
      --with-system-ffi \
      --with-dbmliborder=gdbm:ndbm
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/Python-${pkgver}/build-${_arch}"
    make install DESTDIR="$pkgdir"
    install -m 644 libpython${_pybasever}.dll.a "$pkgdir"/usr/${_arch}/lib
    rm -rf "$pkgdir"/usr/${_arch}/share
    rm "$pkgdir"/usr/${_arch}/bin/idle
    rm "$pkgdir"/usr/${_arch}/bin/pydoc
    rm "$pkgdir"/usr/${_arch}/bin/2to3
    rm "$pkgdir"/usr/${_arch}/bin/python-config*
    rm "$pkgdir"/usr/${_arch}/bin/python.exe
    mv "${pkgdir}"/usr/${_arch}/bin/smtpd.py "${pkgdir}"/usr/${_arch}/lib/python${_pybasever}/
    rm "${pkgdir}"/usr/${_arch}/lib/pkgconfig/python.pc    
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
