# Maintainer: chris_L <chrislcenter dash mamoru at yahoo.com dot mx>
# Contributor: Miguel Useche <migueluseche@skatox.com>
# Contributor: Pat <whaevr@archlinux.us>
pkgname=gnomenu
pkgver=2.9.1
pkgrel=1
pkgdesc="A consolidated menu for xfce,gnome2,awn,cairo-dock,docky,xfapplet,etc that brings eye candy to the world of the Gnome menu's."
arch=(any)
url="https://launchpad.net/gnomenu"
license=('GPL3')
depends=('python2' 'pyxdg' 'pycairo' 'pyxml' 'pygtk')
optdepends=('python-xlib' 'python-numpy' 'gettext')
makedepends=('python2-distribute')
source=("http://launchpad.net/gnomenu/trunk/${pkgver}/+download/gnomenu-${pkgver}.tar.gz")
md5sums=('f8b3cd0c342f5e01be32acfbc335b8be')

build() {
	cd ${srcdir}/gnomenu
	sed -i "s|python|python2|" Makefile
}

package() {
	export PYTHON=/usr/bin/python2
	cd ${srcdir}/gnomenu
	make DESTDIR=${pkgdir} install || return 1
	
	cd $pkgdir
	sed -i -e "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" $(find ./ -name '*.py')
	sed -i "s|python\ -u|python2\ -u |g" usr/bin/GnoMenu.py
}
