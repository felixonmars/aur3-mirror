# Maintainer: mar77i <mysatyre@gmail.com>
# Contributor: Max Nemeth <max.nemeth@gmail.com>
# Contributor: Henri HÃ¤kkinen <henuxd@gmail.com>

pkgname=finch-mini
pkgver=2.10.6
pkgrel=1
pkgdesc="A minimal version of Finch; without pidgin, gtkspell, avahi etc. and does not install development files."
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
depends=('nss' 'libxml2')
makedepends=('ca-certificates')
provides=("pidgin=$pkgver" "libpurple=$pkgver")
conflicts=('pidgin' 'libpurple')
options=('!libtool')
source=(http://downloads.sourceforge.net/pidgin/pidgin-$pkgver.tar.bz2)
sha256sums=('3e25a633b97cbfa8326999a30282e7a662a9b9bbf2853be84af0b8fb60392c96')

build() {
  cd "$srcdir/pidgin-$pkgver"

  ./configure --disable-nls --prefix=/usr --sysconfdir=/etc --disable-gtkui \
    --disable-screensaver --disable-sm --disable-startup-notification \
    --disable-gtkspell --disable-gestures --disable-schemas-install \
    --disable-gstreamer --disable-farsight --disable-vv --disable-idn \
    --disable-meanwhile --disable-avahi --disable-dbus --disable-perl \
    --disable-gnutls --disable-tcl --disable-tk --disable-pixmaps-install \
    --disable-doxygen --disable-devhelp --with-system-ssl-certs=/etc/ssl/certs \
    --disable-farstream --disable-gstreamer-interfaces

  make
  make DESTDIR="$pkgdir" install

  # Remove GConf schema file
  rm -rf "$pkgdir/etc"

  # Remove libpurple and pidgin development headers
  rm -rf "$pkgdir/usr/include"

  # Remove pkgconfig scripts
  rm -rf "$pkgdir/usr/lib/pkgconfig"

  # Remove libpurple aclocal macro file
  rm -rf "$pkgdir/usr/share/aclocal"
}
