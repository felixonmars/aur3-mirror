# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>

pkgname="libgit2-git"
pkgver=20120425
pkgrel=1
pkgdesc='a linkable library for git'
arch=('i686' 'x86_64')
url="https://github.com/libgit2/libgit2"
license=('GPL2')
depends=('openssl' 'zlib')
makedepends=('git')
provides=('libgit2')
conflicts=('libgit2')

_gitroot="https://github.com/libgit2/libgit2.git"
_gitname="libgit2"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake . -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_CLAR=OFF
  make
}

#check() {
#  make -C "$_gitname-build" test
#}

package() {
  make -C "$_gitname-build" DESTDIR="$pkgdir" install
}
