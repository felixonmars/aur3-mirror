# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=1.4.0
pkgrel=1
# The AUR does not support split packages; otherwise it might make sense to have
# the library in a "wimlib" package and wimlib-imagex in a "wimtools" package,
# or something.
pkgdesc="A library and program to extract, create, modify, and mount WIM (Windows Imaging) files"
arch=("i686" "x86_64")
url="http://sourceforge.net/projects/wimlib"
license=("GPL3")
depends=("openssl" "fuse" "libxml2" "ntfs-3g" "attr")
optdepends=("cdrkit: for making ISO image of Windows PE"
	"mtools: for making disk image of Windows PE"
	"syslinux: for making disk image of Windows PE"
	"cabextract: for extracting Windows PE from the WAIK"
	"ntfsprogs: for making NTFS filesystems")
checkdepends=("ntfsprogs")
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")

build() {
	cd "$pkgname-$pkgver"
	./configure --with-libcrypto --with-fuse --with-ntfs-3g \
			--enable-xattr --prefix=/usr
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	for file in README NEWS README.WINDOWS; do
		install -Dm644 $file "$pkgdir/usr/share/doc/$pkgname/$file"
	done
}
md5sums=('41153eeffd9c5278e17e4b105603b120')
