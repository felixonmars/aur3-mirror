# Maintainer: DMBuce <dmbuce@gmail.com>
pkgname=clicraft-devel-git
pkgver=20120706
pkgrel=1
pkgdesc="A command-line wrapper for a minecraft or bukkit server."
arch=("any")
url="https://github.com/DMBuce/clicraft"
license=('BSD')
depends=("bash" "tmux" "curl" "java-runtime")
makedepends=("git" "asciidoc")
#checkdepends=()
optdepends=("c10t: for map.sh action script" "mcexplore: for explore.sh action script")
provides=('clicraft')
conflicts=('clicraft')
backup=("etc/clicraft/action.d/explore.sh"
        "etc/clicraft/action.d/map.sh")
install="$pkgname.install"
source=("$pkgname.install"
        "$pkgname.rc")
md5sums=('631669f302dffa059b4bdc915374d3d6'
         'fbd593f78d04be79bf4b884c6b747379')

_gitname="clicraft"
_gitroot="git://github.com/DMBuce/clicraft.git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname" --branch devel
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build" --branch devel
  cd "$srcdir/$_gitname-build"

  autoconf
  ./configure --prefix=/usr \
              --libexecdir=/usr/lib \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-mcexplore \
              --with-c10t
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install

  install -Dm755 "$startdir/$pkgname.rc" "${pkgdir}/etc/rc.d/$pkgname"
}

# vim:set ts=2 sw=2 et:
