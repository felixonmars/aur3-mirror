# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Superstar655 <choman000 at hotmail dot com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=sickbeard
pkgver=alpha494
pkgrel=1
pkgdesc="A PVR application that downloads and manages your TV shows."
arch=(any)
url="http://www.sickbeard.com"
license=(GPL3)
depends=(python2-cheetah)
optdepends=('sabnzbd: a NZB downloader that integrates well with Sick-Beard'
            'python-notify: to enable desktop notifications')
backup=(etc/conf.d/${pkgname}
        etc/${pkgname}/config.ini)
install=${pkgname}.install
changelog=Changelog
conflicts=(${pkgname}-git)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/midgetspy/Sick-Beard/tarball/build-${pkgver#alpha}"
        "${pkgname}.sh"
        "${pkgname}.conf.d"
        "${pkgname}.rc.d"
        "${pkgname}.ini")
md5sums=('ffa9a21ee0c99af8319e434323f4ca64'
         '1e3b4ab901fa799d5e1f61fd5cef00ae'
         'f7c466362ceab946a1df0d682d6f912f'
         '5691396c67e0d59c2800b0d252f81b08'
         '3ebf06fc9cecaf3b71e4c035ce8a0230')

build() {
  cd "${srcdir}"/midgetspy*/

  msg2 "Remove unneeded stuff..."
  rm -rvf tests/ init.* setup.py updater.py updates.txt googlecode_upload.py

  msg2 "Fix standalone Python scripts shebangs and permissions..."
  sed -i '1s/python/python2/' autoProcessTV/*.py
  chmod -v +x autoProcessTV/*.py
}

package() {
  cd "${srcdir}"/midgetspy*/

  msg2 "Install sources in /usr/lib/${pkgname}..."
  install -dm755                                                 "${pkgdir}/usr/lib/${pkgname}"
  cp -r {autoProcessTV,cherrypy,data,lib,sickbeard,SickBeard.py} "${pkgdir}/usr/lib/${pkgname}/"

  msg2 "Install executable in /usr/bin..."
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  msg2 "Install daemon script in /etc/rc.d..."
  install -Dm755 "${srcdir}/${pkgname}.rc.d" "${pkgdir}/etc/rc.d/${pkgname}"

  msg2 "Install daemon conf file in /etc/conf.d..."
  install -Dm644 "${srcdir}/${pkgname}.conf.d" "${pkgdir}/etc/conf.d/${pkgname}"

  msg2 "Install configuration file in /etc..."
  install -Dm644 "${srcdir}/${pkgname}.ini" "${pkgdir}/etc/${pkgname}/config.ini"

  msg2 "Install documentation resources in /usr/share/doc/${pkgname}..."
  install -Dm644 readme.md "${pkgdir}/usr/share/doc/${pkgname}/readme.md"

  msg2 "Install copyright resources in /usr/share/licenses/${pkgname}..."
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt"
}

# vim:set ts=2 sw=2 et:
