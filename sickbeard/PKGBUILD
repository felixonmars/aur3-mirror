# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Superstar655 <choman000 at hotmail dot com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=sickbeard
pkgver=alpha502
pkgrel=1
pkgdesc="A PVR application that downloads and manages your TV shows."
arch=(any)
url="http://www.sickbeard.com"
license=(GPL3)
depends=(python2-cheetah)
optdepends=('sabnzbd: a NZB downloader that integrates well with Sick-Beard'
            'python-notify: to enable desktop notifications')
backup=(etc/conf.d/${pkgname}
        var/lib/${pkgname}/config.ini)
install=${pkgname}.install
changelog=Changelog
conflicts=(${pkgname}-git)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/midgetspy/Sick-Beard/tarball/build-${pkgver#alpha}"
        "${pkgname}.sh"
        "${pkgname}.conf.d"
        "${pkgname}.service"
        "${pkgname}.tmpfiles"
        "${pkgname}.ini")
md5sums=('edbdf0f5d40cf84c9817bf13bbcc5e14'
         '1e3b4ab901fa799d5e1f61fd5cef00ae'
         '7ccc670beaa2121f7c31c3a82a09a8e8'
         'dfcc53847e1d5d60e2b5add0d5a922db'
         'e392308a8a2611c74786821c2e15fd5e'
         '3ebf06fc9cecaf3b71e4c035ce8a0230')

build() {
  cd "${srcdir}"/midgetspy*/

  msg2 "Remove unneeded stuff..."
  rm -rvf tests/ init.* setup.py updater.py updates.txt googlecode_upload.py

  msg2 "Fix standalone Python scripts shebangs and permissions..."
  sed -i '1s/python/python2/' autoProcessTV/*.py
  chmod -v +x autoProcessTV/*.py

  msg2 "Patch version.py..."
  sed -i "1s/build Nonea/${pkgver}/" sickbeard/version.py
}

package() {
  cd "${srcdir}"/midgetspy*/

  msg2 "Install sources in /usr/lib/${pkgname}..."
  install -dm755                                                 "${pkgdir}/usr/lib/${pkgname}"
  cp -r {autoProcessTV,cherrypy,data,lib,sickbeard,SickBeard.py} "${pkgdir}/usr/lib/${pkgname}/"

  msg2 "Generate /usr/lib/${pkgname}/version.txt..."
  echo 0 > "${pkgdir}/usr/lib/${pkgname}/version.txt"

  msg2 "Install executable in /usr/bin..."
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  msg2 "Install daemon script in /usr/lib/systemd/system..."
  install -Dm755 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

  msg2 "Install daemon conf file in /etc/conf.d..."
  install -Dm644 "${srcdir}/${pkgname}.conf.d" "${pkgdir}/etc/conf.d/${pkgname}"

  msg2 "Install temporary files configuration in /usr/lib/tmpfiles.d..."
  install -Dm755 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

  msg2 "Install configuration file in /var/lib/${pkgname}..."
  install -Dm644 "${srcdir}/${pkgname}.ini" "${pkgdir}/var/lib/${pkgname}/config.ini"

  msg2 "Install documentation resources in /usr/share/doc/${pkgname}..."
  install -Dm644 readme.md "${pkgdir}/usr/share/doc/${pkgname}/readme.md"

  msg2 "Install copyright resources in /usr/share/licenses/${pkgname}..."
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt"
}

# vim:set ts=2 sw=2 et:
