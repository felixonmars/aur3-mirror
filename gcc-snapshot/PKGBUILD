# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-snapshot
_ver=4.8
_snapshot=20120916
pkgver=${_ver}.${_snapshot}
pkgrel=1
pkgdesc="The GNU Compiler Collection developmental snapshot"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL' 'custom')
url="http://gcc.gnu.org"
depends=('glibc' 'binutils' 'gmp' 'mpfr' 'libmpc' 'isl' 'cloog')
makedepends=('flex' 'bison')
checkdepends=('dejagnu')
options=('!libtool')
source=(ftp://gcc.gnu.org/pub/gcc/snapshots/${_ver}-${_snapshot}/gcc-${_ver}-${_snapshot}.tar.bz2
	gcc_pure64.patch)
md5sums=('d590f1b6b2c8d06ca471e01bcd7ac4b5'
         'ced48436c1b3c981d721a829f1094de1')

build() {
  # add ada,fortran,go,objc,obj-c++ as wanted
  # additional languages not tested... leave a comment about conflicts
  _languages="c,c++,lto"

  cd ${srcdir}/gcc-${_ver}-${_snapshot}

  # Do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  # Do not run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 -i ${srcdir}/gcc_pure64.patch
  fi

  cd ${srcdir}
  mkdir gcc-build && cd gcc-build

  ${srcdir}/gcc-${_ver}-${_snapshot}/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --datadir=/usr/share/gcc-${_ver} \
      --with-bugurl=http://aur.archlinux.org/packages.php?ID=16045 \
      --enable-languages=${_languages} \
      --enable-shared --enable-threads=posix \
      --with-system-zlib --enable-__cxa_atexit \
      --disable-libunwind-exceptions --enable-clocale=gnu \
      --disable-libstdcxx-pch --enable-libstdcxx-time \
      --enable-gnu-unique-object --enable-linker-build-id \
      --enable-lto --enable-gold --enable-ld=default \
      --enable-plugin --with-plugin-ld=ld.gold \
      --with-linker-hash-style=gnu \
      --disable-multilib --disable-libssp \
      --enable-checking=release --disable-werror \
      --program-suffix=-${_ver} --enable-version-specific-runtime-libs

  make
}

check() {
  cd gcc-build

  # increase stack size to prevent test failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=31827
  ulimit -s 32768

  # do not abort on error as some are "expected"
  make -k check || true
  ${srcdir}/gcc-${_ver}-${_snapshot}/contrib/test_summary
}

package() {
  cd ${srcdir}/gcc-build

  make -j1 DESTDIR=${pkgdir} install

  rm $pkgdir/usr/lib/gcc/$CHOST/4.8.0/libstdc++.so.6.0.17-gdb.py

  # deal with conflicting man and info pages
  rm ${pkgdir}/usr/share/man/man7/{fsf-funding,gfdl,gpl}.7
  for i in $pkgdir/usr/share/info/*; do
    mv $i ${i//.info}-${_ver}.info
  done

  install -Dm644 ${srcdir}/gcc-${_ver}-${_snapshot}/COPYING.RUNTIME \
    ${pkgdir}/usr/share/licenses/$pkgname/RUNTIME.LIBRARY.EXCEPTION
}
