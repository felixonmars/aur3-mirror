# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
pkgname=comodo-modules
pkgver=3.17.1_1
_kernel=${pkgver/_/-}-ARCH
pkgrel=1
pkgdesc="Modules for Comodo Antivirus for providing realtime protection"
arch=('i686' 'x86_64')
url="http://www.bondoffamily-net.com/~kinta-chan"
license=('GPL3')
depends=('linux>=3.17' 'linux<3.18' 'comodo')
makedepends=('linux-headers>=3.17' 'linux-headers<3.18')
install=comodo-modules.install
source=("http://www.bondoffamily-net.com/~kinta-chan/techknow/DownLoad/src/beta/driver.tar")
md5sums=('fcc72b21bc510f26f79d3020977dd56c')

prepare() {
	cd "$srcdir/driver"

  # make sure we build for the installed kernel, not the booted one
  sed -i "s:\`uname -r\`:${_kernel}:g" Makefile
}

build() {
	cd "$srcdir/driver"
	ROOT_PATH="$srcdir/driver" make all
}

package() {
	cd "$srcdir/driver"
	ROOT_PATH="$srcdir/driver" make INSTALL_MOD_PATH="$pkgdir/usr" install

  # fix .install file
  sed -i "s:KERNEL=.*:KERNEL=${_kernel}:" "${startdir}/comodo-modules.install"
}
