# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Contributor: Leonardo Milani <leonardo@leonardomiliani.com>

pkgname=jag
pkgver=0.3.1
pkgrel=6
pkgdesc="A free arcade-puzzle 2D game in which you have to break all the target pieces"
arch=('i686' 'x86_64')
url="http://jag.xlabsoft.com/"
depends=('qt' 'sdl_mixer')
optdepends=('jag-themes: for additional graphic themes')
license=('GPL3')
source=(http://jag.xlabsoft.com/files/$pkgname-$pkgver-src.zip 
		'jag.desktop' 'jag.png'
		'jag-level-editor.desktop' 'jag-leveleditor.png')
md5sums=('78a408629ca9858dc8279d9c124386f7'
		 '89494a96f138793852744484a05f62d2'
		 '75bad1a145baa8a9b3fb79b65c959ae6'
		 '60a2470557e66eed3317d93cf47f4fdd'
		 '6a733b853844f834efcd182a9182aba6'
		 ) 
build() {
	cd $pkgname-$pkgver-src

    ### fixing paths & linker flags ###
    sed -i -e 's#/usr/local/bin#/usr/bin#g' \
           -e 's#/usr/local/games#/usr/share#g'\
           -e 's#LIBS += -lSDLmain#LIBS += -lSDL -lX11#'\
    Game.pro main.cpp editor/editor.pro
    
    ## building game
	qmake 
	make 
	
    ## building level editor  
	cd editor
	qmake 
	make 
}
package() {
    ## installing game
    cd $pkgname-$pkgver-src
	make INSTALL_ROOT=${pkgdir} install 
	install -D -m644 ../jag.png ${pkgdir}/usr/share/pixmaps/jag.png 
	install -D -m644 ../jag.desktop ${pkgdir}/usr/share/applications/jag.desktop     	
    
    ## installing level editor
    cd editor
	make INSTALL_ROOT=${pkgdir} install 
	install -D -m644 ${srcdir}/jag-leveleditor.png  ${pkgdir}/usr/share/pixmaps/jag-leveleditor.png 
	install -D -m644 ${srcdir}/jag-level-editor.desktop ${pkgdir}/usr/share/applications/jag-level-editor.desktop     	
}
