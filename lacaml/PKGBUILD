# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Kostas Andreadis <kmandreadis@gmail.com>

pkgname=lacaml
pkgver=7.0.3
pkgrel=2
pkgdesc="Linear Algebra for OCaml"
arch=('i686' 'x86_64')
url="http://ocaml.info/home/ocaml_sources.html"
license=('LGPL')
depends=('ocaml' 'lapack' 'blas')
makedepends=('ocaml-findlib')
source=("https://bitbucket.org/mmottl/lacaml/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('06d83c5061c512a7c661ba7931e9ac03')
options=(!strip)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
