# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=gnome-disk-utility
pkgname=$_pkgname-git
pkgver=3.8.1.42.g256290b
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
groups=('gnome-extra')
depends=('gtk3' 'udisks2>=2.1.0' 'hicolor-icon-theme' 'libsecret' 'libpwquality' 'desktop-file-utils' "xz>=1.5.0")
makedepends=('intltool' 'docbook-xsl')
options=('!libtool' '!emptydirs')
install=gnome-disk-utility.install
replaces=('gnome-disk-utility')
provides=('gnome-disk-utility' 'gnome-disk-utility=3.9.0')
conflicts=('gnome-disk-utility')
source=('git://git.gnome.org/gnome-disk-utility')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
