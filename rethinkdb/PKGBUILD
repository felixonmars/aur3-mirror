# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>

pkgname=rethinkdb
pkgver=1.2.6
pkgrel=1
pkgdesc="An open-source distributed database built with love."
arch=('i686' 'x86_64')
url="http://www.rethinkdb.com/"
license="AGPL"
depends=('boost-libs' 'protobuf')
makedepends=('boost' 'curl' 'ctags' 'python2' 'lessc' 'coffee-script' 'ruby_protobuf')
install=rethinkdb.install
source=(
	$pkgname-$pkgver.tar.gz::https://github.com/rethinkdb/$pkgname/archive/v$pkgver.tar.gz
	rethinkdb-tmpfile.conf
	rethinkdb.service
)

build() {
	cd "$srcdir/$pkgname-$pkgver"

	#Use dyn.linked protobuf
    #Change python invocations to python2
	sed -e '191 c STATIC_RECOMMENDS_INDIFFERENT:=boost_serialization boost_program_options' \
	    -e '192 c LDFLAGS+=-lprotobuf' \
	    -e 's/DebianV8Version/#DebianV8Version/' \
	    -e 's/python -c/python2 -c/g' \
	    -i src/Makefile

	sed -e 's/libprotobuf.a/libprotobuf.so/g' \
	    -e 's/libprotoc.a/libprotoc.so/g' \
	    -i external/protobuf-plugin-closure/Makefile

	find "$srcdir/$pkgname-$pkgver" -type f -exec sed -i '1 s/python$/python2/' {} +

    cd src
	make prefix=/usr DESTDIR="$pkgdir" FORCEVERSION=1 PVERSION=$pkgver DEBUG=0 NO_TCMALLOC=1 BUILD_DRIVERS=0 WEBRESDIR=/usr/share/$pkgname/web
}


package() {
	cd "$srcdir/$pkgname-$pkgver"
	make prefix=/usr DESTDIR="$pkgdir" FORCEVERSION=1 PVERSION=$pkgver DEBUG=0 NO_TCMALLOC=1 BUILD_DRIVERS=0 WEBRESDIR=/usr/share/$pkgname/web install

	cd "$pkgdir"
	rm -rf etc var

	cd usr/share/rethinkdb
	install -Dm644 etc/bash_completion.d/rethinkdb.bash "$pkgdir/usr/share/bash-completion/completions/rethinkdb"
	install -Dm755 scripts/rdb_migrate "$pkgdir/usr/bin/rdb_migrate"
	rm -rf etc scripts

	install -Dm644 "$srcdir/rethinkdb-tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/rethinkdb.conf"
	install -Dm644 "$srcdir/rethinkdb.service" "$pkgdir/usr/lib/systemd/system/rethinkdb.service"
}
sha256sums=('33540213b36150ab35bee758e8921041a2a49858b883cf50f9954549bfe98b50'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            '9e80e5dd18ceb6006c8d82d1e602b47c680b41fa9ea5af90d643bfc8153a0ea3')
