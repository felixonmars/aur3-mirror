# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor Daniel Illgen <daniel.illgen@googlemail.com>
# Contributor: Christer Solskogen <solskogen@carebears.mine.nu>
# Contributor: Giovanni Condello <condellog@gmail.com>

pkgname=aranym
pkgver=0.9.13
pkgrel=1
pkgdesc="Atari Running on Any Machine"
url="http://aranym.sourceforge.net"
depends=('gcc-libs' 'bash' 'sdl_image' 'mpfr')
arch=('i686' 'x86_64')
license=('GPL')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/"$pkgname"_"$pkgver".orig.tar.gz)
md5sums=('3f72901c5752a5affa939dd38f3f9174')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # aranym-jit isn't available for x86_64 architecture
  if [ "$CARCH" == "i686" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --disable-nat-debug --enable-jit-compiler --enable-nfjpeg
    make
    mv aranym aranym-jit
    make clean
  fi

  ./configure --prefix=/usr --mandir=/usr/share/man --disable-nat-debug --enable-fullmmu --enable-lilo --enable-fixedvideoram --enable-nfjpeg
  make
  mv aranym aranym-mmu
  make clean

  ./configure --prefix=/usr --mandir=/usr/share/man --disable-nat-debug --disable-nat-debug --enable-nfjpeg
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  if [ "$CARCH" == "i686" ]; then
    install aranym-jit "$pkgdir"/usr/bin
  fi

  install aranym-mmu "$pkgdir"/usr/bin
  install aratapif "$pkgdir"/usr/bin
  install -D contrib/icon-32.png \
    ${pkgdir}/usr/share/pixmaps/aranym.png
  install -D contrib/aranym.desktop \
    ${pkgdir}/usr/share/applications/aranym.desktop
}
