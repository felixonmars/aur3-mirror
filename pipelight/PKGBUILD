# Maintainer: Anish Bhatt anish [at] gatech [dot] edu

pkgname='pipelight'
pkgver=1
pkgrel=7
pkgdesc="A browser plugin which allows one to use windows only plugins inside Linux browser"
url="https://launchpad.net/pipelight"
arch=('i686' 'x86_64')
license=('LGPL')

depends=('wine-silverlight' 'ttf-ms-fonts' 'wget')

# Set value to 1 if you want to compile pluginloader yourself
_compilepluginloader=0

if [[ $_compilepluginloader != 0 ]]; then
  if [[ "$CARCH" == "x86_64" ]]; then
  makedepends=('mingw-w64-gcc')
  else
  makedepends=('mingw32-gcc')
  fi
fi

source=("https://bitbucket.org/mmueller2012/pipelight/get/v0.$pkgver.tar.bz2"
  "https://launchpad.net/pipelight/trunk/0.1/+download/pluginloader-prebuilt-v0.1-1.tar.gz"
  "wine-silverlight5.1-installer.exe::http://silverlight.dlservice.microsoft.com/download/B/3/C/B3CF6815-40B1-4E36-8746-C4A0381AD260/20513.00/runtime/Silverlight.exe"
  "wine-silverlight5.1-installer.install-script")

md5sums=('1027bbf2de406e81750d7350f47c5e73'
         '13631622630d054fe643f1cf7acb13ee'
         '971894515dd26a26175883031521d8b3'
         '3ce904b5915209411aad68218e7ba620')

install=pipelight.install

_prefix=/usr

#change this if your wine-silverlight is installed elsewhere, or if you prefer a different wine directory
_wine=/usr
_wineprefix=.wine
_srcdir=mmueller2012-pipelight-93b506606a0f

prepare() {
  pushd $_srcdir

  #The next two lines can't be used if you want to compile your own pluginloader.exe
  if [[ $_compilepluginloader == 0 ]]; then
    sed -i 's|src/windows||g' Makefile
    sed -i '15d' Makefile
  fi

  pushd src/windows
  sed -i '2 a\ CXXFLAGS        := -static-libgcc -static-libstdc++ $(CXXFLAGS)' Makefile
  popd
  pushd share
  sed -i "s|/opt/wine-compholio|$_wine|g" pipelight
  sed -i "s|PLUGIN_LOADER_PATH|$_prefix/share/pipelight/pluginloader.exe|g" pipelight
  sed -i 's|true|false|g' pipelight
  if [[ "$CARCH" == "x86_64" ]]; then
    sed -i 's|Files|Files (x86)|g' pipelight
  fi

  # this needs to change if you're using a custom prefix for wine-silverlight, will happen automatically if _wineprefix changed
  sed -i "s|.wine-pipelight|$_wineprefix|g" pipelight
  popd
  cd ..

  _home='$HOME'/$_wineprefix
  sed -i "1 a\WINE=$_wine/bin/wine" wine-silverlight5.1-installer.install-script
  sed -i "2 a\INSTDIR=$_prefix/share/pipelight" wine-silverlight5.1-installer.install-script
  sed -i "3 a\WINEPREFIX=$_home" wine-silverlight5.1-installer.install-script
  if [[ "$CARCH" == "x86_64" ]]; then
    sed -i 's|Files|Files (x86)|g' wine-silverlight5.1-installer.install-script
  fi
  popd
}

build() {
  pushd $_srcdir
  ./configure --prefix=$_prefix
  make
  popd
}

package() {
  make -C $_srcdir PREFIX=$_prefix DESTDIR=$pkgdir install
  install -Dm644 wine-silverlight5.1-installer.exe ${pkgdir}/$_prefix/share/pipelight/.
  install -Dm755 wine-silverlight5.1-installer.install-script ${pkgdir}/$_prefix/share/pipelight/wine-silverlight5.1.installer

  if [[ $_compilepluginloader  == 0 ]]; then
    install -Dm644 l*.dll ${pkgdir}/$_prefix/share/pipelight/.
    install -Dm644 pluginloader.exe ${pkgdir}/$_prefix/share/pipelight/.
  fi
}

# vim:set ts=2 sw=2 et:
