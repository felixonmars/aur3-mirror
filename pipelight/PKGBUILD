# Maintainer: Anish Bhatt anish [at] gatech [dot] edu

pkgname='pipelight'
pkgver=1
pkgrel=5
pkgdesc="A browser plugin which allows one to use windows only plugins inside Linux browser"
url="https://launchpad.net/pipelight"
arch=('i686' 'x86_64')
license=('LGPL')

depends=('wine-silverlight' 'ttf-ms-fonts' 'wget')
if [[ "$CARCH" == "x86_64" ]]; then
makedepends=('mingw-w64-gcc')
else
makedepends=('mingw32-gcc')
fi

source=("https://bitbucket.org/mmueller2012/pipelight/get/v0.$pkgver.tar.bz2"
  "wine-silverlight5.1-installer.exe::http://silverlight.dlservice.microsoft.com/download/B/3/C/B3CF6815-40B1-4E36-8746-C4A0381AD260/20513.00/runtime/Silverlight.exe"
  "wine-silverlight5.1-installer.install-script")

md5sums=('1027bbf2de406e81750d7350f47c5e73'
         '971894515dd26a26175883031521d8b3'
         '1b479698988fb02899669b4a6e523d5f')

install=pipelight.install

_prefix=/usr

#change this if your wine-silverlight is installed elsewhere, or if you prefer a different wine directory
_wine=/usr
_wineprefix=.wine
_srcdir=mmueller2012-pipelight-93b506606a0f

prepare() {
  pushd $_srcdir
  pushd src/windows
  sed -i '2 a\ CXXFLAGS        := -static-libgcc -static-libstdc++ $(CXXFLAGS)' Makefile
  popd
  pushd share
  sed -i "s|/opt/wine-compholio|$_wine|g" pipelight
  sed -i "s|PLUGIN_LOADER_PATH|$_prefix/share/pipelight/pluginloader.exe|g" pipelight
  sed -i 's|true|false|g' pipelight
  if [[ "$CARCH" == "x86_64" ]]; then
    sed -i 's|Files|Files (x86)|g' pipelight
  fi

  # this needs to change if you're using a custom prefix for wine-silverlight, will happen automatically if _wineprefix changed
  sed -i "s|.wine-pipelight|$_wineprefix|g" pipelight
  popd
  cd ..

  _home='$HOME'/$_wineprefix
  sed -i "1 a\WINE=$_wine/bin/wine" wine-silverlight5.1-installer.install-script
  sed -i "2 a\INSTDIR=$_prefix/share/pipelight" wine-silverlight5.1-installer.install-script
  sed -i "3 a\WINEPREFIX=$_home" wine-silverlight5.1-installer.install-script
  if [[ "$CARCH" == "x86_64" ]]; then
    sed -i 's|Files|Files (x86)|g' wine-silverlight5.1-installer.install-script
  fi
  popd
}

build() {
  pushd $_srcdir
  ./configure --prefix=$_prefix
  make
  popd
}

package() {
  make -C $_srcdir PREFIX=$_prefix DESTDIR=$pkgdir install
  install -Dm644 wine-silverlight5.1-installer.exe ${pkgdir}/$_prefix/share/pipelight/.
  install -Dm644 wine-silverlight5.1-installer.install-script ${pkgdir}/$_prefix/share/pipelight/wine-silverlight5.1.installer
}

# vim:set ts=2 sw=2 et:
