# Maintainer: Anish Bhatt anish [at] gatech [dot] edu

pkgname='pipelight'
pkgver=0.1.4
pkgrel=1
epoch=1
pkgdesc="A browser plugin which allows one to use windows only plugins inside Linux browser"
url="https://launchpad.net/pipelight"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1' 'MPL')

depends=('wine-silverlight' 'wine-browser-installer' 'ttf-ms-fonts' 'wget')
if  [[ "$CARCH" == "x86_64" ]]; then
  depends+=('lib32-libsm' 'lib32-libgl')
else
  depends+=('libsm' 'libgl')
fi

# Set value to 1 if you want to compile pluginloader yourself
_compilepluginloader=0

if [[ $_compilepluginloader != 0 ]]; then
  if [[ "$CARCH" == "x86_64" ]]; then
  makedepends=('mingw-w64-gcc')
  else
  makedepends=('mingw32-gcc')
  fi
fi

makedepends+=('cabextract')

source=("https://bitbucket.org/mmueller2012/pipelight/get/v${pkgver%.*}-${pkgver##*.}.tar.bz2"
  "https://launchpad.net/pipelight/trunk/0.1/+download/pluginloader-prebuilt-v${pkgver%.*}-${pkgver##*.}.tar.gz")

md5sums=('cf8bbec066a7475bcf5b92310609b3c2'
         '65564d3e363d6be81c8f602133aa7afe')

install=pipelight.install

_srcdir=mmueller2012-pipelight-927e48b6b1b9

#this changes the install location for the pipelight executables
_prefix=/usr

#change this if your wine-silverlight is installed elsewhere, or if you prefer a different wine directory
_wine=/usr
_wineprefix=.wine-pipelight

prepare() {
  pushd $_srcdir

  #The next two lines can't be used if you want to compile your own pluginloader.exe
  if [[ $_compilepluginloader == 0 ]]; then
    sed -i '1s|src/windows||g' Makefile
    sed -i '16d' Makefile
    sed -i '54s|gccRun|#gccRun|g' share/pipelight
  fi

  pushd src/windows
  sed -i '2 a\ CXXFLAGS        := -static-libgcc -static-libstdc++ $(CXXFLAGS)' Makefile
  popd
  pushd share
  sed -i "21s|/opt/wine-compholio|$_wine|g" pipelight
  sed -i "s|PLUGIN_LOADER_PATH|$_prefix/share/pipelight/pluginloader.exe|g" pipelight
  sed -i "s|wine-browser-installer|wine-silverlight|g" pipelight
  sed -i "115s|wine-silverlight/install-dependency|wine-silverlight/install-dependency-pipelight|g" pipelight

  # embed bug fixed, this is no longer needed
  #sed -i '109s|true|false|g' pipelight

  #in case you want to use 64bit wine, you would need this
  #if [[ "$CARCH" == "x86_64" ]]; then
  #  sed -i 's|Files|Files (x86)|g' pipelight
  #fi

  # this needs to change if you're using a custom prefix for wine-silverlight, will happen automatically if _wineprefix changed
  sed -i "30s|.wine-pipelight|$_wineprefix|g" pipelight
  popd
  popd
}

build() {
  pushd $_srcdir
  ./configure --prefix=$_prefix
  make
  popd
}

package() {
  make -C $_srcdir PREFIX=$_prefix DESTDIR=$pkgdir install
  install -dm755 ${pkgdir}/usr/share/wine-silverlight/.
  install -Dm755 ${_srcdir}/misc/install-dependency ${pkgdir}/usr/share/wine-silverlight/install-dependency-pipelight

  if [[ $_compilepluginloader  == 0 ]]; then
    install -Dm644 l*.dll ${pkgdir}/$_prefix/share/pipelight/.
    install -Dm644 pluginloader.exe ${pkgdir}/$_prefix/share/pipelight/.
  fi
}

# vim:set ts=2 sw=2 et:
