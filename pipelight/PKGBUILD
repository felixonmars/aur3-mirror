# Maintainer: Anish Bhatt anish [at] gatech [dot] edu

pkgname='pipelight'
pkgver=1
_relver=2-prerelease1
pkgrel=9
pkgdesc="A browser plugin which allows one to use windows only plugins inside Linux browser"
url="https://launchpad.net/pipelight"
arch=('i686' 'x86_64')
license=('LGPL')

depends=('wine-silverlight' 'ttf-ms-fonts' 'wget')

# Set value to 1 if you want to compile pluginloader yourself
_compilepluginloader=0

if [[ $_compilepluginloader != 0 ]]; then
  if [[ "$CARCH" == "x86_64" ]]; then
  makedepends=('mingw-w64-gcc')
  else
  makedepends=('mingw32-gcc')
  fi
fi

source=("https://bitbucket.org/mmueller2012/pipelight/get/v0.$pkgver-$_relver.tar.bz2"
  "https://launchpad.net/pipelight/trunk/0.1/+download/pluginloader-prebuilt-v0.$pkgver-$_relver.tar.gz"
  "wine-silverlight5.1-installer.exe::http://silverlight.dlservice.microsoft.com/download/B/3/C/B3CF6815-40B1-4E36-8746-C4A0381AD260/20513.00/runtime/Silverlight.exe"
  "wine-silverlight5.1-installer.install-script")

md5sums=('dae174f866c3e602ff3e1c9217ce9f67'
         '8e5191954970b2eb513b9375be99a2d0'
         '971894515dd26a26175883031521d8b3'
         'accbbd4d49d3c2e484703eb22d87a2bf')

install=pipelight.install

_prefix=/usr

#change this if your wine-silverlight is installed elsewhere, or if you prefer a different wine directory
_wine=/usr
_wineprefix=.wine-pipelight
_srcdir=mmueller2012-pipelight-afdc68a75e93

prepare() {
  pushd $_srcdir

  #The next two lines can't be used if you want to compile your own pluginloader.exe
  if [[ $_compilepluginloader == 0 ]]; then
    sed -i 's|src/windows||g' Makefile
    sed -i '15d' Makefile
    sed -i '22s|gccrun|#gccrun|g' share/pipelight
  fi

  pushd src/windows
  sed -i '2 a\ CXXFLAGS        := -static-libgcc -static-libstdc++ $(CXXFLAGS)' Makefile
  popd
  pushd share
  sed -i "4s|/opt/wine-compholio|$_wine|g" pipelight
  sed -i "s|PLUGIN_LOADER_PATH|$_prefix/share/pipelight/pluginloader.exe|g" pipelight
  #sed -i '76s|true|false|g' pipelight
  if [[ "$CARCH" == "x86_64" ]]; then
    sed -i 's|Files|Files (x86)|g' pipelight
  fi

  # this needs to change if you're using a custom prefix for wine-silverlight, will happen automatically if _wineprefix changed
  sed -i "9s|.wine-pipelight|$_wineprefix|g" pipelight
  popd
  cd ..

  _home='$HOME'/$_wineprefix
  sed -i "1 a\WINE=$_wine/bin/wine" wine-silverlight5.1-installer.install-script
  sed -i "2 a\INSTDIR=$_prefix/share/pipelight" wine-silverlight5.1-installer.install-script
  sed -i "3 a\WINEPRFX=$_home" wine-silverlight5.1-installer.install-script
  if [[ "$CARCH" == "x86_64" ]]; then
    sed -i 's|Files|Files (x86)|g' wine-silverlight5.1-installer.install-script
  fi
  popd
}

build() {
  pushd $_srcdir
  ./configure --prefix=$_prefix
  make
  popd
}

package() {
  make -C $_srcdir PREFIX=$_prefix DESTDIR=$pkgdir install
  install -Dm644 wine-silverlight5.1-installer.exe ${pkgdir}/$_prefix/share/pipelight/.
  install -Dm755 wine-silverlight5.1-installer.install-script ${pkgdir}/$_prefix/share/pipelight/wine-silverlight5.1.installer

  if [[ $_compilepluginloader  == 0 ]]; then
    install -Dm644 l*.dll ${pkgdir}/$_prefix/share/pipelight/.
    install -Dm644 pluginloader.exe ${pkgdir}/$_prefix/share/pipelight/.
  fi
}

# vim:set ts=2 sw=2 et:
