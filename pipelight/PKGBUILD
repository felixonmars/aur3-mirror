# Maintainer: Anish Bhatt anish [at] gatech [dot] edu

pkgname='pipelight'
pkgver=0.2.4.2
pkgrel=1
epoch=1
pkgdesc="A browser plugin which allows one to use windows only plugins inside Linux browser"
url="https://launchpad.net/pipelight"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1' 'MPL')

depends=('wine-silverlight>=1.7.11-1' 'ttf-ms-fonts' 'wget')
if  [[ "$CARCH" == "x86_64" ]]; then
  depends+=('lib32-libsm' 'lib32-libgl')
else
  depends+=('libsm' 'libgl')
fi

optdepends=('zenity: needed for file dialogs',
            'kdebase-kdialog: needed for file dialogs in KDE')

# Set value to 1 if you want to compile pluginloader yourself
_compilepluginloader=0

if [[ $_compilepluginloader != 0 ]]; then
  if [[ "$CARCH" == "x86_64" ]]; then
  makedepends=('mingw-w64-gcc')
  else
  makedepends=('mingw32-gcc')
  fi
fi

makedepends+=('cabextract' 'xz')

#source=("https://bitbucket.org/mmueller2012/pipelight/get/v${pkgver%.*}-${pkgver##*.}.tar.bz2"
#  "https://launchpad.net/pipelight/trunk/0.1/+download/pluginloader-prebuilt-v${pkgver%.*}-${pkgver##*.}.tar.gz")
source=("https://bitbucket.org/mmueller2012/pipelight/get/v${pkgver}.tar.bz2"
  "https://launchpad.net/pipelight/trunk/${pkgver%.*.*}/+download/pluginloader-prebuilt-v${pkgver}.tar.xz")

md5sums=('e6f4dcc26ad929ef110c6e741796030f'
         '146cfda90ea5514d2822c0c59ce7ee7f')

install=pipelight.install

_srcdir=mmueller2012-pipelight-851cf1925536

#this changes the install location for the pipelight executables
_prefix=/usr

#change this if your wine-silverlight is installed elsewhere, or if you prefer a different wine directory
_wine=/usr
_wineprefix=.wine-pipelight

prepare() {
  pushd $_srcdir

  #The next two lines can't be used if you want to compile your own pluginloader.exe
  if [[ $_compilepluginloader == 0 ]]; then
    sed -i '1s| src/windows||g' Makefile
    sed -i '42d' Makefile
  fi

  pushd src/windows
  sed -i '2 a\ CXXFLAGS        := -static-libgcc -static-libstdc++ $(CXXFLAGS)' Makefile
  popd
  pushd plugin-configs
  sed -i '50s|^gccRun|#gccRun|g' pipelight-*
  sed -i "118s|wine-silverlight/install-dependency|wine-silverlight/install-dependency-pipelight|g" pipelight-*

  #in case you want to use 64bit wine, you would need this
  #if [[ "$CARCH" == "x86_64" ]]; then
  #  sed -i 's|Files|Files (x86)|g' pipelight
  #fi

  # this needs to change if you're using a custom prefix for wine-silverlight, will happen automatically if _wineprefix changed
  #sed -i "30s|.wine-pipelight|$_wineprefix|g" pipelight
  popd
  popd
}

build() {
  pushd $_srcdir
  ./configure --prefix=$_prefix --wine-path=$_wine/bin/wine --win32-static
  make
  popd
}

package() {
  make -C $_srcdir PREFIX=$_prefix DESTDIR=$pkgdir install

  if [[ $_compilepluginloader  == 0 ]]; then
#   As of v0.2.1, pluginloader is always statically compiled, no longer needed
#   install -Dm644 l*.dll ${pkgdir}/$_prefix/share/pipelight/.
    install -Dm644 pluginloader.exe ${pkgdir}/$_prefix/share/pipelight/.
  fi
# All plugin creation has now been moved to pipelight.install
}

# vim:set ts=2 sw=2 et:
