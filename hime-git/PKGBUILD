# Contributor: taylorchu <tailinchu@gmail.com>
# arch PKGBUILD template

_pkgname=hime
pkgname=hime-git
pkgver=970.18e50fd
pkgrel=1
pkgdesc="A GTK2+/GTK3+ based universal input method platform"
arch=('i686' 'x86_64')
url="http://hime.luna.com.tw/"
license=('LGPL2' 'FDL' 'GPL2')
depends=('gtk2' 'libxtst')
makedepends=('git' 'gettext' 'gtk3' 'qt' 'anthy' 'libchewing')
optdepends=('gtk3: enable gtk3 im module' 'qt: enable qt4 im module' 'anthy: enable Anthy input method' 'libchewing: enable Chewing input method')
options=(!strip)
install=hime-git.install
conflicts=('hime')
provides=('hime')
source=('git://github.com/hime-ime/hime.git')
sha512sums=('SKIP')


pkgver() {
	cd $_pkgname
	echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
        cd "${srcdir}/${_pkgname}"

	./configure --prefix=/usr --disable-lib64
	make
}

package() {
	cd "$srcdir/${_pkgname}"

	make DESTDIR="$pkgdir" install

	mkdir -p "$pkgdir/usr/share/licenses/$_pkgname/"
	install -Dm644 fdl-1.2.txt gpl-2.0.txt lgpl-2.1.txt "$pkgdir/usr/share/licenses/$_pkgname/"
}
