# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

_patchlevel=0
_basever=0.155

pkgname=sdlmamefamily-tools
pkgver="${_basever}.u${_patchlevel}"
pkgrel=1
pkgdesc="Tools for manage MAME/MESS/UME roms"
url="http://mamedev.org/"
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('libxinerama' 'fontconfig' 'sdl_ttf' 'flac' 'sqlite' 'portmidi' 'libjpeg-turbo')
conflicts=('sdlmame' 'sdlmess' 'sdlume')
makedepends=('nasm' 'mesa' 'glu' 'wget' 'python2')
DLAGENTS=('http::/usr/bin/wget -U "Mozilla/5.0 (X11; U; Linux x86_64; en-US; rv:1.9.1.2) Gecko/20090804 Shiretoko/3.5.2" -c -t 3 --waitretry=3 -O %o %u')

for i in $(seq 1 "${_patchlevel}"); do
  _patches="${_patches} sdlmame-${_basever/./}u${i}_diff.zip::http://mamedev.org/updates/${_basever/./}u${i}_diff.zip"
done

source=("mame${_basever/./}s.zip::http://mamedev.org/downloader.php?file=releases/mame${_basever/./}s.zip"
        ${_patches})
sha1sums=('61da5d2977100eda89a8d5946f93edb1521810ac')

prepare() {
  cd "${srcdir}"
  rm -fr build && mkdir -p build && cd build
  bsdtar -xf "${srcdir}/mame.zip"
  find . -type f -not -name "*.png" -exec perl -pi -e 's|\r\n?|\n|g' "{}" \;
  for i in $(seq 1 "${_patchlevel}"); do
    msg2 "Patch#${i}"
    patch --silent -p0 -E -i "${srcdir}/${_basever/./}u${i}.diff"
  done

  sed -e 's|# NO_USE_QTDEBUG|NO_USE_QTDEBUG|g' -i src/osd/sdl/sdl.mak
}

build() {
  cd "${srcdir}/build"

  make tools PYTHON=python2 OPTIMIZE=2 NOWERROR=1 BUILD_EXPAT=0 BUILD_ZLIB=0 BUILD_FLAC=0 BUILD_JPEGLIB=0 BUILD_MIDILIB=0 BUILD_SQLITE3=0 TARGET=mame
  make tools PYTHON=python2 OPTIMIZE=2 NOWERROR=1 BUILD_EXPAT=0 BUILD_ZLIB=0 BUILD_FLAC=0 BUILD_JPEGLIB=0 BUILD_MIDILIB=0 BUILD_SQLITE3=0 TARGET=mess
  make tools PYTHON=python2 OPTIMIZE=2 NOWERROR=1 BUILD_EXPAT=0 BUILD_ZLIB=0 BUILD_FLAC=0 BUILD_JPEGLIB=0 BUILD_MIDILIB=0 BUILD_SQLITE3=0 TARGET=ume
}

package() {
  cd "${srcdir}/build"

  # Install the applications
  install -Dm755 castool    "${pkgdir}/usr/bin/castool"
  install -Dm755 chdman     "${pkgdir}/usr/bin/chdman"
  install -Dm755 floptool   "${pkgdir}/usr/bin/floptool"
  install -Dm755 imgtool    "${pkgdir}/usr/bin/imgtool"
  install -Dm755 jedutil    "${pkgdir}/usr/bin/jedutil"
  install -Dm755 ldresample "${pkgdir}/usr/bin/ldresample"
  install -Dm755 ldverify   "${pkgdir}/usr/bin/ldverify"
  install -Dm755 nltool     "${pkgdir}/usr/bin/nltool"
  install -Dm755 pngcmp     "${pkgdir}/usr/bin/pngrep"
  install -Dm755 regrep     "${pkgdir}/usr/bin/regrep"
  install -Dm755 romcmp     "${pkgdir}/usr/bin/romcmp"
  install -Dm755 split      "${pkgdir}/usr/bin/splitmamerom"
  install -Dm755 src2html   "${pkgdir}/usr/bin/src2html"
  install -Dm755 srcclean   "${pkgdir}/usr/bin/srcclean"
  install -Dm755 testkeys   "${pkgdir}/usr/bin/testkeys"
  install -Dm755 unidasm    "${pkgdir}/usr/bin/unidasm"

  # Install the extra bits
  install -d "${pkgdir}/usr/share/man/man1"
  install -m644 src/osd/sdl/man/*.1* "${pkgdir}/usr/share/man/man1/"

  find "${pkgdir}"/usr/bin -type f -exec strip "{}" \;

  # The license
  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/custom/${pkgname}/license.txt"
}
