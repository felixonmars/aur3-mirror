# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=nagios-nrpe
pkgver=2.12
pkgrel=10
pkgdesc="Daemon to answer plugin requests from Nagios on remote hosts."
arch=(i686 x86_64)
license=('GPL')
depends=('openssl')
options=(!libtool)
install="$pkgname.install"
backup=('etc/nrpe/nrpe.cfg' 'etc/xinetd.d/nrpe')
url="http://www.nagios.org/"
source=("http://downloads.sourceforge.net/nagios/nrpe-$pkgver.tar.gz" 'rc.nrpe')
md5sums=('b2d75e2962f1e3151ef58794d60c9e97'
         '417a0d692d79b80bf9e8b2d4a26467f9')

build() {
	cd $srcdir/nrpe-$pkgver

	./configure --prefix=/usr \
    --sysconfdir=/etc/nrpe \
		--with-nrpe-user=nrpe \
    --with-nrpe-group=nrpe \
		--enable-command-args
  
	make
	
}

package() {
  cd $srcdir/nrpe-$pkgver
  
  # install sample config file
  install -D -m644 sample-config/nrpe.cfg $pkgdir/etc/nrpe/nrpe.cfg
  
  # comment out default samples as they won't work and may lessen security
  sed -i 's/^command/#command/' $pkgdir/etc/nrpe/nrpe.cfg

  # include conf dir by default to allow other packages to drop check commands in
  sed -i 's|#include_dir=<somedirectory>|include_dir=conf.d/|g' $pkgdir/etc/nrpe/nrpe.cfg
  mkdir $pkgdir/etc/nrpe/conf.d/
cat > $pkgdir/etc/nrpe/conf.d/process_checks.cfg <<EOT
command[check_zombie_procs]=/usr/share/nagios/libexec/check_procs -w 5 -c 10 -s Z
command[check_total_procs]=/usr/share/nagios/libexec/check_procs -w 150 -c 200
EOT
  
  # install main application
  install -D -m755 src/nrpe $pkgdir/usr/sbin/nrpe
  
  # install xinetd template
  install -Dm644 sample-config/nrpe.xinetd $pkgdir/etc/xinetd.d/nrpe
  sed \
    -e 's|disable         = no|disable         = yes|g' \
    -e 's|/usr/bin/nrpe|/usr/sbin/nrpe|g' \
    -i $pkgdir/etc/xinetd.d/nrpe
  
  # install rc init script
  install -D -m755 $srcdir/rc.nrpe $pkgdir/etc/rc.d/nrpe
}

# vim:set ts=2 sw=2 et:
