# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Dale Blount <dale@archlinux.org>

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=nagios-nrpe
pkgver=2.15
pkgrel=2
pkgdesc="Daemon to answer plugin requests from Nagios on remote hosts."
arch=('i686' 'x86_64')
license=('GPL')
depends=('openssl')
options=(!libtool)
install="$pkgname.install"
backup=('etc/nrpe/nrpe.cfg' 'etc/xinetd.d/nrpe')
url='http://exchange.nagios.org/directory/Addons/Monitoring-Agents/NRPE--2D-Nagios-Remote-Plugin-Executor/details'
source=("http://downloads.sourceforge.net/nagios/nrpe-$pkgver.tar.gz"
        'nrpe.service'
        'nrpe.socket'
        'nrpe@.service'
        'nrpe.tmpfiles'
        'nrpe.xinetd'
        'nrpe.cfg')
md5sums=('3921ddc598312983f604541784b35a50'
         '265512fbfc65f89eab1ca23d11ec749d'
         'f239bb4e7b885449d1f4067b209d89fd'
         '143278e9495821bd887cf84ce0f97af6'
         'd4b89b7ff8ffb187d44fedf294e7f5de'
         'fcd93e2825c7f15355402c9d3569330d'
         '8c867371c83ce5848ed841e88be4998a')

build() {
	cd "$srcdir"/nrpe-$pkgver

	./configure \
    --prefix=/usr \
    --sysconfdir=/etc/nrpe \
		--with-nrpe-user=nrpe \
    --with-nrpe-group=nrpe \
		--enable-command-args
  
	make
}

package() {
  cd "$srcdir"/nrpe-$pkgver

  # install and fix sample config file
  install -D -m644 "$srcdir"/nrpe.cfg "$pkgdir"/etc/nrpe/nrpe.cfg

  # fix paths to nagios-plugins
  sed -e 's|/usr/libexec/|/usr/share/nagios/libexec/|g' \
      -i "$pkgdir"/etc/nrpe/nrpe.cfg

  install -dm755 "$pkgdir"/etc/nrpe/conf.d/
cat > "$pkgdir"/etc/nrpe/conf.d/process_checks.cfg <<EOT
command[check_zombie_procs]=/usr/share/nagios/libexec/check_procs -w 5 -c 10 -s Z
command[check_total_procs]=/usr/share/nagios/libexec/check_procs -w 150 -c 200
EOT

  # install main application
  install -D -m755 src/nrpe "$pkgdir"/usr/bin/nrpe

  # install systemd "unit" files
  install -D -m644 "$srcdir"/nrpe.service   "$pkgdir"/usr/lib/systemd/system/nrpe.service
  install -D -m644 "$srcdir"/nrpe.socket    "$pkgdir"/usr/lib/systemd/system/nrpe.socket
  install -D -m644 "$srcdir"/nrpe@.service  "$pkgdir"/usr/lib/systemd/system/nrpe@.service

  # install xinetd template
  install -Dm644 "$srcdir"/nrpe.xinetd "$pkgdir"/etc/xinetd.d/nrpe
}

# vim:set ts=2 sw=2 et:
