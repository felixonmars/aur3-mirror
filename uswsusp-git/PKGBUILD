# Maintainer: cantabile <cantabile dot desu at gmail dot com>

_appname=uswsusp
_suffix=-git
pkgname=${_appname}${_suffix}
pkgver=20110711
pkgrel=1
pkgdesc="Userspace software suspend - latest code from git."
arch=('i686' 'x86_64')
url='http://suspend.sourceforge.net/'
license=('GPL')
depends=('libgcrypt' 'libx86' 'lzo2')
makedepends=('git')
provides=("$_appname")
conflicts=("$_appname")
backup=('etc/suspend.conf')
install=${_appname}.install
source=('uresume-hook'
        'uresume-install'
        'no-deprecated-switch.patch')

_gitroot="git://git.kernel.org/pub/scm/linux/kernel/git/rafael/suspend-utils.git"
_gitname="$_appname"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  patch -p1 -i "$srcdir/no-deprecated-switch.patch"

  ./autogen.sh
  ./configure \
     --prefix=/usr \
     --enable-compress \
     --enable-encrypt \
     --enable-threads \
     --disable-resume-static \
     --disable-static \
     --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  mkdir "$pkgdir/etc"
  make DESTDIR="$pkgdir/" install

  install -D -m644 "$srcdir/uresume-hook" "$pkgdir/lib/initcpio/hooks/uresume"
  install -D -m644 "$srcdir/uresume-install" "$pkgdir/lib/initcpio/install/uresume"
} 

sha256sums=('b7f81d576775135f51d0c85afdc0bc475a219858c6dfdbb269551a3a064284d4'
            'e9de70f2086487e0691f938cd2bee903dabb1492f2d3c48f2edd578890540588'
            '35819ee80e64c58c406aba14607df9f7a4e3950522295c4f95a815fa25776a02')
