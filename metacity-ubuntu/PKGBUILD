# Maintainer: M4rQu1Nh0S <zonadomarquinhos@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity-ubuntu
_pkgname=metacity

pkgver=2.34.1
pkgrel=3
pkgdesc="A window manager for GNOME with Ubuntu's modifications"
arch=(i686 x86_64)
license=('GPL')
depends=('gtk2' 'startup-notification' 'gconf' 'zenity' 'libcanberra' 'libgtop' 'libsm')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils')
optdepends=('gconf-editor')
provides=("metacity=$pkgver")
conflicts=('metacity')
url="http://www.gnome.org"
options=('!libtool' '!emptydirs')
install=metacity.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz
        04_support_drag_drop_with_alt_tab.patch
        06_Add_UXD_shadows_and_borders.patch
        11_hide_tooltip_on_decorator.patch
        13_better_support_for_button_layout.patch
        15_show_maximized_titlebars.patch
        16-capture-before-unmap.patch
        17-workspace-switcher-cycle.patch
        18-auto-maximize-windows.patch
        20_do-not-place-windows-over-the-launcher.patch
        21_fix_compositing_startup.patch
        100_fade_on_long_title.patch
        101_override_gconf_settings.patch)
sha256sums=('f144cb43925ca00d8eaac129a14df3260c54a32324b5cd0a4d78b0fd7ff1028a'
            'da7a17fd6a2123f59c167059acf3ba4362e96e3c459d5ddc45a479ecad4afa8e'
            '820ac42aee12375b74aa0153fe1a0487c5f9324ba70fb85c2c1cd04e93264371'
            '630e4ca4263ed4c2db4948cf887700baf7aed89989e11490bdcd807a6d71e760'
            '7f728664911e5fe5158c5364695f006f719058ddaf84754edfaae28f8d0926be'
            '262aaeba8048b04eb60ba2a111884f4dbe14c75bd9bc56a4003fcddf4151cf69'
            '2d1dab5ee9bc7ee77319d2c139b1db487a970ea4177fe03da93730e2a0ba070e'
            '093e5b55788240467efc3b6e4c2700079a2169965a3992b1807b8a5646bef478'
            '148d2f12bf147299df097840499420c77b71185a2449220dd14ce6ead6a56b6f'
            '5f0e9c7c654ad1326b02dc766a6a8e7074324eb17ae900442102fbe78f2b9c0d'
            'c9d33a3b64de93ec68801d44e30a364ae7fdcd75a5e5d56cd9b9e67b8c54e900'
            'b0d1fa0abb44362dd0b106ae1f2321c36ce910290f0967ff4248a9a7b60bd186'
            'b53e6560f233e174cc2174c03b8710f429962331a023a8fc1707ddd1d9bf28c6')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # Add drag and drop support when using alt + tab
  patch -Np1 -i "${srcdir}/04_support_drag_drop_with_alt_tab.patch"

  # A new key in the Ubuntu theme for shadows and borders (required for ubuntu-light-themes)
  patch -Np1 -i "${srcdir}/06_Add_UXD_shadows_and_borders.patch"

  # Add a gconf key to disable tooltip in decorator bar (new feature)
  patch -Np1 -i "${srcdir}/11_hide_tooltip_on_decorator.patch"

  # Corrected support for buttons backgrounds with transparency (required for ubuntu-light-themes)
  patch -Np1 -i "${srcdir}/13_better_support_for_button_layout.patch"

  # Add an option to make it possible to hide maximized titlebars (required for unity-2d)
  patch -Np1 -i "${srcdir}/15_show_maximized_titlebars.patch"

  # Add an option to make metacity save a pixmap of each window before it is unmapped (required for unity-2d)
  patch -Np1 -i "${srcdir}/16-capture-before-unmap.patch"

  # Add an option to cause workspace switcher to cycle from last workspace to first workspace (required for unity-2d)
  patch -Np1 -i "${srcdir}/17-workspace-switcher-cycle.patch"

  # Add an option to automatically maximize windows when they appear on the screen if they cover most of the screen (required for unity-2d)
  patch -Np1 -i "${srcdir}/18-auto-maximize-windows.patch"

 # Do not initially position windows over the launcher (required for unity-2d)
  patch -Np1 -i "${srcdir}/20_do-not-place-windows-over-the-launcher.patch"

  # Fix some weird rendering effect at startup with compositing activated
  patch -Np1 -i "${srcdir}/21_fix_compositing_startup.patch"

  # Cut with fading the Bar title when it's longer than the decoration bar
  patch -Np1 -i "${srcdir}/100_fade_on_long_title.patch"

  # Gives a way to override gconf settings by a keyfile (required for unity-2d)
  patch -Np1 -i "${srcdir}/101_override_gconf_settings.patch"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/metacity \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgname}.schemas" --domain metacity ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
