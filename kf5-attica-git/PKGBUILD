# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5-attica-git
pkgver=v0.4.1.19.g8f3b796
pkgrel=1
pkgdesc='A library to access Open Collaboration Service providers'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('qtbase-git')
makedepends=('extra-cmake-modules-git' 'git' 'mesa-libgl')
conflicts=('kf5-attica')
provides=('kf5-attica')
options=('debug')
source=('git://anongit.kde.org/attica')
md5sums=('SKIP')

pkgver() {
  cd attica
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../attica \
    -DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
