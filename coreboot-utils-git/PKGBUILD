# Maintainer: Anatol Pomozov

pkgname=coreboot-utils-git
pkgver=4.0.r7878.g543888d
pkgrel=1
pkgdesc='Tools and utilities to work with Coreboot firmware'
url='http://www.coreboot.org/'
license=(GPL)
arch=(i686 x86_64)
depends=(glibc)
makedepends=(git)
source=(git+https://review.coreboot.org/p/coreboot)
sha256sums=('SKIP')

pkgver() {
  cd coreboot
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd coreboot/util

  (cd cbfstool && make)
  (cd ifdtool && make)
  (cd nvramtool && make)
}

package() {
  cd coreboot/util
  install -m755 -d "$pkgdir/usr/bin" 
  install -m755 -t "$pkgdir/usr/bin" cbfstool/{cbfstool,rmodtool} ifdtool/ifdtool nvramtool/nvramtool

  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
