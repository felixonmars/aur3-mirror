# Maintainer: Sarkasper <echo a2FzcGVyLm1lbnRlbkBnbXguY29tCg== | base64 -d>
# Contributor: Christian Himpel <chressie at gmail dot com>
# Contributor: Johannes Hanika  <hanatos at gmail dot com>
# Contributor: Kevin Brubeck Unhammer <unhammer at member dot fsf dot org>
# Contributor: orbisvicis <orbisvicis at gmail dot com>

pkgname=darktable-git
pkgver=20120515
pkgrel=1
pkgdesc="A virtual lighttable and darkroom for photographers"
arch=('i686' 'x86_64')
url=http://www.darktable.org/
license=('GPL3')
depends=('sqlite' 'libjpeg-turbo' 'libpng' 'libraw' 'gtk2' 'cairo' 'lcms2'
         'exiv2' 'libtiff' 'curl' 'libgphoto2' 'dbus-glib' 'libgnome-keyring'
	 'openexr') #'fop' 
makedepends=('git' 'cmake')
provides=(darktable)
conflicts=(darktable)
backup=()
options=(!emptydirs !libtool)
install=darktable.install
source=(darktable.install)
md5sums=('3dc3625eba9dd701aba9793a80b4c638')

_gitroot=git://darktable.git.sf.net/gitroot/darktable/darktable
_gitname=darktable

build() {
  local _gitdir=$srcdir/$_gitname
  msg "Connecting to GIT server...."

  if [[ -d $_gitdir/.git ]]; then
    cd $_gitdir && git pull
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitdir
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd $_gitdir
  git clean -dfx
  git reset --hard
  [[ ! -d build ]] && mkdir -p build
  cd build

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DDONT_INSTALL_GCONF_SCHEMAS=True \
      -DBINARY_PACKAGE_BUILD=1 \
      -DUSE_GCONF_BACKEND=Off \
      -DBUILD_USERMANUAL=False \
      ..
  make
}

package() {
  cd $srcdir/$_gitname/build
  make DESTDIR=$pkgdir install
  mv "${pkgdir}/usr/share/doc/darktable" "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
#  mkdir -p "${pkgdir}/usr/share/gconf/schemas/"
#  mv "${pkgdir}/etc/gconf/schemas/darktable.schemas" "${pkgdir}/usr/share/gconf/schemas/"
}
