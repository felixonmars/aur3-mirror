# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=pology-svn
pkgver=1258654
pkgrel=4
pkgdesc='A framework for custom processing of PO files.'
arch=('any')
license=('GPL3')
url='http://techbase.kde.org/Localization/Tools/Pology'
depends=('python2' 'gettext')
makedepends=('subversion' 'cmake' 'docbook-xsl' 'epydoc')
optdepends=('python2-pyenchant: to spell check'
	    'aspell-lang: a dictionary in your language'
	    'hunspell-lang: a dictionary in your language'
	    'languagetool: used by the check-grammar sieve'
	    'dbus-python: for communication with Lokalize'
	    'apertium: used by the pomtrans script'
	    'git: for processing files under version control'
	    'subversion: for processing files under version control')
provides=('pology')
install=pology.install
options=(!makeflags)

_svntrunk="svn://anonsvn.kde.org/home/kde/trunk/l10n-support/pology"
_svnmod="pology"

build() {
  cd ${srcdir}

  if [ -d ${_svnmod}/.svn ]; then
    msg "Updating SVN tree ..."
    (cd ${_svnmod} && svn up)
    msg "SVN update finished, starting installation..."
  else
    msg "Doing SVN checkout ..."
    svn co ${_svntrunk} ${_svnmod}
    msg "SVN checkout finished, starting installation..."
  fi

  cd pology

  sed -i -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' $(find ${srcdir} -name '*.py')

  mkdir -p build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DDOCBOOK_XSL_DIR=/usr/share/xml/docbook/xsl-stylesheets-1.77.1/
  make PREFIX=/usr
}

package() {
  cd ${srcdir}/pology/build
  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/apps/katepart/syntax
  ln -s /usr/share/pology/syntax/kate/synder.xml ${pkgdir}/usr/share/apps/katepart/syntax/synder.xml

  install -d ${pkgdir}/etc/bash_completion.d
  ln -s /usr/share/pology/completion/bash/pology ${pkgdir}/etc/bash_completion.d/pology
}
