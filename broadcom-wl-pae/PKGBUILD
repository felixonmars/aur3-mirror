# Contributor: Austin ( doorknob60 [at] gmail [dot] com )
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: agnotek ( agnostic.sn at gmail dot com )
# I take from AUR broadcom-wl, then change it to fit linux-pae
# only tested in my own laptop

pkgname=broadcom-wl-pae
pkgver=6.30.223.141
pkgrel=7
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('i686' 'x86_64')
license=('custom')
depends=('linux-pae')
makedepends=('linux-pae-headers')
[[ $CARCH = x86_64 ]] && _arch=_64 || _arch=
source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35${_arch}-nodebug-pcoem-${pkgver//./_}.tar.gz"
        'modprobe.d'
        'license.patch'
        'linux-recent.patch')
sha1sums=('2917d785ffd9663b1d23cfa388b9bd7c9f03cc3b'
          '8f5234da6769784b3331d2fefac4a38b20b5723f'
          '81c05d48b234d56ad2e18eaee5ce89b79550ef20'
          '67c355ae746dba70b8794b4ef21428b54ff84098')
[[ $CARCH = x86_64 ]] && sha1sums[0]='3a2453c71a07030f41a8fc613c87badf8452848c'

install=install

_kernmajor="$(pacman -Q linux-pae | awk '{print $2}' | cut -d - -f1 | cut -d . -f1,2)"
_extramodules="extramodules-${_kernmajor}-pae"
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

prepare() {
	cd "${srcdir}"

	patch -p1 -i linux-recent.patch
	patch -p1 -i license.patch

        sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" \
            -i src/wl/sys/wl_linux.c
}

build() {
	cd "${srcdir}"

	make -C /usr/lib/modules/${_kernver}/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -Dm644 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"

	install -Dm644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 modprobe.d "${pkgdir}/usr/lib/modprobe.d/broadcom-wl.conf"
}
