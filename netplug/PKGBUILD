# vim:set ts=2 sw=2 et:

pkgname=netplug
pkgver=1.2.9.2
pkgrel=2
pkgdesc='A Linux daemon that manages network interfaces in response to network cables being plugged in and out.'
arch=('i686' 'x86_64')
url='http://www.red-bean.com/~bos/'
license=('GPL2')
depends=('iproute2>=2.4.7')
backup=('etc/netplug/netplug' 'etc/netplug/netplugd.conf')
options=('!purge')
install="${pkgname}d.install"
source=("${url}${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        "rc.${pkgname}d"
        "${pkgname}d.conf.d"
        "${pkgname}d.install")
md5sums=('1d6db99536bdf875ce441f2c0e45ebf2'
         '1f7a33ca800f04b2cdb01555292a55a7'
         'ddda834021991b23d4c4fdf161523387'
         '50c01189419b0fc26c0fd8d3efd7e1f6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove tarball building stuff with hg from Makefile
  sed --in-place '33,46 d' Makefile
  make scriptdir=/etc/netplug

  # Adjust path to confifuration files into man pages
  sed --in-place \
    -e 's#/etc/netplug.d#/etc/netplug#' \
    -e 's#/init.d##' \
  man/man8/netplugd.8
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install \
    bindir=/usr/sbin/ \
    initdir=/etc/rc.d/ \
    scriptdir=/etc/netplug
  rm "${pkgdir}/etc/rc.d/${pkgname}d"
  install -o root -g root -d -m755 "${pkgdir}/etc/conf.d/"
  install -o root -g root -m644 -D "${srcdir}/${pkgname}d.conf.d" "${pkgdir}/etc/conf.d/${pkgname}d"
  install -o root -g root -m755 "${srcdir}/rc.${pkgname}d" "${pkgdir}/etc/rc.d/${pkgname}d"
} 
