# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Josef Lusticky <evramp@gmail.com>

pkgname=counter-strike-2d
pkgver=0.1.2.0
pkgrel=1
pkgdesc="A more than just a freeware clone of the well known game Counter-Strike"
arch=('i686' 'x86_64')
url="http://www.cs2d.com/"
license=('custom')
changelog=ChangeLog
if [ "$CARCH" == "i686" ]; then
    depends=('freetype2' 'libtxc_dxtn' 'libxcursor' 'mesa' 'openal')
    makedepends=('unzip')
elif [ "$CARCH" == "x86_64" ]; then
    depends=('lib32-freetype2' 'lib32-libtxc_dxtn' 'lib32-libxcursor' 'lib32-mesa' 'lib32-openal')
    makedepends=('unzip' 'gcc-multilib')
fi
source=(cs2d_${pkgver//.}_linux.zip::http://www.unrealsoftware.de/get.php?get=cs2d_${pkgver//.}_linux.zip
        cs2d_${pkgver//.}_win.zip::http://www.unrealsoftware.de/get.php?get=cs2d_${pkgver//.}_win.zip
        cs2d.desktop
        cs2d.png)
noextract=(cs2d_${pkgver//.}_linux.zip
           cs2d_${pkgver//.}_win.zip)
md5sums=('8dfe86c5725d07876964e422957a4d83'
         'f5c50a38cd21cfc5286611de217281fb'
         '9bd65c757a3d3613c152fa21cad22355'
         'b4bb759de5f6db90a0363f37c4d400c8')
sha1sums=('bdeacde0b965ab02f47059a2f584012b21fa338c'
          '3023537a2c72832080c1b30b8cc009aa683c787e'
          '04f5bb933d95d6085a08dc0f1937f583f9a2e550'
          '313200eb8d8a120f6abed5c052798def9ea6269e')

package() {
  install -d -m 755 "$pkgdir"/usr/share/cs2d
  unzip cs2d_${pkgver//.}_win.zip -d "$pkgdir"/usr/share/cs2d
  unzip cs2d_${pkgver//.}_linux.zip -d "$pkgdir"/usr/share/cs2d
  
  install -d -m 755 "$pkgdir"/usr/bin
  ln -s /usr/share/cs2d/CounterStrike2D "$pkgdir"/usr/bin/cs2d
  chmod +x "$pkgdir"/usr/share/cs2d/CounterStrike2D
  
  install -Dm644 cs2d.png "$pkgdir"/usr/share/pixmaps/cs2d.png
  install -Dm755 cs2d.desktop "$pkgdir"/usr/share/applications/cs2d.desktop
}

