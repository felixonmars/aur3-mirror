# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=swordfish-git
pkgver=0.0.0
pkgrel=1
pkgdesc="File manager"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL')
depends=('qtdeclarative-git' 'qtquickcontrols-git' 'qttools-git')
makedepends=('git' 'cmake')
options=('debug')

_gitroot="git://github.com/mauios/swordfish.git"
_gitname=swordfish
_gitbranch=dev
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
	mkdir -p build
}

build() {
	export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
	export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib
	export QT_SELECT=git

	cd build
	cmake ../${_gitname} \
		-DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
		-DCMAKE_INSTALL_PREFIX=/opt/hawaii-git \
		-DCMAKE_INSTALL_LIBDIR=/opt/hawaii-git/lib \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install

	# Since we are using Qt from git we create a script that
	# sets LD_LIBRARY_PATH and runs the executable
	path=/opt/hawaii-git
	exe=swordfish
	mv $pkgdir/$path/bin/$exe $pkgdir/$path/bin/$exe.real
	cat > $pkgdir/$path/bin/$exe <<EOF
#!/bin/bash
export LD_LIBRARY_PATH=/opt/qt-git/lib:/opt/kf5/lib:/opt/hawaii-git/lib
$path/bin/$exe.real "\$@"
EOF
	chmod 755 $pkgdir/$path/bin/$exe
}
