# Maintainer: Bruno Nova <brunomb.nova@gmail.com>
pkgname=drmips
pkgver=1.2.2
pkgrel=4
pkgdesc="Graphical MIPS simulator to support computer architecture teaching and learning"
arch=('any')
url="https://bitbucket.org/brunonova/drmips"
license=('GPL3')
depends=('java-environment' 'desktop-file-utils' 'hicolor-icon-theme' 'ttf-font')
makedepends=('jdk7-openjdk' 'cmake')
optdepends=('drmips-doc: user manuals')
install="$pkgname.install"
source=("https://bitbucket.org/brunonova/$pkgname/downloads/DrMIPS_v$pkgver.tar.gz"
        "$pkgname.desktop"
        "$pkgname.1"
        "$pkgname.pt.1")
md5sums=('499bc462d6b9aa3367fd7d41fa2bdd24'
         '02ba7efa585b621e978f6a23e8fd210e'
         '4910bafb9087b68ed9fa40bfd46838c5'
         '70954b479c35eeaff5f9306d0ec5eff9')

build() {
	cd "$srcdir"
	cmake . -DCMAKE_INSTALL_PREFIX=/usr -DDRMIPS_BUILD_MANUALS=off
	make
}

package() {
	cd "$srcdir"
	make DESTDIR="$pkgdir" install
	for _size in 16 24 32 48 64 96 128 256 512; do
		install -Dm644 "src/pc/DrMIPS/src/res/icons/x$_size/$pkgname.png" "$pkgdir/usr/share/icons/hicolor/${_size}x${_size}/apps/$pkgname.png"
	done
	install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
	install -Dm644 "$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
	install -Dm644 "$pkgname.pt.1" "$pkgdir/usr/share/man/pt/man1/$pkgname.1"
}
