# Maintainer: Siddhartha Sahu <sh.siddhartha@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

_pkgname=verlihub
pkgname=$_pkgname-git
pkgver=791.8e1df13
pkgrel=1
pkgdesc="A DC++ protocol server (hub) - GIT version"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.verlihub-project.org/"
depends=('geoip' 'mariadb' 'gettext' 'pcre' 'zlib' 'openssl')
makedepends=('git' 'cmake' 'lua51' 'python2')
optdepends=('lua51: for Lua plugins'
            'python2: for Python plugins')
conflicts=('verlihub')
provides=('verlihub')
install=$pkgname.install
source=('git://github.com/VerliHub/verlihub.git'
        'verlihub.service')
md5sums=('SKIP'
         '5796a52003035027bb4976150f658c0a')

pkgver() {
    cd "$srcdir/$_pkgname"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
    cd "$srcdir/$_pkgname"
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_LIBRARY="/usr/lib/libpython2.7.so.1.0" -DPYTHON_INCLUDE_DIR="/usr/include/python2.7" .
    make
}

package() {
    cd "$srcdir/$_pkgname"
    make DESTDIR="${pkgdir}" install
    
    install -m755 -d "${pkgdir}/usr/lib/systemd/system"
    install -m644 "${srcdir}/verlihub.service" "${pkgdir}/usr/lib/systemd/system/"
}
