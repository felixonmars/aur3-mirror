# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mark Coolen <mark.coolen@gmail.com>

pkgname=berusky2
pkgver=0.5
pkgrel=2
pkgdesc="A logic game based on the puzzle game Sokoban"
arch=('i686' 'x86_64')
url="http://www.anakreon.cz/en/"
license=('GPL')
depends=('sdl_image' 'mesa' 'berusky2-data')
source=("http://www.anakreon.cz/download/${pkgname}-${pkgver}.tar.gz"
        'berusky2.png' 'berusky2.desktop')
md5sums=('7ef7a5ea042b1497d714449a535a5da7'
         '8a5f5607b4883f4c86d1ba9fd5ab6156'
         '3363c8d4d28da48a25e9ee403680fdbe')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
 --with-ini-file-global-dir=\"/usr/share/berusky2\"
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
    
  install -m644 -D ${srcdir}/berusky2.png ${pkgdir}/usr/share/pixmaps/berusky2.png
  install -m644 -D ${srcdir}/berusky2.desktop ${pkgdir}/usr/share/applications/berusky2.desktop
}
