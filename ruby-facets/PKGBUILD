# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
# Maintainer: Steven Allen <steven {a} stebalien {d} com>

pkgname=ruby-facets
pkgver=2.9.3
pkgrel=1
pkgdesc="The largest collection of core extension methods and standard library additions for Ruby"
arch=(any)
url="http://rubyworks.github.com/facets/"
license=("RUBY")
depends=("ruby")
source=($pkgname-$pkgver.tar.gz::"https://github.com/rubyworks/facets/tarball/$pkgver")
md5sums=('4a68c2caf80396e680889bb983aa2856')

build() {
  cd "$srcdir"/rubyworks-facets-*
  gem build facets.gemspec
}

package() {
  cd "$srcdir"/rubyworks-facets-*
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --local --no-user-install --ignore-dependencies --verbose -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" facets-$pkgver.gem
}


# vim:set ts=2 sw=2 et:
