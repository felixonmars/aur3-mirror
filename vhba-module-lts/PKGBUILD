# Maintainer: Maximilien Noal <noal dot maximilien at gmail dot com> [AUR: xcomcmdr]
# $Id: PKGBUILD 97305 2013-09-17 06:53:27Z tpowa $
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=vhba-module-lts
_srcname=vhba-module
pkgver=20130607
_extramodules=extramodules-3.10-lts
pkgrel=10
pkgdesc="Kernel module that emulates SCSI devices. For linux-lts."
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('linux-lts>=3.10' 'linux-lts<3.11')
makedepends=('linux-lts' 'linux-lts-headers')
options=(!makeflags)
provides=('vhba-module')
install=$pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$_srcname-$pkgver.tar.bz2"
        '60-vhba.rules')
md5sums=('660df41d08b1fd86baad1f16a0264438'
         '32dcd54f5e1284e44076059ed58dd9b8')

build() {
  cd $_srcname-$pkgver
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  make KDIR=/usr/lib/modules/$_kernver/build
}

package() {
  cd $_srcname-$pkgver
  install -Dm644 vhba.ko "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"
  if [ ! -f '/usr/lib/udev/rules.d/60-vhba.rules' ]; then
    install -Dm644 ../60-vhba.rules "$pkgdir/usr/lib/udev/rules.d/60-vhba.rules"
  fi

  cd $startdir
  cp -f $install ${install}.pkg
  true && install=${install}.pkg
  sed -i "s/EXTRAMODULES=.*/EXTRAMODULES=$_extramodules/" $install
}

# vim:set ts=2 sw=2 et:
