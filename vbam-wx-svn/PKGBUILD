# Maintainer: Tilman Blumenbach <tilman+aur AT ax86 DOT net>
pkgname=vbam-wx-svn
_svndir=vbam-trunk
pkgver=1223
pkgrel=3
pkgdesc="Improved (Super) Game Boy Color/Advance emulator - wxWidgets GUI, SVN version"
arch=(i686 x86_64)
url="http://vba-m.com/"
license=('GPL2')
depends=(zlib libpng mesa sdl sfml1.6 openal wxgtk2.8
  hicolor-icon-theme desktop-file-utils xdg-utils)
makedepends=(cmake subversion nasm imagemagick)
conflicts=(vba-m-wx vba-m-wx-svn)
replaces=(vba-m-wx-svn)
provides=("vba-m-wx-svn=${pkgver}")
install=vbam.install
source=("${_svndir}::svn://svn.code.sf.net/p/vbam/code/trunk"
  sfml-build-fixes.patch
  missing-util-decl.patch)
md5sums=(SKIP
  4c5715b43d383f63b02cd749d156efc1
  6aebdbcf7652b4984b7c313488d96487)

pkgver() {
  cd "$srcdir/$_svndir"
  svnversion | tr -d '[A-z]'
}

prepare() {
  cd "$srcdir/$_svndir"

  patch -Np0 -i "$srcdir/sfml-build-fixes.patch"
  patch -Np0 -i "$srcdir/missing-util-decl.patch"
}

build() {
  cd "$srcdir/$_svndir"

  # ENABLE_ASM_CORE breaks GBA colors (at least with Pokemon Fire Red USA 1.0).
  # It also only works with 32 bit CPUs.
  cmake -D CMAKE_INSTALL_PREFIX:string=/usr -D ENABLE_ASM_SCALERS:bool=ON \
    -D ENABLE_GTK:bool=OFF -D ENABLE_WX:bool=ON -D ENABLE_SDL:bool=OFF \
    -D ENABLE_LINK:bool=ON -D wxWidgets_CONFIG_EXECUTABLE:string=/usr/bin/wx-config-2.8 .
  make
}

package() {
  cd "$srcdir/$_svndir"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
