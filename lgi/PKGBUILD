# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lgi
pkgver=0.5
pkgrel=1
pkgdesc="Lua binadings for gnome/gobject using gobject-introspection library."
arch=(i686 x86_64)
url="https://github.com/pavouk/lgi"
license=('custom:MIT')
depends=('lua' 'gobject-introspection')
optdepends=('clidebugger: dump-typelib tool')
source=("https://github.com/downloads/pavouk/lgi/$pkgname-$pkgver.tar.gz")
md5sums=('f196ea81f85f49147b0cd7217f5342e9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir/" install

  # dump typelib tool
  install -Dm755 tools/dump-typelib.lua \
    "$pkgdir/usr/bin/dump-typelib"

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 docs/* \
    "$pkgdir/usr/share/doc/$pkgname"

  # samples
  install -d "$pkgdir/usr/share/$pkgname/samples/gtk-demo"
  install -Dm644 samples/*.lua \
    "$pkgdir/usr/share/$pkgname/samples"
  install -Dm644 samples/gtk-demo/* \
    "$pkgdir/usr/share/$pkgname/samples/gtk-demo"

  # license
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
} 
