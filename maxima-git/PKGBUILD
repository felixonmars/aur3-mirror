# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=maxima-git
pkgver=20120522
pkgrel=1
pkgdesc="a sophisticated computer algebra system - git-version"
arch=('any')
url="http://maxima.sourceforge.net"
license=('GPL')
depends=('sbcl' 'texinfo' 'sh')
makedepends=('git' 'cl-ppcre' 'perl' 'python')
optdepends=('gnuplot: plotting capabilities' 'rlwrap: readline support via /usr/bin/rmaxima' 'tk: graphical xmaxima interface')
provides=('maxima')
conflicts=('maxima')
options=('!makeflags' '!zipman')
install=maxima.install
source=(maxima.desktop)
md5sums=('24aa81126fbb8b726854e5a80d4c2415')

_gitroot="git://maxima.git.sourceforge.net/gitroot/maxima/maxima"
_gitname="maxima"
build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin 
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  [ -d "$srcdir/$_gitname-build" ] && rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  ./bootstrap
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --infodir=/usr/share/info  --libexecdir=/usr/lib \
    --enable-sbcl --with-default-lisp=sbcl
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install  
  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/maxima.desktop \
  	${pkgdir}/usr/share/applications/${pkgname}.desktop
  
  # make sure, we have a nice icon for the desktop file at the right place ;)
  install -d ${pkgdir}/usr/share/pixmaps/
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png \
	${pkgdir}/usr/share/pixmaps/${pkgname}.png
}
