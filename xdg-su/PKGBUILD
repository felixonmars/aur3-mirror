# Maintainer: Atilla ÖNTAŞ <tarakbumba@gmail.com>
#Contributor: Mitchell Nemitz <mitchell.nemitz@gmail.com>
#Contributor: sebcactus <sebcactus@gmail.com>
pkgname=xdg-su
pkgver=1.2.2
pkgrel=1
pkgdesc="Run a program as root with a graphical interface"
arch=('any')
url="https://github.com/tarakbumba/xdg-su"
license=('MIT')
depends=('sh')
makedepends=( 'links' 'xmlto' 'docbook-xsl')
source=( "https://github.com/tarakbumba/$pkgname/archive/v$pkgver.tar.gz" )
md5sums=('31fdae865cc1c457200675c810648033')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --mandir=/usr/share/man
    make scripts-clean -C scripts
    make man scripts -C scripts 
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}