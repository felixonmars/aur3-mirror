# Maintainer: Ner0

pkgname=scratch-bzr
pkgver=725
pkgrel=1
pkgdesc="A text editor written in Vala by elementary"
arch=('i686' 'x86_64')
url="https://launchpad.net/scratch"
license=('GPL3')
depends=('gtksourceview3' 'libgee' 'gobject-introspection' 'glib2' 'desktop-file-utils'
         'gconf' 'gtk3' 'granite' 'libpeas' 'pango' 'dconf' 'hicolor-icon-theme' 'vte3')
makedepends=('bzr' 'cmake' 'vala')
optdepends=('contractor-bzr: Contractor integration [AUR]'
            'libsoup: Pastebin plugin')
conflicts=('scratch-editor')
install=$pkgname.install

_bzrtrunk=lp:scratch
_bzrmod=scratch

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build

  sed -e 's/add_subdirectory(po)//' \
      -e 's/add_subdirectory(plugins)//' \
      -e 's/;vte-2.90//' \
      -i CMakeLists.txt

  [ -d build ] && rm -rf build
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DGSETTINGS_LOCALINSTALL=OFF
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
