# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

pkgname=freetype2-git-infinality
pkgver=2.4.99.git20120616
pkgrel=1
_infinality_version=2.4.10
_infinality_release=01
_infinality_date=20120616
pkgdesc="TrueType font rendering library with Infinality patch"
arch=(i686 x86_64)
url="http://www.infinality.net/"
license=('GPL')
depends=('bzip2' 'zlib')
makedepends=('git')
optdepends=('ttf-win7-fonts: (AUR) Fonts included in Windows 7'
            'fontconfig-infinality: (AUR) Infinality configuration files')
provides=("freetype2=$pkgver" "freetype2-infinality=$pkgver")
conflicts=('freetype2' 'freetype2-infinality')
backup=('etc/profile.d/infinality-settings.sh')
options=('!libtool')
install='infinality.install'
source=(http://www.infinality.net/fedora/linux/zips/freetype-infinality-${_infinality_version}-${_infinality_date}_${_infinality_release}-x86_64.tar.bz2
        freetype2-enable-valid.patch)
sha256sums=('dc8eb149fdab21ecdbe624b58f7c26895a9223f4af71f220b111844567445f58'
            '97751d04014b7f2a42bce3ed99417eeb058563e2b7c94251f05f489666fec5bb')
_freetype_gitroot="git://git.sv.nongnu.org/freetype/freetype2.git"
_freetype_gitcommit="320613eeb83d0cd704de3e6adfb13dce787861bb"

build() {
  cd "${srcdir}"

  # Get rid of old build directories
  rm -rf freetype2-build

  # Clone or update sources from freetype2 git repository
  if [ -d ${srcdir}/freetype2 ]; then
    msg "Updating Git repository"
    cd freetype2 && git pull
  else
    msg "Cloning Git repository"
    git clone ${_freetype_gitroot}
  fi

  msg "Preparing build directory"
  cp -r ${srcdir}/freetype2 ${srcdir}/freetype2-build

  cd "${srcdir}/freetype2-build"

  # Check out specific commit for repeatability
  git checkout --detach ${_freetype_gitcommit}

  # We need to generate the configure script BEFORE applying patches because
  # Infinality patches this script
  sh autogen.sh

  # Apply miscellaneous patches.
  patch -Np0 -i "${srcdir}/freetype2-enable-valid.patch"

  # Apply Infinality patches. See README file for correct order. Unfortunately,
  # the Infinality patches may not have the same version number, so we use
  # wildcards here.
  patch -Np1 -i "${srcdir}/freetype-add-subpixel-hinting-infinality-"*.patch
  patch -Np1 -i "${srcdir}/freetype-enable-subpixel-hinting-infinality-"*.patch
  patch -Np1 -i "${srcdir}/freetype-entire-infinality-patchset-"*.patch

  # Run configure script
  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/freetype2-build"

  make DESTDIR="${pkgdir}" install

  # Install settings file from the freetype-infinality source tarball.
  # freetype-infinality.sh is not installed because LD_PRELOAD isn't neccessary
  # as we are replacing the unpatched freetype2 package.
  install -m 755 -D -T "${srcdir}/infinality-settings.sh" "${pkgdir}/etc/profile.d/infinality-settings.sh"
}

# vim: set ts=2 sw=2 ft=sh noet:
