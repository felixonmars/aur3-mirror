# Maintainer: Martin Schmoelzer <mschmoelzer@gmail.com>

pkgname=freetype2-git-infinality
_gitcommit=f4ec60fdb8271aade40c3d0faf0a65bda8bae8ac
pkgver=2.4.99.git20111025
pkgrel=1
pkgdesc="TrueType font rendering library with infinality patch"
arch=(i686 x86_64)
url="http://freetype.sourceforge.net"
license=('GPL')
depends=('bzip2' 'zlib')
makedepends=('git')
optdepends=('ttf-win7-fonts: (AUR) Fonts included in Windows 7')
provides=("freetype2=$pkgver")
conflicts=('freetype2')
backup=('etc/profile.d/infinality-settings.sh'
        'etc/skel/.Xresources')
options=('!libtool')
install='infinality.install'
source=(Xresources
        http://www.infinality.net/files/local.conf
        http://www.infinality.net/files/infinality-settings.sh
        http://www.infinality.net/files/freetype-add-subpixel-hinting-infinality-20110604-1.patch
        http://www.infinality.net/files/freetype-enable-subpixel-hinting-infinality-20100909-1.patch
        http://www.infinality.net/files/freetype-entire-infinality-patchset-20110604-1.patch
        http://aoliynik-overlay.googlecode.com/svn-history/r361/trunk/media-libs/freetype/files/freetype-2.3.2-enable-valid.patch)
sha256sums=('de531952dbe41889e91a250618e2dd94d9a48e2eddc07c0d7e3fd0f7cc984bd4'
            '408a051856aece027bc83134074e354a4d7060778662206c8e2dcfbc94985969'
            '003570b32179ba43f475a8806ba4ae91a216b023d0d1393e052d9e571f10a2bc'
            'eeb398a06787f5f6d8aa29f660ce26e74bd46b43904ebde7638b486578c8d7ed'
            '6090e599455eef159ff51cf653a3d2b3776e2b2e7a8e109b057ace409cf42c99'
            '9be1418c6b3452ef2b5429522573f2b31ae15bc97f102862cc55a7e40da31035'
            '3c26cd8b92510490b4bdbdd12b078e33a4f8607eaee64a800c3ea23097d5d43b')

build() {
  cd "${srcdir}"

  # Get sources from freetype2 git repository
  msg "Cloning freetype2 git repository..."
  git clone git://git.sv.nongnu.org/freetype/freetype2.git

  cd "${srcdir}/freetype2"

  # Check out specific commit for repeatability
  git checkout --detach ${_gitcommit}

  # Apply patches
  patch -Np1 -i "${srcdir}/freetype-add-subpixel-hinting-infinality-20110604-1.patch"
  patch -Np1 -i "${srcdir}/freetype-enable-subpixel-hinting-infinality-20100909-1.patch"
  patch -Np1 -i "${srcdir}/freetype-entire-infinality-patchset-20110604-1.patch"
  patch -Np1 -i "${srcdir}/freetype-2.3.2-enable-valid.patch"

  # Generate configure script
  sh autogen.sh

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/freetype2"

  make DESTDIR="${pkgdir}" install

  install -m 644 -D -T "${srcdir}/Xresources" "${pkgdir}/etc/skel/.Xresources"
  install -m 644 -D -T "${srcdir}/local.conf" "${pkgdir}/usr/share/freetype2-git-infinality/local.conf"
  install -m 755 -D -T "${srcdir}/infinality-settings.sh" "${pkgdir}/etc/profile.d/infinality-settings.sh"
}
