# Maintainer: Sara <sara at archlinux dot us>

pkgname=fbpdf-git
pkgver=20120501
pkgrel=1
pkgdesc="A small framebuffer pdf viewer, based on MuPDF"
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/fbpdf.git"
license=('custom:BSD')
depends=('mupdf')
makedepends=('git')
provides=('fbpdf')
conflicts=('fbpdf')
source=('fbpdf.1'
        'LICENSE')
md5sums=('74df138e68c0cac36baaeeb89d49545a'
         'ec67f29a7dec10f86ef31515ed657a91')

_gitroot=git://repo.or.cz/fbpdf.git
_gitname=fbpdf

build(){
  cd "${srcdir}"
  msg "Connecting to GIT server...."
  
  if [[ -d ${_gitname} ]]; then
    cd "${_gitname}"
    git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}"
    cd "${_gitname}"
  fi
  msg "Checkout done or server timeout"
  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  msg "Starting make..."
  make fbpdf
}

package(){
  install -Dm755 "${srcdir}/${_gitname}-build/${_gitname}" "${pkgdir}/usr/bin/${_gitname}"
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}/usr/share/licenses/${_gitname}/LICENSE"
  # Install man page
  install -Dm644 "${srcdir}/${_gitname}.1" "$pkgdir/usr/share/man/man1/${_gitname}.1"
}

# vim:set ts=2 sw=2 et:
