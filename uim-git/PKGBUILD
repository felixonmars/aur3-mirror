# Contributor: noonov <noonov@gmail.com>

pkgname=uim-git
pkgver=20121013
pkgrel=1
pkgdesc="A multilingual input method framework"
arch=('i686' 'x86_64')
url="http://code.google.com/p/uim/"
license=('BSD')
depends=('gtk2')
makedepends=('git' 'intltool' 'perl' 'ruby' 'librsvg' 'asciidoc' 'ed')
provides=('uim')
conflicts=('uim' 'uim-svn')
options=('!libtool' '!docs' '!emptydirs')
install=uim-git.install
source=()
md5sums=()

_gitroot="https://github.com/uim/uim.git"
# _gitroot="https://code.google.com/p/uim/"
_gitname="uim"

build() {
  cd ${srcdir}

  msg "Connecting to GIT server..."
  if [[ -d ${_gitname} ]]; then
    (cd ${_gitname} && git pull origin)
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_gitname}-build
  git clone ${_gitname} ${_gitname}-build

  cd ${srcdir}/${_gitname}-build

  touch scm/json-parser-expanded.scm

  ./make-wc.sh --prefix=/usr --libexecdir=/usr/lib/uim
  make
}

package() {
  cd ${srcdir}/${_gitname}-build

  make -j1 DESTDIR=${pkgdir} install

  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
