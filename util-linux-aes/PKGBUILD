# Maintainer: xerc <aur[at]xerc.de>
# Contributor: milomouse <vincent[at]fea.st>
# Contributor: judd <jvinet[at]zeroflux.org>
# Contributor: falconindy <d[at]falconindy.com>

_basename=util-linux
pkgname=${_basename}-aes
_basever=2.21
pkgver=${_basever}.2
pkgrel=1
_loopaesdiff=${_basename}-${_basever}-20120228.diff

pkgdesc="Miscellaneous system utilities for Linux, with loop-AES support"
url="http://sourceforge.net/projects/loop-aes/"
license=('GPL2')
arch=('any')
groups=('base')
depends=('bash' 'ncurses>=5.7' 'zlib' 'filesystem')
makedepends=('autoconf' 'automake')
optdepends=('perl: for chkdupexe support')
provides=('linux32' "util-linux=${pkgver}" "util-linux-ng=${pkgver}")
conflicts=('linux32' 'util-linux' 'util-linux-ng' 'e2fsprogs<1.41.8-2')
replaces=('linux32' 'util-linux' 'util-linux-ng', 'util-linux-ng-aes')
options=('!libtool')

install=${pkgname}.install
source=(http://ftp.kernel.org/pub/linux/utils/${_basename}/v${_basever}/${_basename}-${pkgver}.tar.bz2
		http://ftp.kernel.org/pub/linux/utils/${_basename}/v${_basever}/${_basename}-${_basever}.tar.bz2
        http://loop-aes.sourceforge.net/updates/${_loopaesdiff}.bz2
        ${pkgname}.install)

build() {
  cd "${srcdir}/${_basename}-${pkgver}"
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c

  msg "Using losetup.8 from ${_basever}"
  cp -f "../${_basename}-${_basever}/sys-utils/losetup.8" "sys-utils/losetup.8"
  
  msg "Patching with loop-AES"
  patch -Np1 -i "${srcdir}/${_loopaesdiff}"
  
  msg "Starting autogen"
  ./autogen.sh
  msg "Starting configure"
  ./configure --enable-arch --enable-write --enable-raw --disable-wall --enable-partx
  msg "Starting make"
  make
}

package() {
  cd "${srcdir}/${_basename}-${pkgver}"
  install -dm0755 "${pkgdir}/var/lib/hwclock"
  make DESTDIR="${pkgdir}" install
}

md5sums=('b75b3cfecb943f74338382fde693c2c3'
         '3c7a26963f3921c1e775a80330321870'
         '0704840db13570720b5b2fef74597e39'
         '7bfa755b056a0c48a504153a3809cf1c')
