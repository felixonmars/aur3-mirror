## Maintainer : shahid <helllamer@gmail.com>
## Long PKGBUILD description, todo, wiki:
## * https://bitbucket.org/helllamer/archlinux-pkgbuild/wiki/pkgbuild/bitcoin

## Wanna take part on development my PKGBUILDs? Feel welcome on my repo:
## * https://bitbucket.org/helllamer/archlinux-pkgbuild/

name=bitcoin
pkgname=$name
pkgver=0.5.0
pkgrel=2
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.bitcoin.org/"
depends=('qt>=4.6' 'libpng>=1.4' 'expat' 'gcc-libs' 'boost-libs>=1.46' 'miniupnpc>=1.6' 'openssl')
makedepends=('boost' 'gcc' 'make' 'automoc4')
conflicts=('bitcoin-bin' 'bitcoin-git' 'bitcoin-daemon')
license=('MIT')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-linux.tar.gz"
	"${pkgname}.desktop")
md5sums=('0094ff1fcaf30cf5d035c3b294ca1b68'
	 '77139ce8a40f46bd7d7ad0a743672113')
s1=$srcdir/${pkgname}-${pkgver}-linux
src_qt=$s1/src
src_d=$s1/src/src/
makefile_unix=makefile.unix

build() {
	cd $src_d

	msg "Patching $makefile_unix..."
	sed -i $makefile_unix \
		-e 's/\(-DNOPCH\)/\1 -DBOOST_FILESYSTEM_VERSION=2/' \
		-e 's/-Bstatic/--as-needed ${LDFLAGS}/g' \
		-e 's/\(USE_UPNP:=\)0/\11/' \
		-e 's/$(DEBUGFLAGS)//g' \
		-e 's/CXXFLAGS/CPPFLAGS/g' \
		-e 's/-O[0-3]/${CXXFLAGS}/g'

	# make bitcoind
	make $MAKEFLAGS -f $makefile_unix bitcoind

	# and make qt gui
	cd $src_qt
	qmake
	make $MAKEFLAGS
}


package() {
	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/share/pixmaps
	mkdir -p $pkgdir/usr/share/applications

	# get compiled binaries
	install -D -m755 $src_qt/bitcoin-qt $pkgdir/usr/bin/
	install -D -m755 $src_d/bitcoind $pkgdir/usr/bin/
	
	# add icon and .desktop file to pkg
	install -D -m644 $src_qt/src/qt/res/icons/bitcoin.png $pkgdir/usr/share/pixmaps/
	install -D -m644 $srcdir/$name.desktop $pkgdir/usr/share/applications/ 
}
