pkgname=cgal
pkgver=4.0.2
pkgrel=1
_pkgid=31176
pkgdesc="Computational Geometry Algorithms Library"
arch=('i686' 'x86_64')
url="http://www.cgal.org"
source=(http://gforge.inria.fr/frs/download.php/$_pkgid/CGAL-$pkgver.tar.xz)
depends=('mpfr' 'boost' 'gmp')
optdepends=('qt: for CGAL_Qt4')
makedepends=('cmake' 'xz')
license=('GPL' 'LGPL')
md5sums=('38c3ebb2249d43a84733bc33f3fdfa20')

build() {
  cd $srcdir/CGAL-$pkgver
  cmake . -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/CGAL-$pkgver
  make install DESTDIR=$pkgdir

  # The tarball still has all theese licenses included
  for _license in LICENSE{,.FREE_USE,.GPL,.LGPL}; do
    install -D -m644 $srcdir/CGAL-$pkgver/$_license $pkgdir/usr/share/licenses/$pkgname/$_license
  done
}
