pkgname=cgal
pkgver=4.1
pkgrel=2
_pkgid=31639
pkgdesc="Computational Geometry Algorithms Library"
arch=('i686' 'x86_64')
url="http://www.cgal.org"
source=(http://gforge.inria.fr/frs/download.php/$_pkgid/CGAL-$pkgver.tar.xz)
depends=('mpfr' 'boost' 'gmp' 'mesa')
optdepends=('qt: for CGAL_Qt4' 'eigen3: for some packages, see the CGAL manual')
makedepends=('cmake' 'xz')
license=('GPL' 'LGPL')
md5sums=('05efa9a1772fe05c180649c2c8ca8f09')

build() {
  cd $srcdir/CGAL-$pkgver
  cmake . -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/CGAL-$pkgver
  make install DESTDIR=$pkgdir

  # The tarball still has all theese licenses included
  for _license in LICENSE{,.FREE_USE,.GPL,.LGPL}; do
    install -D -m644 $srcdir/CGAL-$pkgver/$_license $pkgdir/usr/share/licenses/$pkgname/$_license
  done
}
