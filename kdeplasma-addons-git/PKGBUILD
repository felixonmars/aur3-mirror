# Maintainer: Antonio Rojas
# Contributor: Stefano Avallone <stavallo@gmail.com>

pkgbase=kdeplasma-addons-git
pkgname=(kdeplasma-addons-applets-calculator-git
# kdeplasma-addons-applets-eyes-git
 kdeplasma-addons-applets-notes-git
 kdeplasma-addons-applets-timer-git
 kdeplasma-addons-applets-kimpanel-git
 kdeplasma-addons-applets-fuzzy-clock-git
 kdeplasma-addons-applets-systemloadviewer-git
 kdeplasma-addons-applets-fifteenpuzzle-git
 kdeplasma-addons-wallpapers-git
 kdeplasma-addons-runners-converter-git
 kdeplasma-addons-runners-dictionary-git
 kdeplasma-addons-git)
pkgver=r6571.c74fbc8
pkgrel=1
pkgdesc="All kind of addons to improve your Plasma experience"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdeplasma-addons'
license=('LGPL')
makedepends=('kdoctools' 'extra-cmake-modules' 'git' 'krunner' 'kdelibs4support' 'kcmutils' 'scim' 'libibus')
source=('git://anongit.kde.org/kdeplasma-addons.git')
md5sums=('SKIP')

pkgver() {
  cd kdeplasma-addons
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdeplasma-addons \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
}

package_kdeplasma-addons-applets-calculator-git() {
  pkgdesc='Calculator applet for Plasma'
  depends=('plasma-workspace-git')
  cd build/applets/calculator
  make DESTDIR="$pkgdir" install
}

#package_kdeplasma-addons-applets-eyes-git() {
#  pkgdesc='XEyes clone for Plasma'
#  depends=('plasma-workspace-git')
#  cd build/applets/eyes
#  make DESTDIR="$pkgdir" install
#}

package_kdeplasma-addons-applets-notes-git() {
  pkgdesc='Notes applet for Plasma'
  depends=('plasma-workspace-git')
  cd build/applets/notes
  make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-applets-timer-git() {
  pkgdesc='Timer applet for plasma'
  depends=('plasma-workspace-git')
  cd build/applets/timer
  make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-applets-kimpanel-git() {
 pkgdesc='A generic input method panel for Oriental languages'
 depends=('plasma-workspace-git')
 optdepends=('scim: SCIM backend' 'fcitx: FCITX backend' 'libibus: IBUS backend')
 cd build/dataengines/kimpanel
 make DESTDIR="$pkgdir" install
 cd $srcdir/build/applets/kimpanel
 make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-applets-fuzzy-clock-git() {
  pkgdesc='Time displayed in a less precise format'
  depends=('plasma-workspace-git')
  cd build/applets/fuzzy-clock
  make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-applets-systemloadviewer-git() {
  pkgdesc='Tiny CPU/RAM/Swap monitor'
  depends=('plasma-workspace-git')
  cd build/applets/systemloadviewer
  make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-applets-fifteenpuzzle-git() {
  pkgdesc='Put the pieces in order'
  depends=('plasma-workspace-git')
  cd build/applets/fifteenPuzzle
  make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-wallpapers-git() {
  pkgdesc='QML wallpapers for Plasma'
  depends=('plasma-workspace-git')
  cd build/wallpapers
  make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-runners-converter-git() {
  pkgdesc='Unit conversion runner'
  depends=('plasma-workspace-git')
  cd build/runners/converter
  make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-runners-dictionary-git() {
  pkgdesc='Dictionary runner'
  depends=('plasma-workspace-git')
  cd build/runners/dictionary
  make DESTDIR="$pkgdir" install
}

package_kdeplasma-addons-git() {
  pkgdesc='All kind of addons to improve your Plasma experience'
  depends=(kdeplasma-addons-applets-calculator-git
#   kdeplasma-addons-applets-eyes-git
   kdeplasma-addons-applets-notes-git
   kdeplasma-addons-applets-timer-git
   kdeplasma-addons-applets-kimpanel-git
   kdeplasma-addons-applets-fuzzy-clock-git
   kdeplasma-addons-applets-systemloadviewer-git
   kdeplasma-addons-applets-fifteenpuzzle-git
   kdeplasma-addons-wallpapers-git
   kdeplasma-addons-runners-converter-git
   kdeplasma-addons-runners-dictionary-git)
}
