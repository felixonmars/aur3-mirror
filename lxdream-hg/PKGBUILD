# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
pkgname=lxdream-hg
pkgver=1289
pkgrel=1
pkgdesc="A linux-based emulator for the Sega Dreamcast system."
arch=('i686' 'x86_64')
url="http://www.lxdream.org/"
license=('GPL')
depends=('gtk2' 'libgl' 'libisofs' 'sdl')
makedepends=('mercurial>=2.1.1')
optdepends=('lirc-utils')
conflicts=('lxdream')
install='lxdream.install'
options=('!makeflags')
source=("glib-fixes.patch")
md5sums=('ce7f049918200dbd4066b74c9a26085b')

_hgroot=http://www.lxdream.org/hg/lxdream
_hgrepo=default

build() {
  cd "${srcdir}"
  msg "Connecting to Mercurial server...."

  if [[ -d "${_hgrepo}" ]]; then
    cd "${_hgrepo}"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "${_hgroot}" "${_hgrepo}"
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_hgrepo}-build"
  cp -r "${srcdir}/${_hgrepo}" "${srcdir}/${_hgrepo}-build"
  cd "${srcdir}/${_hgrepo}-build"

  # Fix compile with recent glib.
  patch -p1 -i "${srcdir}/glib-fixes.patch"

  #
  # BUILD HERE
  #
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-shared \
    --with-gtk \
    --with-sdl \
    --without-osmesa \
    --without-esd \
    --without-pulse
  make
}

package() {
  cd "${srcdir}/${_hgrepo}-build"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
