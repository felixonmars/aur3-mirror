# Maintainer: Jonathan Kotta <jpkotta AT gmail DOT com>
# Based on emacs from [extra] and emacs-bzr from the AUR

pkgname=emacs-beta
pkgver=24.4.rc1
_tarball_ver=${pkgver/.rc/-rc}
_real_ver=${pkgver/.rc?/}
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting real-time display editor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('librsvg' 'gpm' 'giflib' 'libxpm' 'libotf' 'm17n-lib' 'hicolor-icon-theme' 'desktop-file-utils' 'alsa-lib' 'imagemagick' 'gnutls' 'gtk3' 'gconf')
install=emacs.install
conflicts=('emacs')
provides=('emacs')
source=(ftp://alpha.gnu.org/gnu/emacs/pretest/emacs-${_tarball_ver}.tar.xz)
md5sums=('ad487658ad7421ad8d7b5152192eb945')

build() {
  cd "$srcdir"/emacs-$_real_ver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --with-x-toolkit=gtk3 --with-xft
  make
}

package() {
  cd "$srcdir"/emacs-$_real_ver
  make DESTDIR="$pkgdir" install
  
  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
  
  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/$_real_ver -exec chown root:root {} \;
  
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
  
  # remove conflict with texinfo package
  rm "$pkgdir"/usr/share/info/info.info.gz
  rm "$pkgdir"/usr/share/info/dir
}
