# Maintainer: Karl-Felix Glatzer <karl.glatzer@gmx.de>
pkgname=mingw-w64-orc
pkgver=0.4.19
pkgrel=1
pkgdesc="The Oild Runtime Compiler (mingw-w64)"
arch=('any')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!strip !buildflags !libtool' staticlibs)
source=(http://gstreamer.freedesktop.org/src/orc/orc-${pkgver}.tar.gz)
md5sums=('2cacea6271aade6d592fe1622a136f19')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build () {
  for _arch in ${_architectures}; do
    mkdir -p ${srcdir}/build-${_arch} && cd ${srcdir}/build-${_arch}

    unset LDFLAGS CPPFLAGS
    $srcdir/orc-$pkgver/configure --prefix=/usr/${_arch} --host=${_arch} --enable-static
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/build-${_arch}
    make DESTDIR="$pkgdir" install
    install -Dm644 ${srcdir}/orc-$pkgver/COPYING "$pkgdir/usr/${_arch}/share/licenses/orc/COPYING"

    ${_arch}-strip -x -g ${pkgdir}/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/*.a
    rm ${pkgdir}/usr/${_arch}/bin/*.exe
    rm -r ${pkgdir}/usr/${_arch}/share/gtk-doc
  done
}
