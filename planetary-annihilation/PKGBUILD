# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Swift Geek

# Please note you'll need to download the Linux tar.bz2 in order for this
# PKGBUILD to work, since PA is propietary software.

pkgname=planetary-annihilation
_alphaver=53072
pkgver=alpha${_alphaver}
_curlver=7.23.0
pkgrel=1
pkgdesc="RTS gameplay in a way that's never been seen before."
arch=("x86_64")
url="http://www.uberent.com/pa/"
license=("custom")
depends=("alsa-lib" "gconf" "nss" "gtk2")
options=(!strip)
source=(PA_Linux_${_alphaver}.tar.bz2::
        http://curl.haxx.se/download/curl-${_curlver}.tar.gz
        PA.sh)
noextract=(PA_Linux_${_alphaver}.tar.bz2)
PKGEXT=".pkg.tar"
md5sums=('a1cc8f7f81458218d6d43846a7703386'
         '018a9acee77ed70017c6f6cec855635a'
         'c68a1185aaa237a8924201bf98937e95')

build() {
  cd "$srcdir/curl-${_curlver}"
  ./configure
  make
}

package() {
  install -d "${pkgdir}/opt/"
  tar xf "${srcdir}/PA_Linux_${_alphaver}.tar.bz2" -C "${pkgdir}/opt/"

  install -Dm 644 "$srcdir/curl-${_curlver}/lib/.libs/libcurl.so.4" "${pkgdir}/opt/PA"

  install -Dm 644 "${pkgdir}/opt/PA/licenses.txt" "${pkgdir}/usr/share/licenses/$pkgname/licenses.txt"

  install -d "${pkgdir}"/usr/{lib,bin}
  ln -s /usr/lib/libudev.so "${pkgdir}/usr/lib/libudev.so.0"
  install -Dm 755 "${srcdir}/PA.sh" "${pkgdir}/usr/bin/PA"
}

