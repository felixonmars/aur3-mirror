# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Konstantin Nikiforov <helllamer@gmail.com> 
# Thanks to Alper Kanat <alperkanat@raptiye.org> for cassandra rc.d script. 
pkgname=dsc-cassandra
pkgver=1.1.6
pkgrel=1
pkgdesc='DataStax Community Edition distribution of Apache Cassandra'
arch=('any')
url='http://www.datastax.com/'
license=('Apache')
depends=('java-runtime' 'python2' 'thrift')
conflicts=('cassandra')
makedepends=('gnupg')
url_tgz="http://downloads.datastax.com/community/${pkgname}-${pkgver}-bin.tar.gz"
source=($url_tgz 'cassandra' 'cassandra.conf' "${pkgname}.install")
install="${pkgname}.install"
backup=(
	'etc/cassandra/cassandra-env.sh'
	'etc/cassandra/cassandra-topology.properties'
	'etc/cassandra/cassandra.yaml'
	'etc/cassandra/commitlog_archiving.properties'
	'etc/cassandra/log4j-server.properties'
	'etc/cassandra/log4j-tools.properties')

package() {

	cd $srcdir/${pkgname}-${pkgver}

	mkdir -p ${pkgdir}/etc
	mkdir -p ${pkgdir}/usr/bin
	mkdir -p ${pkgdir}/usr/share/cassandra
	mkdir -p ${pkgdir}/usr/lib/python2.7/site-packages

	sed -i 's_`dirname $0`/.._/usr/share/cassandra_' bin/cassandra.in.sh

	cp -a pylib/{cql,cqlshlib} ${pkgdir}/usr/lib/python2.7/site-packages
	cp -a {lib,interface} ${pkgdir}/usr/share/cassandra/
	cp -a conf ${pkgdir}/etc/cassandra/

	cd "${srcdir}/${pkgname}-${pkgver}/bin"

	install \
		cassandra \
		cassandra-cli \
		cqlsh \
		json2sstable \
		nodetool \
		sstable2json \
		sstablekeys \
		sstableloader \
		sstablescrub \
		stop-server \
		${pkgdir}/usr/bin/

	install \
		cassandra.in.sh \
		${pkgdir}/usr/share/cassandra/

	install -D -m755 ${startdir}/cassandra.conf ${pkgdir}/etc/conf.d/cassandra
	install -D -m755 ${startdir}/cassandra ${pkgdir}/etc/rc.d/cassandra
}
md5sums=('c6cd6d5c4e9474cd2583f2c0191ed1ee'
         '8dee56fb202728dd9d1e3c055b39fd4e'
         '184b5f3eb73574ff1dfa24ef7400c890'
         '84cd5c24d6222b89b2a6ca91e5770f7c')
