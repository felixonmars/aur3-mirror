# Contributor: Johannes Dewender  arch at JonnyJD dot net
pkgname=picard-plugins
pkgver=1.2
pkgrel=7
pkgdesc="Plugins for MusicBrainz Picard"
arch=('any')
url="http://musicbrainz.org/doc/MusicBrainz_Picard/Plugins"
license=('GPL')
depends=('picard')
replaces=('picard-plugins-lastfmplus')
conflicts=('picard-plugins-lastfmplus')
source=(
http://users.musicbrainz.org/~luks/picard-plugins/addrelease.py
http://users.musicbrainz.org/~luks/picard-plugins/classicdiscnumber.py
http://users.musicbrainz.org/~luks/picard-plugins/cuesheet.py
http://users.musicbrainz.org/~luks/picard-plugins/discnumber.py
http://users.musicbrainz.org/~luks/picard-plugins/featartist.py
http://users.musicbrainz.org/~luks/picard-plugins/featartistsintitles.py
lastfm_init.py::http://users.musicbrainz.org/~luks/picard-plugins/lastfm/__init__.py
http://users.musicbrainz.org/~luks/picard-plugins/lastfm/ui_options_lastfm.py
http://users.musicbrainz.org/~luks/picard-plugins/lastfm/options_lastfm.ui
lastfmplus_init.py::http://users.musicbrainz.org/~luks/picard-plugins/lastfmplus/__init__.py
lastfmplus_options.py::http://users.musicbrainz.org/~luks/picard-plugins/lastfmplus/ui_options_lastfm.py
http://users.musicbrainz.org/~luks/picard-plugins/no_release.py
http://users.musicbrainz.org/~luks/picard-plugins/release_type.py
replaygain_init.py::http://users.musicbrainz.org/~luks/picard-plugins/replaygain/__init__.py
http://users.musicbrainz.org/~luks/picard-plugins/replaygain/ui_options_replaygain.py
http://users.musicbrainz.org/~luks/picard-plugins/replaygain/options_replaygain.ui
http://users.musicbrainz.org/~luks/picard-plugins/swapprefix.py
http://users.musicbrainz.org/~luks/picard-plugins/titlecase.py
http://users.musicbrainz.org/~luks/picard-plugins/tracks2clipboard.py
)
md5sums=('a529674a3fc0919547b42d922c9b0259'
         'c0043da418d5bb67441e23dcbbfa62b0'
         'ced21c06d002b0d9bde3a145c1cca8be'
         '324fa53c1a81386fae1840430f95fb11'
         '78c29f9742227b8a5eb04eb3a2e331e5'
         'a8b013fb210c84c012f4c872e08c35f7'
         'e44039237cf03e1dbd97c000f63883b1'
         '438e917b1edd4edbd4a68b907e6c0549'
         '258ec7b5d26a381edfee7547435d527f'
         '079e7506311d4f8703f8936a00e0ae04'
         'ec8aac7b6ea903ee94d897bf81a06e6f'
         'ebcc001aa0797e80a26ef55e269ae96f'
         '9737786d388b9fc829ed1f28cc0f43b4'
         'c150f3fca36dab69461d6d21c77e3e9f'
         '1ce491b7a7c093ddda1fafe124f7f89b'
         '04af5f23ec34309e438a0ec405333697'
         '07aad5da4832b0484819be8a952c1820'
         '4723aa93b2ea8d0243b68368b5937a79'
         '5e0b997245984862935f432bc1cb4522')

package() {
  plugindir="${pkgdir}/usr/lib/python2.7/site-packages/picard/plugins"
  cd "$srcdir"
  install -d "$plugindir"
  install -m644 -t "$plugindir" \
    addrelease.py classicdiscnumber.py cuesheet.py discnumber.py \
    featartist.py featartistsintitles.py no_release.py \
    release_type.py swapprefix.py \
    titlecase.py tracks2clipboard.py

  install -m644 -D lastfm_init.py "$plugindir/lastfm/__init__.py"
  install -m644 ui_options_lastfm.py "$plugindir/lastfm/"

  install -m644 -D lastfmplus_options.py \
                              "$plugindir/lastfmplus/ui_options_lastfm.py"
  install -m644 lastfmplus_init.py "$plugindir/lastfmplus/__init__.py"

  install -m644 -D replaygain_init.py "$plugindir/replaygain/__init__.py"
  install -m644 -t "$plugindir/replaygain" \
    ui_options_replaygain.py options_replaygain.ui
}

# vim:set ts=2 sw=2 et:
