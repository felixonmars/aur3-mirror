# Maintainer: Mike WB2FKO <mph at sportscliche dot com>
pkgname=wsjt-svn
pkgver=2459
pkgrel=1
pkgdesc="Weak Signal Communication by K1JT (svn)"
arch=('i686')
url="http://physics.princeton.edu/pulsar/K1JT/wsjt.html"
license=('GPL')
depends=('python2' 'python-imaging' 'python2-numpy' 'tk' 'libsamplerate' 'portaudio' 'fftw' 'g95')
makedepends=('subversion')
source=(patch
        $pkgname.png
        $pkgname.desktop)
md5sums=('3c28a97d157f5122607fc7915f9a0c06'
         'dd21318d4aa3423f1fbefc53061cc40d'
         '399255ed273fa4a3ca4a9da3d869b6e2')

_svntrunk="svn://svn.berlios.de/wsjt/trunk"
_svnmod="wsjt-code"

build() {
# Set the build environment to python2
 export PYTHON="/usr/bin/python2"
 export F2PY="/usr/bin/python2 f2py.py"
 unset LDFLAGS
 cd "$srcdir"

# If the most current subversion cannot be built, 
# checkout the most recent version that can.  Uncomment
# the following line and comment the five lines following.

# svn co -r 2405 $_svntrunk $_svnmod

if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
# This patch gets f2py2e from python2-numpy
  install -Dm644 patch $srcdir/$_svnmod-build/f2py.py
  cd "$srcdir/$_svnmod-build"

# Make problems can sometimes be fixed by replacing g95 with gfortran 
  ./configure --prefix=/usr --enable-g95
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  rm -rf build/
  python2 setup.py install --root=$pkgdir/ 
  sed -i 's:python:python2:g' wsjt
  install -m 755 wsjt $pkgdir/usr/bin/
  mkdir -p $pkgdir/usr/share/wsjt
  install -m 0644 CALL3.TXT $pkgdir/usr/share/wsjt/
  install -m 0644 wsjtrc $pkgdir/usr/share/wsjt/
  install -m 0644 dmet_*.dat $pkgdir/usr/share/wsjt/
  install -m 0644 *.TXT $pkgdir/usr/share/wsjt/
  install -m 0644 *.pdf $pkgdir/usr/share/wsjt/
  cp -ra $srcdir/$_svnmod-build/RxWav/Samples/*.WAV $pkgdir/usr/share/wsjt/

# Install a menu item with icon
  cd $srcdir
  mkdir -p $pkgdir/usr/share/pixmaps
  mkdir -p $pkgdir/usr/share/applications
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644  $pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
