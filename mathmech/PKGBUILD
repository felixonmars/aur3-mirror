# Author: Evgeniy "arcanis" Alexeev <esalexeev@gmail.com>
# Maintainer: Evgeniy "arcanis" Alexeev <esalexeev@gmail.com>

pkgname=mathmech
pkgver=1.1.1
pkgrel=1
pkgdesc="Software package for analysis of molecular dynamics trajectories"
arch=('i686' 'x86_64')
url="https://github.com/arcan1s/moldyn"
license=('GPL')
depends=('qt4' 'qwt')
makedepends=('automoc4' 'cmake')
source=(https://github.com/arcan1s/moldyn/releases/download/mm-${pkgver}/${pkgname}-${pkgver}-src.zip)
md5sums=('6a7f3dd17ae4ad0498110aaaadea5a2b')
_cmakekeys="-DCMAKE_INSTALL_PREFIX=/usr
            -DQWT_INCLUDE_PATH=/usr/include/qwt
            -DQWT_LIBRARY_PATH=/usr/lib
            -DMM_PREFIX=mm_
            -DADD_DOCS:BOOL=0
            -DCMAKE_BUILD_TYPE=Release"

build ()
{
  if [[ -d ${srcdir}/build ]]; then
    rm -rf "${srcdir}/build"
  fi
  mkdir "${srcdir}/build"; cd "${srcdir}/build"
  cmake ${_cmakekeys} ../
  make
}

package()
{
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
