# Maintainer: Garrett <floft.net/contact>
pkgname=openlp
pkgver=1.9.9
pkgrel=1
pkgdesc="Church presentation software."
arch=('any')
url='http://openlp.org/'
license=('GPLv2')
depends=('qt' 'python2' 'python2-pyqt' 'python2-distribute' 'phonon' 'python2-chardet'
	'python2-lxml' 'python-beautifulsoup' 'python2-sqlalchemy' 'python2-sip'
	'python-pysqlite-legacy' 'python2-pyenchant' 'python-mako' 'python2-migrate')
optdepends=('libreoffice-impress: display impress presentations')
install=openlp.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/OpenLP-$pkgver.tar.gz openlp.sh)
md5sums=('25ea5c569d953a96942ce2fce3d75e35'
         'bb78637dd085de567dc8aea8699939bf')
sha256sums=('a840ad008ec00779f6deca6682e6d13c8d2835c5492a9326aa7876e8ea7f4224'
            'c85060eeb512410b8b9b9a3a638c70006f45ded0e4b551fc7135fdcd2ff915d5')

build() {
  cd "$srcdir/OpenLP-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv "$pkgdir/usr/bin/openlp"{.pyw,}

  echo "$pkgver" > "$pkgdir/usr/lib/python2.7/site-packages/openlp/.version"

  install -Dm0755 "$srcdir/openlp.sh" "$pkgdir/etc/profile.d/openlp.sh"
  install -Dm0644 "resources/openlp.desktop" "$pkgdir/usr/share/applications/openlp.desktop"
  install -Dm0644 "resources/images/openlp-logo.svg" "$pkgdir/usr/share/pixmaps/openlp.svg"
  install -Dm0644 "resources/images/openlp-logo-48x48.png" "$pkgdir/usr/share/pixmaps/openlp.png"

  #translations
  tsrcdir="$srcdir/OpenLP-$pkgver/resources/i18n"
  tdestdir="$pkgdir/usr/share/$pkgname/i18n"
  mkdir -p "$tdestdir"
  cd "$tsrcdir"
  
  for file in *.ts; do
    lconvert -i "$file" -o "$tdestdir/${file%%ts}qm"
  done
}
