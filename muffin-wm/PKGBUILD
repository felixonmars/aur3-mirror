# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Maintainer: Ner0

pkgname=muffin-wm
pkgver=1.0.9
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gconf' 'gobject-introspection' 'gsettings-desktop-schemas' 'libcanberra' 'libxxf86vm' 'startup-notification' 'zenity' 'dconf')
makedepends=('libltdl' 'intltool' 'gnome-doc-utils' 'pkg-config')
conflicts=('muffin-git')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/muffin/tarball/$pkgver")
md5sums=('5d3675d20718c142be672475573f3d7b')

build() {
  cd linuxmint-*

  autoreconf -vfi
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin --localstatedir=/var --disable-static --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir"/linuxmint-*
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
