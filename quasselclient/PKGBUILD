# Maintainer: Jochen Schalanda <jochen+aur (-at-) schalanda.name>

pkgname=quasselclient
pkgver=0.8.0
pkgrel=1
pkgdesc="Qt4 IRC client with a separated core - client only"
url="http://quassel-irc.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdelibs' 'oxygen-icons' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4')
source="${url}/pub/quassel-${pkgver}.tar.bz2"
install=quassel.install

build() {
  [[ ! -d build ]] && mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=1 \
    -DWITH_OPENSSL=ON \
    -DCMAKE_BUILD_TYPE="Release" \
    -DWANT_CORE=OFF \
    -DWANT_MONO=OFF \
    ../quassel-${pkgver}/ \
    -Wno-dev

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
md5sums=('546e2a950f44c4c0262c09d48f14e998')
