# $Id$
# Maintainer: Pedro Martinez-Julia <pedromj@um.es>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-matplotlib-noqt
pkgver=1.3.1
pkgrel=2
pkgdesc="A python plotting library, making publication quality plots"
arch=('i686' 'x86_64')
url='http://matplotlib.org'
license=('custom')
depends=('python-pytz' 'python-numpy' 'python-cairo' 'python-dateutil' 'python-pyparsing')
makedepends=('python-pytz' 'python-numpy' 'tk' 'python-cairo' 'python-dateutil'
    'python-gobject' 'python-pyparsing' 'python-six' 'ghostscript' 'texlive-bin')
optdepends=('tk: used by the TkAgg backend'
    'ghostscript: usetex dependencies'
    'texlive-bin: usetex dependencies')
source=("http://sourceforge.net/projects/matplotlib/files/matplotlib/matplotlib-${pkgver}/matplotlib-${pkgver}.tar.gz")
sha1sums=('8578afc86424392591c0ee03f7613ffa9b6f68ee')

build() {
   cp -a matplotlib-${pkgver} matplotlib-${pkgver}-py3

   cd matplotlib-${pkgver}-py3
   for file in $(find . -name '*.py' -print); do
      sed -i -e "s|^#!.*/usr/bin/python|#!/usr/bin/python3|" \
             -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python3|" ${file}
   done

   python3 setup.py build
}

package() {
   cd "${srcdir}"/matplotlib-${pkgver}-py3
   python3 setup.py install -O1 --skip-build --root "${pkgdir}" --prefix=/usr

   install -dm755 "${pkgdir}"/usr/share/licenses/python-matplotlib-noqt
   install -m 644 doc/users/license.rst "${pkgdir}"/usr/share/licenses/python-matplotlib-noqt
}
