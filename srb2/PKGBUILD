# Maintainer: DeedleFake <yisszev at beckforce dot com>

pkgname=srb2
pkgver=2.0.6
pkgrel=7
pkgdesc="Sonic Robo Blast 2. A 3-D Sonic fan-game based off of Doom Legacy."
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.srb2.org'
depends=('sdl_mixer' 'libpng12' "srb2-data=${pkgver}")
provides=("srb2")
conflicts=('srb2-bin' "bin32-srb2")
if [ "$CARCH" == "x86_64" ]; then
	makedepends=('subversion')
else
	makedepends=('subversion' 'nasm')
fi
source=(srb2.bashcomp
		SRB2.desktop
		SRB2-OpenGL.desktop)
md5sums=('f8f020fcb255da3f6afb414d67a421c2'
         '7388d3393eba404df482bc01da618fed'
         '4ddefbaee0bb25fcb485e0f95e45a115')

build() {
	cd "$srcdir"

	msg2 'Fetching code from svn...'
	svn co -q "svn://code.srb2.org/SRB2/tags/SRB2v${pkgver}" srb2-src

	cd "$srcdir/srb2-src"
	sh comptime.sh "$srcdir/srb2-src/src" || :

	cd "$srcdir/srb2-src/src"
	if [ "$CARCH" == "x86_64" ]; then
		local _makeline='LINUX64=1 PNG_CFLAGS=-I../tools/libpng-src PNG_LDFLAGS=-lpng12'
	else
		local _makeline='LINUX=1 PNG_CFLAGS=-I../tools/libpng-src PNG_LDFLAGS=-lpng12'
	fi
	msg2 'Building...'
	make $_makeline
}

package() {
  cd "$srcdir"

  if [ "$CARCH" == "x86_64" ]; then
    local _path="$srcdir/srb2-src/bin/Linux64/Release"
  else
    local _path="$srcdir/srb2-src/bin/Linux/Release"
  fi

  install -m 755 -D "$_path/lsdlsrb2" "$pkgdir/usr/bin/srb2"

  install -m 644 -D SRB2.desktop "$pkgdir/usr/share/applications/SRB2.desktop"
  install -m 644 -D SRB2-OpenGL.desktop "$pkgdir/usr/share/applications/SRB2-OpenGL.desktop"
  install -m 644 -D srb2.bashcomp "$pkgdir/etc/bash_completion.d/srb2"
}
