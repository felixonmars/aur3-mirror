# Maintainer:  Eugen Zagorodniy <https://github.com/ezag>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Nick Ã˜stergaard <oe.nick at gmail.com>

pkgname=qwt5
pkgver=5.2.3
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications (version 5.X)"
arch=('i686' 'x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt4')
options=('!makeflags')
license=("custom:$pkgname")
provides=('qwt')
source=("http://downloads.sourceforge.net/sourceforge/qwt/qwt-$pkgver.tar.bz2" \
        "qwtconfig-archlinux.pri")
md5sums=('7d37a11d02bc7d095d0ca6427ec97b8d'
         '15db52c3312b5f8f7cbbdabc4831591b')

build() {
  cd ${srcdir}/qwt-${pkgver}

  # copy our config file to the right place
  install -Dm644 ${srcdir}/qwtconfig-archlinux.pri \
${srcdir}/qwt-${pkgver}/qwtconfig.pri

  # build qwt:
  qmake-qt4 qwt.pro
  make || return 1
}

package() {
  cd ${srcdir}/qwt-${pkgver}

  make INSTALL_ROOT=${pkgdir} QTDIR=/usr install

  # rename conflicting symlinks
  mv ${pkgdir}/usr/lib/libqwt.so ${pkgdir}/usr/lib/libqwt5.so
  mv ${pkgdir}/usr/lib/qt4/plugins/designer/libqwt_designer_plugin.so \
     ${pkgdir}/usr/lib/qt4/plugins/designer/libqwt_designer_plugin5.so

  # move html docs to proper place and move man pages to avoid conflict
  # with qwt from [extra]
  install -d ${pkgdir}/usr/share/doc/${pkgname}
  mv ${pkgdir}/usr/share/html ${pkgdir}/usr/share/doc/${pkgname}/
  mv ${pkgdir}/usr/share/man ${pkgdir}/usr/share/doc/${pkgname}/

  # install licence
  install -Dm644 ${srcdir}/qwt-${pkgver}/COPYING \
                 ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
