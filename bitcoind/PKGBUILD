# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: shahid <helllamer@gmail.com>

pkgname=bitcoind
pkgver=0.8.6
pkgrel=1
pkgdesc="Bitcoin daemon for long-running server usage"
arch=('i686' 'x86_64')
url="http://bitcoin.org/"
depends=('boost-libs' 'miniupnpc' 'openssl')
makedepends=('automoc4' 'boost' 'miniupnpc' 'qrencode')
license=('MIT')
source=(http://sourceforge.net/projects/bitcoin/files/Bitcoin/${pkgname%d}-$pkgver/${pkgname%d}-$pkgver-linux.tar.gz
        bitcoin.conf
        bitcoin.logrotate
        bitcoin.service)
sha256sums=('73495de53d1a30676884961e39ff46c3851ff770eeaa767331d065ff0ce8dd0c'
            '7f850b453cee125a5264282af0cfb0ff77f16cdbd602c600632d26e31e3d0e42'
            '8f05207b586916d489b7d25a68eaacf6e678d7cbb5bfbac551903506b32f904f'
            '4f745a7bd50293e474ac1224012dcebce3aa1e3cbd1217fc03a7879e5926b8f4')
backup=('etc/bitcoin/bitcoin.conf'
        'etc/logrotate.d/bitcoind')
provides=('bitcoin-daemon')
conflicts=('bitcoin-daemon')
install=bitcoin.install

build() {
  cd "$srcdir/${pkgname%d}-$pkgver-linux/src"

  msg 'Building...'
  make -f makefile.unix -C src CXXFLAGS="$CXXFLAGS"
}

package() {
  cd "$srcdir/${pkgname%d}-$pkgver-linux/src"

  msg 'Installing license...'
  install -Dm 644 COPYING $pkgdir/usr/share/licenses/${pkgname%d}/COPYING

  msg 'Installing documentation and man pages...'
  install -Dm 644 contrib/debian/examples/bitcoin.conf $pkgdir/usr/share/doc/${pkgname%d}/examples/bitcoin.conf
  install -Dm 644 contrib/debian/manpages/bitcoind.1 $pkgdir/usr/share/man/man1/bitcoind.1
  install -Dm 644 contrib/debian/manpages/bitcoin.conf.5 $pkgdir/usr/share/man/man5/bitcoin.conf.5

  msg 'Installing executable...'
  install -Dm 755 src/bitcoind $pkgdir/usr/bin/bitcoind

  msg 'Installing bitcoin.conf file...'
  install -Dm 600 $srcdir/bitcoin.conf $pkgdir/etc/bitcoin/bitcoin.conf

  msg 'Installing systemd service files...'
  install -Dm 644 $srcdir/bitcoin.service $pkgdir/usr/lib/systemd/system/bitcoin.service

  msg 'Readying logrotate...'
  install -Dm 644 $srcdir/bitcoin.logrotate $pkgdir/etc/logrotate.d/bitcoin
}
