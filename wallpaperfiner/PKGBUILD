# Author: Peter Deak <hyper80[AT]gmail[DOT]com>
# Maintainer: Tomasz "ludvick' Niedzielski <ludvick0[AT]gmail[DOT]com>

pkgname=wallpaperfiner
_webname=wpfiner
_zipname=WallpaperFiner
pkgver=0.94
pkgrel=1
pkgdesc="A small and easy to use utility which adjusts images to fit your desktop size"
arch=('i686' 'x86_64')
url='http://hyperprog.com/wpfiner/'
license=('GPL2')
depends=('qt>=4.8.0')
makedepends=('make')
source=(http://hyperprog.com/$_webname/$_webname-source-$pkgver.zip)
md5sums=('ba4e4ea40a1fec3019af40d413933d8f')

build() {
	cd "${srcdir}/${_zipname}"

	qmake -makefile
	make
}

package() {
	cd "${srcdir}/${_zipname}"

	install -Dm755 wallpaperfiner "$pkgdir/usr/bin/wallpaperfiner"
	install -Dm644 wpfiner.png "$pkgdir/usr/share/pixmaps/wallpaperfiner.png"

	cat > wallpaperfiner.desktop << EoF
[Desktop Entry]
Name=Wallpaper Finer
Name[hu]=Háttérkép Igazító
Name[pl]=Dopasuj tapetę
Comment=A small and easy to use utility which adjusts images to fit your desktop size
Comment[hu]=
Comment[pl]=Niewielkie i proste w użyciu narzędzie, dopasowujujące obrazy do rozmiarów pulpitu
Type=Application
Categories=Qt;KDE;Graphics;
Icon=/usr/share/pixmaps/wallpaperfiner.png
Exec=/usr/bin/wallpaperfiner
Terminal=false
EoF

	install -Dm644 wallpaperfiner.desktop "$pkgdir/usr/share/applications/wallpaperfiner.desktop"
}
