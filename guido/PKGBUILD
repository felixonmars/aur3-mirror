# Maintainer : Bernardo Barros <bernardobarros@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=guido
pkgver=1.49
pkgrel=1
pkgdesc="A formal language for score level music representation and a Qt editing suite."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/guidolib"
license=('GPL')
depends=('qt')
optdepends=('pd: pure data support')
install="$pkgname.install"
source=("http://sourceforge.net/projects/guidolib/files/Guidolib-src/guidolib-$pkgver-src.tgz"
        "guidoeditor.desktop"
        "guidoscenecomposer.desktop"
        "guidoqtfontviewer.desktop"
        "guidoqtviewer.desktop")
md5sums=('30838e314b17bd5501e5c1155b62131a'
         '8b4fe7dc4802ccffd7dbf62fbc29bf6a'
         '47fc38c558151973d45a5c8eb5fe5c67'
         '25ed9b380987bf793effabf7924fd606'
         '5dfa4a7f60e04f9ad5a7bc9cb25df7cc')

build() {
  cd "$srcdir/guidolib-$pkgver-src"

  # remove lib64 destination
  sed -i "s|lib64|lib|g" `grep -Erl lib64 .`

  # let Qt apps build against GUIDOEngine
  sed -i "s|lGUIDOEngine|& -L../../build|g" Qt/GUIDOEngineLink.pri

  cd cmake && cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make

  # Build Qt apps
  cd ../Qt 
  make unix
  # cd GuidoQtViewer && qmake && make 
}

package() {
  cd "$srcdir/guidolib-$pkgver-src/cmake"

  make DESTDIR="$pkgdir/" install

  # Install Qt apps
  install -Dm 755 ../Qt/bin/GuidoEditor "$pkgdir/usr/bin/guidoeditor"
  install -Dm 755 ../Qt/bin/GuidoSceneComposer "$pkgdir/usr/bin/guidoscenecomposer"
  install -Dm 755 ../Qt/bin/GuidoQtFontViewer "$pkgdir/usr/bin/guidoqtfontviewer"
  # install -Dm 755 ../Qt/bin/GuidoQtViewer "$pkgdir/usr/bin/guidoqtviewer"
  # install -Dm 755 ../Qt/bin/guido2Image "$pkgdir/usr/bin/guido2image"

  # Install guido score font
  install -Dm 644 ../src/guido2.ttf "$pkgdir/usr/share/fonts/TTF/guido2.ttf"

  # docs: 
  install -d "$pkgdir/usr/share/doc/guido"
  cp -a ../doc/[^doxygen]* "$pkgdir/usr/share/doc/guido"

  # Install examples
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a ../gmn-examples "$pkgdir/usr/share/$pkgname"

  # Install desktop files and pixmaps
  install -d "$pkgdir/usr/share/applications"
  install -Dm644 "$srcdir/"*.desktop "$pkgdir/usr/share/applications"
  install -Dm644 ../Qt/GuidoSceneComposer/rsc/addFromFileGuido.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

