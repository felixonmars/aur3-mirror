# Maintainer: Bastien Dejean <nihilhill@gmail.com>

pkgname=sxhkd
pkgver=0.4.3
pkgrel=1
pkgdesc='A simple X hotkey daemon'
arch=('i686' 'x86_64')
url='https://github.com/baskerville/sxhkd'
license=('custom:BSD')
depends=('libxcb' 'xcb-util-keysyms')
makedepends=('xcb-util')
provides=('sxhkd')
conflicts=('sxhkd')
source=("https://github.com/baskerville/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('d19d83e020066507c664de0546d0c3b6')

build() {
    cd "$srcdir/${pkgname}-${pkgver}"
    make PREFIX="/usr"
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    make PREFIX="/usr" DESTDIR="$pkgdir" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
