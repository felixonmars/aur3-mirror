# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=edfbrowser
pkgver=1.47
pkgrel=1
pkgdesc="A viewer and toolbox for timeseries storage files like EEG, EMG, ECG, BioImpedance, etc."
arch=(i686 x86_64)
url="http://www.teuniz.net/edfbrowser/"
license=('GPL')
depends=('qt')
install="$pkgname.install"
source=("${url}${pkgname}_${pkgver/.}_source.tar.gz"
	"$pkgname.desktop")
md5sums=('411b346f72135220573b660ffef1dbc0'
         '15ee52eb704bf15498326a8ab29e2c91')

build() {
  cd ${srcdir}/${pkgname}_${pkgver/.}_source
  qmake && make
}

package() {
  cd ${srcdir}/${pkgname}_${pkgver/.}_source

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 images/edf.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
