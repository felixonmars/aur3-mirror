# Maintainer: James Duley <jagduley gmail>

pkgname=openpilot-git
_gitname=OpenPilot
pkgver=RELEASE.14.06.01.r13.g0336dc4
pkgrel=1
pkgdesc="Ground control station with firmware for OpenPilot."
arch=('i686' 'x86_64')
url="https://openpilot.org"
license=('GPL')
depends=('qt5-svg' 'qt5-serialport' 'qt5-multimedia' 'sdl' 'libusb')
makedepends=('git' 'python2' 'gcc-arm-none-eabi' 'qt5-quick1' 'qt5-quickcontrols')
source=("$_gitname::git://git.openpilot.org/OpenPilot.git#branch=parched/master-archlinux")
#for development use this source (and manually clone first)
#source=("$_gitname::git+ssh://gitolite@git.openpilot.org/OpenPilot.git#branch=parched/master-archlinux")
# Because the sources are not static, skip Git checksum:
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  make all
}

package() {
  cd "$srcdir/$_gitname/build"
  install -d "$pkgdir/usr/bin"
  install -d "$pkgdir/usr/lib"
  install -d "$pkgdir/usr/lib/udev/rules.d"
  install -d "$pkgdir/usr/share"
  install -d "$pkgdir/usr/share/applications"
  install -d "$pkgdir/usr/share/pixmaps"

  cp openpilotgcs_release/bin/openpilotgcs.bin $pkgdir/usr/bin/openpilot-gcs
  cp openpilotgcs_release/bin/udp_test $pkgdir/usr/bin
  cp -r openpilotgcs_release/lib/openpilotgcs $pkgdir/usr/lib
  cp -r openpilotgcs_release/share/openpilotgcs $pkgdir/usr/share
  rm $pkgdir/usr/share/openpilotgcs/translations/Makefile

  cd "$srcdir/$_gitname/package/linux"
  cp openpilot.desktop "$pkgdir/usr/share/applications"
  cp openpilot.png "$pkgdir/usr/share/pixmaps"
  cp 45-openpilot-permissions.rules "$pkgdir/usr/lib/udev/rules.d/55-openpilot.rules"
}
