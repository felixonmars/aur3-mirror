# Maintainer: Marcel Korpel <marcel[dot]korpel[at]gmail>

_pkgname=xf86-input-evdev
pkgname=$_pkgname-remap-git
pkgver=20121016
pkgrel=1
pkgdesc='X.org evdev input driver with keycode remapping'
arch=(i686 x86_64)
url=http://www.thenautilus.net/cgit/$_pkgname/
license=(custom)
depends=(glibc)
makedepends=(git mtdev xorg-server-devel resourceproto 'xorg-server>=1.7.0')
conflicts=($_pkgname)
provides=($_pkgname=2.7.0)
options=(!libtool !makeflags)
groups=(xorg-input-drivers)

_gitroot=git://www.thenautilus.net/$_pkgname
_gitname=$_pkgname
_remap=code-remap-2.7.0

build() {
  cd "$srcdir"

  msg 'Connecting to GIT server....'
  if [[ -d $_gitname/.git ]] ; then
    pushd $_gitname
    git pull origin
    popd
    msg 'The local files are updated.'
  else
    git clone $_gitroot
  fi
  msg 'GIT checkout done or server timeout'

  [[ -d $_gitname-build ]] && rm -rf $_gitname-build
  cp -r $_gitname/ $_gitname-build
  cd $_gitname-build

  git pull --no-edit $_gitroot $_remap  # applies patches

  msg 'Starting make...'

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$_gitname-build
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/COPYING"
}
