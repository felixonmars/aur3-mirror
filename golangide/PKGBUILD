# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=golangide
pkgver=11
_pkgver='-beta3-0502'
pkgrel=1
pkgdesc="Simple IDE for Go to edit code and build projects"
arch=('x86_64' 'i686')
url="http://code.google.com/p/golangide/"
depends=('go' 'qt')
license=('LGPL')
options=(!strip)
if [ "$CARCH" == "x86_64" ]
then
  source=("http://$pkgname.googlecode.com/files/liteide-linux-x86_64-x${pkgver}$_pkgver.tar.bz2"
          "golangide.desktop"
          "golangide.png")
  md5sums=('c46154cc2f1c86d75e499d5ffa062aab'
         '61cc285d9aa94bacc8c3bebc58f6565d'
         '7f459be003d754a3a2c34be91b8c0b1f')
else
  source=("http://$pkgname.googlecode.com/files/liteide-linux-x86-x${pkgver}$_pkgver.tar.bz2"
          "golangide.desktop"
          "golangide.png")
  md5sums=('2d36b054f5540d39c47a3b16a6e46c0c'
           '61cc285d9aa94bacc8c3bebc58f6565d'
           '7f459be003d754a3a2c34be91b8c0b1f')          
fi

build() {
  cd "$srcdir/liteide"

  msg2 "Fixing insecure RPATH..."
  find . -name "*.so" -type f -exec sed -i 's|/home/win|/usr/lib/|g' {} \;
  find . -name liteide -type f -exec sed -i 's|/home/win|/usr/lib/|g' {} \;
}

package() {
  cd "$srcdir/liteide"

  msg2 "Creating directories..."
  mkdir -p "$pkgdir/usr/lib/liteide"
  mkdir -p "$pkgdir/usr/share/liteide"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"

  msg2 "Packaging executables..."
  install -Dm755 bin/goastview "$pkgdir/usr/bin/goastview"
  install -Dm755 bin/godocview "$pkgdir/usr/bin/godocview"
  install -Dm755 bin/goexec "$pkgdir/usr/bin/goexec"
  install -Dm755 bin/gopromake "$pkgdir/usr/bin/gopromake"
  install -Dm755 bin/liteide "$pkgdir/usr/bin/golangide"

  msg2 "Packaging resources..."
  cp -r share/liteide/* "$pkgdir/usr/share/liteide"

  msg2 "Packaging libraries and plugins..."
  cp -r lib/liteide/* "$pkgdir/usr/lib/liteide"

  msg2 "Packaging docs..."
  cp -r share/liteide/doc/*.{html,css} "$pkgdir/usr/share/doc/$pkgname"

  msg2 "Packaging license..."
  install -Dm644 LICENSE.LGPL \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.LGPL"

  msg2 "Packaging license exception..."
  install -Dm644 LGPL_EXCEPTION.TXT \
    "$pkgdir/usr/share/licenses/$pkgname/LGPL_EXCEPTION.TXT"

  cd ..

  msg2 "Packaging menu entry and icon..."
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 "Cleaning up..."
  rm -rf "$pkgdir/usr/share/$pkgname/doc"
}

# vim:set ts=2 sw=2 et:
