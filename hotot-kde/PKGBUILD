# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=hotot-kde
pkgver=0.9.8.10
pkgrel=2
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
arch=('i686' 'x86_64')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('hotot-data' 'kdebase-runtime')
makedepends=('cmake' 'intltool' 'python2')
install="hotot-kde.install"
source=("https://github.com/shellex/Hotot/tarball/$pkgver")
md5sums=('ce4fdb6b635cae008e8c8e861bedb6d5')

build() {
  cd ${srcdir}/shellex-Hotot-*
  [ -d bld ] || mkdir bld && cd bld

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DWITH_GTK=OFF \
           -DWITH_GIR=OFF \
           -DWITH_QT=ON \
           -DWITH_KDE=ON \
           -DWITH_CHROME=OFF \
           -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd ${srcdir}/shellex-Hotot-*/bld

  # bin
  install -Dm755 qt/hotot-qt \
    "$pkgdir/usr/bin/hotot-kde"

  # desktop file
  install -Dm644 misc/hotot-qt.desktop \
    "$pkgdir/usr/share/applications/hotot-kde.desktop"
  sed -i "s/qt/kde/;s/Qt/Kde/" \
    "$pkgdir/usr/share/applications/hotot-kde.desktop"
}

# vim:set ts=2 sw=2 et:
