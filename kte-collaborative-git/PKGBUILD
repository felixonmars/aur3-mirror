# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kte-collaborative-git
pkgver=617.f0b0744
pkgrel=1
pkgdesc='A plugin for KTextEditor which allows to edit text documents collaboratively'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/network/kte-collaborative'
license=('GPL')
depends=('telepathy-kde-common-internals-git' 'libqinfinity-git')
makedepends=('cmake' 'automoc4' 'git')
conflicts=('kte-collaborative')
source=('git://anongit.kde.org/kte-collaborative')
md5sums=('SKIP')

pkgver() {
  cd kte-collaborative
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kte-collaborative \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

