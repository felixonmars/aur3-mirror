# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg-turbo-svn
pkgver=721
pkgrel=1
pkgdesc="A derivative of libjpeg which uses SIMD instructions to accelerate baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
makedepends=('subversion' 'nasm')
provides=('libjpeg-turbo' 'libjpeg=8')
conflicts=('libjpeg-turbo')
options=('!libtool')
depends=('glibc')
source=('LICENSE')
md5sums=('8b3f4d426b97ea335b053353f9f220b2') 
_svntrunk=https://libjpeg-turbo.svn.sourceforge.net/svnroot/libjpeg-turbo/trunk
_svnmod=libjpeg-turbo

build() {
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf $_svnmod-build
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  # BUILD
  autoreconf -fiv
  ./configure --with-jpeg8 --prefix=/usr
  make
}
package() {
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
  cd $_svnmod-build
  make DESTDIR="$pkgdir/" install
}

