# Maintainer: joat
# Submitter: BxS <bxsbxs at gmail dot com>

pkgname=microchip-mplabxc16-bin
pkgver=1.21
pkgrel=1
pkgdesc="C compiler for PIC24 MCUs and dsPIC DSCs"
arch=(i686 x86_64)
url=http://www.microchip.com/xc16
license=(custom)
depends=(xclm-dirs)
[ $CARCH = x86_64 ] && depends=(lib32-gcc-libs)
provides=(mplabxc16)
conflicts=(mplabxc16)
options=(!strip docs libtool emptydirs !zipman)
install=$pkgname.install
instdir=/opt/microchip/xc16/v$pkgver
installer=xc16-v$pkgver-linux-installer.run
source=(http://ww1.microchip.com/downloads/en/DeviceDoc/$installer.tar)
[ $CARCH = x86_64 ] && source+=(fakeroot-i686.pkg.tar.xz::http://www.archlinux.org/packages/core/i686/fakeroot/download/)
md5sums=(ed96548bceb3a7603e69d0e3af2ccb49)
[ $CARCH = x86_64 ] && md5sums+=(8a5fbb413d0b0d2971d203d07ce96901)

package() {
  echo -e "Creating the Package\n  Please wait..."

  mkdir -p $pkgdir$instdir
  chmod 0755 $srcdir/$installer
  echo -e "\n\n\n\n\n\n\n\n\n\n\n\n\ny\n\n\n$pkgdir$instdir\n\n\ni\ni\nn\nn\nn\n\n" >inst_input

  ORIGINAL_LD=$LD_LIBRARY_PATH
  [ $CARCH = x86_64 ] && LD_LIBRARY_PATH=$srcdir/usr/lib/libfakeroot:\$LD_LIBRARY_PATH
  $srcdir/$installer --mode text <inst_input &>/dev/null || true
  LD_LIBRARY_PATH=$ORIGINAL_LD

  rm -r inst_input $pkgdir$instdir/{Uninstall*,bin/{roam.lic,xclm},etc,rollbackBackupDirectory} || true

  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  ln -s $instdir/MPLAB_XC16_Compiler_License.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
