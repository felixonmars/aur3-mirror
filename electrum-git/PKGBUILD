# Maintainer : RunningDroid <runningdroid AT zoho.com>
# Contributor: Samuel Tardieu <sam@rfc1149.net>
# Contributor: Justin Gottula <justin@jgottula.com>

pkgname=electrum-git
pkgver=1.9.6.21.gc0ba368
pkgrel=1
pkgdesc="Python-Based Bitcoin Client"
arch=('any')
url="http://electrum-desktop.com/"
license=('GPL3')
depends=('gettext' 'hicolor-icon-theme' 'python2-pyqt4' 'python2-ecdsa' 'python2-slowaes')
makedepends=('python2-pycurl'
             'git')
optdepends=('python2-zbar: QRcode reading support'
            'desktop-file-utils')
provides=('electrum')
conflicts=('electrum')
install='electrum-git.install'
source=('git+https://github.com/spesmilo/electrum.git#branch=master'
        'electrum.png'
        'electrum-git.install')
sha1sums=('SKIP'
          'cd2427ce24b6de8009641d4ead8f4c8ff83de386'
          '9857c880b461e4a3573e2ccf4ca5b2030b1e2ddc')

_gitname='electrum'

pkgver() {
  cd $_gitname
  echo $(git describe --always | sed 's/-/./g')
}

build() {
  cd $_gitname

  sed -i -e 's|^#!/usr/bin/env pythoni$|#!/usr/bin/env python2|' \
      -e 's|^#!/usr/bin/python$|#!/usr/bin/python2|' \
      $(find . -type f)

  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname"
  python2 setup.py install --root=$pkgdir

  cd "$pkgdir"
  mv home/*/.local/share/applications usr/share
  mv home/*/.local/share/locale usr/share
  rm -rf home

  install -Dm755 "${srcdir}"/electrum.png \
      usr/share/icons/hicolor/128x128/apps/electrum.png

}
