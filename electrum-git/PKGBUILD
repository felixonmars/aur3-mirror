# Maintainer: Samuel Tardieu <sam@rfc1149.net>
# Contributor: Justin Gottula <justin@jgottula.com>
pkgname=electrum-git
pkgver=20120815
pkgrel=3
pkgdesc="Python-Based Bitcoin Client"
arch=('any')
url="http://electrum-desktop.com/"
license=('GPL3')
groups=()
depends=('python2' 'qt' 'python2-pyqt' 'python2-ecdsa' 'python2-slowaes')
makedepends=('git' 'gcc' 'make')
optdepends=('python2-zbar: QRcode reading support')
provides=('electrum')
conflicts=('electrum')
replaces=()
backup=()
options=()
install=
source=()
noextract=()

_gitroot='git://github.com/spesmilo/electrum.git'
_gitname='electrum'

# change this if you'd like to build a different branch
_gitbranch='master'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin $_gitbranch
    msg "The local files are updated."
  else
    git clone -b $_gitbranch "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  sed -ie 's#/usr/bin/env python#/usr/bin/env python2#' lib/*.py

  python2 mki18n.py
  pyrcc4 icons.qrc -o lib/icons_rc.py
  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname-build"
  python2 setup.py install --root=$pkgdir
}
