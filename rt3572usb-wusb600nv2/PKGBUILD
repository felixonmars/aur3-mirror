# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>

## 23/05/11: updated following the instructions of this post:
## http://ubuntuforums.org/showthread.php?t=1690402
pkgname=rt3572usb-wusb600nv2
pkgver=2.5.0.0
pkgrel=12
__date=2011_0427
pkgdesc='Linksys WUSB600Nv2 wireless adapter driver'
arch=('i686' 'x86_64')
url='http://www.ralinktech.com/en/04_support/support.php?sn=501'
license=('GPL' 'GPL3' 'custom:ralink-firmware')
backup=('etc/Wireless/RT2870STA/RT2870STA.dat')
source=(http://mirror.thebasementserver.com/soft/ralink/linux/2011_0427_RT3572_Linux_STA_v${pkgver}.DPO.bz2
        patch.config
        patch.wusb600nv2
        rt2870-firmware-update.pl
        rt3572sta.conf)

md5sums=('6a857c9d74987c7f3fa61b15ade179cc'
         '405bb4acfcca2372a5317a2c69d153ea'
         '88a73f32b6246860d43f2dc70f7fcbb9'
         'ccff67ac30e1334a42a30721884662d5'
         'e72148b7fd853a31386d5572575eb348')

depends=('linux-firmware' 'perl-libwww' 'perl-archive-zip' 'usbutils')
makedepends=('linux-headers')
install=rt3572usb-wusb600nv2.install

build() {
    cd ${__date}_RT3572_Linux_STA_v${pkgver}.DPO/os/linux
    patch -Np0 -i ../../../../patch.config 
    cd ../../common/
    patch -Np0 -i ../../../patch.wusb600nv2
    cd ..
    sed -i -e 's#usb_buffer_alloc#usb_alloc_coherent#g' \
           -e 's#usb_buffer_free#usb_free_coherent#g' \
           include/os/rt_linux.h
    make
}

package() {
   install -D -m 755 rt2870-firmware-update.pl ${pkgdir}/usr/bin/rt2870-firmware-update
   install -D -m 644 rt3572sta.conf ${pkgdir}/etc/modprobe.d/rt3572sta.conf
   cd ${__date}_RT3572_Linux_STA_v${pkgver}.DPO
   install -D -m 755 os/linux/rt3572sta.ko ${pkgdir}/lib/modules/$(uname -r)/kernel/drivers/net/wireless/rt3572sta.ko
   install -D -m 644 RT2870STA.dat ${pkgdir}/etc/Wireless/RT2870STA/RT2870STA.dat
   install -D -m 644 RT2870STACard.dat ${pkgdir}/etc/Wireless/RT2870STA/RT2870STACard.dat
   install -D -m 644 LICENSE\ ralink-firmware.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.ralink-firmware.txt
}

