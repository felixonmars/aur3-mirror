# Maintainer: Det
# Contributors: The Ringmaster, Christian Berendt, Balwinder S "bsd" Dheeman, thotypous
# Based on virtualbox-bin and virtualbox-ext-oracle

pkgname=virtualbox-beta-bin
true && pkgname=("$pkgname" 'virtualbox-ext-oracle-beta')
pkgver=4.1.12
_build=77245
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL2')
options=('!strip')
_arch='x86'
[ "$CARCH" = 'x86_64' ] && _arch='amd64'
source=("http://dlc.sun.com.edgesuite.net/virtualbox/$pkgver/VirtualBox-$pkgver-$_build-Linux_$_arch.run"
        "http://dlc.sun.com.edgesuite.net/virtualbox/$pkgver/Oracle_VM_VirtualBox_Extension_Pack-$pkgver-$_build.vbox-extpack"
        '10-vboxdrv.rules'
        'vboxdrv'
        'vboxdrv.conf'
        'vboxweb'
        'vboxweb.conf'
        'PUEL')
md5sums=(`curl -Ls ${source/V*}MD5SUMS | grep $_arch.run | cut -d " " -f1` # VirtualBox-$pkgver-$_build-Linux_$_arch.run
         `curl -Ls ${source/V*}MD5SUMS | grep -m1 VM | cut -d " " -f1` # Oracle_VM_VirtualBox_Extension_Pack-$pkgver-$_build.vbox-extpack
         'fe60f9510502bea67383d9198ae8c13c'  # 10-vboxdrv.rules
         'e7a94c97b1b1e1e843c1bb85181d2de8'  # vboxdrv
         '951bf4a5524e919fc4aaee6f46041e3d'  # vboxdrv.conf
         'c159d683ba1947290fc2ad2c64194150'  # vboxweb
         '3ac185709bfe688bb753c46e170d0546'  # vboxweb.conf
         '08b28b82d0ebd6962025100d4b5414a1') # PUEL

package_virtualbox-beta-bin() {
  pkgdesc="A powerful x86 virtualizer - Bleeding edge Personal Use Edition"
  depends=('fontconfig' 'gcc' 'libgl' 'libidl2' 'libxcursor' 'libxinerama' 'libxmu' 'linux-headers' 'python2' 'sdl')
  optdepends=('virtualbox-ext-oracle-beta: for Oracle extensions'
              'dkms: for handling kernel modules with dkms')
  provides=("virtualbox=$pkgver")
  conflicts=('virtualbox' 'virtualbox-modules')
  backup=('etc/vbox/vbox.cfg' 'etc/conf.d/vboxdrv' 'etc/conf.d/vboxweb')
  install=$pkgname.install
  _installdir=/opt/VirtualBox

  msg2 "Unpacking the .run package"
  echo yes | sh VirtualBox-$pkgver-$_build-Linux_$_arch.run --target . --nox11 --noexec &> /dev/null

  msg2 "Creating required dirs"
  mkdir -p "$pkgdir"/{$_installdir,etc/{vbox,{rc,conf}.d},lib/udev/rules.d,usr/{bin,src,share/{applications,doc/$pkgname,mime/packages,pixmaps}},var/run/VirtualBox}

  msg2 "Extracting VirtualBox.tar.bz2"
  cd "$pkgdir/$_installdir"
  tar -xjf "$srcdir/VirtualBox.tar.bz2"

  msg2 "Hardened build: Mark binaries suid root, create symlinks for working around
                     unsupported $ORIGIN/.. in VBoxC.so and make sure the
                     directory is only writable by the user"
  chmod 4511 VirtualBox VBox{SDL,Headless,NetDHCP,NetAdpCtl}
  for _lib in VBox{VMM,REM,RT,DDU,XPCOM}.so; do
    ln -sf $_installdir/$_lib components/$_lib
  done
  chmod go-w .

  msg2 "Patching 'vboxshell.py' for Python 2"
  sed -i 's#/usr/bin/python#\02#' vboxshell.py

  msg2 "Fixing VBox.sh according to Arch's initscripts"
  sed -i -e 's,/etc/init.d/,/etc/rc.d/,g' VBox.sh

  msg2 "Installing the scripts and confs"
  cd "$srcdir"
  install -m755 vbox{drv,web} "$pkgdir/etc/rc.d/"
  install -m644 vboxdrv.conf "$pkgdir/etc/conf.d/vboxdrv"
  install -m644 vboxweb.conf "$pkgdir/etc/conf.d/vboxweb"

  msg2 "Installing the udev rules"
  install -m644 10-vboxdrv.rules "$pkgdir/lib/udev/rules.d/"
  ln -s "$_installdir/VBoxCreateUSBNode.sh" "$pkgdir/lib/udev/"

  msg2 "Installing the SDK"
  cd "$pkgdir/$_installdir/sdk/installer"
  VBOX_INSTALL_PATH=$_installdir python2 vboxapisetup.py install --root "$pkgdir"
  rm -r -f build
  cd -

  msg2 "Symlinking the launchers" # 2nd can fail, if fs not case sensitive
  for _bin in VirtualBox VBox{Headless,Manage,SDL,SVC,Tunctl,NetAdpCtl} rdesktop-vrdp; do
    ln -s $_installdir/$_bin "$pkgdir/usr/bin/$_bin"
    ln -s $_installdir/$_bin "$pkgdir/usr/bin/${_bin,,}" &>/dev/null || :
  done

  msg2 "Symlinking the desktop icon, mime info, doc, module sources and .desktop files"
  ln -s $_installdir/{VBox,icons/128x128/virtualbox}.png "$pkgdir/usr/share/pixmaps/"
  ln -s $_installdir/virtualbox.desktop "$pkgdir/usr/share/applications/"
  ln -s $_installdir/virtualbox.xml "$pkgdir/usr/share/mime/packages/"
  ln -s $_installdir/VirtualBox.chm "$pkgdir/usr/share/doc/$pkgname/"
  ln -s $_installdir/src/vboxhost "$pkgdir/usr/src/vboxhost-$pkgver"

  msg2 "Symlinking the icons"
  cd "$pkgdir/$_installdir/icons"
  for _dir in *; do
    cd "$_dir"
    mkdir -p "$pkgdir/usr/share/icons/hicolor/$_dir/"{apps,mimetypes}
    for _icon in *; do
      if [[ "$_icon" = 'virtualbox.png' ]]; then
          ln -s $_installdir/icons/$_dir/$_icon "$pkgdir/usr/share/icons/hicolor/$_dir/apps/"
      else
          ln -s $_installdir/icons/$_dir/$_icon "$pkgdir/usr/share/icons/hicolor/$_dir/mimetypes/"
      fi
    done
    cd - >/dev/null
  done

  msg2 "Writing the configuration file"
  echo "# VirtualBox installation directory
INSTALL_DIR=$_installdir
# VirtualBox version
INSTALL_VER=$pkgver
INSTALL_REV=$_build" >> "$pkgdir/etc/vbox/vbox.cfg"
}

package_virtualbox-ext-oracle-beta() {
  pkgdesc="An extension pack for VirtualBox - Bleeding edge"
  arch=('any')
  depends=("virtualbox-beta-bin=$pkgver")
  license=('custom:PUEL')
  install="$pkgname.install"

  msg2 "Installing the extension pack and license"
  install -Dm644 Oracle_VM_VirtualBox_Extension_Pack-$pkgver-$_build.vbox-extpack "$pkgdir/usr/share/virtualbox/extensions/Oracle_VM_VirtualBox_Extension_Pack-$pkgver.vbox-extpack"
  install -Dm644 PUEL "$pkgdir/usr/share/licenses/$pkgname/PUEL"
}

pkgdesc="A powerful x86 virtualizer - bleeding edge Personal Use Edition + Extension Pack"
depends=('libidl2' 'libxcursor' 'libxinerama' 'libxslt' 'curl' 'linux-headers' 'python2')
license=('GPL2' 'custom:PUEL')

# vim:set ts=2 sw=2 ft=sh et: