# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=guvcview
pkgver=1.6.0
pkgrel=2
pkgdesc="A video viewer and capturer for the linux uvc driver" 
arch=('i686' 'x86_64')
url="http://guvcview.sourceforge.net/"
license=('GPL')
depends=('portaudio' 'gtk3' 'ffmpeg' 'v4l-utils' 'udev')
makedepends=('pkg-config' 'intltool')
conflicts=('guvcview-svn')
optdepends=('pulseaudio: for PulseAudio support')
source=(
	"http://downloads.sourceforge.net/project/$pkgname/source/$pkgname-src-$pkgver.tar.gz"
	'undefined-vars-in-vcodecs.c.patch'
)
md5sums=(
	'a8c9b1f1267fbf9982266b5f36d87b2b'
	'125266620efb5b573d47eb11414a542a'
)

build() {
	cd "$srcdir/$pkgname-src-$pkgver"
	patch -p1 < "$srcdir/undefined-vars-in-vcodecs.c.patch"
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-src-$pkgver"
	make DESTDIR="$pkgdir" install
}
