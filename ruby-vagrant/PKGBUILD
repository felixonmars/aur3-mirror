# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Mathieu Clabaut <mathieu.clabaut@gmail.com>
# Contributor: helios <aur@wiresphere.de>
_name=vagrant
pkgname=ruby-$_name
pkgver=1.3.5
pkgrel=1
pkgdesc="Tool for building and distributing virtualized development environments"
arch=('i686' 'x86_64')
url='http://vagrantup.com/'
license=('MIT')
depends=('ruby' 'virtualbox>=4.0' 'ruby-net-ssh>=2.6.6' \
         'ruby-net-scp>=1.1.0' 'ruby-erubis>=2.7.0' 'ruby-i18n>=0.6.0' \
         'ruby-log4r>=1.1.9' 'ruby-childprocess>=0.3.7')
makedepends=('git' 'rubygems' 'rake' 'ruby-bundler')
provides=('vagrant')
conflicts=('vagrant')
options=(!strip)
source=("https://github.com/mitchellh/${_name}/archive/v${pkgver}.tar.gz")
md5sums=('662ad831b87afce794816380e3373699')

build() {
  cd "${srcdir}/${_name}-${pkgver}"

  bundle install --path=bundle
  bundle exec rake build
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --no-user-install --ignore-dependencies \
    -i "${pkgdir}${_gemdir}" -n "${pkgdir}"/usr/bin "pkg/${_name}-${pkgver}.gem"
}

# vim:set ts=2 sw=2 et:
