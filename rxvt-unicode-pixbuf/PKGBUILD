# Maintainer: Dirk Sohler <dirk@archlinux.de>
# vim: ts=4:sw=4

pkgname=rxvt-unicode-pixbuf
_dlname=rxvt-unicode
pkgver=9.20
pkgrel=2
pkgdesc='urxvt with pixbuf (custom icons, custom background) enabled (see FS36113, FS35329 and FS34862)'

url='http://software.schmorp.de/pkg/rxvt-unicode.html'
arch=('i686' 'x86_64')
license=('GPL')

depends=('gdk-pixbuf2' 'libxft' 'perl' 'startup-notification')
makedepends=('libxft' 'perl' 'startup-notification')

optdepends=('gtk2-perl: for using the tabbed interface')

conflicts=('rxvt-unicode' 'rxvt-unicode-terminfo')
provides=('rxvt-unicode' 'rxvt-unicode-terminfo')

source=("http://dist.schmorp.de/rxvt-unicode/$_dlname-$pkgver.tar.bz2"
		'urxvt.desktop'
		'urxvtc.desktop'
		'urxvt-tabbed.desktop')

sha256sums=('e73e13fe64b59fd3c8e6e20c00f149d388741f141b8155e4700d3ed40aa94b4e'
			'5f9c435d559371216d1c5b49c6ec44bfdb786b12d925d543c286b0764dea0319'
			'91536bb27c6504d6cb0d33775a0c4709a4b439670b900f0c278c25037f19ad66'
			'ccd7c436e959bdc9ab4f15801a67c695b382565b31d8c352254362e67412afcb')

build() {
	cd $_dlname-$pkgver
	./configure \
		--prefix=/usr \
		--with-terminfo=/usr/share/terminfo \
		--enable-256-color \
		--enable-combining \
		--enable-fading \
		--enable-font-styles \
		--enable-iso14755 \
		--enable-keepscrolling \
		--enable-lastlog \
		--enable-mousewheel \
		--enable-next-scroll \
		--enable-perl \
		--enable-pointer-blank \
		--enable-rxvt-scroll \
		--enable-selectionscrolling \
		--enable-slipwheeling \
		--enable-startup-notification \
		--enable-transparency \
		--enable-unicode3 \
		--enable-utmp \
		--enable-wtmp \
		--enable-xft \
		--enable-xim \
		--enable-xterm-scroll \
		--enable-pixbuf \
		--disable-smart-resize \
		--disable-frills
	make
}

package() {
	# Install desktop files
	_a='usr/share/applications'
	install -Dm644 urxvt.desktop "$pkgdir/$_a/urxvt.desktop"
	install -Dm644 urxvtc.desktop "$pkgdir/$_a/urxvtc.desktop"
	install -Dm644 urxvt-tabbed.desktop "$pkgdir/$_a/urxvt-tabbed.desktop"

	# Install urxvt with terminfo
	cd $_dlname-$pkgver
	export TERMINFO="$pkgdir/usr/share/terminfo"
	install -d "$TERMINFO"
	make DESTDIR="$pkgdir" install

	# For tabbed version with gtk2-perl
	sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
	install -Dm 755 doc/rxvt-tabbed "$pkgdir/usr/bin/urxvt-tabbed"
}
