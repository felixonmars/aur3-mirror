# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
pkgver=v1.0.r143.g1790d07
pkgname=erlexec-git
pkgrel=1
arch=('i686' 'x86_64')
license=('BSD')
pkgdesc="Execute and control OS processes from Erlang VM"
url="http://saleyn.github.com/erlexec/"
depends=('erlang-nox')
makedepends=('git')
source=('git+https://github.com/saleyn/erlexec.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"
  mkdir -m755 -p $pkgdir/usr/lib/erlang/lib/${pkgname%-git}
  tar cf - . \
    --exclude-vcs-ignores --exclude-vcs \
    --exclude="LICENSE" --exclude="TODO" --exclude="AUTHORS" \
    --exclude="core*" --exclude="erl_crash.dump" \
    --exclude="*.tgz" --exclude="*.swp" --exclude="c_src" \
    --exclude="Makefile" --exclude="rebar.*" --exclude="*.mk" \
    --exclude="*.o" \
    ${pkgname%-git} \
  | tar xf - -C $pkgdir/usr/lib/erlang/lib/${pkgname%-git}
}
