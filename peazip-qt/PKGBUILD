# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: TuxSpirit <tuxspirit@archlinux.fr>

_pkgname=peazip
pkgname=$_pkgname-qt
pkgver=4.7.1
pkgrel=2
pkgdesc="QT archiver utility"
arch=(i686 x86_64)
url=http://www.peazip.org/peazip-linux.html
license=(LGPL)
depends=(balz gdk-pixbuf2 p7zip quad unace upx)
if [[ $CARCH = "i686" ]]; then
    depends+=(gmp4 qt4pas)
else
    depends+=(lib32-gmp4 lib32-qt4pas)
fi
provides=($_pkgname)
conflicts=($_pkgname-portable-gtk1 $_pkgname-beta-{gtk2,qt} $_pkgname-gtk2)
replaces=($_pkgname-{beta,portable,portable-beta}-qt)
install=$_pkgname.install
source=($pkgname.tgz::http://downloads.sourceforge.net/project/$_pkgname/$pkgver/${_pkgname}-$pkgver.LINUX.Qt.tgz $_pkgname.desktop)
noextract=($pkgname.tgz)
sha256sums=('cd5e979a1dffbef3f2c908ac8378f07e0fa5ff28e6086bf64c407ad0e370bd8e'
    'c3ca0fa3e89626447acc376e2392fdd4e452c2b8f0685324eb978ab63c217fce')
sha512sums=('6585cd77425dba80e24db775aca0ed3a49902bef51e9ee34149ee8df5152f33dda7e4634493ebd9a4dda8e9f0c64d74e3faeeac4bd2052da7c5b201039931a4d'
    '0fb2e432f3b403df55134ebd57aacd2fb1eea75ef96520a5e4d7827ec319ca16a45c6e147504af0472704872fc601273b38d8f45ebfb8f68328266d240a1f71f')

_resdir="$pkgdir"/usr/lib/$_pkgname/res

package() {
    install -d "$srcdir"/$pkgname/
    cd "$srcdir"/$pkgname/
    tar xf ../$pkgname.tgz -C ./
    cd usr/local/share/PeaZip/res/

    install -Dm755 ../$_pkgname "$pkgdir"/usr/lib/$_pkgname/$_pkgname

    for i in pea pealauncher rnd zpaq/{zpaq,lzppre}; do
        install -Dm755 $i "$_resdir"/$i
    done
    for i in arc/{arc,*.sfx}; do
        install -Dm755 $i "$_resdir"/$i
    done
    for i in altconf.txt lang/* themes/{{nographic,seven}-embedded/*,*.7z} zpaq/{min,max,mid}.cfg arc/arc.{ini,groups}; do
        install -Dm644 $i "$_resdir"/$i
    done

    install -d "$_resdir"/7z/Codecs/
    for i in 7z{,a,r,.so,Con.sfx,.sfx} Codecs/Rar29.so; do
        ln -sf /usr/lib/p7zip/$i "$_resdir"/7z/$i
    done
    for i in quad/{balz,quad} unace/unace upx/upx lpaq/lpaq8 paq/paq8o; do
        install -d "$_resdir"/$(dirname $i)/
        ln -sf /usr/bin/$(basename $i) "$_resdir"/$i
    done
    install -d "$pkgdir"/usr/bin/
    for i in /usr/lib/$_pkgname/{$_pkgname,res/pea,res/pealauncher}; do
        ln -sf $i "$pkgdir"/usr/bin/$(basename $i)
    done

    install -Dm644 "$srcdir"/$pkgname/usr/local/share/icons/$_pkgname.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png
    desktop-file-install "$srcdir"/$_pkgname.desktop --dir "$pkgdir"/usr/share/applications/
}
