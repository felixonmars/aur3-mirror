# Maintainer: flan_suse

# This suite contains the following:
#        * xfwm4 theme
#        * metacity theme
#        * gtk2 theme
#        * gtk3 theme
#        * wallpaper

pkgname=xfce-theme-bluebird
_pkgname=Bluebird
pkgver=0.6
pkgrel=1
_gitdir=Bluebird
_gitbranch=master
_giturl="git://github.com/shimmerproject/$_gitdir.git"
pkgdesc="A light blue Xfce theme, introduced in the release of Xubuntu 10.10"
arch=('any')
url="http://shimmerproject.org/projects/bluebird/"
license=('GPL3' 'CCPL')
groups=('xfce-themes-shimmer-collection')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('elementary-xfce-icons: matching icon set, use dark icon theme'
            'gtk-engine-unico: required for gtk3 support')

build() {

    # Change into source directory
    cd "${srcdir}"

    # Clone or pull from GIT server, and notify user of the status
    msg "Connecting to GIT server...."
    if [[ -d $_gitdir ]] ; then
      cd "$_gitdir" && git pull origin "$_gitbranch"
      msg "The local files are updated."
    else
      git clone "$_giturl" "$_gitdir"
      cd "$_gitdir"
    fi

    # Checkout current git-tag stable release
    # Comment out the next line if you wish to use the master branch instead; not recommended
    git checkout "v$pkgver"
    echo "$_pkgname version $pkgver is ready to be packaged!"

}

package() {
    
    # Change into source directory
    cd "${srcdir}" 

    # Create installation directories
    install -d -m755 "${pkgdir}/usr/share/themes/"
    install -d -m755 "${pkgdir}/usr/share/backgrounds/"
    install -d -m755 "${pkgdir}/usr/share/xfce4/backdrops/"

    # Copy the wallpaper to the correct directory, then remove the backdrops folder
    cp -f "$_pkgname/backdrops/bluebird-2010-08-1920x1440-notext.png" "${pkgdir}/usr/share/backgrounds/$_pkgname.png"

    # Install the rest of the theme
    cp -rf "$_pkgname" "${pkgdir}/usr/share/themes/"

    # Make a symbolic link inside of the Xfce backdrops directory, for use with Desktop Settings
    cd "${pkgdir}/usr/share/xfce4/backdrops/"
    ln -s "../../backgrounds/$_pkgname.png" "$_pkgname.png"

    # Remove excess files
    rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/.gitignore"
    rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/.git"
    rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/backdrops"
    rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/icons"

}


