# Maintainer: Aay Jay Chan <aayjaychan at gmail dot com>

pkgname=gnatcoll
pkgver=2013
pkgrel=1
pkgdesc="GNAT GPL release of the GNAT reusable components for Ada"
arch=('i686' 'x86_64')
url="http://libre.adacore.com/tools/gnat-component-collection/"
license=('GPL3')
depends=(
    'gcc-libs'
)
optdepends=(
    'gcc-ada: GNAT Ada compiler'
    'gprbuild: GNAT project files support'
    'gtkada: binding to GUI toolkit GTK+'
    'sqlite: SQLite database backend'
    'postgresql-libs: PostgreSQL database backend'
    'python2: gnatcoll_db2ada database abstraction tool'
)
makedepends=(
    'gnat_util'
    'gprbuild'
    'python2'
)
changelog="ChangeLog"
source=(
    "$pkgname-gpl-$pkgver-src.tgz::http://mirrors.cdn.adacore.com/art/778d32c0fd12c3deaa9b66842bfb02e49743685a"
    "$pkgname.patch"
)
sha256sums=(
    '9fcaa6f4abcd6d27fdbfb14f0404a83de7724e68ba3ab496bc6fd88da87eb68f'
    'c5d6605b8b6d6b6ecf97119c42706c310da89cac7e2493f79b0b46c09cbb2494'
)

prepare() {
    cd "$srcdir/$pkgname-gpl-$pkgver-src"
    patch -p1 -i "$srcdir/$pkgname.patch"
}

build() {
    if  [ ! -n "$ADA_PROJECT_PATH" ] || [ $ADA_PROJECT_PATH != "*/usr/lib/gnat*" ] ; then
        echo 'Adding /usr/lib/gnat to ADA_PROJECT_PATH'
        export ADA_PROJECT_PATH="/usr/lib/gnat:$ADA_PROJECT_PATH"
    fi
    cd "$srcdir/$pkgname-gpl-$pkgver-src"
    ./configure --prefix="/usr" --with-python-exec="python3" --disable-shared
    make
}

#check() {
#    export PATH="$srcdir/$pkgname-gpl-$pkgver-src/local_install/bin:$PATH"
#    cd "$srcdir/$pkgname-gpl-$pkgver-src"
#    make test_verbose
#}

package() {
    cd "$srcdir/$pkgname-gpl-$pkgver-src"
    make install DESTDIR="$pkgdir/"
}