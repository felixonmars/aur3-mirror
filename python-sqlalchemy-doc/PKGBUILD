# $Id: PKGBUILD 48930 2011-06-07 18:55:04Z angvp $ 
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
pkgname="python-sqlalchemy-doc"
pkgver=0.7.9
pkgdesc="Python SQL toolkit and Object Relational Mapper"
pkgrel=1
arch=('any')
url="http://www.sqlalchemy.org/"
license=('MIT')
makedepends=('python-sphinx' 'python-mako')
source=(http://downloads.sourceforge.net/sourceforge/sqlalchemy/SQLAlchemy-$pkgver.tar.gz)
md5sums=('c4852d586d95a59fbc9358f4467875d5')

build() {
  cd "${srcdir}/SQLAlchemy-${pkgver}"
  sed -i -r "s@^(templates_path = \[')templates@\1doc/build/templates@" doc/build/conf.py
  python2 sa2to3.py -w lib
  cd doc/build
  make html
}

package() {
  cd "${srcdir}/SQLAlchemy-${pkgver}"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  while read -d ''; do
    install -Dm644 "$REPLY" "$pkgdir/usr/share/doc/$pkgname/${REPLY#build/sphinx/html/}"
  done < <(find doc/build/output/html -type f -print0)
}
