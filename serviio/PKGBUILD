# Contributor: Casper Bruun <casperbruun@gmail.com>

pkgname=serviio
pkgver=1.3
pkgrel=1
pkgdesc="Free DLNA media server"
arch=('any')
url="http://www.serviio.org/"
license=('custom')
depends=('java-environment' 'ffmpeg')
source=(http://download.serviio.org/releases/serviio-${pkgver}-linux.tar.gz
        $pkgname.sh
        $pkgname-console.sh
        $pkgname.service)
md5sums=('e8a30c35a31b67b96333f1d53a6d0570'
         '09a31027c559dd729ffbfa7aa50318e1'
         '81ffa70910e220574f0423834de99159'
         'e958d9812448f97fe325dfffe8b44620')

package() {
  cd "$pkgname-$pkgver"

  # define variable for Serviio home directory
  SERVIIO_HOME=$pkgdir/usr/share/java/$pkgname

  # create the folders needed in the main directory
  install -dm755 ${SERVIIO_HOME}/{config,lib,library,plugins}

  # place the serviio scripts in the /usr/bin folder
  install -Dm755 ${srcdir}/$pkgname.sh $pkgdir/usr/bin/$pkgname
  install -Dm755 ${srcdir}/$pkgname-console.sh $pkgdir/usr/bin/$pkgname-console

  # copy xml config files to the config folder
  install -m644 config/*.xml ${SERVIIO_HOME}/config

  # copy all jar files to the lib folder
  install -m644 lib/*.jar ${SERVIIO_HOME}/lib

  # copy derby.properties to library folder
  install -m644 library/derby.properties ${SERVIIO_HOME}/library

  # copy plugins-readme.txt to plugins folder
  install -m644 plugins/plugins-readme.txt ${SERVIIO_HOME}/plugins

  # create the log folder in /var/log
  install -dm755 $pkgdir/var/log/$pkgname

  # create a symlink that puts all log files in /var/log
  ln -s /var/log/$pkgname ${SERVIIO_HOME}/log

  # supply the license file
  install -Dm644 LICENCE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE

  # install systemd-service
  install -Dm755 "${srcdir}/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}
