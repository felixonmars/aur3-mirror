#Contributor: Casper Bruun <casperbruun@gmail.com>
#Maintainer: Bjoern Franke <bjo@nord-west.org>

pkgname=serviio
pkgver=1.0.1
pkgrel=1
pkgdesc="Free DLNA media server"
arch=(any)
url="http://www.serviio.org/"
license=(Proprietary)
depends=('java-environment' 'ffmpeg')
source=(http://download.serviio.org/releases/serviio-${pkgver}-linux.tar.gz
        ${pkgname}.sh
        ${pkgname}-console.sh
        ${pkgname}d
	${pkgname}.service)
md5sums=('a6b193711093b3a6c2349e4266f1ea36'
         'ee81eec5db0ea7d3171c29f75f246949'
         '9724819b0b627cc85e2eb478ad13de3b'
         'd96a82157c2910f1041ad3f0af279e25'
	 'e958d9812448f97fe325dfffe8b44620')

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    
    # define variable for Serviio home directory
    SERVIIO_HOME=${pkgdir}/usr/share/java/${pkgname}
    
    # create the folders needed in the main directory
    install -dm755 ${SERVIIO_HOME}/{config,lib,library,plugins}
    # ... and the bin folder
    install -dm755 ${pkgdir}/usr/bin
    
    # place the serviio scripts in the /usr/bin folder
    install -m755 ${srcdir}/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
    install -m755 ${srcdir}/${pkgname}-console.sh ${pkgdir}/usr/bin/${pkgname}-console
    
    # copy xml config files to the config folder
    install -m644 config/*.xml ${SERVIIO_HOME}/config
    
    # copy all jar files to the lib folder
    install -m644 lib/*.jar ${SERVIIO_HOME}/lib
    
    # copy derby.properties to library folder
    install -m644 library/derby.properties ${SERVIIO_HOME}/library
    
    # copy plugins-readme.txt to plugins folder
    install -m644 plugins/plugins-readme.txt ${SERVIIO_HOME}/plugins
    
    # create the log folder in /var/log
    install -dm755 ${pkgdir}/var/log/${pkgname}
    
    # create a symlink that puts all log files in /var/log
    ln -s /var/log/${pkgname} ${SERVIIO_HOME}/log
    
    # supply the license file
    install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
    install -m644 LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

    # install rc.d-script
    install -Dm755 "${srcdir}/${pkgname}d" "${pkgdir}/etc/rc.d/${pkgname}d"
    
    # install systemd-service
    install -Dm755 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

}

