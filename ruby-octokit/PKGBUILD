# Maintainer: Chris Fordham <chris at fordham-nagy dot id dot au>

_gemname=octokit
pkgname=ruby-$_gemname
pkgver=2.7.0
pkgrel=1
pkgdesc="Simple wrapper for the GitHub API"
arch=(any)
url="https://github.com/octokit/octokit.rb"
license=('MIT')
depends=(ruby-sawyer) # Full dependency information is available in the yaml specification
makedepends=(rubygems)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
md5sums=('f1d72892dd7dec66a66258d8156076b7')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"
}

# vim:set ts=2 sw=2 et:
