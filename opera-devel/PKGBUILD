# Maintainer: ruario 
# Contributor: RobertMe
# Contributor: BlackEagle
# Contributor: Skunnyk
# Contributor: totoloco
pkgname=opera-devel # Set to opera if you want to install as your main/stable build
_bigrelease=11.10
_buildver=2042
_randomizer=trolls
pkgver=${_bigrelease}_${_buildver}
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. Alpha version."
url="http://my.opera.com/desktopteam/blog/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
provides=('opera')
optdepends=('gtk2: GTK integration'
	    'kdebase-runtime: KDE4 integration'
	    'gstreamer0.10-base-plugins: HTML5 open codecs support'
	    'gstreamer0.10-good: HTML5 open codecs support'
	    'gstreamer0.10-ffmpeg: HTML5 not so open codecs support'
	    'gstreamer0.10-bad-plugins: HTML5 not so open codecs support')
install=opera-devel.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386
[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://snapshot.opera.com/unix/${_randomizer}_${_bigrelease}-${_buildver}/opera-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz)
md5sums=('0412f200aac2f4c9d89a7d1b18847757')
[ "$CARCH" = "x86_64" ] && md5sums=('fea09c5d107cd22daa33c3313b3b9b2f')

# Uncomment the following line, if you want your User Agent to include Arch Linux.
#_opdistro="Arch Linux"

package() {

	# Check if the $pkgname includes a suffix and if so set ${_suffix} for a suffix install.
	if echo $pkgname | grep -q "^opera-[a-zA-Z0-9]\{1,\}$"
	then
		_suffix="--suffix ${pkgname:6}"
	fi

	# 'Install' Opera into $pkgdir
	opera-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr ${_suffix} --repackage "${pkgdir}"/usr
	install -D -m 644 "${pkgdir}"/usr/share/${pkgname}/defaults/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt

	# Insert an Arch User Agent string if set
	if [ -n "${_opdistro}" ]
	then
		mkdir -p "${pkgdir}"/usr/share/${pkgname}/custom/defaults
		echo "[ISP]" > "${pkgdir}"/usr/share/${pkgname}/custom/defaults/operaprefs.ini
		echo "Id=${_opdistro}" >> "${pkgdir}"/usr/share/${pkgname}/custom/defaults/operaprefs.ini
		chmod 644 "${pkgdir}"/usr/share/${pkgname}/custom/defaults/operaprefs.ini
	fi
}
