# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-meteor-git
pkgver=109.17b051c
pkgrel=1
pkgdesc="libretro implementation of Meteor. (Game Boy Advance)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/meteor-libretro"
license=('GPL3')
makedepends=('git')

_gitname=meteor-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/meteor_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ${_gitname}/libretro
  make
}

package() {
  install -Dm644 "${_gitname}/libretro/meteor_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-meteor.so"
  install -Dm644 "meteor_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-meteor.info"
}
