# Puppet: Installer: Arch
# Maintainer: Luca Cesari <mirshann (at) bewq (dot) org>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>

pkgname=puppet-git
pkgver=20111004
pkgrel=1
pkgdesc="A system for automating system administration tasks."
arch=("any")
url="http://puppetlabs.com/projects/puppet/"
license=("GPL")
depends=("ruby" "ruby-shadow" "facter" "net-tools" "inetutils")
backup=("etc/puppet/puppet.conf")
options=(emptydirs)
install="puppet.install"
source=("puppet"
        "puppetmaster"
        "puppet.conf")
md5sums=('59dbf39e251bc4877e7604a5876c642d'
         '4c507c580cb28d7a5adc9ea6b3626657'
         'a6c0ce2eb12d62fdd3421aa9dff3477d')

_gitroot="https://github.com/puppetlabs/puppet.git"
_gitname="puppet.git"

build() {
    cd ${srcdir}
    msg "Connecting to git server..."

    if [ -d ${srcdir}/${_gitname} ]; then
        cd ${_gitname} && git pull origin
        msg "The local files are updated."
    else
        git clone ${_gitroot} ${_gitname}
    fi

    msg "GIT checkout done or server timeout"
}

package() {
  cd $srcdir/$_gitname

  # Install
  ruby ./install.rb \
    --destdir=$pkgdir \
    --bindir=/usr/bin \
    --sbindir=/usr/sbin \
    --mandir=/usr/share/man

  # Set up vim and emacs
  install -Dp -m0644 ext/vim/ftdetect/puppet.vim $pkgdir/usr/share/vim/vimfiles/ftdetect/puppet.vim
  install -Dp -m0644 ext/vim/syntax/puppet.vim $pkgdir/usr/share/vim/vimfiles/syntax/puppet.vim
  install -Dp -m0644 ext/emacs/puppet-mode.el $pkgdir/usr/share/emacs/site-lisp/puppet-mode.el
  install -Dp -m0644 ext/emacs/puppet-mode-init.el $pkgdir/usr/share/emacs/site-lisp/site-start.d/puppet-mode-init.el
  
  # Configuration and rc scripts
  install -d $pkgdir/etc/{puppet,rc.d}
  install -m 644 $srcdir/puppet.conf $pkgdir/etc/puppet/puppet.conf

  install -D $srcdir/puppet $pkgdir/etc/rc.d/puppet
  install -D $srcdir/puppetmaster $pkgdir/etc/rc.d/puppetmaster

  # Move pi to prevent file conflict
  mv $pkgdir/usr/bin/pi $pkgdir/usr/bin/puppet-pi
}
