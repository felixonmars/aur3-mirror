# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: jiribb <jiribb@gmail.com>
# Contributor: David Spicer <azleifel at googlemail dot com>
# Contributor: Andrew Brouwers
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: David Spicer <azleifel at googlemail dot com>

# This PKGBUILD uses a split package in AUR workaround!
# (See https://bugs.archlinux.org/task/16394#comment68053)

pkgbase=handbrake-svn
pkgname=handbrake-svn
true && pkgname=('handbrake-svn' 'handbrake-cli-svn')
pkgver=5096
pkgrel=1
arch=('i686' 'x86_64')
url="http://handbrake.fr/"
license=('GPL')
makedepends=('intltool' 'python2' 'gettext' 'subversion' 'wget' 'yasm')

_svntrunk=svn://svn.handbrake.fr/HandBrake/trunk
_svnmod=hb-trunk

build() {
  cd ${srcdir}

  msg "Connecting to ${_svnmod} SVN server...."
  if [ -d ${_svnmod}/.svn ]; then
    ( cd ${_svnmod} && svn up -r $pkgver )
  else
    svn co ${_svntrunk} --config-dir ./ -r $pkgver ${_svnmod}
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting the build..."

  # Copy source for the build
  [ -d ${srcdir}/${_svnmod}-build ] && rm -rf ${srcdir}/${_svnmod}-build
  cp -r ${srcdir}/${_svnmod} ${srcdir}/${_svnmod}-build

  # Create build-specific temporary directory
  # (ffmpeg needs a temporary directory that is not set noexec)
  [ -d ${srcdir}/tmp ] && rm -rf ${srcdir}/tmp
  install -d -m755 ${srcdir}/tmp

  # 
  # Build
  #

  cd ${srcdir}/${_svnmod}-build

  # Substitutions for python2
  sed -i -e 's|python $(srcdir)|python2 $(srcdir)|' gtk/src/Makefile.am
  sed -i -e '1c#! /usr/bin/python2' gtk/src/makedeps.py
  sed -i -e '1c#! /usr/bin/python2' gtk/src/create_resources.py
  sed -i -e '1c#! /usr/bin/python2' gtk/src/quotestring.py

  # Use --disable-gtk-update-checks because we don't want to download gui updates
  ./configure --force --verbose --prefix=/usr --disable-gtk-update-checks --launch-jobs=0

  cd ${srcdir}/${_svnmod}-build/build
  TMPDIR=${srcdir}/tmp make
}

package_handbrake-svn() {
  pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (GUI version)"
  optdepends=('gstreamer0.10-base-plugins: For Preview Window'
              'gstreamer0.10-bad-plugins: For Preview Window'
              'gstreamer0.10-good-plugins: For Preview Window'
              'gstreamer0.10-ugly-plugins: For Preview Window'
              'gstreamer0.10-ffmpeg: For Preview Window') 
  provides=('handbrake')
  conflicts=('handbrake')
  install=${pkgname}.install

  cd ${srcdir}/${_svnmod}-build/build
  make DESTDIR="${pkgdir}" install

  rm ${pkgdir}/usr/bin/HandBrakeCLI

  # Delete icon-theme.cache (is rebuilt on install)
  rm -f ${pkgdir}/usr/share/icons/hicolor/icon-theme.cache
}

package_handbrake-cli-svn() {
  pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (CLI version)"
  depends=('gcc-libs' 'libass' 'libsamplerate' 'libtheora' 'libxml2')
  provides=('handbrake-cli')
  conflicts=('handbrake-cli')

  install -D -m755 ${srcdir}/${_svnmod}-build/build/HandBrakeCLI ${pkgdir}/usr/bin/HandBrakeCLI

  # Remove build directories
  rm -rf ${srcdir}/tmp
  rm -rf ${srcdir}/${_svnmod}-build
}

# Global pkgdesc and depends are here so that they will be picked up by AUR
pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter"
depends=('dbus-glib' 'desktop-file-utils' 'gstreamer0.10-base' 'gtk2' 'hicolor-icon-theme' 'libass' 'libnotify' 'libsamplerate' 'libtheora' 'systemd-tools')

