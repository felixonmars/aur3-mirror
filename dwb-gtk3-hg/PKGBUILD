# Contributor: portix <portix@gmx.net>

pkgname=dwb-gtk3-hg
pkgver=1866
pkgrel=2
pkgdesc="A webkit web browser with vi-like keyboard shortcuts, latest hg-pull, GTK3-version" 
url="http://portix.bitbucket.org/dwb/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('webkitgtk3' 'desktop-file-utils')
provides=(dwb)
install=dwb.install
conflicts=('dwb' 'dwb-hg' 'dwb-gtk3')
makedepends=('mercurial' 'json-c')

_hgroot="https://bitbucket.org/portix"
_hgrepo="dwb"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [[ -d "$_hgrepo" ]]; then
    cd "$_hgrepo"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "$_hgroot" "$_hgrepo"
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  make GTK=3
}
package() {
  cd "${srcdir}/${_hgrepo}-build"
  make DESTDIR="${pkgdir}" install \
      BASHCOMPLETION=/usr/share/bash-completion/completions
}
