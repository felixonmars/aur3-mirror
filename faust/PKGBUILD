# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=faust
pkgver=0.9.46
pkgrel=3
pkgdesc="A compiled language for real-time audio signal processing."
arch=('i686' 'x86_64')
url="http://faust.grame.fr/"
license=('GPL')
depends=('ruby')
optdepends=('oscpack: osc support')
conflicts=('faust-cvs' 'faust-git' 'faust2-git')
source=("http://downloads.sourceforge.net/project/faudiostream/$pkgname-$pkgver.tar.gz")
md5sums=('fe76d8a5905547d62817631fda5d4204')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.7 fix
  sed -i "26i#include<unistd.h>" \
    compiler/parser/faustlexer.cpp \
    architecture/osclib/faust/src/nodes/RootNode.cpp

  make PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir/" install

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 documentation/*.{pdf,txt} \
    "$pkgdir/usr/share/doc/$pkgname"

  # fix paths in faust utils and Makefiles
  sed -i 's|/local||g' "$pkgdir/usr/bin/faust2"* \
                       "$pkgdir/usr/lib/faust/Makefile"*

  ## syntax highlighting files
  cd syntax-highlighting

  # kate
  install -Dm644 $pkgname.xml \
    "$pkgdir/usr/share/apps/katepart/syntax/$pkgname.xml"

  # gedit
  install -Dm644 $pkgname.lang \
    "$pkgdir/usr/share/gtksourceview-2.0/language-specs/$pkgname.lang"
  install -Dm644 $pkgname.lang \
    "$pkgdir/usr/share/gtksourceview-3.0/language-specs/$pkgname.lang"

  # highlight 
  install -Dm644 dsp.lang \
    "$pkgdir/usr/share/highlight/langDefs/dsp.lang"

  # vim
  install -Dm644 $pkgname.vim \
    "$pkgdir/usr/share/vim/vimfiles/syntax/$pkgname.vim"
}
