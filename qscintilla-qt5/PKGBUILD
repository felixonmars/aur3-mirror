# $Id$
# Maintainer: Ilya87 <yast4ik@yahoo.com>

pkgbase=qscintilla-qt5
pkgname=('qscintilla-qt5' 'python-qscintilla-qt5' 'python2-qscintilla-qt5'
         'python-qscintilla-qt5-common')
pkgver=2.8.2
pkgrel=1
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.co.uk/software/qscintilla/intro"
makedepends=('python2-pyqt5' 'python-pyqt5' 'chrpath')
options=('!buildflags')
source=("http://downloads.sourceforge.net/pyqt/QScintilla-gpl-${pkgver}.tar.gz"
        'configure.py.diff')
md5sums=('900c3626dcb6ef8418ef283160e4e238'
         '2272a1b0ab7187da6909604f131bb870')

prepare() {
    cd QScintilla-gpl-${pkgver}
    patch -Np1 -i "${srcdir}/configure.py-objdir-support.diff"
}

build() {
    cd QScintilla-gpl-${pkgver}/Qt4Qt5
    qmake qscintilla.pro
    make

    cd ../designer-Qt4Qt5
    qmake designer.pro INCLUDEPATH+=../Qt4Qt5 QMAKE_LIBDIR+=../Qt4Qt5
    make

    cd ../
    cp -rf Python Python2
    cd Python
    python3 configure.py -n ../Qt4Qt5/ -o ../Qt4Qt5/ -c --pyqt=PyQt5 --pyqt-sipdir=/usr/share/sip/PyQt5 --qsci-sipdir=/usr/share/sip/PyQt5  --qmake /usr/bin/qmake
    make

    cd ../Python2
    python2 configure.py -n ../Qt4Qt5/ -o ../Qt4Qt5/ -c --pyqt=PyQt5 --pyqt-sipdir=/usr/share/sip/PyQt5 --qsci-sipdir=/usr/share/sip/PyQt5 --qmake /usr/bin/qmake
    make
}

package_qscintilla-qt5() {
    pkgdesc="A port to Qt5 of Neil Hodgson's Scintilla C++ editor class"
    depends=('qt5-base')
    conflicts=('qscintilla')

    cd QScintilla-gpl-${pkgver}/Qt4Qt5
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    cd ../designer-Qt4Qt5
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

package_python-qscintilla-qt5-common() {
    pkgdesc="Common python qscintilla bindings files shared between python-qscintilla-qt5 and python2-qscintilla-qt5"
    depends=('qscintilla-qt5')

    cd QScintilla-gpl-${pkgver}/Python
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    # Provided by python-qscintilla
    rm "${pkgdir}"/usr/lib/python3.4/site-packages/PyQt5/Qsci.so
}

package_python-qscintilla-qt5() {
    pkgdesc="Python 3.x bindings for QScintilla2"
    depends=('python-qscintilla-qt5-common' 'python-pyqt5')

    cd QScintilla-gpl-${pkgver}/Python
    install -Dm755 Qsci.so \
      "${pkgdir}"/usr/lib/python3.4/site-packages/PyQt5/Qsci.so
}

package_python2-qscintilla-qt5() {
    pkgdesc="Python 2.x bindings for QScintilla2"
    depends=('python-qscintilla-qt5-common' 'python2-pyqt5')

    cd QScintilla-gpl-${pkgver}/Python2
    install -Dm755 Qsci.so \
      "${pkgdir}"/usr/lib/python2.7/site-packages/PyQt5/Qsci.so
}
