# Maintainer: James Griffis <setkeh@gmail.com>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=asterisk18
pkgver=1.8.32.3
pkgrel=1
pkgdesc="Asterisk is a free and open source framework for building communications applications"
arch=('i686' 'x86_64')
groups=()
url="http://www.asterisk.org"
license=('GPL2')
depends=('gmime' 'gsm' 'sox' 'libxml2' 'openssl' 'curl' 'libldap' 'speex' 'sqlite3' 'libcap' 'libvorbis' 'unixodbc' 'libnewt' 'alsa-lib' 'popt')
optdepends=('radiusclient-ng' 'libpri' 'dahdi' 'libresample' 'iksemel' 'libusb' 'net-snmp')
makedepends=('linux-headers' 'dotconf' 'xmlstarlet')
conflicts=('asterisk' 'asterisk16' 'asterisk-svn' 'asterisk-lts' 'asterisk-10' 'asterisk-svn')
provides=()
options=()
install=asterisk.install
source=(http://downloads.asterisk.org/pub/telephony/asterisk/asterisk-${pkgver}.tar.gz
  asterisk.service asterisk.install asterisk.logrotated directory.patch)
sha1sums=('ba0fd90fd744e423950d66c5d3e777419050d62e'
          '84ff6ebaeddb4d5347e67a8a536496c2abd9aa6a'
          'b104b72f0357d84637fef44d1165c8168ef7541b'
          '38a772e20c9a1987faf433ca38c8536012fbada0'
          '6d4675da5640508e7230f81af0bc10f9869e2ed0')

prepare() {
  # move /var/run -> /run for pid file
  patch -Np0 -i ${srcdir}/directory.patch
}

build() {
  cd $srcdir/asterisk-${pkgver}

  msg "Configuring..."
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var

  msg "Building..."
  make
}

package() {
  cd ${srcdir}/asterisk-${pkgver}
  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} samples
  install -D -m 644 ${srcdir}/asterisk.service ${pkgdir}/usr/lib/systemd/system/asterisk.service
}
