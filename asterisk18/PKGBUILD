# Maintainer: Lev Lybin <lev.lybin@gmail.com>
# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=asterisk18
pkgver=1.8.18.0
pkgrel=6
pkgdesc="A complete PBX solution"
arch=('i686' 'x86_64')
groups=()
url="http://www.asterisk.org"
license=('GPL2')
depends=('libusb' 'libxml2' 'openssl' 'curl' 'gmime' 'iksemel' 'libldap' 'net-snmp' 'postgresql-libs' 'speex' 'sqlite3' 'libcap' 'libvorbis' 'unixodbc' 'libnewt' 'libresample' 'alsa-lib' 'popt')
optdepends=('radiusclient-ng' 'libpri' 'dahdi')
makedepends=('linux-headers')
conflicts=('asterisk' 'asterisk16' 'asterisk-svn' 'asterisk-lts' 'asterisk-10' 'asterisk-svn')
provides=()
options=()
install=asterisk.install
source=(http://downloads.asterisk.org/pub/telephony/asterisk/asterisk-${pkgver}.tar.gz
  asterisk.service asterisk.install asterisk.logrotated)
sha1sums=('7e118539669addedc6651827b2703ab257319a83'
  '60b50e3512c3504e43b91cf93a9c94c2721acb67'
  'b104b72f0357d84637fef44d1165c8168ef7541b'
  '38a772e20c9a1987faf433ca38c8536012fbada0')
build() {
  cd $srcdir/asterisk-${pkgver}

  msg "Configuring..."
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var

  msg "Building..."
  make
}

package() {
  cd ${srcdir}/asterisk-${pkgver}
  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} samples
  install -D -m 644 ${srcdir}/asterisk.service ${pkgdir}/usr/lib/systemd/system/asterisk.service
}
