# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=thunar-extended
pkgver=1.3.0
pkgrel=6
pkgdesc="Thunar with cursor audio preview and extra options for trash, desktop files and user actions"
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4')
conflicts=('thunar')
replaces=('thunar')
provides=('thunar=1.3.0')
depends=('desktop-file-utils' 'libexif>=0.6.19' 'hicolor-icon-theme' 'libnotify' 'udev' \
         'gtk2' 'exo>=0.6.0' 'libxfce4util>=4.8.1' 'libxfce4ui' 'libpng' 'libjpeg>=8.0.1')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'xfce4-panel>=4.8' 'xfce4-dev-tools')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
	    'gamin: for monitoring file changes without gvfs'
            'xfce4-panel: for trash applet'
	    'tumbler: for thumbnail previews'
	    'thunar-volman: manages removable devices'
	    'thunar-archive-plugin: create and deflate archives'
	    'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
install=thunar.install
backup=('etc/polkit-1/localauthority/50-local.d/org.freedesktop.udisks.pkla')
source=(http://archive.xfce.org/src/xfce/thunar/1.3/Thunar-${pkgver}.tar.bz2 more_options.patch uca_num_files.patch preview.patch org.freedesktop.udisks.pkla)

build() {
  cd ${srcdir}/Thunar-${pkgver}
  patch -Np1 -i ../more_options.patch
  patch -Np1 -i ../uca_num_files.patch
  patch -Np1 -i ../preview.patch
  
  xdt-autogen || return 1
  
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static --disable-wallpaper-plugin \
    --enable-gstreamer --enable-gio-unix --enable-dbus --enable-exif \
    --enable-pcre --enable-gudev --disable-startup-notification \
    --enable-notifications --enable-gtk-doc
  
  make || return 1
}

package() {
  cd ${srcdir}/Thunar-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop

  # Install udisks permission file
  install -dm700 ${pkgdir}/etc/polkit-1/localauthority
  install -dm755 ${pkgdir}/etc/polkit-1/localauthority/50-local.d
  install -m644 ${srcdir}/org.freedesktop.udisks.pkla ${pkgdir}/etc/polkit-1/localauthority/50-local.d/
}
md5sums=('ab6f728384c0d925b40afae2f41268f3' '9806ecf2b2795712380b740437ba86a4' '69d88bc605687dd7ca0a0b84ab9da879' '189a505e82851d0c045a20ad2817ffbe' 'a7ddb5eec02d9a8e91a2997862e73cd8')
