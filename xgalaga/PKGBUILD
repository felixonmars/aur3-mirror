# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Loui Chang <louipc.ist@gmail.com>

pkgname=xgalaga
pkgver=2.0.34
pkgrel=5
pkgdesc="An open source remake of the classic arcade game Galaga"
arch=('i686' 'x86_64')
url="http://rumsey.org/xgal.html"
license=('GPL')
depends=('libxmu' 'libxpm')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-altmask.patch"
        "${pkgname}-includes.patch"
        "${pkgname}-makefile.patch"
        "${pkgname}.png"
        "${pkgname}.desktop")
md5sums=('9f7ee685e9c4741b5f0edc3f91df9510'
         '94fac9d26483683b0c1565f67cc78db8'
         'b528e42e20b1e07aae8385fe56c51cd0'
         '4ef51627ff7b92e57a119305cb5710e6'
         '3168c3fec9bd8345946ac93c6b9d7b32'
         'e8fbde4e6049202a087ebc7daf0f0170')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply patches
  patch -Np1 -i ../${pkgname}-altmask.patch
  patch -Np1 -i ../${pkgname}-includes.patch
  patch -Np1 -i ../${pkgname}-makefile.patch

  # Fix path to the scores file
  sed -i "s_\$prefix/scores_/var/lib/${pkgname}/scores_" configure

  # Fix warning about missing font
  sed -i "s_adobe-helvetica_*-*_" libsprite/defs.h

  LDFLAGS='' ./configure \
    --prefix=/usr/share/${pkgname} \
    --exec-prefix=/usr/bin \
    --with-xpm-lib=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install game files
  make prefix="${pkgdir}/usr/share/${pkgname}" \
       exec_prefix="${pkgdir}/usr/bin" \
       scoredir="${pkgdir}/var/lib/${pkgname}" \
       install

  # Install a desktop entry
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install a readme file
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
