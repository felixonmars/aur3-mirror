# Maintainer: Devin J. Pohly <djpohly+arch@gmail.com>
pkgname=neo4j
pkgver=1.7.2
pkgrel=1
pkgdesc="A fully transactional graph database implemented in Java"
arch=(any)
url="http://neo4j.org/"
license=(custom)
depends=(bash java-runtime java-commons-io java-commons-logging java-commons-collections
         antlr2 slf4j)
backup=(etc/neo4j/neo4j.properties etc/neo4j/neo4j-server.properties etc/neo4j/logging.properties)
options=(!strip)
source=("http://dist.neo4j.org/$pkgname-community-$pkgver-unix.tar.gz"
        neo4j-shell neo4j.rc neo4j.conf
        config.patch)
md5sums=('115afbb7cc72b089577e371ce0b1116d'
         '43f0aaef9e403162430295491474c072'
         '71b6556dde2a9fba73b618b97cb96283'
         '36888b912754c660b4e54bc5b0606f53'
         'adc180ddb90c6b40900531dc3055ab24')

build() {
  cd "$srcdir/$pkgname-community-$pkgver"

  # Adjust configuration to match new directory structure
  patch -Np1 -i ../config.patch
}

package() {
  cd "$srcdir"

  install -d \
    "$pkgdir/usr/share/java" \
    "$pkgdir/usr/share/licenses/$pkgname" \
    "$pkgdir/var/lib/$pkgname/data" \
    "$pkgdir/var/log/$pkgname" \
    "$pkgdir/etc/$pkgname/ssl"

  install -D neo4j.rc "$pkgdir/etc/rc.d/neo4j"
  install -D neo4j.conf "$pkgdir/etc/conf.d/neo4j"
  install -D neo4j-shell "$pkgdir/usr/bin/neo4j-shell"

  cd "$pkgname-community-$pkgver"

  # Configuration in /etc, minus the wrapper (which we don't use)
  cp conf/* "$pkgdir/etc/$pkgname"
  rm "$pkgdir/etc/$pkgname/neo4j-wrapper.conf"
  rm "$pkgdir/etc/$pkgname/windows-wrapper-logging.properties"

  # Put JARs in lib, lib/system, and lib/plugins
  cp -r lib "$pkgdir/usr/share/java/$pkgname"
  cp -r system/lib "$pkgdir/usr/share/java/$pkgname/system"
  cp -r plugins "$pkgdir/usr/share/java/$pkgname/plugins"

  # Factor out dependencies
  rm "$pkgdir/usr/share/java/$pkgname/system/"{antlr,commons-{collections,io,logging},{jcl,log4j}-over-slf4j,slf4j-{api,jdk14}}-*.jar

  cp LICENSE.txt LICENSES.txt NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
