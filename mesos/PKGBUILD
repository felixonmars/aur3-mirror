# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
#_pkgver_minor=-rc3
_pkgver_minor=
_python2_ver_major=$(pacman -Qi python2|awk '$1~/Version/{split($3,v,".");print v[1] "." v[2]}')
pkgname=mesos
pkgver=0.21.1
pkgrel=1
pkgdesc="A cluster manager that simplifies the complexity of running applications on a shared pool of servers"
arch=('i686' 'x86_64')
url='http://mesos.apache.org/'
license=('Apache')
groups=('science')
depends=('python2' 'python2-boto' 'curl' 'libsasl' 'leveldb' 'java-environment' 'libunwind' 'python2-protobuf')
makedepends=('java-environment' 'maven' 'http-parser' 'python2-http-parser' 'protobuf' 'google-glog' 'gperftools')
#rc releases
#source=("https://github.com/apache/mesos/archive/$pkgver${_pkgver_minor}.tar.gz"
source=("http://www.apache.org/dist/$pkgname/$pkgver/$pkgname-${pkgver}.tar.gz"
  "$pkgname-master.service"
  "$pkgname-slave.service"
)
md5sums=('5b335a7c48a590d319eccbeba33e484f'
         'da6ff9d8a2df96325bee325a957eb760'
         '69df716316170056ff2a54c5299d8cb4')

prepare() {
	cd "$srcdir/$pkgname-$pkgver${_pkgver_minor}"
  if [ ! -d build ]; then
    mkdir build
  fi
  if [ ! -f configure ]; then
    ./bootstrap
  fi
}

build() {
	cd "$srcdir/$pkgname-$pkgver${_pkgver_minor}"/build
  PYTHON_VERSION=${_python2_ver_major} \
  LIBS='-lprotobuf -lglog' \
  ../configure \
   --prefix=/usr \
   --exec-prefix=/usr \
   --sbindir=/usr/bin \
   --with-glog=/usr \
   --with-leveldb=/usr \
   --with-gperftools=/usr

	make
}

check() {
  echo "skipping 'make check'."
  # (skip tests because some tests don't pass)
  #cd "$srcdir/$pkgname-$pkgver${_pkgver_minor}"/build
  #make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver${_pkgver_minor}"/build
	make DESTDIR="$pkgdir/" install
  mkdir -p -m755 $pkgdir/usr/share/java/$pkgname 
  mkdir -p -m755 $pkgdir/var/{lib,log}/$pkgname
  install -m644 ./src/java/mesos.pom $pkgdir/usr/share/java/$pkgname/
  install -m644 ./src/java/target/*.jar $pkgdir/usr/share/java/$pkgname/
  mkdir -p -m755 $pkgdir/usr/lib/systemd/system
  install -m644 $srcdir/$pkgname-{master,slave}.service $pkgdir/usr/lib/systemd/system
  
  # python
  cd ./src/python
  python2 setup.py install --root="$pkgdir" --optimize=1
}
