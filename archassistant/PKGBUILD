# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Charlie Cox <ccoxiv@yahoo.com>

pkgname=archassistant
pkgver=0.4
pkgrel=3
pkgdesc="GUI program for configuring Arch Linux"
arch=(i686 x86_64)
url="http://www.kde-apps.org/content/show.php/ArchAssistant?content=76760"
license=(GPL)
depends=(qt)
install=$pkgname.install
source=(http://www.kde-apps.org/CONTENT/content-files/76760-$pkgname-$pkgver.tar.bz2 $pkgname.desktop)
sha256sums=('320a70eeff67beba2bd456535ccc02f4c624958ddc59914979dd7a851ee06614'
    'c4186f52bab9b42d24aeff24c576c2ed7e4c8d62055733eb7a0038dc8d5ab47d')
sha512sums=('e82b99f2a8c9e06903c58342cc00fdee688e0e3642fd5c43560b0da68d3c46dd47bc3fa21efca94f0c2869abab9b3378e7131682b8fade79194d6dde0e33d7c9'
    '7f75a71e6db6fd93ade4e87f0f4998868becc0f46064e8c1a1a8f6f5b2b323f2640c97d84c9c7d6acff8b390d50314165ba02fe732411d02908c8bd24088a8a5')

build() {
    cd "$srcdir"/$pkgname-$pkgver/
    qmake archassistant.pro
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    install -Dm755 bin/$pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname
    desktop-file-install ../$pkgname.desktop --dir "$pkgdir"/usr/share/applications/

    lrelease $pkgname.pro
    find ts/ -name '*.qm' -execdir install -Dm755 '{}' "$pkgdir"/usr/share/translations/'{}' \;
}
