# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-termcap
pkgver=1.3.1
pkgrel=1
arch=(any)
pkgdesc="Terminal feature database (mingw-w64)"
depends=(mingw-w64-crt)
makedepends=(mingw32-gcc)
options=(!strip !buildflags !libtool)
license=(GPL LGPL)
url="ftp://ftp.gnu.org/gnu/termcap/"
source=("ftp://ftp.gnu.org/gnu/termcap/termcap-${pkgver}.tar.gz")
md5sums=('ffe6f86e63a3a29fa53ac645faaabdfa')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "$srcdir/termcap-$pkgver"
  
  unset LDFLAGS

  autoconf

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch}
    make

    # Build a shared library.  No need for -fPIC on Windows.
    ${_arch}-gcc -shared \
      -Wl,--out-implib,libtermcap.dll.a \
      -o libtermcap-0.dll \
      termcap.o tparam.o version.o
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/termcap-$pkgver/build-${_arch}"
    # make install has no support for destdir
    make install prefix=${pkgdir}/usr/${_arch} exec_prefix=${pkgdir}/usr/${_arch} oldincludeprefix=

    mkdir -p ${pkgdir}/usr/${_arch}/{bin,lib}
    install -m 0755 libtermcap-0.dll ${pkgdir}/usr/${_arch}/bin
    install -m 0755 libtermcap.dll.a ${pkgdir}/usr/${_arch}/lib

    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
    rm -rf "$pkgdir/usr/${_arch}/info"
  done
}
