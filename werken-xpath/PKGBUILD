# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on Fedora's packaging
_fedora_rel=8.beta.12.4.fc18

pkgname=werken-xpath
pkgver=0.9.4
pkgrel=1.beta.12.4
pkgdesc="XPath implementation using JDOM"
arch=('any')
url="http://sourceforge.net/projects/werken-xpath/"
license=('Apache')
depends=('java-jdom1')
makedepends=('antlr2' 'apache-ant' 'java-xml-commons-external-fixed' 'xerces2-java')
source=("http://kojipkgs.fedoraproject.org//packages/werken-xpath/${pkgver}/${_fedora_rel}/src/werken-xpath-${pkgver}-${_fedora_rel}.src.rpm")
sha512sums=('8041e102e703118121981af2f85c212a2c0cb7952ebfb5176e426fbbae03eedde6e9587afe401c2268d43d4dda0e143d3a70a8c064b3a8da16ecdba60d98afd1')

build() {
  cd "${srcdir}/"

  tar zxvf werken.xpath-${pkgver}-beta-src.tar.gz

  cd werken.xpath

  # Apply Fedora's patches
  for i in $(grep ^Patch "${srcdir}/werken-xpath.spec" | \
             awk -F '%{name}' '{print $2}'); do
    patch -p0 -i "${srcdir}/${pkgname}${i}"
  done

  find . -name '*.jar' -o -name '*.class' -delete

  cp "${srcdir}/werken-xpath-${pkgver}.pom" .

  export CLASSPATH="/usr/share/java/jdom/jdom.jar:/usr/share/java/antlr2.jar"
  ant -Dbuild.compiler=modern package compile-test
  #ant package
}

check() {
  cd "${srcdir}/werken.xpath"
  export CLASSPATH="${CLASSPATH}:./build/werken.xpath.jar:build/test/classes"
  sh runtests.sh
}

package() {
  cd "${srcdir}/werken.xpath"

  install -dm755 "${pkgdir}/usr/share/java/"
  install -m644 build/werken.xpath.jar "${pkgdir}/usr/share/java/"
}

# vim:set ts=2 sw=2 et:
