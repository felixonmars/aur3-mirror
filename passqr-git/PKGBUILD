# Maintainer: Emil Lundberg <lundberg.emil@gmail.com> (AUR: Lorde; GitHub: emlun)

_pkgname=passqr
pkgname="${_pkgname}-git"
pkgver=r59.6bb07c0
pkgrel=1
pkgdesc="Generate 2D barcodes from password-store entries"
arch=('any')
url='https://github.com/emlun/passqr'
license=('GPL')
backup=("etc/${_pkgname}.conf")
depends=('pass' 'qrencode')
optdepends=(
    'feh: fast image viewer'
    'gpicview: fast image viewer'
    'imagemagick: fast image viewer'
    'nomacs: fast image viewer'
    'qiv: fast image viewer'
    'xv: fast image viewer'
    'gimmage: image viewer'
    'mirage: image viewer'
    'ristretto: image viewer'
    'viewnior: image viewer'
    'fbida: console image viewer'
    'fbv: console image viewer'
    )
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
changelog='changelog.txt'
source=("${changelog}"
        "${pkgname}::git+${url}.git#branch=master")
md5sums=('23450b37e07ab9a3fb4ef6e6f3bb9cd0'
         'SKIP')
sha1sums=('d14dffa2b688065089028e483ec11083c298afad'
          'SKIP')
sha256sums=('b99f2694f7dc608505353a841b2ab2271fa928e62b98c05a5556bdc91dcecf2b'
            'SKIP')
sha512sums=('66739d69270ddc2d4a37f90adfc4e70f1d0c324c1a2272a6b94922e2089b178ad4d2b61231b8819fb0f9f89585b4643f8438c56eb086f24ad867eb1de463f5ef'
            'SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "${srcdir}/${pkgname}"
    make install DESTDIR="${pkgdir}"
}
