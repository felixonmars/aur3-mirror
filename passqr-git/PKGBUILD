# Maintainer: Emil Lundberg <lundberg.emil@gmail.com> (AUR: Lorde; GitHub: emlun)

_pkgname=passqr
pkgname="${_pkgname}-git"
pkgver=r69.345da38
pkgrel=1
pkgdesc="Generate 2D barcodes from password-store entries"
arch=('any')
url='https://github.com/emlun/passqr'
license=('GPL')
backup=("etc/${_pkgname}.conf")
depends=('pass' 'qrencode')
optdepends=(
    'feh: fast image viewer'
    'gpicview: fast image viewer'
    'imagemagick: fast image viewer'
    'nomacs: fast image viewer'
    'qiv: fast image viewer'
    'xv: fast image viewer'
    'gimmage: image viewer'
    'mirage: image viewer'
    'ristretto: image viewer'
    'viewnior: image viewer'
    'fbida: console image viewer'
    'fbv: console image viewer'
    )
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("${pkgname}::git+${url}.git#branch=master")
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "${srcdir}/${pkgname}"
    make install DESTDIR="${pkgdir}"
}
