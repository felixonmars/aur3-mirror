# $Id$
# Maintainer: <matmoser@wu.ac.at>
# Original Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=r-mkl
pkgver=2.15.1
pkgrel=1
pkgdesc="Language and environment for statistical computing and graphics"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('blas' 'lapack' 'bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'libxt' 'libxmu' 'pango' 'xz' 'desktop-file-utils' 'intel-mkl')
makedepends=('openjdk6' 'gcc-fortran' 'tk')
optdepends=('tk: tcl/tk interface' 'texlive-bin: latex sty files')
backup=('etc/R/Makeconf' 'etc/R/Renviron' 'etc/R/ldpaths' 'etc/R/repositories')
options=('!makeflags' '!emptydirs')
source=("http://cran.r-project.org/src/base/R-2/R-${pkgver}.tar.gz"
	'r.desktop'
	'r.png'
	'R.conf')
install=r.install
sha1sums=('f0e6912be6dfc0d1fdc4be66048304d8befe8424'
          '13aa29219bcaa102e575de8c1c8e0833d233e836'
          'a69a07ec363440efc18ce0a7f2af103375dea978'
          '5de97f1f23b1ef2ce4ac4592d0ea7091c847cea1')
provides=(r)
conflicts=(r)

build() {
   cd "${srcdir}/R-${pkgver}"
   sed -i 's|#define NeedFunctionPrototypes 0|#define NeedFunctionPrototypes 1|g' src/modules/X11/dataentry.c

  # set texmf dir correctly in makefile
  sed -i 's|$(rsharedir)/texmf|${datarootdir}/texmf|' share/Makefile.in
  MKLROOT=/opt/intel/composerxe/mkl
 if [[ $CARCH == x86_64 ]]; then
      MKLPATH="${MKLROOT}/lib/intel64"
      MKLSUFFIX="_lp64"
   else
      MKLPATH="${MKLROOT}/lib/ia32"
      MKLSUFFIX=""
   fi
   MKL="-L$MKLPATH $MKLPATH/libmkl_solver$MKLSUFFIX.a -Wl,--start-group -lmkl_gf$MKLSUFFIX -lmkl_gnu_thread -lmkl_core -Wl,--end-group -fopenmp -lpthread"
   LDFLAGS="-lpthread"

   ./configure  --prefix=/usr \
		--libdir=/usr/lib \
		--sysconfdir=/etc/R \
		--datarootdir=/usr/share \
		  rsharedir=/usr/share/R/ \
		  rincludedir=/usr/include/R/ \
		  rdocdir=/usr/share/doc/R/ \
                --with-x \
		--enable-R-shlib \
                --with-lapack \
		--with-blas="$MKL" \
                F77=gfortran \
		LIBnn=lib
   make
}

package() {
   cd "${srcdir}/R-${pkgver}"
   make -j1 DESTDIR="${pkgdir}" install

   #  Fixup R wrapper scripts.
   sed -i "s|${pkgdir} ||" "${pkgdir}/usr/bin/R"
   rm "${pkgdir}/usr/lib/R/bin/R"
   cd "${pkgdir}/usr/lib/R/bin"
   ln -s ../../../bin/R

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/r.desktop" \
	"${pkgdir}/usr/share/applications/r.desktop"
  install -Dm644 "${srcdir}/r.png" \
	"${pkgdir}/usr/share/pixmaps/r.png"

  # move the config directory to /etc and create symlinks
  install -d "${pkgdir}/etc/R"
  cd "${pkgdir}/usr/lib/R/etc"
  for i in *; do
    mv -f ${i} "${pkgdir}/etc/R"
    ln -s /etc/R/${i} ${i}
  done

  # Install ld.so.conf.d file to ensure other applications access the shared lib
  install -Dm644 ${srcdir}/R.conf ${pkgdir}/etc/ld.so.conf.d/R.conf
}
