# Contributor: Christian Sturm <reezer@reezer.org>
pkgname=perl-ppix-regexp
pkgver=0.028
pkgrel=1
pkgdesc="Represent a regular expression of some sort"
arch=(any)
url="https://metacpan.org/release/PPIx-Regexp"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' perl-list-moreutils perl-params-util perl-readonly perl-task-weaken)
options=(!emptydirs)
source=(http://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-Regexp-$pkgver.tar.gz)
md5sums=('de7334a0c2f121812e59e5ccddea96ec')

build() {
  cd "$srcdir/PPIx-Regexp-$pkgver"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor || return 1
  make
}

check() {
  cd "$srcdir/PPIx-Regexp-$pkgver"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/PPIx-Regexp-$pkgver"
  make install DESTDIR="$pkgdir/"

  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}

# vim:set ts=2 sw=2 et:
