# Contributor: Kirill "reflexing" Churin <reflexing@reflexing.ru>
# Maintainer: Kyrylo Silin <kyrylosilin@gmail.com>

pkgname=kag
pkgver=360
pkgrel=1
pkgdesc="King Arthur's Gold is a game about building castles and destroying your enemies (inspired by Soldat and Minecraft)"
arch=('any')
url="http://kag2d.com/"
license=('custom')
depends=('desktop-file-utils' 'xdg-utils')
if test "$CARCH" == x86_64; then
  depends=("${depends[@]}" lib32-libgl)
else
  depends=("${depends[@]}" libgl)
fi
install=kag.install
source=(http://kag2d.com/en/download/kag_x.tar.gz
        kag
        kag.desktop
        irrlicht_1_32x32x24.png
        LICENSE)

package() {
  cd ${srcdir}

  install -dm755 ${pkgdir}/usr/bin
  install -dm777 ${pkgdir}/usr/share/${pkgname}
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}

  install -Dm755 kag ${pkgdir}/usr/bin/kag

  cp -dr --preserve=mode,timestamp kag_x/* ${pkgdir}/usr/share/${pkgname}
  chmod -R go+w ${pkgdir}/usr/share/${pkgname}

  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}

  # Proper installation of .desktop file
  desktop-file-install -m 644 --dir ${pkgdir}/usr/share/applications/ kag.desktop

  # Install icon (got from source package with "icotool -x irrlicht.ico")
  install -m644 -D irrlicht_1_32x32x24.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/kag.png
}

md5sums=('af9a814989b28c5642bf92c675db0d21'
         'cc45655ce4c9af8768db8b2c60ccdc7a'
         '3d3b3ea71998582cc88269d3f2da4863'
         '81f2bae53e67f9220802b1187b8f398f'
         '35cae9aeae1c2d561677230795d0d827')
