# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kde-gtk-config-frameworks
pkgver=2.9.90
pkgrel=3
pkgdesc='GTK2 and GTK3 Configurator for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kde-gtk-config'
license=('LGPL')
depends=('kcmutils' 'knewstuff')
makedepends=('extra-cmake-modules' 'gtk2' 'gtk3')
conflicts=('kde-gtk-config')
source=("http://download.kde.org/unstable/kde-gtk-config/$pkgver/src/kde-gtk-config-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('76801042a8086fbfa50360d9eaf4f7d6')

prepare() {
  mkdir -p build

  sed '/Msgfmt/d' -i kde-gtk-config-$pkgver/CMakeLists.txt
}

build() {
  cd build
  cmake ../kde-gtk-config-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
