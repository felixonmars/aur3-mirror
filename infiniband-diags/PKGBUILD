# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=infiniband-diags
pkgver=1.6.2
pkgrel=1
pkgdesc="OpenFabrics Alliance InfiniBand diagnostic tools"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibmad' 'opensm' 'perl')
source=("http://www.openfabrics.org/downloads/management/${pkgname}-${pkgver}.tar.gz")
# cksum start
md5sums=('0df49878f90c52ab7a997ca55e57ef51')
sha512sums=('37a76b35a9f687c1514c3ada4defb5139cfc3217bcb9fbbad3136327122e3366dc26f235f143bb27f6f0ee35641b3a2e87d0368b098806b7bd16f0554808e55f')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-ibpath_override=/usr/sbin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}