# Maintainer: pisuka <tekmon@gmail.com>
pkgname=lightdm-bzr
pkgver=1061
pkgrel=1
pkgdesc="A lightweight display manager."
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm"
license=('GPL3' 'LGPL3')
depends=('gnome-themes-standard' 'gnome-icon-theme' 'dbus-glib' 'libxklavier' 'intltool')
optdepends=(
  'qt: qt greeters'
  'xorg-xserver-xephr: run lightdm in test mode'
)
makedepends=('bzr' 'vala' 'gnome-common' 'gtk-doc' 'gnome-doc-utils' 'pkg-config')
provides=('lightdm')
conflicts=('lightdm')
source=('lightdm')
md5sums=('990fdeb198f2ef928274bde0b9d2df99')
backup=('etc/lightdm.conf')
options=(!libtool)

_bzrtrunk="lp:lightdm"
_bzrmod="lightdm-trunk"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  #patch -p2 < "$srcdir/python.patch"

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm \
    --disable-static \
    --enable-introspection=no
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/etc/init"

  mkdir -p "$pkgdir/etc/rc.d"
  install -m755 "$srcdir/lightdm" "$pkgdir/etc/rc.d"
}
