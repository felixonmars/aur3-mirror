# Maintainer: Marc 'bomba' Schulte <bomba at nerdstube dot de>


# Kernel needs CONFIG_ACPI_PROC_EVENT=y for wireless radio switch event
#(DEVICES_TO_ENABLE_ON_RADIOSW)

pkgname=tlp
pkgver=0.3.9
pkgrel=4
pkgdesc="Powersaving scripts for ThinkPads(c)"
arch=('any')
url="http://linrunner.de/en/tlp/docs/tlp-linux-advanced-power-management.html"
license=('GPL2')

depends=('hdparm' 'bash' 'pm-utils' 'wireless_tools' 'rfkill' 'ethtool' 'upower')
makedepends=('git')
optdepends=('smartmontools: enable tlp-stat to show S.M.A.R.T. values of hard disks' 
'tp_smapi: for setting battery thresholds on ThinkPad laptops' 
'acpi_call: battery functions on Sandy Bridge ThinkPad models'
'upower: Required for enabling tlp.service'
'iw: linux wireless'
'acpid: acpi daemon')
backup=(etc/default/tlp)
conflicts=('laptop-mode-tools')
_gitname=TLP

source=("$_gitname::git://github.com/linrunner/$_gitname.git")

install=tlp.install

package() {
  cd "$srcdir/$_gitname"

  #first patch the makefile to change from init.d to rc.d
  sed -i -e 's|init.d|rc.d|' Makefile

  #install it using the official makefile
  make DESTDIR="${pkgdir}" SBIN="${pkgdir}/usr/bin" install-tlp  > /dev/null 2>&1

  #install man files
  install -d ${pkgdir}/usr/share/man/man1/
  install -m 644 man/bluetooth.1 ${pkgdir}/usr/share/man/man1/
  install -m 644 man/run-on-ac.1 ${pkgdir}/usr/share/man/man1/
  install -m 644 man/run-on-bat.1 ${pkgdir}/usr/share/man/man1/
  install -m 644 man/wifi.1 ${pkgdir}/usr/share/man/man1/
  install -m 644 man/wwan.1 ${pkgdir}/usr/share/man/man1/

  install -d ${pkgdir}/usr/share/man/man8/
  install -m 644 man/tlp.8 ${pkgdir}/usr/share/man/man8/
  install -m 644 man/tlp-stat.8 ${pkgdir}/usr/share/man/man8/
  
  #install license files
  install -d "${pkgdir}"/usr/share/licenses/tlp
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/tlp
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/tlp

  install -d ${pkgdir}/usr/share/doc/tlp/
  install -m 644 README ${pkgdir}/usr/share/doc/tlp/

  #install systemd service
  install -d ${pkgdir}/usr/lib/systemd/system/
  install -m 644 tlp.service ${pkgdir}/usr/lib/systemd/system/ 

}
md5sums=('SKIP')
