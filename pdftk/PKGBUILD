# Contributor: osc
# Contributor: StefanHusmann
# Maintainer: larsrh <hupel@in.tum.de>

pkgname=pdftk
pkgver=1.44
pkgrel=6
pkgdesc="Pdftk is a simple tool for doing everyday things with PDF documents"
arch=('i686' 'x86_64')
url="http://www.pdfhacks.com/pdftk"
license="GPL"
depends=('gcc-gcj>=4.6.2' 'gcc-gcj-ecj')
options=('!makeflags')
source=("http://www.pdflabs.com/tools/pdftk-the-pdf-toolkit/${pkgname}-${pkgver}-src.zip"
        "makefile.patch")
md5sums=('9eb50fffcd621a627d387750c60982b4'
         'd20b6a9cb00d94554c461a3914b0d21b')

build() {
	cd $srcdir/$pkgname-$pkgver-dist/$pkgname

	# Apply makefile patch
	patch < $srcdir/makefile.patch

	# Compile the package
	make -f Makefile.Debian
}

package() {
	cd $srcdir/$pkgname-$pkgver-dist/$pkgname

	# Install binary
	install -D -m 755 pdftk $pkgdir/usr/bin/pdftk

	# Install licenses and adjust permissions
	cd ..
	LICENSE_DIR="$pkgdir/usr/share/licenses/$pkgname"
	mkdir -p "$LICENSE_DIR"
	cp -R license_gpl_pdftk/* "$LICENSE_DIR"
	chmod -R 0755 "$LICENSE_DIR"
	find "$LICENSE_DIR" -type f -exec chmod -x {} \;

	# Install manpage
	install -D -m 644 "pdftk.1" "$pkgdir/usr/share/man/man1/pdftk.1"
}
