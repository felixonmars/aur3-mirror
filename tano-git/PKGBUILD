pkgname=tano-git
pkgver=20120602
pkgrel=1
pkgdesc="An open-source cross-platform IP TV player based on Qt and libvlc"
arch=('i686' 'x86_64')
url="http://projects.tano.si/en/player"
license=('GPL')
depends=('vlc>=1.1' 'qt>=4.6' 'libvlc-qt-git')
makedepends=('cmake>=2.6' 'qt>=4.6' 'dbus>=1.4' 'libvlc-qt-git' 'desktop-file-utils')
provides=('tano')
conflicts=('tano')

_gitroot='git://github.com/ntadej/tano.git'
_gitname='tano'
_buildir="$_gitname-build"

_optimal_make_jobs() {
	if [ -r /proc/cpuinfo ]; then
		local core_count=$(grep -Fc processor /proc/cpuinfo)
	else
		local core_count=0
	fi

	if [ $core_count -gt 1 ]; then
		echo -n $[$core_count-1]
	else
		echo -n 1
	fi
}

build() {
	cd "$srcdir"

	msg 'Connecting to GIT server...'
	if [ -d "$_gitname" ]; then
		cd "$_gitname" && git pull origin
		cd ..
	else
		git clone "$_gitroot"
	fi
	msg 'GIT checkout done or server timeout.'

	if [ -d "$_buildir" ]; then
		msg 'Cleaning previous build...'
		rm -rf "$_buildir"
	fi

	git clone "$_gitname" "$_buildir"
	cd "$_buildir"

	msg 'Running cmake...'
	cmake . -DCMAKE_INSTALL_PREFIX=/usr

	msg 'Running make...'
	make -j$(_optimal_make_jobs)
}

package() {
	cd "$srcdir/$_buildir"
	make DESTDIR="$pkgdir" install
	install -Dm 644 tano-editor.desktop "$pkgdir/usr/share/applications/tano-editor.desktop"
	install -Dm 644 tano.desktop "$pkgdir/usr/share/applications/tano.desktop"
}
