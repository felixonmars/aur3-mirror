# Maintainer: Muflone <webreg@vbsimple.net>

pkgname=remmina-plugin-webkit
pkgver=1.0.0.1
pkgrel=2
pkgdesc="A protocol plugin for Remmina to launch a GTK+ Webkit browser."
arch=(i686 x86_64)
url="https://code.google.com/p/remmina-plugins-misc/"
license=('GPL')
depends=('gtk3' 'gtk2' 'zlib' 'remmina' 'webkitgtk2')
makedepends=('intltool' 'pkgconfig' 'cmake')
optdepends=()
install=remmina-plugin-webkit.install
source=(
  "https://github.com/downloads/FreeRDP/Remmina/Remmina-1.0.0.tar.gz"
  "https://github.com/FreeRDP/Remmina/commit/569d9bb702d06c061dfbbf3c590d57ea83a04369.diff"
  "https://github.com/FreeRDP/Remmina/commit/f7d1038ba358a295fb46c681db5c47e4ead97306.diff"
  "${pkgname}_${pkgver}.tar.gz::https://github.com/muflone/${pkgname}/archive/${pkgver}.tar.gz"
  "${pkgname}_${pkgver}.diff"
)
md5sums=(
  '701c540acaab961bc3adf130a2ddb8b1'
  'a979c898d5e53446cec9d01912ad9243'
  'b2a9c0e80ac7b0c8a26935bbe289aa3b'
  '4ec3cca706c93b000336fda73a43daf3'
  '178ced3c031c91181c325f2451a06602'
)

build() {
  #cd ${srcdir}/FreeRDP-Remmina-356c033/
  [ -d build ] && rm -rf build
  cp -r FreeRDP-Remmina-356c033 build
  cp -r "${srcdir}/${pkgname}-${pkgver}/remmina-gtkwebkit-browser" build
  cp -r "${srcdir}/${pkgname}-${pkgver}/remmina-plugin-webkit" build
  cd build
  
  patch -p1 -i "../569d9bb702d06c061dfbbf3c590d57ea83a04369.diff"
  patch -p1 -i "../f7d1038ba358a295fb46c681db5c47e4ead97306.diff"
  patch -p1 -i "../${pkgname}_${pkgver}.diff"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd ${srcdir}/build/
  make DESTDIR=${pkgdir} install
}
