# Maintainer: Pascal E. <archlinux at hardfalcon dot net>
# Contributor: Giovanni Scafora <giovanni at archlinux dot org>
# Contributor: Daniel Plaza <daniel at plaza.espi dot gmail.com>

pkgname=pcsclite-libudev
pkgver=1.7.2
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library. This version uses udev instead of libusb"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('udev')
makedepends=('pkgconfig')
provides=('pcsclite')
conflicts=('pcsclite')
options=('!libtool' '!docs')
source=(https://alioth.debian.org/frs/download.php/3533/pcsc-lite-${pkgver}.tar.bz2
        pcscd)
md5sums=('47e7055cfc14399fdaa1b7a4aa06e5aa'
         '80d90388e4d2a551cb7aefe3bd5147bd')

build() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  ./configure --prefix=/usr \
              --enable-libudev \
              --disable-libusb \
              --sysconfdir=/etc \
              --enable-usbdropdir=/usr/lib/pcsc/drivers

  make
}

package() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -D -m755 ${srcdir}/pcscd ${pkgdir}/etc/rc.d/pcscd
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
