# Maintainer: Nuno Araujo <nuno.araujo@russo79.com>
pkgname=lg4l-gnome15
pkgver=0.14
_extramodules=$(ls -v /usr/lib/modules/ | grep extra | tail -n 1)
pkgrel=9
pkgdesc="Drivers for Logitech G devices (Gnome15 branch)"
arch=(i686 x86_64)
url="http://www.russo79.com/gnome15"
license=('GPL')
depends=('linux>=3.14' 'linux<3.15')
makedepends=('linux-headers')
conflicts=('lg4l-git' 'lg4l-gnome15-lts')
provides=('lg4l')
install=lg4l-gnome15.install
source=("https://projects.russo79.com/attachments/download/139/$pkgname-$pkgver.tar.gz")
md5sums=('ad50739683fbeb5ec76440e6abc8c1b1')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i "s#uname -r#ls -v /usr/lib/modules/ | grep -v extra | tail -n 1#" Makefile
	sed -i "s#/lib/modules/\$(KVERSION)/updates/g-series#/lib/modules/\$(ls -v /usr/lib/modules/ | grep extra | tail -n 1)#" Makefile
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	install -d "${pkgdir}/usr/lib/modules/$_extramodules/"
	install -t "${pkgdir}/usr/lib/modules/$_extramodules/" *.ko

        install -d "${pkgdir}/usr/lib/udev/rules.d/"
        install -t "${pkgdir}/usr/lib/udev/rules.d/" 90-lg4l.rules

        install -d "${pkgdir}/usr/share/doc/${pkgname}/"
        install -t "${pkgdir}/usr/share/doc/${pkgname}/" README.md
        install -t "${pkgdir}/usr/share/doc/${pkgname}/" rebind
}
