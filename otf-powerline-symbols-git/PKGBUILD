# Maintainer: Ariel Popper <a@arielp.com>

_gitname=powerline
_gitbranch=develop
pkgname="otf-powerline-symbols-git"
pkgver=800.c1ae7f3
pkgrel=1
pkgdesc='Fallback font for the Powerline symbols'
url='https://github.com/Lokaltog/powerline'
license=('MIT')
arch=('any')
makedepends=('git')
source=("${_gitname}::git://github.com/Lokaltog/${_gitname}.git#branch=${_gitbranch}")
install=powerline-symbols.install
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_gitname}"
	echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

package() {
	cd "${srcdir}/${_gitname}"

	install -dm755 "${pkgdir}/etc/fonts/conf.d"
	install -Dm644 "font/PowerlineSymbols.otf" "${pkgdir}/usr/share/fonts/OTF/PowerlineSymbols.otf"
	install -Dm644 "font/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.avail/10-powerline-symbols.conf"
	ln -s "../conf.avail/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.d/10-powerline-symbols.conf"
	
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
