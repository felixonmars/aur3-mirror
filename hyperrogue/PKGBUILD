# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=hyperrogue
pkgver=4.0
_pkgver=40
pkgrel=2
pkgdesc="You are a lone outsider in a strange, non-Euclidean hyperbolic world."
arch=('i686' 'x86_64')
url="http://www.roguetemple.com/z/hyper.php"
license=('GPL2')
depends=('sdl_gfx' 'sdl_ttf' 'sdl_mixer')
source=(http://www.roguetemple.com/z/$pkgname-${_pkgver}t.zip)
# nomusic 404s
md5sums=('7ce9ffabdd91b7913cddaa5e32384a9d')

build() {
    cd "$srcdir/$pkgname-$_pkgver"
    chmod -x *
    sed -i 's|VeraBd.ttf|/usr/share/hyperrogue/&|' graph.cpp
    make
}

package() {
    cd "$srcdir/$pkgname-$_pkgver"
    install -Dm755 hyper      "$pkgdir/usr/bin/hyperrogue"
    install -Dm644 VeraBd.ttf "$pkgdir/usr/share/hyperrogue/VeraBd.ttf"
    install -Dm644 hyperrogue-music.txt "$pkgdir/usr/share/hyperrogue/hyperrogue-music.txt"
    cp *.ogg "$pkgdir/usr/share/hyperrogue/"
}

