# Maintainer: joseph28cb

pkgname=vuze-extreme-mod
pkgver=4.7.0.3_b14
pkgrel=1
pkgdesc="A modded version of vuze BitTorrent client"
arch=('i686' 'x86_64')
url="http://www.sb-innovation.de/f41/"
license=('GPL' 'custom')
depends=('java-runtime' 'gnome-vfs' 'libgnome' 'libgnomeui')
optdepends=('xulrunner: needed for browsing the vuze content')
makedepends=('libarchive')
source=("http://downloads.sourceforge.net/project/azureus/vuze/Vuze_4702/Vuze_4702_linux.tar.bz2"
        "http://www.sb-innovation.de/attachments/f41/12347d1327255438-vuze-extreme-mod-sb-innovation-4-7-0-3_b14-beta-vpem_4703-14.zip"
        "mono_icons.tar.xz")
provides=('vuze')
conflicts=('vuze')
noextract=("12347d1327255438-vuze-extreme-mod-sb-innovation-4-7-0-3_b14-beta-vpem_4703-14.zip" "mono_icons.tar.xz")
md5sums=('5a8999882d416de2d70b2e37b9abb6e9'
         '39fb179392f15fe82a29c5b279e98553'
         '4a17a214f5dd35c3b2c50c85ace88534')
if [ $CARCH = 'x86_64' ]; then
  source[0]="http://downloads.sourceforge.net/project/azureus/vuze/Vuze_4702/Vuze_4702_linux-x86_64.tar.bz2"
  md5sums[0]='c6230f17d383410476fc807c37a1daad'
fi


  # Install mono systray icon (change to "yes")
  _mono="no"


package() {
  install -dm755 "$pkgdir/usr/share"
  cp -rf vuze "$pkgdir/usr/share/$pkgname"
  install -Dm755 vuze/vuze "$pkgdir/usr/bin/$pkgname"

  sed -i 's|#PROGRAM_DIR=.*|PROGRAM_DIR="/usr/share/vuze-extreme-mod"|' "$pkgdir/usr/bin/$pkgname"
  install -Dm644 vuze/vuze.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  sed -e 's|Name=Vuze|Name=Vuze Extreme Mod|' \
      -e 's|Exec=vuze %f|Exec=vuze-extreme-mod %f|' \
      -e 's|Icon=vuze.png|Icon=vuze-extreme-mod.png|' \
      -i vuze/vuze.desktop

  install -Dm644 vuze/vuze.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  bsdtar -xf $(basename ${source[1]}) -C "$pkgdir/usr/share/$pkgname"

  if [ $_mono = "yes" ]; then
    mkdir tmp
    bsdtar -xf $(basename ${source[1]}) Azureus2.jar
    bsdtar -xf Azureus2.jar -C tmp
    bsdtar -xf mono_icons.tar.xz -C tmp/org/gudy/azureus2/ui/icons/
    cd tmp
    jar cf Azureus2.jar ./*/
    install -m644 Azureus2.jar "$pkgdir/usr/share/$pkgname"
  fi
}
