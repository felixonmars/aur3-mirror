# Maintainer: trya <tryagainprod@gmail.com>

pkgname=mamepgui
pkgver=1.5.0
pkgrel=2
_date=20100305
pkgdesc="Frontend for SDLMAME/SDLMESS based on MAME Plus!"
arch=('i686' 'x86_64')
url="http://mameicons.free.fr/mame32p"
license=('custom:WTFPL')
depends=('qt' 'sdl')
optdepends=('sdlmame: port of MAME using SDL'
            'sdlmess: port of MESS using SDL')
source=("http://et6.free.fr/temp/$pkgname-$pkgver.src-$_date.zip"
        'COPYING')
md5sums=('70903903311130e6d110305bd24dfdea'
         '389a9e29629d1f05e115f8f05c283df5')

build() {
  cd ${srcdir}

  # Edit mamepgui.pro to disable static compilation and SDL 1.3 linking
  sed -e '1,2d' -i mamepgui.pro

  # Edit gamelist.cpp to disable joystick detection, which is a SDL 1.3 feature
  sed -e '42d' -i gamelist.cpp

  # Patches to fix compilation with Qt 4.7
  sed -e 's/ipspath(NULL)/ipspath()/' -i ips.cpp
  sed -e 's/void setGuiStyle(QString = NULL);/void setGuiStyle(QString);/' \
      -e 's/void setBgPixmap(QString = NULL);/void setBgPixmap(QString);/' -i mamepgui_main.h

  lrelease mamepgui.pro
  qmake
  make
}

package() {
  # install license
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
  
  # install executable
  install -Dm755 ${srcdir}/mamepgui ${pkgdir}/usr/bin/mamepgui
}
