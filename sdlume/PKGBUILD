# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=sdlume
pkgver=0.146u5
pkgrel=1

_basever=146u5

pkgdesc='Universal Machine Emulator combines the features of MAME and MESS into a single multi-purpose emulator.'
url='http://mamedev.emulab.it/haze/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat' 'gtk2')
makedepends=('mesa')
[ "$CARCH" = 'i686' ] && makedepends=('mesa' 'nasm')
optdepends=('ttf-liberation: recommended UI font')

source=(sdlume.sh
        "http://mamedev.emulab.it/haze/ume/ume${_basever}.7z")
md5sums=('e1d320d42f62f3a18532640b015ee29a'
         'f4a79f70bf357441cb629ed091758cf8')
install=sdlume.install

build() {
  cd "$srcdir"
  
  find . -type f -not -name '*.png' -exec perl -pi -e 's/\r\n?/\n/g' '{}' \;
  
  make TARGET=ume SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
}

package() {
  cd "$srcdir"
  
  # Installing the wrapper script
  install -Dm755 "$srcdir/sdlume.sh" "$pkgdir/usr/bin/sdlume"
  
  # Installing binaries
  install -Dm755 ume "$pkgdir/usr/share/$pkgname/sdlume"
  
  # Installing extra bits
  install -d "$pkgdir/usr/share/$pkgname/"{artwork,hash,shader,keymaps}
  
  install -m644 artwork/* "$pkgdir/usr/share/$pkgname/artwork/"
  install -m644 hash/* "$pkgdir/usr/share/$pkgname/hash/"
  install -m644 src/osd/sdl/shader/glsl*.*h "$pkgdir/usr/share/$pkgname/shader/"
  install -m644 src/osd/sdl/keymaps/* "$pkgdir/usr/share/$pkgname/keymaps/"
  
  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/$pkgname/license.txt"
}
