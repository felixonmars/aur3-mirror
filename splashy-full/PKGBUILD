# Contributor: Det <nimetonmaili@gmail.com>
# Contributor: Lexiw <llexiw@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: dongiovanni <dongiovanni@archlinux.de>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>
# Contributor: Tons of people here http://bbs.archlinux.org/viewtopic.php?id=48978

pkgname=splashy-full
pkgver=20110906
pkgrel=1
pkgdesc="A boot splashing system"
arch=('i686' 'x86_64')
url="http://splashy.alioth.debian.org/"
license=('GPL')
depends=('glib2' 'directfb')
makedepends=('automake1.10' 'git')
options=('!libtool' '!distcc' '!makeflags')
provides=('splashy')
install=${pkgname}.install
backup=('etc/splash.conf'
        'etc/splashy/config.xml')
source=(splash.conf
        splash-initscripts
        splashy-functions
        splashy.initcpio_hook
        splashy.initcpio_install)
sha1sums=('7b1f6661665932379032e6d0da430e71ecfc8a45'  # splash.conf
          '524d2df3f5d22da814000e68acb37ec8a8475dd3'  # splash-initscripts
          'f6135b85eb336865c3f52d004ee5f4b50409aa0f'  # splashy-functions
          '300620ed933ac14ce7beb9bfb233ba3d3b2f7627'  # splashy.initcpio_hook
          '002b1b95e760fc93388fdf9fb006b65842ee654a')  # splashy.initcpio_install

_gitroot='git://anonscm.debian.org/git/splashy/splashy.git'
_gitname='splashy'

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d ${_gitname} ]; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done. Preparing sources..."

  cd "${srcdir}"
  rm -rf ${_gitname}-build
  cp -r ${_gitname} ${_gitname}-build

  cd ${_gitname}-build

  # Fix the build
  sed -e 's|-Werror||g' -i configure.ac

  ./autogen.sh --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc --sbindir=/sbin --datarootdir=/usr/share --mandir=/usr/share/man --includedir=/usr/include

  make
}

package() {
  cd ${_gitname}-build

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/"{etc/{console-tools,default,init.d,lsb-base-logging.sh},usr/share/initramfs-tools}

  install -Dm644 ../splashy.initcpio_install "${pkgdir}/lib/initcpio/install/splashy"
  install -Dm644 ../splashy.initcpio_hook "${pkgdir}/lib/initcpio/hooks/splashy"
  install -Dm644 ../splashy-functions "${pkgdir}/etc/rc.d/splashy-functions"
  install -Dm644 ../splash-initscripts "${pkgdir}/etc/rc.d/functions.d/splash"
  install -Dm644 ../splash.conf "${pkgdir}/etc/splash.conf"

  sed -e 's|>/etc/splashy/themes<|>/usr/share/splashy/themes<|' -i "${pkgdir}/etc/splashy/config.xml"
}