# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=gnome-inform7
pkgver=6G60
pkgrel=4
pkgdesc="GNOME port of the Inform 7 IDE for the Inform 7 programming language for interactive fiction"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/gnome-inform7"
license=('GPL3')
depends=('aspell-en' 'desktop-file-utils' 'gconf' 'goocanvas1' 'gtksourceview2'
         'gtkspell' 'hicolor-icon-theme' 'webkitgtk2')
makedepends=('autoconf>=2.60' 'automake>=1.10' 'graphviz'
             'intltool' 'pkgconfig' 'texlive-core')
options=('!libtool')
install="${pkgname}.install"
source=("http://inform7.com/download/content/${pkgver}/I7_${pkgver}_Linux_all.tar.gz"
        "http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/I7_${pkgver}_GNOME_Source.tar.gz"
        "Makefile.am.patch")
md5sums=('375403e827fcb92bc1d7881c89b97168'
         '224c4f75cecb6e0be60f7beea6e786b5'
         'ef90fa9b30dd6a984ba67d123b5319d5')
[ "${CARCH}" = "i686" ]   && _arch='i386'
[ "${CARCH}" = "x86_64" ] && _arch='x86_64'

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/Makefile.am.patch"
  autoreconf
  bsdtar -C src/ni --strip-components 3 -xf "${srcdir}/inform7-${pkgver}/inform7-compilers_${pkgver}_${_arch}.tar.gz" share/inform7/Compilers/ni
  CFLAGS="${CFLAGS} -fno-strict-aliasing" ./configure --prefix=/usr --sysconfdir=/etc --enable-manuals
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
    "${pkgdir}/etc/gconf/schemas"/*.schemas
  rm -f "${pkgdir}/etc/gconf/schemas"/*.schemas >/dev/null
  cd "${pkgdir}/etc" && rmdir -p gconf/schemas >/dev/null

  chown -R `id -u`:`id -g` "${pkgdir}/usr/share/gnome-inform7"/{Documentation,Extensions}
}

# vim:set ts=2 sw=2 et:
