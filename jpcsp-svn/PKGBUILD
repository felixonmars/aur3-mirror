# Maintainer: Bug <bug2000@gmail.com>
# Contributor: Bug <bug2000@gmail.com>
pkgname=jpcsp-svn
pkgver=2503
pkgrel=1
pkgdesc="JAVA PSP Emulator"
arch=('i686' 'x86_64')
url="http://jpcsp.org/"
license=('GPL')
depends=(jogl)
makedepends=('subversion' 'apache-ant')
provides=('jpcsp')
conflicts=('jpcsp')
install=jpcsp.install
source=('jpcsp.sh' 'jpcsp.desktop')
md5sums=('12651a77193afbf5049720b3dd447d51'
         'cd39aad03798576eb5228ed4184885c8')

_svntrunk=http://jpcsp.googlecode.com/svn/trunk/
_svnmod=jpcsp-read-only

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD HERE
  #
  mv build.xml build-bkp.xml
  mv build-auto.xml build.xml
  /usr/share/java/apache-ant/bin/ant jar
}

package() {
  cd "$srcdir/$_svnmod-build"
  mkdir -p $pkgdir/opt/jpcsp/lib
  rm -R bin/class
  cp -a bin $pkgdir/opt/jpcsp/
  rm -R lib/{macosx,windows-x86,windows-amd64}
  if [ $CARCH == x86_64 ] ; then
    rm -R lib/linux-x86
    cp lib/linux-amd64/liblwjgl64.so lib/linux-amd64/liblwjgl.so
  else #32bit
    rm -R lib/linux-amd64
  fi
   
  cp -a lib/ $pkgdir/opt/jpcsp/
  install -D ../../jpcsp.sh $pkgdir/usr/bin/jpcsp
  install -D ../../jpcsp.desktop $pkgdir/usr/share/applications/jpcsp.desktop
}

# vim:set ts=2 sw=2 et:
