# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=ipxe-git
pkgver=1.0.0.r1343.g6f43ba4
pkgrel=2
pkgdesc="iPXE open source boot firmware - git checkout"
arch=('any')
url="http://www.ipxe.org/"
license=('GPL')
makedepends=('git' 'syslinux' 'cdrkit')
provides=('ipxe')
conflicts=('ipxe')
install=ipxe.install
source=('git://git.ipxe.org/ipxe.git'
	'https://www.kernel.org/pub/linux/utils/boot/syslinux/syslinux-4.07.tar.xz'
	'http://www.eworm.de/download/linux/ipxe_gitversion.patch'
	'http://www.eworm.de/download/linux/ipxe_arch.patch'
	'grub'
	'chain-default.ipxe'
	'chain-default-3928.ipxe'
	'syslinux.patch')
backup=('etc/grub.d/20_ipxe')

pkgver() {
	cd ipxe/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

prepare() {
	cd ipxe/src/

	# Show git version and compile timestamp
	patch -Np2 < ${srcdir}/ipxe_gitversion.patch

	# ArchLinux branding
	patch -Np2 < ${srcdir}/ipxe_arch.patch

	# fix path for syslinux 6.x
	#patch -Np2 < ${srcdir}/syslinux.patch

	# use isolinux from syslinux 4.07 for now
	export ISOLINUX_BIN="${srcdir}/syslinux-4.07/core/isolinux.bin"

	# read and set keymap
	[ -s /etc/rc.conf ] && . /etc/rc.conf
	[ -s /etc/vconsole.conf ] && . /etc/vconsole.conf
	if [ -n "${KEYMAP}" ]; then
		sed -i "/^#define\tKEYBOARD_MAP/c #define KEYBOARD_MAP ${KEYMAP}" config/console.h
	fi

	# change menu colours
	sed -i "/COLOR_[A-Z]*_BG/s/COLOR_BLUE/COLOR_BLACK/" config/colour.h

	# enable neighbour command
	sed -i "/NEIGHBOUR_CMD/c #define NEIGHBOUR_CMD" config/general.h

	# enable reboot and poweroff
	sed -i -e "/REBOOT_CMD/c #define REBOOT_CMD" \
		-e "/POWEROFF_CMD/c #define POWEROFF_CMD" config/general.h
}

build() {
	cd ipxe/src/

	make
	mv bin/undionly.kpxe bin/undionly-dist.kpxe

	make bin/undionly.kpxe EMBED=${srcdir}/chain-default.ipxe
	mv bin/undionly.kpxe bin/undionly-default.kpxe

	make bin/undionly.kpxe EMBED=${srcdir}/chain-default-3928.ipxe
	mv bin/undionly.kpxe bin/undionly-default-3928.kpxe
}

package() {
	cd ipxe/

	install -D -m0755 ${srcdir}/grub ${pkgdir}/etc/grub.d/20_ipxe

	install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/ipxe/COPYING
	install -D -m0644 COPYRIGHTS ${pkgdir}/usr/share/licenses/ipxe/COPYRIGRIGHTS

	cd src/

	install -D -m0644 bin/ipxe.dsk ${pkgdir}/usr/share/ipxe/ipxe.dsk
	install -D -m0644 bin/ipxe.usb ${pkgdir}/usr/share/ipxe/ipxe.usb
	install -D -m0644 bin/ipxe.iso ${pkgdir}/usr/share/ipxe/ipxe.iso

	install -D -m0644 bin/undionly-dist.kpxe ${pkgdir}/usr/lib/ipxe/undionly.kpxe

	install -D -m0644 bin/ipxe.lkrn ${pkgdir}/boot/ipxe/ipxe.lkrn

	install -D -m0644 bin/undionly-default.kpxe ${pkgdir}/usr/lib/ipxe/undionly-default.kpxe
	install -D -m0644 bin/undionly-default-3928.kpxe ${pkgdir}/usr/lib/ipxe/undionly-default-3928.kpxe
}

sha256sums=('SKIP'
            '8e228c34b8a98166a4d42bf0c8ad0ff767ea9ff38dc03b6543248944be6e1887'
            'cb25a959eb741f0123eeb8f8b061e9a625250a38b3b26cc9495613a02e719e3e'
            '7ee090abf5493bfc18524c7abacae02ccd6395d97f76832862ee29605ffdf2f3'
            'b3c9ee1bac1f85f9e8d9455abd94e4a3a0627d2b54ad030e29e247168db2dc83'
            '9f876dbb74f82d2fa3dd20b8160ad1575e7182117d3eb62e02df5df145533d1c'
            'c850aee5f7cd0af4926c44233e7af2887949a872fac51a98247cc26a13b4358a'
            'fe1db92a2c73531916511660ce3d9a781f3108f7d500f5ab3bd6621ffd6e4f2f')
