# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=ipxe-git
pkgver=1.0.0.r1380.g017e6c5
_syslinux_ver="4.07"
pkgrel=3
pkgdesc="iPXE open source boot firmware - git checkout"
arch=('any')
url="http://www.ipxe.org/"
license=('GPL')
makedepends=('git' 'syslinux' 'cdrkit')
provides=('ipxe')
conflicts=('ipxe')
install=ipxe.install
source=('git://git.ipxe.org/ipxe.git'
	"https://www.kernel.org/pub/linux/utils/boot/syslinux/syslinux-${_syslinux_ver}.tar.xz"
	'http://www.eworm.de/download/linux/ipxe_gitversion.patch'
	'http://www.eworm.de/download/linux/ipxe_arch.patch'
	'http://www.eworm.de/download/linux/ipxe_zimage_version.patch'
	'grub'
	'chain-default.ipxe'
	'chain-default-3928.ipxe')
backup=('etc/grub.d/20_ipxe')

pkgver() {
	cd ipxe/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

prepare() {
	cd ipxe/src/

	# Show git version and compile timestamp
	patch -Np2 < ${srcdir}/ipxe_gitversion.patch

	# ArchLinux branding
	patch -Np2 < ${srcdir}/ipxe_arch.patch

	# Add the version string to zImage, it is referenced in multiboot header.
	# This can be extracted later, for example  by the magic tool file.
	patch -Np2 < ${srcdir}/ipxe_zimage_version.patch

	# use isolinux from syslinux 4.07 for now
	export ISOLINUX_BIN="${srcdir}/syslinux-${_syslinux_ver}/core/isolinux.bin"

	# read and set keymap
	[ -s /etc/rc.conf ] && . /etc/rc.conf
	[ -s /etc/vconsole.conf ] && . /etc/vconsole.conf
	if [ -n "${KEYMAP}" ]; then
		sed -i "/^#define\tKEYBOARD_MAP/c #define KEYBOARD_MAP ${KEYMAP}" config/console.h
	fi

	# change menu colours
	sed -i "/COLOR_[A-Z]*_BG/s/COLOR_BLUE/COLOR_BLACK/" config/colour.h

	# enable neighbour command
	sed -i "/NEIGHBOUR_CMD/c #define NEIGHBOUR_CMD" config/general.h

	# enable ping command
	sed -i "/PING_CMD/c #define PING_CMD" config/general.h

	# enable reboot and poweroff
	sed -i -e "/REBOOT_CMD/c #define REBOOT_CMD" \
		-e "/POWEROFF_CMD/c #define POWEROFF_CMD" config/general.h
}

build() {
	cd ipxe/src/

	make
	mv bin/undionly.kpxe bin/undionly-dist.kpxe

	make bin/undionly.kpxe EMBED=${srcdir}/chain-default.ipxe
	mv bin/undionly.kpxe bin/undionly-default.kpxe

	make bin/undionly.kpxe EMBED=${srcdir}/chain-default-3928.ipxe
	mv bin/undionly.kpxe bin/undionly-default-3928.kpxe
}

package() {
	cd ipxe/

	install -D -m0755 ${srcdir}/grub ${pkgdir}/etc/grub.d/20_ipxe

	install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/ipxe/COPYING
	install -D -m0644 COPYRIGHTS ${pkgdir}/usr/share/licenses/ipxe/COPYRIGRIGHTS

	cd src/

	install -D -m0644 bin/ipxe.dsk ${pkgdir}/usr/share/ipxe/ipxe.dsk
	install -D -m0644 bin/ipxe.usb ${pkgdir}/usr/share/ipxe/ipxe.usb
	install -D -m0644 bin/ipxe.iso ${pkgdir}/usr/share/ipxe/ipxe.iso

	install -D -m0644 bin/undionly-dist.kpxe ${pkgdir}/usr/lib/ipxe/undionly.kpxe

	install -D -m0644 bin/ipxe.lkrn ${pkgdir}/boot/ipxe/ipxe.lkrn

	install -D -m0644 bin/undionly-default.kpxe ${pkgdir}/usr/lib/ipxe/undionly-default.kpxe
	install -D -m0644 bin/undionly-default-3928.kpxe ${pkgdir}/usr/lib/ipxe/undionly-default-3928.kpxe
}

sha256sums=('SKIP'
            '8e228c34b8a98166a4d42bf0c8ad0ff767ea9ff38dc03b6543248944be6e1887'
            'cb25a959eb741f0123eeb8f8b061e9a625250a38b3b26cc9495613a02e719e3e'
            '7ee090abf5493bfc18524c7abacae02ccd6395d97f76832862ee29605ffdf2f3'
            '15d048618e3a38e51a52eb8a71120a511812c8a9a5a7e84daba6fb293c33737a'
            'b3c9ee1bac1f85f9e8d9455abd94e4a3a0627d2b54ad030e29e247168db2dc83'
            'f7ec78e26671f4df90d89440d8b2a69473c15cb6b25dda32c773023378fec42a'
            'e26a54b4e99816b34baebcb7a15d99d57c9395c9689ffbae2329cc675248f9b9')
