# Maintainer: Paul Colomiets <paul@colomiets.name>

pkgname=paperjam
pkgver=0.1
pkgrel=1
pkgdesc="A device implementation for zeromq and crossroads io"
arch=('i686' 'x86_64')
url="http://github.com/tailhook/paperjam"
license=('MIT')
# You can remove zeromq or libxs from dependencies
# And also remove `check` function as both are tested
depends=('zeromq' 'libxs')
# python-pyzmq is needed only for tests
makedepends=('coyaml' 'python-pyzmq')
source=(https://github.com/downloads/tailhook/paperjam/$pkgname-$pkgver.tar.gz)
md5sums=('8c89e8de9b82060ba4ba45d7733f4b3a')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./waf configure --prefix=/usr
  ./waf build
}

check() {
  cd $srcdir/$pkgname-$pkgver
  ./waf test
}

package() {
  cd $srcdir/$pkgname-$pkgver
  ./waf install --destdir=$pkgdir
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
