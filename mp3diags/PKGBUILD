# Maintainer: Jordi De Groof <jordi.degroof@gmail.com>
# Contributors: Dan Serban, Dany Martineau, RTFreedman, Harvey

pkgname="mp3diags"
pkgver="1.0.12.079"
pkgrel=1
pkgdesc="Identify issues with your MP3 files, fix those issues and make other changes"
url="http://mp3diags.sourceforge.net/"
license=("GPL")
arch=("i686" "x86_64")
depends=("boost-libs" "qt")
makedepends=("boost")
optdepends=('mp3gain: MP3 normalization support')
install='mp3diags.install'
source=("http://downloads.sourceforge.net/project/mp3diags/mp3diags-src/MP3Diags-${pkgver}.tar.gz")
sha1sums=('dfcdda867215a22820f935e81aa5bb61c4d5fb81')

build() {
	cd "${srcdir}/MP3Diags-${pkgver}"
	./AdjustMt.sh
	qmake
	make
}

package() {
	cd "${srcdir}/MP3Diags-${pkgver}"
	mkdir -p "${pkgdir}/usr/bin" "${pkgdir}/usr/share/applications"
	install -p -m755 "bin/MP3Diags" "${pkgdir}/usr/bin/"
	install -p -m644 "desktop/MP3Diags.desktop" "${pkgdir}/usr/share/applications/"


	for i in "16" "22" "24" "32" "36" "40" "48"; do
		mkdir -p "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"
		install -p -m644 "desktop/MP3Diags${i}.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/MP3Diags.png"
	done
}
