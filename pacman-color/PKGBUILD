# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: vogo <vogo(at)seznam(dot)cz>

pkgname=pacman-color
pkgver=3.5.4
pkgrel=1
pkgdesc="A color patched command-line frontend for libalpm (Pacman)"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman>=3.5' 'pacman<3.6')
backup=('etc/pacman.d/color.conf')
source=("http://mirrors.kernel.org/archlinux/other/pacman/pacman-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.patch" 'color.conf')
md5sums=('2dd3a85d55ee3dd89abd349aa837447c'
         'a99d2c644ac4d43340f52c223c1efef1'
         '47665f5054196c20ba0dd280a8d4c5e1')

build() {
  cd "pacman-${pkgver}"
  patch -p1 -i "${srcdir}/${pkgname}-${pkgver}.patch"
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-doc
  make ${MAKEFLAGS}
}

package() {
  # install pacman-color & color.conf
  install -Dm755 "pacman-${pkgver}/src/pacman/.libs/pacman" "${pkgdir}/usr/bin/pacman-color"
  install -Dm644 'color.conf' "${pkgdir}/etc/pacman.d/color.conf"
}
