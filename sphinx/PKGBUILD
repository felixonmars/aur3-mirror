# Maintainer: dryes <joswiseman@gmail>
# Contributor: Dan Serban
# Contributor: Vishnevsky Roman <aka dot x0x01 at gmail dot com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname='sphinx'
pkgver=2.0.5
pkgrel=1
pkgdesc='Free open-source SQL full-text search engine.'
arch=('i686' 'x86_64')
url='http://www.sphinxsearch.com/'
license=('GPL')
depends=('unixodbc' 'expat' 'libmysqlclient' 'postgresql-libs')
optdepends=('postgresql')
backup=('etc/conf.d/sphinx')
source=("http://sphinxsearch.com/files/${pkgname}-${pkgver}-release.tar.gz" 'sphinx.conf.d' 'sphinx.rc.d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-release"
  ./configure --prefix=/usr --exec-prefix=/usr --localstatedir=/var/lib/sphinx \
    --sysconfdir=/etc/sphinx --with-pgsql --enable-id64

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-release"

  make DESTDIR="${pkgdir}" install

  for _f in "${pkgdir}/usr/bin/"*; do
    ln -s "/usr/bin/${_f##*/}" "${pkgdir}/usr/bin/sphinx-${_f##*/}"
  done

  install -Dm755 "${srcdir}/sphinx.rc.d" "${pkgdir}/etc/rc.d/sphinx"
  install -Dm644 "${srcdir}/sphinx.conf.d" "${pkgdir}/etc/conf.d/sphinx"
}

md5sums=('e71fdb5b0c2911247d48fb30550b9584'
         '48e3e1857919d26d5104a48caffb531b'
         'd886222593c6bba891fd3ce982c90c00')
