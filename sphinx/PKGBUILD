# Maintainer: dryes <joswiseman@gmail>
# Contributor: Dan Serban
# Contributor: Vishnevsky Roman <aka dot x0x01 at gmail dot com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname='sphinx'
pkgver=2.0.4
pkgrel=4
pkgdesc='Free open-source SQL full-text search engine.'
arch=('i686' 'x86_64')
url='http://www.sphinxsearch.com/'
license=('GPL')
depends=('unixodbc' 'expat' 'libmysqlclient' 'postgresql-libs')
optdepends=('postgresql')
backup=('etc/conf.d/sphinx')
source=("http://sphinxsearch.com/files/${pkgname}-${pkgver}-release.tar.gz" 'sphinx.conf.d' 'sphinx.rc.d')

build() {
  sed -i 's/T val = ExprEval/T val = this->ExprEval/g' "${srcdir}/${pkgname}-${pkgver}-release/src/sphinxexpr.cpp"
  sed -i '15083,15083 s/x00/x21/' "${srcdir}/${pkgname}-${pkgver}-release/src/searchd.cpp"

  cd "${srcdir}/${pkgname}-${pkgver}-release"
  ./configure --prefix=/usr --exec-prefix=/usr --program-prefix='sphinx-' \
    --localstatedir=/var/lib/sphinx --sysconfdir=/etc/sphinx --with-pgsql --enable-id64
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-release"
  make DESTDIR="${pkgdir}" install
  install -Dm755 "${srcdir}/sphinx.rc.d" "${pkgdir}/etc/rc.d/sphinx"
  install -Dm644 "${srcdir}/sphinx.conf.d" "${pkgdir}/etc/conf.d/sphinx"
}

md5sums=('7da4df3df3decb24d8c6fb8f47de1d3d'
         '48e3e1857919d26d5104a48caffb531b'
         'd886222593c6bba891fd3ce982c90c00')
