# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=xbmc-eventclients-wiiremote
pkgver=12.2
pkgrel=2
pkgdesc="XBMC Media Center Wiimote (Wii Remote) Eventclient"
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL3')
depends=('xbmc' 'wiiuse' 'bluez4')
source=("http://mirrors.xbmc.org/releases/source/xbmc-$pkgver.tar.gz"
        "xbmc-wiiremote.service")

build() {
    cd "${srcdir}/xbmc-$pkgver/tools/EventClients/Clients/WiiRemote"

    # instructions from WiiUse_README
    g++ -lwiiuse WiiUse_WiiRemote.cpp -o WiiUse_WiiRemote
}

package() {
    cd "${srcdir}/xbmc-$pkgver/tools/EventClients/Clients/WiiRemote"

    install -Dm755 WiiUse_WiiRemote "${pkgdir}"/usr/bin/xbmc-wiiremote
    install -Dm644 "${srcdir}"/xbmc-$pkgver/docs/manpages/xbmc-wiiremote.1 \
        "${pkgdir}"/usr/share/man/man1/xbmc-wiiremote.1
    install -Dm755 "${srcdir}"/xbmc-wiiremote.service \
        "${pkgdir}"/usr/lib/systemd/system/xbmc-wiiremote.service
}

md5sums=('489f3877decae4e265ece54f9eaef0ba'
         '6a5a60510a05119161ffb7ac8c5b53e7')
