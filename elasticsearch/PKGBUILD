# Maintainer: Eugen Zagorodniy <https://github.com/ezag>
pkgname=elasticsearch
pkgver=0.19.11
pkgrel=3
pkgdesc="Distributed RESTful search engine built on top of Lucene"
arch=('i686' 'x86_64')
url="http://www.elasticsearch.org/"
license=('APACHE')
depends=('java-runtime')
install='elasticsearch.install'
source=(
  "https://github.com/downloads/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
  elasticsearch.service
)
md5sums=('c914469c1f7d39d7aae50b36c4347b64'
	 '06200c889036bdd3e77db9e7b025fbac')

build() {
  cd $srcdir
  wget https://github.com/$pkgname/$pkgname-servicewrapper/tarball/master \
       -O $pkgname-servicewrapper.tar.gz --no-check-certificate
  tar xf $pkgname-servicewrapper.tar.gz
  mv *-$pkgname-servicewrapper-*/service $srcdir/$pkgname-$pkgver/bin/
}

package() {
  mkdir $pkgdir/opt
  mv $srcdir/$pkgname-$pkgver $pkgdir/opt/$pkgname
  cd $pkgdir/opt/$pkgname
  mkdir lock

  # Remove unnesessary libraries
  case $CARCH in
    i686) _bits=32 ;;
    x86_64) _bits=64 ;;
    *) echo "Unknown architecture '$CARCH'"; exit 1
  esac
  find lib/sigar/* ! -name '*.jar' ! -name '*x86-linux.so' -delete
  find bin/service/lib/* ! -name '*.jar' ! -name "*linux-x86-$_bits.so" -delete
  find bin/service/exec/* ! -name "*linux-x86-$_bits" -delete

  # Configure service wrapper
  sed -e '1 a . /etc/profile'                                           \
      -e 's/^#\(RUN_AS_USER=\).*$/\1elasticsearch/'                     \
      -e "/^WRAPPER_TEST_CMD=\"\"$/iDIST_ARCH=x86; DIST_BITS=$_bits"    \
      -i bin/service/elasticsearch

  # Fix log file location
  # See https://github.com/elasticsearch/elasticsearch-servicewrapper/issues/8
  sed -e 's/\(wrapper\.logfile\=\).*$/\1%ES_HOME%\/logs\/service.log/'  \
      -i bin/service/elasticsearch.conf

  # Set proper permissions
  chmod +x bin/service/exec/*

  # Install systemd script
  install -Dm644 "$srcdir/elasticsearch.service" "$pkgdir/usr/lib/systemd/system/elasticsearch.service"
}

