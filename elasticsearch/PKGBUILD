# Maintainer: Marcello "mererghost" Rocha <https://github.com/mereghost>
# Refactored by Bla≈æ "Speed" Hrastnik <https://github.com/archSeer>
pkgname=elasticsearch
pkgver=0.90.1
pkgrel=1
pkgdesc="Distributed RESTful search engine built on top of Lucene"
arch=('i686' 'x86_64')
url="http://www.elasticsearch.org/"
license=('APACHE')
depends=('java-runtime')
install='elasticsearch.install'
source=(
  "http://download.elasticsearch.org/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
  elasticsearch.service
)

backup=('etc/elasticsearch/elasticsearch.yml'
        'etc/elasticsearch/logging.yml')

build() {
  # Remove unnesessary libraries
  cd "$srcdir/$pkgname-$pkgver"
  find lib/sigar/* ! -name '*.jar' ! -name '*x86-linux.so' -delete

  # Edit the values of the config files
  sed -e 's/# \(path\.conf: \).*$/\1\/etc\/elasticsearch/' \
  -e '0,/# \(path\.data: \).*$/s//\1\/var\/lib\/elasticsearch/' \
	-e 's/# \(path\.work: \).*$/\1\/tmp\/elasticsearch/' \
	-e 's/# \(path\.logs: \).*$/\1\/var\/log\/elasticsearch/' \
	-i config/elasticsearch.yml
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # creates the necessary dirs
  install -dm755 "$pkgdir/usr/share/elasticsearch/bin"
  install -dm755 "$pkgdir/etc/elasticsearch"
  install -dm755 "$pkgdir/var/lib/elasticsearch"
  install -dm755 "$pkgdir/var/log/elasticsearch"

  cp config/* "$pkgdir/etc/elasticsearch/"
  cp -r {bin,lib}   "$pkgdir/usr/share/elasticsearch"
	
  # install systemd script
  install -Dm644 "$srcdir/elasticsearch.service" "$pkgdir/usr/lib/systemd/system/elasticsearch.service"
}

md5sums=('973e1e8a965ab9e22d434bdc54113760'
         '369ca068da004f702842e2d4293e3723')
