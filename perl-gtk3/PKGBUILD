# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=perl-gtk3
_pkgname=Gtk3
pkgver=0.006
pkgrel=1
pkgdesc="Perl interface to the 3.x series of the Gimp Toolkit library"
arch=('any')
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('perl-glib-object-introspection' 'perl-cairo-gobject' 'gtk3')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/$_pkgname-$pkgver.tar.gz)
md5sums=('f0fdf9cc4c715ddc9548ba36be16d56e')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

