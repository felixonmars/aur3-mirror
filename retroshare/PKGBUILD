# Maintainer: stqn
# Contributor: JHeaton <jheaton at archlinux dot us>
# Contributor: Tristero <tristero at online dot de>
# Contributor: funkyou

pkgname=retroshare
pkgver=0.5.3c
pkgrel=1
pkgdesc="Serverless encrypted instant messenger with filesharing, chatgroups, e-mail."
arch=('i686' 'x86_64')
url="http://retroshare.sourceforge.net/"
license=('LGPL' 'GPL')
depends=('qt' 'libupnp' 'gpgme' 'libgnome-keyring' 'libxss')
install="${pkgname}.install"

source=(http://sourceforge.net/projects/retroshare/files/RetroShare/${pkgver}/RetroShare-v${pkgver}.tar.gz \
	#unistd_patch.diff \
        ${pkgname}.sh \
        ${pkgname}.install \
        ${pkgname}.desktop)

sha256sums=('ed4ba0edb03350aacbc8e31a2ba7e62a3c25b352ac94876fbcf5d3e25701b0bb'
            '8c2d5808026235ee718d48faccc62e511ec32c9bec82fcc0ed970bfc531eebbf'
            '4b50547648612e9091536205402a4da9ddea9c18c0f71e5d6cd30b2226f206d9'
            '70be00968f2477e368f75393f193e76f366fff2dadab869c855e92048060cf29')

# Setting this to true activates the compilation and packaging
# of the plugins which are not yet officially released.
# NOTE: doesn't work at the moment.
_USE_PLUGINS=false

# Setting this to true activates the compilation and packaging
# of retroshare-nogui
_USE_NOGUI=false

build() {
  cd "${srcdir}"

  #msg "Patching..."
  #patch -p0 <../unistd_patch.diff

  msg "Compiling libbitdht..."
  cd "${srcdir}/trunk/libbitdht/src"
  qmake
  make

  msg "Compiling libretroshare..."
  cd "${srcdir}/trunk/libretroshare/src"
  qmake
  make

  if [[ $_USE_PLUGINS == "true" ]] ; then
    msg "Compiling retroshare plugins..."
    cd "${srcdir}/trunk/plugins"
    qmake
    make
  fi

  msg "Compiling retroshare-gui..."
  cd "${srcdir}/trunk/retroshare-gui/src"
  # Add missing libs
  sed -i "s/-lssl -lgpgme -lupnp -lixml -lXss -lgnome-keyring/-lssl -lcrypto -ldl -lX11 -lgpgme -lupnp -lixml -lXss -lgnome-keyring/" RetroShare.pro
  qmake
  make

  if [[ $_USE_NOGUI == "true" ]] ; then
    msg "Compiling retroshare-nogui..."
    cd "${srcdir}/trunk/retroshare-nogui/src"
    # Add missing libs
    sed -i "s/-lssl -lgpgme -lupnp -lixml -lgnome-keyring/-lssl -lcrypto -ldl -lgpgme -lupnp -lixml -lgnome-keyring/" retroshare-nogui.pro
    qmake
    make
  fi
}

package() {

  # --- Install Files ---

  msg "Installing files to fakeroot-environment..."

  # - Binaries
  # Not installing the static libraries since there's no SDK anyway
  #install -D -m 644 \
  #  "${srcdir}/trunk/libbitdht/src/lib/libbitdht.a" \
  #  "${pkgdir}/usr/lib/libbitdht.a"
  #install -D -m 644 \
  #  "${srcdir}/trunk/libretroshare/src/lib/libretroshare.a" \
  #  "${pkgdir}/usr/lib/libretroshare.a"

  install -D -m 755 \
    "${srcdir}/trunk/retroshare-gui/src/RetroShare" \
    "${pkgdir}/opt/${pkgname}/${pkgname}"

  install -D -m 755 \
    "${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"

  if [[ $_USE_NOGUI == "true" ]] ; then
    install -D -m 755 \
	"${srcdir}/trunk/retroshare-nogui/src/retroshare-nogui" \
        "${pkgdir}/usr/bin/${pkgname}-nogui"
  fi

  # plugins

  if [[ $_USE_PLUGINS == "true" ]] ; then
    # missing : LinksCloud
    install -D -m 755 \
	"${srcdir}/trunk/plugins/bin/libcalendar_plugin.so" \
	"${pkgdir}/usr/lib/retroshare/plugins/libcalendar_plugin.so"
    install -D -m 755 \
	"${srcdir}/trunk/plugins/bin/libpuzzle_plugin.so" \
	"${pkgdir}/usr/lib/retroshare/plugins/libpuzzle_plugin.so"
    install -D -m 755 \
	"${srcdir}/trunk/plugins/bin/libqcheckers_plugin.so" \
	"${pkgdir}/usr/lib/retroshare/plugins/libqcheckers_plugin.so"
    install -D -m 755 \
	"${srcdir}/trunk/plugins/bin/libqdiagram_plugin.so" \
	"${pkgdir}/usr/lib/retroshare/plugins/libqdiagram_plugin.so"
  fi

  # Style sheets
  cp -r "${srcdir}/trunk/retroshare-gui/src/qss" "$pkgdir/opt/retroshare/"
  chmod 755 "$pkgdir/opt/retroshare/qss"

  # Icons
  #install -D -m 644 \
  #  "${srcdir}/trunk/retroshare-gui/src/gui/images/retrosharelogo1.png" \
  #  "${pkgdir}/usr/share/pixmaps/retroshare_blue.png"
  install -D -m 644 \
    "${srcdir}/trunk/retroshare-gui/src/gui/images/retrosharelogo2.png" \
    "${pkgdir}/usr/share/pixmaps/retroshare.png"

  # Desktop File
  install -D -m 644 \
    "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # bdboot (needed to bootstrap the DHT)
  install -D -m 644 \
    "${srcdir}/trunk/libbitdht/src/bitdht/bdboot.txt" \
    "${pkgdir}/usr/share/RetroShare/bdboot.txt"
}
