# Maintainer: drrossum <d.r.vanrossum[at]gmx dot de>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: trapanator <trap[at]trapanator[dot]com>
# Contributor: tomprogrammer <Thomas-Bahn[at]gmx[dot]net>

pkgname=dkms-phc-intel
pkgver=0.3.2.12.12
_realver=pack-rev12
pkgrel=1
pkgdesc="frequency driver for Intel CPUs with undervolting feature"
url="http://www.linux-phc.org"
arch=('any')
license=('GPL')
provides=('linux-phc')
depends=('dkms')
backup=(etc/default/phc-intel)
install=dkms-phc-intel.install
source=(phc-intel-$_realver.tar.bz2::$url/forum/download/file.php?id=149
        phc-intel.{default,sh,sleep,system-sleep}
        dkms.conf)
sha256sums=('96dd605e8c2062e87435afc7f2199419e3d3b1596e180462dcf2f704534ba710'
            'ce08a5a4107be1d5723f1f169d515e67b6c77893f3994fc2d0d2ccf611307ed3'
            '2eece37e251cd18163c1d6f0bdf83925ecc5b14cc4c9f1e1bf4fa26b4dca1507'
            '569b85988cb38380fec85c25688b76abc24a46601aa8f58eb24eaebf863eebef'
            '2e17c90d7bfae8f5070e46388e95d443188eaa7beb5ffdd418a0da090f2e7557'
            'f56356450be09dfbe93319d283826d7fb1e3c5e08f8793a62c3be4d11686b564')

prepare() {
	cd phc-intel-$_realver
	sed -e 's,$(DESTDIR)/lib/modules/$(KERNELRELEASE)/extra$,$(DESTDIR)/usr/lib/modules/$(KERNELRELEASE)/extramodules,' \
	    -e 's,^\tinstall -m 644 -o root -g root phc-intel.modprobe,#\tinstall -m 644 -o root -g root phc-intel.modprobe,' -i Makefile
	sed -i 's,/sbin/modprobe phc-intel |,/sbin/modprobe phc-intel \&\& /usr/bin/phc-intel set |,' phc-intel.modprobe
}

package() {
	install -Dm644 phc-intel.default "$pkgdir/etc/default/phc-intel"
	install -Dm755 phc-intel.sh "$pkgdir/usr/bin/phc-intel"
	install -Dm755 phc-intel.sleep "$pkgdir/usr/lib/pm-utils/sleep.d/00phc-intel"
	install -Dm755 phc-intel.system-sleep "$pkgdir/usr/lib/systemd/system-sleep/phc-intel"

 	install -d ${pkgdir}/usr/src/${pkgname}-${pkgver}
	cp dkms.conf ${pkgdir}/usr/src/${pkgname}-${pkgver}

	cd phc-intel-$_realver
	install -Dm644 phc-intel.modprobe "$pkgdir/usr/lib/modprobe.d/phc-intel.conf"
	install -d "$pkgdir/usr/src/phc-intel/"
	cp -R inc Makefile "$pkgdir/usr/src/phc-intel/"
}
