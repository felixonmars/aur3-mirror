# Maintainer: Garrett <floft.net/contact>
pkgname=cinelerra-heroine
pkgver=4.5
pkgrel=3
pkgdesc="Cinelerra from the HeroineWarrior.com (64-bit)"
arch=('x86_64')
url="http://www.heroinewarrior.com/cinelerra.php"
license=('GPL')
depends=('libpng' 'freeglut' 'libxv' 'ncurses' 'libva' 'openexr')
makedepends=('yasm' 'nasm' 'libtool')
conflicts=('cinelerra-cv')
source=(https://sourceforge.net/projects/heroines/files/cinelerra-$pkgver-src.tar.xz
        quicktime.patch
        plugin_dir.patch
        speech_tools.patch
        texi2html.patch
        gpac.patch
        libavcodec.patch
        cinelerra.patch
        cinelerra.desktop)
md5sums=('99b161b92636a81ec19a8e158b487e1f'
         '0babae23875017653914bd121477ec97'
         'cf9861e808948fe8ea4d4be27765ffa5'
         '1d15ee1b2e783a4f8ad8c02d53a77439'
         '431503db853032e14ade0438ed3eea29'
         'cefa6a8efe5ba5bc00ad9fc5649dd25c'
         '4dd7fd1184dfe3813a8821d47ef70a55'
         '2b62004366f8eaa7f27f1981765205b1'
         '54ee50b75e5318509da5d4e8fa55caf3')

prepare() {
  cd "$srcdir/cinelerra-$pkgver"

  # For some reason fails to build when using -Wl,--as-needed
  export LDFLAGS="$LDFLAGS -Wl,--no-as-needed"

  # Use ncurses instead of termcap
  sed -i 's/ltermcap/lncurses/g' ./thirdparty/speech_tools/{configure{,.in},config/config}

  # fixes to make it compile
  patch -Np1 -i "$srcdir/quicktime.patch"
  patch -Np1 -i "$srcdir/speech_tools.patch"
  patch -Np1 -i "$srcdir/texi2html.patch"
  patch -Np1 -i "$srcdir/gpac.patch"
  patch -Np1 -i "$srcdir/libavcodec.patch"
  patch -Np1 -i "$srcdir/cinelerra.patch"
  
  # fix problems with Cinelerra_plugins in read-only directories
  patch -Np1 -i "$srcdir/plugin_dir.patch"
}

build() {
  cd "$srcdir/cinelerra-$pkgver"

  ./configure
  make
}

package() {
  cd "$srcdir/cinelerra-$pkgver"

  # makes repackaging work
  [ ! -e "bin/cinelerra" ] && \
  	make -C cinelerra && \
	make -C plugins

  make install

  # install puts it in bin/
  rm -rf "$pkgdir/usr/lib/cinelerra/"
  mkdir -p "$pkgdir/usr/lib/cinelerra/"
  mv bin/* "$pkgdir/usr/lib/cinelerra/"

  mkdir -p "$pkgdir/usr/bin/"
  ln -s /usr/lib/cinelerra/cinelerra "$pkgdir/usr/bin/cinelerra"

  mkdir -p "$pkgdir/usr/share/applications/"
  install -Dm644 "$srcdir/cinelerra.desktop" "$pkgdir/usr/share/applications/"
}
