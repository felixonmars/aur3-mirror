# Maintainer: Pio <xpio at tut dot by>
pkgname=mrrescue
_name='Mr. Rescue'
pkgver=1.02b
pkgrel=4
pkgdesc="An arcade styled 2d action game centered around evacuating civilians from burning buildings."
arch=(any)
url="http://tangramgames.dk/games/mrrescue/"
license=('CCPL:by-sa' 'zlib')
depends=(love08)
makedepends=('gendesk')
source=(https://github.com/SimonLarsen/mrrescue/releases/download/v1.02b/${pkgname}-${pkgver}-love.zip)
md5sums=('2c9d3e6a2d17ffeeb02a68a076988d14')

build() {
	# Generate desktop file
	cd "${srcdir}"
	gendesk -n

	# Generate run script
	echo 'love08 /usr/share/mrrescue/mrrescue.love' > ${pkgname}.sh
}

package() {
	# Copy game, readme and license
	cd "${srcdir}/${pkgname}-${pkgver}-love"
	install -Dm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README.txt"
	install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
	install -Dm644 ${pkgname}.love "${pkgdir}/usr/share/${pkgname}/${pkgname}.love"
	# Extract an image to be used as icon
	unzip -p mrrescue.love data/splash.png > ../mrrescue.png
	# Copy desktop file, icon and run script
	install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	install -D ../${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
}
