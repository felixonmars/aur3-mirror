# Maintainer: Pio <xpio at tut dot by>
pkgname=mrrescue
_name='Mr. Rescue'
pkgver=1.02b
_filenamever='1.02-love'
pkgrel=1
pkgdesc="An arcade styled 2d action game centered around evacuating civilians from burning buildings."
arch=(any)
url="http://tangramgames.dk/games/mrrescue/"
license=('CCPL:by-nc-sa' 'zlib')
depends=(love)
makedepends=('gendesk')
source=(${pkgname}.png::https://secure.gravatar.com/avatar/28e0ad8d1093c9f9868e9bd3e99818c6
        https://bitbucket.org/SimonLarsen/tangram-files/downloads/mrrescue-1.02b-love.zip)

md5sums=('c24689257dc1c588d6336db221a410f8'
         '3510432cb3ff6afa12f4ee7d1deed0a4')

build() {
	# Generate desktop file
	cd "${srcdir}"
	gendesk -n

	# Generate run script
	echo 'love /usr/share/mrrescue/mrrescue.love' > ${pkgname}.sh
}

package() {
	# Copy game, readme and license
	cd "${srcdir}/${pkgname}-${_filenamever}"
	install -Dm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README.txt"
	install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
	install -Dm644 ${pkgname}.love "${pkgdir}/usr/share/${pkgname}/${pkgname}.love"

	# Copy desktop file, icon and run script
	install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	install -D ../${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
}
