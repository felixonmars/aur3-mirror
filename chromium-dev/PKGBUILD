# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Mikhail Vorozhtsov <mikhail.vorozhtsov@gmail.com>
# Contributor: Nagisa <simonas@kazlauskas.me>
# Contributor: Misc <andreas.reis@gmail.com>
# Contributor: Jeagoss <jgoliver@jeago.com>

#########################
## -- Build options -- ##
#########################

_use_aura=0            # Use Aura native decoration
_use_clang=0           # Use clang compiler. Results in faster build and smaller chromium.
_use_libpdf=1          # Enable Chrome PDF support.
_use_pax=0             # If set 1, change PaX permisions in executables NOTE: only use if use PaX environment (IN TEST)
_use_pepperflash=1     # Use Pepper Flash plugin. Depends on Native Client!
_use_unittest=0        # Run unit test? # IN TEST, DON'T ACTIVE

##############################################
## -- Package and components information -- ##
##############################################
pkgname=chromium-dev
pkgver=33.0.1734.0
_toolchains_rev=12388
pkgrel=1
pkgdesc='The open-source project behind Google Chrome (Dev channel)'
arch=('i686' 'x86_64')
url="http://www.chromium.org/"
license=('BSD')
depends=('alsa-lib' 'desktop-file-utils' 'gtk2' 'harfbuzz-icu' 'hicolor-icon-theme' 'jsoncpp' 'libevent' 'libxslt' 'libxss' 'minizip' 
         'nss' 'opus' 'pciutils' 'protobuf' 're2' 'snappy' 'speech-dispatcher' 'speex' 'ttf-font' 'udev' 'xdg-utils') # 'libwebp'  'libvpx'
makedepends=('elfutils' 'gperf' 'mesa' 'ninja' 'perl-json' 'python2-jinja' 'python2-ply' 'python2-simplejson' 'subversion' 'yasm' 'depot_tools-svn') # 'libsrtp'
[ "${CARCH}" = "x86_64" ] && makedepends+=('lib32-gcc-libs' 'lib32-zlib') && warning "To build need active [multilib] repository, if not , fail build"
[ "${_use_pax}" = "1" ] && makedepends+=('paxctl')
options=('!strip')
install="chromium-dev.install"
backup=('etc/chromium-dev/default')
source=(
#        "https://gsdview.appspot.com/chromium-browser-official/chromium-${pkgver}.tar.xz"
        "https://commondatastorage.googleapis.com/chromium-browser-official/chromium-${pkgver}.tar.xz"
        'chromium-dev.desktop'
        'chromium-dev.sh'
        'default'
        # Patch form Gentoo
        'http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/www-client/chromium/files/chromium-system-jinja-r2.patch'
        'http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/www-client/chromium/files/chromium-build_ffmpeg-r0.patch'
        # Misc Patches
        # none
        # Nacl Components
        "naclsdk_nacl_linux_x86-${_toolchains_rev}.tgz::https://gsdview.appspot.com/nativeclient-archive2/toolchain/${_toolchains_rev}/naclsdk_linux_x86.tgz"
        "naclsdk_pnacl_linux_x86-${_toolchains_rev}.tgz::https://gsdview.appspot.com/nativeclient-archive2/toolchain/${_toolchains_rev}/naclsdk_pnacl_linux_x86.tgz"
        "naclsdk_pnacl_translator-${_toolchains_rev}.tgz::https://gsdview.appspot.com/nativeclient-archive2/toolchain/${_toolchains_rev}/naclsdk_pnacl_translator.tgz"
        "naclsdk_pnacl_translator-${_toolchains_rev}.tgz.sha1hash::https://gsdview.appspot.com/nativeclient-archive2/toolchain/${_toolchains_rev}/naclsdk_pnacl_translator.tgz.sha1hash")
sha1sums=("$(curl -sL "https://gsdview.appspot.com/chromium-browser-official/chromium-${pkgver}.tar.xz.hashes" | grep sha1 | cut -d " " -f3)"
          '40a4e41ffb73c9d7845ce8bcdb06c6e0675ec663'
          '22cb5f2044120ea0be5799fc2181a8ff189be512'
          'd6d2da45c0729dfd1c606a15c8ffb7591dbc7b44'
          # Patch form Gentoo
          'fe72370222a189fd067da8b58e714e25769aae63'
          '4f1ff72063d360a24f33352dd6ce71d68a2808c0'
          # Misc Patches
          # none
          # Nacl Components
          "$(curl -sL "https://gsdview.appspot.com/nativeclient-archive2/toolchain/${_toolchains_rev}/naclsdk_linux_x86.tgz.sha1hash")"
          "$(curl -sL "https://gsdview.appspot.com/nativeclient-archive2/toolchain/${_toolchains_rev}/naclsdk_pnacl_linux_x86.tgz.sha1hash")"
          "$(curl -sL "https://gsdview.appspot.com/nativeclient-archive2/toolchain/${_toolchains_rev}/naclsdk_pnacl_translator.tgz.sha1hash")"
          'b986dce8383aa93663ca749e01227cabf114320f')
noextract=("naclsdk_nacl_linux_x86-${_toolchains_rev}.tgz"
           "naclsdk_pnacl_linux_x86-${_toolchains_rev}.tgz"
           "naclsdk_pnacl_translator-${_toolchains_rev}.tgz")

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact foutrelis@archlinux.org for
# more information.
_google_api_key="AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM"
_google_default_client_id="413772536636.apps.googleusercontent.com"
_google_default_client_secret="0ZChLK6AxeA3Isu96MkwqDR4"

################################################
## -- Don't touch anything below this line -- ##
################################################

[ "${CARCH}" = "i686" ] && _rpm_arch='i386' && _target_arch='ia32'
[ "${CARCH}" = "x86_64" ] && _rpm_arch='x86_64' && _target_arch='x64'

# Pepper Flash Plugin and/or libpdf?
if [ "${_use_pepperflash}" = "1" -o "${_use_libpdf}" = "1" ]; then
  rm -f filelists.xml*
  curl -sL "https://dl.google.com/linux/chrome/rpm/stable/${_rpm_arch}/repodata/filelists.xml.gz" | gzip -d > filelists.xml
  _rpm_build="$(grep -e "unstable" filelists.xml | cut -d '"' -f12 | head -n 1)"
  _rpm_ver="$(grep -e "unstable" filelists.xml | cut -d '"' -f10 | head -n 1)"
  _rpm_sha1sum="$(grep -e "unstable" filelists.xml | cut -d '"' -f2 | head -n 1)"
  rm -f filelists.xml*

  source+=("https://dl.google.com/linux/chrome/rpm/stable/${_rpm_arch}/google-chrome-unstable-${_rpm_ver}-${_rpm_build}.${_rpm_arch}.rpm")
  sha1sums+=("${_rpm_sha1sum}")
  noextract+=("google-chrome-unstable-${_rpm_ver}-${_rpm_build}.${_rpm_arch}.rpm")
fi

[ "${_use_pepperflash}" = "1" ] && provides+=('chromium-pepper-flash') && conflicts+=('chromium-pepper-flash')
[ "${_use_libpdf}" = "1" ] && provides+=('chromium-libpdf') && conflicts+=('chromium-libpdf')

# Unit Test sources
if [ "${_use_unittest}" = "1" ]; then
  source+=(
#           "https://gsdview.appspot.com/chromium-browser-official/chromium-${pkgver}-testdata.tar.xz")
           "https://commondatastorage.googleapis.com/chromium-browser-official/chromium-${pkgver}-testdata.tar.xz")
  sha1sums+=("$(curl -sL h"ttps://gsdview.appspot.com/chromium-browser-official/chromium-${pkgver}-testdata.tar.xz.hashes" | grep sha1 | cut -d " " -f3)")
  makedepends+=('python2-pyftpdlib')
  _unittest="base_unittests cacheinvalidation_unittests content_unittests crypto_unittests gpu_unittests net_unittests printing_unittests sql_unittests"
fi


# Are we in Gnome?
_use_gnome=0
if [ -f /usr/lib/libgnome-keyring.so ]; then
  depends+=('libgnome-keyring')
  _use_gnome=1
fi

# Use Pulseaudio?
_use_pulseaudio=0
if [ -x /usr/bin/pulseaudio ]; then
  _use_pulseaudio=1
fi

prepare() {
  ##############################
  ## -- Get ready to build -- ##
  ##############################

  cd "${srcdir}"

  rm -rf chromium
  sync
  mv "chromium-${pkgver}" chromium
  sync
  cd chromium

  msg2 "Fix to save configuration in ~/.config/chromium-dev"
  sed -e "s|'filename': 'chromium-browser'|'filename': 'chromium-dev'|" \
      -e "s|'confdir': 'chromium'|'confdir': 'chromium-dev'|" \
      -i chrome/chrome_exe.gypi
  sed -e 's|config_dir.Append("chromium")|config_dir.Append("chromium-dev")|' \
      -e 's|config_dir.Append("chrome-frame")|config_dir.Append("chrome-frame-dev")|' \
      -i chrome/common/chrome_paths_linux.cc

  msg2 "Remove unnecesary components to save space"
  # Remove most bundled libraries. Some are still needed.
  python2 build/linux/unbundle/remove_bundled_libraries.py \
	'base/third_party/dmg_fp' \
	'base/third_party/dynamic_annotations' \
	'base/third_party/icu' \
	'base/third_party/nspr' \
	'base/third_party/symbolize' \
	'base/third_party/valgrind' \
	'base/third_party/xdg_mime' \
	'base/third_party/xdg_user_dirs' \
	'breakpad/src/third_party/curl' \
	'chrome/third_party/mozilla_security_manager' \
	'crypto/third_party/nss' \
	'native_client/src/third_party/dlmalloc' \
	'net/third_party/mozilla_security_manager' \
	'net/third_party/nss' \
	'third_party/WebKit' \
	'third_party/angle_dx11' \
	'third_party/cacheinvalidation' \
	'third_party/cld' \
	'third_party/cros_system_api' \
	'third_party/ffmpeg' \
	'third_party/flot' \
	'third_party/hunspell' \
	'third_party/iccjpeg' \
	'third_party/jstemplate' \
	'third_party/khronos' \
	'third_party/leveldatabase' \
	'third_party/libaddressinput' \
	'third_party/libjingle' \
	'third_party/liblouis' \
	'third_party/libphonenumber' \
	'third_party/libsrtp' \
	'third_party/libusb' \
	'third_party/libvpx' \
	'third_party/libwebp' \
	'third_party/libxml/chromium' \
	'third_party/libXNVCtrl' \
	'third_party/libyuv' \
	'third_party/lss' \
	'third_party/lzma_sdk' \
	'third_party/markupsafe' \
	'third_party/mesa' \
	'third_party/modp_b64' \
	'third_party/mt19937ar' \
	'third_party/npapi' \
	'third_party/ots' \
	'third_party/polymer' \
	'third_party/pywebsocket' \
	'third_party/qcms' \
	'third_party/readability' \
	'third_party/sfntly' \
	'third_party/skia' \
	'third_party/smhasher' \
	'third_party/sqlite' \
	'third_party/tcmalloc' \
	'third_party/tlslite' \
	'third_party/trace-viewer' \
	'third_party/undoview' \
	'third_party/usrsctp' \
	'third_party/webdriver' \
	'third_party/webrtc' \
	'third_party/widevine' \
	'third_party/x86inc' \
	'third_party/zlib/google' \
	'tools/gn/secondary' \
	'url/third_party/mozilla' \
	'v8/src/third_party/valgrind' \
	--do-remove
	
  msg2 "Misc patches"

  # Make it possible to remove third_party/adobe
  echo > "${srcdir}/flapper_version.h"

  # Patch sources
  patch -p0 -i "${srcdir}/chromium-system-jinja-r2.patch"
  patch -p1 -i "${srcdir}/chromium-build_ffmpeg-r0.patch"

  # Compiler specific configuration...
  if [ "${_use_clang}" = "1" ]; then
    msg2 "Download clang"
    sh tools/clang/scripts/update.sh
  fi

  msg2 "Fix to really use python2."
  rm -rf "${srcdir}/python"
  mkdir "${srcdir}/python"
  ln -s /usr/bin/python2 "${srcdir}/python/python"
  export PATH="${srcdir}/python":$PATH
  find . -type f -exec sed -i -r -e 's|/usr/bin/python$|&2|g' -e 's|(/usr/bin/python2)\.4$|\1|g' {} \;

  msg2 "Prepare NaCl SDK"
  mkdir native_client/toolchain/{.tars,pnacl_translator}
  ln -s "${srcdir}/naclsdk_nacl_linux_x86-${_toolchains_rev}.tgz" native_client/toolchain/.tars/naclsdk_linux_x86.tgz
  ln -s "${srcdir}/naclsdk_pnacl_linux_x86-${_toolchains_rev}.tgz" native_client/toolchain/.tars/naclsdk_pnacl_linux_x86.tgz
  ln -s "${srcdir}/naclsdk_pnacl_translator-${_toolchains_rev}.tgz" native_client/toolchain/.tars/naclsdk_pnacl_translator.tgz
  ln -s "${srcdir}/naclsdk_pnacl_translator-${_toolchains_rev}.tgz.sha1hash" native_client/toolchain/pnacl_translator/SOURCE_SHA1

  # Re-configure bundled ffmpeg
  msg2 "Configuring bundled ffmpeg..."
  pushd third_party/ffmpeg > /dev/null
  chromium/scripts/build_ffmpeg.sh linux ${_target_arch} "${srcdir}/chromium/third_party/ffmpeg" config-only
  chromium/scripts/copy_config.sh
  popd > /dev/null

  msg2 "Download GN tools with depot_tools"
  pushd tools/gn/bin/linux > /dev/null
  python2 /opt/depot_tools-svn/download_from_google_storage.py -b chromium-gn -s gn.sha1
  python2 /opt/depot_tools-svn/download_from_google_storage.py -b chromium-gn -s gn32.sha1
  popd > /dev/null
}

build() {
  cd "${srcdir}/chromium"

  #######################
  ## -- Let's build -- ##
  #######################
  msg2 "Building Chromium..."

  # CFLAGS are passed through -Drelease_extra_cflags=
  export -n CFLAGS CXXFLAGS

  # Silence "typedef 'x' locally defined but not used" warnings
  CFLAGS+=' -Wno-unused-local-typedefs'

  # NOTES:
  # -Denable_sql_database=0                  | http://crbug.com/22208
  # -Dlogging_like_official_build=1          | Save space by removing DLOG and DCHECK messages (about 6% reduction).
  # -Dlinux_use_gold_flags=0                 | Never use bundled gold binary. Disable gold linker flags for now.
  # -Dusb_ids_path=/usr/share/hwdata/usb.ids | Use the file at run time instead of effectively compiling it in.
  # -Dlinux_use_tcmalloc=0                   | https://bugs.gentoo.org/show_bug.cgi?id=413637

  local _flags=""

  _flags+="-Ddisable_glibc=1
           -Ddisable_nacl=0
           -Ddisable_pnacl=0
           -Ddisable_sse2=1
           -Dffmpeg_branding=Chrome
           -Dflapper_version_h_file="${srcdir}/flapper_version.h"
           -Dgoogle_api_key="${_google_api_key}"
           -Dgoogle_default_client_id="${_google_default_client_id}"
           -Dgoogle_default_client_secret="${_google_default_client_secret}"
           -Dlibspeechd_h_prefix=speech-dispatcher/
           -Dlinux_link_gnome_keyring="${_use_gnome}"
           -Dlinux_link_gsettings=1
           -Dlinux_link_libpci=1
           -Dlinux_link_libspeechd=1
           -Dlinux_link_pulseaudio="${_use_pulseaudio}"
           -Dlinux_strip_binary=1
           -Dlinux_use_gold_binary=0
           -Dlinux_use_gold_flags=0
           -Dlinux_use_tcmalloc=0
           -Dlogging_like_official_build=1
           -Dno_strict_aliasing=1
           -Dproprietary_codecs=1
           -Dpython_ver=2.7
           -Dremove_webcore_debug_symbols=1
           -Dtarget_arch="${_target_arch}"
           -Dusb_ids_path=/usr/share/hwdata/usb.ids
           -Duse_aura="${_use_aura}"
           -Duse_gconf=0
           -Duse_gnome_keyring="${_use_gnome}"
           -Duse_pulseaudio="${_use_pulseaudio}"
           -Dwerror="

           # Broken flag in Ninja: -Drelease_extra_cflags="\"${CFLAGS}\"" | http://crbug.com/312030

  # TODO
  # -Duse_system_hunspell=1    | upstream changes needed
  # -Duse_system_libsrtp=1     | https://bugs.gentoo.org/show_bug.cgi?id=459932
  # -Duse_system_libusb=1      | http://crbug.com/266149
  # -Duse_system_libvpx=1      | https://bugs.gentoo.org/show_bug.cgi?id=487926
  # -Duse_system_libwebp=1     | http://crbug.com/288019
  # -Duse_system_ssl=1         | http://crbug.com/58087
  # -Duse_system_sqlite=1      | http://crbug.com/22208

  _replaced_system_libs="-Duse_system_bzip2=1
                         -Duse_system_expat=1
                         -Duse_system_flac=1
                         -Duse_system_harfbuzz=1
                         -Duse_system_icu=1
                         -Duse_system_jsoncpp=1
                         -Duse_system_libevent=1
                         -Duse_system_libjpeg=1
                         -Duse_system_libpng=1
                         -Duse_system_libsrtp=0
                         -Duse_system_libusb=0
                         -Duse_system_libvpx=0
                         -Duse_system_libwebp=0
                         -Duse_system_libxml=1
                         -Duse_system_libxslt=1
                         -Duse_system_nspr=1
                         -Duse_system_mesa=1
                         -Duse_system_minizip=1
                         -Duse_system_opus=1
                         -Duse_system_openssl=1
                         -Duse_system_protobuf=1
                         -Duse_system_re2=1
                         -Duse_system_snappy=1
                         -Duse_system_speex=1
                         -Duse_system_sqlite=0
                         -Duse_system_ssl=0
                         -Duse_system_v8=0
                         -Duse_system_xdg_utils=1
                         -Duse_system_yasm=1
                         -Duse_system_zlib=1"

  if [ "${_use_clang}" = "1" ]; then
    _flags+=" -Dclang=1
              -Dclang_use_chrome_plugins=1"
  fi

  build/linux/unbundle/replace_gyp_files.py ${_replaced_system_libs}
  build/gyp_chromium --depth=. ${_flags} ${_replaced_system_libs}

  mkdir -p out/Release

  if [ "${_use_pax}" = "1" ]; then
    # Build mksnapshot and pax-mark it.
    ninja -C out/Release -v ${MAKEFLAGS} "mksnapshot.${_target_arch}"
    paxctl -cm "out/Release/mksnapshot.${_target_arch}"
  fi

  # LetsGo the p4rTy!
  ninja -C out/Release -v ${MAKEFLAGS} chrome chrome_sandbox chromedriver ${_unittest}

  if [ "${_use_pax}" = "1" ]; then
    # Pax-mark again
    paxctl -cm out/Release/chrome
    if [ "${_use_unittest}" = "1" ]; then
      for x in ${_unittest}; do
        paxctl -cm out/Release/${x}
      done
    fi
  fi

  rm -rf "${srcdir}/chromium-build/third_party"
}

check() {
  if [ "${_use_unittest}" = "1" ]; then
    cd "${srcdir}/chromium"

    # need this locale to run test
    local LC_ALL='en_US.utf8'

    out/Release/base_unittests

    out/Release/cacheinvalidation_unittests

    local excluded_content_unittests=(
            "RendererDateTimePickerTest.*"
    )
    out/Release/content_unittests "${excluded_content_unittests[@]}"

    out/Release/crypto_unittests

    out/Release/gpu_unittests

    # TODO: add media_unittests after fixing compile
    # runtest out/Release/media_unittests

    local excluded_net_unittests=(
            "NetUtilTest.IDNToUnicode*"
            "NetUtilTest.FormatUrl*"
            "SpdyFramerTests/SpdyFramerTest.CreatePushPromiseCompressed/2"
            "HostResolverImplTest.FlushCacheOnIPAddressChange"
            "HostResolverImplTest.ResolveFromCache"
            "ProxyResolverV8TracingTest.*"
            "SSLClientSocketTest.ConnectMismatched"
            "UDPSocketTest.*"
            "*EndToEndTest*"
    )

    out/Release/net_unittests "${excluded_net_unittests[@]}"

    out/Release/printing_unittests

    out/Release/sql_unittests "${excluded_sql_unittests[@]}"
  else
    true
  fi
}

package() {
  cd "${srcdir}/chromium/out/Release/"

  install -Dm644 "${srcdir}/default" "${pkgdir}/etc/chromium-dev/default"
  install -Dm755 "${srcdir}/chromium-dev.sh" "${pkgdir}/usr/bin/chromium-dev"
  install -Dm755 chrome "${pkgdir}/usr/lib/chromium-dev/chromium"
  install -Dm4755 -o root -g root chrome_sandbox "${pkgdir}/usr/lib/chromium-dev/chrome-sandbox"
  install -Dm755 chromedriver "${pkgdir}/usr/lib/chromium-dev/chromedriver"
  install -Dm644 {chrome,chrome_100_percent,content_resources,resources}.pak "${pkgdir}/usr/lib/chromium-dev"
  install -Dm755 libffmpegsumo.so "${pkgdir}/usr/lib/chromium-dev/libffmpegsumo.so"
  install -d "${pkgdir}/usr/lib/chromium-dev/locales"
  for i in $(find locales -name "*.pak"); do  install -Dm644 "${i}" "${pkgdir}/usr/lib/chromium-dev/${i}"; done
  install -Dm644 "${srcdir}/chromium-dev.desktop" "${pkgdir}/usr/share/applications/chromium-dev.desktop"
  install -Dm644 chrome.1 "${pkgdir}/usr/share/man/man1/chromium-dev.1"
  install -Dm644 "${srcdir}/chromium/LICENSE" "${pkgdir}/usr/share/licenses/chromium-dev/LICENSE"
  for _size in 16 22 24 32 48 128 256; do
    case ${_size} in
      16|32) branding="${srcdir}/chromium/chrome/app/theme/default_100_percent/chromium" ;;
      *) branding="${srcdir}/chromium/chrome/app/theme/chromium" ;;
    esac
    install -Dm644 "${branding}/product_logo_${_size}.png" "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/chromium-dev.png"
  done
  install -Dm755 libppGoogleNaClPluginChrome.so "${pkgdir}/usr/lib/chromium-dev/libppGoogleNaClPluginChrome.so"
  install -Dm755 nacl_helper{,_bootstrap} "${pkgdir}/usr/lib/chromium-dev"
  [ "${CARCH}" = "i686" ] && install -Dm755 nacl_irt_x86_32.nexe "${pkgdir}/usr/lib/chromium-dev/nacl_irt_x86_32.nexe"
  [ "${CARCH}" = "x86_64" ] && install -Dm755 nacl_irt_x86_64.nexe "${pkgdir}/usr/lib/chromium-dev/nacl_irt_x86_64.nexe"

  # Extract and install PepperFlash
  if [ "${_use_pepperflash}" = "1" ]; then
    msg2 "Adding PepperFlash"
    cd "${srcdir}"
    bsdtar -xf "google-chrome-unstable-${_rpm_ver}-${_rpm_build}.${_rpm_arch}.rpm" opt/google/chrome/PepperFlash
    install -d "${pkgdir}/usr/lib/chromium-dev/PepperFlash"
    for i in "${srcdir}/opt/google/chrome/PepperFlash/"*; do install -Dm644 "$i" "${pkgdir}/usr/lib/chromium-dev/PepperFlash"; done
    chmod 775 "${pkgdir}/usr/lib/chromium-dev/PepperFlash/libpepflashplayer.so"
    rm -fr "${srcdir}/opt"
  fi

  # Extract and install libpdf.so
  if [ "${_use_libpdf}" = "1" ]; then
    msg2 "Adding libpdf"
    cd "${srcdir}"
    bsdtar -xf "google-chrome-unstable-${_rpm_ver}-${_rpm_build}.${_rpm_arch}.rpm" opt/google/chrome/libpdf.so
    install -Dm755 "${srcdir}/opt/google/chrome/libpdf.so" "${pkgdir}/usr/lib/chromium-dev/libpdf.so"
    rm -rf "${srcdir}/opt"
  fi
}
