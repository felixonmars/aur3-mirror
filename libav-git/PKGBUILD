# Maintainer: hi117
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=libav-git
pkgver=20120818
pkgrel=1
pkgdesc="FFmpeg fork"
arch=('i686' 'x86_64')
url="http://libav.org/"
license=('GPL')
depends=('alsa-lib' 'bzip2' 'faac' 'lame' 'libtheora' 'libva' 'libvdpau' 'libvorbis' 'libvpx' 'libxfixes' 'sdl' 'x264-git' 'zlib')
makedepends=('git' 'yasm' 'perl')
conflicts=('ffmpeg')
provides=("ffmpeg=$pkgver")

_gitroot="git://git.libav.org/libav.git"
_gitname="libav"

build() {
  cd "$srcdir"
  msg "Connecting to the Git repository..."
  
  if [[ -d "$srcdir"/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  
  msg "Starting make..."
  
  # fix for pod2man not being in path, NEEDS A BETTER SOLUTION
  PATH="$PATH:/usr/bin/core_perl/"
  
  rm -rf "$srcdir"/$_gitname-build
  git clone "$srcdir"/$_gitname "$srcdir"/$_gitname-build
  
  cd "$srcdir"/$_gitname-build

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libx264 \
    --enable-nonfree \
    --enable-vaapi \
    --enable-vdpau \
    --enable-x11grab
 
  make
  make doc/av{conv,play,probe,server}.1
}

package() {
  cd "$srcdir"/$_gitname-build  
  make DESTDIR="$pkgdir" install install-man
  rm -rf "$srcdir"/$_gitname-build
}
