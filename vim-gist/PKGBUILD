# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=vim-gist
pkgver=7.0
pkgrel=1
pkgdesc="Vim script for gist.github.com"
arch=(any)
url="http://www.vim.org/scripts/script.php?script_id=2423"
license=(custom)
depends=('vim>=7')
optdepends=('git: git profile support')
install=vimdoc.install
source=($pkgname-$pkgver.zip::'http://www.vim.org/scripts/download_script.php?src_id=17734'
    license.txt)
noextract=($pkgname-$pkgver.zip)
sha256sums=('6adc335451b22dba4d4c66236a1b65ceb1033eec081246061ebc81cd6881f0ec'
    '4689a6d3ed3bca186ab8fea8360f32fdbb84f84c82fb21698390202efa7b7d19')
sha512sums=('68a34442895fe39b9dde4b36064f89bbe985d3b6570ab8643e71ad3e40ca4175934c1b639036352fdb6ddae747652ca494ed6e47083d54f5211fe83028665390'
    '5fda2700cf3ac7c3f671f4786c19933df87a0fc36838260d5c4907c294ecb0e39165a02f2d63e2091f5846926476c7ac13b09b0b398581aabc5d5be632a00bf1')

package() {
    cd "$srcdir"
    # ensure clean dir
    install -d $pkgname-$pkgver/
    bsdtar -xf $pkgname-$pkgver.zip -C $pkgname-$pkgver/
    cd $pkgname-$pkgver/

    find -mindepth 2 -type f -exec install -Dm644 '{}' "$pkgdir"/usr/share/vim/vimfiles/'{}' \;
    install -Dm644 ../license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
