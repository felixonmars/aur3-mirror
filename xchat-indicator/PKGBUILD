# Maintainer: György Balló <ballogy@freestart.hu>

pkgname=xchat-indicator
pkgver=0.3.11
pkgrel=1
pkgdesc="Plugin to make XChat use the messaging indicator"
arch=('i686' 'x86_64')
url="https://launchpad.net/xchat-indicator"
license=('GPL')
depends=('xchat' 'indicator-messages' 'libunity')
options=('!libtool')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        libmessaging.patch)
md5sums=('5d4572a2ac1ab6531a3156a966ff385b'
         '2b67d309b3bcb305dd5a72ffa97d229c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/libmessaging.patch"

  # Install for XChat instead of XChat-GNOME
  sed -i 's/xchat-gnome/xchat/' Makefile.am

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
