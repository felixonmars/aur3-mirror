# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=xchat-indicator
pkgname=xchat-indicator
true && pkgname=('xchat-indicator' 'xchat-gnome-indicator')
pkgver=0.3.10
pkgrel=1
arch=('i686' 'x86_64')
url="https://launchpad.net/xchat-indicator"
license=('GPL')
options=('!libtool')
makedepends=('libindicate' 'gtk2' 'libunity')
source=(http://launchpad.net/$pkgbase/trunk/$pkgver/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('891e20928a7c31f3b70245c874a5a07f')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package_xchat-indicator() {
  pkgdesc="Plugin to make XChat use the messaging indicator"
  depends=('xchat' 'libindicate' 'libunity')

  cd "$srcdir/$pkgbase-$pkgver"

  install -Dm644 .libs/indicator.so "$pkgdir/usr/lib/xchat/plugins/indicator.so"
  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/xchat.desktop' > $pkgdir/usr/share/indicators/messages/applications/xchat
}

package_xchat-gnome-indicator() {
  pkgdesc="Plugin to make XChat-GNOME use the messaging indicator"
  depends=('xchat-gnome' 'libindicate' 'libunity')

  cd "$srcdir/$pkgbase-$pkgver"

  install -Dm644 .libs/indicator.so "$pkgdir/usr/lib/xchat-gnome/plugins/indicator.so"
  mkdir -p $pkgdir/usr/share/indicators/messages/applications
  echo '/usr/share/applications/xchat-gnome.desktop' > $pkgdir/usr/share/indicators/messages/applications/xchat-gnome
}

pkgdesc="Plugin to make XChat and XChat-GNOME use the messaging indicator"
depends=('xchat' 'xchat-gnome-git' 'libindicate' 'libunity')
true && depends=()
