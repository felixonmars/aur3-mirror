# Contributor: Jonathan Liu <net147@gmail.com>
pkgname=asterisk16
_realpkgname=asterisk
pkgver=1.6.2.22
pkgrel=1
pkgdesc="A complete PBX solution"
arch=('i686' 'x86_64')
url="http://www.asterisk.org/"
license=('GPL2')
depends=('alsa-lib' 'libcap' 'libnewt' 'libvorbis' 'libxml2' 'openssl' 'popt' 'unixodbc')
makedepends=('imap' 'libresample' 'libusb'
             'curl' 'dahdi' 'dahdi-tools' 'freetds'
             'jack' 'gmime' 'iksemel' 'libldap'
             'libpri' 'lua' 'net-snmp' 'postgresql-libs' 'radiusclient-ng'
             'sdl' 'speex' 'sqlite3')
optdepends=('curl: CURL support'
            'dahdi: DAHDI support'
            'dahdi-tools: DAHDI support'
            'freetds: Sybase and MS SQL Server database support'
            'jack: JACK Audio Connection Kit support'
            'gmime: HTTP POST upload support'
            'iksemel: GTalk, Jingle and Jabber support'
            'libldap: LDAP support'
            'libpri: DAHDI Primary Rate ISDN support'
            'lua: LUA scripting support'
            'net-snmp: SNMP support'
            'postgresql-libs: PostgreSQL database support'
            'radiusclient-ng: RADIUS support'
            'sdl: OSS support'
            'speex: Speex codec support'
            'sqlite3: SQLite database support')
provides=("asterisk=${pkgver}")
conflicts=('asterisk')
backup=('etc/asterisk/asterisk.conf')
source=("http://downloads.asterisk.org/pub/telephony/asterisk/releases/asterisk-$pkgver.tar.gz"
        "http://downloads.asterisk.org/pub/telephony/sounds/releases/asterisk-core-sounds-en-gsm-1.4.22.tar.gz"
        "http://downloads.asterisk.org/pub/telephony/sounds/releases/asterisk-core-sounds-en-gsm-1.4.22.tar.gz.sha1"
        "http://downloads.asterisk.org/pub/telephony/sounds/releases/asterisk-moh-opsound-wav-2.03.tar.gz"
        "http://downloads.asterisk.org/pub/telephony/sounds/releases/asterisk-moh-opsound-wav-2.03.tar.gz.sha1"
        "Makefile.patch"
        "logrotate.patch"
        "asterisk")
install="${pkgname}.install"
md5sums=('f9d657d2002b065a77636fb9af0ebdf7'
         '2a99a45c0675135255bc65796b12f7c6'
         'af248890a947e30d0838dced58276656'
         '8277e2c693fd056773b1c15e4d52077d'
         '011fa4dc9fe35dc3d34ed557bb04263f'
         '4800ea15d8fce3788f1a2a9fcc2abf18'
         'af4d636d86b2cb0a26dd063f8589e1cc'
         '59146273738605f1e011d8e7bed3561a')

build() {
  cd "${srcdir}/${_realpkgname}-${pkgver}"

  ln -sf "$srcdir/asterisk-core-sounds-en-gsm-1.4.22.tar.gz" "sounds/"
  ln -sf "$srcdir/asterisk-core-sounds-en-gsm-1.4.22.tar.gz.sha1" "sounds/"
  ln -sf "$srcdir/asterisk-moh-opsound-wav-2.03.tar.gz" "sounds/"
  ln -sf "$srcdir/asterisk-moh-opsound-wav-2.03.tar.gz.sha1" "sounds/"

  patch -Np1 -i "${srcdir}/Makefile.patch"
  patch -Np1 -i "${srcdir}/logrotate.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --without-h323 --without-pwlib

  # Uncomment one of the following lines to configure Asterisk
  #make menuconfig
  #make cmenuconfig
  #make gmenuconfig
  #make nmenuconfig

  make
  # make h323 
  make DESTDIR="${pkgdir}" samples
}

package() {
  cd "${srcdir}/${_realpkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-logrotate
  
  cd contrib/utils
  gcc ${CXXFLAGS} rawplayer.c -o "${pkgdir}/usr/sbin/rawplayer"
  cd ../..

  (cd "${pkgdir}/etc/asterisk" &&
  ls -I asterisk.conf | xargs -I '{}' mv '{}' '{}.default')
  sed -i -e "s#/var/run#/var/run/asterisk#" "${pkgdir}/etc/asterisk/asterisk.conf"
  
  install -d "${pkgdir}/var/run/asterisk"
  install -D -m755 "${srcdir}/asterisk" "${pkgdir}/etc/rc.d/asterisk"
}

# vim:set ts=2 sw=2 et:
