# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=zeitgeist
pkgver=0.8.0
pkgrel=1
pkgdesc="A service which logs the users's activities and events and makes relevant information available to other applications"
arch=('any')
url="http://zeitgeist-project.com/"
license=('GPL')
depends=('dbus-python' 'pygobject' 'pyxdg')
makedepends=('intltool>=0.35.0' 'raptor' 'rdflib')
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('75612831b68fc4ca2681eadc6a1b0d9d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' extra/rdfxml2py
  sed -i 's|python - 2>/dev/null|$PYTHON - 2>/dev/null|' configure{,.ac}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
