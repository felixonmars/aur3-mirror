pkgname=linphone-git
pkgver=20111024
pkgrel=2
pkgdesc="Linphone is an internet phone or Voice Over IP phone (VoIP)."
arch=(i686 x86_64)
url="http://www.linphone.org/"
license=('GPL2')
depends=('gtk2' 'mediastreamer-git' 'libexosip2')
makedepends=('git')
options=(!libtool)
conflicts=('linphone')
provides=('linphone')

_gitroot="git://git.linphone.org/linphone.git"
_gitname="linphone"

build() {
  cd $srcdir
  msg "Connecting to GIT server...."

  if [ -d $startdir/src/$_gitname ] ; then
    cd $_gitname && git config remote.origin.url $_gitroot && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $srcdir/$_gitname-build
  cp -r $srcdir/$_gitname $srcdir/$_gitname-build
  cd $srcdir/$_gitname-build

  # autogen.sh does not check if they exists or not...
  mkdir -p oRTP mediastreamer2
  touch oRTP/autogen.sh mediastreamer2/autogen.sh
  chmod +x oRTP/autogen.sh mediastreamer2/autogen.sh

  ./autogen.sh
  ./configure --prefix=/usr --enable-external-ortp --enable-external-mediastreamer
  make
}

package() {
  cd $srcdir/$_gitname-build
  make DESTDIR=$pkgdir install
}
