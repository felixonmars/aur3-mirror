# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae
pkgname=fs-uae-devel
pkgver=2.3.16dev
pkgrel=1
pkgdesc="Cross-platform Amiga emulator based on UAE/WinUAE (development version)."
arch=("i686" "x86_64")
url="http://fs-uae.net/download-devel"
license=("GPL2")
depends=("sdl" "libpng" "openal" "mesa" "glu" "gettext" "freetype2" "lua" "xdg-utils" "hicolor-icon-theme")   # 'glib2' provided by 'gettext', and 'zlib' by 'libpng'
install="${pkgname}.install"
source=("http://fs-uae.net/devel/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
provides=("fs-uae")
conflicts=("fs-uae")
md5sums=('5bd3063bb3270ccc317c0588dcdedeb7')




## alternative build method for 64bit platform ##
## (with multilib enabled)                     ##
## comment variable to build 64bit version     ##
## 0 = build 32bit jit version                 ##
## 1 and 2 = dual 32/64 bit                    ##
##   1 = default 32bit exe                     ##
##   2 = default 64bit exe                     ##
#fsuae3264=0



if [[ -n $fsuae3264 && $CARCH == "x86_64" && -n $(gcc -v 2>&1|grep '\-\-enable-multilib') ]]; then
   if [[ $fsuae3264 == [1-2] ]]; then
      depends+=("lib32-sdl" "lib32-libpng" "lib32-glib2" "lib32-openal" "lib32-mesa" "lib32-glu" "lib32-gettext" "lib32-freetype2" "lib32-lua")
      makedepends+=("libtool-multilib")
      pkgdesc="${pkgdesc/(dev/dual 64 and 32-bit dev}"
   elif [[ $fsuae3264 == 0 ]]; then
      unset depends
      depends+=("lib32-sdl" "lib32-libpng" "lib32-glib2" "lib32-openal" "lib32-mesa" "lib32-glu" "lib32-gettext" "lib32-freetype2" "lib32-lua" "xdg-utils" "hicolor-icon-theme")
      makedepends+=("libtool-multilib")
      pkgdesc="${pkgdesc/(dev/(32-bit dev}"
   fi
fi




build() {
   cd ${_pkgname}-${pkgver}

   if [[ $fsuae3264 != 0 ]]; then
      make
   fi

   if [[ $fsuae3264 == [1-2] ]]; then
      cp fs-uae ../fs-uae-64
      make -C libfsemu clean
      make clean
   fi

   if [[ $fsuae3264 == [0-2] ]]; then
      sed 's|\(sdl-config\)|\1-32|g' -i Makefile libfsemu/Makefile
      make CC="gcc -m32" CXX="g++ -m32" CFLAGS="${CFLAGS/x86-64/i686}" CXXFLAGS="${CXXFLAGS/x86-64/i686}"
   fi

   if [[ $fsuae3264 == [1-2] ]]; then
      let local fua=$fsuae3264*32
      echo -e "#!/bin/sh\n[[ -z \$FSUAE_EXE ]] && FSUAE_EXE=${fua}\nexec /usr/bin/fs-uae-\${FSUAE_EXE} \"\$@\"" > fs-uae.sh
   fi
}



package() {
   cd ${_pkgname}-${pkgver}

   make install prefix="${pkgdir}"/usr

   if [[ $fsuae3264 == [1-2] ]]; then
      mv "${pkgdir}"/usr/bin/fs-uae{,-32}
      install -m755 ../fs-uae-64 "${pkgdir}"/usr/bin
      install -m755 fs-uae.sh "${pkgdir}"/usr/bin/fs-uae
   fi
}


# vim:set ts=3 sw=3 sts=3 et:

