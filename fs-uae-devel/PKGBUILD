# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae
_pkgver=1.3.31u3

pkgname=fs-uae-devel
pkgver=1.3.31.u3
pkgrel=1
pkgdesc="An Amiga multi-platform emulator based on UAE/WinUAE, with a focus on emulating games (development version)."
arch=("i686" "x86_64")
url="http://fengestad.no/fs-uae/download-beta"
license=("GPL2")
depends=("sdl" "libpng" "glib2" "openal" "zlib" "mesa" "gettext" "xdg-utils" "hicolor-icon-theme")
install="${pkgname}.install"
source=("http://fengestad.no/${_pkgname}/devel/${_pkgver}/${_pkgname}-${_pkgver}.tar.gz")
provides=("fs-uae")
conflicts=("fs-uae")
md5sums=('0606d49aff993b3d62099700c592522b')




## uncomment for build 32 and 64 bit versions ##
## (only for 64bit and multilib enabled) ...  ##
## 1 = def 32bit exe , 2 = def 64bit exe
#fsuae3264=1



if [[ -n $fsuae3264 && $CARCH == "x86_64" ]] && [[ -n $(gcc -v 2>&1|grep '\-\-enable-multilib') ]]; then
   depends+=("lib32-sdl" "lib32-libpng" "lib32-glib2" "lib32-openal" "lib32-zlib" "lib32-mesa" "lib32-gettext")
   makedepends+=("libtool-multilib")
else
   fsuae3264=
fi






build() {
   cd ${_pkgname}-${_pkgver}

   # change to lowercase
   mv share/fs-uae/shaders/{Scale4xHQ,scale4xhq}.shader

   make

   if [[ -n $fsuae3264 ]]; then
      cp out/fs-uae ../fs-uae-64
      make -C libfsemu clean
      make clean
      sed 's|\(sdl-config\)|\1-32|g' -i Makefile libfsemu/Makefile
      make CC="gcc -m32" CXX="g++ -m32" CFLAGS="${CFLAGS/x86-64/i686}" CXXFLAGS="${CXXFLAGS/x86-64/i686}"
      [[ $fsuae3264 == 1 ]] && local fua="32"
      [[ $fsuae3264 == 2 ]] && local fua="64"
      echo -e "#!/bin/sh\nexec /usr/bin/fs-uae-${fua} \"\$@\"" > fs-uae.sh
   fi
}



package() {
   cd ${_pkgname}-${_pkgver}

   make install prefix="${pkgdir}"/usr

   if [[ -n $fsuae3264 ]]; then
      mv "${pkgdir}"/usr/bin/fs-uae{,-32}
      install -m755 ../fs-uae-64 "${pkgdir}"/usr/bin
      install -m755 fs-uae.sh "${pkgdir}"/usr/bin/fs-uae
   fi
}


