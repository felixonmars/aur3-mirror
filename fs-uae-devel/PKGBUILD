# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae
pkgname=fs-uae-devel
pkgver=1.3.9
pkgrel=1
pkgdesc="A multi-platform Amiga emulator based on UAE/WinUAE, with a focus on emulating floppy-disk and CD-ROM based games (development version)"
arch=("i686" "x86_64")
url="http://fengestad.no/fs-uae"
license=("GPL")
depends=("sdl" "libpng" "glib2" "openal" "zlib" "mesa" "gettext" "xdg-utils")
install="${pkgname}.install"
source=("http://fengestad.no/${_pkgname}/files/${_pkgname}-${pkgver}.tar.gz" "pl.po")
provides=("fs-uae")
conflicts=("fs-uae")
md5sums=('a9df4013dfcf726e7302136954ce0a15'
         'fa6186f9a946729829820c8786f61894')


## uncomment for build 32 and 64 bit versions
## (only for 64bit and multilib enabled) ...

#fsuae3264=1



if [[ -n "$fsuae3264" && "$CARCH" == "x86_64" ]] && [[ -n "$(gcc -v 2>&1|grep '\-\-enable-multilib')" ]]; then
   depends+=("lib32-sdl" "lib32-libpng" "lib32-glib2" "lib32-openal" "lib32-zlib" "lib32-mesa" "lib32-gettext")
   makedepends+=("gcc-multilib")
else
   fsuae3264=
fi


build() {
   cd ${_pkgname}-${pkgver}

   cp -f ../pl.po po/
   mv share/fs-uae/shaders/{Scale4xHQ,scale4xhq}.shader

   make

   if [[ -n "$fsuae3264" ]]; then
      cp out/fs-uae ../
      make -C libfsemu clean
      make clean
      sed 's|\(sdl-config\)|\1-32|g' -i Makefile libfsemu/Makefile
      make CC="gcc -m32" CXX="g++ -m32" CFLAGS="${CFLAGS/x86-64/i686}" CXXFLAGS="${CXXFLAGS/x86-64/i686}"
   fi
}


package() {
   cd ${_pkgname}-${pkgver}

   make install prefix=${pkgdir}/usr

   if [[ -n "$fsuae3264" ]]; then
      mv "${pkgdir}"/usr/bin/fs-uae{,-32}
      install -m755 ../fs-uae "${pkgdir}"/usr/bin
   fi
}


