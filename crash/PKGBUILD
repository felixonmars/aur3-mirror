# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Contributor: Mathieu Pasquet <mathieui@mathieui.net>

pkgname=crash
pkgver=7.0.4
pkgrel=1
pkgdesc='Linux kernel crashdump analysis tool'
license=(GPL)
depends=(xz ncurses zlib)
makedepends=(flex bison)
arch=(i686 x86_64)
source=(http://people.redhat.com/~anderson/$pkgname-$pkgver.tar.gz)
url='http://people.redhat.com/~anderson'
sha1sums=(192ba4cb64e15529346dba8ec7b7f0f53cc1c4d5)

prepare() {
  cd $pkgname-$pkgver
  make gdb_unzip
  sed -i '/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/' gdb-7.6/libiberty/configure
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man8"
  install $pkgname "$pkgdir/usr/bin/"
  install $pkgname.8 "$pkgdir/usr/share/man/man8/"
}
