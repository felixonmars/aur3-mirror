# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Contributor: Mathieu Pasquet <mathieui@mathieui.net>

pkgname=crash
pkgver=7.0.3
pkgrel=1
pkgdesc='Linux kernel crashdump analysis tool'
license=(GPL)
makedepends=(flex bison zlib ncurses)
arch=(i686 x86_64)
source=(http://people.redhat.com/~anderson/$pkgname-$pkgver.tar.gz)
url='http://people.redhat.com/~anderson'
sha1sums=('62872fb484fbb4cc8114ac4eef7a25c23b640d08')

prepare() {
  cd $pkgname-$pkgver
  make gdb_unzip
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" gdb-7.6/libiberty/configure
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man8"
  install $pkgname "$pkgdir/usr/bin/"
  install $pkgname.8 "$pkgdir/usr/share/man/man8/"
}
