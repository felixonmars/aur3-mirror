# Maintainer: Det
# Based on both jre (https://aur.archlinux.org/packages.php?ID=51908) and jdk (https://aur.archlinux.org/packages.php?ID=51906)

pkgname=jdk-devel
_major=8
_build=b65
_date=15_nov_2012
pkgver=$_major$_build
pkgrel=1
pkgdesc="Java $_major Development Kit Snapshot"
arch=('i686' 'x86_64')
url=http://jdk$_major.java.net/
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libx11' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound support'
            'ttf-dejavu: fonts')
provides=("java-environment=$_major" "java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major")
conflicts=("${provides[@]}" "${provides[@]/$_major/7}")
backup=('etc/conf.d/derby-network-server'
        'etc/profile.d/jdk.csh'
        'etc/profile.d/jdk.sh')
options=('!emptydirs')
install=$pkgname.install
_arch=i586  # Workaround for the AUR Web interface Source parser
_arch2=i386; [ "$CARCH" = 'x86_64' ] && _arch=x64 && _arch2=amd64
source=("http://download.java.net/jdk$_major/archive/$_build/binaries/jdk-$_major-ea-bin-$_build-linux-$_arch-$_date.tar.gz"
        'derby-network-server'
        'derby-network-server.conf'
        'java-monitoring-and-management-console.desktop'
        'java-policy-settings.desktop'
        'java-visualvm.desktop'
        'javaws-launcher'
        'jdk.csh'
        'jdk.sh')
md5sums=(`curl -Ls ${source/.t*}.md5 | cut -d " " -f4` # jdk-$_major-ea-bin-$_build-linux-$_arch-$_date.tar.gz
         'a279e195e249000646895d93e199860d'  # derby-network-server
         '4bdff6982c66d24a879c424aaac3d04d'  # derby-network-server.conf
         'da10de5e6507c392fc9871076ef53ec6'  # java-monitoring-and-management-console.desktop
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '35fd89c5c170021d2183593335703703'  # java-visualvm.desktop
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         'e81bb7853b071df6adca0b90f0c2ac2d'  # jdk.csh
         'a06d46b277e2926d73d2ee11c9950973') # jdk.sh

PKGEXT=".pkg.tar"

package() {
  msg2 "Creating required dirs"
  cd jdk1.$_major.0
  mkdir -p "$pkgdir"/{opt/java/,usr/{lib/mozilla/plugins,share/licenses/jdk},etc/{.java/.systemPrefs,{profile,rc}.d}}

  msg2 "Removing the redundancies"
  rm -r db/bin/*.bat jre/{plugin/,COPYRIGHT,LICENSE,*.txt} man/ja # lib/{desktop,visualvm/platform/docs}

  msg2 "Moving stuff in place"
  mv jre/lib/desktop/{applications,icons,mime} man "$pkgdir"/usr/share/
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/jdk/
  mv * "$pkgdir"/opt/java/

  msg2 "Symlinking the plugin"
  ln -s /opt/java/jre/lib/$_arch2/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing the scripts, confs and .desktops of our own"
  cd "$srcdir"
  install -m755 jdk.{,c}sh "$pkgdir"/etc/profile.d/
  install -Dm644 derby-network-server.conf "$pkgdir"/etc/conf.d/derby-network-server
  install -m755 derby-network-server "$pkgdir"/etc/rc.d/
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -m644 *.desktop "$pkgdir"/usr/share/applications/

  msg2 "Tweaking the javaws .desktop file"
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}