# Maintainer: Fortunato Ventre (voRia) <vorione AT gmail DOT com>

pkgname=samsung-tools
pkgver=2.1
pkgrel=3
pkgdesc="Tools for Samsung laptops"
arch=('any')
url="https://launchpad.net/samsung-tools"
license=('GPL3')
depends=('pm-utils' 'xbindkeys' 'rfkill' 'dbus-python' 'python-notify' 'vbetool')
optdepends=('phc-intel: for CPU undervolting')
backup=(etc/samsung-tools/system.conf
        etc/samsung-tools/session.conf
        etc/samsung-tools/scripts/bluetooth-on
        etc/samsung-tools/scripts/bluetooth-off
        etc/samsung-tools/scripts/wireless-on
        etc/samsung-tools/scripts/wireless-off)
install=samsung-tools.install
source=(http://launchpad.net/samsung-tools/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        samsung-tools.rcd)
md5sums=('594be701b1dfc3d07b241ea3b4c53f95'
         'f13fe6219c58420ddcea6917faf1e1b7')
         
package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # Fix shebangs in all .py files in order to use python2
  find . -name \*.py | while read FILE; do
    sed -i "1s:#!/usr/bin/env python:#!/usr/bin/python2:" "$FILE"
  done
  
  make DESTDIR="$pkgdir/" install || return 1
  install -D -m755 "${srcdir}/samsung-tools.rcd" "${pkgdir}/etc/rc.d/samsung-tools" || return 1
}

# vim:set ts=2 sw=2 et:

