# Maintainer:  David Kaylor <dpkaylor@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=vegastrike
pkgver=0.5.1.r1
pkgrel=2
pkgdesc="3D action-space-sim allowing player to trade and bounty hunt in a vast universe"
arch=('i686' 'x86_64')
url="http://vegastrike.sourceforge.net/"
license=('GPL')
depends=('gcc-libs' 'freetype2' 'freeglut' 'gtk2' 'openal' 'sdl' 'python2' 'libvorbis' 'openal' 'boost-libs' 'vegastrike-data')
makedepends=('boost')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-src-${pkgver}.tar.bz2)
md5sums=('6f3f2f1ff56a29710b7d0fdd8c3a255f')
sha1sums=('6314a0fc05cf66053e20272916976c0c16dcf048')

build() {
  cd ${srcdir}/$pkgname-src-$pkgver

  export CXXFLAGS="$CXXFLAGS -fpermissive"

  ./configure \
     --disable-boost \
     --prefix=/usr \
     --disable-ffmpeg \
     --with-boost=system \
     --with-data-dir=/usr/share/vegastrike

  make
}

package() {
  cd ${srcdir}/$pkgname-src-$pkgver

  #cd build
  make DESTDIR=${pkgdir} install

  # move objconv dir to right path and avoiding conflict with mysql's 'replace' file
  install -d ${pkgdir}/usr/share/$pkgname
  mv -f ${pkgdir}/usr/objconv ${pkgdir}/usr/share/$pkgname/objconv
  mv ${pkgdir}/usr/bin/replace ${pkgdir}/usr/bin/vsreplace
}
