# Original Contributor: aquavitae <aquavitae69: gmail>
# Current Maintainer: Andrew Fischer <andrew_at_ltengsoft.com>

pkgname=openfoam

# The distributors package name
_distpkgname=OpenFOAM

pkgver=2.1.1
pkgrel=1
pkgdesc="The open source CFD toolbox"
arch=('i686' 'x86_64')
url="http://www.openfoam.com"
license=('GPL')
groups=()
depends=(paraview parmetis scotch ptscotch-openmpi)
makedepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
source=(http://downloads.sourceforge.net/foam/$_distpkgname-$pkgver.tgz)
noextract=()
install=openfoam.install

md5sums=('dad652c531ea76da4389903e145ef6a5')

# NOTE: build() and package() are split here due to OpenFOAM's use of the mpicc executable
# to determine the system openmpi settings during build(). 
# This currently does not function under fakeroot, for details see:
# https://svn.open-mpi.org/trac/ompi/ticket/2812
# https://bbs.archlinux.org/viewtopic.php?id=117493

build() {
  # Extract the current version and major of paraview and of scotch for use in the system preferences
  _pversion=`pacman -Qi paraview | grep Version | sed -e 's/Version.*: //g' | sed -e 's/-.*//g'`
  _pmajor=`echo $_pversion | sed -e 's/^\([0-9][0-9]*\.[0-9][0-9]*\).*$/\1/'`
  _sversion=`pacman -Qi scotch | grep Version | sed -e 's/Version.*: //g' | sed -e 's/-.*//g'`

  # Generate and install the system preferences file
  echo "compilerInstall=system" > $startdir/prefs.sh
  echo "export WM_MPLIB=SYSTEMOPENMPI" >> $startdir/prefs.sh
  echo "export ParaView_VERSION=$_pversion" >> $startdir/prefs.sh
  echo "export ParaView_MAJOR=$_pmajor" >> $startdir/prefs.sh
  cp $startdir/prefs.sh $srcdir/$_distpkgname-$pkgver/etc || return 1

  # Generate the scotch.sh file for arch
  echo "export SCOTCH_VERSION=scotch_$_sversion" > $startdir/scotch.sh
  echo "export SCOTCH_ARCH_PATH=/usr" >> $startdir/scotch.sh
  cp $startdir/scotch.sh $srcdir/$_distpkgname-$pkgver/etc/config || return 1

  # Patch for archlinux parmetis and paraview
  patch -p1 < $startdir/decomp-options.patch
  patch -p1 < $startdir/paraFoam.patch

  # Setup the build environment
  export FOAM_INST_DIR=$srcdir 
  foamDotFile=$srcdir/$_distpkgname-$pkgver/etc/bashrc
  [ -f $foamDotFile ] && . $foamDotFile || return 1

  # Enter build directory
  cd "$srcdir/$_distpkgname-$pkgver" || return 1

  # Build and clean up OpenFOAM
  ./Allwmake || return 1
  wclean all || return 1
  cd "$startdir"
}

package() {
  # Create destination directories
  mkdir -p "$pkgdir/opt/$_distpkgname" "$pkgdir/etc/profile.d" || return 1

  # Move package to pkgdir
  mv "$srcdir/$_distpkgname-$pkgver" "$pkgdir/opt/$_distpkgname/$_distpkgname-$pkgver" || return 1

  # Add source file
  cp "$startdir/openfoam.sh" "$pkgdir/etc/profile.d/openfoam.sh" || return 1

  # Add stub thirdparty directory to keep openfoam happy
  mkdir -p "$pkgdir/opt/$_distpkgname/ThirdParty-$pkgver"

  # Permission fixes - for system-wide install and use
  chmod -R go+r "$pkgdir/opt"
  chmod -R 755 "$pkgdir/opt/$_distpkgname/$_distpkgname-$pkgver/bin"
  chmod -R 755 "$pkgdir/opt/$_distpkgname/$_distpkgname-$pkgver/etc"
}

# vim:set ts=2 sw=2 et:

