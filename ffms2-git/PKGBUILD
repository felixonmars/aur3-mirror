pkgname=ffms2-git
pkgver=2.19.32.g57ae962
pkgrel=1
pkgdesc="An FFmpeg/Libav based source library and Avisynth/Vapoursynth plugin for easy frame accurate access (GIT version)"
url="https://github.com/FFMS/ffms2"
arch=('x86_64' 'i686')
license=('MIT')
depends=('ffmpeg')
makedepends=('git')
provides=('ffms2')
conflicts=('ffms2')
replaces=('ffmpegsource')

source=('git://github.com/FFMS/ffms2')
md5sums=('SKIP')
_gitname="ffms2"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --enable-shared=yes --enable-static=no --enable-avresample
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
