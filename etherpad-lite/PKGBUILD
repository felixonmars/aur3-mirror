# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

pkgname=etherpad-lite
pkgver=1.3.0
pkgrel=2
pkgdesc="A lightweight fork of etherpad based on javascript"
arch=(any)
url="http://etherpad.org"
_watch=('http://etherpad.org','Documentation <small>v([\d.]*)</small>')
license=('GPL2')
depends=('curl' 'python2' 'openssl' 'nodejs')
conflicts=('etherpad-lite-git')
backup=(usr/share/webapps/etherpad-lite/settings.json)
install='etherpad-lite.install'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ether/${pkgname}/archive/${pkgver}.tar.gz"
	"etherpad-lite.service")
sha512sums=('033d54c5ec9daa23d8f57b0543b8d87cb76e3568f888f5f380454821ad2c6171e1c6b64e56a4e9476c2f757da698fb75a170693c963334bcc42dfd4145fb181d'
	    '4964e3455892e9216c7f4c596c7e89591dabe5d4e14f1dc650f6aadaf142b8be62f992ab599d899f0391ccdf5b55abe7731dbdf8e2b7df93e528861f2096d550')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./bin/installDeps.sh
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  mkdir -p "${pkgdir}/usr/share/webapps" "${pkgdir}/var/run/etherpad-lite"
  rm start.bat
  cp -r . "${pkgdir}/usr/share/webapps/${pkgname}"
  install -Dm644 "${srcdir}/etherpad-lite.service" "${pkgdir}/usr/lib/systemd/system/etherpad-lite.service"
  install -D "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
