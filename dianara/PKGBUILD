# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=dianara
pkgver=0.8
pkgrel=1
pkgdesc="A Qt pump.io client"
arch=(i686 x86_64)
url="http://dianara.nongnu.org/"
license=('GPL')
depends=('qjson' 'qoauth' 'qca-ossl')
install="$pkgname.install"
source=("http://download-mirror.savannah.gnu.org/releases/$pkgname/$pkgname-v$pkgver.tar.gz")
md5sums=('a409d8666f708e062931086b9c260c27')

prepare() {
  cd "$srcdir/$pkgname-v$pkgver"

  # translation path fix
  sed -i 's|:/translations|/usr/share/locale|' src/main.cpp

  # resources path fix
  sed -e 's|:/icon/64x64|/usr/share/pixmaps|' \
      -e 's|:/images|/usr/share/dianara|' \
      -i src/mainwindow.cpp
}

build() {
  cd "$srcdir/$pkgname-v$pkgver"
  [ -d bld ] || mkdir bld && cd bld
  qmake-qt4 ..
  make
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"

  # bin
  install -Dm755 bld/$pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 icon/64x64/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # translations
  install -d "$pkgdir/usr/share/locale"
  install -Dm644 translations/*.qm \
    "$pkgdir/usr/share/locale"
}

# vim:set ts=2 sw=2 et:
