# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-file-manager
pkgver=20130129
pkgrel=1
pkgdesc="The MATE shell and file manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'exempi' 'mate-icon-theme' 'shared-mime-info'
         'desktop-file-utils' 'gvfs' 'libunique' 'mate-desktop')
makedepends=('pkgconfig' 'intltool' 'git' 'mate-common>=20130129')
url="https://github.com/mate-desktop/mate-file-manager/"
options=('!libtool' '!emptydirs')
install=mate-file-manager.install
source=()
sha256sums=()
groups=('mate')

_gitroot=git://github.com/mate-desktop/mate-file-manager.git
_gitname=mate-file-manager

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"


  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/caja \
    --enable-unique \
    --disable-update-mimedb || return 1

  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"

  make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d ${pkgdir}/usr/share/mateconf/schemas

  mateconf-merge-schema ${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
