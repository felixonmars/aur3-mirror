# Contributor: Dave Reisner <d@falconindy.com>

pkgname=dbus-git
pkgver=20110213
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=('i686' 'x86_64')
license=('GPL' 'custom')
depends=('libx11' 'expat' 'coreutils' 'filesystem')
makedepends=('git')
provides=('dbus' 'dbus-core')
conflicts=('dbus' 'dbus-core')
options=('!libtool' 'emptydirs')
install=dbus.install
source=('dbus.rc'
        '30-dbus')
md5sums=('08f93dd19cffd1b45ab05c1fd4efb560'
         'd14e59575f04e55d21a04907b6fd9f3c')

_gitroot="git://anongit.freedesktop.org/git/dbus/dbus"
_gitname="dbus"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --disable-verbose-mode --disable-static \
      --enable-inotify --disable-dnotify \
      --disable-tests --disable-asserts

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install-man

  pushd tools
  install -m755 -d "$pkgdir/etc/X11/xinit/xinitrc.d"
  install -m755 "$srcdir/30-dbus" "$pkgdir/etc/X11/xinit/xinitrc.d/"
  install -d -m755 "$pkgdir/usr/share/licenses/dbus"
  popd

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"

  install -o 81 -g 81 -dm755 "$pkgdir/var/run/dbus"
  install -Dm755 "$srcdir/dbus.rc" "$pkgdir/etc/rc.d/dbus"

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "$pkgdir/etc/dbus-1/system.conf"

  #install .keep files so pacman doesn't delete empty dirs
  touch "$pkgdir/usr/share/dbus-1/services/.keep"
  touch "$pkgdir/usr/share/dbus-1/system-services/.keep"
  touch "$pkgdir/etc/dbus-1/session.d/.keep"
  touch "$pkgdir/etc/dbus-1/system.d/.keep"

  install -dm755 "$pkgdir/usr/share/licenses/dbus-core"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus-core/COPYING"
}

