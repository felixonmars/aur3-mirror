# Contributor: jwwolf <jwwolf+arch@gmail.com>
# Maintainer: SkyDrome <irc.freenode.net>

pkgname=tor-git
pkgver=20120731
pkgrel=2
pkgdesc="An anonymizing overlay network (development version)"
arch=('i686' 'x86_64')
url="http://www.torproject.org"
license=('BSD')
depends=('openssl' 'libevent' 'tsocks')
makedepends=('ca-certificates' 'asciidoc')
conflicts=('tor' 'tor-unstable')
provides=('tor')
install='tor.install'
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf'
        'etc/conf.d/tor')

source=('torrc'
        'tor'
        'tor.conf.d'
        'tor.service')
md5sums=('12fe6620b6836b1206a65f6f13861801'
         'f8e6868a389877346e7eebaacd1078bb'
         '5c7c6834064b3530c442def6079ac3aa'
         'cf23b97a1da09670214da6229a3ecb09')

_gitroot="git://git.torproject.org/tor.git"
_gitname="$pkgname"

build() {
    cd "$srcdir"

    msg "Connecting to GIT server..."
    if [ -d "$_gitname/.git" ]; then
        cd "$_gitname"
        #git clean -xdf
        git pull --depth 1 origin
        msg "The local repository was updated"
    else
        git clone --depth 1 ${_gitroot} ${_gitname}
        cd "$_gitname"
        msg "The remote repository was cloned"
    fi

    msg "Starting build..."
    ./autogen.sh
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
    make
}

package() {
    cd "$srcdir/$_gitname"

    make DESTDIR="$pkgdir" install

    install -dm0700 "${pkgdir}/var/lib/tor"
    mv "${pkgdir}/etc/tor/torrc.sample"     "${pkgdir}/etc/tor/torrc-dist"
    install -Dm0644 "${srcdir}/torrc"       "${pkgdir}/etc/tor/torrc"
    install -Dm0755 "${srcdir}/tor"         "${pkgdir}/etc/rc.d/tor"
    install -Dm0644 "${srcdir}/tor.conf.d"  "${pkgdir}/etc/conf.d/tor"
    install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"
    install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/tor/LICENSE"
}
