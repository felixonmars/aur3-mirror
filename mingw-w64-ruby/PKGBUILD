pkgname=mingw-w64-ruby
pkgver=2.0.0_p247
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=(any)
url="http://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config ruby)
depends=(mingw-w64-crt mingw-w64-openssl mingw-w64-libyaml mingw-w64-libffi)
options=(!libtool !strip !buildflags)
source=("ftp://ftp.ruby-lang.org/pub/ruby/${pkgver%.*}/ruby-${pkgver//_/-}.tar.bz2")
md5sums=('60913f3eec0c4071f44df42600be2604')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    CPPFLAGS+="$(${_arch}-pkg-config --cflags libffi)"
    if [ ${_arch} = "i686-w64-mingw32" ]; then
      _disabled_exts="readline,sdbm"
    else
      _disabled_exts="fiddle,readline,sdbm"
    fi
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/ruby-${pkgver//_/-}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-install-doc \
      --disable-install-rdoc \
      --disable-install-capi \
      --with-out-ext=${_disabled_exts} \
      --disable-ipv6
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' -o -name '*.so' | xargs -rtl1 ${_arch}-strip -g
    find "$pkgdir/usr/${_arch}" -name '*.dll' -o -name '*.so' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' | xargs -rtl1 rm
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
