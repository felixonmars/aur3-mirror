# $Id: PKGBUILD 354 2013-03-04 14:08:40Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-zendoptimizerplus
pkgver=7.0.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='The Zend Optimizer+ provides faster PHP execution through opcode caching and optimization.'
url='http://pecl.php.net/package/ZendOptimizerPlus'
depends=('php')
license=('PHP')
source=("http://pecl.php.net/get/zendoptimizerplus-${pkgver}.tgz")
backup=('etc/php/conf.d/zendoptimizerplus.ini')
md5sums=('e36cfa750b7b3aeaf71183ecc90d94e2')

build() {
	cd $srcdir/zendoptimizerplus-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

# check() {
# 	cd $srcdir/zendoptimizerplus-$pkgver
# 	make test
# }

package() {
	cd $srcdir/zendoptimizerplus-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';zend_extension=/usr/lib/php/modules/ZendOptimizerPlus.so' > zendoptimizerplus.ini
	install -D -m644 zendoptimizerplus.ini $pkgdir/etc/php/conf.d/zendoptimizerplus.ini
	install -D -m644 README $pkgdir/usr/share/doc/php-zendoptimizerplus/README
}
