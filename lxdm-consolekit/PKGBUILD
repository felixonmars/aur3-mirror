# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: kiefer <jorgelmadrid@gmail.com>
# Maintainer: Aaditya Bagga <aaditya_gnulinux@zoho.com>

pkgname=lxdm-consolekit
_prgname=lxdm
pkgver=0.5.0
pkgrel=2
pkgdesc='LXDM with consolekit support enabled'
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/lxdm/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'xorg-server')
conflicts=('lxdm')
provides=('lxdm')
makedepends=('intltool' 'iso-codes')
optdepends=('gtk-engines: default GTK+ theme'
            'librsvg: display the default background' 
	    'consolekit: for using reboot and shutdown options')
install=$_prgname.install
backup=('etc/lxdm/lxdm.conf' 'etc/pam.d/lxdm' 'etc/lxdm/Xsession'
        'etc/lxdm/PreLogin' 'etc/lxdm/LoginReady' 'etc/lxdm/PostLogin'
        'etc/lxdm/PostLogout' 'etc/lxdm/PreReboot' 'etc/lxdm/PreShutdown')
source=(http://downloads.sourceforge.net/lxdm/$_prgname-$pkgver.tar.xz
        default-config.patch
        lxdm.pam
        Xsession
	lxdm-set_DESKTOP_SESSION_in_env.diff)
md5sums=('a51686720e606ca456d7f56ae4159d1f'
         '2ba18992efef43f84061717f0550e4b6'
         'c941ef896248bc7c03901b513490425c'
         'd9c8f8c9e6de52dbc389696454c8f572'
	 '1daf0bb13c4cf8bb1d79a1f2dff22e8f')

prepare(){
  cd "$srcdir/$_prgname-$pkgver"

  # Adjust Arch-specific settings
  patch -Np1 -i ../default-config.patch

  # Use our custom pam and Xsession files
  cp ../lxdm.pam pam/lxdm
  cp ../Xsession data/Xsession
  
  # Support for pulseaudio
  echo 'test -x /usr/bin/pax11publish && /usr/bin/pax11publish -r' >>data/PostLogout.in

  # Support ck (Patch from Slackware guys via slackbuilds.org)
  patch -p1 < ../lxdm-set_DESKTOP_SESSION_in_env.diff
}

build() {
  cd "$srcdir/$_prgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib/lxdm \
              --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$_prgname-$pkgver"
  make DESTDIR="$pkgdir" install
  chmod 644 "$pkgdir/etc/lxdm/lxdm.conf"

  # Home directory
  install -dm 755 "$pkgdir/var/lib/lxdm"
  echo 'GDK_CORE_DEVICE_EVENTS=true' > "$pkgdir"/var/lib/lxdm/.pam_environment
  chown -R 121:121 "$pkgdir/var/lib/lxdm"
}
