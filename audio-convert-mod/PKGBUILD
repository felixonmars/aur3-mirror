# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>
# Contributor: Rafael G. Martins <rafael@rafaelmartins.com>
# Contributor: Kostin Dmitrij [DsTr] <kostindima@ymail.com>
# Contributor: Rodney 'Caffne' <caffne@undecaf.net>
# Contributor: Felipe de Oliveira Tanus <fotanus@gmail.com>

pkgname=audio-convert-mod
pkgver="3.46.0b"
pkgrel=6
pkgdesc="Simple audio file converter"
arch=('any')
url="http://www.diffingo.com/oss/${pkgname}"
license=('GPL')
depends=('pygtk' 'glade' 'mutagen')
optdepends=('mac: reading and writing APE'
            'vorbis-tools: reading and writing OGG'
            'musepack-tools: reading and writing MPC'
            'faad2: reading AAC'
            'faac: writing AAC'
            'lame: reading and writing MP3'
            'flac: reading and writing FLAC'
            'a52dec: reading AC3'
            'ffmpeg: writing AC3 and resampling'
            'wavpack: reading and writing WV')
makedepends=('intltool')
options=(!strip !zipman)
source=(http://www.diffingo.com/downloads/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ee500c8cdee8923df6cc15d2ea5e137c')

build() {
  cd $pkgname-$pkgver
  sed -i "s|#! /usr/bin/python|#! /usr/bin/python2|" src/acm-runapp.pyw
  sed -i "s|python|python2|" bin/${pkgname}.in
  sed -i "s|mpp|mpc|g" src/${pkgname//-/_}/formats.py src/acm-runapp.pyw
  ./configure --prefix=/usr PYTHON=python2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=${pkgdir} install
}
