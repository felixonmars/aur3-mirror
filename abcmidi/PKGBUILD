# Contributor: Christopher Arndt <chris@chrisarndt.de>
# Contributor: Luis Pablo Gasparotto <lpgasparotto at gmail dot com>
# Contributor: Phillip Fynan <pfynan@gmail.com>
# Maintainer: Peter Sutton <foxxy@foxdogstudios.com>

pkgname=abcmidi
pkgver=20141225
pkgrel=1
pkgdesc="A set of tools for converting ABC files to MIDI files and vice versa, as well as other small utilities"
url="http://abc.sourceforge.net/abcMIDI/"
license=('GPL')
depends=('glibc')
makedepends=('unzip')
changelog=ChangeLog
source=('http://ifdo.pugmarks.com/~seymour/runabc/abcMIDI-2014-12-25.zip')
arch=(i686 x86_64 ppc)
sha256sums=('eb58236eb80c4b454c16c7df36ebb38712f60074ebbd0cc5cc992fb2ff6bc3cc')

build() {
    cd $srcdir/abcmidi
    make 'binaries=abc2midi midi2abc abc2abc mftext midicopy abcmatch' || return 1
}

package() {
    cd $srcdir/abcmidi
    mkdir -p $pkgdir/usr/bin
    make prefix=$pkgdir/usr 'binaries=abc2midi midi2abc abc2abc mftext midicopy abcmatch' install
    # install programming documentation to docdir
    install -m 755 -d $pkgdir/usr/share/doc/$pkgname/programming
    install -m 644 doc/programming/*.txt doc/programming/*.abc \
        $pkgdir/usr/share/doc/$pkgname/programming
    # install example ABC files to docdir
    install -m 755 -d $pkgdir/usr/share/doc/$pkgname/samples
    install -m 644 samples/*.abc $pkgdir/usr/share/doc/$pkgname/samples
}

