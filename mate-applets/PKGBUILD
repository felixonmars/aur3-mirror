# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-applets
pkgver=1.6.1
pkgrel=12
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://mate-desktop.org"
license=('GPL')
depends=('mate-character-map' 'mate-panel' 'python2-dbus' 'python2-gobject'
         'upower')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'mate-doc-utils' 'networkmanager' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-extras')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        48ca1a9f1638fa5321af4785a36c3adef46268b2.diff
        302344d247784c10ab688f71b6c8e1b8638bf35d.diff
        f17654d853e603b1e4c6c22ffb1abbd6b364f4dc.diff
        1e464e89fa0305266e72e3fce43f49e59b4a8ef8.diff)
sha1sums=('520b064b45c9e0677ea5dea0f51698d3bc982371'
          '0b0f47c76f8fa513f05f8bb3749ab9cc5ab4fc40'
          '6ff60e0f6840d904ab96d433e62113f57f93f84c'
          '2ec083203e9d7609aa0e5eeb870160f00706c5e2'
          '36c4507e54b21fbcff49732077912dbc5283807a')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    # Corrent the invest icon name
    patch -Np1 -i "${srcdir}/48ca1a9f1638fa5321af4785a36c3adef46268b2.diff"
    
    # Upate yahoo finance http address
    # - https://github.com/mate-desktop/mate-applets/pull/61
    patch -Np1 -i "${srcdir}/302344d247784c10ab688f71b6c8e1b8638bf35d.diff"
    
    # battstat applet: ready for upower-1.0
    # - https://github.com/mate-desktop/mate-applets/pull/63
    # - https://github.com/mate-desktop/mate-applets/pull/64
    patch -Np1 -i "${srcdir}/f17654d853e603b1e4c6c22ffb1abbd6b364f4dc.diff"    
    patch -Np1 -i "${srcdir}/1e464e89fa0305266e72e3fce43f49e59b4a8ef8.diff"
    # Use python2
    sed -i 's/env python/env python2/' invest-applet/invest/{chart.py,invest-applet.py,mate-invest-chart}
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-polkit \
        --enable-networkmanager \
        --enable-ipv6 \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
