# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Lukas Nagl <LukasN(at)gmx(dot)at>
# Contributor: DocterD <docterdee(at)gmail(dot)com>
# Contributor: benwbooth <benwbooth(at)gmail(dot)com>

pkgname=air-video-server
pkgver=alpha6
pkgrel=5
pkgdesc="Air Video is a Client/Server-Mediaplayer, which allows you to stream videos directly from your PC to your iPhone"
arch=('i686' 'x86_64')
url="http://www.inmethod.com/air-video/index.html"
license=('custom')
depends=('zlib' 'faad2>=2.7' 'lame' 'x264>=20100107' 'faac' 'gpac' 'java-runtime')
makedepends=('yasm')
backup=('etc/conf.d/air-video-server')
install=${pkgname}.install
source=(http://inmethod.com/air-video/download/ffmpeg-for-2.4.5-beta7.tar.bz2
        http://www.inmethod.com/air-video/download/linux/${pkgver}/AirVideoServerLinux.jar
        ${pkgname}.properties
        ${pkgname}.sh
        ${pkgname}.rc
        ${pkgname}.autostart
        ${pkgname}.service
        mp4creator.sh
        LICENSE)
noextract=('AirVideoServerLinux.jar')
md5sums=('a5d399dc97a1d1efc924f7807f57b506'
         'b619c088eea230afa92181393a36e1c0'
         '45a6f1ec1d67dba8de940c8ddd327a56'
         '0dd2a26625ca5754380c346dc34a871b'
         '2b738be41e70fe1aac3259b4f9584f29'
         '702f82c153faf742ab451bf2f26500ea'
         'e099a2317c599d99d75a9af2f8ba6fd0'
         '6e9403f1dd14654844ece68a3cc35f7e'
         '44dd1b2e8cf08cb372ba51217d267ad5')
                                    
build() {
  mkdir ${srcdir}/tmp
  export TMPDIR=${srcdir}/tmp

  cd ${srcdir}/ffmpeg
  ./configure --prefix=/usr --enable-pthreads --disable-shared --enable-static \
    --enable-gpl --enable-libx264 --enable-libmp3lame
  make

  unset TMPDIR
  rmdir ${srcdir}/tmp
}

package() {
  cd ${srcdir}
  
  install -m755 -d ${pkgdir}/usr/share/${pkgname}
  install -m644 AirVideoServerLinux.jar ${pkgdir}/usr/share/${pkgname}
  install -m644 ${pkgname}.properties ${pkgdir}/usr/share/${pkgname}
  install -m755 ffmpeg/ffmpeg ${pkgdir}/usr/share/${pkgname}
  install -Dm755 mp4creator.sh ${pkgdir}/usr/share/${pkgname}/mp4creator
  install -Dm755 ${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}

  install -Dm755 ${pkgname}.rc ${pkgdir}/etc/rc.d/${pkgname}
  install -Dm644 ${pkgname}.properties ${pkgdir}/etc/conf.d/${pkgname}
  install -Dm644 ${pkgname}.autostart ${pkgdir}/etc/xdg/autostart/${pkgname}.desktop
  install -Dm644 ${pkgname}.service ${pkgdir}/etc/avahi/services/${pkgname}.service

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}/ffmpeg
  install -m644 ffmpeg/{LICENSE,COPYING.*} ${pkgdir}/usr/share/licenses/${pkgname}/ffmpeg
}
