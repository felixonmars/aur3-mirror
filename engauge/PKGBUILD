# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=engauge  
pkgver=4_1
pkgrel=7
pkgdesc="Engauge Digitizer: A Digitizer for curves and scanned plots"
url="http://digitizer.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt3' 'fftw')
source=("http://downloads.sourceforge.net/sourceforge/digitizer/digit-src-$pkgver.tar.gz" "$pkgname.desktop")
md5sums=('b76b5d6c6b47ad57e789fd3ffbbb543f'
         'a7f054aa909e135c262b2e23f4bd7286')

build() {
  cd $srcdir/$pkgname
  export QTDIR=/opt/qt
  export PATH=/opt/qt/bin:$PATH
  export LDFLAGS="${LDFLAGS} -L/opt/qt/lib -L/opt/kde/lib -L/opt/kde/lib/kde3"
  [ $CARCH == x86_64 ] && export QMAKESPEC=/opt/qt/mkspecs/linux-g++-64
  [ $CARCH == i686 ] && export QMAKESPEC=/opt/qt/mkspecs/linux-g++
  
  sed -i "s/stdlib\.h/unistd\.h/g" src/pointmatch.cpp
  qmake -unix digitizer.pro
  make 
}
package() {
  cd $srcdir/$pkgname
  install -Dm755 bin/$pkgname $pkgdir/usr/bin/$pkgname
  install -Dm644 usermanual/animation.png \
    $pkgdir/usr/share/icons/$pkgname.png 
  install -d $pkgdir/usr/share/$pkgname/doc 
  cp -r samples $pkgdir/usr/share/$pkgname/doc 
  cp -r usermanual $pkgdir/usr/share/$pkgname/doc 
 install -Dm644 $srcdir/$pkgname.desktop \
   $pkgdir/usr/share/applications/$pkgname.desktop
}
