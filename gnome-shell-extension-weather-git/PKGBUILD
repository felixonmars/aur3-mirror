#Submitter: linx56
#Maintainer: Simon Legner <Simon.Legner@gmail.com>
pkgname=gnome-shell-extension-weather-git
pkgver=20111023
pkgrel=1
pkgdesc="A GNOME Shell extension for displaying weather notifications"
arch=('any')
url="https://github.com/simon04/gnome-shell-extension-weather"
license=('GPL3')
depends=('gnome-shell-extensions-common>=3.2.0' 'dconf' 'gettext' 'pkg-config')
makedepends=('git' 'gnome-common' 'autoconf' 'automake' 'intltool')
install=('gschemas.install')

_gitroot="https://github.com/simon04/gnome-shell-extension-weather"
_gitname="gnome-shell-extension-weather"
build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"

  cd "$srcdir/$_gitname"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR=${pkgdir} install
}
