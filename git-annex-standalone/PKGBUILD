# Original Author: Sairon Istyar <saironiq@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-annex-standalone
pkgver=5.20140127
pkgrel=1
pkgdesc='Standalone precompiled version of git-annex bundled with libs and utils, batteries included.'
arch=('i686' 'x86_64')
url="http://git-annex.branchable.com/"
license=(GPL3)
provides=(git-annex)
conflicts=(git-annex git-annex-bin)
source=(git-annex runshell.patch)
md5sums=(2214005f3cd746b6c51e8e59402770a4 f67548693207e48892631e912de5c9e8)

# binaries to remove from resulting standalone package
_rmbin=(cp curl git gpg lsof rsync sh sha1sum sha224sum \
        sha256sum sha384sum sha512sum ssh wget xargs)

# all of these can be left out if not deleting $_rmbin binaries
depends=(coreutils curl git gnupg lsof rsync bash openssh wget findutils dnsutils)

if [ "${CARCH}" = "x86_64" ] ; then
  _file=git-annex-standalone-amd64-${pkgver}.tar.gz
  source+=(${_file}::https://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-amd64.tar.gz)
  md5sums+=(ca0a0a97daf8e14b870a01368955609e)
else
  _file=git-annex-standalone-i386-${pkgver}.tar.gz
  source+=(${_file}::https://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-i386.tar.gz)
  md5sums+=(162de994a1036a1d8bb479a578bbacb8)
fi

package() {
  mkdir -p $pkgdir/opt $pkgdir/usr/bin

  cp -R $srcdir/git-annex.linux $pkgdir/opt/

  patch $pkgdir/opt/git-annex.linux/runshell $srcdir/runshell.patch

  for file in ${_rmbin[@]} ; do
    msg "  Removing $file executable from package..."
    rm $pkgdir/opt/git-annex.linux/bin/$file || true # curl (and lsof) does not exist in 64-bit build -.-"
  done

  # 64-bit curl fix (endless loop in assistant when creating new repo - incompatible curl bin (system) and lib (package))
  if [ "${CARCH}" = "x86_64" ] ; then
    rm $pkgdir/opt/git-annex.linux/usr/lib/x86_64-linux-gnu/libcurl.so.4
  fi

  cp $srcdir/git-annex $pkgdir/usr/bin/
  chmod +x $pkgdir/usr/bin/git-annex
  ln -s git-annex $pkgdir/usr/bin/git-annex-webapp
  ln -s git-annex $pkgdir/usr/bin/git-annex-shell
}
