# Maintainer: Sairon Istyar <saironiq@gmail.com>

pkgname=git-annex-standalone
pkgver=4.20130909
pkgrel=2
pkgdesc='Standalone precompiled version of git-annex bundled with libs and utils, batteries included. Choking Hazard: This product has small parts; keep away from children under 3 years old.'
arch=('i686' 'x86_64')
url="http://git-annex.branchable.com/"
license=(GPL3)
provides=(git-annex)
conflicts=(git-annex git-annex-bin)

# binaries to remove from resulting standalone package
_rmbin=(cp curl git gpg lsof rsync sh sha1sum sha224sum \
        sha256sum sha384sum sha512sum ssh wget xargs)

# all of these can be left out if not deleting $_rmbin binaries
depends=(coreutils curl git gnupg lsof rsync bash openssh wget findutils)

if [ "${CARCH}" = "x86_64" ] ; then
  _file=git-annex-standalone-amd64-${pkgver}.tar.gz
  source=(${_file}::http://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-amd64.tar.gz \
          git-annex \
          runshell.patch)
  md5sums=('f1ccfd5fe6c8af4fd8a2e0747e026d9f' \
           '2214005f3cd746b6c51e8e59402770a4' \
           'f67548693207e48892631e912de5c9e8')
else
  _file=git-annex-standalone-i386-${pkgver}.tar.gz
  source=(${_file}::http://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-i386.tar.gz \
          git-annex \
          runshell.patch)
  md5sums=('c316692bdcac5736cef0329e77cb874b' \
           '2214005f3cd746b6c51e8e59402770a4' \
           'f67548693207e48892631e912de5c9e8')
fi

package() {
  mkdir -p $pkgdir/opt $pkgdir/usr/bin

  cp -R $srcdir/git-annex.linux $pkgdir/opt/

  patch $pkgdir/opt/git-annex.linux/runshell $srcdir/runshell.patch

  for file in ${_rmbin[@]} ; do
    msg "  Removing $file executable from package..."
    rm $pkgdir/opt/git-annex.linux/bin/$file || true # curl (and lsof) does not exist in 64-bit build -.-"
  done

  # 64-bit curl fix (endless loop in assistant when creating new repo - incompatible curl bin (system) and lib (package))
  if [ "${CARCH}" = "x86_64" ] ; then
    rm $pkgdir/opt/git-annex.linux/usr/lib/x86_64-linux-gnu/libcurl.so.4
  fi

  cp $srcdir/git-annex $pkgdir/usr/bin/
  chmod +x $pkgdir/usr/bin/git-annex
  ln -s git-annex $pkgdir/usr/bin/git-annex-webapp
  ln -s git-annex $pkgdir/usr/bin/git-annex-shell
}
