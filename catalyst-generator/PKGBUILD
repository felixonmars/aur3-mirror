# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Shen Miren <dickeny@gmail.com> (catalyst_build_module idea and important code)
# Great Contributor: B. M. Kwapinski <lord.xml@web.de> (makepkg idea)
# Contributor: aidanlinz
# Contributor: Rip-Rip
# Contributor: nob
# Contributor: Sebastian Siebert


pkgname=catalyst-generator
pkgver=12.6
pkgrel=1
pkgdesc="AMD/ATI drivers. Generator of catalyst-{kernver} packages with fglrx module inside"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('catalyst-utils' 'gcc-libs' 'gcc>4.0.0' 'make' 'patch')
replaces=('catalyst' 'catalyst-hook' 'catalyst-daemon')
conflicts=('catalyst-test' 'nvidia' 'xf86-video-ati' 'xf86-video-radeonhd' 'ati-dri' 'catalyst-hook' 'catalyst' 'catalyst-daemon')
provides=("catalyst=${pkgver}")
install=${pkgname}.install

#patch_file="fglrx-2.6.36.patch"
#patch_md5="eb58dbf993dbf8d77924055cc8caaeb6"
#patch_ver="2.6.36"

source=(
    http://www2.ati.com/drivers/linux/amd-driver-installer-${pkgver/./-}-x86.x86_64.run
    catalyst_build_module
    catalyst-PKGBUILD
    catalyst.install
    ati_make.sh
    makefile_compat.patch
    3.4.patch)
#    ${patch_file})

md5sums=('6bd4f0875202390ed7122696db29be77'
         '89cfe9284e94a2ea1b4666757b7796c4'
	 'db806714224445853a8d58cf0454a9d9'
	 '52798397c5089b5cca1036cad1d3ab0f'
	 '660396540b0ceaff71d6155c986734de'
	 '3e1b82bd69774ea808da69c983d6a43b'
	 '7b229c6e48ee3f1af0d97059b9fa5cfe')
#         ${patch_md5})


build() {
  ## Unpack archive
    msg "Unpacking archive ..."
    /bin/sh ./amd-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files
}

package() {
    # License
      install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}" || return 1
      install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1

      if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/arch/x86/usr" || return 1
	_lib=lib
      elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/arch/x86_64/usr" || return 1
	_lib=lib64
      fi

    # Prepare modules source files
      _archdir=x86_64
      test "${CARCH}" = "i686" && _archdir=x86
      cd "${srcdir}/archive_files/"
      install -m755 -d "${pkgdir}/usr/share/ati/build_mod" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/*.c \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/*.h \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/2.6.x/Makefile \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m755 -d "${pkgdir}/usr/bin" || return 1
      install -m755 "${srcdir}/catalyst_build_module" "${pkgdir}/usr/bin" || return 1

      sed -i -e "s/catver=.*/catver=${pkgver}-${pkgrel}/" "${pkgdir}/usr/bin/catalyst_build_module"

    # catalyst pkgbuild
      install -m644 "${srcdir}/catalyst-PKGBUILD" "${pkgdir}/usr/share/ati/build_mod/PKGBUILD" || return 1
      sed -i -e "s/pkgver=.*/pkgver=${pkgver}/" "${pkgdir}/usr/share/ati/build_mod/PKGBUILD" || return 1
      sed -i -e "s/pkgrel=.*/pkgrel=${pkgrel}/" "${pkgdir}/usr/share/ati/build_mod/PKGBUILD" || return 1

    # catalyst.install
      install -m644 "${srcdir}/catalyst.install" "${pkgdir}/usr/share/ati/build_mod/" || return 1

    # modified ati's make.sh script
      install -m755 "${srcdir}/ati_make.sh" "${pkgdir}/usr/share/ati/build_mod/" || return 1

    # makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
    # works only in combination with ati_make.sh script
      install -m644 "${srcdir}/makefile_compat.patch" "${pkgdir}/usr/share/ati/build_mod/" || return 1

      install -m644 "${srcdir}/3.4.patch" "${pkgdir}/usr/share/ati/build_mod/" || return 1
    # optional patch
#      for ver in ${patch_ver}; do
#      install -m644 "${srcdir}/${patch_file}" \
#                "${pkgdir}/usr/share/ati/build_mod/${ver}.patch" || return 1
#      done

}
