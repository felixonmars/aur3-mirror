# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=simpleid
pkgver=0.8.2
pkgrel=1
pkgdesc='Simple, personal OpenID provider written in PHP'
arch=('any')
url='http://simpleid.sourceforge.net/'
license=('GPL2')
backup=("etc/webapps/$pkgname/config.inc")
depends=('php')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('bf3a21f6acf2e98f464d2f5056b0189c')

package() {
  cd $pkgname
  # webapps
  install -d -m 755 "$pkgdir/usr/share/webapps"
  cp -a www "$pkgdir/usr/share/webapps/$pkgname"
  # storing directory
  install -d -m 755 "$pkgdir"/var/lib/$pkgname/{store,identities}
  install -d -m 755 "$pkgdir/var/cache/$pkgname"
  # docs
  install -d -m 755 "$pkgdir/usr/share/doc/$pkgname"
  for i in CHANGELOG.txt README.txt UPGRADE.txt COPYING.txt \
      identities/example.identity.dist; do
    install -D -m 644 "$i" "$pkgdir/usr/share/doc/$pkgname/$(basename "$i")"
  done
  # move config into /etc
  install -d -m 755 "$pkgdir/etc/webapps/$pkgname"
  mv "$pkgdir/usr/share/webapps/$pkgname/config.inc.dist" \
     "$pkgdir/etc/webapps/$pkgname/config.inc"
  ln -s "/etc/webapps/$pkgname/config.inc" \
        "$pkgdir/usr/share/webapps/$pkgname/config.inc"
  # default apache config
  install -d -m 755 "$pkgdir/etc/httpd/conf/extra"
  cat > "$pkgdir/etc/httpd/conf/extra/$pkgname.conf" <<-EOF
  Alias /$pkgname "/usr/share/webapps/$pkgname"
  <Directory "/usr/share/webapps/$pkgname">
    AllowOverride All
    Options FollowSymlinks
    Order allow,deny
    Allow from all
  </Directory>
EOF
}

# vim:set ts=2 sw=2 ft=sh et:
