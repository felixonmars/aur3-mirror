# Maintainer: Ryon Sherman <ryon.sherman@gmail.com>
# Contributor: Schala
# Contributor: kart0ffelsack

_pkgname="craftbukkit"

pkgname="${_pkgname}-devel"
pkgver=1.7.2_R0.1_20131201.031937_2
pkgrel=1
pkgdesc="Minecraft server implementing the Bukkit API (development)"

_pkgver="$(echo ${pkgver} | cut -d _ -f 1,2)"
_pkgver="${_pkgver/_/-}"
__pkgver="${_pkgver/-/}"
_timestamp="$(echo ${pkgver} | cut -d _ -f 3)"
_pkgnum="$(echo ${pkgver} | cut -d _ -f 4)"

provides=("${_pkgname}=${__pkgver,,}" "${_pkgname}-stable=${__pkgver,,}")
conflicts=("${_pkgname}" "${_pkgname}-stable")

arch=('any')
license=('LGPL')
url="http://bukkit.org"

depends=(java-runtime-headless tmux sudo)
makedepends=('xmlstarlet')

_jarfile="${_pkgname}-${_pkgver}-${_timestamp}-${_pkgnum}.jar"

source=(
    "http://repo.bukkit.org/content/groups/public/org/bukkit/${_pkgname}/${_pkgver}-SNAPSHOT/${_jarfile}"
    "${_pkgname}.service"
    "${_pkgname}.sh"
)
md5sums=(
    'SKIP'
    '0ba6b038ca1b04ef1e4f2746e3011a71'
    'b7905af90a28e46e14e2771a2b3a71ff'
)

noextract=($_jarfile)
install=craftbukkit-devel.install

pkgver() {
    rss=$(curl -s http://dl.bukkit.org/downloads/bukkit/feeds/latest-dev.rss)
    rss=$(echo ${rss} | xml sel -t -v "/rss/channel/item[1]/title")
    version=$(echo ${rss} | cut -d ' ' -f 5)
    build=$(echo ${rss} | cut -d ' ' -f 7)
    xml=$(curl -s "http://repo.bukkit.org/content/groups/public/org/bukkit/${_pkgname}/${version}-SNAPSHOT/maven-metadata.xml")
    timestamp=$(echo ${xml} | xml sel -t -v "/metadata/versioning/snapshot/timestamp")
    number=$(echo ${xml} | xml sel -t -v "/metadata/versioning/snapshot/buildNumber")
    echo "${version/-/_}_${timestamp}_${number}"
}

package() {
    install -Dm0644 "${srcdir}/${_jarfile}" "${pkgdir}/srv/${_pkgname}/${_pkgname}.jar"
    install -Dm0644 "${srcdir}/${_pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${_pkgname}.service"
    install -Dm0755 "${srcdir}/${_pkgname}.sh" "${pkgdir}/srv/${_pkgname}/${_pkgname}.sh"
}
