# Contributor: Cian Mc Govern <cian@cianmcgovern.com>
# Contributor: Roland Singer <roland@manjaro.org>
# Contributor: TheBenj <thebenj88 *AT* gmail *DOT* com>
# Maintainer: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>

pkgname=crossover
pkgver=12.2.0
pkgrel=1
_pkgdebrel=1
pkgdesc="Run Windows Programs on Linux"
arch=('i686' 'x86_64')
url="http://www.codeweavers.com"
license=('custom')
changelog=CHANGELOG
makedepends=('tar')
install=${pkgname}.install
replaces=('crossover-games' 'crossover-pro' 'crossover-standard')
depends=('python' 'alsa-lib' 'libsm' 'libxext' 'libxrandr' 'libice' 'pygtk'
    'desktop-file-utils' 'fontconfig' 'libxcursor' 'libxdamage'
    'libxxf86dga' 'mesa' 'libpng' 'libjpeg' 'libcups' 'python2-dbus')

optdepends=('libxcursor: coloured mouse pointer support'
    'libxi: enables joystick and tablet support'
    'libxinerama: enables spanning multiple screens'
    'openssl:  support for secure Internet communication'
    'libxxf86vm: perform gamma adjustments'
    'unzip: required to install Guild Wars, automatic installer extraction'
)
source=("http://media.codeweavers.com/pub/${pkgname}/cxlinux/demo/${pkgname}_${pkgver}-1_i386.deb" "cxoffice.conf")
sha256sums=('1fd0006356f39b8bb13fee17ff37697cd05bba729e9988832f3aa69f8e65fc59'
            '8b3b1d0d996ed904ea5c161b446b2ea5ee2195a8cf79e9f7732e57aa99a83a01')

if [ $CARCH = "x86_64" ]; then
    depends=('python' 'fontconfig' 'desktop-file-utils' 'alsa-lib' 'lib32-alsa-lib'
        'lib32-fontconfig' 'lib32-libxcursor' 'libxxf86dga' 'libxrandr'
        'libxdamage' 'lib32-libxdamage' 'mesa' 'lib32-mesa' 'lib32-glibc'
        'libxcursor' 'lib32-libsm' 'lib32-libxext' 'lib32-zlib' 'lib32-gcc-libs'
        'lib32-libxrandr' 'lib32-libice' 'lib32-util-linux-ng' 'lib32-e2fsprogs'
        'pygtk' 'lib32-lcms' 'lib32-libpng' 'lib32-libjpeg' 'lib32-libcups'
        'python2-dbus'
    )

    optdepends=('lib32-nvidia-utils: enables 3D under nvidia cards'
        'lib32-catalyst-utils: enables 3D under ati cards'
        'lib32-libxcursor: coloured mouse pointer support'
        'lib32-libxinerama: enables spanning multiple screens'
        'lib32-openssl:  support for secure Internet communication'
        'lib32-libxxf86vm: perform gamma adjustments'
        'lib32-libxi: enables joystick and tablet support'
        'unzip: required to install Guild Wars, automatic installer extraction'
    )

    source[0]="http://media.codeweavers.com/pub/${pkgname}/cxlinux/demo/ia32-${pkgname}_${pkgver}-1_amd64.deb"
    sha256sums[0]="4eb22d727d9a5c58621203dc133e5ce71d891698e0c2579451011855daac38d9"
fi

package() {
    cd "${srcdir}"

    if [ $CARCH = 'i686' ] ; then
        ar -p crossover_${pkgver}-${_pkgdebrel}_i386.deb data.tar.gz | tar zxf - -C "${pkgdir}" || return 1
        rm -fr ${pkgdir}/opt/cxoffice/lib/nsplugin-linux64.so
    else
        ar -p ia32-crossover_${pkgver}-${_pkgdebrel}_amd64.deb data.tar.gz | tar zxf - -C "${pkgdir}" || return 1
    fi

    rm "${pkgdir}/opt/cxoffice/doc" # remove symbolic link
    mkdir "${pkgdir}/opt/cxoffice/doc" # create real directory


    if [ $CARCH = 'i686' ] ; then
        mv "${pkgdir}/usr/share/doc/crossover/"* "${pkgdir}/opt/cxoffice/doc"
    else
        mv "${pkgdir}/usr/share/doc/ia32-crossover/"* "${pkgdir}/opt/cxoffice/doc"
    fi


    gzip -d "${pkgdir}/opt/cxoffice/doc/license.txt.gz"
    rm "${pkgdir}/usr" -r
    #install -m 644 -D "${pkgdir}/opt/cxoffice/doc/license.txt" "${pkgdir}/usr/share/licenses/crossover/license"
    sed s/\;\;"\"MenuRoot\" = \"\""/"MenuRoot = Windows Games/" -i "${pkgdir}/opt/cxoffice/share/crossover/bottle_data/cxbottle.conf"
    sed s/\;\;"\"MenuStrip\" = \"\""/"MenuStrip = 1/" -i "${pkgdir}/opt/cxoffice/share/crossover/bottle_data/cxbottle.conf"

    mkdir -p "${pkgdir}/usr/bin"
    ln -s /opt/cxoffice/bin/wine "${pkgdir}/usr/bin/crossover"

    # Fix Auto update error
    install -m 644 -D "${srcdir}/cxoffice.conf" "${pkgdir}/opt/cxoffice/etc/cxoffice.conf"

    # Changelog for pacman -Qc
    zcat "${pkgdir}/opt/cxoffice/doc/changelog.gz" > "${startdir}/CHANGELOG"

    # place license in correct directory
    install -D -m644 /opt/cxoffice/doc/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
