# Maintainer: Christofer Bertonha <chritoferbertonha@gmail.com>

pkgname=vtk-python
mainver=5.6
pkgver=${mainver}.1
pkgrel=3
pkgdesc='A software system for 3D computer graphics, image processing, and visualization which supports a wide variety of visualization algorithms and advanced modeling techniques.'
arch=('i686' 'x86_64')
url='http://www.vtk.org/'
license=('BSD')
depends=('python2' 'zlib' 'qt' 'libpng' 'libtiff' 'libxml2' 'expat' 'nvidia-cg-toolkit' 'boost-libs' 'libjpeg-turbo')
makedepends=('cmake' 'python2' 'zlib' 'qt' 'libpng' 'libtiff' 'libxml2' 'expat' 'nvidia-cg-toolkit' 'boost' 'libjpeg-turbo')
provides=(vtk)
conflicts=(vtk)
source=("http://www.vtk.org/files/release/${mainver}/vtk-${pkgver}.tar.gz")
md5sums=('b80a76435207c5d0f74dfcab15b75181')

build() {
  cd ${srcdir}

  sed \
    -e '1i#include <cstddef>' \
    -i VTK/Common/vtkPythonUtil.cxx

 sed \
    -e 's#ptrdiff_t#std::ptrdiff_t#g' \
    -i VTK/Utilities/vtkmetaio/metaUtils.cxx

 sed \
    -e 's#png_set_gray_1_2_4_to_8#png_set_expand_gray_1_2_4_to_8#g' \
    -i VTK/IO/vtkPNGReader.cxx

  mkdir -p build && cd build

  cmake \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DBUILD_TESTING:BOOL=OFF \
    -DBUILD_EXAMPLES:BOOL=OFF \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DVTK_USE_CG_SHADERS:BOOL=ON\
    -DVTK_USE_BOOST:BOOL=ON \
    -DVTK_USE_QT:BOOL=ON \
    -DVTK_USE_TK:BOOL=OFF \
    -DVTK_USE_QVTK_QTOPENGL:BOOL=ON \
    -DVTK_USE_SYSTEM_EXPAT:BOOL=ON \
    -DVTK_USE_SYSTEM_FREETYPE:BOOL=ON \
    -DVTK_USE_SYSTEM_JPEG:BOOL=ON \
    -DVTK_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DVTK_USE_SYSTEM_PNG:BOOL=ON \
    -DVTK_USE_SYSTEM_TIFF:BOOL=ON \
    -DVTK_USE_SYSTEM_ZLIB:BOOL=ON \
    -DVTK_WRAP_PYTHON:BOOL=ON \
    -DPYTHON_INCLUDE_DIR:FILEPATH=/usr/include/python2.7 \
    -DPYTHON_LIBRARY:FILEPATH=/usr/lib/libpython2.7.so \
    -DVTK_PYTHON_SETUP_ARGS:STRING=--root=${pkgdir} \
    ../VTK

  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
  
  ln -s ${pkgdir}/usr/lib/vtk-${mainver}/*.so* ${pkgdir}/usr/lib/

  # install BSD license
  install -d ${pkgdir}/usr/share/licenses/vtk
  install -m 644 ../VTK/Copyright.txt \
    ${pkgdir}/usr/share/licenses/vtk
}
