# Get help on the forums
# http://forum.diyefi.org/viewtopic.php?f=41&t=2124

# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>
pkgname=freeems-loader-git
_gitname=freeems-loader
pkgver=309.5b1a497
pkgrel=1
pkgdesc="GUI based s19 loader for use with s12x targets with special features for freeems"
arch=("i686" "x86_64")
url="https://github.com/seank/FreeEMS-SerialIO"
license=('GPL')
depends=("qt4>=4.7.0" "freeems-serialio-git")
makedepends=("git" "qconf")
source=("freeems-loader::git+git://github.com/seank/freeems-loader.git"
        "0001-Ack-the-libserialIO-path.patch"
        "0001-Do-not-treat-alerts-as-errors.patch")
md5sums=('SKIP'
         '69bc2affc19e2bd7f80c8eafdc81a970'
         '053a1680fbca43b4a5fd8915ce188e45')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}
build() {
  cd "$_gitname"

  git apply "$srcdir/0001-Ack-the-libserialIO-path.patch"
  git apply "$srcdir/0001-Do-not-treat-alerts-as-errors.patch"

  qmake-qt4 PREFIX="/usr" \
    LIBS="/usr/lib/libSerialIO.so" \
    PRE_TARGETDEPS="/usr/lib/libSerialIO.so" \
    INCLUDEPATH="/usr/include"
  make
}

package() {
  cd "$srcdir/$_gitname"
  make INSTALL_ROOT="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
