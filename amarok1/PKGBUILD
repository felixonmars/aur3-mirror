# Maintainer: Christos Nouskas <nous%archlinux.us>
# Maintainer: Juanma Hern√°ndez <juanmah@gmail.com>

pkgname=amarok1
_origname=amarok
pkgver=1.4.10
pkgrel=14
pkgdesc="amaroK - a media player for KDE - v1.4.10 xine engine with wikipedia patch (single package)"
arch=("i686" "x86_64")
url="http://amarok.kde.org"
license=('GPL')
depends=('xine-lib>=1.1.3' 'libmysqlclient>=5.0.45' 'libnjb>=2.2.5' 'postgresql-libs>=8.2.4' 'ruby' 'kdelibs3>=3.5.7' 'libgpod>=0.6.0'  'libifp>=1.0.0.2' 'libvisual>=0.4.0' 'libmtp' 'taglib')
makedepends=('kdelibs3' 'libmtp>=0.2.6.1' 'xine-lib>=1.1.8' 'sdl>=1.2.12' 'gcc43')
provides=('amarok1')
options=(!makeflags)
install=amarok.install
source=("ftp://ftp.slackware.at/slackware-12.2/source/kde/amarok/$_origname-$pkgver.tar.bz2"
        "amarok_addaspodcast.desktop"
        "wikipedia.patch"
        "libmtp.patch"
#        "gcc44.patch"
#        "gcc45.patch"
        "amarok-1.4.10.coverfetcher.diff"
        "ruby19_configure2.patch"
        "ruby19_rstring.patch"
	"_gem_commands.rb"
	"mtpmediadevice.patch")
md5sums=('3bdbf26181bf5e5925f48968caba7ac2'
         '7736a7e3e22b15818180df3cee7e4a79'
         'a93eb0dad2078afa045a69f38256b369'
         '02eb286127594b9427d27da7eb01f0de'
#         'cfa9310dec03c33048e5a067a0334266'
#         'c441d4711f2c0ffc28a4a0b9757d397b'
         'f4e8754cde659342bdfd3fca36612bb5'
         'e3c5201663052b4f346d5002a196421a'
         '2a7ac1f359eb3cdb8bf724c8be660295'
	 '3629cf2e9acf7b0527cf590692240291'
	 '477872c47adf584d8d9a8b9f1d834a3b')

build() {
  for patch in "$srcdir"/*.{diff,patch}; do
    patch -Np0 < "$patch"
  done

  cd "$srcdir/$_origname-$pkgver"

  . /etc/profile.d/qt3.sh
  . /etc/profile.d/kde3.sh

#[ "$QTDIR" = "" ] && source /etc/profile.d/qt3.sh 
#[ "$KDEDIR" = "" ] && source /etc/profile.d/kde3.sh

  # prepare build
  ./configure CC=gcc-4.3 CXX=g++-4.3 --prefix=/opt/kde \
      --with-gnu-ld \
      --enable-mysql \
      --enable-postgresql \
      --with-mp4v2 \
      --with-ifp \
      --with-libmtp \
      --with-libnjb \
      --with-libgpod \
      --without-arts \
      --without-gstreamer \
      --with-xine \
      --without-nmm \
      --without-mas \
      --with-libvisual \
      --disable-debug \
      --enable-debug=no \
      --without-xmms

  # build
  sed -i -e 's/konquisidebar//g' amarok/src/Makefile
  # Patching convert from a char** to a const * char *.
  sed -i 's/char \*\*xine_urls = NULL/const char *const *xine_urls = NULL/' "$srcdir/amarok-1.4.10/amarok/src/engine/xine/xine-engine.cpp"
  # Why the hell is ._gem_commands.rb not extracted????
  cp $srcdir/_gem_commands.rb $srcdir/amarok-$pkgver/amarok/src/mediadevice/daap/mongrel/lib/rubygems/._gem_commands.rb

  make
}

package() {
  cd "$srcdir/$_origname-$pkgver"

  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/opt/kde/share/apps/konqueror/servicemenus/"
  install -m644 "$srcdir/amarok_addaspodcast.desktop" "$pkgdir/opt/kde/share/apps/konqueror/servicemenus/"
}
