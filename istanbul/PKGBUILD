# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=istanbul
pkgver=0.2.2
pkgrel=7
pkgdesc='Desktop Session Recorder'
arch=('i686' 'x86_64')
url='http://live.gnome.org/Istanbul'
license=('GPL2')
options=('!libtool')
depends=(
  'pygtk'
  'gnome-python-extras'
  'gstreamer0.10'
  'gstreamer0.10-python'
  'gstreamer0.10-base-plugins'
  'python2-gconf'
  'python-xlib'
  'libxdamage'
  'libxext'
  'libsm'
)
makedepends=(
  'intltool'
  'gettext'
)
install=istanbul.install
source=(
  "http://zaheer.merali.org/istanbul-$pkgver.tar.bz2"
  '01-python2.patch'
  '02-audio_eos.patch'
  '03-python-import.patch'
  '04-window-record.patch'
  '05-manpage.patch'
  '06-desktop-entry.patch'
  '07-honour-umask.patch'
  '08-eggtrayicon-removal.patch'
  '09-fix_grab_xid.patch'
  '10-fix_preview_window.patch'
  '11-dir-overwrite.patch'
  '12-kfreebsd-noalsa.patch'
  '13-URI_exception.patch'
)
md5sums=('8ddcfd5a29dcd10fdafc10af9f66848b'
         '56efc2c55d8f988b31ec02bae87faf29'
         'dbf652294a2234bac49d577ab34228ff'
         '8dac58e911bc370cd49982fe5eb4c908'
         'e22f538802b3ca36339164e72f2c8b16'
         '2a8e1036c8ec282bd5ff55b8c2a0ff8d'
         'b36448158ff1b37faaebb9aeb6571c4b'
         '622368974346109cf93ba30d89267a43'
         '0082b642e97e3481f200cefcd5acd115'
         '92ea18c9ec1c43ce15207ed2d549e405'
         '522c4a6237d2313cefecb45d363ec9e9'
         '358d015c727e3f39ad61b373fc6f29f9'
         '07a86deaf544f44f64dd78c389d1dfdc'
         '93a5aaaa5c9e535a24b78e4f90af47cc')

build() {
  shopt -s nullglob
  cd $pkgname-$pkgver
  msg "Patching..."
  for p in "$srcdir"/??-*.patch; do
    msg2 "Applying ${p##*/}"
    patch -p1 -i "$p"
  done
  ./configure --prefix=/usr \
              --sysconfdir=/etc  \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $pkgname-$pkgver
  # fix crazy python module compiling
  cp py-compile common/py-compile-destdir
  make DESTDIR="$pkgdir" install

  install -d -m 755 "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" "$pkgdir"/usr/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/usr/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 ft=sh et:
