# Maintainer: Alessandro Sagratini <ale_sagra at hotmail dot com>
# Contributor: Ben Webb <bjwebb67 at googlemail dot com>

pkgname=perl-net-ssh2
pkgver=0.46
pkgrel=2
pkgdesc="Perl support for the SSH 2 protocol via libssh2."
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Net-SSH2/"
license=('GPL' 'PerlArtistic')
depends=('perl' 'libssh2' 'openssl' 'zlib')
options=(!emptydirs)
changelog=Changes
source=(http://search.cpan.org/CPAN/authors/id/R/RK/RKITOVER/Net-SSH2-$pkgver.tar.gz)
md5sums=('3e9b26d96db5a6fe13840827deab95a4')

build() {
    cd "$srcdir/Net-SSH2-$pkgver"

    # install module in vendor directories.
    PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor || return 1
    make || return 1
}

package() {
    cd "$srcdir/Net-SSH2-$pkgver"
    make install DESTDIR=${pkgdir} || return 1
    # remove perllocal.pod and .packlist
    find "$pkgdir" -name perllocal.pod -delete
    find "$pkgdir" -name .packlist -delete
}
