# Maintainer: Nathan Hulse <nat.hulse@gmail.com>
# Workarounds taken from Xiao-Long Chen's PKGBUILDs
pkgname=compiz-core-dev
pkgseries=0.9.7
pkgver=${pkgseries}.6  
pkgrel=1
pkgdesc="Core package of the Compiz development release, with optional GNOME integration."
url="https://launchpad.net/compiz-core"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'MIT')
depends=('boost' 'xorg-server' 'libxext' 'libxdamage' 'libxcomposite' 'startup-notification' 'librsvg' 'libgl' 'dbus' 'mesa' 'libxslt' 'fuse' 'glib2' 'glibmm' 'libsm' 'libxml2' 'libxrender' 'libice' 'pango' 'libpng')
optdepends=(
  'ccsm-dev: CompizConfig Settings Manager'
  'compizconfig-backend-gconf-dev: Store settings in GNOME GConf database'
  'compiz-plugins-main-dev: Main plugins'
  'compiz-plugins-extra-dev: Extra plugins'
  'metacity: Metacity window decoration themes support (need to rebuild this package)'
  'gnome-control-center: GNOME keybindings support (need to rebuild this package)'
  'kdebase-lib: KDE window decoration support (need to rebuild this package)' 
  'automoc: KDE window decoration support (need to rebuild this package)'
)
makedepends=('intltool' 'cmake')
provides=('compiz-core')
conflicts=('compiz-core')
install="compiz.install"
source=(
  "https://launchpad.net/compiz-core/${pkgseries}/${pkgver}/+download/compiz-${pkgver}.tar.bz2"
  'compiz.desktop'
  'compiz.install'
  'gnome-compiz.desktop'
  'gnome-compiz.session'
)
md5sums=(
  'c74ed90fcee257079a0ade8966659ea5'
  '9b52b04a2d8cca7aa76a8c6569be1eb8'
  'd03e695891c8cd5237af3e9d38680f1d'
  '924d93eac6447e69bcbcc16fd5de45db'
  '1afae9c4517a3725c076260bab8c73e2'
)

#GTK window decorator support
GTKWINDOWDECORATOR="On"
#KDE window decorator support
KDEWINDOWDECORATOR="On"
#Add desktop session entries for starting compiz under GNOME 3
GNOME3SESSION="Off"

build() {
  cd "compiz-${pkgver}"  
  [[ -d build ]] || mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DBUILD_GTK="${GTKWINDOWDECORATOR}" \
    -DUSE_GCONF=On \
    -DUSE_GSETTINGS=Off \
    -DBUILD_KDE4="${KDEWINDOWDECORATOR}" \
    -DCOMPIZ_BUILD_TESTING=Off \
    -DCOMPIZ_DEFAULT_PLUGINS="composite,opengl,decor,resize,place,move" \
    -DCOMPIZ_DISABLE_PLUGIN_FADE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_SCREENSHOT=Off \
    -DCOMPIZ_DISABLE_PLUGIN_OBS=Off \
    -DCOMPIZ_DISABLE_PLUGIN_DECOR=Off \
    -DCOMPIZ_DISABLE_PLUGIN_SCALE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_RESIZE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_CLONE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_WATER=Off \
    -DCOMPIZ_DISABLE_PLUGIN_ANNOTATE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_COMMANDS=Off \
    -DCOMPIZ_DISABLE_PLUGIN_BLUR=Off \
    -DCOMPIZ_DISABLE_PLUGIN_CUBE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_COMPOSITE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_COPYTEX=Off \
    -DCOMPIZ_DISABLE_PLUGIN_GNOMECOMPAT=Off \
    -DCOMPIZ_DISABLE_PLUGIN_OPENGL=Off \
    -DCOMPIZ_DISABLE_PLUGIN_KDE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_REGEX=Off \
    -DCOMPIZ_DISABLE_PLUGIN_COMPIZTOOLBOX=Off \
    -DCOMPIZ_DISABLE_PLUGIN_SWITCHER=Off \
    -DCOMPIZ_DISABLE_PLUGIN_INOTIFY=Off \
    -DCOMPIZ_DISABLE_PLUGIN_ROTATE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_PLACE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_MOVE=Off \
    -DCOMPIZ_DISABLE_PLUGIN_WOBBLY=Off \
    -DCOMPIZ_DISABLE_PLUGIN_IMGPNG=Off \
    -DCOMPIZ_DISABLE_PLUGIN_DBUS=Off \
    -DCOMPIZ_DISABLE_PLUGIN_IMGSVG=Off 
  make
}

package() {
  if [ "${GNOME3SESSION}" == "On" ]; then
    install -dm755 -d "${pkgdir}/usr/share/"{gnome-session/sessions,xsessions,applications}/
    install -m644 gnome-compiz.session "${pkgdir}/usr/share/gnome-session/sessions/"
    install -m644 gnome-compiz.desktop "${pkgdir}/usr/share/xsessions/"
    install -m644 compiz.desktop "${pkgdir}/usr/share/applications/"
  fi
  
  cd "compiz-${pkgver}/build"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
  
  # Stupid findcompiz_install needs COMPIZ_DESTDIR and install needs DESTDIR
  #make findcompiz_install
  CMAKE_DIR=$(cmake --system-information | grep '^CMAKE_ROOT' | awk -F\" '{print $2}')
  install -dm755 "${pkgdir}${CMAKE_DIR}/Modules/"
  install -m644 ../cmake/FindCompiz.cmake "${pkgdir}${CMAKE_DIR}/Modules/"
  
  # Install documentation
  install -dm755 "${pkgdir}/usr/share/doc/compiz-core/"
  install ../{AUTHORS,NEWS,README,TODO} "${pkgdir}/usr/share/doc/compiz-core/"
}
