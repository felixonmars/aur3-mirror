# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: hybraries <macwolf@archlinux.de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Navi <navitwo.at.gmail.dot.com>
# Contributor: rabyte <rabyte.at.gmail.dot.com>
# Contributor: Johannes Krampf <wuischke.at.amule.dot.org>

pkgname=midori-gtk2-git
pkgver=20120406
pkgrel=1
pkgdesc="Lightweight web browser based on WebKit and GTK2. Git development version."
arch=('x86_64' 'i686')
url="http://twotoasts.de/index.php?/pages/midori_summary.html"
license=('LGPL')
depends=('libnotify' 'libxss' 'hicolor-icon-theme' 'desktop-file-utils' 'libwebkit' 'libunique' 'openssh') # 'granite'
makedepends=('pkg-config' 'git' 'python2' 'libxml2' 'gtk2' 'intltool' 'python2-docutils' 'libsoup' 'vala' 'librsvg')
optdepends=('aria2: download utility'
            'steadyflow: download manager')
provides=('midori')
conflicts=('midori' 'midori-git')
install=midori.install
_gitroot="git://git.xfce.org/apps/midori"
_gitname="midori"

build() {
  cd "$srcdir"

  msg "Connecting to the midori git repository..."
  if [ -d "$srcdir/$_gitname" ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  cd "$srcdir"
  rm -rf $_gitname-build
  git clone $_gitname $_gitname-build
  cd "$_gitname-build"

  msg2 "Python2 fix..."
  sed -i '0,/on/s//on2/' waf wscript
  msg2 "Configuring..."
  ./waf configure --prefix=/usr --disable-gtk3 --disable-granite
  msg2 "Compiling..."
  ./waf build 
}

package() {
  cd "$_gitname-build"

  msg2 "Packaging..."
  DESTDIR="$pkgdir" ./waf install
  msg2 "Cleaning up..."
  rmdir "$pkgdir/usr/share/gir-1.0"
}

# vim:set ts=2 sw=2 et:
