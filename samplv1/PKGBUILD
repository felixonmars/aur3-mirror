# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=samplv1
pkgver=svn661
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(i686 x86_64)
url="http://www.rncbc.org/drupal/node/473"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'jack' 'qt')
source=("http://www.rncbc.org/snapshots/$pkgname-$pkgver.tar.gz")
md5sums=('6df9bb1dd31d95c53a9838a8f7484098')

build() {
  for _i in jack lv2; do

    cd "$srcdir/$pkgname-$pkgver"
    [ -d $_i ] || mkdir $_i && cd $_i
    [ "$_i" = 'lv2' ] && mv ../$pkgname.lv2 .
    qmake ../${pkgname}_$_i.pro
    make

  done
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # standalone
  install -Dm755 jack/${pkgname}_jack \
    "$pkgdir/usr/bin/${pkgname}_jack"

  # lv2
  install -d "$pkgdir/usr/lib/lv2/$pkgname.lv2"
  install -D lv2/$pkgname.lv2/* \
    "$pkgdir/usr/lib/lv2/$pkgname.lv2"
}

# vim:set ts=2 sw=2 et:
