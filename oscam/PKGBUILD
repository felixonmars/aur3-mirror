# Maintainer: mastero23 <mastero23 at gmail dot com>

pkgname=oscam
pkgver=1.10
pkgrel=4
pkgdesc="An Open Source Conditional Access Module software"
arch=('i686' 'x86_64')
url="http://oscam.to/"
license=('unknown')
depends=('bash' 'openssl')
makedepends=('cmake' 'subversion')
options=(emptydirs)
install=oscam.install
source=(oscam.conf.d
        oscam.rc.d)
md5sums=('79ad19c4e1a9ffbc105698914240e940'
         '148f0d5e027c5e7b0ebe0fa45fd3e42e')

_svnrepo=http://streamboard.de.vu/svn/oscam/tags/1.10
_svnmod=oscam
_svnrev=5954

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $_svnrev)
  else
    svn co $_svnrepo --config-dir ./ -r $_svnrev $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DWEBIF=1 \
        -DWITH_SSL=1 \
        "$srcdir/$_svnmod-build"
  make
}

package() {
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir/" install

  install -m 755 -d "$pkgdir/etc/oscam"
  install -D -m 755 "$srcdir/oscam.rc.d" "$pkgdir/etc/rc.d/oscam"
  install -D -m 644 "$srcdir/oscam.conf.d" "$pkgdir/etc/conf.d/oscam"
}
