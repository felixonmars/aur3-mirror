# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Michal Zalewski <lcamtuf[at]coredump[dot]cx>
# Contributor: cbreaker <cbreaker[at]tlen[dot]pl>]
# Contributor: Tino Reichardt
# Contributor: Timoth√©e Ravier <tim[at]siosm[dot]fr>

pkgname=p0f
pkgver=3.06b
pkgrel=4
pkgdesc="Purely passive TCP/IP traffic fingerprinting tool"
url="http://lcamtuf.coredump.cx/p0f3/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libpcap')
install=p0f.install
source=("http://lcamtuf.coredump.cx/p0f3/releases/${pkgname}-${pkgver}.tgz")
sha256sums=('d151931be2557ac45f9baa0a9384b8643979f4227c391ed395792874f2c6cde7')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/\/usr\/local\/include\//\/usr\/include\//g' -i build.sh
  sed -e 's/\/usr\/local\/lib\//\/usr\/lib\//g' -i build.sh
  sed -e 's/"p0f.fp"/"\/etc\/p0f\/p0f.fp"/g' -i config.h
}

build() {
  cd ${pkgname}-${pkgver}
  CPPFLAGS="$CPPFLAGS -O2"
  make
  cd tools
  make p0f-client
  make p0f-sendsyn
  make p0f-sendsyn6
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 p0f "${pkgdir}"/usr/bin/p0f
  install -Dm 644 p0f.fp "${pkgdir}"/etc/p0f/p0f.fp
  install -Dm 644 docs/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm 644 docs/README "${pkgdir}"/usr/share/doc/${pkgname}/README
  install -m 755 tools/{p0f-client,p0f-sendsyn,p0f-sendsyn6} "${pkgdir}"/usr/bin
}

# vim: ts=2 sw=2 et:
