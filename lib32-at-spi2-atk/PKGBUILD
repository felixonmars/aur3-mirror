# $Id: PKGBUILD 169848 2012-10-30 22:23:57Z heftig $
# Maintainer:  jtts
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbase=at-spi2-atk
pkgname=lib32-$_pkgbase
pkgver=2.6.2
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('lib32-at-spi2-core' 'lib32-libx11' 'lib32-atk' 'lib32-dconf' 'at-spi2-atk')
makedepends=('gcc-multilib' 'intltool')
#install=at-spi2-atk.install
options=('!libtool')
source=(http://download.gnome.org/sources/$_pkgbase/${pkgver%.*}/$_pkgbase-$pkgver.tar.xz)
sha256sums=('496c8432e8ab82735145f9af5d45209e9b708bf3c94e527ee091d08641a9bcfa')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $_pkgbase-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --disable-schemas-compile
  make
}

package() {
  cd $_pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  # cleanup for lib32 package
  rm -rf "${pkgdir}"/usr/{include,share,lib32/gnome-settings-daemon-3.0}
}

# vim:set ts=2 sw=2 et:
