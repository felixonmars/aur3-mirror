# Maintainer: Christian Babeux <christian.babeux@0x80.ca> 
pkgname=sysdig
pkgver=0.1.96
pkgrel=1
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('i686' 'x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('linux-headers' 'dkms' 'jsoncpp' 'luajit' 'zlib')
makedepends=('git' 'cmake')
source=("https://github.com/draios/sysdig/archive/$pkgver.tar.gz")
sha1sums=('86a8b1d21a7cbb392f3127b98b08f1d56b0f3927')
install=${pkgname}.install

build()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    rm -rf build
    mkdir build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DDIR_ETC=/etc -DSYSDIG_VERSION=${pkgver} -DUSE_BUNDLED_LUAJIT=OFF -DUSE_BUNDLED_JSONCPP=OFF -DUSE_BUNDLED_ZLIB=OFF ..
    make
}

package()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    cd build
    make install DESTDIR=${pkgdir}
}
