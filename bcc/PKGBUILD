# Maintainer: Andre Bartke <dev@bartke.cc>
 
pkgname=bcc
pkgver=4.4.2
pkgrel=1
pkgdesc="Gaisler BCC cross-compiler for SPARC V8 LEON2 and LEON3 processors"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gcc/"
license=('GPL')
depends=("lib32-glibc" "lib32-zlib")
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip')
conflicts=("bcc-dev")
install=$pkgname.install
source=("http://www.gaisler.com/anonftp/bcc/bin/linux/sparc-elf-4.4.2-1.0.41.tar.bz2")
 
package() {
  mkdir -p $pkgdir/opt/bcc
  mkdir -p $pkgdir/usr/bin
  cp -r $srcdir/sparc-elf-$pkgver/* $pkgdir/opt/bcc

  msg "Cleaning-up cross compiler tree..."
  rm -rf ${pkgdir}/opt/{info,man}

  msg "Creating out-of-path executables..."
  mkdir -p ${pkgdir}/opt/bcc/sparc-elf/bin/
  rm -f ${pkgdir}/opt/bcc/sparc-elf/bin/*
  cd ${pkgdir}/opt/bcc/sparc-elf/bin/
  for bin in ${pkgdir}/opt/bcc/bin/sparc-elf-*; do
    bbin=`basename "$bin"`;
    ln -s "/opt/bcc/bin/${bbin}" `echo "$bbin" | sed "s#^sparc-elf-##"`;
  done

  msg "Creating /usr/bin symlinks..."
  for bin in ${pkgdir}/opt/bcc/bin/sparc-elf-*; do
    bbin=`basename "$bin"`;
    ln -s "/opt/bcc/bin/${bbin}" "${pkgdir}/usr/bin/${bbin}";
  done
}

md5sums=('95745d68bb8d9469d3d980a84e459074')
