# Maintainer: Daniel Ehlers <danielehlers@mindeye.net>
pkgname=coin-cbc
pkgver=2.6.4
pkgrel=3
pkgdesc="Cbc (Coin-or branch and cut) is an open-source mixed integer programming solver written in C++."
arch=('i686')
url="https://projects.coin-or.org/Cbc"
license=('EPL')
groups=('coin-or')
depends=('coin-utils>=2.7.1' 'coin-cgl>=0.56.2' 'coin-clp>=1.13.3')
makedepends=('coin-utils>=2.7.1' 'coin-cgl>=0.56.2' 'coin-clp>=1.13.3')
source=(http://www.coin-or.org/download/source/Cbc/Cbc-$pkgver.tgz)
md5sums=('ba14184561faa968778142cc03a7a092')

build() {
  cd "$srcdir/Cbc-$pkgver"
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="`pkg-config --libs osi`" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-clp-lib="`pkg-config --libs clp`" \
              --with-clp-incdir="/usr/include/coin/" \
              --with-cgl-lib="`pkg-config --libs cgl`" \
              --with-cgl-incdir="/usr/include/coin/" \
              --with-vol-lib="`pkg-config --libs vol`" \
              --with-vol-incdir="/usr/include/coin/" \
              --with-coinutils-lib="`pkg-config --libs coinutils`" \
              --with-coinutils-incdir="/usr/include/coin/" -C
  make
}

check() {
  cd "$srcdir/Cbc-$pkgver"
  make test
}

package() {
  cd "$srcdir/Cbc-$pkgver"
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir/" install
}
