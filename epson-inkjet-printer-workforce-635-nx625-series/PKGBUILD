# Contributor: Andre Klitzing <andre () incubo () de>

pkgname=epson-inkjet-printer-workforce-635-nx625-series
_pkgname_filter=epson-inkjet-printer-filter
pkgver=1.0.1
pkgrel=4
pkgdesc="Epson printer driver (ME OFFICE 82WD / 85ND / 900WD / 960 WD, Stylus NX625 / SX525WD / SX620FW, TX560WD, Stylus Office B42WD / BX525WD / BX625FWD / TX620FWD, WorkForce T42WD /  60 / 625 / 630 / 633 / 635)"
arch=('i686' 'x86_64')
url="http://avasys.jp/eng/linux_driver/download/lsb/epson-inkjet/escp/"
license=('GPL' 'LGPL' 'custom:AVASYS Public Licence')
depends=('cups')
makedepends=('rpmextract' 'libtool' 'make' 'automake' 'autoconf')
source=(http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/SRPMS/$pkgname-$pkgver-1lsb3.2.src.rpm)

build() {
  cd "$startdir/src"
  rpmextract.sh $pkgname-$pkgver-1lsb3.2.src.rpm || return 1
  tar xzf $pkgname-$pkgver.tar.gz || return 1
  tar xzf $_pkgname_filter-1.0.0.tar.gz || return 1

  cd $_pkgname_filter-1.0.0
  aclocal
  libtoolize
  chmod +x configure
  # if you have runtime problems: add "--enable-debug" and look into /tmp/epson-inkjet-printer-filter.txt
  ./configure LDFLAGS="$LDFLAGS -Wl,--no-as-needed" --prefix=/opt/$pkgname || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "$pkgdir/opt/$pkgname/"
  if [ "$CARCH" = "x86_64" ]; then
     cp -r lib64 "$pkgdir/opt/$pkgname/"
  else
     cp -r lib "$pkgdir/opt/$pkgname/"
  fi
  cp -r resource "$pkgdir/opt/$pkgname/"
  cp -r watermark "$pkgdir/opt/$pkgname/"
  install -d "$pkgdir/usr/share/cups/model/$pkgname"
  install -m 644 ppds/* "$pkgdir/usr/share/cups/model/$pkgname"

  cd "$srcdir/$_pkgname_filter-1.0.0"
  install -d "$pkgdir/opt/$pkgname/cups/lib/filter/"
  install -m 755 src/epson_inkjet_printer_filter "$pkgdir/opt/$pkgname/cups/lib/filter/epson_inkjet_printer_filter"
}
sha1sums=('719a98c771003778200363cec8d90906b9ef82a2')
