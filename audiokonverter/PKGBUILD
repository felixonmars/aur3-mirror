# Mantainer: Roberto Calabrese <robertocalabrese75@gmail.com>
# Maintainer: Diogo Leal <estranho@diogoleal.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslav Lichtblau <tu@dragonlord.cz>

pkgname=audiokonverter
pkgver=5.9.4
pkgrel=1
pkgdesc="A service menu for audio conversion from OGG, MP3, AAC, M4A, FLAC, WMA, RealAudio, Musepack, Wavpack, WAV"
arch=('any')
url="http://www.kde-apps.org/content/show.php?content=12608"
license=('GPL')
depends=('kdebase-lib' 'mplayer' 'ffmpeg' 'wavpack')
optdepends=('id3lib' 'id3v2' 'musepack-tools' 'vorbis-tools' 'apetag'
    'kdebase-dolphin' 'kdebase-konqueror')
source=(http://www.kde-apps.org/CONTENT/content-files/12608-${pkgname}-${pkgver}.tar.bz2)
md5sums=('05ca759224c41ae948e4ee51499f2747')

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    install -d "${pkgdir}/usr/share/kde4/services/ServiceMenus" \
        "${pkgdir}/usr/share/mimelnk/audio" \
        "${pkgdir}/usr/bin"
    # install SCRIPTS
    install -m755 anytowav4 audioconvert4 movie2sound4 oggdrop-lx \
        "${pkgdir}/usr/bin"
    # install MIMEFILES
    install -m644 x-wavpack.desktop x-flv.desktop \
        "${pkgdir}/usr/share/mimelnk/audio"
    #  install DESKTOPFILES
    install -m644 audioconvert4.desktop audiofrommovie4.desktop \
        "${pkgdir}/usr/share/kde4/services/ServiceMenus"
}