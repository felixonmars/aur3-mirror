# Maintainer: Taylor Venable <taylor@metasyntax.net>

pkgname='swi-prolog-devel'
pkgver='6.1.3'
pkgrel=1
pkgdesc='Prolog environment (development version)'
arch=('i686' 'x86_64')
url='http://www.swi-prolog.org/'
license=('GPL' 'LGPL')
conflicts=('swi-prolog')
depends=('gmp' 'readline' 'openssl')
makedepends=('libxft' 'libjpeg' 'unixodbc' 'libxpm' 'libxinerama')
optdepends=('unixodbc: for using the odbc4pl library'
            'libjpeg: for using the pl2xpce library'
            'libunwind: to get C-stack backtraces'
            'libxpm: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft: for using the pl2xpce library')
options=('!makeflags')
source=("http://www.swi-prolog.org/download/devel/src/pl-${pkgver}.tar.gz")
md5sums=('00d57e99ac3976259197bcd743a2f8a9')

build() {
  cd pl-${pkgver}

  # enable shared because x86 doesn't build with shared libs, upstream made it intentional...
  ./configure \
    --prefix=/usr \
    --with-world \
    --enable-shared

  make
}

package() {
  cd pl-${pkgver}

  make DESTDIR=${pkgdir} install

  # fix https://bugs.archlinux.org/task/20873
  chmod +x ${pkgdir}/usr/lib/swipl-${pkgver}/library/dialect/sicstus/swipl-lfr.pl
}
