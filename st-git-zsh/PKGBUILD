# Maintainer: Michele Sr <mikefender@cryptolab.net>

pkgname=st-git-zsh
_pkgname=st
pkgver=20150406.5bb9012
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X configured for zsh shell, fg col #eee, bg col #111'
url='http://st.suckless.org/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft' 'fontconfig' 'zsh' 'ttf-liberation')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st')
sha1sums=('SKIP')

provides=("${_pkgname}")
conflicts=("${_pkgname}" "st-git")

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	shell=`which zsh`
	# set font and colors
	font='Liberation Mono:size=12'
	fg_color='#111111'
	bg_color='#eeeeee'

	_shell=$(echo ${shell} | sed -e 's/\//\\\//g')
	cd "${srcdir}/${_pkgname}"
	sed \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-i config.mk

	sed \
		-e "s/pixelsize=12:antialias=false:autohint=false/${font}/g" \
		-e "s/\/bin\/sh/${_shell}/g" \
		-e "/#cccccc/a \\\t\"${bg_color}\",\n\t\"${fg_color}\"," \
        	-e 's/defaultfg = 7/defaultfg = 257/g' \
		-e 's/defaultbg = 0/defaultbg = 258/g' \
		-i config.def.h
}

build() {
	cd "${srcdir}/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}