# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=vsimagereader
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20130112.1f2fd82
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/chikuzen/${_plug}"
license=('LGPL2.1')
depends=('vapoursynth' 'libpng12' 'turbojpeg')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
makedepends=('git')
source=("git://github.com/chikuzen/${_plug}.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare(){
  cd "${srcdir}/${_gitname}/src"
  chmod +x configure
}

build() {
  cd "${srcdir}/${_gitname}/src"

  ./configure --extra-cflags="$CFLAGS"
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  install -Dm755 "src/lib${_plug}.so" "${pkgdir}/usr/lib/vapoursynth/lib${_plug}.so"
  install -Dm644 readme.rst "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
