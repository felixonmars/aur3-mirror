# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Nathan Owe <ndowens04 at gmail>

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=freeipmi
pkgver=1.2.3
pkgrel=1
pkgdesc="sensor monitoring, system event monitoring, power control, and serial-over-LAN (SOL)"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/freeipmi/"
license=('GPL')
depends=('libgcrypt')
options=('!libtool')
source=("http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('b1e21641c31b9d3291bdfe4a2b323d03')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --infodir=/usr/share/info

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir/ install

  # Move init scripts and config to Arch paths
  mv $pkgdir/etc/init.d $pkgdir/etc/rc.d
  mv $pkgdir/etc/sysconfig $pkgdir/etc/conf.d

  # Fix references to 'init.d' and 'sysconfig'
  sed -e 's|/etc/init.d/|/etc/rc.d/|g' \
      -e 's|/etc/rc.d/init.d/functions|/etc/rc.d/functions|g' \
      -i $pkgdir/etc/rc.d/*
  sed -e 's|CONFFILE=/etc/sysconfig/bmc-watchdog|CONFFILE=/etc/cond.f/bmc-watchdog|g' \
      -i $pkgdir/etc/rc.d/bmc-watchdog
  
  rm -Rf $pkgdir/usr/share/info/dir
}

# vim:set ts=2 sw=2 et:
