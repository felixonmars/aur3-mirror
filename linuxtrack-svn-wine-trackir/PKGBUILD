# Maintainer: Tom Guillen <freetoken at zoho dot com>
pkgname=linuxtrack-svn-wine-trackir
pkgver=713
pkgrel=2
pkgdesc="HeadTrack with wine games, TrackIr and SmartNav4 support, use head tracker as mouse"
arch=('i686' 'x86_64')
url="http://code.google.com/p/linux-track/"
license=('MIT')
depends=('libusb' 'v4l-utils' 'zlib' 'qt4' 'qtwebkit' 'mxml' 'opencv' 'wine')
optdepends=('wiiuse: for Wiimote (needed at make time)')
# you might want to remove nsis and --enable-wine-plugin below if you don't want support for wine games
makedepends=('subversion' 'gcc-objc' 'xplane-sdk-devel' 'nsis-wine')
#if [[ $CARCH == 'i686' ]]; then
  # Strip "-multilib" on i686
#  makedepends=(${makedepends[@]/*-multilib*/})
#fi
provides=('linuxtrack' 'trackir-udev' 'linuxtrack-svn')
conflicts=('linuxtrack' 'trackir-udev' 'linuxtrack-svn')
install=$pkgname.install

_svntrunk=http://linux-track.googlecode.com/svn/trunk
_svnmod=linuxtrack
source=($_svnmod::svn+$_svntrunk)
md5sums=('SKIP')

# automatically bump $pkgver with latest version from svn
pkgver() {
  cd $_svnmod
  svnversion | tr -d [A-z]
}

build() {
  # build sources with specific qmake path
  cd $_svnmod
  ./configure --prefix=/usr --enable-wine-plugin --enable-mickey QMAKE_PATH=/usr/bin/qmake-qt4
  make
}

package() {
  # install pkg
  cd "$srcdir/$_svnmod"
  make DESTDIR="$pkgdir/" install
  # update pixmaps
  install -Dm644 src/linuxtrack-wii.png src/linuxtrack.png "${pkgdir}/usr/share/pixmaps"
  # install udev rules
  install -Dm644 src/51-TIR.rules "${pkgdir}/etc/udev/rules.d/51-TIR.rules"
  install -Dm644 src/51-Mickey.rules "${pkgdir}/etc/udev/rules.d/51-Mickey.rules"
  # install license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
