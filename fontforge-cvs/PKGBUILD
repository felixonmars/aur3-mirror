# Maintainer: franzrogar <franzrogar at gmail dot com>
# Contributor: David Gowers <00ai99 at gmail dot com>
pkgname=fontforge-cvs
pkgver=20101022
pkgrel=1
pkgdesc="An outline and bitmap font editor"
arch=('i686' 'x86_64')
url="http://www.fontforge.org"
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libpng' 'libtiff' 'libjpeg' 'python2' 'pango')
options=('!libtool' '!makeflags')
makedepends=('cvs')
optdepends=('libspiro: bezier curves support')
conflicts=('fontforge')
provides=('fontforge-cvs')
source=('python27-compat.patch')
sha1sums=('83c7a09a59c71d93f5276411e8c18ae3bec557f4')

_cvsroot=":pserver:anonymous@fontforge.cvs.sourceforge.net:/cvsroot/fontforge"
_cvsmod="${pkgname}-${pkgver}"

build() {
  cd "${srcdir}"

  msg "Updating CVS...."
  if [ -d fontforge/CVS ]; then
    cd fontforge
    cvs -z3 update -d || exit 1
  else
    cvs -z3 -d $_cvsroot co -f fontforge || exit 1
  fi

  msg "Creating build copy..."
  cp -rf "${srcdir}/fontforge" "${srcdir}/fontforge-build"

  msg "Applying patches..."
  cd "${srcdir}/fontforge-build"
  patch -p1 < ../python27-compat.patch

  msg "Starting make..."
  cd "${srcdir}/fontforge-build"
  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --enable-type3 \
      --enable-double \
      --enable-devicetables \
      --with-regular-link \
      --with-python=python2 \
      --enable-pyextension
  make
}

package(){
  cd "${srcdir}/fontforge-build"
  make -j1 DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
