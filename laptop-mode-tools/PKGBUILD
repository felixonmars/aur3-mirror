# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Aaditya Bagga <aaditya_gnulinux@zoho.com>

pkgname=laptop-mode-tools
pkgver=1.65
pkgrel=3
pkgdesc='A kernel mode that allows you to extend the battery life of your laptop'
arch=('any')
url='https://github.com/rickysarraf/laptop-mode-tools'
license=('GPL')
depends=('bash')
optdepends=('acpid: ACPI support'
            'bluez-utils: Bluetooth support'
            'hdparm: hard disk power management'
            'sdparm: SCSI disk power management'
            'ethtool: Ethernet support'
            'wireless_tools: Wi-Fi support'
            'xorg-xset: DPMS standby support'
	    )
backup=('etc/laptop-mode/conf.d/ac97-powersave.conf'
        'etc/laptop-mode/conf.d/auto-hibernate.conf'
        'etc/laptop-mode/conf.d/battery-level-polling.conf'
        'etc/laptop-mode/conf.d/bluetooth.conf'
        'etc/laptop-mode/conf.d/configuration-file-control.conf'
        'etc/laptop-mode/conf.d/cpufreq.conf'
        'etc/laptop-mode/conf.d/dpms-standby.conf'
        'etc/laptop-mode/conf.d/eee-superhe.conf'
        'etc/laptop-mode/conf.d/ethernet.conf'
        'etc/laptop-mode/conf.d/exec-commands.conf'
        'etc/laptop-mode/conf.d/hal-polling.conf'
        'etc/laptop-mode/conf.d/intel-hda-powersave.conf'
        'etc/laptop-mode/conf.d/intel-sata-powermgmt.conf'
        'etc/laptop-mode/conf.d/lcd-brightness.conf'
        'etc/laptop-mode/conf.d/nmi-watchdog.conf'
        'etc/laptop-mode/conf.d/pcie-aspm.conf'
        'etc/laptop-mode/conf.d/runtime-pm.conf'
        'etc/laptop-mode/conf.d/sched-mc-power-savings.conf'
        'etc/laptop-mode/conf.d/sched-smt-power-savings.conf'
        'etc/laptop-mode/conf.d/start-stop-programs.conf'
        'etc/laptop-mode/conf.d/terminal-blanking.conf'
        'etc/laptop-mode/conf.d/video-out.conf'
        'etc/laptop-mode/conf.d/wireless-ipw-power.conf'
        'etc/laptop-mode/conf.d/wireless-iwl-power.conf'
        'etc/laptop-mode/conf.d/wireless-power.conf'
        'etc/laptop-mode/laptop-mode.conf'
        'etc/laptop-mode/lm-profiler.conf'
	)
install=laptop-mode.install
source=("http://www.samwel.tk/laptop_mode/tools/downloads/${pkgname}_${pkgver}.tar.gz")
sha1sums=('bff5d9719510427ee17901a1cf3b60d41eb48b25')

package() {
	cd "${pkgname}_${pkgver}"

	# Remove deprecated usb-autosuspend module
	rm etc/laptop-mode/conf.d/usb-autosuspend.conf
	rm usr/share/laptop-mode-tools/modules/usb-autosuspend

	make DESTDIR="${pkgdir}" MAN_D=/usr/share/man LIB_D=/usr/lib install

	# use bin instead of sbin
	mv "${pkgdir}/usr/sbin" "${pkgdir}/usr/bin"
	find "${pkgdir}" -type f -exec sed -i 's|sbin/laptop_mode|bin/laptop_mode|g' '{}' ';'
}
