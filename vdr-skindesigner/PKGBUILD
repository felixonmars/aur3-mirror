# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgbase='vdr-skindesigner'
pkgname=($pkgbase skindesigner-{blackhole,metrixhd,nopacity})
pkgver=0.4.5
_vdrapi=2.2.0
pkgrel=1
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
makedepends=('git' 'librsvg' 'vdr')
_plugname=$(echo $pkgbase | sed 's/vdr-//g')
source=("http://projects.vdr-developer.org/git/vdr-plugin-skindesigner.git/snapshot/vdr-plugin-$_plugname-$pkgver.tar.bz2"
        'skindesigner-use-opensans-font.diff'
        "50-$_plugname.conf")
options=('!emptydirs')
md5sums=('b1397bb0c26bac3c32b1d51e9f6d0334'
         '861e1814ccf3b7d2b69908fc76b60967'
         'ebc6537bf0d3e604c59884ec408f7e9c')

prepare() {
  cd "${srcdir}/vdr-plugin-$_plugname-$pkgver"
  patch -p1 -i "$srcdir/skindesigner-use-opensans-font.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-$_plugname-$pkgver"
  make
}

package_vdr-skindesigner() {
  pkgdesc="Highly customizable native true color skin for the Video Disc Recorder"
  depends=('librsvg' "vdr-api=${_vdrapi}")
  optdepends=('vdr-scraper2vdr: To display scraped images in various EPG views (requires epgd)'
              'vdr-tvscraper: To display scraped images in various EPG views')
  backup=("etc/vdr/conf.avail/50-$_plugname.conf")

  cd "${srcdir}/vdr-plugin-$_plugname-$pkgver"
  make DESTDIR="$pkgdir" PREFIX='/usr' install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
  rm -r "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  rm -r "$pkgdir/var/lib/vdr/themes"

  cd "$pkgdir/usr/lib/vdr/plugins/skindesigner/scripts"
  mv temperatures.default temperatures
  rm README temperatures.*
}

package_skindesigner-blackhole() {
  arch=('any')
  pkgdesc="Blackhole Skin for SkinDesigner"
  depends=('ttf-sourcesanspro' 'vdr-skindesigner')

  cd "${srcdir}/vdr-plugin-$_plugname-$pkgver"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  cp -r skins/blackhole "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  cp -r themes/blackhole-* "$pkgdir/var/lib/vdr/themes"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}

package_skindesigner-metrixhd() {
  arch=('any')
  pkgdesc="MetrixHD Skin for SkinDesigner"
  depends=('ttf-opensans' 'vdr-skindesigner')

  cd "${srcdir}/vdr-plugin-$_plugname-$pkgver"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  cp -r skins/metrixhd "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
#   cp -r themes/metrixhd-* "$pkgdir/var/lib/vdr/themes"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}

package_skindesigner-nopacity() {
  arch=('any')
  pkgdesc="nOpacity Skin for SkinDesigner"
  depends=('vdr-skindesigner')

  cd "${srcdir}/vdr-plugin-$_plugname-$pkgver"
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  cp -r skins/nopacity "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  cp -r themes/nopacity-* "$pkgdir/var/lib/vdr/themes"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
