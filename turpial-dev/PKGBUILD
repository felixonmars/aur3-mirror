# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jean-Francois Chevrette <jfchevrette@gmail.com>
# Contributor: Jonathas Rodrigues <jonathas@archlinux.us>
# Contributor: Karl Kochs <captainhaggy@i2pmail.org>
# Contributor: abr4xas <me@abr4xas.org>


pkgname=turpial-dev
pkgver=1.7.6.a1.1467.9266407
pkgrel=1
pkgdesc="Twitter client written in Python. Light, fast, fully functional and integrated to the user desktop"
arch=(any)
url=http://turpial.org.ve
license=(GPL3)
depends=(gstreamer0.10-python hspell libturpial-git notification-daemon pygtk python2-babel
    python2-gtkspell python2-notify python2-pygame python2-simplejson pywebkitgtk)
makedepends=(git python2-setuptools)
provides=(${pkgname%-*}=$pkgver)
conflicts=(${pkgname%-*})
source=($pkgname::git://github.com/satanas/Turpial.git#branch=development config.patch)
sha256sums=('SKIP'
'86596ec8aac43ab83b01209fbde8c46f820dc81bacbdcdae4b211902ef87f35f')
sha512sums=('SKIP'
'de0b705413dd4c70072be807d5aecef0fc1bde3d01b935edcad5f822345a30817703cdc19ef9d804e8095ef612d1bb14b7d7cb28a876e6fd56e9b6132a87c540')

pkgver() {
    cd $pkgname/
    echo $(head -n1 ChangeLog | awk '{print $1}' | sed 's/v//;s/-/./').$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
    cd $pkgname/
    python2 setup.py build
}

package() {
    cd $pkgname/
    python2 setup.py install --root="$pkgdir" --optimize=1
}
