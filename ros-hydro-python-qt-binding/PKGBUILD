
pkgdesc="ROS - This stack provides Python bindings for Qt."
url='http://www.ros.org/'

pkgname='ros-hydro-python-qt-binding'
pkgver='0.2.12'
_pkgver_patch=0
arch=('i686' 'x86_64')
pkgrel=3
license=('BSD, LGPL, GPL')

ros_makedepends=(ros-hydro-catkin
  ros-hydro-rosbuild)
makedepends=('cmake' 'git' 'ros-build-tools'
  ${ros_makedepends[@]}
  python2-pyqt4-10
  qt4)

ros_depends=()
depends=(${ros_depends[@]}
  python2-pyqt4-10)

_tag=release/hydro/python_qt_binding/${pkgver}-${_pkgver_patch}
_dir=python_qt_binding
source=("${_dir}"::"git+https://github.com/ros-gbp/python_qt_binding-release.git"#tag=${_tag})
md5sums=('SKIP')

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python2/Python3 conflicts
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Build project
  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
