# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=pkgconf
pkgver=0.8.6
pkgrel=1
pkgdesc='pkg-config compatible utility which does not depend on glib'
url='https://github.com/nenolod/pkgconf'
arch=('i686' 'x86_64')
license=('ISC')
makedepends=('popt')
conflicts=('pkg-config')
provides=('pkg-config')
source=($pkgname-$pkgver.tar.gz::http://github.com/nenolod/$pkgname/tarball/$pkgname-$pkgver)

build() {                                                                       
    cd "$srcdir"/nenolod-$pkgname-*
    ./autogen.sh ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --mandir=/usr/share/man \
        --infodir=/usr/share/info \
        --localstatedir=/var
    make
}

package() {
    cd "$srcdir"/nenolod-$pkgname-*
    make DESTDIR="$pkgdir" install
    ln -s /usr/bin/pkgconf "$pkgdir"/usr/bin/pkg-config
}

md5sums=('25e26d0a25996bb30c4840058fc0706a')

# vim:set ts=2 sw=2 et:
