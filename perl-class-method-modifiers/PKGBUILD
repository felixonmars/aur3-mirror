pkgname='perl-class-method-modifiers'
pkgver='2.03'
pkgrel='1'
pkgdesc="provides Moose-like method modifiers"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-fatal')
url='http://search.cpan.org/dist/Class-Method-Modifiers'
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/Class-Method-Modifiers-${pkgver}.tar.gz")
md5sums=('dae7ee62bc445e88b811ded0bab3d083')
sha512sums=('77bb36c525060561ceef183fcd5bb8bce805a57a88a9363af738a23c29169df16b2ac817bfe2e08454fd1b2e4dc008fa464da69231fbcaa91837205e68770ba0')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd ${srcdir}/Class-Method-Modifiers-${pkgver}
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd ${srcdir}/Class-Method-Modifiers-${pkgver}
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd ${srcdir}/Class-Method-Modifiers-${pkgver}
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
