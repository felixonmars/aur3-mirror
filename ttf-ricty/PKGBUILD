# Maintainer: eagletmt <eagletmt@gmail.com>
pkgname=ttf-ricty
pkgver=3.2.1
pkgrel=1
pkgdesc='Japanese TrueType font for developers by mixing Inconsolata and Migu 1M.'
arch=('any')
url='http://save.sys.t.u-tokyo.ac.jp/~yusa/fonts/ricty.html'
license=('custom')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('fontforge' 'ttf-inconsolata' 'ttf-migu')
install=ttf.install
source=(Ricty-$pkgver.tar.gz::https://github.com/yascentur/Ricty/archive/$pkgver.tar.gz)

build() {
  cd "$srcdir/Ricty-$pkgver"
  ./ricty_generator.sh /usr/share/fonts/TTF/{ttf-inconsolata.otf,migu-1m-regular.ttf,migu-1m-bold.ttf}
}

package() {
  cd "$srcdir/Ricty-$pkgver"
  install -d "$pkgdir/usr/share/fonts/TTF"
  install -m644 *.ttf "$pkgdir/usr/share/fonts/TTF/"
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 README.md "$pkgdir/usr/share/licenses/$pkgname/"
}

md5sums=('0152fe017ae37f7f1d16ca09261d25af')
