# Maintainer: eagletmt <eagletmt@gmail.com>
pkgname=ttf-ricty
pkgver=3.1.3
pkgrel=1
pkgdesc='Japanese TrueType font for developers by mixing Inconsolata and Migu 1M.'
arch=('any')
url='http://save.sys.t.u-tokyo.ac.jp/~yusa/fonts/ricty.html'
license=('public domain')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('fontforge' 'ttf-inconsolata' 'ttf-migu')
install=ttf.install
source=(http://save.sys.t.u-tokyo.ac.jp/~yusa/fonts/ricty/Ricty-$pkgver.tar.gz)

build() {
  cd "$srcdir/Ricty"
  ./ricty_generator.sh /usr/share/fonts/TTF/{ttf-inconsolata.otf,migu-1m-regular.ttf,migu-1m-bold.ttf}
}

package() {
  cd "$srcdir/Ricty"
  install -d "$pkgdir/usr/share/fonts/TTF"
  install -m644 *.ttf "$pkgdir/usr/share/fonts/TTF/"
}

md5sums=('662c255d7f88b353007815d3f0ed4140')
