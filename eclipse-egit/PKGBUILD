#Maintainer: Alex Ferrando <alferpal@gmail.com>
# Contributor: Archan Paul <arp@archan.org>

pkgname=eclipse-egit
pkgver=3.4.1.201406201815
pkgrel=1
pkgdesc="An Eclipse Team provider for the Git version control system"
arch=('any')
url="http://eclipse.org/egit/"
license=('EPL')
depends=('eclipse>=4.4' 'eclipse-mylyn>=3.6')
source=("http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/egit/updates/org.eclipse.egit.repository-${pkgver}-r.zip")
#source=("http://eclipse.mirror.triple-it.nl/egit/updates/org.eclipse.egit.repository-${pkgver}-r.zip")
#source=("http://eclipse.ialto.com/egit/updates-3.1/org.eclipse.egit.repository-${pkgver}-r.zip")

md5sums=('51c4f013b85dc0b97eb2bc041e379833')

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}
  
  # Features
  find features -type f | while read _feature ; do
    echo ${_feature}
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}

