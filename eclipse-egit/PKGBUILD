#Maintainer: Alex Ferrando <alferpal@gmail.com>
# Contributor: Archan Paul <arp@archan.org>

pkgname=eclipse-egit
pkgver=3.2.0.201312181205
pkgrel=1
pkgdesc="An Eclipse Team provider for the Git version control system"
arch=('any')
url="http://eclipse.org/egit/"
license=('EPL')
depends=('eclipse>=3.6' 'eclipse-mylyn>=3.6')
#source=("http://eclipse.ialto.com/egit/updates-3.1/org.eclipse.egit.repository-${pkgver}-r.zip")
source=("http://eclipse.mirror.triple-it.nl/egit/updates/org.eclipse.egit.repository-${pkgver}-r.zip")

md5sums=('d0729f8489f8497dc394142fbc6dcd07')

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}
  
  # Features
  find features -type f | while read _feature ; do
    echo ${_feature}
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}

