# Maintainer: Yosef Or Boczko <yosefor3@walla.com>

_pkgname=at-spi2-atk-gnome-3-10
pkgname=$_pkgname-git
pkgver=AT_SPI2_ATK_2_9_2
pkgrel=1
_realver=2.9.2
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('at-spi2-core' 'libx11' 'atk')
makedepends=('git' 'intltool')
options=('!libtool')
replaces=('at-spi2-atk')
provides=('at-spi2-atk' "at-spi2-atk=$_realver" "$_pkgname=$_realver")
conflicts=('at-spi2-atk')
source=(git://git.gnome.org/at-spi2-atk)
sha256sums=('SKIP')

 pkgver() {
  cd "$srcdir/at-spi2-atk"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/at-spi2-atk"

  sed -i -e '/AC_PATH_XTRA/d' configure.ac

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/at-spi2-atk"
  make DESTDIR="$pkgdir" install
}
