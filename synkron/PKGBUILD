# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=synkron
pkgver=1.6.2
pkgrel=1
pkgdesc="Qt application for folder synchronisation"
arch=('i686' 'x86_64')
url="http://synkron.sourceforge.net/"
license=('GPL2')
depends=('qt')
source=("http://downloads.sourceforge.net/${pkgname}/Synkron-${pkgver}-src.tar.gz"
        "${pkgname}.desktop")
install=${pkgname}.install
md5sums=('a54301030a0451401bb253f2d80ac61b'
         '49893d3e67828515164590f527f08932')

build() {
  cd "${srcdir}/Synkron-${pkgver}-src"

  lrelease Synkron.pro
  qmake -config release
  make
}

package() {
  cd "${srcdir}/Synkron-${pkgver}-src"

  # install bin, icon and desktop files
  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 "${pkgname}" "${pkgdir}/usr/bin/"

  install -d -m755 "${pkgdir}"/usr/share/{applications,pixmaps}
  install -m644 "${srcdir}/Synkron-${pkgver}-src/images/Synkron48.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"
}
