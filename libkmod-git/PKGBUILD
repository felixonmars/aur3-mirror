# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=libkmod-git
pkgver=20111202
pkgrel=1
pkgdesc="library for linux kernel module manipulation"
arch=('i686' 'x86_64')
url="http://git.profusion.mobi/cgit.cgi/lucas/libkmod/"
license=('GPL2')
depends=('glibc')
makedepends=('git')
conflicts=('libkmod')
provides=('libkmod')

_gitroot="git://git.profusion.mobi/users/lucas/libkmod.git"
_gitname="libkmod"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  make
}

check() {
  make -C "$srcdir/$_gitname-build" check
}

package() {
  make -C "$srcdir/$_gitname-build" DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
