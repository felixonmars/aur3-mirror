# Maintainer: Kozec <kozec at kozec dot com>

pkgname='mcedit'
pkgver='0.1.8build665'
pkgrel=1
pkgdesc='Minecraft world editor'
arch=('i686' 'x86_64')
url='https://github.com/mcedit/mcedit'
license='BSD'

depends=('python2' 'python2-numpy' 'python2-opengl' 'glu'
	'python2-pygame' 'ttf-bitstream-vera' "pymclevel>=$pkgver")
conflicts=('mcedit-git' 'pymclevel-git')

source=(MCEdit MCEdit.desktop
	directories.patch
	disable-portable-mode.patch
	filters-in-home.patch
	https://github.com/mcedit/mcedit/archive/${pkgver}.tar.gz
	)

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	# Provided setup.py depends on something weird, but I'm not even
	# going to bother.
	
	msg directories.patch
	patch -Np0 -i ../directories.patch || return 1
	msg disable-portable-mode.patch
	patch -Np0 -i ../disable-portable-mode.patch || return 1
	msg filters-in-home.patch
	patch -Np0 -i ../filters-in-home.patch || return 1
}


package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	mkdir -p "${pkgdir}/usr/bin"
	mkdir -p "${pkgdir}/usr/lib/mcedit"
	mkdir -p "${pkgdir}/usr/share/mcedit"
	mkdir -p "${pkgdir}/usr/share/applications"
	mkdir -p "${pkgdir}/usr/share/pixmaps"
	# Pys
	cp *.py "${pkgdir}/usr/lib/mcedit"
	for i in albow editortools ; do
		cp -R $i "${pkgdir}/usr/lib/mcedit/$i"
	done
	# Images and stuff
	cp *.png "${pkgdir}/usr/share/mcedit"
	for i in stock-schematics toolicons filters; do
		cp -R $i "${pkgdir}/usr/share/mcedit/$i"
	done
	ln -s "/usr/share/mcedit/favicon.png" "${pkgdir}/usr/share/pixmaps/MCEdit.png"
	
	# Launcher
	install -D -m755 "${srcdir}/MCEdit" "${pkgdir}/usr/bin/"
	install -D "${srcdir}/MCEdit.desktop" "${pkgdir}/usr/share/applications"
}

md5sums=('b9de4195ddf84d85c0170e816ec0659e'
         'e598244d2770ee075d397d6c11dc140d'
         'ecac7e2652a266aa1353e1242230b5ae'
         'b8b5e679ed9bbf9f6a0e489599d2c587'
         '5f84a8bf43e385e16f476fbd8ee4dc51'
         '99936d45d06ec050ca840e820c930927')
