# Current Maintainer: Kozec <kozec at kozec dot com>
# Original Maintainer: Tyler <tjb0607 at gmail dot com>
pkgname=mcedit
pkgver=stable33
pkgrel=1
pkgdesc="Minecraft world editor"
arch=('any')
url="http://www.minecraftforum.net/topic/13807-mcedit-minecraft-world-editor-compatible-with-mc-beta-173/"
depends=('python2' 'python2-numpy' 'python-opengl' 'python-pygame')
license=(custom)
install=
source=("http://minecraft.dokommander.eu/dl/Sonstiges/Tools/MCEdit/MCEdit-${pkgver}-linux-python2.7.tgz" 'LICENSE' 'MCEdit' 'MCEdit.desktop' "directories.py" 'mcedit.ini')

build() {
	# With original directories.pyo MCEdit tries to start in "portable"
	# mode and fails with Permission denied error.
	cd "$srcdir"
	( echo "import py_compile" && \
	echo "py_compile.compile('directories.py')") | \
	python2.7 -O
	}

package() {
	cd "$srcdir"
	install -d "${pkgdir}/usr/bin"
	install -d "${pkgdir}/usr/share/applications"
	install -d "${pkgdir}/usr/share/pixmaps"
	install -d "${pkgdir}/usr/share/licenses"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cp -R MCEdit-${pkgver}-linux "${pkgdir}/usr/share/${pkgname}"
	install -D -m755 MCEdit "${pkgdir}/usr/bin/"
	install -D MCEdit.desktop "${pkgdir}/usr/share/applications"
	install -D MCEdit-${pkgver}-linux/MCEditData/favicon.png "${pkgdir}/usr/share/pixmaps/MCEdit.png"
	rm "${pkgdir}/usr/share/${pkgname}/mcedit.sh" 
	rm "${pkgdir}/usr/share/${pkgname}/readme-linux.txt" 
	rm "${pkgdir}/usr/share/${pkgname}/MCEditData/directories.pyo"
	install -D -m644 "directories.pyo" "${pkgdir}/usr/share/${pkgname}/MCEditData"
	# This "default" config is required too for starting in "non-portable" mode,
	# along with modified launcher
	install -D -m644 "mcedit.ini" "${pkgdir}/usr/share/${pkgname}/"
}

md5sums=('abdf47f36822da39505d559411d0cb60'
		 '0595060165a26581fe4c0c8c58664d44'
		 'b95133ec880f930f9aa056f280b1b606'
		 'e598244d2770ee075d397d6c11dc140d'
		 '0f60e27ab010478d14db0bbbe5edc2c7'
		 '420cfa36d52d49dc596c7e2698a9d0be')
