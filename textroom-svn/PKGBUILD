# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=textroom-svn
pkgver=278
pkgrel=1
pkgdesc="Full Screen text editor heavily inspired by Q10 and JDarkRoom - svn-version "
arch=('i686' 'x86_64')
url="http://code.google.com/p/textroom/"
license=('GPL3')
depends=('curl' 'libxml++' 'hunspell' 'qt' 'sdl_mixer')
makedepends=('subversion' 'flo')
provides=('textroom')
conflicts=('textroom')
install=textroom.install
noextract=() 
md5sums=()

_svntrunk="http://textroom.googlecode.com/svn/trunk"
_svnmod="textroom-read-only"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  [ -d "$srcdir/$_svnmod-build" ] && rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #
  qmake
  sed 's+/usr/share/sounds+/usr/share/sounds/textroom+g' \
    -i application/src/textroom.cpp
  sed 's+-lhunspell+-lhunspell-1.3+' -i Makefile -i \
    application/application.pro
  sed 's/linux-g++/unix/' -i application/application.pro
  cp /usr/lib/libflo.a $srcdir/${_svnmod}-build/lib/libflo.a
  sed -i '19i#include <unistd.h>' library/sxfile/getusername.cpp
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make INSTALL_ROOT="${pkgdir}" install
  rm "${pkgdir}"/usr/bin/textroom-uninstall
  install -Dm644 "${pkgdir}"/usr/share/sounds/keyenter.wav \
    "${pkgdir}"/usr/share/sounds/textroom/keyenter.wav
  install -Dm644 "${pkgdir}"/usr/share/sounds/keyany.wav \
    "${pkgdir}"/usr/share/sounds/textroom/keyany.wav
  rm "${pkgdir}"/usr/share/sounds/*.wav
  rm -rf "${pkgdir}"/usr/share/hunspell
}
