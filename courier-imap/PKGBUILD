# $Id: PKGBUILD 73306 2012-07-06 03:48:18Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: tobias <tobias@archlinux.org>
# Committer: Manolis Tzanidakis <manolis@archlinux.org>

pkgname=courier-imap
pkgver=4.11.0
pkgrel=4
pkgdesc="IMAP(s)/POP3(s) Server"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.courier-mta.org/imap/"
depends=('courier-maildrop>=2.5.0' 'gcc-libs' 'gamin' 'gdbm' 'openssl')
backup=('etc/courier-imap/imapd.cnf' 'etc/courier-imap/pop3d.cnf' \
        'etc/courier-imap/imapd' 'etc/courier-imap/imapd-ssl' \
        'etc/courier-imap/pop3d' 'etc/courier-imap/pop3d-ssl'\
        'etc/conf.d/courier-imap')
conflicts=('courier-mta')
provides=('imap-server' 'pop3-server')
options=('!libtool')
#install=$pkgname.install
source=(http://downloads.sourceforge.net/project/courier/imap/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        imapd.rc.d
        imapd-ssl.rc.d
        pop3d.rc.d
        pop3d-ssl.rc.d
	courier-imapd-ssl.service
	courier-imapd.service)
sha512sums=('b4d2ab9e282f384bb713fe0b7797fb746a65ee66eea7ed628e832d0e003c6be5be88405fc50a179a5d34f7f19d4347bbf57446b626d89678ec31e556f621444e'
	    'd8d6ea922e92fb52c40277ebd56d5be656fcce19dee614ce239ad2cef4d2ff69509c5a81fceafc905866a22a79730d98931a3c4470bb0c7cb440e91784921898'
	    '25c1718d796fcd6d5e0e7a1f9bc73111cafd32a5d4ad4587d192a2aa654a28b3b421eb2e6370df7a76cfce3890542c83d5bcfbe13a32543cb2190446b6f26dc9'
	    'a73862209ce54a123a51bd970f7a4bde060af8029a66822f22506e23d643b2e1d63413b6f659779883b7b2992aece3baf926710db19898b212822accda293af5'
	    'fd9b8512a2f08fb2e968e4353964652e8ee517700c3c88317497a9677dded3f5abfb45f2910a2c4c709521aded64f4a9a721c798ea014a8f10e814abeff93e80'
	    'baf0e6c52933ea92bc145f6350c07f8b63bb5b7efa553aaad5d0863af7f857d6ecf8ed7a42b0a5739eac25fcae2a689ed9ed06cd4941c6e7cc82aace1e7aecc1'
	    'daec41de02a8b056a0b54e9c5446e144192762e4354875fb18b306473e006eadee74664e65f45e07e071b70158f741720a9acd41c14474d61ee0c619a59e8a98')




build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix a tiny bug
  sed -i -e \
    's|--with-authchangepwdir=/var/tmp/dev/null|--with-authchangepwdir=$libexecdir/authlib|' \
    configure && chmod 755 configure

  ./configure --prefix=/usr \
    --sysconfdir=/etc/courier-imap \
    --libexecdir=/usr/lib/courier-imap \
    --localstatedir=/var/spool/courier-imap \
    --disable-root-check \
    --enable-unicode \
    --enable-workarounds-for-imap-client-bugs \
    --with-piddir=/var/run/courier \
    --with-trashquota \
    --with-db=gdbm \
    --with-trashquota \
    --with-mailuser=courier --with-mailgroup=courier
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # cleanup - provided by courier-maildrop
  rm ${pkgdir}/usr/bin/{deliverquota,maildirmake}
  rm ${pkgdir}/usr/share/man/man1/maildirmake*
  rm ${pkgdir}/usr/share/man/man8/deliverquota*
  find ${pkgdir} -name '*\.a' -exec -rm -f {} \;
  ###############################################################################
  # this is what usually "make install-configure" does
  # *.dist files get rid of "dist"
  for distfile in ${pkgdir}/etc/courier-imap/*.dist; do
    mv ${distfile} ${pkgdir}/etc/courier-imap/$(basename ${distfile} .dist)
  done
  sed -i 's|TLS_CERTFILE=/usr/share/|TLS_CERTFILE=/etc/courier-imap/|' \
    ${pkgdir}/etc/courier-imap/*-ssl
  for pamfile in imap/*.pam; do
    sed -i "s|/lib/security/||;s|pam_pwdb|pam_unix|" ${pamfile}
    install -Dm 644 ${pamfile} \
      ${pkgdir}/etc/pam.d/$(basename ${pamfile} .pam | sed "s/d$//")
  done
  # install thingies
  install -D -m 755 ${srcdir}/imapd.rc.d ${pkgdir}/etc/rc.d/imapd
  install -D -m 755 ${srcdir}/imapd-ssl.rc.d ${pkgdir}/etc/rc.d/imapd-ssl
  install -D -m 755 ${srcdir}/pop3d.rc.d ${pkgdir}/etc/rc.d/pop3d
  install -D -m 755 ${srcdir}/pop3d-ssl.rc.d ${pkgdir}/etc/rc.d/pop3d-ssl
  install -D -m 644 ${srcdir}/courier-imapd.service ${pkgdir}/usr/lib/systemd/system/courier-imapd.service
  install -D -m 644 ${srcdir}/courier-imapd-ssl.service ${pkgdir}/usr/lib/systemd/system/courier-imapd-ssl.service
}
