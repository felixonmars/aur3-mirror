# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=hotot-kde-git
pkgver=20120901
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
arch=('i686' 'x86_64')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('hotot-data-git' 'kdebase-runtime')
makedepends=('git' 'cmake' 'intltool' 'python2')
provides=("${pkgname/-git}")
conflicts=("${pkgname/-git}")
install="$pkgname.install"

_gitroot="https://github.com/shellex/Hotot.git"
_gitname="hotot"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DWITH_GTK=OFF \
          -DWITH_GIR=OFF \
          -DWITH_QT=ON \
          -DWITH_KDE=ON \
          -DWITH_CHROME=OFF \
          -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  # bin
  install -Dm755 qt/hotot-qt \
    "$pkgdir/usr/bin/hotot-kde"

  # desktop file
  install -Dm644 misc/hotot-qt.desktop \
    "$pkgdir/usr/share/applications/hotot-kde.desktop"
  sed -i "s/qt/kde/;s/Qt/Kde/" \
    "$pkgdir/usr/share/applications/hotot-kde.desktop"
}

# vim:set ts=2 sw=2 et:
