pkgname=mate-settings-daemon
pkgver=2011.12.16
pkgrel=2
pkgdesc="The MATE Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatekbd' 'mate-desktop' 'libmatenotify' 'gstreamer0.10-base' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig')
options=('!libtool')
url="http://matsusoft.com.ar/projects/mate/"
install=mate-settings-daemon.install
groups=('mate')
source=(http://matsusoft.com.ar/projects/mate/files/${pkgname}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('3011f710013bf7073f74ca8dd808b9a844587dc092d009f0da15b4b7262c2aed')


build() {
    cd "$srcdir"
    # Remove --disable-pulse if you want pulseaudio support
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/mate-settings-daemon \
        --disable-static \
        --disable-pulse \
        --disable-schemas-install \
        --enable-polkit || return 1
    make || return 1
}

package() {
    cd "${srcdir}"

    make DESTDIR="${pkgdir}" MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

    install -d -m755 "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
