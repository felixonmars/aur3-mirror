# Maintainer : speps <speps at aur at archlinux dot org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Slash <demodevil5 [at] yahoo [dot] com>

pkgname=nut
pkgver=17.10
pkgrel=1
pkgdesc="Analyze meals with the USDA Nutrient Database"
arch=('i686' 'x86_64')
url='http://nut.sourceforge.net/'
license=('GPL')
source=("http://downloads.sourceforge.net/project/nut/nut/$pkgver/nut-$pkgver.tar.gz")
md5sums=('dfc3fc691fa3de663e95cff95f0dbbfd')

build() {
  cd "$pkgname-$pkgver"

  # settable prefix and fix man path
  sed -i "s|^prefix |&+|;s|/man|/share&|" Makefile

  make prefix=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make prefix="$pkgdir/usr" install
}
