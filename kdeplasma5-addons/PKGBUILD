# Maintainer: Antonio Rojas

pkgname=kdeplasma5-addons
_pkgname=kdeplasma-addons
pkgver=5.0.95
pkgrel=1
pkgdesc="All kind of addons to improve your Plasma experience"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdeplasma-addons'
license=('LGPL')
makedepends=('kdoctools' 'extra-cmake-modules' 'krunner' 'kdelibs4support' 'kcmutils' 'scim' 'libibus')
depends=('plasma-workspace')
source=("http://download.kde.org/unstable/plasma/$pkgver/$_pkgname-$pkgver.tar.xz")
md5sums=('738593956841e26efdf7f0080c96c25b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
