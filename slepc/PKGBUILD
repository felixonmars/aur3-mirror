# Maintainer: Myles English <myles at rockhead dot biz>
pkgname=slepc
pkgver=3.3_p2
_config=arch-linux2-cxx-opt
pkgrel=1
pkgdesc="Scalable library for Eigenvalue problem computations"
arch=('any')
url="http://www.grycap.upv.es/slepc"
license=('GNUv3')
depends=('python2' 'gcc' 'petsc')
install=slepc.install
source=(http://www.grycap.upv.es/slepc/download/distrib/${pkgname}-${pkgver/_/-}.tar.gz)
md5sums=('402a89f45fd7d4c15c1fea887197893f')
_build_dir=${srcdir}/${pkgname}-${pkgver/_/-}

# get SLEPC_DIR
source /etc/profile.d/petsc.sh    # gets PETSC_DIR
_install_dir=/usr/slepc/`basename ${PETSC_DIR}`

export MAKEFLAGS="-j1"

build() {
    cd ${_build_dir}

    unset PETSC_ARCH
    export SLEPC_DIR=${_build_dir}

    python2 ./configure --prefix=${pkgdir}${_install_dir}
    export PETSC_ARCH=arch-installed-petsc
    make
}


package() {
    cd ${_build_dir}
    export PETSC_ARCH=arch-installed-petsc
    export SLEPC_DIR=${_build_dir}
    source /etc/profile.d/petsc.sh  # sets PETSC_DIR

    make install > /dev/null # redirect in order to suppress confusing messages

    export SLEPC_DIR=${_install_dir}
    unset PETSC_ARCH

    mkdir -p ${pkgdir}/etc/profile.d
    echo "export SLEPC_DIR=${_install_dir}" > ${pkgdir}/etc/profile.d/slepc.sh
    chmod +x ${pkgdir}/etc/profile.d/slepc.sh

    # show where the shared libraries are
    install -d -m755 "${pkgdir}"/etc/ld.so.conf.d/
    echo "${_install_dir}/lib" > "${pkgdir}"/etc/ld.so.conf.d/slepc.conf
}
