# Maintainer: Army <uli armbruster who uses the google mail service>

pkgname=rtspeccy-git
pkgver=20110630
pkgrel=1
pkgdesc="Real time spectrum analyzer (audio)"
arch=('i686' 'x86_64')
url="http://www.uninformativ.de/projects/?q=rtspeccy"
license=(GPL3)
depends=('glut' 'alsa-lib' 'fftw')
makedepends=('git')

_gitname="rtspeccy"
_gitroot="https://github.com/vain/${_gitname}.git"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."
	
	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi
	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "${srcdir}/${_gitname}-build"
	cp -a "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

	cd "${srcdir}/${_gitname}-build"
	if test -e $startdir/config.h
	then
		msg "use custom config.h"
		cp ${startdir}/config.h .
	else
		msg "use example config.h"
		cp config.h.example config.h
	fi
	make
}

package() {
	install -Dm755 "${srcdir}/${_gitname}-build/${_gitname}" "${pkgdir}/usr/bin/${_gitname}"
}
