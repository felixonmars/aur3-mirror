# Maintainer: Sam S. <smls75@gmail.com>

pkgname=closure-hib
pkgver=1.1_20121222
_hibver=1.1-2012-12-22
pkgrel=1
pkgdesc='A puzzle platformer game that centers around light (Humble Bundle version)'
url='http://closuregame.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('libgl' 'sdl' 'nvidia-cg-toolkit' 'openal' 'freealut')
source=('closure-hib.desktop')
md5sums=('d691e2d56865f8331999374684bdf354')
options=('!strip' '!upx')
PKGEXT='.pkg.tar'

_installer="Closure-Linux-${_hibver}.sh"
_installer_md5='901014f99aebe01e4fbab90888d54911';


package() {
  cd $srcdir

  # Get installer
  _get_local_source "${_installer}" --md5 "${_installer_md5}" || {
    error "Unable to find the game installer. Please download it from your
           Humble Bundle page, and place it into one of the above locations."
    exit 1; }
  
  # Execute installer
  msg "Starting setup..."
   [[ -d "./temp" ]] && rm -r "./temp"
   sh "./${_installer}" --unattended --no-register --packager pacman \
                        --keep --overwrite \
                        --target  "${srcdir}/temp" \
                        --bindir  "${srcdir}/bin" \
                        --datadir "${pkgdir}/opt"
  
  # Remove bundled libraries & helper binaries (use system packages instead)
  rm -rf "${pkgdir}/opt/Closure/lib"{,64}
  rm -rf "${pkgdir}/opt/Closure/"{xdg-open,xdg-utils}
  
  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "${pkgdir}/opt/Closure/Closure.png" \
                 "${pkgdir}/usr/share/pixmaps/closure.png"

  # Install launch script
  case $CARCH in i686) _arch='x86';; x86_64) _arch='x86_64';; esac
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/Closure/Closure.bin.${_arch}" "${pkgdir}/usr/bin/closure"
}


###### HELPER FUNCTIONS ######

# Locates a file or folder provided by the user, and symlinks it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."; rm -f "$srcdir/$1"
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    echo -n "    - in $_key [${_dir:-<undefined>}] ... ";
    if [[ -z "$_dir" || ! -e "$_dir/$1" ]]; then
      echo "NOT FOUND"
    elif [[ -n $2 && "$(${2#--}sum "$_dir/$1"|awk '{print $1}')" != $3 ]]; then
      echo "CHECKSUM FAILED";
    else
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}
