# Maintainer:  WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=btrfs-progs-unstable-integration-git
_gitname=${pkgname%-integration-git}
pkgver=next.v0.20.rc1.358.g194aa4a
pkgrel=1
pkgdesc="Btrfs filesystem utilities - David Sterba's unstable 'integration-next' branch"
arch=("i686" "x86_64")
url="http://btrfs.wiki.kernel.org/index.php/Main_Page"
license=('GPL')
depends=('glibc' 'e2fsprogs' 'lzo2')
makedepends=('git')
provides=('btrfs-progs')
conflicts=('btrfs-progs' 'btrfs-progs-git' 'btrfs-progs-unstable-integration')
_url=https://projects.archlinux.org/svntogit/packages.git/plain/trunk/
source=(btrfs-progs-unstable::"git://repo.or.cz/btrfs-progs-unstable/devel.git"
        "initcpio-hook-btrfs::${_url}initcpio-hook-btrfs?h=packages/btrfs-progs"
        "initcpio-install-btrfs::${_url}initcpio-install-btrfs?h=packages/btrfs-progs")
md5sums=('SKIP'
         'b09688a915a0ec8f40e2f5aacbabc9ad'
         '7241ba3a4286d08da0d50b7176941112')

pkgver() {
  cd "$_gitname"
  echo "next.$(git --no-pager describe --tags | sed 's:-:.:g')"
}

prepare() {
  cd "$_gitname"
  git checkout integration-next
}

build() {
  cd "$_gitname"
  make CFLAGS="$CFLAGS" all btrfs-select-super
}

package() {
  # install mkinitcpio files
  install -Dm644 initcpio-hook-btrfs "$pkgdir/usr/lib/initcpio/hooks/btrfs"
  install -Dm644 initcpio-install-btrfs "$pkgdir/usr/lib/initcpio/install/btrfs"

  cd "$_gitname"
  make prefix="$pkgdir/usr" bindir="$pkgdir/usr/bin" install
  install -m755 btrfs-select-super "$pkgdir/usr/bin/btrfs-select-super"
  install -Dm644 INSTALL "$pkgdir/usr/share/doc/btrfs/README"

  # fix manpage location
  mv "$pkgdir/usr/man" "$pkgdir/usr/share/"
}

# vim:set ts=2 sw=2 et:
