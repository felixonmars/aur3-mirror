# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=llpp-hjkl-git
pkgver=1425.b83b97a
pkgrel=1
pkgdesc='Lightweight but fast and featureful PDF viewer based on MuPDF with hjkl navigation patches.'
arch=(i686 x86_64)
url='http://repo.or.cz/w/llpp.git'
license=(GPL)
provides=(llpp)
conflicts=(llpp)
options=(!strip) # Required or failure reading pdf documents
depends=(mesa libx11 ncurses)
makedepends=(git wget unzip ocaml)

source=("$pkgname"::git+http://repo.or.cz/r/llpp.git
        llpp.desktop
        hjkl.patch)

md5sums=('SKIP'
         '9c4891f779eac44569671ee3a9cc0335'
         'c253b5d7aa83a9c4dc5cd79e6d8a7a29')

pkgver() {
    cd "$pkgname"
    printf "%s\n" "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$pkgname"
    patch -Np1 -i "$srcdir"/hjkl.patch
}

build() {
    cd "$pkgname"
    # Use upstreams buildscript, not executable - need to invoke shell
    sh buildall.sh
}

package() {
    cd "$pkgname"
    install -Dm644 "$srcdir"/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
    install -Dm755 llpp "$pkgdir"/usr/bin/llpp
}
