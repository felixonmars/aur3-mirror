# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: David Stelter <david.stelter@gmail.com>
# Contributor: Francisco Lopes <chico.lopes@gmail.com>

pkgname='pfff'
pkgver=0.28.1
pkgrel=1
pkgdesc='Tools and APIs for code analysis, visualization and transformation'
arch=('x86_64')
url='https://github.com/facebook/pfff'
license=('LGPL2.1')
optdepends=('gtk2: required by codegraph and codemap')
makedepends=('ocaml' 'ocaml-findlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/facebook/pfff/archive/v$pkgver.tar.gz")
options=(!makeflags staticlibs) # parallel build fails, need *.a files
sha256sums=('ed411376d975d14ceca10414f9d76b212d957b7a69aa393667fe5d5584c9927c')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure --prefix=/usr
	make depend
	make
	make opt
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
	install -dm755 "$pkgdir"/usr/lib/ocaml
	make OCAMLFIND_LDCONF="$pkgdir"/tmp/ld.conf OCAMLFIND_DESTDIR="$pkgdir"/usr/lib/ocaml install-findlib
}
