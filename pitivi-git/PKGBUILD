# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>
# Contributor: Kerrick Staley <mail@kerrickstaley.com>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Gabor Nyekhelyi (n0gabor) <n0gabor@vipmail.hu>

pkgname=pitivi-git
pkgver=0.15.2.5313.47f0747
pkgrel=1
pkgdesc='PiTiVi allows users to easily edit audio/video projects based on the GStreamer framework (Git version)'
arch=('any')
license=('LGPL')
depends=('gstreamer' 'gst-plugins-base' 'gobject-introspection' 'python2' 'python2-gobject' 'goocanvas' 'gst-python2-git' 'gst-editing-services-git' 'gnonlin-git' 'python2-xdg' 'desktop-file-utils' 'hicolor-icon-theme' 'python2-numpy')
makedepends=('automake' 'libtool' 'intltool' 'itstool' 'pygobject2-devel' 'gtk-doc' 'gnome-doc-utils')
optdepends=('python2-pycanberra-git: sound notifications')
install=pitivi-git.install
provides=('pitivi')
conflicts=('pitivi')
url='http://www.pitivi.org/'

source=('git://git.gnome.org/pitivi')
_gitname='pitivi'
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  
  version=$(grep AC_INIT configure.ac | sed 's/AC_INIT(PiTiVi, //' | sed 's/,//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  
  echo $version.$revision.$hash  
}

build() {
  cd $_gitname
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
  install -D -m644 data/pitivi.desktop "$pkgdir/usr/share/applications/pitivi.desktop"
}
