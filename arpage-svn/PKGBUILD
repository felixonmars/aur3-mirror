# Maintainer: David Adler <david dot jo dot adler at gmail dot com>
pkgname=arpage-svn
pkgver=128
pkgrel=1
pkgdesc="JACK-MIDI arpeggiator with transport and tempo sync"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/arpage/"
license=('GPL')
depends=('jack' 'gtkmm' 'libxml++')
makedepends=('subversion' 'intltool')
provides=('arpage')
conflicts=('arpage')
_svntrunk=https://arpage.svn.sourceforge.net/svnroot/arpage
_svnmod=arpage

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build/trunk"

  # fix XML syntax
  sed -i "s_\(arpeggiator\)/_/\1_" -i src/arpstorage.cc

  # fix undeclared sleep
  sed -i 's/#include <cstdio>/&\n#include <unistd.h>/' src/jack_process.cc

  # fix undeclared NULL
  sed -i 's/#include <vector>/&\n#include <cstddef>/' src/scales.h

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build/trunk"
  make DESTDIR="$pkgdir/" install
}
