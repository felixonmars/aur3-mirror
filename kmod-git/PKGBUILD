# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod-git
pkgver=20120902
pkgrel=1
pkgdesc="interface to kernel module operations"
arch=('i686' 'x86_64')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL2')
depends=('glibc' 'zlib')
makedepends=('git' 'gtk-doc')
conflicts=('kmod' 'module-init-tools')
provides=('kmod=5' 'module-init-tools=3.16')
replaces=('module-init-tools')
options=('!libtool')
source=('depmod-search.conf')
md5sums=('4b8cbcbc54b9029c99fd730e257d4436')

_gitroot="git://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git"
_gitname="kmod"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    ( cd "$_gitname" && git pull origin )
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_gitname-build"
  cp -r "$_gitname"{,-build}
  cd "$_gitname-build"

  ./bootstrap-configure \
    --enable-debug \
    --without-xz

  make
}

check() {
  make -C "$_gitname-build" check
}

package() {
  make -C "$_gitname-build" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/sbin "$pkgdir"/{etc,lib}/{depmod,modprobe}.d

  # add symlinks to kmod
  ln -s ../usr/bin/kmod "$pkgdir/sbin/modprobe"
  ln -s ../usr/bin/kmod "$pkgdir/sbin/depmod"
  for tool in {ins,ls,rm}mod modinfo; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -m644 "depmod-search.conf" "$pkgdir/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
