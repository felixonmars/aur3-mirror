# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod-git
pkgver=16.1
pkgrel=1
pkgdesc="interface to kernel module operations"
arch=('i686' 'x86_64')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL2')
depends=('glibc' 'zlib')
makedepends=('git' 'gtk-doc')
conflicts=('kmod' 'module-init-tools')
provides=("kmod=$pkgver" 'module-init-tools=3.16')
replaces=('module-init-tools')
source=('git://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git'
        'depmod-search.conf')
md5sums=(SKIP
         '4b8cbcbc54b9029c99fd730e257d4436')

pkgver() {
  cd 'kmod'
  git describe | {
    IFS=- read M m _
    printf '%s.%s' "${M#v}" "$m"
  }
}

build() {
  cd kmod

  ./bootstrap-configure \
      --without-xz

  make
}

package() {
  make -C kmod DESTDIR="$pkgdir" install

  # tool directories
  install -dm755 "$pkgdir"/{,s}bin

  # config directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d

  # add symlinks to kmod
  for tool in {ins,ls,rm}mod modinfo modprobe depmod; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -m644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
