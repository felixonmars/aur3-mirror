# Maintainer: eagletmt <eagletmt@gmail.com>
pkgname=yaskkserv
pkgver=0.5.3
pkgrel=2
pkgdesc='Yet another SKK Japanese input method dictionary server'
arch=('x86_64' 'i686')
url='http://umiushi.org/~wac/yaskkserv/'
license=('GPL')
depends=('gcc-libs')
makedepends=('perl' 'skk-jisyo')
backup=('etc/rc.d/yaskkserv' 'etc/conf.d/yaskkserv')
source=(http://umiushi.org/~wac/$pkgname/$pkgname-$pkgver.tar.bz2
        yaskkserv
        yaskkserv.conf
        yaskkserv.service)
install=yaskkserv.install

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LC_ALL=C ./configure --prefix=/usr --enable-google-japanese-input
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install_all

  mkdir -p "$pkgdir/usr/share/skk"
  "$pkgdir/usr/bin/yaskkserv_make_dictionary" /usr/share/skk/SKK-JISYO.L "$pkgdir/usr/share/skk/SKK-JISYO.L.yaskkserv"

  mkdir -p "$pkgdir/etc/conf.d"
  install -m644 "$srcdir/yaskkserv.conf" "$pkgdir/etc/conf.d/yaskkserv"

  mkdir -p "$pkgdir/etc/rc.d"
  install -m755 "$srcdir/yaskkserv" "$pkgdir/etc/rc.d/"

  local _systemd_dir="$pkgdir/usr/lib/systemd"
  mkdir -p "$_systemd_dir/system"
  install -m644 "$srcdir/yaskkserv.service" "$_systemd_dir/system/"
}

# vim:set ts=2 sw=2 et:
md5sums=('349bff4d5a293559d1825928ab271818'
         '367cf44ce6262e391e7bfea64ff984c2'
         'b588383351e59e4df09e70b53c053f6d'
         'fcb568744780da96d3cb8df0dd701ee7')
