# Contributor: David Spicer <azleifel at gmail dot com>

pkgname=xine-ui-vdr-hg
pkgver=3130
pkgrel=1
pkgdesc="A free media player (main development branch) with VDR keys enabled"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('curl' 'desktop-file-utils' 'hicolor-icon-theme' 'libpng'
         'libxft' 'libxss' 'lirc-utils' 'shared-mime-info' 'tar'
         'xdg-utils' 'xine-lib')
makedepends=('mercurial' 'libxt' 'pkg-config')
provides=('xine-ui' 'xine-ui-vdr')
conflicts=('xine-ui' 'xine-ui-vdr')
options=('!emptydirs')
install=xine-ui.install

_hgroot="http://hg.debian.org/hg/xine-lib/"
_hgrepo="xine-ui"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server..."

  if [[ -d "$_hgrepo" ]]; then
    cd "$_hgrepo"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "$_hgroot" "$_hgrepo"
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  ./autogen.sh --prefix=/usr --mandir=/usr/share/man \
    --enable-vdr-keys \
    --without-aalib \
    --without-caca

  make
}

package() {
  cd "$srcdir/$_hgrepo-build"
  make DESTDIR="${pkgdir}" install
}
