# $Id$
# Maintainer: Andreas Messer <andi@bastelmap.de>
# Based on arm-elf-gcc-base by Sergej Pupykin <pupykin.s+arch@gmail.com>

_target=arm-eabi

pkgname=${_target}-gcc-base
pkgver=4.7.0
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=("${_target}-binutils" 'libmpc' 'libelf')
options=(!libtool !emptydirs zipman docs !strip)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2)
md5sums=('2a0f1d99fda235c29d40b561f81d9a77')

build() {
  cd $srcdir/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  rm -rf build
  mkdir build
  cd build

  ../configure --prefix=/usr \
	--target=${_target} \
	--host=$CHOST \
	--build=$CHOST \
	--enable-shared --disable-nls --enable-languages=c --enable-multilib \
	--with-local-prefix=/usr/$CHOST/${_target}/lib \
	--with-as=/usr/bin/${_target}-as --with-ld=/usr/bin/${_target}-ld \
	--enable-softfloat \
	--with-float=soft \
	--with-newlib \
	--with-sysroot=/usr/$CHOST/${_target}

  make all-gcc all-target-libgcc
}

package() {
  cd $srcdir/gcc-$pkgver/build

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  make DESTDIR=$pkgdir install-gcc install-target-libgcc

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info

  cp -r $pkgdir/usr/libexec/* $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/libexec

  # strip it manually
  strip $pkgdir/usr/bin/* 2>/dev/null || true
  find $pkgdir/usr/lib -type f -exec ${_target}-strip --strip-debug --strip-unneeded {} \; 2>/dev/null || true
}
