# Maintainer: Ethan Hall
# Contributors: Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Thomas Dziedzic, Dan Serban

pkgname=jdk6
pkgver=6u45
_pkgver=b06
pkgrel=1
pkgdesc="Java 6 Development Kit"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=(custom)
depends=('glibc' 'jre6')
provides=('java-environment=6')
conflicts=('java-environment')
replaces=('j2sdk')
install=jdk.install

DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -o %o %u --header "Cookie:oraclelicensejdk-${pkgver}-oth-JPR=accept-securebackup-cookie;gpw_e24=http://edelivery.oracle.com"')

source=("http://download.oracle.com/otn-pub/java/jdk/${pkgver}-${_pkgver}/jdk-${pkgver}-linux-i586.bin"
        'java-control-panel.desktop'
        'java-monitoring-and-management-console.desktop'
        'java-policy-settings.desktop'
        'java-visualvm.desktop'
        'java-web-start.desktop'
        'jdk6.profile'
        'jdk6.profile.csh'
        'derby-network-server'
        'derby-network-server.conf'
        'construct.sh')

sha256sums=('d53b5a2518d80e1d95565f0adda54eee229dc5f4a1d1a3c2f7bf5045b168a357'
            '30cdb439741f8c6fe1f2ede18886d0fca05839fa7a90694fe21a46fa4ea5779b'
            '99f67d528cd01265f56358c506126283a7b036df56e7b6ddf332934eaa0067d5'
            '3b128d2dfb4e18ea8325dc859334d9aedc1736721b7806fb902e475d07234e3d'
            '35dfaa5ba59a45ccac458633db91f3e8786f5956f172ccfcbbc63592bb2f796e'
            'be68e8d0afbe1bf14c6556b2d2244469afcdd40fe89da8636331dbe02f67495c'
            'c60049f325e76f0553715aad605771d2a6af8db365d3ed0216f0fee51712f43c'
            '858b4a863e024064c3ebb75d41108e862dd00cce9605bfec72a236656fbf7f64'
            'd00f6a0ff92a99ccc78656507072cf2c0f13e4429b4b328016ab5c82843d30d2'
            'd6d69b7276e18459d1ee3a4a4ed8e5416a6ca0f7751b674776cc08a6bd0f0665'
            '2525915f36fa59634f630bf9c171637a6968ff8af615b32c0994ffc3e4949305')

[ "${CARCH}" == 'i686' ] && _arch=i586

[ "${CARCH}" == 'x86_64' ] \
  && _arch=x64 && source[0]="http://download.oracle.com/otn-pub/java/jdk/${pkgver}-${_pkgver}/jdk-${pkgver}-linux-x64.bin" \
  && sha256sums[0]='6b493aeab16c940cae9e3d07ad2a5c5684fb49cf06c5d44c400c7993db0d12e8'

package() {
  rm -rf unbundle-jdk
  mkdir unbundle-jdk
  cd unbundle-jdk
  
  sh ../jdk-${pkgver}-linux-${_arch}.bin -noregister
  
  cd ..

  sh construct.sh unbundle-jdk linux-jdk linux-jre
  
  rm -rf linux-jdk/jre
  
  install -d "${pkgdir}"/opt
  mv linux-jdk "${pkgdir}"/opt/java6
  mkdir -p "${pkgdir}"/usr/share/licenses/jdk6
  mkdir -p "${pkgdir}"/etc/profile.d/
  
  install -m644 "${pkgdir}"/opt/java6/COPYRIGHT "${pkgdir}"/usr/share/licenses/jdk6
  install -m644 "${pkgdir}"/opt/java6/LICENSE "${pkgdir}"/usr/share/licenses/jdk6
  install -m644 "${pkgdir}"/opt/java6/THIRDPARTYLICENSEREADME.txt "${pkgdir}"/usr/share/licenses/jdk6
  
  install -m755 jdk6.profile "${pkgdir}"/etc/profile.d/jdk6.sh
  install -m755 jdk6.profile.csh "${pkgdir}"/etc/profile.d/jdk6.csh
  
  for i in kinit ktab klist; do
    rm "${pkgdir}"/opt/java6/bin/${i}
    rm "${pkgdir}"/opt/java6/man/ja_JP.eucJP/man1/${i}.1
    rm "${pkgdir}"/opt/java6/man/man1/${i}.1
  done
}
