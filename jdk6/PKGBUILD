# Maintainer: Ethan Hall
# Contributors: Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Thomas Dziedzic, Dan Serban

pkgname=jdk6
pkgver=6u33
pkgrel=b04
pkgdesc="Java 6 Development Kit"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=(custom)
depends=('glibc' 'jre6')
provides=('java-environment=6' 'j2sdk')
conflicts=('java-environment=6' 'j2sdk')
replaces=('j2sdk')
install=jdk.install

DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -o %o %u --header "Cookie:oraclelicensejdk-${pkgver}-oth-JPR=accept-securebackup-cookie;gpw_e24=http://edelivery.oracle.com"')

source=("http://download.oracle.com/otn-pub/java/jdk/${pkgver}-${pkgrel}/jdk-${pkgver}-linux-i586.bin"
        'java-control-panel.desktop'
        'java-monitoring-and-management-console.desktop'
        'java-policy-settings.desktop'
        'java-visualvm.desktop'
        'java-web-start.desktop'
        'jdk6.profile'
        'jdk6.profile.csh'
        'derby-network-server'
        'derby-network-server.conf'
        'construct.sh')
        
md5sums=('7eb4177d558b4ec2ddc2f792ab087364'
         'e4d814c0f310d77ed6990c731bccd0fb'
         '4bc2b0be93f76ed07b471e3c89eb5a8a'
         '8e3cec7ab641ec449cabd068be583f5c'
         '9ba148ebabce8ed3351b813a9a66b6e5'
         '619ec32235dcfe454234ef4316f8a7cc'
         '803ceeb0d98abdf800cf7452fce12e7c'
         '3ce59ff37dc2598a5fcf66613851e1d5'
         'a279e195e249000646895d93e199860d'
         '4bdff6982c66d24a879c424aaac3d04d'
         '70b34ef3d5b997e7c15b1b50053d3e37')

[ "${CARCH}" == 'i686' ] && _arch=i586

[ "${CARCH}" == 'x86_64' ] && _arch=x64 && source[0]="http://download.oracle.com/otn-pub/java/jdk/${pkgver}-${pkgrel}/jdk-${pkgver}-linux-x64.bin" && md5sums[0]='94a1de1f4c00a31df1cee902962bc2f5'

build()
{
  rm -rf unbundle-jdk
  mkdir unbundle-jdk
  cd unbundle-jdk
  
  sh ../jdk-${pkgver}-linux-${_arch}.bin -noregister
  
  cd ..

  sh ../construct.sh unbundle-jdk linux-jdk linux-jre
  
  rm -rf linux-jdk/jre
  
  install -d "${pkgdir}"/opt
  mv linux-jdk "${pkgdir}"/opt/java6
  mkdir -p "${pkgdir}"/usr/share/licenses/jdk6
  mkdir -p "${pkgdir}"/etc/profile.d/
  
  install -m644 "${pkgdir}"/opt/java6/COPYRIGHT "${pkgdir}"/usr/share/licenses/jdk6
  install -m644 "${pkgdir}"/opt/java6/LICENSE "${pkgdir}"/usr/share/licenses/jdk6
  install -m644 "${pkgdir}"/opt/java6/THIRDPARTYLICENSEREADME.txt "${pkgdir}"/usr/share/licenses/jdk6
  
  install -m755 jdk6.profile "${pkgdir}"/etc/profile.d/jdk6.sh
  install -m755 jdk6.profile.csh "${pkgdir}"/etc/profile.d/jdk6.csh
  
  for i in kinit ktab klist; do
    rm "${pkgdir}"/opt/java6/bin/${i}
    rm "${pkgdir}"/opt/java6/man/ja_JP.eucJP/man1/${i}.1
    rm "${pkgdir}"/opt/java6/man/man1/${i}.1
  done
}

