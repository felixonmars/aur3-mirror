# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-kde-common-internals-git
_gitname="ktp-common-internals"
pkgver=v0.6.80.1.gbda5eb1
pkgrel=1
epoch=1
pkgdesc="Common components for Kde-Telepathy"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/network/telepathy/ktp-common-internals"
license=('LGPL')
depends=('kdebase-runtime' 'telepathy-logger-qt-git')
makedepends=('git' 'cmake' 'automoc4')
install="$pkgname.install"
conflicts=('telepathy-kde-common-internals' 'telepathy-kde-nepomuk-service-git')
replaces=('telepathy-kde-nepomuk-service-git')
source=("git://anongit.kde.org/ktp-common-internals.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_gitname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on
   make
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
