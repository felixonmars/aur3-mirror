# Maintainer: Moritz Lipp <mlq@pwmt.org>

pkgname=bear
_pkgname=Bear
pkgver=1.4.1
pkgrel=1
pkgdesc="tool to generate compilation database for clang tooling"
arch=('i686' 'x86_64')
url="https://github.com/rizsotto/Bear"
license=('GPL3')
makedepends=('git' 'cmake' 'make')
depends=('libconfig>=1.4')
conflicts=('bear')
provides=('bear')
source=(https://github.com/rizsotto/$_pkgname/archive/$pkgver.tar.gz)
md5sums=('bc08705a6b6e6b757db854b4e7c7db91')

build() {
	cd "$srcdir/$_pkgname-$pkgver"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    .
	make all
}

check() {
	cd "$srcdir/$_pkgname-$pkgver"
  PATH=/usr/bin:$PATH make -k check
}

package() {
	cd "$srcdir/$_pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
