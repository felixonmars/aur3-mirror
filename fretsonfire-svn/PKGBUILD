# Contributor: schlaefer <schlaefer[at]gamez-planet.org> 

pkgname=fretsonfire-svn
pkgver=178
pkgrel=1
pkgdesc="A game of musical skill and fast fingers. Play guitar with the keyboard as accurately as possible. Like guitar Hero."
url="http://fretsonfire.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl' 'sdl_ttf' 'sdl_mixer' 'python-pygame' 'python-opengl' 'pil' 'python-numpy')
makedepends=('subversion')
optdepends=('glew: Used for render-to-texture support' 'pyogg: Used for streaming OGG files from the disk')
[ "$CARCH" = "i686" ] && optdepends[3]='psyco: Used for speed optimization'
provides=('fretsonfire' 'bin32-fretsonfire ')
conflicts=('fretsonfire' 'bin32-fretsonfire ')
source=(fretsonfire.desktop
        fretsonfire.sh
        fretsonfire.png)
    
md5sums=('e792e7dfe01775c0034781393c457696' 
         'ae24d6024235ec8ec92b8a391e675a03' 
         '66668cad0211bf4e6ab1bd5efa8f65bf')
     
_svntrunk=https://fretsonfire.svn.sourceforge.net/svnroot/fretsonfire/trunk
_svnmod=fretsonfire
     
build() {

  #Grab the sources with svn
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver) || return 1
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod || return 1
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  rm -r $srcdir/$_svnmod-build
  cp -r $srcdir/$_svnmod $srcdir/$_svnmod-build
  cd $srcdir/$_svnmod-build

  #Installing The files
  install -d $pkgdir/usr/share/fretsonfire/bin $pkgdir/usr/share/fretsonfire/data || return 1
  cp -r src/* $pkgdir/usr/share/fretsonfire/bin/ || return 1
  cp -r data/* $pkgdir/usr/share/fretsonfire/data/ || return 1
  
  #Removing junk and setting permissions
  cd $pkgdir/usr/share/fretsonfire/data
  rm -r win32/ launcher* Makefile *.svg songs/tutorial/*.svg mods/Chilly/*.svg mods/LightGraphics/*.svg 
  find $pkgdir/usr/share/fretsonfire -type f -exec chmod 664 {} + 
  find $pkgdir/usr/share/fretsonfire -type d -exec chmod 775 {} + 
  
  #Installing the .desktop and icon files
  install -D $srcdir/fretsonfire.desktop  $pkgdir/usr/share/applications/fretsonfire.desktop
  install -D $srcdir/fretsonfire.png $pkgdir/usr/share/pixmaps/fretsonfire.png
  
  #Installing the bin file
  install -D -m0755 $srcdir/fretsonfire.sh $pkgdir/usr/bin/fretsonfire
    
}
