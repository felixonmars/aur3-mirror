# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Ewoud Nuyts <ewoud.nuyts@gmail.com>
pkgname=tesseract-svn
pkgver=731
pkgrel=1
pkgdesc="An OCR Engine that was developed at HP Labs between 1985 and 1995"
arch=('i686' 'x86_64')
url="http://code.google.com/p/tesseract-ocr/"
license=('Apache')
depends=('gcc-libs' 'libtiff' 'libpng' 'leptonica' 'giflib')
makedepends=('subversion')
provides=('tesseract')
conflicts=('tesseract')
options=('!libtool')
source=()
md5sums=()

_svntrunk="http://tesseract-ocr.googlecode.com/svn/trunk/"
_svnmod="tesseract-ocr"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi
  rm -rf $_svnmod-build

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-langs
}
