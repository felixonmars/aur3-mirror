# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=libcgroup-git
pkgver=20110409
pkgrel=1
pkgdesc="Library that abstracts the control group file system in Linux"
arch=(i686 x86_64)
url="http://libcg.sourceforge.net"
license=(LGPL)
makedepends=('git')
backup=(etc/conf.d/cgred etc/conf.d/cgconfig etc/cgconfig.conf etc/cgrules.conf)
source=(cgconfig cgred)
provides=('libcgroup')
md5sums=('54e64c0eaa4eafcef947d3824dc82bbb'
         'ccc0891adbd8863dc11b7bcc339f22d5')

_gitroot="git://libcg.git.sourceforge.net/gitroot/libcg/libcg"
_gitname="libcg"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./bootstrap.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --enable-pam-module-dir=/lib/security

  # build breaks with -j13. maybe it works reliably with less
  unset MAKEFLAGS

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install

  install -Dm644 samples/cgred.conf "$pkgdir/etc/conf.d/cgred"
  install -Dm644 samples/cgconfig.sysconfig "$pkgdir/etc/conf.d/cgconfig"
  install -Dm644 samples/cgconfig.conf "$pkgdir/etc/cgconfig.conf"
  install -Dm644 samples/cgrules.conf "$pkgdir/etc/cgrules.conf"

  cd "$pkgdir"

  rm -rf etc/rc.d
  install -Dm755 "$srcdir/cgred" etc/rc.d/cgred
  install -Dm755 "$srcdir/cgconfig" etc/rc.d/cgconfig
}
