# Maintainer: Nicolas Pouillard (http://nicolaspouillard.fr)
# Contributors:
#   Peter Simons <simons@cryp.to>
#   Andreas Hilboll <andreas@hilboll.de>
#   kljohann@gmail.com
pkgname=mr
pkgver=1.13
pkgrel=1
pkgdesc='Checkout, update, or perform other actions on a set of VC repositories.'
arch=('any')
url='http://kitenet.net/~joey/code/mr/'
license=('GPL2')
depends=('perl' 'perl-libwww' 'perl-html-parser' 'perl-uri')
makedepends=('git' 'rsync')
optdepends=('git: support for git repositories'
            'cvs: support for cvs repositories'
            'svn: support for subversion repositories'
            'mercurial: support for mercurial repositories'
            'bzr: support for bzr repositories'
            'darcs: support for darcs repositories')

build()
{
  local _gitrepo="$pkgname"
  local _gitroot='git://git.kitenet.net/'"$pkgname"

  cd "${srcdir}"
  if [ -d $_gitrepo ] ; then
    cd $_gitrepo
    git fetch origin || return 1
    git reset --hard origin/master || return 1
    msg "The local files are updated."
  else
    # --depth=1 should only be used when no merge is done
    # but it has been reported to not work well with tags in
    # some cases
    git clone "${_gitroot}" $_gitrepo || return 1
  fi
  cd "${srcdir}/$_gitrepo"
  git checkout $pkgver || return 1
  rsync -aH --del "${srcdir}/mr/" "${srcdir}/build/"
  cd "${srcdir}/build"
  make
}

check()
{
  cd "${srcdir}/build"
  make test
}

package()
{
  cd "${srcdir}/build"
  install -m755 -D mr "${pkgdir}/usr/bin/mr"
  install -m755 -D webcheckout "${pkgdir}/usr/bin/webcheckout"
  install -m644 -D mr.1 "${pkgdir}/usr/share/man/man1/mr.1"
  install -m644 -D webcheckout.1 "${pkgdir}/usr/share/man/man1/webcheckout.1"
  install -m644 -D README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -m644 -D lib/git-fake-bare "${pkgdir}/usr/share/mr/git-fake-bare"
  install -m644 -D lib/git-subtree "${pkgdir}/usr/share/mr/git-subtree"
  install -m644 -D lib/git-svn "${pkgdir}/usr/share/mr/git-svn"
  install -m644 -D lib/unison "${pkgdir}/usr/share/mr/unison"
  install -m644 -D lib/vcsh "${pkgdir}/usr/share/mr/vcsh"
}
