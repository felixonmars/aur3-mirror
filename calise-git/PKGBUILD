# Maintainer: Nicolo' Barbon <smilzoboboz@gmail.com>
pkgname=calise-git
pkgver=20120707
pkgrel=1
pkgdesc="A program that computes ambient brightness and sets screen's correct backlight using a webcam. Development GIT Version."
arch=('any')
url="http://calise.sourceforge.net/"
license=('GPL3')
groups=()
depends=('python2-xdg'
         'python2-pyephem'
         'python2-dbus'
         'python2-gobject2'
         'hicolor-icon-theme')
makedepends=('python2-distutils-extra'
             'intltool'
             'libx11'
             'git'
             'linux-headers')
optdepends=('python2-pyqt: gui (non-service only)'
            'pm-utils: graceful pause and resume on suspend/hiberante')
provides=('calise')
conflicts=('calise')
replaces=('camsensor')
backup=()
options=()
changelog=
noextract=()
source=()
sha1sums=()

_gitroot='git://calise.git.sourceforge.net/gitroot/calise/calise'
_gitname='calise'
_buildir='calise-build'

build() {

    cd ${srcdir}
    msg 'Connecting to GIT server...'

    # if calise dir exists update, else clone new
    if [ -d ${_gitname} ]
    then
        cd ${_gitname} && git pull origin
        cd ..
    else
        git clone ${_gitroot}
    fi
    msg 'GIT checkout done or server timeout.'

    # if build-dir exists remove it
    if [ -d ${_buildir} ]
    then
        msg 'Cleaning previous build...'
        rm -rf ${_buildir}
    fi

    # clone into build-dir
    git clone ${_gitname} ${_buildir}
    cd ${_buildir}

    # build
    env python2 setup.py build
}

package() {

    cd "${srcdir}/${_buildir}"

    # preliminary check
    if [ ! -d /sys/class/backlight/ ]
    then
        return 1
    fi

    # udev rules creation (for all available interfaces)
    interfaces=""
    for path in /sys/class/backlight/*
    do
        interfaces="`udevadm info -a -p ${path} |
            grep "KERNEL=" |
            sed s'/KERNEL==//' |
            awk -F ['"'] '{print $2}'` ${interfaces}"
    done
    if [ -z $interfaces ]
    then
        echo "Your configuration is currently not supported."
        return 2
    fi
    for interface in ${interfaces}
    do
        if [ ! -f /sys/class/backlight/$interface/brightness ]
        then
            echo "Your configuration is currently not supported."
            return 3
        fi
        udevrule="${interface}.rules"
        echo "KERNEL==\"${interface}\", RUN+=\"/bin/chmod 664 /sys/class/backlight/${interface}/brightness\"" > $udevrule
        echo "KERNEL==\"${interface}\", RUN+=\"/bin/chgrp video /sys/class/backlight/${interface}/brightness\"" >> $udevrule
        install -Dm644 ${udevrule} "${pkgdir}/usr/lib/udev/rules.d/99-backlight-${udevrule}"
    done

    # dbus policy exception ("rm" for distutils-extra's path error workaround)
    install -Dm644 "other/org.${_gitname}.conf" "${pkgdir}/etc/dbus-1/system.d/org.${_gitname}.conf"
    rm "other/org.${_gitname}.conf"

    # pm-utils sleep script
    if [ -d /usr/lib/pm-utils ]
    then
        install -Dm755 "other/53${_gitname}d" "${pkgdir}/usr/lib/pm-utils/sleep.d/53${_gitname}d"
    fi

    # final install
    env python2 setup.py install --prefix="${pkgdir}/usr/"
}
