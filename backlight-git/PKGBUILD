# Maintainer: Bastien Dejean <baskerville at lavabit.com>

pkgname=backlight-git
pkgver=20121229
pkgrel=1
pkgdesc='Get and set backlight brightness'
arch=('i686' 'x86_64')
url='https://github.com/baskerville/backlight'
license=('custom:Unlicense')
makedepends=('git')
provides=('backlight')
conflicts=('backlight')
_gitroot='git://github.com/baskerville/backlight.git'
_gitname='backlight'

build() {
    cd "$srcdir"
    if [ -d "$_gitname" ] ; then
        cd "$_gitname" && git pull origin
    else
        git clone "$_gitroot" "$_gitname"
    fi
    cd "$srcdir/$_gitname"
    make clean && make PREFIX="/usr"
}

package() {
    cd "$srcdir/$_gitname"
    make PREFIX="/usr" DESTDIR="$pkgdir" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
