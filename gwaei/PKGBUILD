# Maintainer:  TDY <tdy@gmx.com>
# Contributor: lswest <lswest34 : gmail>
# Contributor: Christoph Zeiler <rabyte : gmail>

pkgname=gwaei
pkgver=1.5.2
pkgrel=1
pkgdesc="A Japanese-English dictionary program"
arch=('i686' 'x86_64')
url="http://gwaei.sourceforge.net/"
license=('GPL3')
depends=('curl>=7.19.6' 'gconf>=2.24.0' 'hicolor-icon-theme' 'libsexy>=0.1.11'
         'libunique>=1.0.0')
makedepends=('gnome-doc-utils>=0.14.0' 'pkg-config>=0.9.0')
optdepends=('rsync: provide incremental dictionary updates'
            'ttf-arphic-ukai'
            'ttf-kanjistrokeorders: show stroke order diagrams')
install=gwaei.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('f51ae472828f077170ae88f69a14d775')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-scrollkeeper  --disable-schemas-install \
      --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
  glib-compile-schemas "$pkgdir/usr/share/glib-2.0/schemas"
  ln -sf $pkgname "$pkgdir/usr/bin/waei"
}
