pkgname=ganglia
pkgver=3.4.0
pkgrel=3  # DON'T FORGET TO CHANGE THIS
pkgdesc="A scalable distributed monitoring system for high-performance computing systems such as clusters and Grids."
arch=('i686' 'x86_64')
url="http://ganglia.sourceforge.net/"
license=('BSD')
depends=('apr' 'confuse' 'expat' 'pcre' 'python2' 'rrdtool')
options=('!libtool')
optdepends=('ganglia-web: Web frontend')
install='ganglia.install'
backup=('etc/ganglia/gmond.conf'
        'etc/ganglia/gmetad.conf')
source=("http://downloads.sourceforge.net/ganglia/$pkgname-$pkgver.tar.gz" 
        'gmond.rc'
        'gmetad.rc'
        'gmond.service'
        'gmetad.service'
        'ganglia.install'
        'uid.patch')
md5sums=('3734a381f6fa652a8b957b63f144b397'
         '12b2f85361754eaf6fc5129cccec7034'
         '292b74a5a09a0bd9a4a658823adc034b'
         '0b1ec1a31e5893fb7e7ccdc4ab5d8686'
         '2d3f1e979492cfa82cf943052d84ecbc'
         'acad2273520f13f4d0638ca9109ca2d8'
         'c51419016a2bd041f09bac978b9d60c3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  for patch in $srcdir/*.patch; do
    msg2 "Applying $(basename $patch)"
    patch -Np1 -i $patch
  done

  msg2 "Starting build"
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc/ganglia \
              --with-gmetad --enable-gexec --enable-status \
              --with-python=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/lib/ganglia/python_modules"

  msg2 "Generating default gmond.conf"
  ./gmond/gmond --default_config > "$pkgdir/etc/ganglia/gmond.conf"
  
  # Install the rc files
  install -m 755 -D "$srcdir/gmond.rc" "$pkgdir/etc/rc.d/gmond"
  install -m 755 -D "$srcdir/gmetad.rc" "$pkgdir/etc/rc.d/gmetad"
  # Install the unit files
  for unit in gmond.service gmetad.service; do
    install -m 644 -D "$srcdir/$unit" "$pkgdir/usr/lib/systemd/system/$unit"
  done
}
