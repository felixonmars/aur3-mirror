pkgname=ganglia
pkgver=3.7.1
pkgrel=2
pkgdesc="A scalable distributed monitoring system for high-performance computing systems such as clusters and Grids."
arch=('i686' 'x86_64' 'armv6h')
url="http://ganglia.sourceforge.net/"
license=('BSD')
depends=('apr' 'confuse' 'python2' 'rrdtool')
options=('!libtool')
optdepends=('ganglia-web: Web frontend')
install='ganglia.install'
backup=('etc/ganglia/gmond.conf'
        'etc/ganglia/gmetad.conf')
source=("http://downloads.sourceforge.net/ganglia/$pkgname-$pkgver.tar.gz"
        'gmond.rc'
        'gmetad.rc'
        'gmond.service'
        'gmetad.service'
        'ganglia.install'
        'uid.patch')
md5sums=('8e9de3d5746896ad9aca1d94ca99e1eb'
         '12b2f85361754eaf6fc5129cccec7034'
         '292b74a5a09a0bd9a4a658823adc034b'
         '25ebc6cf829089a9f330ad177befdd45'
         'd795bca44fb73cdf8a2b550e27c50b6f'
         'acad2273520f13f4d0638ca9109ca2d8'
         'a0d469dd2b9ec40dbe20377c51ca904c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  for patch in $srcdir/*.patch; do
    msg2 "Applying $(basename $patch)"
    patch -Np1 -i $patch
  done

  # The original service files don't create a pidfile
  cp "$srcdir/gmond.service" "$srcdir/$pkgname-$pkgver/gmond/gmond.service.in"
  cp "$srcdir/gmetad.service" "$srcdir/$pkgname-$pkgver/gmetad/gmetad.service.in"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc/ganglia \
              --with-gmetad --enable-gexec --enable-status \
              --with-python=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Move sbin to bin
  mv "$pkgdir/usr/sbin"/* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/usr/sbin"

  # Install Python modules
  mkdir -p "$pkgdir/usr/lib/ganglia/python_modules"
  find "gmond/python_modules" -name *.py \
       -exec cp \{\} "$pkgdir/usr/lib/ganglia/python_modules/" \;
  cp -R "gmond/python_modules/conf.d" "$pkgdir/etc/ganglia/"

  msg2 "Generating default gmond.conf"
  ./gmond/gmond --default_config > "$pkgdir/etc/ganglia/gmond.conf"

  install -m 644 -D COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"

  # Uncomment these lines to install the rc files
  #install -m 755 -D "$srcdir/gmond.rc" "$pkgdir/etc/rc.d/gmond"
  #install -m 755 -D "$srcdir/gmetad.rc" "$pkgdir/etc/rc.d/gmetad"
}
