# Contributor: Andras Biro <bbandi86@gmail.com>
# PKGBUILD based on the binutils-avr PKGBUILD by 
#	Corrado Primier <bardo@aur.archlinux.org> and danst0 <danst0@west.de>

pkgname=binutils-avr-tinyos
pkgver=2.17
pkgrel=3
pkgdesc="A set of programs to assemble and manipulate binary and object files for the avr architecture, with TinyOS patches"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('glibc>=2.12-5' 'zlib')
makedepends=('subversion')
provides=('binutils-avr')
options=('!libtool' '!distcc' '!ccache')
source=("http://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2")
md5sums=('1d81edd0569de4d84091526fd578dd7b')


build() {
	cd ${srcdir}
	svn checkout http://tinyos-main.googlecode.com/svn/trunk/packaging/old-toolchain/avr-binutils/patch/
	cd binutils-${pkgver}
	cat ../patch/*.patch|patch -p0

  install -d build
  cd build

  CC="gcc -L$(pwd)/bfd/.libs/"
  if [ "${CARCH}" = "x86_64" ]; then
    ../configure --build=$(../config.guess) \
                 --disable-multilib \
                 --disable-nls \
                 --enable-64-bit-bfd \
                 --enable-install-libbfd \
                 --includedir=/usr/$(../config.guess)/include \
                 --infodir=/usr/share/info \
                 --libdir=/usr/lib \
                 --mandir=/usr/share/man \
                 --prefix=/usr \
                 --target=avr \
                 --disable-werror
  else
    ../configure --build=$(../config.guess) \
                 --disable-nls \
                 --enable-install-libbfd \
                 --includedir=/usr/$(../config.guess)/include \
                 --infodir=/usr/share/info \
                 --libdir=/usr/lib \
                 --mandir=/usr/share/man \
                 --prefix=/usr \
                 --target=avr \
                 --disable-werror
  fi

  # This checks the host environment and makes sure all the necessary tools are available to compile Binutils.
  make configure-host || return 1

  make tooldir=/usr || return 1
}

package() {
  cd ${srcdir}/binutils-${pkgver}

  cd build
  make DESTDIR=${pkgdir} tooldir=/usr install || return 1

  rm -f ${pkgdir}/usr/lib/libiberty.a

  for bin in addr2line ar as c++filt gprof ld nm objcopy objdump ranlib readelf size strings strip ; do
    rm -f ${pkgdir}/usr/bin/${bin} || return 1
  done

  for info in as bfd binutils configure ld standards; do
    mv ${pkgdir}/usr/share/info/${info}.info ${pkgdir}/usr/share/info/avr-${info}.info || return 1
  done
}

# vim:set ts=2 sw=2 et:
