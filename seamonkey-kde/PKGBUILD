# Mantainer: Franco Tortoriello
# based on the official seamonkey and firefox packages

pkgname=seamonkey-kde
pkgver=2.13.1
pkgrel=2
pkgdesc="SeaMonkey internet suite with OpenSUSE patches for better KDE integration"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'libevent' 'libnotify' 'nss' 'libjpeg-turbo' 'libvpx'
	 'mime-types' 'mozilla-common' 'startup-notification' 'kmozillahelper')
makedepends=('unzip' 'zip' 'python2' 'yasm' 'wireless_tools' 'mesa'
	     'autoconf2.13' 'imake' 'ccache')
optdepends=('wireless_tools: Location detection via available WiFi networks')
provides=("seamonkey=$pkgver")
conflicts=('seamonkey')
options=(!emptydirs)
install=seamonkey.install
url="http://www.seamonkey-project.org/"
source=("ftp://ftp.mozilla.org/pub/mozilla.org/seamonkey/releases/$pkgver/source/seamonkey-$pkgver.source.tar.bz2"
	'seamonkey.desktop' 'mozconfig' 'vendor.js' 'install-dir.patch' 'prefs.patch' 'system-cairo.patch'
	# seamonkey opensuse patches
	https://api.opensuse.org/public/source/mozilla:Factory/seamonkey/seamonkey-shared-nss-db.patch
	https://api.opensuse.org/public/source/mozilla:Factory/seamonkey/mozilla-gstreamer.patch
	https://api.opensuse.org/public/source/mozilla:Factory/seamonkey/mozilla-gstreamer-760140.patch
	https://api.opensuse.org/public/source/mozilla:Factory/seamonkey/mozilla-nongnome-proxies.patch
	https://api.opensuse.org/public/source/mozilla:Factory/seamonkey/mozilla-ntlm-full-path.patch
	https://api.opensuse.org/public/source/mozilla:Factory/seamonkey/mozilla-shared-nss-db.patch
	# firefox opensuse patches
	https://api.opensuse.org/public/source/mozilla:Factory/MozillaFirefox/firefox-browser-css.patch
	https://api.opensuse.org/public/source/mozilla:Factory/MozillaFirefox/firefox-kde.patch
	https://api.opensuse.org/public/source/mozilla:Factory/MozillaFirefox/mozilla-kde.patch)

build() {
  cd "$srcdir/comm-release/mozilla"

  msg2 "Patching ..."
  patch -Np1 -i "$srcdir/mozilla-gstreamer.patch"
  patch -Np1 -i "$srcdir/mozilla-gstreamer-760140.patch"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-ntlm-full-path.patch"
  patch -Np1 -i "$srcdir/mozilla-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/firefox-browser-css.patch"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"

  cd ..
  patch -Np1 -i "$srcdir/seamonkey-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/install-dir.patch"
  patch -Np1 -i "$srcdir/prefs.patch"
  patch -Np1 -i "$srcdir/system-cairo.patch"
  cp ../mozconfig .mozconfig

  # Fix PRE_RELEASE_SUFFIX
  sed '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    -i mozilla/browser/base/Makefile.in

  # Don't exit with error when some libs are missing which we have in
  # system.
  sed '/^MOZ_PKG_FATAL_WARNINGS/s@= 1@= 0@' \
      -i suite/installer/Makefile.in

  # Fix an issue with makepkg -R (it tries to remove files already removed)
  sed 's/xargs rm$/xargs rm -f/' -i mozilla/toolkit/mozapps/installer/packager.mk

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/seamonkey"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  msg2 "Starting build ..."
  make -f client.mk build
}

package() {
  cd "$srcdir/comm-release"

  make -j1 -f client.mk DESTDIR="$pkgdir" install
  install -Dm644 ../vendor.js "$pkgdir/usr/lib/seamonkey/defaults/preferences/vendor.js"

  # Use system-provided dictionaries
  rm -r "$pkgdir"/usr/lib/seamonkey/dictionaries
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/seamonkey/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/seamonkey/hyphenation"

  # Desktop files
  install -Dm644 suite/branding/nightly/icons/gtk/seamonkey.png \
	"$pkgdir/usr/share/pixmaps/seamonkey.png"
  install -Dm644 ../seamonkey.desktop \
	"$pkgdir/usr/share/applications/seamonkey.desktop"

  # man page
  install -Dm644 obj-*/mozilla/dist/man/man1/seamonkey.1 "$pkgdir/usr/share/man/man1/seamonkey.1"
  gzip -9 "$pkgdir/usr/share/man/man1/seamonkey.1"

  # Remove development stuff
  rm -r "$pkgdir"/usr/{include,lib/seamonkey-devel,share/idl}

  # Remove a reference to srcdir
  sed -e "\|$srcdir|d" -i "$pkgdir/usr/lib/seamonkey/defaults/pref/channel-prefs.js"

  msg2 "The Lightning calendar extensions are in $srcdir/comm-release/obj-*/mozilla/dist/xpi-stage"
  msg2 "They have to be installed manually or they won't work"
}

md5sums=('469248830dddd8bbd6a1c5017ebd0fe3'
         '219ad4600af34f4dc9301372ae4f4752'
         '96c89ad284206f9a7cfca5be414aee33'
         '9b3b71cec2cb4ab6eb098c3a1c56df10'
         'ec5a6b361be03bddb5af997fb6215f99'
         '2d1d8fcb1e579f1c7a334d0f33797314'
         '183d71a79c0f02eaa1b52b8c71690883'
         '9646b7682d0ce02e7ebb2e13ffe50157'
         '9d5924e4cbe928662b5b2d01c775c432'
         '94eb526298bc2360410b01f575f63383'
         '8b0ecfdf697485d7b7dd26291c0dc478'
         '57b03086987b58680bb18903076f61ad'
         'ddaa2f9c65458f8d3b53d32170959593'
         'df34733c7b43225d85453ebbb87521c6'
         'e308ff4a14b122d385247e3335ddd42a'
         '0d4cb76bd92d3e9e26b11bdc17964f00')
