# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: TuxSpirit <tuxspirit@archlinux.fr>

_pkgname=peazip
pkgname=$_pkgname-gtk2
pkgver=4.7.1
pkgrel=2
pkgdesc="GTK2 archiver utility"
arch=(i686 x86_64)
url="http://www.peazip.org/peazip-linux.html"
license=(LGPL)
depends=(balz desktop-file-utils gdk-pixbuf lpaq paq8o p7zip quad unace upx)
if [ "$CARCH" == "x86_64" ]; then
    depends+=(lib32-gmp4 lib32-gtk2 lib32-ncurses)
else
    depends+=(gmp4 gtk2)
fi
provides=($_pkgname)
conflicts=($_pkgname-portable-gtk1 $_pkgname-beta-{gtk2,qt} $_pkgname-qt)
replaces=($_pkgname-{beta,portable,portable-beta}-gtk2)
install=$_pkgname.install
source=($pkgname.tgz::http://downloads.sourceforge.net/project/$_pkgname/$pkgver/${_pkgname}-$pkgver.LINUX.GTK2.tgz $_pkgname.desktop)
noextract=($pkgname.tgz)
sha256sums=('99d9a6be71f6df2134c293dffca2f97b2ac24c84fb643a570b9a6b3a90faef06'
            '43c674f2f040de1e8f6ecb03b1f768207c6d7393fb83f148ebfe84e1daa7c915')
sha512sums=('550e7dbd16bdff34d9a36b1e39868057bc5dfa676400864bc8d4f46a166736cda4d462f98665808e67aa660fe7a83476d63b87e09a9d13d3c97481cf97ee2734'
            '110d6e7d5ec1ce64f7f597e03c73f66f220a1d22ad1cd9604471605b1e47e031f50fc06a00307677ea34e3a0cdcc652726b12061d75bdfb970c0178dbd298281')

_pkgprefix="$pkgdir"/usr/lib/$_pkgname
_resourcedir="$_pkgprefix"/res

package() {
    install -d "$srcdir"/$pkgname/
    cd "$srcdir"/$pkgname/
    tar xf ../$pkgname.tgz -C ./
    cd usr/local/share/PeaZip/res/

    install -Dm755 ../$_pkgname "$_pkgprefix"/$_pkgname
    for i in pea pealauncher rnd zpaq/{zpaq,lzppre}; do
        install -Dm755 $i "$_resourcedir"/$i
    done

    for i in arc/{arc,*.sfx}; do
        install -Dm755 $i "$_resourcedir"/$i
    done

    for i in altconf.txt lang/* themes/{{nographic,seven}-embedded/*,*.7z} zpaq/{min,max,mid}.cfg arc/arc.{ini,groups}; do
        install -Dm644 $i "$_resourcedir"/$i
    done

    install -d "$_resourcedir"/7z/Codecs/
    for i in 7z{,a,r,.so,Conf.sfx,.sfx} Codecs/Rar29.so; do
        ln -sf /usr/lib/p7zip/$i "$_resourcedir"/7z/$i
    done

    for i in quad/{balz,quad} unace/unace upx/upx lpaq/lpaq8 paq/paq8o; do
        install -d "$_resourcedir"/$(dirname $i)/
        ln -sf /usr/bin/$(basename $i) "$_resourcedir"/$i
    done

    install -Dm644 "$srcdir"/$pkgname/usr/local/share/icons/$_pkgname.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png
    desktop-file-install "$srcdir"/$_pkgname.desktop --dir="$pkgdir"/usr/share/applications/

    install -d "$pkgdir"/usr/bin/
    for i in /usr/lib/$_pkgname/{$_pkgname,res/pea,res/pealauncher}; do
        ln -sf $i "$pkgdir"/usr/bin/$(basename $i)
    done
}
