# Maintainer: James Laird <jhl@mafipulation.org>
pkgname=triggerhappy-git
pkgver=20130407
pkgrel=1
pkgdesc="A lightweight hotkey daemon"
arch=('i686' 'x86_64')
url="https://github.com/wertarbyte/triggerhappy"
license=('GPL')
depends=('glibc')
makedepends=('git' 'linux-api-headers')
install='triggerhappy.install'
backup=('etc/triggerhappy.conf')
source=('triggerhappy.service' 'triggerhappy.conf' 'triggerhappy-gcc4.8.patch')
md5sums=('54c8ab1e0bbf4180d794abb22c51489c'
         '0e5e9c51fa040e6acf65f43ba9b3684d'
         'b91d0df17e3e8102d2f330ab6038daf5')

_gitroot="https://github.com/wertarbyte/triggerhappy.git"
_gitname=triggerhappy

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -p1 < "$srcdir/triggerhappy-gcc4.8.patch"
  make
}

package() {
  mkdir -p "$pkgdir/usr/lib/systemd/system" "$pkgdir/etc" "$pkgdir/usr/lib/udev/rules.d"
  install -m 644 triggerhappy.service "$pkgdir/usr/lib/systemd/system/"
  install -m 644 triggerhappy.conf "$pkgdir/etc/"

  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  install -m 644 -T udev/triggerhappy-udev.rules "$pkgdir/usr/lib/udev/rules.d/97-triggerhappy.rules"
}

# vim:set ts=2 sw=2 et:
