# Maintainer: Mauro Santos <registo.mailling@gmail.com>
# Contributor: Benjamin van der Burgh <benjaminvdb@gmail.com>
# Contributor: Leif Warner <abimelech@gmail.com>
# Contributor: Christophe Gueret <christophe.gueret@gmail.com>
pkgname=dvb-usb-rtl2832u-openpli
pkgver=20130127
pkgrel=1
pkgdesc="Linux module for the RTL2832U DVB-T USB2.0 device"
arch=('i686' 'x86_64')
url="http://openpli.org/forums/topic/20899-rtl2832u-chipset-support-proposal"
license=('GPL')
depends=('linux')
makedepends=('linux-headers')
install="${pkgname}.install"
provides=('dvb-usb-rtl2832u')
source=('dvb-usb-rtl2832u.patch::http://openpli.git.sourceforge.net/git/gitweb.cgi?p=openpli/openpli-oe-core;a=blob_plain;f=meta-openpli/recipes-linux/linux/linux-etxx00/dvb-usb-rtl2832.patch;h=fe9decd3a42ecded975f1df38f13d7aaa646c353;hb=3d094f57b0123d877e1ed73a974c8f92ef862b60'
        'Makefile'
        'dvb-usb-rtl28xxu.conf'
        'linux3.7-arm-fixes.patch')
md5sums=('a236b5fd218a9a0deb0390376a8179b5'
         'b91ef30d49fc0e7d8e76b39c98cf3ff6'
         '063be4e255e010899574fc3cdec67120'
         'a0348f4d26f6248412d4eae166959ea5')

_extramodules="extramodules-$(uname -r | cut -f-2 -d.)-$(uname -r | cut -f3 -d-)"
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

build() {
  cd "$srcdir"
  
  [[ -e build_dir ]] && rm -r build_dir
  mkdir  build_dir
  cd build_dir

  # Get the sources from OpenPLi kernel patch
  patch -p5 < "$srcdir/dvb-usb-rtl2832u.patch" &> /dev/null && return 1
  
  # Copy Makefile
  cp "$srcdir/Makefile" .
    
  ## Patches go here ##
  patch -p1 < "$srcdir/linux3.7-arm-fixes.patch"
  
  # Build module
  export KBUILD_SRC="/usr/src/linux-${_kernver}"
  export KVER="${_kernver}"
  make
}

package() {
  install -D -m644 "${srcdir}/dvb-usb-rtl28xxu.conf" "${pkgdir}/etc/modprobe.d/dvb-usb-rtl28xxu.conf"
  install -D -m644 "${srcdir}/build_dir/dvb-usb-rtl2832u.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/dvb-usb-rtl2832u.ko"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/${install}"
} 
