# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=yaprm
pkgver=0.7.1
pkgrel=3
pkgdesc="Yet Another POV-Ray Modeller is a powerful modeller designed to seamlessly interact with the POV-Ray ray tracing renderer."
arch=(i686 x86_64)
url="http://sourceforge.net/apps/phpwebsite/yaprm/index.php"
license=('GPL')
depends=('povray' 'libgle' 'gdl' 'gtkglextmm' 'ftgl' 'shared-mime-info')
makedepends=('intltool')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/Y.A.P.R.M./Y.A.P.R.M.%20$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('965e14c739135bd32b33ab1452000d7d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # include missing glu library
  export LIBS="-lGLU"

  # remove xdg-mime install
  sed -i "/xdg-mime/d" data/Makefile.*

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 data/yaprm-yml_mime.xml "$pkgdir/usr/share/mime/packages/yaprm-yml.xml"
  install -Dm644 data/yaprm-ypm_mime.xml "$pkgdir/usr/share/mime/packages/yaprm-ypm.xml"
}

# vim:set ts=2 sw=2 et:
