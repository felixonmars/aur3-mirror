# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=mididings
pkgver=20101119
pkgrel=2
pkgdesc="A MIDI router and processor based on Python, supporting ALSA and JACK MIDI."
arch=('i686' 'x86_64')
url="http://das.nasophon.de/mididings/"
license=('GPL')
depends=('boost-libs' 'python2' 'jack' 'libsmf')
makedepends=('boost')
optdepends=('dbus-python: to send DBUS messages'
            'pyliblo: to send or receive OSC messages'
            'pyinotify: to automatically restart')
source=("http://das.nasophon.de/download/$pkgname-$pkgver.tar.gz")
md5sums=('a9a3308f05f40deff7c790ed108e734d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON="python2"

  python2 setup.py build --enable-smf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root=$pkgdir

  # Install docs and examples
  mkdir -p "$pkgdir"/usr/share/{doc/,}$pkgname
  install -Dm644 doc/*.* "$pkgdir/usr/share/doc/$pkgname"
  cp -a doc/examples "$pkgdir/usr/share/$pkgname"
}