# Maintainer: lilydjwg <lilydjwg@gmail.com>
# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Jan-Erik Meyer-Luetgens <nyan at meyer-luetgens dot de>
pkgname=python-shiboken
pkgver=1.1.2
pkgrel=1
_pyver=3.3
pkgdesc="Python 3 bindings generator for Qt C++ libraries"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.pyside.org"
depends=("python>=${_pyver}" "qt>=4.8" 'openssl')
optdepends=("python-sphinx: Documentation")
makedepends=('cmake' 'automoc4' 'generatorrunner>=0.6.16')
conflicts=("libshiboken")
provides=("libshiboken" "shiboken")
source=("http://qt-project.org/uploads/pyside/shiboken-$pkgver.tar.bz2" "FindPython3Libs.cmake.patch")
md5sums=('0a37b5011b3a7276bf4584317412a4b6' 'e21f354434c2c43b8ebdcf582d512de5')

build(){
    patch $srcdir/shiboken-$pkgver/cmake/Modules/FindPython3Libs.cmake FindPython3Libs.cmake.patch
    cd $srcdir/shiboken-$pkgver
    mkdir -p build && cd build
    cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
              -DCMAKE_BUILD_TYPE=Release   \
              -DBUILD_TESTS=OFF            \
              -DUSE_PYTHON3=yes
    make
}

package(){
    cd $srcdir/shiboken-$pkgver/build
    make DESTDIR=$pkgdir install

    cd generator
    _pyverflags=$(python -c 'import sysconfig; print(sysconfig.get_config_var("SOABI"))')
    install -D -m 755 ../data/ShibokenConfig.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -D -m 755 ../data/ShibokenConfigVersion.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -D -m 755 ../data/ShibokenConfig.${_pyverflags}.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -D -m 755 ../data/shiboken.pc $pkgdir/usr/lib/pkgconfig/
}

