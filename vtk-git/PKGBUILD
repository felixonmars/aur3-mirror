# Maintainer: Maurizio D'Addona <mauritiusdadd@gmail.com>

#http://vtk.org/gitweb

_gitname='VTK'
#_gitbranch="release" #Maintenance of latest release 
_gitbranch="master" #Development

pkgname=vtk-git
pkgver=6.1.0.r1500.g8fc05fa
pkgrel=1
pkgdesc='A software system for 3D computer graphics, image processing, and visualization.'
arch=('i686' 'x86_64')
url='http://www.vtk.org/'
license=('BSD')
options=(!strip staticlibs)
conflicts=('vtk')
provides=('vtk')
depends=('libjpeg-turbo' 'boost'
         'ffmpeg' 'qt4' 'lesstif'
         'mariadb' 'hdf5' 'glu'
         'python2' 'tk' 'gnuplot'
         'qtwebkit' 'java-environment')
makedepends=('cmake' 'doxygen' 'mesa-libgl')
optdepends=('java-runtime: java bindings'
            'graphviz: drawing tools')

source=("VTK::git://vtk.org/${_gitname}.git#branch=$_gitbranch" "VTKData::git://vtk.org/${_gitname}Data.git#branch=$_gitbranch")
md5sums=('SKIP' 'SKIP')

pkgver()
{
   cd "$srcdir/$_gitname"
   if [ $_gitbranch = "release" ]; then
       git describe --tags | sed -E 's|([^-]*-g)|r\1|;s|-|.|g;s|v||g'
   else
       git describe --long | sed -E 's|([^-]*-g)|r\1|;s|-|.|g;s|v||g'
   fi
}

build() 
{
    cd "${srcdir}"

    rm -rf $pkgname-build
    mkdir  $pkgname-build
    cd  $pkgname-build

    msg "\x1b[5m\e[93mWARNING\e[39m: an internet connection may be required in order to compile the package!\x1b[0m"

    sleep 4

    # Cache for external data needed during compilation
    mkdir -p $srcdir/ExternalData

    cmake "${srcdir}/VTK" \
      -Wno-dev \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DVTK_DATA_STORE=${srcdir}/VTKData \
      -DExternalData_OBJECT_STORES=$srcdir/ExternalData \
      -DCMAKE_CXX_FLAGS="-D__STDC_CONSTANT_MACROS" \
      -DCMAKE_BUILD_TYPE="Release" \
      -DVTK_USE_SYSTEM_EXPAT=ON \
      -DVTK_USE_SYSTEM_FREETYPE=ON \
      -DVTK_USE_SYSTEM_HDF5=ON \
      -DVTK_USE_SYSTEM_JPEG=ON \
      -DVTK_USE_SYSTEM_LIBXML2=ON \
      -DVTK_USE_SYSTEM_OGGTHEORA=ON \
      -DVTK_USE_SYSTEM_PNG=ON \
      -DVTK_USE_SYSTEM_TIFF=ON \
      -DVTK_USE_SYSTEM_ZLIB=ON \
      -DBUILD_SHARED_LIBS=ON \
      -DBUILD_DOCUMENTATION=ON \
      -DDOCUMENTATION_HTML_HELP=ON \
      -DDOCUMENTATION_HTML_TARZ=ON \
      -DBUILD_EXAMPLES=ON \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
      -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
      -DPYTHON_UTIL_LIBRARY=/usr/lib64/libutil.so \
      -DVTK_USE_FFMPEG_ENCODER=ON \
      -DVTK_USE_TK=ON \
      -DVTK_USE_X=ON \
      -DVTK_WRAP_JAVA=ON \
      -DVTK_WRAP_PYTHON=ON \
      -DVTK_WRAP_TCL=ON \
      -DVTK_TCL_TK_STATIC=ON \
      -DVTK_Group_Imaging=ON \
      -DVTK_Group_Qt=ON \
      -DVTK_Group_Rendering=ON \
      -DVTK_Group_StandAlone=ON \
      -DVTK_Group_Tk=ON \
      -DVTK_Group_Views=ON \
      -DVTK_Group_Web=OFF \

    
    make ${MAKEFLAGS}

    echo "export VTK_DATA_ROOT=/usr/share/vtk/VTKData" > vtk.sh
    echo "export VTK_ROOT_DIR=/usr" >> vtk.sh
}

package()
{
    cd "${srcdir}/$pkgname-build"
    
    make DESTDIR="$pkgdir" install
    
    for subd in $(find $pkgdir/usr/lib/tcltk -maxdepth 1 -type d -name "vtk*")
    do 
        echo "export TCLLIBPATH=\"\${TCLLIBPATH} /usr/lib/tcltk/$(basename $subd)\""\
            >> vtk.sh
    done
    
    install -dv "${pkgdir}/usr/share/vtk/VTKData"
    install -dv "${pkgdir}/usr/share/java/vtk"
    
    mv -v "${pkgdir}/usr/lib/vtk.jar" "${pkgdir}/usr/share/java/vtk"
    
    mkdir -p "${pkgdir}/usr/lib/qt4/plugins/designer/"
    mv -v "${pkgdir}/usr/plugins/designer/libQVTKWidgetPlugin.so" "${pkgdir}/usr/lib/qt4/plugins/designer/libQVTKWidgetPlugin.so"    
    rm -r "${pkgdir}/usr/plugins/"
    
    cp -dpr --no-preserve=ownership -f "${srcdir}/VTKData/Data"\
        "${pkgdir}/usr/share/vtk/VTKData/Data"
    
    install -Dm644 "${srcdir}/VTK/Copyright.txt"\
        "${pkgdir}/usr/share/licenses/${pkgname}/Copyright.txt"
    
    install -Dm644 "${srcdir}/VTKData/Copyright.txt"\
        "${pkgdir}/usr/share/licenses/${pkgname}/Copyright-Data.txt"
    
    install -Dm755 "${srcdir}/$pkgname-build/vtk.sh"\
        "$pkgdir/etc/profile.d/vtk.sh"
    
    sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"\
        $(find "${pkgdir}" -type f)
}
