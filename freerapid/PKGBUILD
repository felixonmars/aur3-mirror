# Maintainer: Vojtech Kral <kral.vojtech at gmail>
# Contributor:  Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=freerapid
pkgver=0.85u1
_build=''
pkgrel=3
pkgdesc="A Java-based download manager for Rapidshare and other file sharing archives"
arch=('any')
url="http://wordrider.net/freerapid/"
license=('GPL')
depends=('java-runtime')
provides=('frd')
source=(http://files.kral.hk/frd/FreeRapid-${pkgver}${_build}.zip
        $pkgname.sh
        $pkgname.desktop)

sha256sums=('c38596786bfcd9e64a95d3de62209c63c43850b4b0177157cf93e7b8013e0655'
            'd165e2a4aa8dbf2e567589a657546f805998284d7939f20be75dab50139abd84'
            '5bbca23e1d4225f86aecc84d50ebe100c32b42522cb684a55feecab1ca21e13c')

# build()
# {
# }

package()
{
    cd $srcdir/FreeRapid-$pkgver$_build

    # Copy data and fix permissions
    install -d "$pkgdir/usr/share/$pkgname"
    cp -rf frd.jar lib/ lookandfeel/ plugins/ "$pkgdir/usr/share/$pkgname/"
    find "$pkgdir/usr/share/$pkgname" -type f -exec chmod 644 '{}' +

    # Install bin, icon and desktop file
    install -Dm 755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
    ln -sf "$pkgname" "$pkgdir/usr/bin/frd"
    install -Dm 644 frd.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
    install -Dm 644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
