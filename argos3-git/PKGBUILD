# Contributor: Victor Noel

pkgname=argos3-git
_gitname=argos3
pkgver=3.0.0.beta22.3.g8898f84
pkgrel=1
pkgdesc="A parallel, multi-engine simulator for heterogeneous swarm robotics"
url="http://iridia.ulb.ac.be/argos"
license=('MIT')
arch=('i686' 'x86_64')
makedepends=('cmake' 'lua51' 'git' 'asciidoc')
depends=('gsl' 'freeimage' 'qt4' 'freeglut' 'glu' 'mesa' 'shared-mime-info' 'lua51')
optdepends=('lua51: lua support')
provides=('argos3')
source=('git+https://github.com/ilpincy/argos3.git')
md5sums=('SKIP')
install=$pkgname.install

pkgver() {
  cd "$_gitname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$_gitname"

  rm -rf build
  mkdir build
  cd build

  # LUA_INCLUDE_DIR is needed because cmake fails to find the correct one
  cmake ../src/ -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DLUA_INCLUDE_DIR=/usr/include/lua5.1 -Wno-dev

  make
  # needed by make install
  make doc
}

package() {

  cd "$_gitname/build"

  make DESTDIR="$pkgdir" install

  cd "$pkgdir"

  mkdir -p usr/share/licenses/argos3-git
  mv usr/doc/argos3/licenses/* usr/share/licenses/argos3-git/
  rm -rf usr/doc/argos3/licenses/

  mkdir -p usr/share

  mv usr/doc usr/share/
  mv usr/man usr/share/
}
