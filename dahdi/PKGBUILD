# Contributor: Jonathan Liu <net147@gmail.com>
pkgname=dahdi
pkgver=2.6.1
_realpkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="DAHDI drivers for Asterisk"
arch=('i686' 'x86_64')
url="http://www.asterisk.org/"
license=('GPL2')
depends=('kernel26' 'libusb' 'perl')
makedepends=('kernel26-headers')
conflicts=('zaptel')
install="${pkgname}.install"
source=("http://downloads.asterisk.org/pub/telephony/dahdi-linux/releases/dahdi-linux-${_realpkgver}.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-oct6114-064-1.05.01.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-oct6114-128-1.05.01.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-oct6114-256-1.05.01.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-tc400m-MR6.12.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-te820-1.76.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-hx8-2.06.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-vpmoct032-1.12.0.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fwload-vpmadt032-1.25.0.tar.gz"
        "oslec-20110706.patch"
        "Makefile.patch")
md5sums=('75932fe3d4a6e656165aaaa7a1377305'
         '88db9b7a07d8392736171b1b3e6bcc66'
         'c1f1a18d3e20d283f42c71e580a64b5a'
         '4b3b763fc89f81d569a42da7c9ef0eee'
         '2ea860bb8a9d8ede2858b9557b74ee3c'
         '07b389e2a4f2a2513240187f1c295626'
         'a7f3886942bb3e9fed349a41b3390c9f'
         '2c7d5b17c23c34624158ee640818043b'
         '19a660c2073749641072ca644e983cbc'
         'cd9546cbdcb273a0d82f96dc07a8b33e'
         '5f27b1a1005d41bd74ba9bdfe88223bb')

build() {
  cd "${srcdir}"
  [ -d linux-stable-61d62b0 ] || patch -Np1 -i "${srcdir}/oslec-20110706.patch"

  cd "${srcdir}/dahdi-linux-${_realpkgver}"
  mkdir -p drivers/staging
  cp -a "${srcdir}/linux-stable-61d62b0" drivers/staging/echo
  ln -sf "${srcdir}"/dahdi-fw*.tar.gz "drivers/dahdi/firmware/"
  mv "${srcdir}"/dahdi-fw*.bin "drivers/dahdi/firmware/"
  patch -Np1 -i "${srcdir}/Makefile.patch"

  make DESTDIR="${pkgdir}" modules
}

package() {
  cd "${srcdir}/dahdi-linux-${_realpkgver}"
  make DYNFS=1 DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
