# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

_pkgbasename=freetype2-git-infinality
pkgname=lib32-${_pkgbasename}
pkgver=2.4.99.git20120524
pkgrel=1
_infinality_version=20120403
_infinality_release=02
pkgdesc="TrueType font rendering library (32-bit) with Infinality patch"
arch=(x86_64)
url="http://www.infinality.net/"
license=('GPL')
depends=('lib32-zlib' "${_pkgbasename}=${pkgver}")
makedepends=('gcc-multilib' 'git')
provides=("lib32-freetype2=$pkgver")
conflicts=('lib32-freetype2')
options=('!libtool')
source=(http://www.infinality.net/fedora/linux/zips/freetype-infinality-2.4.9-${_infinality_version}_${_infinality_release}-x86_64.tar.bz2
        freetype2-enable-valid.patch)
sha256sums=('8681e338b318f30fda4bf2ac44a0d4254987f855fb2b62a50030ec6c1080bb73'
            '97751d04014b7f2a42bce3ed99417eeb058563e2b7c94251f05f489666fec5bb')
_freetype_gitroot="git://git.sv.nongnu.org/freetype/freetype2.git"
_freetype_gitcommit="8be5d3ac0d220870e78ed4e635dc38cd3b17c569"

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}"

  # Get rid of old build directories
  rm -rf freetype2-build

  # Clone or update sources from freetype2 git repository
  if [ -d ${srcdir}/freetype2 ]; then
    msg "Updating Git repository"
    cd freetype2 && git pull
  else
    msg "Cloning Git repository"
    git clone ${_freetype_gitroot}
  fi

  msg "Preparing build directory"
  cp -r ${srcdir}/freetype2 ${srcdir}/freetype2-build

  cd "${srcdir}/freetype2-build"

  # Check out specific commit for repeatability
  git checkout --detach ${_freetype_gitcommit}

  # We need to generate the configure script BEFORE applying patches because
  # Infinality patches this script
  sh autogen.sh

  # Apply miscellaneous patches.
  patch -Np0 -i "${srcdir}/freetype2-enable-valid.patch"

  # Apply Infinality patches. See README file for correct order. Unfortunately,
  # the Infinality patches may not have the same version number, so we use
  # wildcards here.
  patch -Np1 -i "${srcdir}/freetype-add-subpixel-hinting-infinality-"*.patch
  patch -Np1 -i "${srcdir}/freetype-enable-subpixel-hinting-infinality-"*.patch
  patch -Np1 -i "${srcdir}/freetype-entire-infinality-patchset-"*.patch

  # Run configure script
  ./configure --prefix=/usr --libdir=/usr/lib32

  make
}

package() {
  cd "${srcdir}/freetype2-build"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

# vim: set ts=2 sw=2 ft=sh noet:
