# Maintainer: Martin Schmoelzer <mschmoelzer@gmail.com>

_pkgbasename=freetype2-git-infinality
pkgname=lib32-${_pkgbasename}
_gitcommit=bb18bf7d059f2b7272d05be685b267dea6648e23
pkgver=2.4.99.git20111115
pkgrel=1
pkgdesc="TrueType font rendering library (32-bit) with infinality patch"
arch=(x86_64)
url="http://freetype.sourceforge.net"
license=('GPL')
depends=('lib32-zlib' "${_pkgbasename}=${pkgver}")
makedepends=('gcc-multilib' 'git')
provides=("lib32-freetype2=$pkgver")
conflicts=('lib32-freetype2')
options=('!libtool')
source=(http://www.infinality.net/files/freetype-add-subpixel-hinting-infinality-20110604-1.patch
        http://www.infinality.net/files/freetype-enable-subpixel-hinting-infinality-20100909-1.patch
        http://www.infinality.net/files/freetype-entire-infinality-patchset-20110604-1.patch
        http://aoliynik-overlay.googlecode.com/svn-history/r361/trunk/media-libs/freetype/files/freetype-2.3.2-enable-valid.patch)
sha256sums=('eeb398a06787f5f6d8aa29f660ce26e74bd46b43904ebde7638b486578c8d7ed'
            '6090e599455eef159ff51cf653a3d2b3776e2b2e7a8e109b057ace409cf42c99'
            '9be1418c6b3452ef2b5429522573f2b31ae15bc97f102862cc55a7e40da31035'
            '3c26cd8b92510490b4bdbdd12b078e33a4f8607eaee64a800c3ea23097d5d43b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}"

  # Get sources from freetype2 git repository
  msg "Cloning freetype2 git repository..."
  git clone git://git.sv.nongnu.org/freetype/freetype2.git

  cd "${srcdir}/freetype2"

  # Check out specific commit for repeatability
  git checkout --detach ${_gitcommit}

  # Apply patches
  patch -Np1 -i "${srcdir}/freetype-add-subpixel-hinting-infinality-20110604-1.patch"
  patch -Np1 -i "${srcdir}/freetype-enable-subpixel-hinting-infinality-20100909-1.patch"
  patch -Np1 -i "${srcdir}/freetype-entire-infinality-patchset-20110604-1.patch"
  patch -Np1 -i "${srcdir}/freetype-2.3.2-enable-valid.patch"

  # Generate configure script
  sh autogen.sh

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/freetype2"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
