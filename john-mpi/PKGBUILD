#Contributor: MatToufoutu <mattoufootu[at]gmail[dot]com>
#Contributor: Jan Lieven jan[at]das<minus>labor(dot)org

pkgname=john-mpi
pkgver=1.7.9
pkgrel=4
_patchlevel=jumbo-6
arch=('i686' 'x86_64')
pkgdesc='JohnTheRipper password cracker with Jumbo patch and MPI support'
url='http://openwall.info'
license=('GPL')
provides=('john')
conflicts=('john' 'john-latest')
# For OpenCL support change swap the {make}depends arrays
depends=('mpich2')
makedepends=('mpich2')
# Remember to include one of the following packages into the depends
# array for OpenCL to work:
# amdstream
# opencl-nvidia
# intel-opencl-sdk
#depends=('mpich2'
#         'libopencl'
#         'YOUR_OPENCL_VENDOR_HERE')
#makedepends=('opencl-headers'
#             'libopencl')
backup=(etc/john/john.conf
        etc/john/dumb32.conf
        etc/john/dumb16.conf
        etc/john/dynamic.conf)

source=("http://www.openwall.com/john/g/john-${pkgver}-${_patchlevel}.tar.gz"
        "params.h.patch")

build() {
  export PATH=/opt/mpich2/bin:${PATH}

  cd ${srcdir}/john-${pkgver}-${_patchlevel}/src
  patch -p0 < ${srcdir}/params.h.patch

  sed -i 's|#CC = mpicc|CC = mpicc|g' Makefile                                                                                                                    
  sed -i 's|#MPIOBJ = john-mpi.o|MPIOBJ = john-mpi.o|g' Makefile

  if [ "$CARCH" == "x86_64" ]; then
   sed -i 's|CFLAGS = -c -Wall -O2|CFLAGS = -c -Wall -O2 -march=x86-64 -DJOHN_SYSTEMWIDE=1|' Makefile
   sed -i 's|^LDFLAGS =\(.*\)|LDFLAGS =\1 -lm|' Makefile
   sed -i -e 's|-m486||g' Makefile
    if [ $(which icc) -eq 0 ]; then
      make linux-x86-64-icc
    else
# For OpenCL replace the make target with linux-x86-64-opencl
      #make linux-x86-64-opencl
      make linux-x86-64
    fi
  else
    sed -i 's|CFLAGS = -c -Wall -O2|CFLAGS = -c -Wall -O2 -march=native -DJOHN_SYSTEMWIDE=1|' Makefile
# For OpenCL replace the make target with linux-x86-opencl
    #make linux-x86-opencl
    make linux-x86-sse2
  fi
}

package() {
	# config file
	sed -i 's|$JOHN|/usr/share/john|g' ${srcdir}/john-${pkgver}-${_patchlevel}/run/john.conf
  sed -i 's|/usr/share/john/dumb|/etc/john/dumb|g' ${srcdir}/john-${pkgver}-${_patchlevel}/run/john.conf
  sed -i 's|.include <dynamic.conf>|#.include <dynamic.conf>|g' ${srcdir}/john-${pkgver}-${_patchlevel}/run/john.conf
  sed -i 's|.include "/usr/share/john/john.local.conf"|#.include "/usr/share/john/john.local.conf"|g' ${srcdir}/john-${pkgver}-${_patchlevel}/run/john.conf
  install -d ${pkgdir}/etc/john
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/run/*.conf ${pkgdir}/etc/john/
	
	# docs
	install -d ${pkgdir}/usr/share/doc/john
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/doc/* ${pkgdir}/usr/share/doc/john/
	install -d ${pkgdir}/usr/share/john/
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/run/*.chr ${pkgdir}/usr/share/john/
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/run/password.lst ${pkgdir}/usr/share/john/
	install -Dm644 ${srcdir}/john-${pkgver}-${_patchlevel}/doc/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE

	# install password list and charset files
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/run/{{all,alnum,alpha,digits,lanman}.chr,password.lst} \
          ${pkgdir}/usr/share/john/

	# install binaries
  cd ${srcdir}/john-${pkgver}-${_patchlevel}/run/
  for i in $(find . -type f -perm 755); do
    install -Dm755 ${i} ${pkgdir}/usr/bin/${i}
  done
	cd ${pkgdir}/usr/bin
	ln -s john unafs
	ln -s john unique
	ln -s john unshadow
	ln -s john undrop
}

sha512sums=('aeee8e6bdebaad7d0a78daccc359a0d948080959e22ae759f55bb37599a570ae1e007fe5b982521f6d54363fdacb9ae43350586f4229b2b1b20a886c51944ec6'
            'b50e0961a75b57c487046ac6aca0a3149b81a733deb305bcb1e34169e0f37ddd8f11f7ef8dcb04a1d2bc5e689b32298f1b47ce87eef29942a958fb438772465d')
