# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=dart-editor
_pkgname=DartEditor
pkgver=0.1.0.8641
pkgrel=1
pkgdesc="Editor for the Dart programming language."
arch=(i686 x86_64)
url="http://www.dartlang.org/docs/getting-started/editor/index-linux.html"
license=(EPL)
depends=('java-runtime>=6' 'gtk2' 'desktop-file-utils')
backup=(opt/${pkgname}/${_pkgname}.ini)
changelog=Changelog
if [[ ${CARCH} = x86_64 ]];
  then _arch=x86_64; md5sums=('2c1466c585a33274ddc808d604b2e96e')
  else _arch=x86;    md5sums=('8d79f664a5360b4310e9a826493a4f50')
fi
source=(http://gsdview.appspot.com/dart-editor-archive-integration/${pkgver##*.}/${pkgname}-linux.gtk.${_arch}.zip)

build() {
  msg2 "Generate desktop application entry..."
  cat > "${srcdir}"/${pkgname}.desktop << EOF
[Desktop Entry]
Version=${pkgver}
Encoding=UTF-8
Name=Dart editor
Comment=${pkgdesc}
Exec=/usr/bin/${pkgname}
Icon=/opt/${pkgname}/icon.xpm
Terminal=false
Type=Application
Categories=Development;
EOF
}

package() {
  msg2 "Install the assembly at /opt/${pkgname}..."
  install -dm755           "${pkgdir}"/opt/${pkgname}
  cp -a "${srcdir}"/dart/* "${pkgdir}"/opt/${pkgname}

  msg2 "Install links to the executables in /usr/bin..."
  install -dm755                    "${pkgdir}"/usr/bin
  ln -s /opt/${pkgname}/${_pkgname} "${pkgdir}"/usr/bin/${pkgname}

  msg2 "Install links to the documentation resources in /usr/share/doc/${pkgname}..."
  install -dm755                    "${pkgdir}"/usr/share/doc/${pkgname}
  ln -s /opt/${pkgname}/samples     "${pkgdir}"/usr/share/doc/${pkgname}/
  ln -s /opt/${pkgname}/about.html  "${pkgdir}"/usr/share/doc/${pkgname}/
  ln -s /opt/${pkgname}/about_files "${pkgdir}"/usr/share/doc/${pkgname}/

  msg2 "Install link to the config file in /etc..."
  install -dm755                        "${pkgdir}"/etc
  ln -s /opt/${pkgname}/${_pkgname}.ini "${pkgdir}"/etc/${pkgname}.ini

  msg2 "Install desktop application entry in /usr/share/applications..."
  install -Dm644 "${srcdir}"/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
}
