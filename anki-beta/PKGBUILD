# Maintainer: Limao Luo <luolimao@gmail.com>

pkgname=anki-beta
pkgver=2.0
_pkgrel=rc1
pkgrel=${_pkgrel}_1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. 2.0 beta version"
arch=(i686 x86_64)
url='http://ankisrs.net/'
license=(AGPL3)
depends=(python2-pyqt mplayer portaudio)
provides=(anki=$pkgver)
conflicts=(anki ankiqt-git)
install=anki.install
source=(http://ankisrs.net/download/mirror/beta/anki-$pkgver-$_pkgrel.tgz)
sha256sums=('00e3dc331305df00b22c164b1e9ae36f706c949706eb58ae9437bdf8847c28ba')
sha512sums=('2d86d907b591c05f9edaf22385ddbb38a3166627ee72c697e2ad22bd07e932411f4a5e4be39c85486b8c812a8b53ff0327875e976aa6da46e182d296a171b2a7')

build() {
    cd "$srcdir"/anki-$pkgver/

    # remove unneeded python libraries
    if [[ $(perl -e '($x,$y)=@ARGV; print $x cmp $y' $(python2 --version) "Python 2.7") == -1 ]]; then
	_not_python_version="2.7"
    else
	_not_python_version="2.6"
    fi
    if [[ $CARCH == "x86_64" ]]; then
	_not_arch="32"
    else
	_not_arch="64"
    fi
    rm -rf libanki/thirdparty/py{$_not_python_version*,*$_not_arch}/
}

package() {
    cd "$srcdir"/anki-$pkgver/
    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir="$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1

    rm anki{,.xpm,.png,.desktop,.1}
    
    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
