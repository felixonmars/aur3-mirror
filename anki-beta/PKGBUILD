# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=anki-beta
_pkgver=2.0
pkgver=$_pkgver.0
pkgrel=1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. 2.0 beta version"
arch=(any)
url=http://ankisrs.net
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyaudio python2-pyqt)
provides=(anki=$pkgver.$_pkgrel)
conflicts=(anki ankiqt-git)
install=anki.install
source=($url/download/mirror/anki-$pkgver.tgz)
sha256sums=('fef0920e76014d59db064843380592a022d45f1cd6e918ec5b4843db26b64ab4')
sha512sums=('b39e7d950b063418e7864fc12a0a35e2e2f88d7894ae6f1d9943148d138b0e40cccd082e488ea3381368f10acbab969d7c58521c80d4ffbadd9dc525339b353b')

package() {
    cd "$srcdir"/anki-$_pkgver/

    # remove unneeded libraries
    rm -rf libanki/thirdparty/ tools/

    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm anki{,.xpm,.png,.desktop,.1,.xml} LICENSE.logo {libanki,.}/{LICENSE,ChangeLog.old} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
