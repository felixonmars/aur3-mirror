# Maintainer: Limao Luo <luolimao@gmail.com>

pkgname=anki-beta
pkgver=2.0
_pkgrel=beta19
pkgrel=${_pkgrel}_1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. 2.0 beta version"
url='http://ankisrs.net/'
arch=(i686 x86_64)
license=(AGPL3)
depends=(python2-pyqt mplayer portaudio)
conflicts=(anki ankiqt-git)
provides=("anki=$pkgver")
source=(http://ankisrs.net/download/mirror/beta/anki-$pkgver-$_pkgrel.tgz)
sha256sums=('89aa8fe354780c477cebe622a6a45f984606a50a2b42ed0aba969b5af41458ab')
sha512sums=('c451ab3faff19f4f869ab195bbd6e05a78547d8161e8097473bb304286850ec8e50e2a8f1c1dc395ac76648cc441c220ba0f3227d2d1540dd685ad0494deb6c9')
install=anki.install

build() {
    cd $srcdir/anki-$pkgver/

    # remove unneeded python libraries
    if [ $(perl -e '($x,$y)=@ARGV; print $x cmp $y' $(python2 --version) "Python 2.7") == -1 ]; then
	_not_python_version="2.7"
    else
	_not_python_version="2.6"
    fi
    if [ $CARCH == "x86_64" ]; then
	_not_arch="32"
    else
	_not_arch="64"
    fi
    rm -rf libanki/thirdparty/py{$_not_python_version*,*$_not_arch}/
}

package() {
    cd $srcdir/anki-$pkgver/
    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki $pkgdir/usr/bin/anki
    install -Dm644 anki.xpm $pkgdir/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png $pkgdir/usr/share/pixmaps/anki.png
    sed -i -e 's:KDE;:&Qt;:' -e 's:x-anki:&;:' anki.desktop
    desktop-file-install anki.desktop --dir=$pkgdir/usr/share/applications/
    install -Dm644 anki.1 $pkgdir/usr/share/man/man1/anki.1

    rm anki{,.xpm,.png,.desktop,.1}
    
    install -d $pkgdir/usr/share/anki/
    cp -a * $pkgdir/usr/share/anki/
}
