# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=anki-beta
_pkgver=2.0
pkgver=$_pkgver.1
pkgrel=1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. 2.0 beta version"
arch=(any)
url=http://ankisrs.net
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyqt)
optdepends=('python2-pyaudio: to record audio files')
provides=(anki=$pkgver)
conflicts=(anki ankiqt-git)
install=anki.install
source=($url/download/mirror/anki-$pkgver.tgz)
sha256sums=('32442708db8b918e9f3753dbd3cb6062b8b878f37c0d176322c7950bc2e35812')
sha512sums=('191d21f17adfa41970023838c27edfa9ef8e301cecbfc02387de581baa7dac341da53f2d2630976eb864c478da9a39989afd3fe53291ae93080328e484ec75ca')

package() {
    cd "$srcdir"/anki-$_pkgver/

    # remove unneeded libraries
    rm -rf libanki/thirdparty/ tools/

    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm anki{,.xpm,.png,.desktop,.1,.xml} LICENSE.logo {libanki,.}/{LICENSE,ChangeLog.old} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
