# Maintainer: Limao Luo <luolimao@gmail.com>

pkgname=anki-beta
pkgver=2.0
_pkgrel=rc4
pkgrel=${_pkgrel}_1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. 2.0 beta version"
arch=(any)
url='http://ankisrs.net/'
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyaudio python2-pyqt)
provides=(anki=$pkgver)
conflicts=(anki ankiqt-git)
install=anki.install
source=(http://ankisrs.net/download/mirror/beta/anki-$pkgver-$_pkgrel.tgz)
sha256sums=('37904c3e6fd247f7a24e9f1c6f57daf641be7fce14db829e42da32bbf93bf87e')
sha512sums=('f58aa9d90c81ecc2be9d8468e87406b2487a469db86a24839a179cefc788339f358b07c782bbca0c4ad46e8216889eb45164efbe27f031734bc39c19fe944c8c')

package() {
    cd "$srcdir"/anki-$pkgver/

    # remove unneeded libraries
    rm -rf libanki/thirdparty/

    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir="$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm anki{,.xpm,.png,.desktop,.1} LICENSE.logo {libanki,.}/{LICENSE,ChangeLog.old,README*} Makefile
    find -name '*.bat' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
