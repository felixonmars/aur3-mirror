# Maintainer: xzy3186 <xzy3186(at)gmail>
# Contributors: Bob Fanger <bfanger(at)gmail>, Filip <fila pruda com>, Det <nimetonmaili(at)gmail>
# Based on [community-testing]'s r8168: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/r8168

pkgname=r8168-all
_basename=r8168
pkgver=8.034.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards (for all kernels)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux' 'linux-headers')
conflicts=('r8168' 'r8168-ck' 'r8168-pf')
#makedepends=('linux-headers')
source=(https://r8168.googlecode.com/files/$_basename-$pkgver.tar.bz2)
install=$_basename.install
sha256sums=('97f3f342f9f24a3c3346641babde82f52f87b6ef5976c8f4481515f247116206')

_MODULEDIRS=`find /usr/lib/modules -name version | sed 's|/usr/lib/modules/||; s|/version||'`
_KERNELS=`file /boot/vmlinuz* | grep -v symbolic | sed 's/.*version \([^ ]\+\).*/\1/'`

build() {
  # Loop through all detected kernels
  for _kernver in $_KERNELS; do
    cd "$srcdir/$_basename-$pkgver"
    cp -r src src-$_kernver
    cd src-$_kernver
    msg2 "Building module for $_kernver"
    #make clean BASEDIR=/lib/modules/$_kernver modules
    make -C /usr/lib/modules/$_kernver/build \
       SUBDIRS="$srcdir/$_basename-$pkgver/src-$_kernver" \
       EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
       modules
  done
}

package() {
  # Loop through all detected kernels
  for _kernver in $_KERNELS; do
    cd "$srcdir/$_basename-$pkgver"
    # Loop through all detected extramodules directories
    for _extramodules in $_MODULEDIRS; do
      # Check which extramodules directory corresponds with the built module
      if [ `cat "/usr/lib/modules/$_extramodules/version"` = $_kernver ]; then
        install -Dm644 src-$_kernver/$_basename.ko "$pkgdir/usr/lib/modules/$_extramodules/$_basename.ko"
      fi
    done
  done
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
