# Maintainer: Aurelien Cedeyn <aurelien.cedeyn@gmail.com>
pkgbase="clustershell"
pkgname="clustershell"
true && pkgname=(clustershell vim-clustershell)
pkgver=1.6
pkgrel=1
pkgdesc="Python framework for efficient cluster administration"
arch=(any)
url="http://clustershell.sourceforge.net"
license=('custom:CeCILL')
depends=('python2')
makedepends=('python2' 'setuptools')
#source=(http://sourceforge.net/projects/clustershell/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz/download)
source=(https://github.com/downloads/cea-hpc/$pkgname/$pkgname-$pkgver.tar.gz)
#md5sums=('027506d825ecc4b361f49d0742e63332')
md5sums=('68278fb07ab52f2b96e2f00d8fe07601')
changelog=clustershell.changelog

build() {
	echo "Directory: ${srcdir}/${pkgname}-${pkgver}"
	cd ${srcdir}/${pkgname}-${pkgver}
	python2 setup.py build
}
package_clustershell(){
	optdepends=('openssh: Secure SHell client to connect to distant machines')
	cd ${srcdir}/${pkgname}-${pkgver}
	python2 setup.py install -O1 --skip-build --root="${pkgdir}"
	# config files
	install -d ${pkgdir}/etc/clustershell
	install -p -m 0644 conf/*.conf ${pkgdir}/etc/clustershell/

	# man pages
	install -d ${pkgdir}/usr/share/man/{man1,man5}
	install -p -m 0644 doc/man/man1/clubak.1 ${pkgdir}/usr/share/man/man1/
	install -p -m 0644 doc/man/man1/clush.1 ${pkgdir}/usr/share/man/man1/
	install -p -m 0644 doc/man/man1/nodeset.1 ${pkgdir}/usr/share/man/man1/
	install -p -m 0644 doc/man/man5/clush.conf.5 ${pkgdir}/usr/share/man/man5/
	install -p -m 0644 doc/man/man5/groups.conf.5 ${pkgdir}/usr/share/man/man5/
	install -D -m644 Licence_CeCILL-C_V1-en.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_vim-clustershell(){
	depends=(vim)
	local vimdatadir=/usr/share/vim/vimfiles
	cd ${srcdir}/${pkgname}-${pkgver}
	# vim addons
	install -d ${pkgdir}/$vimdatadir/{ftdetect,syntax}
	install -p -m 0644 doc/extras/vim/ftdetect/clustershell.vim ${pkgdir}/${vimdatadir}/ftdetect/
	install -p -m 0644 doc/extras/vim/syntax/clushconf.vim ${pkgdir}/${vimdatadir}/syntax/
	install -p -m 0644 doc/extras/vim/syntax/groupsconf.vim ${pkgdir}/${vimdatadir}/syntax/
}

