# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=gtk3
pkgname=lib32-$_pkgbasename
pkgver=3.0.11
pkgrel=2
pkgdesc="The GTK+ Toolkit (v3) (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gtk.org/"
depends=('lib32-atk' 'lib32-gdk-pixbuf2' 'lib32-gnutls'
	 'lib32-libxcomposite' 'lib32-libcups' 'lib32-libxcursor'
	 'lib32-libxdamage' 'lib32-libxi' 'lib32-libxinerama'
	 'lib32-libxrandr' 'lib32-pango'  'lib32-krb5' "$_pkgbasename")
makedepends=('gcc-multilib')
options=('!libtool' '!docs')
install=gtk3.install
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.0/gtk+-${pkgver}.tar.bz2)
sha256sums=('852d848e2e51373daf04446dc7b427f91bf6953632b45fbe173fb5ed0694100f')

build() {  
  cd "${srcdir}/gtk+-${pkgver}"
  
  CC="gcc -m32" CXX=/bin/false \
  PKG_CONFIG_PATH="/usr/lib32/pkgconfig" \
  ./configure --prefix=/usr \
  	      --sysconfdir=/etc \
  	      --localstatedir=/var \
  	      --libdir=/usr/lib32 \
  	      --enable-gtk2-dependency \
  	      --disable-schemas-compile \
  	      --enable-introspection=no
  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}/usr/bin/gtk-query-immodules-3.0{,-32}
  rm -rf ${pkgdir}/usr/{include,share,bin/gtk3-demo} ${pkgdir}/etc
}
