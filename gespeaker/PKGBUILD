# Maintainer: Muflone http://url.muflone.com/contacts
# Contributor: archtux <antonio dot arias99999 at gmail dot com>

pkgname=gespeaker
pkgver=0.8.2
pkgrel=2
pkgdesc="A GTK+ frontend for espeak and mbrola to speech the read text."
url="http://url.muflone.com/gespeaker"
arch=('any')
license=('GPL2')
depends=('espeak' 'librsvg' 'pygtk' 'python-dbus-common' 'python2-xdg' 'alsa-utils')
optdepends=('mbrola: for enhanced mbrola voices support'
            'mbrola-voices: at least one mbrola voice')
provides=('gespeaker')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/muflone/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('12c74ab7858c818d0ea12cf0ad4a4fd8')

prepare() {
  cd "${pkgname}-${pkgver}"
  # Python2 fix
  sed -i 's#env python#env python2#' setup.py
  sed -i 's#env python#env python2#' gespeaker
  sed -i 's#env python#env python2#' src/gespeaker.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --optimize=1 --root "${pkgdir}"
}

