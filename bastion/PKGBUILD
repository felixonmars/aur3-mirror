#Maintainer: Daniel Wallace < danielwallace at gtmanfred dot com>
pkgname=bastion
pkgver=20120621
_pkgver=2012-06-20
pkgrel=4
pkgdesc="An original action role-playing game set in a lush imaginative world, in which players must create and fight for civilizationâ€™s last refuge as a mysterious narrator marks their every move."
url="http://supergiantgames.com/?page_id=242"
license=('custom')
arch=('i686' 'x86_64')
groups=("humble-indie-bundleV" "games")
depends=('libxft')
makedepends=('curl')
source=("Bastion-HIB-${_pkgver}.sh::http://www.humblebundle.com/downloads?key=${_humblebundleVkey}" "$pkgname.desktop" "mesa$pkgname" "$pkgname.install")
install=$pkgname.install
PKGEXT=".pkg.tar"
case $CARCH in
    i686) _arch=x86 ;;
    x86_64) _arch=x86_64;;
esac
DLAGENTS=("http::/usr/bin/curl -sL %u | grep Bastion-HIB| grep -o -E \"data-web=[^ ]+\" | sed -e \"s/data-web='\(.*\)'/url = \1/\" > /tmp/url && /usr/bin/curl -fLC -  --retry 3 --retry-delay 3 -o %o -K /tmp/url && rm /tmp/url")

package() {
    cd "$srcdir"
    [[ -d "$srcdir"/"$pkgname-$_pkgver" ]] && rm -r "$srcdir"/"$pkgname-$_pkgver"
    sh "${source[0]%%:*}" -u --packager pacman --target "$srcdir"/"$pkgname-$_pkgver" --bindir "$pkgdir"/opt/games/Bastion --datadir "$pkgdir"/opt/games

	find "${pkgdir}" -type f -exec chmod 644 "{}" +
	install -Dm644 "${pkgname}".desktop "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
	install -Dm755 "mesa${pkgname}" "${pkgdir}"/usr/bin/"${pkgname}"mesa
	install -Dm644 "${pkgdir}"/opt/games/Bastion/Bastion.png "${pkgdir}"/usr/share/icons/"${pkgname}".png

	chmod 755 "${pkgdir}"/opt/games/Bastion/Bastion.bin."$_arch"
	ln -s "/opt/games/Bastion/Bastion.bin.$_arch" "${pkgdir}"/usr/bin/"${pkgname}"
}


md5sums=('aa6ccaead3b4b8a5fbd156f4019e8c8b'
         '0facce79c6d93c5914a58e096eca8c92'
         'a42e39c5f52abf19b6975cb79f732907'
         '234862600b3d8792b95eedeae28577b7')
