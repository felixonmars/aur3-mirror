# Contributor: zoulnix <http://goo.gl/HQaP>
pkgname=tomoe
pkgver=0.6.0
pkgrel=7
pkgdesc="A handwriting recognition engine."
arch=('i686' 'x86_64')
url="http://tomoe.sourceforge.jp/"
license=('LGPL')
depends=('libmysqlclient' 'perl' 'pygtk')
makedepends=('gcc' 'intltool' 'make' 'patch' 'pkg-config')
options=('!libtool')
backup=('etc/tomoe/config')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz \
	${pkgname}-${pkgver}-bz502662.patch \
	${pkgname}-${pkgver}-ldflags.diff)
md5sums=('0e51e88d097085e101bf722fc04808ed' 'dcc4170655783becb2d427a0ad79e435' \
	 '6cb37bf86b11edc34bd3fcbf25396f5f')

build() { 
  cd ${srcdir}/${pkgname}-${pkgver}

  # apply patches...
  patch -Np1 -i ../${pkgname}-${pkgver}-bz502662.patch || return 1
  patch -Np1 -i ../${pkgname}-${pkgver}-ldflags.diff || return 1

  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var \
	      --disable-static

  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install || return 1
}
