# Maintainer: birdflesh <antkoul at gmail dot com>

pkgname=kcm-about-distro
_pkgname=about-distro
pkgver=1.0.0
pkgrel=2
pkgdesc="KCM displaying distribution and system information"
arch=('i686' 'x86_64')
url='https://projects.kde.org/about-distro'
license=('GPL3')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/$_pkgname/$pkgver/src/$_pkgname-$pkgver.tar.xz"
        "kcm-about-distrorc"
        "arch-logo.png")
md5sums=('7ae962af0ffcb7dd2715d96dea3e17c3'
         'f2cd5b00b092866b71dacb434cdb14f1'
         '75f412b722b01d8911a5fb16c4ed3766')

build() {
  cd "$srcdir"
  
  # Remove release date, taken from msx's PKGBUILD on ccr
  sed -i 's/%1\ %2/%1/g' $_pkgname-$pkgver/src/Module.cpp
  
  mkdir build
  cd build
  cmake ../$_pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/kcm-about-distrorc" "$pkgdir/usr/share/config/kcm-about-distrorc"
  install -Dm644 "$srcdir/arch-logo.png" "$pkgdir/usr/share/apps/$_pkgname/arch-logo.png"
}
