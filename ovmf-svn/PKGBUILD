# Maintainer: Andre "Osku" Schmidt <andre.osku.schmidt@gmail.com>

pkgname=ovmf-svn
pkgver=13845
pkgrel=1
pkgdesc="UEFI firmware/shell for qemu etc."
arch=('x86_64')
url="http://sourceforge.net/apps/mediawiki/tianocore/index.php?title=OVMF"
license=('BSD')
conflicts=('ovmf-bin')
makedepends=('subversion' 'iasl')
install=$pkgname.install

_svntrunk="https://edk2.svn.sourceforge.net/svnroot/edk2/trunk"
_svnmod=edk2

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD HERE
  #
  mkdir "$srcdir/$_svnmod-build"/lolpython-path
  ln -sf /usr/bin/python2 "$srcdir/$_svnmod-build"/lolpython-path/python;
  export PATH="$srcdir/$_svnmod-build"/lolpython-path:$PATH
  make -C edk2/BaseTools
  cd edk2
  export WORKSPACE="$srcdir/$_svnmod-build"/edk2
  export EDK_TOOLS_PATH="$WORKSPACE"/BaseTools
  . edksetup.sh BaseTools
  sed -i 's/^ACTIVE_PLATFORM\s*=.*/ACTIVE_PLATFORM = OvmfPkg\/OvmfPkgX64\.dsc/g' Conf/target.txt
  sed -i 's/^TARGET\s*=.*/TARGET = RELEASE/g' Conf/target.txt
  sed -i 's/^TARGET_ARCH\s*=.*/TARGET_ARCH = X64/g' Conf/target.txt
  sed -i 's/^TOOL_CHAIN_TAG\s*=.*/TOOL_CHAIN_TAG = GCC46/g' Conf/target.txt
  sed -i 's/-melf_x86_64//g' Conf/tools_def.txt
  ./BaseTools/BinWrappers/PosixLike/build
}

package() {
  cd "$srcdir/$_svnmod-build"
  install -Dm644 edk2/Build/OvmfX64/RELEASE_GCC46/FV/OVMF.fd "$pkgdir"/usr/share/ovmf/bios.bin
}

# vim:set ts=2 sw=2 et:
