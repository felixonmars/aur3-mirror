# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=curl-git
pkgver=20120419
pkgrel=1
pkgdesc="Curl is a tool and libcurl is a library for transferring data with URL syntax."
arch=('i686' 'x86_64')
url="http://curl.haxx.se/"
license=('MIT')
depends=('zlib' 'libssh2' 'openssl' 'libidn' 'bash' 'ca-certificates')
makedepends=('git')
provides=('curl=999')
conflicts=('curl')
options=('!libtool')
source=('curlbuild.h')
md5sums=('751bd433ede935c8fae727377625a8ae')

[[ $CARCH = "x86_64" ]] && _curlbuild=curlbuild-64.h
[[ $CARCH = "i686" ]] && _curlbuild=curlbuild-32.h

_gitroot="git://github.com/bagder/curl.git"
_gitname="curl"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -a "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./buildconf
  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-ldap{,s} \
      --enable-ipv6 \
      --enable-manual \
      --enable-threaded-resolver \
      --enable-versioned-symbols \
      --with-ca-path=/etc/ssl/ca-certificates.crt \
      --with-libidn \
      --with-random=/dev/urandom
  make

}

package() {
  cd "$_gitname-build"

  make DESTDIR="$pkgdir" install

  install -Dm644 docs/libcurl/libcurl.m4 $pkgdir/usr/share/aclocal/libcurl.m4
  mv "$pkgdir/usr/include/curl/curlbuild.h" "$pkgdir/usr/include/curl/$_curlbuild"
  install -m 644 $srcdir/curlbuild.h "$pkgdir/usr/include/curl/curlbuild.h"

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
