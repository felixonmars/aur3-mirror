#Maintainer: TingPing tingping@tingping.se
pkgname=hexchat
pkgver=2.9.4
pkgrel=2
pkgdesc='A GTK+ based IRC client'
arch=('i686' 'x86_64')
url='http://www.hexchat.org/'
license=('GPL')
options=('!libtool')
conflicts=()
depends=('gtk2' 'openssl' 'dbus-glib' 'hicolor-icon-theme' 'libnotify' 'libproxy')
makedepends=('perl' 'python2' 'tcl')
optdepends=('enchant: for spell check'
            'perl: for perl plugin'
            'tcl: for tcl plugin'
            'python2: for python plugin')
install="$pkgname.install"
source=("https://github.com/downloads/hexchat/hexchat/hexchat-$pkgver.tar.xz")
md5sums=('5609cac7200fc91be57196501aaf07fa')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  chmod +x autogen.sh
  ./autogen.sh
  ./configure --prefix=/usr \
    --enable-openssl --enable-dbus --enable-nls --enable-ipv6 --enable-shm --enable-spell=static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m644 "share/icons/hexchat.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/hexchat.svg"
}
