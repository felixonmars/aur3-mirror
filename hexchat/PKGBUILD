#Maintainer: TingPing tingping@tingping.se
pkgname=hexchat
pkgver=2.9.0
pkgrel=3
pkgdesc='A GTK+ based IRC client'
arch=('i686' 'x86_64')
url='http://www.hexchat.org/'
license=('GPL')
options=('!libtool')
conflicts=('hexchat-git')
depends=('gtk2' 'openssl' 'dbus-glib' 'libnotify' 'libproxy')
makedepends=('perl' 'python2' 'tcl')
optdepends=('enchant: for spell check'
            'perl: for perl plugin'
            'tcl: for tcl plugin'
            'python2: for python plugin')
install="$pkgname.install"
provides=('hexchat')
source=("https://github.com/downloads/hexchat/hexchat/hexchat-$pkgver.tar.xz")
md5sums=('7f6a29f4f55bff71913bdf590aa142c7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  chmod +x autogen.sh
  ./autogen.sh
  LIBS+="-lgmodule-2.0" ./configure --prefix=/usr --sysconfdir=/etc --datadir=/usr/share \
    --enable-openssl --enable-dbus --enable-nls --enable-ipv6 --enable-shm \
    --enable-spell=static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
