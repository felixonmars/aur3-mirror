# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=imagemagick-svn  
pkgver=7715
pkgrel=1 
pkgdesc="An image viewing/manipulation program"
url="http://www.imagemagick.org/"
arch=('i686' 'x86_64')
license=('custom')
depends=('djvulibre' 'lcms' 'libtool' 'xz' 'bzip2')
makedepends=('ghostscript' 'openexr' 'libwmf' 'librsvg' 'libxml2' 'jasper' 'libpng')
optdepends=('ghostscript: for Ghostscript support' 'openexr: for OpenEXR support' 'libwmf: for WMF support' 'librsvg: for SVG support' 'libxml2: for XML support' 'jasper: for JPEG-2000 support' 'libjpeg-turbo: for jpeg support' 'libpng: for PNG support')
conflicts=('imagemagick')
provides=('imagemagick' 'imagemagick-doc')
options=('!makeflags' 'libtool')
source=(libpng_mmx_patch_x86_64.patch perlmagick.rpath.patch)
md5sums=('069980fc2590c02aed86420996259302'
         'ff9974decbfe9846f8e347239d87e4eb')
backup=('etc/ImageMagick/coder.xml'
  'etc/ImageMagick/colors.xml'
  'etc/ImageMagick/delegates.xml'
  'etc/ImageMagick/log.xml'
  'etc/ImageMagick/magic.xml'
  'etc/ImageMagick/mime.xml'
  'etc/ImageMagick/policy.xml'
  'etc/ImageMagick/thresholds.xml'
  'etc/ImageMagick/type.xml'
  'etc/ImageMagick/type-dejavu.xml'
  'etc/ImageMagick/type-ghostscript.xml'
  'etc/ImageMagick/type-windows.xml')

_svntrunk=https://www.imagemagick.org/subversion/ImageMagick/trunk 
_svnmod=ImageMagick

build() {
  cd ${srcdir}

  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  [ -d ${_svnmod}-build ] && rm -rf ${_svnmod}-build
  cp -r ${_svnmod} ${_svnmod}-build

  cd ${_svnmod}-build
  if [ "${CARCH}" = 'x86_64' ]; then
    patch -Np1 < ../libpng_mmx_patch_x86_64.patch
  fi

  patch -p0 < ../perlmagick.rpath.patch
  ./configure --prefix=/usr --sysconfdir=/etc --with-modules --disable-static \
    --enable-openmp --with-x --with-wmf --with-openexr --with-xml \
    --with-gslib --with-gs-font-dir=/usr/share/fonts/Type1 \
    --with-perl --with-perl-options="INSTALLDIRS=vendor" \
    --without-gvc --with-djvu --without-autotrace --with-jp2 \
    --with-jbig --without-fpx --without-dps --without-fftw
  make
}

package() {
  cd ${_svnmod}-build
  make DESTDIR=$pkgdir install
  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 NOTICE "${pkgdir}/usr/share/licenses/$pkgname/NOTICE"
}
