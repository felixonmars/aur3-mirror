# Puppet: Installer: Arch
# Maintainer: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>

pkgname=puppet
pkgver=2.6.5
pkgrel=2
pkgdesc="A system for automating system administration tasks."
arch=("any")
url="http://puppetlabs.com/projects/puppet/"
license=("GPL")
depends=("ruby1.8" "ruby1.8-shadow" "facter")
options=(emptydirs)
install="puppet.install"
source=("http://puppetlabs.com/downloads/puppet/$pkgname-$pkgver.tar.gz"
        "puppet"
        "puppetmaster"
        "puppet.conf"
        "pacman.rb")
md5sums=('645543a18050f1d991796d865c53f9a4'
         '59dbf39e251bc4877e7604a5876c642d'
         '4c507c580cb28d7a5adc9ea6b3626657'
         'a6c0ce2eb12d62fdd3421aa9dff3477d'
         '2dd55ee7d43a9bb3388edc3249f28fc2')

package() {
  cd $srcdir/$pkgname-$pkgver

  # Install
  ruby-1.8 ./install.rb \
    --destdir=$pkgdir \
    --sitelibdir=/opt/ruby1.8/lib/ruby/1.8/ \
    --bindir=/usr/bin \
    --sbindir=/usr/sbin \
    --mandir=/usr/share/man

  # Set up vim and emacs
  install -Dp -m0644 ext/vim/ftdetect/puppet.vim $pkgdir/usr/share/vim/vimfiles/ftdetect/puppet.vim
  install -Dp -m0644 ext/vim/syntax/puppet.vim $pkgdir/usr/share/vim/vimfiles/syntax/puppet.vim
  install -Dp -m0644 ext/emacs/puppet-mode.el $pkgdir/usr/share/emacs/site-lisp/puppet-mode.el
  install -Dp -m0644 ext/emacs/puppet-mode-init.el $pkgdir/usr/share/emacs/site-lisp/site-start.d/puppet-mode-init.el
  
  # Configuration and rc scripts
  install -d $pkgdir/etc/{puppet,rc.d}
  install -m 644 $srcdir/puppet.conf $pkgdir/etc/puppet/puppet.conf

  install -D $srcdir/puppet $pkgdir/etc/rc.d/puppet
  install -D $srcdir/puppetmaster $pkgdir/etc/rc.d/puppetmaster

  # Pacman Provider
  install -m 644 $srcdir/pacman.rb $pkgdir/opt/ruby1.8/lib/ruby/1.8/puppet/provider/package/pacman.rb

  # Move pi to prevent file conflict
  mv $pkgdir/usr/bin/pi $pkgdir/usr/bin/puppet-pi

  # Ensure that the shebang lines are correct
  find $pkgdir/usr/bin -type f -exec sed -i 's,^#!/usr/bin/ruby,#!/usr/bin/ruby-1.8,' {} \;
}
