# Puppet: Installer: Arch
# Maintainer: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>

pkgname=puppet
pkgver=2.6.4
pkgrel=3
pkgdesc="A system for automating system administration tasks."
arch=("any")
url="http://puppetlabs.com/projects/puppet/"
license=("GPL")
depends=("ruby1.8" "ruby-shadow" "facter")
options=(emptydirs)
install="puppet.install"
source=("http://puppetlabs.com/downloads/puppet/$pkgname-$pkgver.tar.gz"
        "puppet"
        "puppetmaster"
        "puppet.conf"
        "pacman.rb")
md5sums=('c0b86f40c8000dae2f0f7f2f91579c2a'
         '59dbf39e251bc4877e7604a5876c642d'
         '4c507c580cb28d7a5adc9ea6b3626657'
         'a6c0ce2eb12d62fdd3421aa9dff3477d'
         'ea391561b25eba7c52b042d5be88f6ce')

package() {
  cd $srcdir/$pkgname-$pkgver

  # Install
  ruby-1.8 ./install.rb \
    --destdir=$pkgdir \
    --sitelibdir=/opt/ruby1.8/lib/ruby/1.8/ \
    --bindir=/usr/bin \
    --sbindir=/usr/sbin \
    --mandir=/usr/share/man

  # Directories
  install -d $pkgdir/etc/{puppet,rc.d}
  install -d $pkgdir/etc/puppet/{ssl,manifests}
  install -d $pkgdir/etc/puppet/ssl/{ca,certs,private}
  install -m 1777 -d $pkgdir/etc/puppet/ssl/{private_keys,public_keys}
  install -m 1777 -d $pkgdir/var/run/puppet
  install -d $pkgdir/var/lib/puppet/{yaml,bucket,state,rrd,reports,templates,lib,facts}
  install -d -m 750 $pkgdir/var/lib/puppet/{clientbucket,client_yaml}
  install -m 1755 -d $pkgdir/var/lib/puppet/state
  install -d $pkgdir/var/lib/puppet/state/{graphs,localconfig}
  install -m 0750 -d $pkgdir/var/log/puppet

  # Configuration
  install -D $srcdir/puppet.conf $pkgdir/etc/puppet/puppet.conf

  # Daemons
  install -D $srcdir/puppet $pkgdir/etc/rc.d/puppet
  install -D $srcdir/puppetmaster $pkgdir/etc/rc.d/puppetmaster

  # Pacman Provider
  install -m 644 $srcdir/pacman.rb $pkgdir/opt/ruby1.8/lib/ruby/1.8/puppet/provider/package/pacman.rb

  # Move pi to prevent file conflict
  mv $pkgdir/usr/bin/pi $pkgdir/usr/bin/puppet-pi

  # Ensure that the shebang lines are correct
  find $pkgdir/usr/bin -type f -exec sed -i 's,^#!/usr/bin/ruby,#!/usr/bin/ruby-1.8,' {} \;
}
