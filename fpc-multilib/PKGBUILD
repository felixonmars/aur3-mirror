pkgname=fpc-multilib
pkgver=2.6.0
pkgrel=7
pkgdesc="The Free Pascal Compiler for multilib"
arch=('x86_64')
url="http://www.freepascal.org/"
license=('GPL' 'LGPL' 'custom')
backup=("etc/fpc.cfg")
depends=(ncurses)
makedepends=(binutils-multilib fpc)
conflicts=(fpc ppcross386)
provides=(fpc=2.6.0 ppcross386)
options=(zipman)
source=("ftp://ftp.freepascal.org/pub/fpc/dist/$pkgver/source/fpcbuild-$pkgver.tar.gz")
md5sums=('17375e665a4e1311f85812fe2754f609')

build() {
  cd "${srcdir}/fpcbuild-$pkgver/fpcsrc/compiler"
  fpcmake -Tall
  cd ../..
  make NOGDB=1 build OS_TARGET=linux CPU_TARGET=i386 CROSSOPT="-XP"
  
  cd "${srcdir}/fpcbuild-$pkgver"
  pushd "fpcsrc/compiler"
  fpcmake -Tall
  popd
  make NOGDB=1 build
}

package() {
  cd "${srcdir}/fpcbuild-$pkgver"

  export HOME=$srcdir

  make -j1 NOGDB=1 PREFIX="${pkgdir}/usr" install

  export PATH=$pkgdir/usr/bin:$PATH

  install -Dm0644 "fpcsrc/rtl/COPYING.FPC" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.FPC"

  ln -s "/usr/lib/fpc/${pkgver}/ppcross386" "${pkgdir}/usr/bin/ppcross386"
  ln -s "/usr/lib/fpc/${pkgver}/ppcx64" "${pkgdir}/usr/bin/"

  mkdir -p "${pkgdir}/etc"
  ${pkgdir}/usr/lib/fpc/${pkgver}/samplecfg "$pkgdir/usr/lib/fpc/${pkgver}" "${pkgdir}/etc"
  
  # use -fPIC by default
  echo -e "#ifdef cpux86_64\n# for x86_64 use -fPIC by default\n-Cg\n#endif" >> "$pkgdir/etc/fpc.cfg"

  mv "$pkgdir/usr/man" "$pkgdir/usr/share/"
  
  make NOGDB=1 PREFIX="${pkgdir}/usr" crossinstall OS_TARGET=linux CPU_TARGET=i386 CROSSOPT="-XP"

  find "$pkgdir/etc/" -type f -exec sed -i "s|$pkgdir||g" {} \;
}