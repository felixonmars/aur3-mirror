pkgname=mingw-w64-libxr
pkgver=2.0.0
pkgrel=1
pkgdesc="Client/server XML-RPC library for C (mingw-w64)"
arch=(any)
url="http://megous.com/projekty"
license=("LGPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-glib2 mingw-w64-libxml2 mingw-w64-openssl)
options=(!libtool !strip !buildflags)
source=("https://github.com/downloads/megous/libxr/libxr-$pkgver.tar.bz2")
md5sums=('c88cd853ee64d203ba39b2c7c197753a')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  cd "$srcdir/libxr-$pkgver"
  sed -i 's/tests//g' Makefile.in
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}