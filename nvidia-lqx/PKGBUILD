# Contributor: Meowdib <meowdib at gmail dot com>
# Maintainer: Doug <doug.quaid@gmail.com>

pkgname=nvidia-lqx
pkgver=280.13
_kernver='3.0-lqx'
pkgrel=2
pkgdesc="NVIDIA drivers for kernel26-lqx."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=("linux-lqx>=3.0.1" "linux-lqx<3.0.2" "nvidia-utils=${pkgver}")
conflicts=()
license=('custom')
install=nvidia-lqx.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('b222afe19c6310f1aa2a2574f27fa9e2b877738177daa60162bb5705758bac9b')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('adb707a7dd9baf9905681b6078d6b75820fd2e15296efce185dd519e2306a893')
fi

build() {
    cd $srcdir
    sh ${_pkg}.run --extract-only
    cd ${_pkg}/kernel
    make SYSSRC=/lib/modules/${_kernver}/build module || return 1
}

package() {
    install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
        $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko

    # Blacklist nouveau since 2.6.34
    # Blacklisted via kernel-specific file to not conflict with other nvidia drivers
    # Duplicated blacklist entries are not problematic
    install -d -m755 $pkgdir/etc/modprobe.d/ || return 1
    echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist-lqx.conf || return 1

    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia-lqx.install
}
