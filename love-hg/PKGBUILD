# Maintainer: Bart van Strien <bart.bes@gmail.com>
# Contributor: Linus Sj√∂gren <thelinx@unreliablepollution.net>
# Contributor: Andrzej Giniewicz < gginiu@gmail.com >
pkgname=love-hg
pkgver=1834
pkgrel=1
pkgdesc="An open-source 2D game engine which uses the versatile Lua scripting language to create dynamic gaming experiences."
arch=(i686 x86_64)
url="http://love2d.org/"
license=('zlib')
depends=('luajit' 'physfs' 'freetype2' 'devil' 'mpg123' 'openal' 'libvorbis' 'libmodplug' 'sdl2')
source=('hg+http://bitbucket.org/rude/love')
makedepends=('mercurial')
options=(!strip)
md5sums=('SKIP')

pgkver() {
  cd $srcdir/love
  hg identify -n
}

build() {
  cd $srcdir/love

  msg "Updating version information"
  head -c 15 platform/unix/configure.ac > configure.ac.tmp
  echo " [`hg log -l1 --template '{node|short}'`-`date +%Y%m%d`])" >> configure.ac.tmp
  tail -n +2 platform/unix/configure.ac >> configure.ac.tmp
  mv configure.ac.tmp platform/unix/configure.ac

  head -n 4 platform/unix/Makefile.am > Makefile.am.tmp
  mv Makefile.am.tmp platform/unix/Makefile.am

  msg "Updating compiled modules"
  cd src/scripts
  luajit ./auto.lua audio boot graphics
  cd ../..

  msg "Generating makefiles"
  sh platform/unix/automagic -hg
  ./configure --enable-silent-rules --prefix=/usr LDFLAGS=""

  msg "Building"
  make
}

package() {
  cd "$srcdir/love"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
