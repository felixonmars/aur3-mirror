# Maintainer: Aleksei Lissitsin <aleksei.lissitsin@gmail.com>
pkgname=gedit-latex
pkgver=3.4.1
pkgrel=1
pkgdesc="Gedit plugin that provides features to ease the edition of LaTeX documents"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gedit/LaTeXPlugin"
license=('GPL')
groups=()
depends=(gedit rubber texlive-core python2-gobject)
makedepends=(cairo gettext atk intltool libpeas gtk3 glib2)
provides=()
conflicts=(gedit-latex-plugin)
replaces=(gedit-latex-plugin)
install=gschemas.install
source=(http://ftp.gnome.org/pub/gnome/sources/gedit-latex/3.4/$pkgname-$pkgver.tar.xz)
md5sums=('d221f1ca2ae25f61bb0c15f547781fdc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # fixes taken from the fedora package
  sed -i -e 's|_CONFIG_FILENAME = "/etc/texmf/texmf.cnf"|_CONFIG_FILENAME = "/usr/share/texmf/web2c/texmf.cnf"|' latex/latex/environment.py
  sed -i -e 's|_DEFAULT_TEXMF_DIR = "/usr/share/texmf-texlive"|_DEFAULT_TEXMF_DIR = "/usr/share/texmf"|' latex/latex/environment.py
  
  # no gnome-open, use gvfs-open instead; fixed in master:
  # http://git.gnome.org/browse/gedit-latex/commit/?id=4166f494ca26e1ef0540652fc1658e9dc31e6a0d
  sed -i -e 's:gnome-open:gvfs-open:g' latex/latex/views.py
  sed -i -e 's:gnome-open:gvfs-open:g' latex/latex/views.py
  sed -i -e 's:gnome-open:gvfs-open:g' latex/latex/preview.py
  sed -i -e 's:gnome-open:gvfs-open:g' data/tools.xml
    
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

