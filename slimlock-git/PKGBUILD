# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Joel Burget <joelburget@gmail.com>
# Contributor: Guff <kasull88@gmail.com>
# Contributor: Renato Aguiar
pkgname=slimlock-git
_gitname=slimlock
pkgver=20130405
pkgrel=1
pkgdesc="Unholy screen locker based on SLiM (git)"
arch=('i686' 'x86_64')
url="http://programsthatsmellgood.com/slimlock/"
license=('GPL2')
depends=('libxft' 'imlib2' 'pam' 'libxrandr')
makedepends=('git')
optdepends=('slim')
provides=('slimlock')
conflicts=('slimlock')
backup=('etc/slimlock.conf')
source=('git://github.com/dannyn/slimlock.git'
        'slimlock-git.install'
        'slim.conf'
        'http://downloads.sourceforge.net/project/slim.berlios/slim-archlinux-simple.tar.gz')
install=slimlock-git.install
md5sums=('SKIP'
         '40af4379e8b17203414356edbf57469f'
         '3f75d2a7a85c08a41b2f75eeb548cd87'
         '7c7aaa255561444a4ae3e9f1dfb4be5d')

pkgver() {
  date -u +%Y%m%d
}

build() {
  cd "$srcdir/$_gitname"
  make
}

package() {
  cd "$srcdir/$_gitname"

  make DESTDIR="$pkgdir" MANDIR=/usr/share/man install
  cd "$srcdir"
  install -D -m644 slim.conf \
    "$pkgdir/usr/share/slimlock/example-slim.conf"

  cd "$srcdir/slim-archlinux-simple"

  install -D -m644 slim.theme \
    "$pkgdir/usr/share/slim/themes/slim-archlinux-default/slim.theme"
  install -D -m644 background.png \
    "$pkgdir/usr/share/slim/themes/slim-archlinux-default/background.png"
  install -D -m644 panel.png \
    "$pkgdir/usr/share/slim/themes/slim-archlinux-default/panel.png"
  install -D -m644 preview.png \
    "$pkgdir/usr/share/slim/themes/slim-archlinux-default/preview.png"
}

# vim:set ts=2 sw=2 et:
