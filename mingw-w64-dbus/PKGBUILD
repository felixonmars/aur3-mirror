# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-dbus
pkgver=1.4.16
pkgrel=1
arch=('any')
pkgdesc="MinGW Windows port of D-Bus (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-glib2' 'mingw-w64-libxml2')
makedepends=('mingw-w64-gcc')
options=(!strip !buildflags)
license=('BSD')
url="http://www.freedesktop.org/wiki/Software/dbus"
source=("http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz")
md5sums=('44a2a10678e7e50460879c3eb4453a65')


_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd ${srcdir}/dbus-${pkgver}

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    unset LDFLAGS
    ../configure \
      --host=${_arch} \
      --prefix=/usr/${_arch} \
      --with-xml=libxml
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/dbus-${pkgver}/build-${_arch}"
    make DESTDIR=$pkgdir install
    ${_arch}-strip $pkgdir/usr/${_arch}/bin/*.exe
    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
    rm -rf $pkgdir/usr/${_arch}/share/{doc,man}
    rm -rf $pkgdir/usr/${_arch}/{etc,var,libexec}
  done
}
