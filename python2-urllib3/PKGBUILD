# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Patrice Peterson <runiq at archlinux dot us>

pkgname=python2-urllib3
_pkgname=python-urllib3
pkgver=1.5
pkgrel=1
pkgdesc="HTTP library with thread-safe connection pooling and file post support"
arch=('any')
url="http://github.com/shazow/urllib3"
license=('MIT')
depends=('python2')
makedepends=('setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/shazow/urllib3/tarball/1.5" 
        LICENSE)
md5sums=('d1ec8e2e01264f176e2361d5bbbf49a7'
         '350846ab4dd11ce105b570c15c1b0764')

package() {
  cd "$srcdir/shazow-urllib"*
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README.rst "$pkgdir/usr/share/doc/$pkgname/README.rst"
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}
