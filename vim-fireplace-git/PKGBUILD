# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-fireplace-git
pkgver=20140208
pkgrel=1
pkgdesc="Clojure REPL tease, by tpope"
arch=('any')
depends=('vim' 'vim-clojure-static-git')
makedepends=('git')
optdepends=('vim-classpath-git: run code when no REPL is available, useless and possibly slow if you only care about nREPL support')
groups=('vim-plugins')
url="https://github.com/tpope/vim-fireplace"
license=('custom:vim')
source=(${pkgname%-git}::git+https://github.com/tpope/vim-fireplace)
sha256sums=('SKIP')
provides=('vim-fireplace')
conflicts=('vim-fireplace')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Removing unneeded files...'
  rm -f README*
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  install -dm 755 ${pkgdir}/usr/share/vim/vimfiles/
  tar -c . | tar -x -C ${pkgdir}/usr/share/vim/vimfiles

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
