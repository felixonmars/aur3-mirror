# Maintainer: Joel Pedraza <pkgs-at-joelpedraza-dot-com>

pkgname=android-sdk-preview
pkgver=r21_rc7
pkgrel=1
pkgdesc='Google Android SDK Preview'
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
[[ $CARCH = "i686" ]] &&
  depends=('java-runtime' 'alsa-lib' 'openal' 'libstdc++5' 'libxv'
         'ncurses' 'sdl' 'swt' 'zlib') ||
  depends=('java-runtime' 'lib32-alsa-lib' 'lib32-openal' 'lib32-libstdc++5'
           'lib32-libxv' 'lib32-ncurses' 'lib32-sdl' 'lib32-zlib' 'swt')
provides=('android-sdk=21')
conflicts=('android-sdk')
install=${pkgname}.install
optdepends=('android-udev: udev rules for Android devices'
            'android-sdk-platform-tools: adb, aapt, aidl, dexdump and dx')
source=(http://dl.google.com/android/repository/tools_${pkgver}-linux.zip
        ${pkgname}.png
        ${pkgname}.desktop
        ${pkgname}.sh
        ${pkgname}.csh
        ${pkgname}.conf
        license.html
        adb.service
        source.properties)
sha1sums=('9913958c7627d0299e8708eb5148a38d4de73b20'
          'c249134310ae91dccce1ad839b3985211674ab71'
          'e834f53e13d5926ec7be3fd775fa80aa6f328eb6'
          '78f8574e651c9bf8b7515ecb30c7ef93edbc4a96'
          '08c85aab7523e22b298891c7047bc0e7adbf3437'
          '145bdf3eb41a56574b289c1577a24bc47097ec83'
          'bfb91be7e0b602d765b7a1fcaf0ce1b7e1a93faa'
          '7338275fcafb9fe930139dc91f7c20891867f6d4'
          'a53f9e5f5b42d168a0e40c24f75831ff0756e029')

package() {
  mkdir -p "${pkgdir}/opt/android-sdk"
  mv "${srcdir}/tools" "${pkgdir}/opt/android-sdk/tools"
  chmod -R ugo+rX "${pkgdir}/opt"
  install -Dm755 "${srcdir}/${pkgname}.sh" \
    "${pkgdir}/etc/profile.d/${pkgname}.sh"
  install -Dm755 "${srcdir}/${pkgname}.csh" \
    "${pkgdir}/etc/profile.d/${pkgname}.csh"
  install -Dm644 "${srcdir}/${pkgname}.conf" \
    "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
  install -Dm655 "${srcdir}/${pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm655 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/license.html" \
    "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
  install -Dm644 "${srcdir}/adb.service" \
    "${pkgdir}/usr/lib/systemd/system/adb.service"
  install -Dm644 "${srcdir}/source.properties" \
    "${pkgdir}/opt/android-sdk/tools/source.properties"
  if [[ $CARCH = "i686" ]]; then
    rm -rf ${pkgdir}/opt/android-sdk/tools/lib/{monitor-,}x86_64
  fi
}
