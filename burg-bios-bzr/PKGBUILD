# Maintainer  : Firef0x <Firefgx {at} gmail dot com>
# Contributor : Firef0x <Firefgx (at) gmail dot com>
# Contributor : Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor : cruznick <cruznick@archlinux.us>
# Contributor : fsckd <fsckdaemon@gmail.com>

## Tunables: change y to n to disable
_rm_build_dirs="1"        ## remove build directories
_mk_burg_emu="0"            ## enable burg emu

_mod="burg"
_branch="master"

_builddir="${_mod}-build"
_builddir_emu="${_mod}-emu-build"

pkgname=burg-bios-bzr
pkgver=1845.3.6453766
pkgrel=3
pkgdesc="Brand-new Universal loadeR from Grub2 - Built for i386 BIOS for Arch Linux"
url="http://code.google.com/p/burg/"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('ruby' 'bdf-unifont' 'python2' 'git' 'help2man')
depends=('gettext' 'freetype2' 'os-prober')
optdepends=('memtest86+: An advanced memory diagnostic tool, could be launched on boot')

conflicts=('burg-bios')
replaces=('burg' 'burg-bzr' 'burg-emu')
provides=('burg' 'burg-bios' 'burg-bzr')

if [[ "${_mk_burg_emu}" == "1" ]]; then
    makedepends+=('sdl')
    optdepends+=('sdl: For burg-emu SDL support')
    conflicts+=('burg-emu')
    provides+=('burg-emu')
fi

#options=('!makeflags')
changelog='CHANGELOG.md'
backup=('etc/default/burg' 'etc/burg.d/40_custom')

source=("${_mod}"::'git://github.com/Firef0x/burg.git'
		'burg.default'
		'60_memtest86+'
		'update-burg')

sha256sums=('SKIP'
			'914da1384390e8cd63f335564075843eb52172d67a174dd44226f0710d5f0cde'
            'e83435c4bfa31b45c879f5cc610bd032cb67d21b40cb97775478ab3f3bffbd7b'
            '160e78f9d8a3859ac6b3534f3011a4d238bc78de2b0c1ae489a945c24466e21f')

install='burg.install'

_common_configure_opts="--host="${CARCH}-pc-linux-gnu" \
						--program-prefix="" \
                        --prefix="/usr" \
						--bindir="/usr/bin" \
                        --sbindir="/usr/bin" \
						--mandir="/usr/share/man" \
                        --infodir="/usr/share/info" \
						--sysconfdir="/etc" \
                        --disable-werror"

pkgver() {
	cd "${_mod}"
	echo "1845.$(git rev-list --count ${_branch}).$(git rev-parse --short ${_branch})"
}

_build_dir() {

    local _rm_builddir='0'

    while (( "$#" ))
    do
        local _check="$1"

        if [[ "${_check}" == '-r' ]]; then
            _rm_builddir='1'
        elif [[ "${_check}" == 'emu' ]]; then
            _builddir="${_mod}-build"
            _builddir_emu="${_mod}-emu-build"
        else
            _builddir="${_mod}-$1-build"
        fi

        shift
    done

    if [[ "${_check}" == 'emu' ]]; then
        rm -rf "${srcdir}/${_builddir_emu}" || true
    else
        rm -rf "${srcdir}/${_builddir}" || true
    fi

    if [[ "${_rm_builddir}" == '0' ]]; then
        if [[ "${_check}" == 'emu' ]]; then
            cp -rip "${srcdir}/${_mod}" "${srcdir}/${_builddir_emu}"
            cd "${srcdir}/${_builddir_emu}"
        else
            cp -rip "${srcdir}/${_mod}" "${srcdir}/${_builddir}"
            cd "${srcdir}/${_builddir}/"
        fi
    fi

}

_build_common() {

    local _check="$1"

    if [[ "${_check}" == 'emu' ]]; then
        cd "${srcdir}/${_builddir_emu}"
    else
        cd "${srcdir}/${_builddir}"
    fi

    echo

    ./autogen.sh

    echo

}

_build_bios() {

    msg "Building burg bios...."

    _build_dir

    echo

    echo

    _build_common

    echo

    cd "${srcdir}/${_builddir}/"

    _bios_configure_opts="${_common_configure_opts} --with-platform=pc --disable-grub-emu-usb"
    ./configure ${_bios_configure_opts}

    echo

    CFLAGS="" make

    echo

}

_build_emu() {

    msg "Building burg-emu...."

    _build_dir emu

    echo

    _build_common emu

    echo

    cd "${srcdir}/${_builddir_emu}/"

    _emu_configure_opts="${_common_configure_opts} --with-platform=emu --disable-grub-emu-usb"
    ./configure ${_emu_configure_opts}

    echo

    CFLAGS="" make

    echo

}

build() {

    _build_bios

    echo

    if [[ "${_mk_burg_emu}" == "1" ]]; then
        _build_emu
    fi

    echo

}

package() {

    cd "${srcdir}/${_builddir}/"

    make DESTDIR="${pkgdir}/" install

    echo

    if [[ "${_mk_burg_emu}" == "1" ]]; then
        install -D -m0755 "${srcdir}/${_builddir_emu}/grub-emu" "${pkgdir}/usr/bin/burg-emu"
    fi

    ## Delete deprecated burg-mkconfig helper file
    rm -f "${pkgdir}/usr/lib/burg/update-burg_lib" || true
    rm -rf "${pkgdir}/usr/share/burg" || true

    ## install /etc/default/burg
    install -D -m0644 "${srcdir}/burg.default" "${pkgdir}/etc/default/burg"

    ## install /usr/bin/updburg
    install -D -m0755 "${srcdir}/update-burg" "${pkgdir}/usr/bin/update-burg"

    ## install memtest config detection
    install -D -m0755 "${srcdir}/60_memtest86+" "${pkgdir}/etc/burg.d/60_memtest86+"

    if [[ "${_rm_build_dirs}" == "1" ]]; then
        _build_dir -r

        if [[ "${_mk_burg_emu}" == "1" ]]; then
            _build_dir -r emu
        fi
    fi

    echo

}
