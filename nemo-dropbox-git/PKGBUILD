# Maintainer: twa022 <twa022 at gmail dot com>
# based on nautilus-dropbox package, contributors below...
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>
# Contributor: fazibear <fazibear@gmail.com>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>
# Contributor: Gordin <9ordin @t gmail.com>

_pkgname=nemo-dropbox
pkgname=${_pkgname}-git
pkgver=49.6539547
pkgrel=1
pkgdesc="Dropbox for Linux - Nemo extension"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-extensions"
license=('custom:CC-BY-ND-3' 'GPL')
depends=('libnotify' 'nemo' 'dropbox' 'hicolor-icon-theme')
makedepends=('python2-docutils' 'python2' 'pygtk' 'git')
install=${_pkgname}.install
options=('!libtool' '!emptydirs')
source=("git+https://github.com/linuxmint/nemo-extensions.git")

pkgver() { 
  cd "$srcdir"/nemo-extensions
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir"/nemo-extensions/${_pkgname}

  sed -i "s|AM_CONFIG_HEADER|AC_CONFIG_HEADERS|g" configure.in

  autoreconf -fi
  
  sed -i "s:python:&2:" \
    configure \
  	Makefile.am \
  	dropbox.in \
  	rst2man.py \
	Makefile.in
	
  # since python2-docutils, rst2man.py is named "rst2man2.py"
  sed -i "s:rst2man.py:/usr/bin/rst2man2.py:" configure

  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/nemo-extensions/${_pkgname}
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/bin/dropbox"
  rm "${pkgdir}/usr/share/applications/dropbox.desktop"  
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/COPYING"
}
sha256sums=('SKIP')
sha256sums=('SKIP')
