# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-user-share
pkgver=1.6.1
pkgrel=1
pkgdesc="User level public file sharing via WebDAV or ObexFTP."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('apache' 'dconf' 'libunique' 'mod_dnssd')
makedepends=('libcanberra' 'libnotify' 'mate-common' 'mate-doc-utils'
             'mate-file-manager' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-extra')
install=${pkgname}.install
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/NiceandGently/mate-user-share/commit/906172bb95b10dd064d0b0fde0d0510434d34b96.diff")
sha1sums=('83f161dee79ea0ae4345c54a5b1339f673f68e8f'
          'ffa10ae69f5edf08d605418182f3c5bc975eb3cf')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # do not use download dir for incoming bluetooth downloads if mate-bluetooth isn't installed
    patch -Np1 -i "${srcdir}/906172bb95b10dd064d0b0fde0d0510434d34b96.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --disable-static \
        --disable-scrollkeeper \
        --disable-bluetooth
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make  DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf"
}
