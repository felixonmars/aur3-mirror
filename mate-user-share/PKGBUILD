#Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-user-share
pkgver=1.6.0
pkgrel=2
pkgdesc="User level public file sharing via WebDAV or ObexFTP."
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('apache' 'dbus' 'desktop-file-utils' 'gtk2' 'libcanberra' 'libnotify'
         'libunique' 'mate-bluetooth' 'mate-file-manager' 'mod_dnssd' 'obex-data-server')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('03f5731a29bc53a9ff6882d33b2a45d0270e48e1')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make  DESTDIR="${pkgdir}" install
    rm -f ${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf
}
