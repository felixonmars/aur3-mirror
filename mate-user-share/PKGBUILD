# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-user-share
pkgver=1.6.0
pkgrel=3
pkgdesc="User level public file sharing via WebDAV or ObexFTP."
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('apache' 'dconf' 'libunique' 'mod_dnssd')
makedepends=('libcanberra' 'libnotify' 'mate-common' 'mate-doc-utils'
             'mate-file-manager' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-extra')
install=${pkgname}.install
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        "make-bluetooth-optional.patch")
sha1sums=('03f5731a29bc53a9ff6882d33b2a45d0270e48e1'
          '886d06a6cd0c5158b93a72c82d05b11481d25142')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Make bluetooth optional
    patch -Np1 -i "${srcdir}/make-bluetooth-optional.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --disable-static \
        --disable-scrollkeeper \
        --disable-bluetooth
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make  DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf"
}
