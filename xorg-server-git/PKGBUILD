# Contributor: Det <nimetonmaili@gmail.com>
# Contributor: FoxBuru <FoxBuru at gmail dot com>
# Contributor: SpeedVin <speedvin at archlinux dot us>
# Based on xorg-server-dev

pkgbase=xorg-server
pkgname=xorg-server-git
true && pkgname=("$pkgname" 'xorg-server-common-git' 'xorg-server-devel-git' 'xorg-server-xdmx-git' 'xorg-server-xephyr-git' 'xorg-server-xnest-git' 'xorg-server-xvfb-git')
pkgver=20121009
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('bigreqsproto' 'compositeproto' 'dmxproto' 'dri2proto' 'fontsproto' 'glproto' 'inputproto' 'libdmx' 'libgcrypt' 'libgl' 'libpciaccess' 'libx11' 'libxaw' 'libxfont' 'libxi' 'libxkbfile' 'libxmu' 'libxrender' 'libxres' 'libxtst' 'libxv' 'mesa' 'pixman' 'randrproto' 'recordproto' 'renderproto' 'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xextproto' 'xf86dgaproto' 'xf86driproto' 'xineramaproto' 'xorg-font-util' 'xorg-util-macros' 'xorg-xkbcomp' 'xtrans')
options=('!libtool')
source=(autoconfig-nvidia.patch
        autoconfig-sis.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf)
sha1sums=('c92e1ad815a68b43a2cacc2fef7d9425f39a6b73'  # autoconfig-nvidia.patch
          '74020ecf02dddd79bb18cc231fb3e2a49a009eff'  # autoconfig-sis.patch
          'c94f742d3f9cabf958ae58e4015d9dd185aabedc'  # xvfb-run
          '6838fc00ef4618c924a77e0fb03c05346080908a'  # xvfb-run.1
          '993798f3d22ad672d769dae5f48d1fa068d5578f') # 10-quirks.conf

_gitroot="git://anongit.freedesktop.org/xorg/xserver"
_gitname="xserver"

build() {
  msg "Connecting to anongit.freedesktop.org GIT server...."

  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone --depth 1 $_gitroot
  fi

  msg "GIT checkout done or server timeout."

  rm -rf ${_gitname}-build
  cp -r $_gitname ${_gitname}-build
  cd ${_gitname}-build

  msg "Applying Patches..."

  msg2 "Use nouveau/nv/nvidia drivers for nvidia devices"
  patch -p1 -i ../autoconfig-nvidia.patch

  msg2 "Use unofficial imedia SiS driver for supported SiS devices"
  patch -p0 -i ../autoconfig-sis.patch

  ./autogen.sh --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-common-git() {
  pkgdesc="Xorg server common files - Git ones"
  depends=('xkeyboard-config' 'xorg-fonts-misc' 'xorg-setxkbmap' 'xorg-xkbcomp')
  provides=('xorg-server-common=99')
  conflicts=('xorg-server-common')

  cd ${_gitname}-build
  make -C xkb DESTDIR="$pkgdir" install-data

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/xorg-server-common/COPYING"
  install -Dm644 man/Xserver.1 "$pkgdir/usr/share/man/man1/Xserver.1"
  install -Dm644 dix/protocol.txt "$pkgdir/usr/lib/xorg/protocol.txt"

  # Don't need this
  rm -f "$pkgdir"/var/lib/xkb/README.compiled
}

package_xorg-server-devel-git() {
  pkgdesc="Development files for the X.Org X server from Git"
  # see pkgdir/usr/lib/pkgconfig/xorg-server.pc, xorg-util-macros not technically required but almost every Xorg pkg needs it to build
  depends=('dri2proto' 'fontsproto' 'glproto' 'inputproto' 'kbproto' 'libpciaccess' 'mesa' 'pixman' 'randrproto' 'renderproto' 'videoproto' 'xextproto' 'xf86driproto' 'xineramaproto' 'xproto')
  provides=('xorg-server-devel=99')
  conflicts=('xorg-server-devel')

  cd ${_gitname}-build
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/{usr/{bin,share/{man,doc,X11},lib/xorg},var}
  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-devel"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-devel/"
}

package_xorg-server-xdmx-git() {
  pkgdesc="Distributed Multihead X Server and utilities - Git ones"
  depends=('libdmx' 'libgcrypt' 'libxaw' 'libxfixes' 'libxfont' 'libxi' 'libxrender' 'pixman' 'xorg-server-common')
  provides=('xorg-server-xdmx=99')
  conflicts=('xorg-server-xdmx')

  cd ${_gitname}-build/hw/dmx
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xdmx"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xdmx/"
}

package_xorg-server-xephyr-git() {
  pkgdesc="A nested X server that runs as an X application - Git version"
  depends=('libgcrypt' 'libgl' 'libxfont' 'libxv' 'pixman' 'xorg-server-common')
  provides=('xorg-server-xephyr=99')
  conflicts=('xorg-server-xephyr')

  cd ${_gitname}-build/hw/kdrive
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xephyr/"
}

package_xorg-server-xnest-git() {
  pkgdesc="A nested X server that runs as an X application - Git version"
  depends=('libgcrypt' 'libxext' 'libxfont' 'pixman' 'xorg-server-common')
  provides=('xorg-server-xnest=99')
  conflicts=('xorg-server-xnest')

  cd ${_gitname}-build/hw/xnest
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xnest"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xnest/"
}

package_xorg-server-xvfb-git() {
  pkgdesc="Virtual framebuffer X server - Git version"
  depends=('libgcrypt' 'libxau' 'libxdmcp' 'libxfont' 'pixman' 'xorg-server-common')
  provides=('xorg-server-xvfb=99')
  conflicts=('xorg-server-xvfb')

  cd ${_gitname}-build/hw/vfb
  make DESTDIR="$pkgdir" install

  install -m755 "$srcdir/xvfb-run" "$pkgdir/usr/bin/"
  cp "$srcdir/xvfb-run.1" "$pkgdir/usr/share/man/man1/"

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xvfb"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xvfb/"
}

package_xorg-server-git() {
  pkgdesc="Xorg X server - Git version"
  depends=('libdrm' 'libgcrypt' 'libpciaccess' 'libxau' 'libxdmcp' 'libxfont' 'pixman' 'xf86-input-evdev' 'xorg-server-common')
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  # see xorg-server-1.13.0/hw/xfree86/common/xf86Module.h for major ABI versions for drivers to depend on
  provides=('X-ABI-VIDEODRV_VERSION=13' 'X-ABI-XINPUT_VERSION=18' 'X-ABI-EXTENSION_VERSION=7.0' 'x-server' 'xorg-server=99')
  conflicts=('xorg-server' 'nvidia-utils<=290.10')
  groups=('xorg')

  cd ${_gitname}-build
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/etc/X11"
  mv "$pkgdir/usr/share/X11/xorg.conf.d" "$pkgdir/etc/X11/"
  cp ../10-quirks.conf "$pkgdir/etc/X11/xorg.conf.d/"

  # Needed for non-mesa drivers, libgl will restore it
  mv "$pkgdir"/usr/lib/xorg/modules/extensions/libglx.{so,xorg}

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server/"

  rm -rf "$pkgdir"/usr/{share/{man/man1/Xserver.1,aclocal,X11},lib/{xorg/protocol.txt,pkgconfig},include}

  # Don't need this
  rm -f "$pkgdir"/var/lib/xkb/README.compiled
}