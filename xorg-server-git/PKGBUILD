# Maintainer: FoxBuru <FoxBuru at gmail dot com>
# Contributor: Det <nimetonmaili@gmail.com>
# Contributor: SpeedVin <speedvin at archlinux dot us>
# Based on xorg-server-dev

pkgbase=xorg-server
pkgname=('xorg-server-git')
true && pkgname=('xorg-server-git' 'xorg-server-xephyr-git' 'xorg-server-xdmx-git' 'xorg-server-xvfb-git' 'xorg-server-xnest-git' 'xorg-server-common-git' 'xorg-server-devel-git')
pkgver=20110827
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('pixman' 'libx11' 'mesa' 'libgl' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto' 'randrproto' 'inputproto' 'fontsproto' 'videoproto' 'compositeproto' 'recordproto' 'scrnsaverproto' 'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont' 'renderproto' 'libpciaccess' 'libxv' 'xf86dgaproto' 'libxmu' 'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst' 'libxres' 'xorg-xkbcomp' 'xextproto' 'xorg-util-macros' 'xorg-font-util' 'glproto' 'dri2proto' 'git')
options=('!libtool')
source=(autoconfig-nvidia.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf)
sha1sums=('0249b892f27243d8fe6fe6d226bf4c2391cedf49'
          'c94f742d3f9cabf958ae58e4015d9dd185aabedc'
          '6838fc00ef4618c924a77e0fb03c05346080908a'
          '993798f3d22ad672d769dae5f48d1fa068d5578f')
 
_gitroot="git://anongit.freedesktop.org/xorg/xserver"
_gitname="xserver"
 
build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d ${_gitname} ]; then
      cd ${_gitname} && git pull origin
      msg "The local files are updated."
  else
      git clone ${_gitroot}
      cd ${_gitname}
  fi

  msg "GIT checkout done or server timeout."

  cd ..
  rm -rf ${_gitname}-build
  cp -r ${_gitname} ${_gitname}-build
  cd ${_gitname}-build

  msg "Applying Patches..."

  # Use nouveau/nv/nvidia drivers for nvidia devices
  patch -Np1 -i ../autoconfig-nvidia.patch

  ./autogen.sh --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-common-git() {
  pkgdesc="Xorg server common files - Git version"
  depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap' 'xorg-fonts-misc')
  provides=("xorg-server-common=${pkgver}")
  conflicts=('xorg-server-common')

  cd ${_gitname}-build
  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-common"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xorg-server-common"

  make -C xkb DESTDIR="${pkgdir}" install-data

  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/Xserver.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/lib/xorg"
  install -m644 dix/protocol.txt "${pkgdir}/usr/lib/xorg/"
}

package_xorg-server-xephyr-git() {
  pkgdesc="A nested X server that runs as an X application - Git version"
  depends=(libxfont libgl libgcrypt libxv pixman xorg-server-common)
  provides=("xorg-server-xephyr=${pkgver}")
  conflicts=(xorg-server-xephyr)

  cd ${_gitname}-build/hw/kdrive
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xephyr/COPYING"
}

package_xorg-server-xvfb-git() {
  pkgdesc="Virtual framebuffer X server - Git version"
  depends=(libxfont libxdmcp libxau libgcrypt pixman xorg-server-common)
  provides=("xorg-server-xvfb=${pkgver}")
  conflicts=(xorg-server-xvfb)

  cd ${_gitname}-build/hw/vfb
  make DESTDIR="${pkgdir}" install

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xvfb"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xvfb/COPYING"
}

package_xorg-server-xnest-git() {
  pkgdesc="A nested X server that runs as an X application - Git version"
  depends=(libxfont libxext libgcrypt pixman xorg-server-common)
  provides=("xorg-server-xnest=${pkgver}")
  conflicts=(xorg-server-xnest)

  cd ${_gitname}-build/hw/xnest
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xnest"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xnest/COPYING"
}

package_xorg-server-xdmx-git() {
  pkgdesc="Distributed Multihead X Server and utilities - Git version"
  depends=(libxfont libxi libgcrypt libxaw libxrender libdmx libxfixes pixman xorg-server-common)
  provides=("xorg-server-xdmx=${pkgver}")
  conflicts=(xorg-server-xdmx)

  cd ${_gitname}-build/hw/dmx
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xdmx"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xdmx/COPYING"
}

package_xorg-server-devel-git() {
  pkgdesc="Development files for the X.Org X server - Git version"
  depends=(xproto randrproto renderproto xextproto inputproto kbproto fontsproto videoproto dri2proto xineramaproto xorg-util-macros pixman libpciaccess)
  provides=("xorg-server-devel=${pkgver}")
  conflicts=(xorg-server-devel)

  cd ${_gitname}-build
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/bin"
  rm -rf "${pkgdir}/usr/share/man"
  rm -rf "${pkgdir}/usr/share/doc"
  rm -rf "${pkgdir}/usr/share/X11"
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -rf "${pkgdir}/var"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-devel"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-devel/COPYING"
}

package_xorg-server-git() {
  pkgdesc="Xorg X server - Git version"
  depends=(libxdmcp libxfont udev libpciaccess libdrm pixman libgcrypt libxau xorg-server-common xf86-input-evdev)
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  provides=('x-server' "xorg-server=${pkgver}")
  conflicts=('xorg-server')
  groups=('xorg')

  cd ${_gitname}-build
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 ../10-quirks.conf "${pkgdir}/etc/X11/xorg.conf.d/"

  rmdir "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}"/usr/lib/xorg/modules/extensions/libglx.{so,xorg}

  rm -rf "${pkgdir}/var"

  rm -f "${pkgdir}/usr/share/man/man1/Xserver.1"
  rm -f "${pkgdir}/usr/lib/xorg/protocol.txt"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server/COPYING"

  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/share/aclocal"
}
