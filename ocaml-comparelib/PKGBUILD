# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>

pkgname=ocaml-comparelib
_pkgname=${pkgname/ocaml-/}
pkgver=108.07.01
pkgrel=1
pkgdesc="OCaml syntax extension for deriving 'compare' functions automatically."
arch=('i686' 'x86_64')
# LGPL + linking exception
license=('LGPL')
depends=('ocaml' 'ocaml-type-conv')
makedepends=('ocaml-findlib')
url="http://bitbucket.org/yminsky/ocaml-core/wiki/Home"
source=("http://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('d83e254b7fa7707c3aa55bb7f9f7fe7c')
options=(!strip !makeflags)

build(){
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package(){
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
