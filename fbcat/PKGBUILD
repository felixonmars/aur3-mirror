# Maintainer: Sara <sara at archlinux dot us>
# Contributor: Grigorios Bouzakis <grbzks at xsmail dot com>

pkgname=fbcat
pkgver=0.3
pkgrel=1
pkgdesc="Framebuffer screenshot grabber"
arch=('i686' 'x86_64')
url="http://code.google.com/p/fbcat"
license=('GPL')
depends=('glibc' 'sh')
optdepends=('graphicsmagick: fbgrab wrapper'
            'imagemagick: fbgrab wrapper'
            'netpbm: fbgrab wrapper')
provides=('fbgrab')
conflicts=('fbgrab')
replaces=('fbgrab')
source=(http://fbcat.googlecode.com/files/${pkgname}-${pkgver}.tar.gz \
        ${pkgname}.1 \
        fbgrab.1)
md5sums=('85f6207459ecedd55b406a8c62249ba5'
         '85c31f14d8d5d7e2541f8918680ad2fa'
         'f0ed68439c303a943bae5b4c5d9bb208')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 fbgrab "${pkgdir}"/usr/bin/fbgrab

  install -Dm644 "${srcdir}/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm644 "${srcdir}"/fbgrab.1 "${pkgdir}"/usr/share/man/man1/fbgrab.1
}

# vim:set ts=2 sw=2 et:
