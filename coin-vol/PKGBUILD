# Maintainer: Daniel Ehlers <danielehlers@mindeye.net>
pkgname=coin-vol
pkgver=1.2.2
pkgrel=2
pkgdesc="Vol (Volume Algorithm) is an open-source implementation of a subgradient method that produces primal as well as dual solutions."
arch=('i686')
url="https://projects.coin-or.org/Vol"
license=('EPL')
groups=('coin-or')
depends=('coin-utils>=2.7.1' 'coin-cgl>=0.56.2' 'coin-clp>=1.13.3')
makedepends=('coin-utils>=2.7.1' 'coin-cgl>=0.56.2' 'coin-clp>=1.13.3')
source=(http://www.coin-or.org/download/source/Vol/Vol-$pkgver.tgz)
md5sums=('b9ba939f364019a7238bcdff39586e13')

build() {
  cd "$srcdir/Vol-$pkgver"
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="`pkg-config --libs osi`" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="`pkg-config --libs coinutils`" \
              --with-coinutils-incdir="/usr/include/coin/" -C
  make
}

package() {
  cd "$srcdir/Vol-$pkgver"
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir/" install
}
