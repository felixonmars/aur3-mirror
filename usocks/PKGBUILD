# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=usocks
pkgver=0.5
pkgrel=1
pkgdesc='Minimalistic SOCKS5 proxying library'
url='http://www.normalesup.org/~bisson/src/'
license=('ISC')
arch=('i686' 'x86_64' 'armv7h')
optdepends=('openssh: companion script')
source=("${url}/${pkgname}-${pkgver}.c"
        "${pkgname}")
sha1sums=('15d2e47f62b7e38d012996dd1525b4a1f16b50f7'
          '31e3c9f02325976201b5a3b387b2c169f452000d')

build() {
	cd "${srcdir}"
	cc -fPIC -ldl -shared \
		${CFLAGS} ${LDFLAGS} \
		-o ${pkgname}.so ${pkgname}-${pkgver}.c
}

package() {
	cd "${srcdir}"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm755 "${pkgname}.so" "${pkgdir}/usr/lib/${pkgname}.so"
	head -n 15 "${pkgname}-${pkgver}.c" > "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
