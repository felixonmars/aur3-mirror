# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libbluray-git
pkgver=20120503
pkgrel=1
pkgdesc="Blu-Ray access library"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.videolan.org/developers/libbluray.html"
depends=('libxml2' 'java-environment')
makedepends=('git' 'apache-ant' 'texlive-latexextra')
optdepends=('libaacs-git: Enable AACS decryption')
provides=('libbluray')
conflicts=('libbluray')
options=('!libtool')

_gitroot="git://git.videolan.org/libbluray.git"
_gitname="libbluray"

build() {
    msg "Connecting to GIT server..."

    if [ -d "${srcdir}"/"${_gitname}" ] ; then
        cd "${_gitname}" && git pull
    else
        git clone --depth=1 "${_gitroot}"
    fi

    [ -d "${srcdir}"/"${_gitname}"-build ] && rm -fr "${srcdir}"/"${_gitname}"-build
    cp -R "${srcdir}"/"${_gitname}" "${srcdir}"/"${_gitname}"-build

    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    cd "${srcdir}"/"${_gitname}"-build
 
    source /etc/profile.d/jdk.sh
    ./bootstrap
    ./configure --prefix=/usr --with-jdk="${JAVA_HOME}" --enable-bdjava --disable-static --disable-doxygen-html --disable-doxygen-ps
    make
    make doxygen-pdf
}

package() {
    cd "${srcdir}"/"${_gitname}"-build
    make DESTDIR="${pkgdir}" install
    install -Dm644 src/.libs/libbluray.jar "${pkgdir}"/usr/share/java/libbluray.jar
    install -Dm664 doc/doxygen/libbluray.pdf "${pkgdir}"/usr/share/doc/libbluray/libbluray.pdf
}
