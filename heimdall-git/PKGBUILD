# Contributor: Todd Musall 
# Contributor: dront78 (Ivan)
# Contributor: Victor Noel
# Maintainer: Lari Tikkanen
########################################
_frontend="y"    # Build the Qt GUI y/n
########################################
pkgname=('heimdall-git')
pkgver=20111001
pkgrel=1
pkgdesc="A cross-platform open-source utility to flash firmware (aka ROMs) onto Samsung Galaxy S devices."
arch=('i686' 'x86_64')
url="http://www.glassechidna.com.au/products/heimdall/"
license=('MIT')
if [ "$_frontend" = "y" ]; then
  depends=('gcc-libs' 'libusb' 'qt')      
else
  depends=('gcc-libs' 'libusb')
fi
makedepends=('git')
optdepends=('android-udev: Udev rules to connect Android devices to you linux box')
conflicts=('heimdall')
provides=('heimdall')
source=('heimdall.desktop')
md5sums=('6c4de9e74c0d9e7ab4d50af21303b78a')

_gitroot="git://github.com/Benjamin-Dobell/Heimdall.git"
_gitname="heimdall"

build() {
  cd ${srcdir}

  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin
  else
    git clone --depth=1 $_gitroot $_gitname
  fi  

  rm -rf $srcdir/$_gitname-build

  cp -r $srcdir/$_gitname $srcdir/$_gitname-build

  # Build libpit which is needed for compiling heimdall
  cd $srcdir/$_gitname-build/libpit
  ./configure --prefix=/usr
  make

  # Build heimdall command line tool
  cd ../heimdall/
  
  ./configure --prefix=/usr
  make

  # Build heimdall GUI front end
  if [ "$_frontend" = "y" ]; then
    cd ../heimdall-frontend/

    qmake OUTPUTDIR="${pkgdir}/usr/bin" heimdall-frontend.pro
    make
  fi
}

package() {
  cd ${srcdir}/$_gitname-build

  # Install heimdall command line tool
  cd heimdall/

  # Prevent make install from trying to reload udev
  # We'll do this the Arch way at package install time
  mv Makefile Makefile.orig
  sed -e 's/sudo service udev restart/echo sudo service udev restart/' <Makefile.orig >Makefile

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/lib/"
  install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/$_gitname/LICENSE"

  # Install heimdall GUI front end
  if [ "$_frontend" = "y" ]; then
    cd ../heimdall-frontend
    make DESTDIR="${pkgdir}/" install
    install -m644 -D "${srcdir}/heimdall.desktop" "${pkgdir}/usr/share/applications/heimdall.desktop"
  fi
}
