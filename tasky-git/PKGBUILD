# Maintainer: Robert Orzanna <orschiro@gmail.com>

_gitname=tasky
pkgname=${_gitname}-git
pkgver=27
pkgrel=1
pkgdesc="A command-line desktop client for Google Tasks."
arch=('any')
url="https://github.com/connermcd/tasky"
license=('GPL')
depends=('python2>=2.7' 'python2-google-api-python-client')
makedepends=('git')
provides=("$_gitname")
source=('git://github.com/connermcd/tasky.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	# Use the tag of the last commit
	local ver="$(git describe --long)"
	printf "%s" "${ver//-/.}"
}

prepare() {
	cd "$srcdir/$_gitname"		
	# Fixing shebang to python2
	sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" "${srcdir}/${_gitname}/src/tasky.py"
	# Adjusting tasky_dir to $HOME/.tasky
	sed -i -e "s|import os.path|import os|" "${srcdir}/${_gitname}/src/tasky.py"
	sed -i -e "s|os.path.dirname(os.path.realpath(__file__))|os.environ['HOME'] + '/.${_gitname}'|" "${srcdir}/${_gitname}/src/tasky.py"
	# writing keys.txt to $HOME/.tasky
	sed -i -e '1i\import os' "${srcdir}/${_gitname}/src/keys.py"
	sed -i -e "s|'keys.txt'|os.environ['HOME'] + '/.${_gitname}/keys.txt'|" "${srcdir}/${_gitname}/src/keys.py"
}

package() {
	cd "$srcdir/$_gitname"

	# create user directory
	install -dm755 "${HOME}/.${_gitname}/" 
	
	# copying binaries and libaries
	install -D -m755 "${srcdir}/${_gitname}/src/tasky.py" "${pkgdir}/usr/bin/${_gitname}"
	install -D -m644 "${srcdir}/${_gitname}/src/keys.py" "${pkgdir}/usr/bin/keys.py"	

	# copying readme information
	install -D -m644 "${srcdir}/${_gitname}/README.md" "${pkgdir}/usr/share/doc/${_gitname}/README"
}

# vim:set ts=2 sw=2 et: