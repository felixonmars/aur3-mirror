# Maintainer: Otto Allmendinger <otto.allmendinger@gmail.com>
# Contributor: Malte Rabenseifner <malte@zearan.de> 
# https://github.com/OttoAllmendinger/aur-packages

pkgname=murmur-ice
pkgver=1.2.7
pkgrel=1
pkgdesc="The voice chat application server for Mumble (ICE support)"
arch=('i686' 'x86_64')
url="http://mumble.sourceforge.net"
license=('GPL')
depends=('avahi' 'lsb-release' 'protobuf' 'qt4' 'icu' 'zeroc-ice')
makedepends=('boost' 'mcpp')
conflicts=('murmur' 'murmur-static' 'murmur-git')
backup=("etc/murmur.ini"
    "etc/logrotate.d/murmur"
    "etc/conf.d/murmur" )
install="murmur.install"
source=("https://github.com/mumble-voip/mumble/archive/${pkgver//rc/-rc}.tar.gz"
    "murmur.rc.d"
    "murmur.conf.d"
    "murmur.logrotate.d"
    "murmur.tmpfiles.d"
    "murmur.dbus.conf"
    "murmur.service")

build() {
    cd ${srcdir}/mumble-${pkgver//rc/-rc}

    qmake-qt4 main.pro CONFIG+="no-client"
    make release
}

package() {
    cd ${srcdir}/mumble-${pkgver//rc/-rc}

    sed -e "1i# vi:ft=cfg" \
        -e "s|database=|database=/var/lib/murmur/murmur.sqlite|" \
        -e "s|dbus=session|#dbus=system|" \
        -e "s|#logfile=murmur.log|logfile=/var/log/murmur/murmur.log|" \
        -e "s|#pidfile=|pidfile=/var/run/murmur/murmur.pid|" \
        -e "s|#uname=|uname=murmur|" \
        -i scripts/murmur.ini

    install -dm755 ${pkgdir}/var/lib/murmur
    install -dm755 ${pkgdir}/var/log/murmur
    install -dm755 ${pkgdir}/var/run/murmur
    install -dm755 ${pkgdir}/etc/rc.d

    install -Dm755 release/murmurd ${pkgdir}/usr/bin/murmurd
    install -Dm644 scripts/murmur.ini ${pkgdir}/etc/murmur.ini
    install -Dm755 ${srcdir}/murmur.rc.d ${pkgdir}/etc/rc.d/murmur
    install -Dm644 ${srcdir}/murmur.conf.d ${pkgdir}/etc/conf.d/murmur
    install -Dm644 ${srcdir}/murmur.dbus.conf \
                      ${pkgdir}/etc/dbus-1/system.d/murmur.conf
    install -Dm644 ${srcdir}/murmur.logrotate.d \
                      ${pkgdir}/etc/logrotate.d/murmur
    install -Dm644 README ${pkgdir}/usr/share/doc/murmur/README
    install -Dm644 man/murmurd.1 ${pkgdir}/usr/share/man/man1/murmurd.1
    install -Dm644 src/murmur/Murmur.ice ${pkgdir}/usr/share/murmur/Murmur.ice
    install -Dm644 ${srcdir}/murmur.tmpfiles.d \
                    ${pkgdir}/usr/lib/tmpfiles.d/murmur.conf
    install -Dm644 ${srcdir}/murmur.service \
                    ${pkgdir}/usr/lib/systemd/system/murmur.service
}
md5sums=('fc9e1b680fe17b7f98006824f18ca550'
         '91b23044151707b7612e5e27aff5027e'
         '11c1cc875e8021739ec2087252cf0533'
         'c56726cb267ead68f37f35c28e36e6fa'
         'e00d0d2610b77efebce5b3556609251f'
         'eddea4cdbd0bde4b960a67e16b5d5478'
         'ad00b707f0897b4670b10ae620c8abf4')
