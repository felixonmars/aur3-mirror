# Maintainer: Graziano Giuliani <graziano.giuliani@gmail.com>
pkgname=libbufr
pkgver=10.1.0
pkgrel=1
pkgdesc="NCEP library to encode or decode BUFR messages"
arch=(i686 x86_64)
url="http://www.nco.ncep.noaa.gov/sib/decoders/BUFRLIB/"
license=('Custom')
makedepends=(gcc-fortran)
source=('http://www.nco.ncep.noaa.gov/sib/decoders/BUFRLIB/BUFRLIB_v10-1-0.tar'
        'LICENSE')
md5sums=('f3bc3bea52047c0fe8003e15f01c0849'
         'f9f43492185682b834428d79f718f28e')

build() {
  cd "$srcdir"
  export CC=gcc
  export FC=gfortran
  export FCFLAGS=${CFLAGS}
  export AR=ar
  export ARFLAGS=-rv
  echo ${CC} ${CFLAGS} -c -DUNDERSCORE *.c
  ${CC} ${CFLAGS} -c -DUNDERSCORE *.c
  echo ${FC} ${FCFLAGS} -c -DLITTLE_ENDIAN *.F
  ${FC} ${FCFLAGS} -c -DLITTLE_ENDIAN *.F
  echo ${FC} ${FCFLAGS} -c *.f
  ${FC} ${FCFLAGS} -c *.f
  ${AR} ${ARFLAGS} libbufr.a *.o
}

package() {
  cd "$srcdir"
  mkdir -p "$pkgdir/usr/lib"
  mkdir -p "$pkgdir/usr/include"
  mkdir -p "$pkgdir/usr/share/licenses/libbufr"
  install -m 644 libbufr.a "$pkgdir/usr/lib"
  install -m 644 bufrlib.h "$pkgdir/usr/include"
  install -m 644 LICENSE "$pkgdir/usr/share/licenses/libbufr"
}

# vim:set ts=2 sw=2 et:
