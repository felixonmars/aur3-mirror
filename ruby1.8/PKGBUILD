# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: v01d
# Contributor: cs96and

pkgname=ruby1.8
_pkgname='ruby'
pkgver=1.8.7_p358
_pkgver="${pkgver/_/-}"
pkgrel=1
pkgdesc='An object-oriented language for quick and easy programming'
arch=('i686' 'x86_64' 'arm')
license=('custom')
url='http://www.ruby-lang.org/en/'
provides=('ruby=1.8.7')
options=('!emptydirs')
makedepends=('tk')
depends=('gdbm' 'db' 'openssl' 'zlib' 'readline')
optdepends=('tk: for Ruby/TK')

source=(ftp://ftp.ruby-lang.org/pub/ruby/1.8/$_pkgname-$_pkgver.tar.bz2 fix.patch)
md5sums=('de35f00997f4ccee3e22dff0f2d01b8a' 'f560402603b3dca36d4a6abfcb180cb9')

build() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  ./configure \
    --prefix=/opt/ruby1.8 \
    --disable-rpath \
    --enable-shared \
    --enable-pthread
  patch -Np1 < ${srcdir}/fix.patch
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  install -dm755 $pkgdir/usr/bin
  install -dm755 $pkgdir/usr/lib
  for i in erb irb rdoc ri ruby testrb; do
    ln -s /opt/ruby1.8/bin/$i $pkgdir/usr/bin/$i-1.8
  done
  ln -s /opt/ruby1.8/lib/libruby.so.1.8 $pkgdir/usr/lib/libruby.so.1.8
}
