# Maintainer: Houlala <houlala 'at' gmail 'dot' com>

pkgname=xnconvert
pkgver=1.22
pkgrel=1
pkgdesc="A cross-platform batch image-converter and resizer with a powerful and ease of use experience."
url="http://newsgroup.xnview.com/viewtopic.php?f=79&t=26121"

arch=('x86_64' 'i686')
license=('custom')
depends=('glib2' 'expat' 'libpng12')

install='xnconvert.install'

source=("http://download.xnview.com/XnConvert-linux.tgz")
md5sums=('282316a42cefaa1c81849122bccd7063')

if [ "$CARCH" = 'x86_64' ]; then
  source=("http://download.xnview.com/XnConvert-linux-x64.tgz")
  md5sums=('f5292a32fbef22990f0329472bfdfa79')
fi


package() {
  install -d -m755 "${pkgdir}/opt/${pkgname}"
  install -d -m755 "${pkgdir}/usr/bin"
  install -d -m755 "${pkgdir}/usr/share/applications"

  cp -R "${srcdir}/XnConvert"/* "${pkgdir}/opt/${pkgname}"
  ln -s "/opt/${pkgname}/xnconvert.sh" "${pkgdir}/usr/bin/${pkgname}"

  cat <<-_EOF_ > "${pkgdir}/usr/share/applications/${pkgname}.desktop"
[Desktop Entry]
Encoding=UTF-8
Terminal=0
Exec=${pkgname}
Icon=/opt/${pkgname}/xnconvert.png
Type=Application
Categories=Graphics;
StartupNotify=true
Name=XnConvert
GenericName=XnConvert
MimeType=image/bmp;image/gif;image/x-portable-bitmap;image/x-portable-pixmap;image/x-psd;image/x-tga;image/x-xbitmap;image/tiff;image/jpeg;image/x-psd;image/png;image/x-icon;image/x-xpixmap;image/svg+xml;
_EOF_

  install -D -m644 "${srcdir}/XnConvert/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
