# Maintainer: Chris Molozian (novabyte) <chris DOT molozian AT gmail DOT com>
# Contributor: Sanjuro Makabe (itti) <vuck AT gmx DOT de>
pkgname=gradle
pkgver=1.1
pkgrel=1
pkgdesc="A powerful build system for the JVM"
arch=('any')
url="http://www.gradle.org/"
license=('APACHE')
depends=('java-runtime')
source=(http://services.gradle.org/distributions/$pkgname-$pkgver-bin.zip gradle.sh)

package(){
    # install profile.d script
    install -dm755 "${pkgdir}"/etc/profile.d
    install -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/etc/profile.d/"
    cd "${srcdir}/${pkgname}-${pkgver}"

    # create the necessary directory structure
    mkdir -p "${pkgdir}/usr/share/java/${pkgname}/bin"
    mkdir -p "${pkgdir}/usr/share/java/${pkgname}/lib/plugins"
    mkdir -p "${pkgdir}/usr/share/java/${pkgname}/init.d"
    mkdir -p "${pkgdir}/usr/bin"

    # copy across jar files
    install -Dm644 lib/*.jar "${pkgdir}/usr/share/java/${pkgname}/lib" || return 1
    install -Dm644 lib/plugins/*.jar "${pkgdir}/usr/share/java/${pkgname}/lib/plugins" || return 1

    # copy across supporting text documentation and scripts
    install -m644 NOTICE "${pkgdir}/usr/share/java/${pkgname}" || return 1
    install -m644 LICENSE "${pkgdir}/usr/share/java/${pkgname}" || return 1
    install -m644 *.txt "${pkgdir}/usr/share/java/${pkgname}" || return 1
    install -m644 *.html "${pkgdir}/usr/share/java/${pkgname}" || return 1
    install -m755 bin/gradle "${pkgdir}/usr/share/java/${pkgname}/bin" || return 1
    install -m644 init.d/*.* "${pkgdir}/usr/share/java/${pkgname}/init.d" || return 1

    # link gradle script to /usr/bin
    ln -s /usr/share/java/${pkgname}/bin/${pkgname} ${pkgdir}/usr/bin
}

md5sums=('21f623813a14e3a3784d7e8b19a538b9'
         'd73a375d2796dfe4ec170f2f5af3a0c9')
