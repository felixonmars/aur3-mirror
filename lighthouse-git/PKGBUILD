pkgname=lighthouse-git
pkgver=r105.f0a3fe6
pkgrel=2
pkgdesc='lighthouse - A simple scriptable popup dialog to run on X.'
arch=('i686' 'x86_64')
url='https://github.com/emgram769/lighthouse'
license=('MIT')
depends=('libx11' 'libxcb' 'cairo')
optdepends=('python2: for the default cmd.py')
source=('git://github.com/emgram769/lighthouse.git' 'lighthouse-git.install')
md5sums=('SKIP' 'e2c460679493516b86974710c5453225')
install='lighthouse-git.install'

pkgver() {
    cd "$srcdir/lighthouse"
    ( set -o pipefail
      git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build(){
    cd "${srcdir}/lighthouse"
    make
}

package() {
    cd "${srcdir}/lighthouse"
    mkdir -p ${pkgdir}/usr/{bin,share/lighthouse}
    cp lighthouse "${pkgdir}/usr/bin"
    cp -ir config/* "${pkgdir}/usr/share"
    chmod +x "${pkgdir}/usr/share/lighthouse/cmd"

    # make the default config point to cmd in the /usr/share directory
    # so people don't have to copy cmd before the application can run.
    config="${pkgdir}/usr/share/lighthouse/lighthouserc"
    sed 's/~\/\.config/\/usr\/share/' $config > "${config}.new"
    mv "${config}.new" "${config}"
}
