# Maintainer:  cuihao <cuihao dot leo at gmail dot com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

# Original PKGBUILD (extra/thunar):
# $Id: PKGBUILD 149974 2012-02-11 23:31:34Z allan $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar-devel
_pkgname=thunar
pkgver=1.4.0
pkgrel=1
pkgdesc="Modern file manager for Xfce (Development Version)"
arch=('i686' 'x86_64')
url="http://thunar.xfce.org"
license=('GPL2' 'LGPL2.1')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'gtk2' 'exo' 'libxfce4util-devel>=4.9.0' 'libxfce4ui')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'polkit-gnome: for mounting internal partitions (needs root password)'
            'xfce4-panel-devel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman-devel: manages removable devices'
            'thunar-archive-plugin-devel: create and deflate archives'
            'thunar-media-tags-plugin-devel: view/edit id3/ogg tags')
options=('!libtool')
install=$_pkgname.install
source=(http://archive.xfce.org/src/xfce/${_pkgname}/1.4/Thunar-$pkgver.tar.bz2)
sha1sums=('104b25b38130e008df2116e83e6ab20cd33e4561')

groups=('xfce4-devel')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" "$_pkgname-git")

build() {
  cd "$srcdir/Thunar-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-dbus \
    --enable-startup-notification \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/Thunar-$pkgver"

  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    "$pkgdir/usr/share/applications/Thunar-folder-handler.desktop"
}

# vim:set ts=2 sw=2 et:
