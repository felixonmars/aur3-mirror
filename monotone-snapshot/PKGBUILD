# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>

pkgname=monotone-snapshot
pkgver=1.0.90
pkgrel=2
_snapshot=f7cea8d4e635f311b8c85257b8bfbaf38b961407
_botan_ver=1.10
pkgdesc='A modern distributed version control system, like hg, darcs, or git (dev snapshot)'
url='https://code.monotone.ca/p/monotone'
license=('GPL')
arch=('i686' 'x86_64')
depends=('pcre' 'lua' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost' 'botan')
optdepends=('python2: for CIA bot client script')
source=(monotone.zip::'https://code.monotone.ca/p/monotone/source/download/${_snapshot}')
sha256sums=('627f9a0f62730f1914b7e9312010d28ec28e9c6008065e091b5a21215e1f72b7')

build() {
    cd "$srcdir"
    aclocal
    autoreconf --install
    botan_LIBS="/usr/lib/libbotan-${_botan_ver}.a -lrt" \
    botan_CFLAGS="-I/usr/include/botan-${_botan_ver}/" \
    ./configure --prefix=/usr --sysconfdir=/etc --disable-nls
    make
}

package() {
    cd "$srcdir"
    make DESTDIR="$pkgdir" install
    install -d "${pkgdir}/usr/share/vim/vimfiles/plugin/"
    install -Dm644 contrib/monotone.zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_mtn"
    install -Dm644 contrib/{mtncommit,basicio}.vim "${pkgdir}/usr/share/vim/vimfiles/plugin/"
}
