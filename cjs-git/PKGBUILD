# Maintainer: twa022 <twa022 at gmail dot com>
# Based on gjs PKGBUILD

_pkgname=cjs
pkgname=${_pkgname}-git
pkgver=27.1fa8ca3
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=(i686 x86_64)
url="https://github.com/linuxmint/cjs"
license=(GPL)
depends=('cairo' 'gobject-introspection' 'js185' 'dbus-glib')
makedepends=('gnome-common')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
options=('!libtool')
source=(${_pkgname}::git+https://github.com/linuxmint/cjs.git)
sha256sums=(SKIP)

pkgver() {
  cd ${srcdir}/${_pkgname}
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd ${srcdir}/${_pkgname}

  # Python 2 fix
  sed -i 's:\(^#!.*python\)$:\12:' scripts/make-tests

  ./autogen.sh --prefix=/usr --disable-static
  make
}

package() {
  cd ${srcdir}/${_pkgname}
  make DESTDIR="$pkgdir" install
}
