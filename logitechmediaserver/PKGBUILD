# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=logitechmediaserver
pkgver=7.8.0
pkgrel=3
pkgdesc='Streaming audio server'
url='http://downloads.slimdevices.com/'
license=('GPL' 'custom')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
makedepends=('nasm' 'yasm' 'rsync' 'subversion')
[[ $CARCH = x86_64 ]] && optdepends=(lib32-{glibc,gcc-libs}': transcoding')
depends=('perl<5.21')
source=("http://downloads.slimdevices.com/LogitechMediaServer_v${pkgver}/logitechmediaserver-${pkgver}.tgz"
        'CPAN.upstream::svn+http://svn.slimdevices.com/repos/slim/7.8/trunk/vendor/CPAN/'
        'http://search.cpan.org/CPAN/authors/id/S/SM/SMUELLER/Class-XSAccessor-1.19.tar.gz'
        'perl-recent.patch'
        'service')
sha1sums=('9388eb200a48a3a04511e990686fee22a0d8bb5f'
          'SKIP'
          '77a6a7e2893f93666a6ad1438efd17bf65bbbafc'
          '0c9a4d5b7b1f49bd37af5390e1ae8998be115607'
          '734f84233fc7ab350b169f88aa5bb0cf3decad42')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	rm -r CPAN/arch Bin/{powerpc,sparc}-linux
	case $CARCH in
		x86_64) rm -r Bin/arm-linux ;;
		i686) rm -r Bin/arm-linux ;;
		arm*) rm -r Bin/i386-linux ;;
	esac
	rm ../CPAN.upstream/Compress-Raw-Zlib-2.033.tar.gz CPAN/Compress/Raw/Zlib.pm 
	cp ../Class-XSAccessor-1.19.tar.gz ../CPAN.upstream
	patch -p1 -i ../perl-recent.patch
	sed \
		-e '/build_module Compress-Raw-Zlib/c true' \
		-e 's/XSAccessor-1.05/XSAccessor-1.19/g' \
		-e '/RUN_TESTS=1/c RUN_TESTS=0' \
		-e 's/perl5.12/perl/g' \
		-e 's/5.12/5.20/g' \
		-i ../CPAN.upstream/buildme.sh
}

build() {
	cd "${srcdir}/CPAN.upstream"
	export CFLAGS=${CFLAGS//-O2/-Os}
	sh buildme.sh
	rm -f build/5.20/lib/perl5/*-linux-thread-multi/XML/Parser/Expat.pm
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}"
	cp -a ../CPAN.upstream/build/5.20/lib/perl5/*linux*/* "${pkgdir}/opt/${pkgname}/CPAN"
	ln -s "/opt/${pkgname}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
