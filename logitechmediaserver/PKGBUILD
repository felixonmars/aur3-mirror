# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=logitechmediaserver
pkgver=7.7.2
pkgrel=5
pkgdesc='Streaming audio server supported by Logitech'
url='http://www.mysqueezebox.com/download'
license=('GPL' 'custom')
arch=('i686' 'x86_64')
makedepends=('nasm' 'yasm' 'rsync' 'wget')
[[ $CARCH = x86_64 ]] && optdepends=('lib32-glibc: transcoding' 'lib32-gcc-libs: transcoding')
depends=('perl>5.15' 'perl<5.17')
source=("http://downloads.slimdevices.com/LogitechMediaServer_v${pkgver}/logitechmediaserver-${pkgver}.tgz"
        'perl-recent.patch'
        'service'
        'conf.d'
        'rc.d')
backup=('etc/conf.d/logitechmediaserver')
sha1sums=('081259a0795ddd132351f4c95342e7371d90d4f5'
          '2e9989b5ea49e9e3b0fa2e16590ad3a6957f7b9c'
          '6e3428f9e798bf2403f46d7d20020eb985832356'
          '80d3658796ba7241b497e5fce93813423c57e06e'
          '9a98d601faff6345dfeddae16d1f10547c2aacba')

install=install

build_cpan() {
	svnurl=svn.slimdevices.com/repos/slim/7.7/trunk/vendor/CPAN/
	wget -r --no-parent http://${svnurl} --reject 'mysql-*'
	pushd ${svnurl}

	wget http://search.cpan.org/CPAN/authors/id/S/SM/SMUELLER/Class-XSAccessor-1.13.tar.gz
	sed \
		-e 's/XSAccessor-1.05/XSAccessor-1.13/g' \
		-e '/RUN_TESTS=1/c RUN_TESTS=0' \
		-e 's/perl5.12/perl/g' \
		-e 's/5.12/5.16/g' \
		-i buildme.sh
	sh buildme.sh

	popd
	cp -r ${svnurl}build/5.16/lib/perl5/*-linux-thread-multi/* CPAN/
	cp -r ${svnurl}build/arch CPAN/
	rm -fr svn.slimdevices.com
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"-*

	build_cpan
	patch -p1 -i ../perl-recent.patch

	rm -r CPAN/arch/5.{8,10,12,14}
	#rm -r CPAN/arch/5.16/{arm-linux-gnueabi-thread-multi-64int,i386-linux-thread-multi-64int,powerpc-linux-thread-multi-64int}
	#[[ ${CARCH} = i686   ]] && rm -r CPAN/arch/5.16/x86_64-linux-thread-multi || true
	#[[ ${CARCH} = x86_64 ]] && rm -r CPAN/arch/5.16/i386-linux-thread-multi || true
	rm -r Bin/{arm-linux,darwin,i386-freebsd-64int,powerpc-linux,sparc-linux}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"-*

	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}"

	ln -s /opt/"${pkgname}"/License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	install -Dm644 ../conf.d "${pkgdir}/etc/conf.d/${pkgname}"
	install -Dm755 ../rc.d "${pkgdir}/etc/rc.d/${pkgname}"
}
