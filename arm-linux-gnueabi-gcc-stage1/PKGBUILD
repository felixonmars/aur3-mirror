# Maintainer: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>

_target="arm-linux-gnueabi"
pkgname=${_target}-gcc-stage1
pkgver=4.7.0
pkgrel=1
pkgdesc="The GNU Compiler Collection. Stage 1 for toolchain building (${_target})"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=("${_target}-binutils" 'libmpc' 'libelf')
options=(!libtool !emptydirs zipman docs !strip)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
       gcc-4.7.0-cloog-0.17.patch)

build()
{
  cd $srcdir/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  # compatibility with latest cloog
  patch -p1 -i ${srcdir}/gcc-4.7.0-cloog-0.17.patch

  [[ -d gcc-build ]] || mkdir gcc-build
  cd gcc-build

  [ $NOEXTRACT -eq 1 ] || ../configure \
        --prefix=/usr \
        --program-prefix=${_target}- \
        --target=${_target} \
        --host=$CHOST \
        --build=$CHOST \
        --disable-shared \
        --disable-nls \
        --disable-threads \
        --enable-languages=c \
        --enable-multilib \
        --with-local-prefix=/usr/${_target} \
        --with-sysroot=/usr/${_target} \
        --with-as=/usr/bin/${_target}-as \
        --with-ld=/usr/bin/${_target}-ld \
        --enable-softfloat \
        --with-float=soft \
        --with-newlib \
        --disable-libgomp \
        --enable-interwork \
        --enable-addons

  make all-gcc all-target-libgcc
}

package()
{
  cd $srcdir/gcc-$pkgver/gcc-build

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  make DESTDIR=$pkgdir install-gcc install-target-libgcc

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info

  cp -r $pkgdir/usr/libexec/* $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/libexec

  # strip it manually
  strip $pkgdir/usr/bin/* 2>/dev/null || true
  find $pkgdir/usr/lib -type f -exec /usr/bin/${_target}-strip --strip-unneeded {} \; 2>/dev/null || true
}
md5sums=('2a0f1d99fda235c29d40b561f81d9a77'
         '575f7d17b022e609447a590e481b18b5')
