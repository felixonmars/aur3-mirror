# Maintainer: Paul Weingardt <paulweingardt@web.de>
# Contributor: TDY <tdy@gmx.com>

pkgname=git-cola-git
pkgver=v1.8.5.3.g62b97ce
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('i686' 'x86_64')
url="http://cola.tuxfamily.org/"
license=('GPL')
depends=('git' 'python2-pyqt4>=4.4')
makedepends=('asciidoc' 'docbook-xsl' 'python2-sphinx>=1.1.3' 'rsync' 'xmlto')
optdepends=('python2-pyinotify>=0.7.1: for inotify support'
            'tk: to use the built-in ssh-askpass handler')
options=('!makeflags')
provides=('git-cola')
conflicts=('git-cola')

source=("$pkgname"::'git+https://github.com/davvid/git-cola.git')
sha512sums=('SKIP')

pkgver() {
    cd "$pkgname"
    # Use the tag of the last commit
    local ver="$(git describe --long)"
    printf "%s" "${ver//-/.}"
}

build() {
    cd "$pkgname"
    sed -i 's,= python,&2,' Makefile
    sed -i 's,sphinx-build,&2,' share/doc/git-cola/Makefile
    sed -i 's,env python,&2,' setup.py
    make all doc html
}

package() {
    cd "$pkgname"
    make PREFIX=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}
