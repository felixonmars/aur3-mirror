# Maintainer: Paul Weingardt <paulweingardt@web.de>
# Contributor: TDY <tdy@gmx.com>

pkgname=git-cola-git
pkgver=20120318
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('i686' 'x86_64')
url="http://cola.tuxfamily.org/"
license=('GPL')
depends=('git' 'python2-pyqt>=4.3')
makedepends=('asciidoc' 'docbook-xsl' 'python2-sphinx>=1.1.3' 'rsync' 'xmlto')
optdepends=('python2-pyinotify>=0.7.1: for inotify support'
            'tk: to use the built-in ssh-askpass handler')
options=('!makeflags')
provides=('git-cola')
conflicts=('git-cola')

_gitroot=git://github.com/davvid/git-cola.git
_gitname=git-cola

build() {
  cd "$srcdir"

  if [[ -d $_gitname ]]; then
    cd $_gitname && git pull origin && cd ..
  else
    git clone $_gitroot
  fi

  rm -rf $_gitname-build
  git clone $_gitname $_gitname-build
  cd $_gitname-build

  sed -i 's,= python,&2,' Makefile
  sed -i 's,sphinx-build,&2,' share/doc/git-cola/Makefile
  sed -i 's,env python,&2,' setup.py
  make all doc html
}

package() {
  cd "$srcdir/$_gitname-build"
  make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}
