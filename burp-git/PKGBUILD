# Contributor: Dave Reisner <d@falconindy.com>

pkgname=burp-git
pkgver=20110806
pkgrel=1
pkgdesc="A simple AUR uploader"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/burp"
license=('MIT')
depends=('curl')
makedepends=('git' 'perl')
provides=('burp')
conflicts=('burp')

_gitroot="git://github.com/falconindy/burp.git"
_gitname="burp"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
