# Maintainer: willemw <willemw12@gmail.com>
# Contributor: Frikilinux <frikilinux at frikilinux.com.ar>

pkgname=qeven
pkgver=0.3.2
pkgrel=3
pkgdesc="Qt Easy Video Encoder. Uses MEncoder and MPlayer."
arch=('any')
url="http://qeven.wordpress.com/"
license=('GPL3')
depends=('mencoder' 'mplayer' 'qt4')
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}_$pkgver.tar.bz2
        qeven.desktop)
sha256sums=('8c80b97e17f8eda60c0d5af71837dd6c9d9869dceace590b189a0030a8fc3366'
            '78d6b5c43206ec74fd379077fb764d75856163b2dc54e4f25e11b579f84ef224')

build() {
  cd "$srcdir/${pkgname}_$pkgver"
  qmake-qt4 QEVEN.pro
  make
}

package() {
  install -Dm644 qeven.desktop "$pkgdir/usr/share/applications/qeven.desktop"

  cd "${pkgname}_$pkgver"

  install -Dm755 QEVEN "$pkgdir/usr/bin/qeven"

  install -Dm644 "Image/qeven(64x64).png" "$pkgdir/usr/share/pixmaps/qeven.png"

  install -dm0755 "$pkgdir/usr/share/qeven/"{fonts,translation}
  install -m644 Fonts/* "$pkgdir/usr/share/qeven/fonts/"
  install -m644 translation/*.qm "$pkgdir/usr/share/qeven/translation/"
}

