# Maintainer:	Jesse Jaara		<gmail.com: jesse.jaara>
# Contributor:	Alessandro Pazzaglia	<gmail.com: jackdroido>

pkgname=gstreamer-vaapi
pkgver=0.4.1
pkgrel=1
pkgdesc="A collection of VA-API based plugins for GStreamer"
url="http://gitorious.org/vaapi/gstreamer-vaapi"
arch=('i686' 'x86_64')
license=('GPL2')
depends=("gstreamer0.10-bad" 'libva')
makedepends=('gtk-doc')
optdepends=('xvba-video: for ATI chipsets'
	    'libva-vdpau-driver: for nVidia chipsets'
	    'libva-intel-driver: For Intel chipsets')
source=("http://www.freedesktop.org/software/vaapi/releases/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
options=('!libtool')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  LIBS=-ldl ./configure  --prefix=/usr --disable-static

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

md5sums=('57f895bc98c617de945c026f132daa11')
