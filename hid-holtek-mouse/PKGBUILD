pkgname=hid-holtek-mouse
_srcname=linux-3.10
pkgver=3.10.5
pkgrel=1
pkgdesc="Holtek mouse kernel module ( https://bugs.archlinux.org/task/33322 )"
arch=('i686' 'x86_64')
url="https://patchwork.kernel.org/patch/2595331/"
license=('GPL2')
depends=('linux>=3.10' 'linux<3.11')
makedepends=('linux-headers>=3.10' 'linux-headers<3.11')
options=('!strip')
install=$pkgname.install
changelog=ChangeLog
source=("http://www.kernel.org/pub/linux/kernel/v3.x/${_srcname}.tar.xz"
        "http://www.kernel.org/pub/linux/kernel/v3.x/patch-${pkgver}.xz"
	"driver_holtek.patch::https://patchwork.kernel.org/patch/2595331/raw/"
)
md5sums=('4f25cd5bec5f8d5a7d935b3f2ccb8481'
  '6366a8d4b0429ab6836c296ba298fb0e'
  '9c59fb9bf5c204fb021ecde391e60f29')

#mod_dir=/lib/modules/`uname -r`/updates/drivers/hid/
_uname=${pkgver}-${pkgrel}-ARCH
_mod_dir=/lib/modules/${_uname}/updates/drivers/hid/

prepare() {
  cd ${srcdir}/${_srcname}/
  # add upstream patch
  patch -p1 -i "${srcdir}/patch-${pkgver}"
  # add holtek mouse support
  patch -p1 -i ${srcdir}/driver_holtek.patch
}

build() {
  cd ${srcdir}/${_srcname}/drivers/hid
  make -C /usr/src/linux-${_uname}/ M=`pwd` hid-holtek-mouse.ko
  gzip -9 hid-holtek-mouse.ko
}

package() {
  cd ${srcdir}/${_srcname}/drivers/hid
  mkdir -p ${pkgdir}/${_mod_dir}
  install -D -m 0644 hid-holtek-mouse.ko.gz ${pkgdir}/${_mod_dir}/hid-holtek-mouse.ko.gz
}
