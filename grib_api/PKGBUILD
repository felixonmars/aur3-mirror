# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Graziano Giuliani <graziano.giuliani@poste.it>

pkgname=grib_api
pkgver=1.9.16
pkgrel=2
pkgdesc="A program interface for encoding and decoding GRIB messages"
arch=('i686' 'x86_64')
url="http://www.ecmwf.int/products/data/software/grib_api.html"
license=('LGPL3' 'GPL3')
depends=('openjpeg' 'libpng' 'python2')
makedepends=('gcc-fortran' 'python2' 'python-numpy')
optdepends=('grib_def: definition files')
source=(${url/g*/}/download/software_files/$pkgname-$pkgver.tar.gz)
md5sums=('f1288627031c97fa1631fd5a63e3bbb3')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i configure -e 's|share/samples|share/grib_api/samples|'
  sed -i configure -e 's|python python2|python2 python|'
  ./configure F77="gfortran" FC="gfortran" CPPFLAGS="$CPPFLAGS -fPIC" \
     --prefix=/usr --enable-python --with-png-support \
     --with-netcdf=/usr --datadir=/usr/share/$pkgname \
     --datarootdir=/usr/share/$pkgname/definitions || return 1
  sed -i  ifs_samples/grib1/Makefile \
    -e 's|/usr/ifs_samples/grib1|/usr/share/grib_api/ifs_samples/grib1|'
  sed -i  ifs_samples/grib1_mlgrib2/Makefile \
 -e 's|/usr/ifs_samples/grib1_mlgrib2|/usr/share/grib_api/ifs_samples/grib1_mlgrib2|'
  sed -i  ifs_samples/grib1_mlgrib2_ieee64/Makefile \
 -e 's|/usr/ifs_samples/grib1_mlgrib2_ieee64|/usr/share/grib_api/ifs_samples/grib1_mlgrib2_ieee64|'
  sed -i tools/grib1to2.txt -e 's|/bin/ksh|/bin/bash|'
  make || return 1
  make DESTDIR="$pkgdir" install || return 1
  rm -rf "$pkgdir"/usr/share/$pkgname/definitions
}
