# Maintainer: Link Dupont <link.dupont@gmail.com>
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake1.10
_pkgname=automake
pkgver=1.10.3
pkgrel=2
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
provides=('automake=1.10')
install=automake.install
source=(ftp://ftp.gnu.org/gnu/$_pkgname/$_pkgname-$pkgver.tar.gz)
md5sums=('03bc9ebfa805f9ee5635f1f53fa1fa5f')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # remove the symlinks
  rm ${pkgdir}/usr/bin/{automake,aclocal}

  # make the doc dir version-specific
  mv ${pkgdir}/usr/share/doc/$_pkgname ${pkgdir}/usr/share/doc/$_pkgname-$pkgver

  # make info files version specific
  mv ${pkgdir}/usr/share/info/$_pkgname.info ${pkgdir}/usr/share/info/$_pkgname-1.10.info
  mv ${pkgdir}/usr/share/info/$_pkgname.info-1 ${pkgdir}/usr/share/info/$_pkgname-1.10.info-1
  mv ${pkgdir}/usr/share/info/$_pkgname.info-2 ${pkgdir}/usr/share/info/$_pkgname-1.10.info-2
}
