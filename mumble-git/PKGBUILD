# $Id$
# Maintainer:  Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lauri Niskanen <ape@ape3000.com>

# If you want support for your G15 Keyboard, please add 'g15daemon'
# to the depends and delete "no-g15" in the configure line below

pkgname=mumble-git
pkgver=20120707
pkgrel=1
pkgdesc="A voice chat application similar to TeamSpeak"
url="http://mumble.sourceforge.net/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('qt' 'speex' 'lsb-release' 'libxi' 'avahi' 'libsndfile'
         'protobuf' 'libpulse' 'celt' 'opus')
makedepends=('git' 'boost' 'mesa')
# optdepends=('portaudio: for portaudio back-end' 'g15daemon: G15 Keyboard support')
options=('!libtool')
conflicts=('mumble')
provides=('mumble')
install=mumble.install

_gitroot="git://github.com/mumble-voip/mumble.git"
_gitname="mumble"

build() {
    cd ${srcdir}
    msg "Connecting to GIT server..."

    if [[ -d ${_gitname} ]]; then
        cd ${_gitname} && git pull origin && cd ..
        msg "The local files are updated."
    else
        git clone ${_gitroot}
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf ${_gitname}-build
    git clone ${_gitname}{,-build}
    cd ${_gitname}-build

    # Fetch source of Opus codec
    git submodule init
    git submodule update opus-src

    # Building mumble
    /usr/bin/qmake main.pro \
      CONFIG+="no-bundled-celt no-bundled-speex no-speechd no-g15 no-xevie no-server \
      no-embed-qt-translations no-update packaged" \
      DEFINES+="PLUGIN_PATH=/usr/lib/mumble"
    # Build fix, thanks IncredibleLaser
    sed -i "s/CELT_SET_VBR_RATE/CELT_SET_BITRATE/g" src/mumble/AudioInput.cpp
    make release
}

package() {
    cd ${srcdir}/${_gitname}-build

    # Bin stuff
    install -m755 -D ./release/mumble $pkgdir/usr/bin/mumble
    install -m755 -D ./scripts/mumble-overlay $pkgdir/usr/bin/mumble-overlay

    # Lib stuff
    install -m755 -D ./release/libmumble.so.1.2.4 $pkgdir/usr/lib/mumble/libmumble.so.1.2.4
    ln -s libmumble.so.1.2.4 $pkgdir/usr/lib/mumble/libmumble.so
    ln -s libmumble.so.1.2.4 $pkgdir/usr/lib/mumble/libmumble.so.1
    ln -s libmumble.so.1.2.4 $pkgdir/usr/lib/mumble/libmumble.so.1.2
    ln -s libopus.so.0.0.0 $pkgdir/usr/lib/libopus.so.1
    install -m755 -D ./release/plugins/liblink.so $pkgdir/usr/lib/mumble/liblink.so
    install -m755 -D ./release/plugins/libmanual.so $pkgdir/usr/lib/mumble/libmanual.so

    # Other stuff
    install -m644 -D ./scripts/mumble.desktop $pkgdir/usr/share/applications/mumble.desktop
    install -m755 -d $pkgdir/usr/share/man/man1
    install -m644 -D ./man/mum* $pkgdir/usr/share/man/man1/
    install -m644 -D ./icons/mumble.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/mumble.svg
}
