# Maintainer: Cedric Girard <girard.cedric@gmail.com>
# Contributor: foxbunny <bg.branko@gmail.com>

pkgname=dispcalgui
pkgver=0.9.9.1
pkgrel=1
pkgdesc="A GUI frontend for several utilities from the open source color management system Argyll CMS"
arch=('i686' 'x86_64')
url="http://hoech.net/dispcalGUI/"
license=('GPL3')
makedepends=('libx11' 'libxrandr' 'libxinerama' 'libxxf86vm' 'setuptools' 'xdg-utils')
depends=('argyllcms>=1.3.0' 'python2>=2.5' 'wxpython>=2.8.6' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('python2-numpy: to enable curve viewer'
            'gksu: For running as root')
groups=('painters-studio-tools' 'painters-studio-extras' 'painters-studio-complete')
install=${pkgname}.install

#you may need to download this by hand (with wget) if curl is your download tool in makepkg
source=("http://dispcalgui.hoech.net/download/dispcalGUI.tar.gz" ${pkgname}.desktop)

md5sums=('c57365cdfaa85baed5cbc43923a66692'
         'a65019defc10243c7e0fd2925405d26e')

package() {
  cd "${srcdir}"/dispcalGUI-${pkgver}

  find . -name "*.py" -print0 | xargs -0 sed -i 's!/usr/bin/env python!/usr/bin/env python2!g'

  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 \
    "${srcdir}"/dispcalgui.desktop \
    "${pkgdir}"/usr/share/applications/${pkgname}.desktop

  #udev rules are shipped with argyllcms
  rm -rf "${pkgdir}"/etc/udev/
}

# vim:set ts=2 sw=2 et:
