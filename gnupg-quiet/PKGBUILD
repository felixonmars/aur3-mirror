# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=gnupg-quiet
_pkgname=gnupg
_patchversion="1"
pkgver=2.0.20
pkgrel=2
pkgdesc='Complete and free implementation of the OpenPGP standard. Includes patch to sign quietly (no output).'
url='http://www.gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('curl: gpg2keys_curl'
'libldap: gpg2keys_ldap'
'libusb-compat: scdaemon')
makedepends=('curl' 'libldap' 'libusb-compat')
depends=('bzip2' 'libksba' 'libgcrypt' 'pth' 'libassuan' 'readline' 'pinentry' 'dirmngr')
source=("ftp://ftp.gnupg.org/gcrypt/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"{,.sig}
"https://github.com/graysky2/gnupg_quiet_sign/archive/v${_patchversion}.tar.gz"
'protect-tool-env.patch')
sha256sums=('6e949b7f062cab8a3cf0910f91ecf04cabaad458c0aeeec66298651b8b04b79a'
            'SKIP'
            'dd8c6fa055dd7116cfe856dc70a569ae3c5c98722f76ff4157ba7f86aff55984'
            'd196591b3777383390374a81ffa0c8ea9ca809ddb465457ab1a7e73d3fd8c1eb')

install=$pkgname.install

conflicts=('gnupg2')
provides=("gnupg2=${pkgver}" "gnupg=${pkgver}")
replaces=('gnupg2')

build() {
	cd "${_pkgname}-${pkgver}"

	patch -p1 -i ../protect-tool-env.patch # FS#31900
	patch -p1 -i "${srcdir}/gnupg_quiet_sign-$_patchversion/sign_quietly.patch"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/gnupg \
		--enable-maintainer-mode \
		--enable-symcryptrun \
		--enable-gpgtar \

	make
}

check() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make check
}

package() {
	cd "${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -s gpg2 "${pkgdir}"/usr/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
	rm "${pkgdir}/usr/share/gnupg/com-certs.pem" # FS#33059
}
