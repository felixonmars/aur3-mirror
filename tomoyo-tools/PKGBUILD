# $Id: PKGBUILD 106282 2014-02-26 11:56:44Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jamie Nguyen <jamie AT tomoyolinux.co.uk>

pkgname=tomoyo-tools
_basever=2.5.0
_timestamp=20140105
_file=53357
pkgver=2.5.0.20140105
pkgrel=3
pkgdesc='TOMOYO Linux userspace tools for Linux kernels >=3.2'
arch=('i686' 'x86_64')
url='http://tomoyo.sourceforge.jp'
#url='http://sourceforge.jp/projects/tomoyo/releases/?package_id=9818'
license=('GPL')
depends=('ncurses' 'linux>=3.2')
install=tomoyo-tools.install
source=("http://sourceforge.jp/frs/redir.php?f=/tomoyo/${_file}/${pkgname}-${_basever}-${_timestamp}.tar.gz"{,.asc})
sha256sums=('f4939d4bc8792b3993307034d4ecd29e318972caad51ffa8acaf408cb33a2f61'
            'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}"
  sed -i \
    -e 's|usr/sbin|usr/bin|g' \
    -e 's|sbin|usr/bin|g' \
    usr_lib_tomoyo/init_policy.c
}

build() {
  cd "${srcdir}/${pkgname}"
  make USRSBINDIR=/usr/bin SBINDIR=/usr/bin
}

package() {
  cd "${srcdir}/${pkgname}"
  make USRSBINDIR=/usr/bin SBINDIR=/usr/bin INSTALLDIR="${pkgdir}" install
}
