# Contributor: Patrick Kelly <kameo76890 at gmail dot com>
# Contributor:  Georgios  Tsalikis <aliverius somewhere near tsalikis and a net>
# Contributor: Julian Leyh <julian@vgai.de>

pkgname=gtkada
pkgver=2.18.0
pkgrel=5
pkgdesc="GtkAda is a Gtk2+ binding for Ada using the OOP and other features of this programming language"
arch=('i686' 'x86_64')
url="http://libre.adacore.com/libre/tools/GtkAda/"
license=('GPL')
depends=("gcc-ada" "gtk2")
source=(http://sourceforge.net/projects/gnat-gpl/files/2011/$pkgname-gpl-$pkgver-src.tgz
        opengl-fix.patch
        X11_Xrender.patch
        fix_glib_include.patch)
md5sums=('f5f9c8cb540efd4b261d4914b3410433'
         'aed777b79514a91a51aae4456e2cc2ee'
         '22ae4b1487746d4ee04e6aa85c5d6e6e'
         'cf3a50a9bed53bae86788b3617173c38')

build() {
  cd "$srcdir/$pkgname-gpl-$pkgver-src"

  patch -Np0 -i "$srcdir/opengl-fix.patch"
  patch -Np0 -i "$srcdir/X11_Xrender.patch"
  patch -Np0 -i "$srcdir/fix_glib_include.patch"

  ./configure --prefix=/usr
  make -j1  # It doesn't build with -jn where n>1
}

package() {
  cd $srcdir/$pkgname-gpl-$pkgver-src
  make -j1 prefix=$pkgdir/usr install 
}
