# Maintainer: Bjoern Franke <bjo@nord-west.org>

pkgname=tvheadend

pkgver=4.0.2
pkgrel=1
pkgdesc="TV streaming server for Linux"
arch=('i686' 'x86_64' 'armv6h')
url="https://tvheadend.org/projects/tvheadend"
license=('GPL3')
depends=('avahi' 'openssl' 'python2')
makedepends=('git')
optdepends=('xmltv: For an alternative source of programme listings')
provides=('tvheadend')
conflicts=('tvheadend' 'hts-tvheadend' 'hts-tvheadend-svn' 'hts-tvheadend-git' 'tvheadend-git')
backup=('etc/conf.d/tvheadend')
install=tvheadend.install

source=("https://github.com/tvheadend/tvheadend/archive/v$pkgver.tar.gz" 
	'tvheadend.service')

sha512sums=('6e19315cc676292b2a7015679c765f4cc7f3f899054f8bd5676a8d23db29eeea8c598bd75603abc336537b14a51b95be8202ed02b8dc45f8a4bd47cfd9c9ff7f'
            '456084a08ce0e02a4a32d67a8a7937d45695cb4b08b29297ead8a7e9d2a92963335f653f56d4c9346f4b2729868408fafbcd4caebf9fc8cdaa90171514cf4a57')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr --mandir=/usr/share/man/man1 --python=python2 --release
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="$pkgdir/" install
    install -D -m 644 "$srcdir/tvheadend.service" "$pkgdir/usr/lib/systemd/system/tvheadend.service"
}
