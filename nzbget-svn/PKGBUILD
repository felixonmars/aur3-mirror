# Maintainer: Richard Schwab <mail NOSPAM w.tf-w.tf>

pkgname=nzbget-svn
pkgver=721
pkgrel=1
pkgdesc="Download from Usenet using .nzb files"
arch=('x86_64' 'i686')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('gnutls' 'libpar2' 'libxml2' 'ncurses' 'libsigc++' 'libgcrypt')
makedepends=('svn' 'autoconf' 'automake')
provides=('nzbget')
conflicts=('nzbget')
source=('nzbget::svn://svn.code.sf.net/p/nzbget/code/')
md5sums=(SKIP)

pkgver() {
  cd nzbget
  svnversion | tr -d [A-z]
}

build() {
  cd nzbget/trunk

  ./configure --prefix=/usr --sbindir=/usr/bin --enable-parcheck
  make CXXFLAGS="-lsigc-2.0"
}

package() {
  cd nzbget/trunk

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/$pkgname
  install -m644 -t ${pkgdir}/usr/share/nzbget AUTHORS ChangeLog README
}
