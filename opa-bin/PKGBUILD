# Maintainer: Philip MÃ¼ller <mail at philip.in-aachen dot net>
pkgname=opa-bin
pkgver=1.0.7
pkgrel=3
pkgdesc="Rapid + Secure Web Development"
arch=(i686 x86_64)
url="http://opalang.org"
license=(MIT AGPL)
depends=('openssl>=0.9.8' 'nodejs' 'zlib>=1.2.3.4')
optdepends=('mongodb: database backend')
provides=(opa)
conflicts=(opa-git)
replaces=()
backup=()
options=(!strip)
install=
md5sums=(a7b186311b216cccc6c5f31912c931bb)

_suffix="x86"
[[ "${CARCH}" = "x86_64" ]] && _suffix='x64' && md5sums=(492a8a26e7d5b154789dd1c5f27fb5ae)

_source_script_name=(opa-${pkgver}.${_suffix}.run)
source=(https://github.com/downloads/MLstate/opalang/${_source_script_name})
noextract=(opa-${pkgver}.${_suffix}.run)

build() {
  cd $srcdir
  chmod +x $_source_script_name
  ./$_source_script_name --target $srcdir/opa --noexec --keep --nox11
}

package() {
  cd $pkgdir

  cp -a $srcdir/opa usr
  usr/share/opa/install.sh --dir usr --prefix /usr

  chmod 755 $pkgdir/usr

  rm usr/install.sh
  rm usr/share/opa/uninstall.sh
  rm usr/share/opa/install.sh

  install -d usr/share/licenses/opa-bin usr/share/vim/vimfiles/{ftdetect,syntax}

  ln -s -T /usr/share/doc/opa/copyright "usr/share/licenses/${pkgname}/COPYRIGHT"
  ln -s -T /usr/share/doc/opa/other_licenses "usr/share/licenses/${pkgname}/OTHER_LICENSES"
  ln -s /usr/share/opa/vim/ftdetect/opa.vim usr/share/vim/vimfiles/ftdetect
  ln -s /usr/share/opa/vim/syntax/opa.vim usr/share/vim/vimfiles/syntax
}
