# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=awn-indicator-applet
_pkgname=awn-extras
pkgver=0.4.0
pkgrel=1
pkgdesc="Indicator Applet for avant-window-navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL2')
depends=('avant-window-navigator' 'libindicator')
makedepends=('intltool' 'vala' 'libnotify' 'vte')
optdepends=('indicator-application-gtk2: Application Indicator'
             'indicator-sound-gtk2: Sound Indicator')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=(http://launchpad.net/$_pkgname/${pkgver%.*}/$pkgver/+download/$_pkgname-$pkgver.tar.gz
        add-indicator-applet.patch)
md5sums=('b559d68cd6ad295c961c20fcc5d9f9c0'
         'cc984a86746a78694469cb28498463d6')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i "$srcdir/add-indicator-applet.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-static \
              --disable-pymod-checks \
              --without-gnome \
              --with-indicator=yes \
              PYTHON=/usr/bin/python2
  make -C applets/maintained/indicator-applet
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make -C applets/maintained/indicator-applet GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/awn-applet-indicator-applet.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -r "$pkgdir"/etc
}
