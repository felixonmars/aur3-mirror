# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

_name=cups

pkgname=${_name}-eudev
pkgver=1.6.4
pkgrel=2
pkgdesc="The CUPS Printing System - daemon package with disabled systemd"
arch=('i686' 'x86_64')
url="http://www.cups.org/"
license=('GPL')
groups=('eudev-base')
depends=('acl' 'pam' "libcups>=${pkgver}" 'cups-filters' 'bc' 'colord'
	  'libusb' 'dbus' 'hicolor-icon-theme' 'eudev')
makedepends=('libtiff>=4.0.0' 'libpng>=1.5.7' 'xdg-utils' 'krb5'
	    'gnutls>=2.8.3' 'xinetd' 'gzip' 'autoconf' 'avahi' 'udev')
optdepends=('xdg-utils: xdg .desktop file support'
	    'cups-openrc: cups openrc initscript')
provides=("cups=${pkgver}")
conflicts=('cups')
install=cups.install
backup=('etc/cups/cupsd.conf'
        'etc/cups/snmp.conf'
        'etc/cups/printers.conf'
        'etc/cups/classes.conf'
	'etc/cups/cups-files.conf'
        'etc/cups/subscriptions.conf'
        'etc/dbus-1/system.d/cups.conf'
        'etc/logrotate.d/cups'
        'etc/pam.d/cups'
        'etc/xinetd.d/cups-lpd')
source=("http://www.cups.org/software/${pkgver}/cups-${pkgver}-source.tar.gz"
        'cups.logrotate'
        'cups.pam'
        # improve build and linking
        'cups-no-export-ssllibs.patch'
	'cups-no-gcrypt.patch'
        'cups-no-gzip-man.patch'
        # FC
        'cups-dbus-utf8.patch'
        'cups-res_init.patch'
        'cups-avahi-address.patch'
        'cups-enum-all.patch'
        # Gentoo
        'cups-1.6.0-fix-install-perms.patch'
        'cups-1.6.2-statedir.patch'
        # Debian
        'cupsd-no-crash-on-avahi-threaded-poll-shutdown.patch'
        'get-ppd-file-for-statically-configured-ipp-shared-queues.patch'
        'ppd-poll-with-client-conf.patch')


prepare() {
  cd ${_name}-${pkgver}

  # Do not export SSL libs in cups-config
  patch -Np1 -i "${srcdir}/cups-no-export-ssllibs.patch"

  patch -Np1 -i "${srcdir}/cups-no-gcrypt.patch"

  # don't zip man pages in make install, let makepkg do that / Fedora
  patch -Np1 -i ${srcdir}/cups-no-gzip-man.patch

  # various bugfixes (upstream reports/SVN or Fedora/Debian

  # Ensure attributes are valid UTF-8 in dbus notifier
  patch -Np1 -i ${srcdir}/cups-dbus-utf8.patch

  # Fixed crash which sometimes happens on shutdown of the CUPS daemon, caused by a wrong shutdown sequence for shutting down the Avahi threaded poll.
  patch -Np1 -i ${srcdir}/cupsd-no-crash-on-avahi-threaded-poll-shutdown.patch
  # Applications could not get the PPD file for statically-configured IPP-shared print queues
  patch -Np1 -i ${srcdir}/get-ppd-file-for-statically-configured-ipp-shared-queues.patch
  # If an external server is used via client.conf and the DNS is inconsistent (ex: DNS gives "noname" for many IPs, reverse DNS gives one of these IPs for "noname") local PPDs can get polled for print queues instead of the PPDs of the external server Bug: http://www.cups.org/str.php?L2763
  patch -Np1 -i ${srcdir}/ppd-poll-with-client-conf.patch
  # fix permissions on some files - alternative: cups-0755.patch by FC
  patch -Np0 -i ${srcdir}/cups-1.6.0-fix-install-perms.patch
  # move /var/run -> /run for pid file
  patch -Np1 -i ${srcdir}/cups-1.6.2-statedir.patch
  # Re-initialise the resolver on failure in httpAddrGetList()
  patch -Np1 -i ${srcdir}/cups-res_init.patch
  # Use IP address when resolving DNSSD URIs
  patch -Np1 -i ${srcdir}/cups-avahi-address.patch
  # Return from cupsEnumDests() once all records have been returned.
  patch -Np1 -i ${srcdir}/cups-enum-all.patch

  # Rebuild configure script for not zipping man-pages.
  aclocal -I config-scripts
  autoconf -I config-scripts
}

build() {
  cd ${_name}-${pkgver}
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --sbindir=/usr/bin \
      --libdir=/usr/lib \
      --with-logdir=/var/log/cups \
      --with-docdir=/usr/share/cups/doc \
      --with-cups-user=daemon \
      --with-cups-group=lp \
      --enable-pam=yes \
      --enable-raw-printing \
      --enable-dbus --with-dbusdir=/etc/dbus-1 \
      --enable-ssl=yes --enable-gnutls \
      --enable-threads \
      --enable-avahi\
      --with-php=/usr/bin/php-cgi \
      --with-optim="$CFLAGS"
  make
}

check() {
  cd "$srcdir/${_name}-$pkgver"
  #httpAddrGetList(workstation64): FAIL
  #1 TESTS FAILED!
  #make[1]: *** [testhttp] Error 1
  make -k check || /bin/true
}

package() {
  cd ${srcdir}/${_name}-${pkgver}
  make BUILDROOT=${pkgdir} install-data install-exec

  # this one we ship in the libcups pkg
  rm -f ${pkgdir}/usr/bin/cups-config

  rm -rf ${pkgdir}/etc/rc*.d
  rm -rf ${pkgdir}/etc/init.d

  install -D -m644 $srcdir/cups.logrotate ${pkgdir}/etc/logrotate.d/cups
  install -D -m644 $srcdir/cups.pam ${pkgdir}/etc/pam.d/cups

  # fix perms on /var/spool and /etc
  chmod 755 ${pkgdir}/var/spool
  chmod 755 ${pkgdir}/etc

  # install ssl directory where to store the certs, solves some samba issues
  install -dm700 -g lp ${pkgdir}/etc/cups/ssl
  # remove directory from package, we create it in cups rc.d file
  rm -rf ${pkgdir}/var/run

  # install some more configuration files that will get filled by cupsd
  touch ${pkgdir}/etc/cups/printers.conf
  touch ${pkgdir}/etc/cups/classes.conf
  touch ${pkgdir}/etc/cups/subscriptions.conf
  chgrp -R lp ${pkgdir}/etc/cups

  # fix .desktop file
  sed -i 's|^Exec=htmlview http://localhost:631/|Exec=xdg-open http://localhost:631/|g' ${pkgdir}/usr/share/applications/cups.desktop

  # compress some driver files, adopted from Fedora
  find ${pkgdir}/usr/share/cups/model -name "*.ppd" | xargs gzip -n9f

  # remove client.conf man page
  rm -f ${pkgdir}/usr/share/man/man5/client.conf.5

  # remove files now part of cups-filters
  rm -v ${pkgdir}/usr/share/cups/banners/*
  rm -v ${pkgdir}/usr/share/cups/data/testprint
  # comment out all conversion rules which use any of the removed filters
  perl -p -i -e 's:^(.*\s+bannertops\s*)$:#\1:' ${pkgdir}/usr/share/cups/mime/mime.convs
}
sha256sums=('9452ab7720c0075627407ca13a2ecba4fc03f8bfae7d2cbb9e93f41785a8ad68'
            'b3308353504bc1cc0d5203ad3609bc98639ad9655b52e8ec8257286877532796'
            '57dfd072fd7ef0018c6b0a798367aac1abb5979060ff3f9df22d1048bb71c0d5'
            'ff3eb0782af0405f5dafe89e04b1b4ea7a49afc5496860d724343bd04f375832'
            '1dad0a5af4f812cb9805d117ef3dbd63950e33e80823ebaf6d2ba25f8498d7b6'
            '125e42dc86ecc4adc7c8d0f3743b5ce444708c96c77546448dc2d7a06f6ada75'
            'f1773d964f47ca12bac9bf51f115cbe36827bd4b92839b96b236578dc3c5bfca'
            '8f1a3a6db9293f1efd61c6392bb1c24e70bf7ae0860add452ef3fa083291a7ea'
            '45499f3cee306eb3e1ad2f3b7b47fa5540a4c548af6e18be95b8ba63cf3f81b9'
            'acdfd13ef6f8bce6593a8069e5a2aac27885ed3f799090a9a50f7d60f568a821'
            '4a4a885bb2e111bd67bcb90a5780f33841b18bc02382317fb5e64c384aa0c4c8'
            '52867308281522fce419b704ddf5c2c7c10b670fe4977050cf926f1d2f4259ca'
            'e812de449e1db5075a23e732b7cda6fd0af4ea71336745aebb4dbf501805e0ee'
            '560c9a912314322ed2657347cf46ac269e8bcf2c4e0d5d5ce7235c314681f4e1'
            'ea2dcb454feccdaa7e22a0878b3401e4b0133a3d37a8def1c43d0ef1b79e71c3')
