# Maintainer: Kurt Marasco <celilo _at lavabit _dot com> 

pkgname=hiawatha
pkgver=8.5
pkgrel=1
pkgdesc="Secure and advanced webserver"
url="http://www.hiawatha-webserver.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libxslt')
optdepends=('php-cgi: for php-fcgi support')
makedepends=('cmake')
install=$pkgname.install
backup=(
	etc/rc.d/hiawatha
	etc/rc.d/php-fcgi
	etc/hiawatha/hiawatha.conf
	etc/hiawatha/hiawatha.conf.sample
	etc/hiawatha/php-fcgi.conf
	etc/hiawatha/cgi-wrapper.conf
	etc/hiawatha/mimetype.conf
	srv/http/hiawatha/cdcatalog.xml
	srv/http/hiawatha/cdcatalog.xslt
)

source=(
	"http://hiawatha-webserver.org/files/${pkgname}-${pkgver}.tar.gz" 
	'hiawatha' 
	'php-fcgi' 
	'hiawatha.conf.sample'
	'cdcatalog.xml'
	'cdcatalog.xslt'
)
md5sums=(
	'71d3903657953c93fc930758d158097d'
	'351d1098f29df91362ffab3ce2a2fb0e'
	'e23a8bc0690ba618ae737c7670683562'
	'e5e50c0fdce0aea076b7401af5168c6f'
	'74237747cbda94044a69382504e49685'
	'6af9cdecf8e221be8aae3d5a20dc099f'
)

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  cmake . -DCMAKE_INSTALL_PREFIX="/" \
	-DCMAKE_INSTALL_BINDIR="/usr/bin" \
	-DCMAKE_INSTALL_SBINDIR="/usr/sbin" \
	-DCMAKE_INSTALL_SYSCONFDIR="/etc/hiawatha" \
	-DCMAKE_INSTALL_LIBDIR="/usr/lib" \
	-DCMAKE_INSTALL_MANDIR="/usr/share/man" \
	-DCONFIG_DIR="/etc/hiawatha" \
	-DLOG_DIR="/var/log/hiawatha" \
	-DPID_DIR="/run" \
	-DWEBROOT_DIR="/srv/http/hiawatha" \
	-DWORK_DIR="/var/lib/hiawatha"
	
  make DESTDIR="$pkgdir" install

# The default capabilities are installed by this PKGBUILD
# The following build options are available to customize your installation:
# modify the above cmake command to customize
#option					description					required libraries when feature enabled
# cmake options
# -DENABLE_CACHE=ON|off			Enable cache support in Hiawatha.	
# -DENABLE_CHROOT=on|OFF		Enable chroot support in Hiawatha.	
# -DENABLE_COMMAND=on|OFF		Enable the CommandChannel in Hiawatha.	
# -DENABLE_DEBUG=on|OFF			Enable debug information (for development only).	
# -DENABLE_IPV6=ON|off			Enable IPv6 support in Hiawatha.	
# -DENABLE_MONITOR=on|OFF		Enable support for the Hiawatha Monitor.	
# -DENABLE_SSL=ON|off			Enable SSL (PolarSSL) support in Hiawatha.	
# -DENABLE_TOOLKIT=ON|off		Enable the URL toolkit in Hiawatha.	
# -DENABLE_XSLT=ON|off			Enable XSLT support in Hiawatha. 		libxml2, libxslt1	

# The following path settings are available for CMake.
# -DCMAKE_INSTALL_PREFIX=<path>		The prefix for all other CMAKE_INSTALL directories.	
# -DCMAKE_INSTALL_BINDIR=<path>		Location of the ssi-cgi binary.	
# -DCMAKE_INSTALL_SBINDIR=<path>     	Location of the other Hiawatha binaries.
# -DCMAKE_INSTALL_SYSCONFDIR=<path>	The configuration files will be installed in /hiawatha.	
# -DCMAKE_INSTALL_LIBDIR=<path>		The PolarSSL shared library will be installed in /hiawatha.	
# -DCMAKE_INSTALL_MANDIR=<path>		Manual pages will be installed in /man1.	
# -DCONFIG_DIR=<path>			Location of the Hiawatha configuration files.	
# -DLOG_DIR=<path>			Log directory used in the default hiawatha.conf.	
# -DPID_DIR=<path>			Location of the Hiawatha and php-fcgi PID files.	
# -DWEBROOT_DIR=<path>			Webroot directory used in the default hiawatha.conf.	
# -DWORK_DIR=<path>			Path of directory where Hiawatha can write temporary files.

# Fix hiawatha.conf (formerly httpd.conf)
  sed -e 's|#ServerId = www-data|ServerId = http|' \
      -e 's|/var/www/|/srv/http/|g' \
      -e 's|//|/|g' \
      -i "$pkgdir/etc/hiawatha/hiawatha.conf"

# Fix php-fcgi.conf
  sed -e 's|www-data|http|'\
      -e 's|/usr/bin/php5-cgi|/usr/bin/php-cgi|'\
      -e 's|/etc/php5/cgi/php.ini|/etc/php/cgi/php.ini|'\
      -i "$pkgdir/etc/hiawatha/php-fcgi.conf"
  echo "#unix socket example" >> "$pkgdir/etc/hiawatha/php-fcgi.conf"
  echo "#Server = /usr/bin/php-cgi ; 3 ; /tmp/hiawatha.sock ; http:http ; /etc/php/php.ini" >> "$pkgdir/etc/hiawatha/php-fcgi.conf"
  echo "Server = /usr/bin/php-cgi ; 3 ; 127.0.0.1:2005 ; http:http ; /etc/php/php.ini" >> "$pkgdir/etc/hiawatha/php-fcgi.conf"

# Fix hiawatha logrotate file (/etc/logrotate.d/hiawatha)
  sed -e 's|www-data|http|g' -i "$srcdir/$pkgname-$pkgver/logrotate.d/hiawatha"
      mkdir "$pkgdir/etc/logrotate.d"
      install -m644 "$srcdir/$pkgname-$pkgver/logrotate.d/hiawatha" "$pkgdir/etc/logrotate.d/hiawatha"
      
# Add startup scripts
  install -D -m755 "$srcdir/hiawatha" "$pkgdir/etc/rc.d/hiawatha"
  install -D -m755 "$srcdir/php-fcgi" "$pkgdir/etc/rc.d/php-fcgi"
  
# Install sample configuration implementing a virtual server for phpmyadmin, php-fastcgi, and a default website on localhost
  install -m644 "$srcdir/hiawatha.conf.sample" "$pkgdir/etc/hiawatha/"

# Install sample XSLT transformation on default website on localhost
  install -m644 "$srcdir/cdcatalog.xml" "$pkgdir/srv/http/hiawatha/"
  install -m644 "$srcdir/cdcatalog.xslt" "$pkgdir/srv/http/hiawatha/"

}
