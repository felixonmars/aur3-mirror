#Maintainer: Yichao Yu <yyc1992@gmail.com>
#Contributor: Yichao Yu <yyc1992@gmail.com>

pkgname=pypy-pyscical-git
pkgver=63.91a60a2
pkgrel=1
epoch=1
pkgdesc="Python Scientific Calculator"
arch=('i686' 'x86_64')
provides=('pypy-pyscical')
conflicts=('pypy-pyscical')
url="https://github.com/yuyichao/pyscical"
license=('GPLv3')
depends=('pypy' 'bash' 'pypy-numpy' 'pypy-six')
optdepends=('pypy-ipython: for a better instracting shell')
makedepends=('git' 'cmake' 'coreutils')

_gitname='pyscical'

_gitroot='git://github.com/yuyichao/pyscical.git'
_gitref=master
exec 5>&1

_fetch_git() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    git clean -fdx
    msg "Fetching branch $_gitref from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitref:$_gitref" --
    msg "Checking out branch $_gitref..."
    git checkout "$_gitref" --
    git reset --hard "$_gitref"
    git clean -fdx
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitref from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitref" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"
}

pkgver() {
  (_fetch_git >&5 2>&1)
  cd "$srcdir/$_gitname"

  echo "$(git rev-list --count "${_gitref}").$(git rev-parse --short "${_gitref}")"
}

build() {
  (_fetch_git)
  cd "$srcdir/$_gitname"

  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE=/usr/bin/pypy \
    -DBIN_NAME=pypyscical
  make
}

package() {
  cd "${srcdir}/$_gitname/build"

  make install DESTDIR="${pkgdir}"
}
