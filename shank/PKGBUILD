# Maintainer: N30N <archlinux@alunamation.com>
# Contributor: swiftgeek <swiftgeek@gmail.com>

pkgname="shank"
pkgver=120720110
pkgrel=2
pkgdesc="A 2D side-scrolling beat 'em up."
url="http://www.shankgame.com/"
license=("custom")
arch=("i686" "x86_64")
groups=("humble-indie-bundle4" "games")
if [ "${CARCH}" == "x86_64" ]; then
	depends=("lib32-sdl")
else
	depends=("sdl")
fi
makedepends=("unzip")
source=("shank-linux-${pkgver}-1-bin::/dev/null" "shank.sh" "shank.desktop")
md5sums=("f42fb76ad67316c3d7fa5378ad9c5f57" \
	"dd6523a1d46bab31a07178da66357e9d" \
	"159eddeeba8a32d5c8932c8d802ad1af")
PKGEXT=".pkg.tar"

_humblebundle() {
	_archive="${source[0]%%:*}"
	for group in "${groups[@]}"; do
		case "${group}" in
			"humble-frozen-bundle") _key="_humblebundlefrozenkey" ;;
			"humble-frozen-synapse-bundle") _key="_humblebundlesynapsekey" ;;
			"humble-voxatron-debut") _key="_humblevoxatronkey" ;;
			"humble-indie-bundle"*)
				_key="_humblebundle${group:19}key" ;;
			*) continue ;;
		esac
		if [ -n "${!_key}" ]; then
			msg "Getting your unique ${pkgname} download location."
			_uri="$(curl -s "http://www.humblebundle.com/?key=${!_key}" \
				| grep "${_archive}" | cut -d "'" -f 10)"
			source[0]="${_archive}::${_uri}"
			break
		else
			warning "\"${_key}\" environment variable required for download handling not found."
		fi
	done
	if [ -z "${!_key}" -a ! -f "${startdir}/${_archive}" ]; then
		error "Unable to download \"${_archive}\"."
		exit 1
	fi
	unset _archive _key _uri
}

build() {
	unzip -n "${source[0]%%:*}" || true
}

package() {
	install -Dm755 shank.sh "${pkgdir}/usr/bin/shank"
	install -Dm644 shank.desktop "${pkgdir}/usr/share/applications/shank.desktop"

	cd data
	install -Dm644 bin/.DS_Store "${pkgdir}/opt/shank/bin/.DS_Store"
	install -m755 bin/{libfmodevent-4.30.02.so,libfmodex-4.30.02.so,Shank} \
		"${pkgdir}/opt/shank/bin"
	find {data,data-pc,reslists} -type f \
		-exec install -Dm644 "{}" "${pkgdir}/opt/shank/{}" \;
}

_humblebundle

# vim: set noet ff=unix:
