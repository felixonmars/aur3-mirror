# Maintainer: Tevin Zhang <mail2tevin {at} gmail {dot} com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Andrea Fagiani <andfagiani {at} gmail {dot} com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
pkgname=libxft-ubuntu
pkgver=2.2.0
_ubver=2.2.0-3ubuntu2
pkgrel=3
pkgdesc="FreeType-based font drawing library for X, with Ubuntu's LCD rendering patches"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntu/precise/+source/xft"
license=('GPL')
depends=('fontconfig-ubuntu>=2.6.0' 'freetype2-ubuntu' 'libxrender' 'sh')
conflicts=('libxft' 'freetype2-cleartype' 'freetype2-lcd')
provides=("libxft=$pkgver")
makedepends=('pkgconfig')
options=('!libtool')
source=(http://xorg.freedesktop.org/releases/individual/lib/libXft-$pkgver.tar.bz2
        http://archive.ubuntu.com/ubuntu/pool/main/x/xft/xft_$_ubver.diff.gz)

md5sums=('cce3c327258116493b753f157e0360c7'
         '7a977a1ac9418ee61ab3b39284199d80')

build() {
  cd $srcdir/libXft-$pkgver

  patch -Np1 -i ../xft_$_ubver.diff

  for _f in $(cat debian/patches/series) ; do
    [[ $_f = "001_no_export_freetype.diff" ]] && continue
    patch -Np1 -i debian/patches/$_f
  done

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static

  make
  make DESTDIR=$pkgdir install

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING

}
