# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-desktop
pkgver=1.6.1
pkgrel=4
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=(GPL LGPL)
depends=('desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'libunique' 'python2'
         'startup-notification')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
optdepends=('lightdm-gtk2-greeter: A lightweight display manager')
options=('!emptydirs' '!libtool')
groups=('mate')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('f259a432d4f5847ad98e00ecb4cc593153f61df9')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --disable-static \
        --disable-scrollkeeper \
        --disable-schemas-compile \
        --enable-gtk-doc \
        --with-omf-dir=/usr/share/omf/${pkgname}
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
