# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-desktop
pkgver=20121016
pkgrel=2
pkgdesc="Library with common API for various MATE modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=('libunique' 'libmatenotify' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client')
makedepends=('mate-doc-utils' 'intltool')
options=('!emptydirs' '!libtool')
url="http://mate-desktop.org/"
groups=('mate')
install=mate-desktop.install

_gitroot=git://github.com/mate-desktop/mate-desktop.git
_gitname=mate-desktop

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"


  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-scrollkeeper \
    --disable-startup-notification \
    --enable-unique || return 1
    # --with-gtk=3.0
  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"
	
  make DESTDIR="$pkgdir" install

  # prevent conflicts errors
  mv "${pkgdir}/usr/share/omf" "${pkgdir}/usr/share/mate-omf"
}
