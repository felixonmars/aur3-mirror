# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Jose Valecillos <valecillosjg@gmail.com>

pkgname=acroread
pkgver=9.5.5
pkgrel=2
pkgdesc="Adobe Reader is a PDF file viewer"
arch=(i686 x86_64)
url="http://www.adobe.com/products/reader/"
license=(custom)
depends=(desktop-file-utils lib32-gtk2 lib32-libpng15 lib32-libxml2 lib32-libxt lib32-mesa lib32-xcb-util)
optdepends=('acroread-fonts: CJK and extended font packs'
    'lib32-libcanberra: XDG sound support'
    'lib32-libidn: Signature validation')
[[ $CARCH == "i686" ]] && depends=(${depends[@]/lib32-/}) && optdepends=(${optdepends[@]/lib32-/})
options=(!strip)
install=$pkgname.install
source=($pkgname.desktop
    ftp://ftp.adobe.com/pub/adobe/reader/unix/9.x/$pkgver/enu/AdbeRdr$pkgver-1_i486linux_enu.tar.bz2)
sha256sums=('84476542500e8a1b0552c8dbdfa4571bb8b52015ea4f13ed960742c29c8a5d32'
    'ee0ea9c65b38abd40ec7c92654e767dc138757f02aef5ae92f74e2dc75ac6540')
sha512sums=('2e31d40a5123117607890efa6f5832d970e507cae92a1f84082726593f1a863efcb2cb639d7e0da210fbf24ac572c715a65a5158b4f8012eeb1869e7dc63a25e'
    '0d60f9c0248432445fb0e6ccbc6d7841ed3e63dad61765436c53098b15c2ede77f1dd6369b04004cec020c20df395f195c4aaf464a6e2761e57a9e9ea0cb5361')

build() {
    cd AdobeReader/
    bsdtar -xf COMMON.TAR
    bsdtar -xf ILINXR.TAR

    sed -i "s/_filedir/&_acroread/" Adobe/Reader9/Resource/Shell/acroread_tab
}

package() {
    desktop-file-install $pkgname.desktop --dir "$pkgdir"/usr/share/applications/
    cd AdobeReader/Adobe/Reader9/

    msg2 "Installing Main Files..."
    install -d "$pkgdir"/opt/Adobe/Reader9
    cp -a * "$pkgdir"/opt/Adobe/Reader9/

    msg2 "Installing Bin Files..."
    install -d "$pkgdir"/usr/bin/
    ln -s /opt/Adobe/Reader9/bin/acroread "$pkgdir"/usr/bin/
    install -Dm644 Resource/Shell/acroread.1.gz "$pkgdir"/usr/share/man/man1/acroread.1.gz
    install -Dm644 Resource/Shell/acroread_tab "$pkgdir"/etc/bash_completion.d/acroread

    msg2 "Installing Broswer Plugin..."
    install -d "$pkgdir"/usr/lib/mozilla/plugins/
    ln -s /opt/Adobe/Reader9/Browser/intellinux/nppdf.so "$pkgdir"/usr/lib/mozilla/plugins/

    msg2 "Installing Icon Resources..."
    install -Dm644 Resource/Icons/64x64/AdobeReader9.png "$pkgdir"/usr/share/pixmaps/$pkgname.png

    msg2 "Installing License..."
    install -Dm644 Reader/Legal/en_US/License.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
