# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Jose Valecillos <valecillosjg (at) gmail (dot) com>

pkgname=acroread
pkgver=9.5.1
pkgrel=2
pkgdesc="Adobe Reader is a PDF file viewer"
arch=('i686' 'x86_64')
url="http://www.adobe.com/products/reader/"
license=('custom')
depends=('desktop-file-utils')

if [[ "$CARCH" == 'i686' ]]; then
  depends+=('gtk2' 'mesa' 'freetype2')
  optdepends=('libcanberra: XDG sound support')
else
  depends+=('lib32'-{'gtk2','mesa','libxml2','xcb-util','freetype2'})
  optdepends=('lib32-libcanberra: XDG sound support')
fi

optdepends+=('acroread-fonts: CJK and extended font packs')
options=('!strip')
install=acroread.install
source=(ftp://ftp.adobe.com/pub/adobe/reader/unix/9.x/$pkgver/enu/AdbeRdr$pkgver-1_i486linux_enu.tar.bz2)
md5sums=('e753016e8f11cdeaabcc576a7a320af3')

build() {
	cd "$srcdir/AdobeReader"
	bsdtar -xf COMMON.TAR
	bsdtar -xf ILINXR.TAR
	cd Adobe/Reader9

	msg2 "Installing Main Files..."
	mkdir -p "$pkgdir/opt/Adobe/Reader9"
	cp -a * "$pkgdir/opt/Adobe/Reader9"

	msg2 "Installing Bin Files..."
	mkdir -p "$pkgdir/usr/bin"
	ln -s /opt/Adobe/Reader9/bin/acroread "$pkgdir/usr/bin"
	install -Dm644 Resource/Shell/acroread.1.gz ${pkgdir}/usr/share/man/man1/acroread.1.gz
	install -Dm644 Resource/Shell/acroread_tab ${pkgdir}/etc/bash_completion.d/acroread

	msg2 "Installing Broswer Plugin..."
	mkdir -p "$pkgdir/usr/lib/mozilla/plugins"
	ln -s /opt/Adobe/Reader9/Browser/intellinux/nppdf.so "$pkgdir/usr/lib/mozilla/plugins"
	
	msg2 "Installing Icon Resource..."
	install -D -m644 Resource/Support/AdobeReader.desktop \
	"$pkgdir/usr/share/applications/acroread.desktop"
	sed -i '/^Icon/s|AdobeReader9|acroread|' \
	"$pkgdir/usr/share/applications/acroread.desktop"
	install -D -m644 Resource/Icons/64x64/AdobeReader9.png \
	"$pkgdir/usr/share/pixmaps/acroread.png"

	msg2 "Installing License..."
	install -D -m644 Reader/Legal/en_US/License.txt \
	"$pkgdir/usr/share/licenses/acroread/License.txt"
	
	#Bash completion fix
	sed -i "s/_filedir/_filedir_acroread/" "$pkgdir/etc/bash_completion.d/acroread"
}
