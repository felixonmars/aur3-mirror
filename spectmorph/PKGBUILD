# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=spectmorph
pkgver=0.1.1
pkgrel=1
pkgdesc="A free software project which allows to analyze samples of musical instruments."
arch=('i686' 'x86_64')
url="http://space.twc.de/~stefan/spectmorph.php"
license=('LGPL')
depends=('beast>=0.7.2' 'fftw' 'gtkmm' 'jack' 'libao' 'libsigc++')
options=('!libtool')
source=("http://space.twc.de/~stefan/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('91a7c1fc7279fbf2e361903c9d0cb26e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON="python2"

  # Makefile fixes
  sed -i "s_\(/lib/\)\(lib$pkgname\.\).*_\1.libs/\2so_g" tests/Makefile.*
  sed -i "s_/local/beast__" beast/Makefile.*

  ./configure \
	--prefix=/usr \
	--disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
