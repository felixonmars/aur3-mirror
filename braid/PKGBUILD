# Maintainer: trya <tryagainprod@gmail.com>

pkgname=braid
_gamepkg="braid-linux-build2.run.bin"
pkgver=1.0.2
pkgrel=1
pkgdesc="A puzzle-platformer where the player manipulates the flow of time in strange and unusual ways \
(requires copy of the full game)"
url="http://braid-game.com"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
depends=('sdl' 'nvidia-cg-toolkit')
optdepends=('libtxc_dxtn: open S3TC implementation for free DRI drivers')
makedepends=('unzip')
source=("$_gamepkg"::"local://$_gamepkg"
        'braid.desktop' 'braid.sh')
md5sums=('f8933b7f7c77a234763cf73e2f9312a7'
         '5c2644ab74d7714bed6d16a564df33bb'
         'c0c774073c1b626450b4fa30a2917ab8')
PKGEXT='.pkg.tar' 
DLAGENTS=("${DLAGENTS[@]}" 'local::/bin/true')

build() {
  cd "$srcdir"
  unzip $_gamepkg || true
}

package() {
  cd "$srcdir"
  
  # data
  install -d "$pkgdir/opt"
  cp -a data/gamedata "$pkgdir/opt/Braid"
  
  # startup script and executable
  install -Dm755 braid.sh "$pkgdir/usr/bin/braid"
  if [ "$CARCH" == "x86_64" ]; then
    cp data/amd64/braid "$pkgdir/opt/Braid"
  else
    cp data/x86/braid "$pkgdir/opt/Braid"
  fi
  
  # desktop icon
  install -Dm644 braid.desktop "$pkgdir/usr/share/applications/braid.desktop"
  install -Dm644 data/gamedata/braid.png "$pkgdir/usr/share/pixmaps/braid.png"
}
