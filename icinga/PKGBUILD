pkgname=icinga
pkgver=1.8.1
pkgrel=3
pkgdesc="An open source host, service and network monitoring program."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.icinga.org"
depends=('gd' 'freetype2' 'libtool' 'libdbi-drivers' 'openssl')
optdepends=('nagios-plugins: plugins needed for icinga checks')
source=("http://downloads.sourceforge.net/project/icinga/icinga/$pkgver/$pkgname-$pkgver.tar.gz" 
        "systemd.icinga" 
        "systemd.ido2db" 
				"icinga.install")
install='icinga.install'

_icinga_user="icinga"
_icinga_group="icinga"

getent group ${_icinga_group} >/dev/null || _icinga_group=667 >/dev/null
getent passwd ${_icinga_user} >/dev/null || _icinga_user=667 >/dev/null

build() {
  cd "$srcdir/$pkgname-$pkgver"

	./configure \
    --prefix="/usr/share/icinga" \
    --bindir="/usr/bin" \
    --libdir="/usr/lib/icinga" \
    --sysconfdir="/etc/icinga" \
    --localstatedir="/var/spool/icinga" \
    --with-log-dir="/var/log/icinga" \
    --with-lockfile="/run/icinga.pid" \
		--with-checkresult-dir="/var/spool/icinga/checkresults" \
		\
    --with-icinga-user=${_icinga_user} \
    --with-icinga-group=${_icinga_user} \
    --datarootdir="/usr/share/webapps/icinga" \
		--sbindir="/usr/share/webapps/icinga/cgi-bin" \
    --with-web-user=http \
    --with-web-group=http \
    --with-httpd-conf="/etc/httpd/conf/extra" \
		--with-cgi-log-dir="/var/log/icinga" \
		\
    --enable-idoutils \
    --with-ido2db-lockfile="/run/ido2db.pid" \
    --with-ido-sockfile="/var/spool/icinga/ido2db.sock" \
    --with-idomod-tmpfile="/run/idomod.tmp" \
		\
    --enable-embedded-perl \
    --enable-ssl

  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install \
    install-idoutils \
    install-config

  install -Dm644 sample-config/httpd.conf "$pkgdir/etc/icinga/apache.example.conf"
	install -Dm644 ../systemd.icinga "$pkgdir/usr/lib/systemd/system/icinga.service"
	install -Dm644 ../systemd.ido2db "$pkgdir/usr/lib/systemd/system/ido2db.service"

  install -dm755 "$pkgdir/usr/share/icinga/idoutils"
  cp -r module/idoutils/db "$pkgdir/usr/share/icinga/idoutils"

  mkdir -p "$pkgdir/var/spool/icinga/rw"

  find "$pkgdir/etc/icinga" -name '*cfg' -exec mv "{}" "{}.sample" \; > /dev/null

  # Do some perms fixing
  find "$pkgdir/usr/share/icinga" -type f -exec chmod 644 "{}" \; > /dev/null
  find "$pkgdir/usr/share/icinga" -type d -exec chmod 755 "{}" \; > /dev/null
  find "$pkgdir/etc/icinga" -type f -exec chmod 644 "{}" \; > /dev/null
  find "$pkgdir/etc/icinga" -type d -exec chmod 755 "{}" \; > /dev/null
  chmod -R 755 "$pkgdir/usr/bin"
  chmod -R 755 "$pkgdir/usr/share/webapps/icinga/cgi-bin"
  chown icinga:http "$pkgdir/var/log/icinga"
  chmod 775 "$pkgdir/var/log/icinga"
  chmod 755 "$pkgdir/usr/lib/icinga/idomod.so"
  chown -R root:root "$pkgdir/etc"
  chown -R root:root "$pkgdir/usr/bin"
  chown -R root:root "$pkgdir/usr/share/icinga"
  chown -R ${_icinga_user}:${_icinga_group} "$pkgdir/var/spool/icinga"
}





md5sums=('8180933f1388fd61e816c409e8fec746'
         'e57fd8e2b768fbe280b27e50f2f81071'
         '9ff49d279b8a7b3ebf6229b9c47b852b'
         '73f4dfb35e1e809130cb123d10821e1f')
