pkgname=icinga
pkgver=1.7.2
pkgrel=1
pkgdesc="An open source host, service and network monitoring program."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.icinga.org"
depends=('gd' 'freetype2' 'libtool' 'libdbi-drivers' 'openssl')
optdepends=('nagios-plugins: plugins needed for icinga checks')
source=("http://downloads.sourceforge.net/project/icinga/icinga/$pkgver/$pkgname-$pkgver.tar.gz" 
        "rc.icinga" "systemd.icinga" "icinga.install")
md5sums=('069b9a865bab7fd48be1b6da2953f5c7'
         '3d08782ae1c41d1c9735510e686a0a22'
         '69bc47b5887e4522cce14b378e981d40'
         '73f4dfb35e1e809130cb123d10821e1f')
backup=('etc/httpd/conf/extra/icinga.conf')
install='icinga.install'

_instdir="usr/share/icinga"
_bindir="usr/bin"
_vardir="var/icinga"
_confdir="etc/icinga"
_httpdconfdir="etc/httpd/conf/extra"
_checkresultdir="var/icinga/spool/checkresults"
_icinga_user="icinga"
_icinga_group="icinga"

getent group ${_icinga_group} || _icinga_group=667 >/dev/null
getent passwd ${_icinga_user} || _icinga_user=667 >/dev/null

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --with-icinga-user=${_icinga_user} \
    --with-icinga-group=${_icinga_user} \
    --prefix="/${_instdir}" \
    --bindir="/${_bindir}" \
    --localstatedir="/${_vardir}" \
    --sysconfdir="/${_confdir}" \
    --with-httpd-conf="/${_httpdconfdir}" \
    --with-checkresultdir="/${_checkresultdir}" \
    --enable-idoutils \
    --enable-embedded-perl \
    --enable-ssl

  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make \
    prefix="$pkgdir/${_instdir}" \
    BINDIR="$pkgdir/${_bindir}" \
    LOGDIR="$pkgdir/${_vardir}" \
    CFGDIR="$pkgdir/${_confdir}" \
    HTTPD_CONF="$pkgdir/${_httpdconfdir}" \
    CHECKRESULTDIR="$pkgdir/${_checkresultdir}" \
    install \
    install-idoutils \
    install-config

  install -D -m755 daemon-init "$pkgdir/etc/icinga/daemon-init"
  install -D -m755 "$srcdir/rc.icinga" "$pkgdir/etc/rc.d/icinga"
  install -D -m644 "$srcdir/systemd.icinga" "$pkgdir/usr/lib/systemd/system/icinga.service"
  install -D -m644 sample-config/httpd.conf "$pkgdir/${_httpdconfdir}/icinga.conf"

  mkdir -p "$pkgdir/var/icinga/rw"
  chown ${_icinga_user}:${_icinga_group} "$pkgdir/var/icinga/rw"

  find $pkgdir/etc/icinga -name '*cfg' -exec mv "{}" "{}.sample" \; > /dev/null

  # Do some perms fixing
  find $pkgdir/usr/share/icinga -type f -exec chmod 644 "{}" \; > /dev/null
  find $pkgdir/usr/share/icinga -type d -exec chmod 755 "{}" \; > /dev/null
  find $pkgdir/etc/icinga -type f -exec chmod 644 "{}" \; > /dev/null
  find $pkgdir/etc/icinga -type d -exec chmod 755 "{}" \; > /dev/null
  chmod -R 755 $pkgdir/usr/bin
  chmod 755 $pkgdir/usr/share/icinga/sbin/*
  chmod 755 $pkgdir/usr/share/icinga/lib/idomod.so
  chmod 755 $pkgdir/etc/icinga/daemon-init
  chown -R root:root $pkgdir/etc
  chown -R root:root $pkgdir/usr/bin
  chown -R root:root $pkgdir/usr/share/icinga
}
