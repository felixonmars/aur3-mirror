# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=qgo2-svn
pkgver=762
pkgrel=1
pkgdesc='Go client and full featured SGF editor; svn-snapshot of the next major version'
arch=('i686' 'x86_64')
url='http://qgo.sourceforge.net/'
license=('GPL')
depends=('qt')
makedepends=('subversion')
conflicts=('qgo' 'qgo2-cvs')

_svntrunk='https://qgo.svn.sourceforge.net/svnroot/qgo/trunk'
_svnmod='qgo'

build() {
    #fetch
    cd "${srcdir}"
    if [ -d ${_svnmod} ]; then
        (cd ${_svnmod} && svn up -r ${pkgver})
    else
        svn co ${_svntrunk} -r ${pkgver} ${_svnmod}
    fi
    rm -rf "${_svnmod}-build"
    cp -r "${_svnmod}" "${_svnmod}-build"
    cd "${_svnmod}-build"

    # patch -p1 < ../qt-4.7.patch
    ls src/translations/*.ts | sed 's/\.ts/\.qm/' | xargs touch

    qmake QMAKE_CXXFLAGS="-fpermissive $CXXFLAGS" -o Makefile qgo.pro
    make
}

package() {
    cd "${srcdir}/${_svnmod}-build"
    QTDIR=/usr make INSTALL_ROOT="$pkgdir/" install
}

