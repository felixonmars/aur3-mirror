#Mantainer: Gianni Vialetto <gianni at rootcube dot net>
#Contributor: Stefan J. Betz <info at stefan-betz dot net>
#Contributor: p2k <Patrick dot Schneider at uni-ulm dot de>
#Contributor: Jonathan Liu <net147@gmail.com>
#Contributor: Christopher Grebs <cg@webshox.org>

pkgname=rabbitmq
pkgver=3.1.1
pkgrel=2
pkgdesc="Highly reliable and performant enterprise messaging implementation of AMQP written in Erlang/OTP"
arch=('i686' 'x86_64')
url="http://rabbitmq.com"
license=('MPL')
depends=('erlang' 'systemd')
provides=('rabbitmq-systemd-unit')
conflicts=('rabbitmq-systemd-unit')
backup=('etc/rabbitmq/rabbitmq-env.conf')
source=("http://www.rabbitmq.com/releases/${pkgname}-server/v${pkgver}/${pkgname}-server-generic-unix-${pkgver}.tar.gz"
  "rabbitmq-env.conf"
  "rabbitmq.service")
md5sums=('59cb86f2675c168f10a2a669894fb2d0'
         '98da754397bf31c1c172232597ca5943'
         'c5634073fe6ec84e55633783386f39cf')
install="${pkgname}.install"

package() {
  local libdir="/usr/lib/rabbitmq"
  install -d "${pkgdir}${libdir}"
  cd ${srcdir}
  cp -R ${pkgname}_server-${pkgver}/* "${pkgdir}${libdir}"
  install -d "${pkgdir}/var/log/rabbitmq"

  install -d "${pkgdir}/usr/bin"
  for script in ${pkgdir}${libdir}/sbin/*; do
    ln -s ${script#${pkgdir}} "${pkgdir}/usr/bin/" 
  done
  install -D rabbitmq-env.conf "${pkgdir}/etc/rabbitmq/rabbitmq-env.conf"
  sed -i 's#^SYS_PREFIX=.*$#SYS_PREFIX="${libdir}"#' "${pkgdir}${libdir}/sbin/rabbitmq-defaults"

  install -m 644 -D "${srcdir}/rabbitmq.service" "${pkgdir}/usr/lib/systemd/system/rabbitmq.service"
}

# vim:set ts=2 sw=2 et:
