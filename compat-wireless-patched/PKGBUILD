# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: hokasch <hokasch at operamail dot com>
# Contributor: Dennis Brendel <buddabrod at gmail dot com>
# Contributor: Mathias Buren <mathias.buren at gmail dot com>
# Contributor: Benjamin Mtz (Cruznick) <cruznick at archlinux dot us>

pkgname=compat-wireless-patched
pkgver=3.3_2_n
_upver="${pkgver//_/-}"
pkgrel=1
pkgdesc='Compat wireless driver patched for fixing "fixed-channel -1" issue and better injection'
url='http://wireless.kernel.org/en/users/Download/stable/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('linux')
makedepends=('linux-api-headers' 'linux-headers')
source=("http://www.orbit-lab.org/kernel/compat-wireless-${pkgver:0:1}-stable/v${pkgver:0:3}/compat-wireless-${_upver}.tar.bz2" \
        'mac80211.compat08082009.wl_frag+ack_v1.patch'
        'channel-negative-one-maxim.patch')
sha1sums=('dd18cfabbe705a75440fd7fbe50a09d5fe34bb70'
          '85f7a1b141549b774f5631fba259bc414aeeffb8'
          'a611acdd7994b07b0b39417ef7a5a6ffc866a733')

install=install

build() {
	cd "${srcdir}/compat-wireless-${_upver}"

	# modprobe -l dropped in kmod
	sed 's:modprobe -l \([^ )`]*\):find /lib/modules/*/kernel -name "\1.ko*" | sed "s|.*/kernel||":' -i scripts/*
	sed 's:\$(MODPROBE) -l \([^ )`]*\):find /lib/modules/*/kernel -name "\1.ko*" | sed "s|.*/kernel||":' -i Makefile

	# rfkill.h does not use compat-3.1.h
	echo '#define br_port_exists(dev) (dev->priv_flags & IFF_BRIDGE_PORT)' >> net/wireless/core.h

	patch -p1 -i ../mac80211.compat08082009.wl_frag+ack_v1.patch 
	patch -p1 -i ../channel-negative-one-maxim.patch 

	# Uncomment the line below if you just want one specific driver or group to be built (otherwise, all will be).
	# Supported drivers: ath5k ath9k ath9k_htc carl9170 ath6kl b43 zd1211rw rt2x00 wl1251 wl12xx brcmsmac brcmfmac
	# Supported groups: atheros ath iwlagn brcm80211 iwlagn rtl818x rtlwifi wl12xx atlxx bt
	#scripts/driver-select wl12xx
	
	make
}

package() {
	cd "${srcdir}/compat-wireless-${_upver}"

	make INSTALL_MOD_PATH="${pkgdir}" install-modules
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

	install -d "${pkgdir}"/usr/sbin
	install scripts/{athenable,athload,b43enable,b43load,iwl-enable,iwl-load,madwifi-unload} "${pkgdir}"/usr/sbin/

	install -d "${pkgdir}"/usr/lib/compat-wireless
	install scripts/{check_depmod,modlib.sh} "${pkgdir}"/usr/lib/compat-wireless/

	install -d "${pkgdir}"/lib/udev/rules.d
	install udev/50-compat_firmware.rules "${pkgdir}"/lib/udev/rules.d/
	install udev/compat_firmware.sh	"${pkgdir}"/lib/udev/
}
