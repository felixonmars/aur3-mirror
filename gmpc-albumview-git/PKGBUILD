# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=gmpc-albumview
pkgname=$_pkgname-git
pkgver=20121127
pkgrel=1
pkgdesc="Browse your GMPC library by album covers"
arch=(i686 x86_64)
url=http://gmpc.wikia.com/wiki/GMPC_PLUGIN_ALBUMVIEW
license=(GPL)
depends=(gmpc-git)
makedepends=(git intltool)
provides=($_pkgname=11.8.90)
conflicts=($_pkgname)
options=(!libtool)

_gitroot=git://repo.or.cz/$_pkgname.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone --depth 1 $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
