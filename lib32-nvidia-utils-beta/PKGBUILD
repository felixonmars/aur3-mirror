# Maintainer: Det
# Contributors: Ng Oon-Ee, Dan Vratil, Jeremy Sands, Erik Hardesty, josephgbr
# Based on [multilib]'s lib32-nvidia-utils: https://www.archlinux.org/packages/multilib/x86_64/lib32-nvidia-utils/

pkgname=lib32-nvidia-utils-beta
pkgname+=('lib32-nvidia-libgl-beta' 'lib32-opencl-nvidia-beta')
pkgver=334.16
pkgrel=1
arch=('x86_64')
url="http://www.nvidia.com/"
license=('custom:NVIDIA')
options=('!strip')

_pkg="NVIDIA-Linux-x86-${pkgver}"
#source=("http://us.download.nvidia.com/XFree86/Linux-x86/${pkgver}/${_pkg}.run")
source=("ftp://download.nvidia.com/XFree86/Linux-x86/${pkgver}/${_pkg}.run")
md5sums=('2503a9ce1c17154099b2bfbdde86caf7')

build() {
    # Remove previous builds
    if [ -d ${_pkg} ]; then
        rm -rf ${_pkg}
    fi

    # Extract
    msg2 "Self-Extracting ${_pkg}.run.."
    sh ${_pkg}.run --extract-only
}

package_lib32-opencl-nvidia-beta() {
    pkgdesc="OpenCL implemention for NVIDIA (32-bit, beta)"
    depends=('lib32-libcl' 'lib32-zlib' 'lib32-gcc-libs')
    optdepends=('opencl-headers: headers necessary for OpenCL development')
    conflicts=('lib32-opencl-nvidia')
    provides=('lib32-opencl-nvidia')
    cd ${_pkg}

    # OpenCL
    install -Dm755 "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so.${pkgver}"
    ln -sf "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so.1"
    ln -sf "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so"

    install -Dm755 "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so.${pkgver}"
    ln -sf "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so.1"
    ln -sf "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so"
}

package_lib32-nvidia-libgl-beta() {
    pkgdesc="NVIDIA library symlinks (32-bit, beta)"
    depends=('lib32-nvidia-utils-beta')
    replaces=('lib32-nvidia-utils<=313.26-1')
    conflicts=('lib32-libgl' 'lib32-nvidia-libgl')
    provides=('lib32-libgl' 'lib32-nvidia-libgl')
    cd ${_pkg}

    # libGL
    mkdir -p "${pkgdir}/usr/lib32"
    ln -sf "/usr/lib32/nvidia/libGL.so.${pkgver}" "${pkgdir}/usr/lib32/libGL.so.${pkgver}"
    ln -sf "libGL.so.${pkgver}" "${pkgdir}/usr/lib32/libGL.so.1"
    ln -sf "libGL.so.${pkgver}" "${pkgdir}/usr/lib32/libGL.so"
}

package_lib32-nvidia-utils-beta() {
    pkgdesc="NVIDIA libraries (32-bit, beta)"
    depends=('lib32-zlib' 'lib32-gcc-libs')
    optdepends=('lib32-opencl-nvidia-beta')
    conflicts=('lib32-nvidia-utils')
    provides=("lib32-nvidia-utils=${pkgver}")
    cd ${_pkg}

    # OpenGL library
    install -Dm755 libGL.so.${pkgver} "${pkgdir}/usr/lib32/nvidia/libGL.so.${pkgver}"
    # OpenGL core library
    install -Dm755 libnvidia-glcore.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-glcore.so.${pkgver}"
    # VDPAU
    install -Dm755 libvdpau_nvidia.so.${pkgver} "${pkgdir}/usr/lib32/vdpau/libvdpau_nvidia.so.${pkgver}"
    # CUDA
    install -Dm755 libcuda.so.${pkgver} "${pkgdir}/usr/lib32/libcuda.so.${pkgver}"
    install -Dm755 libnvcuvid.so.${pkgver} "${pkgdir}/usr/lib32/libnvcuvid.so.${pkgver}"
    # nvidia-tls library
    install -Dm755 tls/libnvidia-tls.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-tls.so.${pkgver}"
    install -Dm755 libnvidia-cfg.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-cfg.so.${pkgver}"
    # NVML
    install -Dm755 libnvidia-ml.so.${pkgver} "${pkgdir}/usr/lib32/libnvidia-ml.so.${pkgver}"

    # create soname links
    for _lib in $(find "${pkgdir}" -name '*.so*'); do
        _soname="$(dirname ${_lib})/$(LC_ALL=C readelf -d "$_lib" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
        if [ ! -e "${_soname}" ]; then
            ln -sf "$(basename ${_lib})" "${_soname}"
            ln -sf "$(basename ${_soname})" "${_soname/.[0-9]*/}"
        fi
    done

    # Remove redundancies
    rm -rf "${pkgdir}"/usr/{include,share,bin}
    # License
    mkdir -p "${pkgdir}/usr/share/licenses"
    ln -sf nvidia-utils "${pkgdir}/usr/share/licenses/lib32-nvidia-utils"
}