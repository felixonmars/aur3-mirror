# Maintainer: Mr.Elendig <mrelendig@har-ikkje.net>
# Contributor: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>

pkgname=splintermind-attributes-hg
pkgver=561
pkgrel=2
pkgdesc="A fork of DwarfTherapist, with additional features."
arch=('i686' 'x86_64')
url="https://code.google.com/r/splintermind-attributes"
license=('MIT')
depends=('qt')
makedepends=('mercurial')
source=('fix_file_path_v2.diff'
        'kill_pidof.diff'
        'splintermind-attributes.desktop')
md5sums=('49b0891003ab10722fa5db00edb65740'
         'a9b96224d4c344260e0a815d414c7d3a'
         'af009ab89b37d6a8c4e817b154e8dcdc')

_hgroot=https://code.google.com/r
_hgrepo=splintermind-attributes

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [[ -d "$_hgrepo" ]]; then
    cd "$_hgrepo"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "${_hgroot}/${_hgrepo}" "$_hgrepo"
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  # use pgrep instead of pidof WARNING: breaks compability with old df
  patch -Np1 < "${srcdir}/kill_pidof.diff"
  
  # Change to more suitable linux paths
  patch -Np1 < "${srcdir}/fix_file_path_v2.diff"

  # Build
  qmake dwarftherapist.pro
  make
}
package() {
  cd "${srcdir}/${_hgrepo}-build"

  install -d "${pkgdir}/usr/share/splintermind-attributes/memory_layouts/linux/"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 bin/release/DwarfTherapist "${pkgdir}/usr/bin/splintermind-attributes"
  install -Dm644 etc/game_data.ini \
    "${pkgdir}/usr/share/splintermind-attributes/game_data.ini"
  install -Dm644 etc/memory_layouts/linux/*.ini \
    "${pkgdir}/usr/share/splintermind-attributes/memory_layouts/linux/"
#  install -Dm644 "${srcdir}"/*.ini \
#    "${pkgdir}/usr/share/dwarftherapist/memory_layouts/linux/"

  install -Dm644 "${srcdir}"/splintermind-attributes.desktop\
    "${pkgdir}"/usr/share/applications/splintermind-attributes.desktop
  install -Dm644 img/hammer.png \
    "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/splintermind-attributes.png
}
# vim:set ts=2 sw=2 et:
