# Maintainer: Alexandre Leray <alex @ stdin dot fr>
# Contributor: franzrogar <franzrogar at gmail dot com>
#              David Gowers <00ai99 at gmail dot com>
pkgname=fontforge-git
pkgver=20110222
pkgrel=1
pkgdesc="An outline and bitmap font editor"
arch=('i686' 'x86_64')
url="http://www.fontforge.org"
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libpng' 'libtiff' 'libjpeg' 'python2' 'pango')
options=('!libtool' '!makeflags')
makedepends=('git')
optdepends=('libspiro: bezier curves support')
conflicts=('fontforge' 'fontforge-cvs')
provides=('fontforge-git')

_gitroot="git://fontforge.git.sourceforge.net/gitroot/fontforge/fontforge"
_gitname="fontforge"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --enable-type3 \
      --enable-double \
      --enable-devicetables \
      --with-regular-link \
      --with-python=python2 \
      --enable-pyextension
  make
}

package(){
  cd "${srcdir}/fontforge-build"
  make -j1 DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
