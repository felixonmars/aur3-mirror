# Contributor: Zeph <zeph33@gmail.com>
# Maintainer: Zeph <zeph33@gmail.com>

pkgname=pamac-aur
_pkgname=pamac
pkgver=2.0
_pkgver=2.0
pkgrel=1
# This is the release package so the below _gitcommit variable should (usually) be commented out.
#_gitcommit="d3a3bbfae0c5af2c8e9c414e1f6d972b7026f3c0"
pkgdesc="A Gtk3 frontend for libalpm"
arch=(any)
urlgit="https://github.com/manjaro/pamac"
url="http://wiki.manjaro.org/index.php?title=Pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'yaourt' 'vte3>=0.38' 'gtk3>=3.14' 'libnotify' 'desktop-file-utils')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome' 'lxsession: needed for authentification in Xfce, LXDE etc.')
makedepends=('gettext' 'itstool' 'vala')
backup=('etc/pamac.conf')
provides=('pamac')
conflicts=('pamac')
options=(!emptydirs)
install=pamac.install

if [ "${_gitcommit}" != "" ]; then
  source=("pamac-$pkgver-$pkgrel.tar.gz::$urlgit/archive/$_gitcommit.tar.gz")
else
  source=("pamac-$pkgver-$pkgrel.tar.gz::$urlgit/archive/v$_pkgver.tar.gz")
fi

sha256sums=('0098fb64a13d7568b910f5767f68f3e7245b6204c21285aa0ac69fec71fe72f4')

prepare() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac-$_gitcommit"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  # adjust version string
  #cd src
  #sed -i -e "s|\"2.0\"|\"$pkgver\"|g" manager_window.vala  
}

build() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac-$_gitcommit"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  # build
  make all
  
  # create .mo translation files
  #mkdir -p mo
  #for i in `ls po | sed s'|.po||'` ; do
  #  msgfmt -c "po/$i.po" -o "mo/$i.mo"
  #done
  
  # add translation in policy file
  #itstool -j "data/polkit/org.manjaro.pamac.policy" -o "data/polkit/org.manjaro.pamac.policy.out" `ls mo/*`
}

package() {
  if [ "$_gitcommit" != "" ]; then
    cd "$srcdir/pamac-$_gitcommit"
  else
    cd "$srcdir/pamac-$_pkgver"
  fi

  mkdir -p "${pkgdir}/usr/share/icons/hicolor"
  cp -r "data/icons/"* "${pkgdir}/usr/share/icons/hicolor" 
  install -Dm744 "src/pamac-daemon" "${pkgdir}/usr/bin/pamac-daemon"
  install -Dm755 "src/pamac-manager" "${pkgdir}/usr/bin/pamac-manager"
  install -Dm755 "src/pamac-updater" "${pkgdir}/usr/bin/pamac-updater"
  install -Dm755 "src/pamac-tray" "${pkgdir}/usr/bin/pamac-tray"
  #install -Dm755 "src/pamac-refresh" "${pkgdir}/usr/bin/pamac-refresh"
  install -Dm755 "src/pamac-install" "${pkgdir}/usr/bin/pamac-install"
  install -Dm644 "data/applications/pamac-tray.desktop" "${pkgdir}/etc/xdg/autostart/pamac-tray.desktop"
  install -Dm644 "data/applications/pamac-manager.desktop" "${pkgdir}/usr/share/applications/pamac-manager.desktop"
  install -Dm644 "data/applications/pamac-updater.desktop" "${pkgdir}/usr/share/applications/pamac-updater.desktop"
  install -Dm644 "data/applications/pamac-install.desktop" "${pkgdir}/usr/share/applications/pamac-install.desktop"
  install -Dm644 "data/config/pamac.conf" "${pkgdir}/etc/pamac.conf"
  install -Dm644 "data/dbus/org.manjaro.pamac.conf" "${pkgdir}/etc/dbus-1/system.d/org.manjaro.pamac.conf"
  install -Dm644 "data/dbus/org.manjaro.pamac.service" "${pkgdir}/usr/share/dbus-1/system-services/org.manjaro.pamac.service"
  install -Dm644 "data/systemd/pamac.service" "${pkgdir}/usr/lib/systemd/system/pamac.service"
  #install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
  install -Dm644 "data/mime/x-alpm-package.xml" "${pkgdir}/usr/share/mime/packages/x-alpm-package.xml"

  # rename mo files only here else istool failed
  for i in `cd po && ls *.mo | sed s'|.mo||'` ; do
    mkdir -p "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/"
    cp "po/$i.mo" "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/pamac.mo"
  done
  
  #install -Dm644 "data/polkit/org.manjaro.pamac.policy.out" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"
  install -Dm644 "data/polkit/org.manjaro.pamac.policy" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"
}

# vim:set ts=2 sw=2 et:
