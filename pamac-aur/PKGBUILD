# Contributor: Guillaume Benoit <guillaume@manjaro.org>
# Maintainer: Zeph <zeph33@gmail.com>

pkgname=pamac-aur
_pkgname=pamac
pkgver=0.9.3
pkgrel=3
pkgdesc="A gtk3 frontend for pyalpm"
arch=(any)
url="http://wiki.manjaro.org/index.php?title=Pamac"
license=('GPL')
depends=('python>=3.3' 'python<3.4' 'pacman>=4.1' 'pyalpm-manjaro=0.6.2.1' 'gtk3>=3.6' 'python-requests' 'python-gobject' 'python-dbus' 'dbus-glib' 'libnotify' 'namcap')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome' 'lxpolkit: needed for authentification in Xfce, LXDE etc.')
makedepends=('gettext' 'itstool' 'git')
provides=('pamac')
conflicts=('pamac')
options=(!emptydirs)
install=$_pkgname-install
_git=no
_snapshot=dd702da40a77dca79577e454af57427771c25c59

if [ "$_git" == "Yes" ]; then
   source=(git+http://git.manjaro.org/core/pamac.git)
   sha256sums=('SKIP')
else
#   source=("pamac-$pkgver.tar.gz::http://git.manjaro.org/core/pamac/repository/archive?ref=v$pkgver")
#   sha256sums=('80f4ed08b2d88d2a318f8de13ecf4c79b8f56708c53c8628e695b51ab6a58c0a')
   source=("pamac-$pkgver-$pkgrel.tar.gz::http://git.manjaro.org/core/pamac/repository/archive?ref=$_snapshot")
   sha256sums=('68277758ad30a0381eb09f2237e34ed18822da2d9e53775069a08015c5957438')
fi

package() {
  if [ -e "$srcdir/core-$_pkgname" ]; then
	   cd "$srcdir/core-$_pkgname"
  fi
  if [ -e "$srcdir/$_pkgname" ]; then
     cd "$srcdir/$_pkgname"
  fi
  if [ -e "$srcdir/$_pkgname.git" ]; then
	   cd "$srcdir/$_pkgname.git"
  fi

	python setup.py install --root="$pkgdir/" --optimize=1
	mkdir -p "${pkgdir}/usr/share/pamac"
	cp -r "gui" "${pkgdir}/usr/share/pamac"
	cp -r "data/icons" "${pkgdir}/usr/share/pamac"
	cp -r "data/applications" "${pkgdir}/usr/share/"
	cp -r "data/etc" "${pkgdir}/"
	install -Dm744 "pamac-daemon.py" "${pkgdir}/usr/bin/pamac-daemon"
	install -Dm755 "pamac-manager.py" "${pkgdir}/usr/bin/pamac-manager"
	install -Dm755 "pamac-updater.py" "${pkgdir}/usr/bin/pamac-updater"
	install -Dm755 "pamac-tray.py" "${pkgdir}/usr/bin/pamac-tray"
	install -Dm755 "pamac-refresh.py" "${pkgdir}/usr/bin/pamac-refresh"
	install -Dm755 "pamac-install.py" "${pkgdir}/usr/bin/pamac-install"
	install -Dm644 "data/dbus/org.manjaro.pamac.conf" "${pkgdir}/etc/dbus-1/system.d/org.manjaro.pamac.conf"
	install -Dm644 "data/dbus/org.manjaro.pamac.service" "${pkgdir}/usr/share/dbus-1/system-services/org.manjaro.pamac.service"
	install -Dm644 "data/systemd/pamac.service" "${pkgdir}/usr/lib/systemd/system/pamac.service"
	install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
	# create .mo translation files
	mkdir -p mo
	for i in `ls po | sed s'|.po||'` ; do
		msgfmt -c "po/$i.po" -o "mo/$i.mo"
	done
	# add translation in policy file
	itstool -j "data/polkit/org.manjaro.pamac.policy" -o "data/polkit/org.manjaro.pamac.policy.out" `ls mo/*`
	# rename mo files only here else istool failed
	for i in `ls mo | sed s'|.mo||'` ; do
		mkdir -p "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/"
		mv "mo/$i.mo" "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/pamac.mo"
	done
	install -Dm644 "data/polkit/org.manjaro.pamac.policy.out" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"

}
