# Contributor: Zeph <zeph33@gmail.com>
# Maintainer: Zeph <zeph33@gmail.com>

pkgname=pamac-aur
_pkgname=pamac
pkgver=0.9.5
pkgrel=0
pkgdesc="A gtk3 frontend for pyalpm"
arch=(any)
url="http://wiki.manjaro.org/index.php?title=Pamac"
license=('GPL')
depends=('python>=3.3' 'python<3.4' 'pacman>=4.1' 'pyalpm-manjaro' 'gtk3>=3.6' 'python-requests' 'python-gobject' 'python-dbus' 'dbus-glib' 'libnotify' 'namcap')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome' 'lxpolkit: needed for authentification in Xfce, LXDE etc.')
makedepends=('gettext' 'itstool' 'git')
provides=('pamac')
conflicts=('pamac')
options=(!emptydirs)
install=$_pkgname-install
_git=no
_snapshot=9495b434336d95fa8e0e6642a7e1f4e8941801a8

if [ "$_git" == "Yes" ]; then
   source=(git+http://git.manjaro.org/core/pamac.git)
   sha256sums=('SKIP')
else
   #source=("pamac-$pkgver.tar.gz::http://git.manjaro.org/core/pamac/repository/archive?ref=v$pkgver")
   #sha256sums=('8626557d4c0a416d2a29c356cbaa672fdf9ab7ae2ec412c85e3ad348d19db0a0')
   source=("pamac-$pkgver-$pkgrel.tar.gz::http://git.manjaro.org/core/pamac/repository/archive?ref=$_snapshot")
   sha256sums=('5af9bd627df8434d3e5170886e2d4aea0c3a57ef66596f741bdee1dd53a81cf9')
fi

package() {
	if [ -e "$srcdir/core-$_pkgname" ]; then
	   cd "$srcdir/core-$_pkgname"
	fi
	if [ -e "$srcdir/$_pkgname" ]; then
	 cd "$srcdir/$_pkgname"
	fi
	if [ -e "$srcdir/$_pkgname.git" ]; then
	   cd "$srcdir/$_pkgname.git"
	fi
#	sed -i "s/{pkgname{/{pkgname}/g" ./po/id.po
	# match version with pkgver
	sed -i -e "s|^.*version =.*|version = \'$pkgver-$pkgrel\'|" pamac-manager.py

	python setup.py install --root="$pkgdir/" --optimize=1
	mkdir -p "${pkgdir}/usr/share/pamac"
	cp -r "gui" "${pkgdir}/usr/share/pamac"
	cp -r "data/icons" "${pkgdir}/usr/share/pamac"
	cp -r "data/applications" "${pkgdir}/usr/share/"
	cp -r "data/etc" "${pkgdir}/"
	install -Dm744 "pamac-daemon.py" "${pkgdir}/usr/bin/pamac-daemon"
	install -Dm755 "pamac-manager.py" "${pkgdir}/usr/bin/pamac-manager"
	install -Dm755 "pamac-updater.py" "${pkgdir}/usr/bin/pamac-updater"
	install -Dm755 "pamac-tray.py" "${pkgdir}/usr/bin/pamac-tray"
	install -Dm755 "pamac-refresh.py" "${pkgdir}/usr/bin/pamac-refresh"
	install -Dm755 "pamac-install.py" "${pkgdir}/usr/bin/pamac-install"
	install -Dm755 "pamac-check_updates.py" "${pkgdir}/usr/bin/pamac-check_updates"
	install -Dm644 "data/dbus/org.manjaro.pamac.conf" "${pkgdir}/etc/dbus-1/system.d/org.manjaro.pamac.conf"
	install -Dm644 "data/dbus/org.manjaro.pamac.service" "${pkgdir}/usr/share/dbus-1/system-services/org.manjaro.pamac.service"
	install -Dm644 "data/systemd/pamac.service" "${pkgdir}/usr/lib/systemd/system/pamac.service"
	install -Dm744 "data/networkmanager/99_update_pamac_tray" "${pkgdir}/etc/NetworkManager/dispatcher.d/99_update_pamac_tray"
	# create .mo translation files
	mkdir -p mo
	for i in `ls po | sed s'|.po||'` ; do
		msgfmt -c "po/$i.po" -o "mo/$i.mo"
	done
	# add translation in policy file
	itstool -j "data/polkit/org.manjaro.pamac.policy" -o "data/polkit/org.manjaro.pamac.policy.out" `ls mo/*`
	# rename mo files only here else istool failed
	for i in `ls mo | sed s'|.mo||'` ; do
		mkdir -p "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/"
		mv "mo/$i.mo" "${pkgdir}/usr/share/locale/$i/LC_MESSAGES/pamac.mo"
	done
	install -Dm644 "data/polkit/org.manjaro.pamac.policy.out" "${pkgdir}/usr/share/polkit-1/actions/org.manjaro.pamac.policy"

}

# vim:set ts=2 sw=2 et: