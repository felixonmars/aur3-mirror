# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=swami
pkgver=2.0.0
pkgrel=1
pkgdesc="A collection of free software for editing and sharing MIDI instruments and sounds"
arch=('i686' 'x86_64')
url="http://swami.resonance.org/trac"
license=('GPL')
depends=('audiofile' 'libgnomecanvas' 'libinstpatch' 'librsvg' 'fluidsynth'
         'gtksourceview' 'hicolor-icon-theme' 'shared-mime-info')
options=('!libtool')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('849b40e4a97c4f52faf069006358dd4f')

build() {

  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  export PYTHON="python2"

  # Configure Fix
  sed -i "s_echo\( \$PYTHON\)_which\1_g" -i configure

  # X86_64 Fix
  CFLAGS="${CFLAGS} -fPIC"

  ./configure --prefix="/usr"
  make
}

package() {

  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
