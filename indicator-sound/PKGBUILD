# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-sound
pkgver=12.10.1
pkgrel=1
pkgdesc="A unified sound menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
depends=('libpulse' 'libindicator3' 'libdbusmenu-gtk3' 'ido' 'libnotify' 'libgee' 'dconf')
makedepends=('intltool' 'vala')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/indicator-sound/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120520.tar.gz)
md5sums=('d16b5c1bc530142d862ef95d3b4ed963'
         'bc9504f602f9e9dd9711da7ca5dc6499')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install updated language files
  echo 'ace af am an ar ast az be bg bn bo br bs ca ca@valencia ckb crh cy cs csb da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fo fr fr_CA fur fy ga gd gl gu gv he hi hr ht hu hy id is it ja ka kk kl km kn ko ku ky lb lld lo lt ltg lv mg mhr mk ml mr ms my nan nb ne nl nn oc os pa pl ps pt pt_BR ro ru sc sco sd shn si sk sl sml sn sq sr sv szl ta ta_LK te th tr tt ug uk ur uz vec vi wae zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
