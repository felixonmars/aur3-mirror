# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: breakdown <breakdown(at)archlinux(dot)us>

pkgname=plowshare
pkgver=snapshot_git20120511
pkgrel=1
pkgdesc="Command-line downloader and uploader for Rapidshare, Megaupload, Mediafire and other file sharing websites."
arch=('any')
url="http://code.google.com/p/plowshare/"
license=('GPL')
depends=('curl' 'imagemagick' 'recode' 'js' 'tesseract')
optdepends=('bash-completion: enable bash auto completion'
            'libcaca: ascii display for no X server'
            'tiv: ascii display for no X server'
            'aview: ascii display for no X server')
conflicts=("$pkgname-git")
source=("http://$pkgname.googlecode.com/files/$pkgname-${pkgver/_/-}.tar.gz")
md5sums=('f439db94c1de4e115680afeaa73eeb56')

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  DESTDIR="$pkgdir/" PREFIX=/usr ./setup.sh install

  # bash completion script
  install -Dm644 etc/$pkgname.completion \
    "$pkgdir/etc/bash_completion.d/$pkgname"

  # set CDIR for bash completion
  sed -i "s|local/||" "$pkgdir/etc/bash_completion.d/$pkgname"
}

# vim:set ts=2 sw=2 et:
