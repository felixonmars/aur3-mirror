# Maintainer: Christoffer Ã–jeling <christoffer@ojeling.net>
pkgname=apelsin
pkgver=1.1
pkgrel=3
pkgdesc="Server and community browser for the game Tremulous"
url="http://ojeling.net/apelsin"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('ghc' 'haskell-http=4000.1.1' 'haskell-array=0.3.0.2' 'haskell-bytestring=0.9.1.10' 'haskell-containers=0.4.0.0' 'haskell-directory=1.1.0.0' 'haskell-filepath=1.2.0.0' 'gtk2hs-glib' 'gtk2hs-gtk' 'gtk2hs-pango' 'gtk2hs-buildtools' 'haskell-mtl=2.0.1.0' 'haskell-network=2.3.0.2' 'haskell-process=1.0.1.5' 'haskell-stm=2.2.0.1' 'haskell-transformers=0.2.2.0' 'haskell-tremulous-query>=1.0.3')
depends=('gmp' 'gtk2' 'hicolor-icon-theme')
options=('strip')
install="${pkgname}.install"
source=(http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz 'apelsin.desktop')
md5sums=('dbc0bdc2f533fe303404804a89cbc69f'
         'f2dab889c0b64afbe59ecccf48f56785')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # subdir is somewhat of a hack to set the path for the icons correctly
    runhaskell Setup configure --prefix=/usr --datasubdir='' -O
    runhaskell Setup build
}
package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    install -D -m755 "dist/build/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

    for i in 16 22 24 32 48 64 128 256; do
        install -D -m644  "share/icons/hicolor/${i}x${i}/apps/${pkgname}.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
    done

    install -D -m644 "$srcdir/apelsin.desktop" "$pkgdir/usr/share/applications/apelsin.desktop"
}
