# Maintainer: Christoffer Ã–jeling <christoffer@ojeling.net>
pkgname=apelsin
pkgver=1.2.2
pkgrel=1
pkgdesc="Server and community browser for the game Tremulous"
url="http://ojeling.net/apelsin"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('ghc' 'haskell-http>=4000' 'haskell-array>=0.3' 'haskell-bytestring>=0.9' 'haskell-containers>=0.4' 'cpphs' 'haskell-deepseq>=1.1' 'haskell-directory>=1.1' 'haskell-filepath>=1.2' 'haskell-glib' 'haskell-gtk' 'haskell-mtl>=2.0' 'haskell-network>=2.3' 'haskell-process>=1.0' 'haskell-transformers>=0.2' 'haskell-tremulous-query>=1.0.7' 'haskell-xdg-basedir>=0.2.2-19' 'haskell-cpphs>=1.13.3-1'
)
depends=('gmp' 'gtk2' 'hicolor-icon-theme')
options=('strip')
install="${pkgname}.install"
source=(http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz 'apelsin.desktop')
md5sums=('b0ede3d3a2854e952231d8174a28b536'
         'f2dab889c0b64afbe59ecccf48f56785')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # subdir is somewhat of a hack to set the path for the icons correctly
    runhaskell Setup configure --prefix=/usr --datasubdir='' -O
    runhaskell Setup build
}
package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    install -D -m755 "dist/build/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

    for i in 16 22 24 32 48 64 128 256; do
        install -D -m644  "share/icons/hicolor/${i}x${i}/apps/${pkgname}.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
    done

    install -D -m644 "$srcdir/apelsin.desktop" "$pkgdir/usr/share/applications/apelsin.desktop"
}
