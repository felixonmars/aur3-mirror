# Contributor: quantax -- contact via Arch Linux forum or AUR
# Mantainer: M0Rf30
pkgname=unrealtournament
pkgver=451
pkgrel=2
pkgdesc="The classic Unreal Tournament form 1999. Retail CD or DVD required."
arch=('i686' 'x86_64')
url="http://www.unrealtournament2004.com/utgoty/"
license=('custom')
groups=(ut1999-goty)
depends=(ossp libnotify)
makedepends=(makepkg-unreal unshield)
provides=(ut1999)
conflicts=(ut1999 ut ut-server)
source=(ut436.run::'http://www.liflg.org/?what=dl&catid=6&gameid=51&filename=unreal.tournament_436-multilanguage.run' 
        ut436goty.run::'http://www.liflg.org/?what=dl&catid=6&gameid=51&filename=unreal.tournament_436-multilanguage.goty.run' 
        http://ut-files.com/Video_Renders/OpenGL/utglr36_for_linux.zip
        http://www.utpg.org/patches/UTPGPatch451.tar.bz2 
        ut.desktop
        disk.list 
        utpg.list
        utcustom.sh
        ut-128.png
	ut-16.png
	ut-192.png
	ut-20.png
	ut-24.png
	ut-256.png
	ut-32.png
	ut-40.png
	ut-48.png
	ut-64.png
	ut-8.png
	ut-96.png)

noextract=(UTPGPatch451.tar.bz2)

# You can uncomment and set these two variables in order to override the auto
# detection done in build() by _detect_cdpath() and _detect_cdversion().
#_cdpath=""    # path to your mounted UT CD or DVD
#_cdversion="" # "default" or "anthology"

# Detect the mount point of the install medium.
_detect_cdpath() {
    echo "Searching for mount point of install medium... "

    for mountpoint in $(egrep "(iso9660|udf)" /etc/mtab \
            | awk '{print $2}'); do
        if [ -f "${mountpoint}/SYSTEM/UnrealTournament.exe" ] \
                || [ -f "${mountpoint}/Disk1/data1.hdr" ]; then
            _cdpath="${mountpoint}"
            break
        fi
    done

    if [ -z "${_cdpath}" ]; then
	cat << __EOF__ >&2
    No mounted valid Unreal Tournament CD or Unreal Anthology
    DVD has been detected while scanning all "iso9660"
    and all "udf" filesystems in "/etc/mtab" for the file
    "SYSTEM/UnrealTournament.exe" or the file "Disk1/data1.hdr".
    Make sure you mounted the right disk correctly.  If it still
    doesn't work you can try setting the "_cdpath" and/or the
    "_cdversion" variable in this PKGBUILD to your mount point and
    your version of UT manually.
__EOF__
        return 1    
    else
        echo "    ${_cdpath} looks promising."
    fi
}

# Determine which method should be used for extracting the files from the
# install medium.
_detect_cdversion() {
    echo "Determining install method... "

    if [ -f "${_cdpath}/SYSTEM/UnrealTournament.exe" ]; then
        _cdversion="default"
    elif [ -f "${_cdpath}/Disk1/data1.hdr" ]; then
        _cdversion="anthology"
    elif [ -f "${_cdpath}/System/UnrealTournament.exe" ]; then
        _cdversion="default"
    else
        echo "Could not determine _cdversion." >&2
        return 1
    fi
    echo "    Using \"${_cdversion}\" method."
}

# Install files from most UT99 CDs.
_build_default() {
    echo "Extracting files from ${_cdpath}..."
    cd "${srcdir}"

    _unreal_install_files "${_cdpath}" "${pkgdir}/opt/ut" "*./System400/.*" \
            < disk.list || return 1

    _install_patches || return 1

    echo "Decompressing maps from ${_cdpath}..."
    grep "Maps/" disk.list | sed -e "s/$/\.uz/" \
            | _unreal_decompress_files "${_cdpath}" "${pkgdir}/opt/ut" \
            || return 1
    grep "Maps/" disk.list \
            | _unreal_move_files "${pkgdir}/opt/ut/System" "${pkgdir}/opt/ut" \
            || return 1
    rm -f -- "${pkgdir}/opt/ut/System/ucc.log"
}

# Install files from the Unreal Anthology DVD.
_build_anthology() {
    echo "Extracting files from ${_cdpath}..."
    cd "${srcdir}"

    ln -fs -- ${_cdpath}/Disk*/data* -t . || return 1
    unshield x -g 3_UnrealTournament_Help -d dvd data1.hdr || return 1
    unshield x -g 3_UnrealTournament_Maps -d dvd data1.hdr || return 1
    unshield x -g 3_UnrealTournament_Music -d dvd data1.hdr || return 1
    unshield x -g 3_UnrealTournament_Sounds_All -d dvd data1.hdr \
            || return 1
    unshield x -g 3_UnrealTournament_Sounds_English -d dvd data1.hdr \
            || return 1
    unshield x -g 3_UnrealTournament_System_All -d dvd data1.hdr \
            || return 1
    unshield x -g 3_UnrealTournament_System_English -d dvd data1.hdr \
            || return 1
    unshield x -g 3_UnrealTournament_Textures -d dvd data1.hdr \
            || return 1

    _unreal_move_files dvd "${pkgdir}/opt/ut" < disk.list || return 1
    _install_patches || return 1
}

# Add files for running UT on Linux, apply the patches shipped by Loki and add
# some third party fixes.
_install_patches() {
    echo "Adding Loki's Linux runtime files..."
    cd "${srcdir}"

    sh ./ut436.run --tar xfC 436 || return 1
    sh ./ut436goty.run --tar xfC 436goty || return 1

    cd 436goty
    for size in 8 16 20 22 24 32 40 48 64 96 128 192 256; do
	    install --mode=755 -D ../ut-${size}.png "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/ut.png"
    done
    install --mode=644 -- README "${pkgdir}/opt/ut/Help/README"
    install --mode=644 -- README.Loki "${pkgdir}/opt/ut/Help/README.Loki"
    install --mode=755 -- bin/Linux/x86/ucc "${pkgdir}/opt/ut/ucc"
    install --mode=755 -- ../utcustom.sh "${pkgdir}/usr/bin/ut"
    chmod +x $pkgdir/usr/bin/ut
    ln -fs -- /opt/ut/ut "${pkgdir}ut"

    tar xfC data.tar.gz "${pkgdir}/opt/ut" \
            --exclude=System/UnrealTournament.ini.PATCH || return 1
    chmod 644 -- "${pkgdir}/opt/ut/System/OpenGLDrv.int"
    install --mode=644 -D -- "${pkgdir}/opt/ut/System/License.int" \
            "${pkgdir}/usr/share/licenses/${pkgname}/License.int"

    tar xfC UT436-OpenGLDrv-Linux-090602.tar.gz "${pkgdir}/opt/ut" || return 1
    tar xfC OpenGL.ini.tar.gz "${pkgdir}/opt/ut" || return 1
    tar xfC Credits.tar.gz "${pkgdir}/opt/ut" || return 1
    tar xfC NetGamesUSA.com.tar.gz "${pkgdir}/opt/ut" || return 1

    # As there is no distinction between GOTY and non-GOTY CDs yet, we just try
    # to patch everything that applies.  Also Loki's patcher is too unreliable.
    cd "${srcdir}"
    echo "Trying to apply Loki's 436 Xdelta patches..."
    _unreal_fail_safe_patcher 436/setup.data/data "${pkgdir}/opt/ut"
    _unreal_fail_safe_patcher 436goty/setup.data/data "${pkgdir}/opt/ut"
#   ./436/setup.dataLinux/x86/loki_patch \
#           ./436/setup.data/patch.dat "${pkgdir}/opt/ut"
#   ./436goty/setup.dataLinux/x86/loki_patch \
#           ./436goty/setup.data/patch.dat "${pkgdir}/opt/ut"

    echo "Applying 451 UTPG patch..."
    tar xfC UTPGPatch451.tar.bz2 451utpg || return 1
    _unreal_move_files 451utpg "${pkgdir}/opt/ut" < utpg.list || return 1

    # Fix a small bug until next UTPG release.  Thanks for the hint, elsixdiab.
    sed -i '/^LoadClassMismatch/s/\.%s$//' \
            "${pkgdir}/opt/ut/System/Core.int"
}

build() {
    source /usr/lib/makepkg/unreal.sh || return 1

    if [ -z "${_cdpath}" ]; then
        _detect_cdpath || return 1
    else
        echo "Using ${_cdpath} as install medium."
    fi
    if [ -z "${_cdversion}" ]; then
        _detect_cdversion || return 1
    else
        echo "Using \"${_cdversion}\" install method."
    fi

    install --directory -- ${srcdir}/{436,436goty,451utpg} \
            ${pkgdir}/usr/bin \
            ${pkgdir}/opt/ut/{Help,Logs,Maps,Music,Sounds,System,Textures} \
            ${pkgdir}/opt/ut/Web/{images,inc,plaintext/inc} || return 1
    install --mode=644 -D -- "${srcdir}/ut.desktop" \
            "${pkgdir}/usr/share/applications/ut.desktop"
    # Update OpenGL driver to 3.6
    cp $srcdir/OpenGLDrv.so $pkgdir/opt/ut/System

    case "${_cdversion}" in
        ("default")
            _build_default || return 1
            ;;
        ("anthology")
            _build_anthology || return 1
            ;;
        (*)
            echo "Unknown _cdversion: ${_cdversion}" >&2
            return 1
            ;;
    esac
}

md5sums=('726aede817997a2aefccb8c20601d760'
         '7012dc6caaa9453dcf8951474556912a'
         '0bb18191cafbc6e6ae48e0b3577a39ad'
         '77a735a78b1eb819042338859900b83b'
         '184afd04199fe82b3bb6c8453d1752e7'
         '7fbe728f7cef23d53bdfe6be17b7129a'
         '72efce99d512b1a71587c2c127dccd06'
         '0c9fe6a7f130ca2ac7b7ffa4a62d778b'
         '55ff0f2b38d4f405c6562fe695d06c2f'
         'b4c2877529424eedd563716664253551'
         '8b24f4fd39b2cfc2c258995a609335dc'
         'f42ad47c29cd0c5e1cb0dcc7930fb499'
         '1b94cefffab5570a2b700753bdc258d8'
         '48bd27ad649a0f5c0316c99c5898e11b'
         '1222c629356d223a7a05183e5f046c89'
         '5e8d8777d10308fc5af9fe99b5f52143'
         '211b0884a2b1187747779b09d7ce31d5'
         'da519ad0cae6016a62f532f26bddf36f'
         '55f2cf6cf2b991e062702693adbbf649'
         'c1b6c7d6a21398b7048642dd4906f130')
