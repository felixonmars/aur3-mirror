# Maintainer: Ruan Wessels <ruan9422@gmail.com>
# Contributor: Benjamin Jorand <benjamin.jorand@gmail.com>
# Contributor: Jens Maucher <defcon@archlinux.us>
# Contributor: Emmanuel Gras <gras.emmanuel@gmail.com>

pkgname=ksplice
pkgver=0.9.9
pkgrel=2
pkgdesc="Update the Linux kernel without rebooting"
arch=('i686' 'x86_64')
url="http://www.ksplice.com/"
license=('GPL2')
source=(http://pkgs.fedoraproject.org/repo/pkgs/ksplice/ksplice-0.9.9-src.tar.gz/ceb4301c51d9b075731050b57d9ecd80/ksplice-0.9.9-src.tar.gz)
md5sums=('ceb4301c51d9b075731050b57d9ecd80')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr \
      --with-libbfd=/usr/lib/libbfd.a \
      --with-libiberty=/usr/lib/libiberty.a \
      --libexecdir=/usr/lib/ksplice

  # patch Makefile
  sed -i 's/^LIBS = .*$/& -ldl/' Makefile 
  sed -i 's/#include "objcommon.h"/#include "config.h"\n&/' *.c
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}

