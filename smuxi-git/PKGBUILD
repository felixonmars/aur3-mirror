# Maintainer: Lex Black (autumn-wind at web.de)
# Contributor: 


_pkgname=smuxi
pkgname=smuxi-git
pkgver=20130405
pkgrel=3
pkgdesc="The user-friendly and cross-platform IRC client for sophisticated users for GNOME/GTK+. Git-Version"
arch=('i686' 'x86_64')
url="http://www.smuxi.org/"
license=('GPL')
depends=('hicolor-icon-theme' 'log4net' 'nini' 'stfl' 'gtk-sharp-2' 'xdg-utils')
optdepends=('libindicate: Support for messaging menu'
	'notify-sharp: desktop notifications'
	'dbus-sharp-glib: Support for networkmanager'
	'gtkspell: Spellcheck')
makedepends=('intltool' 'git')
install=smuxi.install
conflicts=('smuxi')
provides=('smuxi')
source=('git://git.qnetp.net/smuxi.git/#branch=master')
md5sums=('SKIP')


pkgver() {
	cd $_pkgname
	git describe --always | sed -e 's|-|.|g'
}

build() {
	cd $srcdir/$_pkgname

	_gitbranch=$(git branch | grep '^\*' | cut -d ' ' -f 2) 
	_gitcommithash=$(git log --no-color --first-parent -n1 --pretty=format:%h)
  
	./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--enable-engine-irc \
		--enable-engine-xmpp \
		--enable-engine-twitter \
		--enable-engine-campfire \
		--enable-frontend-gnome \
		--enable-frontend-stfl \
		--with-vendor-package-version="Arch Linux Git-$pkgver-$pkgrel (${_gitbranch}/${_gitcommithash})"

	make
}

package() {
	cd ${srcdir}/$_pkgname

	make DESTDIR="$pkgdir/" install

	# Uncomment the following lines if you're using the messaging menu in Unity (or the forced use in Gnome3)
	#mkdir -p $pkgdir/usr/share/indicators/messages/applications
	#echo '/usr/share/applications/smuxi-frontend-gnome.desktop' > $pkgdir/usr/share/indicators/messages/applications/smuxi-frontend-gnome
}
