# Maintainer: prettyvanilla <prettyvanilla@lavabit.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>
# Contributor: sausageandeggs <sausageandeggs@archlinux.us>

_pkgname=kdebase-dolphin
pkgname=kdebase-dolphin-klook
pkgver=4.11.4
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Dolphin File Manager with KLook enabled"
url="http://kde.org/applications/system/dolphin/"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
depends=('kdebase-runtime' 'kdebase-lib' 'nepomuk-widgets' 'klook-git')
makedepends=('kdelibs' 'cmake' 'automoc4' 'tidyhtml')
optdepends=('kdegraphics-svgpart: thumbailers for SVG files'
            'kdegraphics-thumbnailers: thumbnailers for various graphics files'
            'kdemultimedia-ffmpegthumbs: thumbnailers for video files'
            'kdemultimedia-mplayerthumbs: thumbnailers for video files'
            'kdesdk-thumbnailers: thumbnailers for development files'
            'icoutils: thumbnailers for MS files'
            'ruby: servicemenu installation')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
install=$_pkgname.install
source=("http://download.kde.org/stable/${pkgver}/src/kde-baseapps-${pkgver}.tar.xz"
        "https://abf.rosalinux.ru/openmandriva/kdebase4/raw/master/kdebase-4.11.0-dolphin-klook.patch")
sha1sums=('de4a7a7802f527cc1f760b051c4a0699d5f6624b'
          '8798477aabaca96961e220642ec0db37df325b75')

prepare() {
  [[ -d build ]] && rm -rf build
  mkdir build

  cd kde-baseapps-$pkgver
  patch -Np1 -i "$srcdir/kdebase-4.11.0-dolphin-klook.patch"
}

build() {
  cd build

  cmake "../kde-baseapps-$pkgver" \
          -DCMAKE_BUILD_TYPE=Release \
          -DKDE4_BUILD_TESTS=OFF \
          -DCMAKE_SKIP_RPATH=ON \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DQT_QMAKE_EXECUTABLE=qmake-qt4

  make
}

package() {
  cd "$srcdir/build/dolphin"
  make DESTDIR="$pkgdir" install
  cd "$srcdir/build/doc/dolphin"
  make DESTDIR="$pkgdir" install
}
