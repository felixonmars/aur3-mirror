# Maintainer: Jekyll Wu <adapte@gmail.com>

pkgname=soundkonverter-git
pkgver=20101015
pkgrel=1
pkgdesc="A KDE frontend to various audio converters"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=29024"
license=('GPL')
source=( )
depends=('kdemultimedia-kioslave' 'taglib')
makedepends=('cmake' 'automoc4')
conflicts=(soundkonverter-kde4)

_gitroot="git://gitorious.org/soundkonverter/soundkonverter.git"
_gitname="soundkonverter"

build() {

    cd "${srcdir}"
    msg "Connecting to GIT server...."

    if [ -d ${_gitname} ] ; then
        cd ${_gitname} && git pull origin
        msg "The local files are updated."
    else
        git clone ${_gitroot} ${_gitname}
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf "${srcdir}/build"

    mkdir "${srcdir}/build" || { msg "Failed to create building folder"  ; exit 1 ; }
    cd    "${srcdir}/build" || { msg "Failed to cd into building folder" ; exit 2 ; }

    cmake -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) ../${_gitname}/src/ || { msg "cmake failed"; exit 3 ;}

    make

}

package() {

    cd    "${srcdir}/build" || { msg "Failed to cd into building folder" ; exit 4 ; }
    make DESTDIR="${pkgdir}" install

}
