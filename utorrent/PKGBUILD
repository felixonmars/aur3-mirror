# Maintainer: Det
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: max1m <keyworker@rambler.ru>

pkgname=utorrent
_version=3.3.1
_build=29812
pkgver=${_version}.build_${_build}
pkgrel=1
pkgdesc="The world's most popular BitTorrent client (Wine version)"
arch=('i686' 'x86_64')
url="http://www.utorrent.com/"
license=('freeware')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'wine' 'xdg-utils')
install=utorrent.install
# source=("http://download.utorrent.com/${_version}/uTorrent.exe"
source=('uTorrent.exe::http://download-new.utorrent.com/endpoint/utorrent/os/windows/track/stable/'
        'utorrent'
        'settings.dat'
        'utorrent.desktop'
        'icons.tar.gz')
md5sums=('SKIP'                              # uTorrent.exe
         'e6cef4f718f0893c0d47d491bdac91ca'  # utorrent
         'c56a47008f90c0068476e2d51d72a22e'  # settings.dat
         '2ab6b56d7a114b148d2e9f130207a4aa'  # utorrent.desktop
         '2b1a481dec28f4e163ceb4be6ea46d5b') # icons.tar.gz
noextract=('icons.tar.gz')

package() {
  msg2 "Installing the launcher"
  install -Dm755 utorrent "${pkgdir}"/usr/bin/utorrent

  msg2 "Installing the program"
  install -Dm644 uTorrent.exe "${pkgdir}"/usr/share/utorrent/utorrent.exe
  
  msg2 "Installing the settings"
  install -m644 settings.dat "${pkgdir}"/usr/share/utorrent/
  
  msg2 "Installing the .desktop"
  install -Dm644 utorrent.desktop "${pkgdir}"/usr/share/applications/utorrent.desktop

  msg2 "Installing icons"
  tar -xf icons.tar.gz -C "${pkgdir}"/usr/share/
}
