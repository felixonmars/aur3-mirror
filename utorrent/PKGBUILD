# Maintainer: Det
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: max1m <keyworker@rambler.ru>

pkgname=utorrent
_version=3.3.2
_build=30303
pkgver=${_version}.${_build}
pkgrel=1
pkgdesc="The world's most popular BitTorrent client (Wine version)"
arch=('i686' 'x86_64')
url="http://www.utorrent.com/"
license=('freeware')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'wine' 'xdg-utils')
install=utorrent.install
source=('uTorrent.exe::http://download-new.utorrent.com/endpoint/utorrent/os/windows/track/stable/'
        'utorrent'
        'settings.dat'
        'utorrent.desktop'
        'utorrent_16.png'
        'utorrent_24.png'
        'utorrent_32.png'
        'utorrent_48.png')
md5sums=('a1a4e96dd68e9233904b107cc2e9251f'
         'e6cef4f718f0893c0d47d491bdac91ca'
         'c56a47008f90c0068476e2d51d72a22e'
         '2ab6b56d7a114b148d2e9f130207a4aa'
         '94f1857386cf0056ef9f43b0c64b4243'
         'b0e424b2a6a80355efa4136f8f10663f'
         '2ec4ae76259cb1ad2950c850603795f1'
         '7318f187b603d1b79aedc45090df9c96')

package() {
  msg2 "Installing launcher"
  install -Dm755 utorrent "${pkgdir}"/usr/bin/utorrent

  msg2 "Installing program"
  install -Dm644 uTorrent.exe "${pkgdir}"/usr/share/utorrent/utorrent.exe
  
  msg2 "Installing settings"
  install -m644 settings.dat "${pkgdir}"/usr/share/utorrent/
  
  msg2 "Installing .desktop"
  install -Dm644 utorrent.desktop "${pkgdir}"/usr/share/applications/utorrent.desktop

  msg2 "Installing icons"
  for i in 16 24 32 48; do
    install -Dm644 utorrent_${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/utorrent.png
  done
}
