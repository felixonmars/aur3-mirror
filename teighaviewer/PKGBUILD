# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=teighaviewer
pkgver=3.6.0
pkgrel=1
pkgdesc="Free cross-platform application for the visualization of CAD drawing files."
arch=('i686' 'x86_64')
url="http://www.opendesign.com/guestfiles/teigha_viewer"
license=('custom')

if [ $CARCH == i686 ]; then
  depends=('qt' 'mesa' 'bash' 'hicolor-icon-theme')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-qt' 'lib32-mesa' 'bash' 'hicolor-icon-theme')
fi

install=${pkgname}.install
source=(http://www.opendesign.com/files/guestdownloads/TeighaViewer/${pkgname}_${pkgver}_i386.deb)
md5sums=('659067c52846dad1dc90796d5bdad7d7')

build() {
  cd "${srcdir}"
  
  [ ! -d ${pkgname}-${pkgver} ] && mkdir ${pkgname}-${pkgver}
  
  ar xo ${pkgname}_${pkgver}_i386.deb
  tar xf data.tar.gz -C ${pkgname}-${pkgver}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # persistent application storage (libs, executables, etc.)
  if [ $CARCH == i686 ]; then
    appstoragedir=usr/lib/teighaviewer
  elif [ $CARCH == x86_64 ]; then
    appstoragedir=usr/lib32/teighaviewer
  fi
  for file in $(ls usr/bin/TeighaViewer_${pkgver}/); do
    install -Dm755 usr/bin/TeighaViewer_${pkgver}/${file} \
        "${pkgdir}/${appstoragedir}/${file}"
  done
  
  # binary
  install -Dm755 usr/bin/TeighaViewer \
    "${pkgdir}/usr/bin/teighaviewer"
  sed -e "s#usr/bin/TeighaViewer_${pkgver}#${appstoragedir}#" \
    -i "${pkgdir}/usr/bin/${pkgname}"
  
  # desktop item
  install -Dm644 usr/share/applications/TeighaViewer_${pkgver}.desktop \
    "${pkgdir}/usr/share/applications/teighaviewer.desktop"
  sed -e "s#/TeighaViewer#/teighaviewer#" \
    -i "${pkgdir}/usr/share/applications/teighaviewer.desktop"
  
  # hicolor icons
  for icon in $(find usr/share/icons -type f); do
    install -Dm644 ${icon} \
        "${pkgdir}/${icon}"
  done
  
  # copyright
  install -Dm644 usr/share/doc/TeighaViewer/copyright \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
  
}
