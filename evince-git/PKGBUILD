# Maintainer: Mauro Fruet <maurofruet@gmail.com>

pkgname=evince-git
pkgver=20121105
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://www.gnome.org/projects/evince/"
license=('GPL')
depends=('gtk3' 'libgxps' 'libspectre' 'gsfonts' 'poppler-glib' 'djvulibre'
         'gnome-icon-theme' 't1lib' 'libgnome-keyring' 'desktop-file-utils'
         'dconf' 'gsettings-desktop-schemas')
makedepends=('git' 'itstool' 'nautilus' 'texlive-bin' 'intltool'
             'gobject-introspection' 'docbook-xsl' 'yelp-tools')
optdepends=('texlive-bin: DVI support'
            'gvfs: bookmark support and session saving')
arch=('i686' 'x86_64')
conflicts=('evince')
provides=('evince')
options=('!libtool' '!emptydirs')
install=evince.install
_gitname="evince"
_gitroot="git://git.gnome.org/${_gitname}"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
      cd "$_gitname" && git pull origin && git submodule init \
          && git submodule update && cd "$srcdir"
      msg "The local files are updated."
  else
      git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$_gitname-build"
  git clone "$_gitname" "$_gitname-build"

  cd "$_gitname-build"

  ./autogen.sh --prefix=/usr            --sysconfdir=/etc \
               --localstatedir=/var     --libexecdir=/usr/lib/evince \
               --disable-static         --enable-nautilus \
               --enable-pdf             --enable-tiff \
               --enable-djvu            --enable-dvi \
               --enable-t1lib           --enable-comics \
               --disable-scrollkeeper   --disable-schemas-compile \
               --enable-introspection
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
