# Maintainer: Eric Schultz eric at schultzter dot ca
# Inspired by (i.e.: copied almost verbatim) xyproto's google-appengine-go package, so buy him a beer!

pkgname=google-appengine-php
pkgver=1.8.1
pkgrel=1
pkgdesc='Google App Engine SDK for PHP'
arch=('any')
depends=('python2' 'php-cgi' 'mysql')
url='https://developers.google.com/appengine/docs/php/gettingstarted'
license=('custom')
options=('!strip')
noextract=("google_appengine_${pkgver}.zip")
makedepends=('unzip')

source=("http://googleappengine.googlecode.com/files/google_appengine_${pkgver}.zip" "google-appengine-php.ini")
md5sums=('05195820a1be5e8f860454533aa4310e' '19947a945ae3ff6044211456fc532961')

prepare() {
  cd "$srcdir"

  unzip -q "google_appengine_${pkgver}.zip"
}

build() {
  cd "$srcdir/google_appengine"

  msg2 'Correcting file permissions (Only make the .py files executable)...'
  find . -type f -exec chmod 644 '{}' \;
  chmod +x *.py
  
  for f in *.py
  do
    msg2 "Modifying script to use Python 2 $f ..."
	  sed -i '0,/on/s//on2/' $f
  done
}

package() {
  cd "$srcdir/google_appengine"

  msg2 'Packaging files...'
  mkdir "$pkgdir/opt"
  cp -R "./" "$pkgdir/opt/$pkgname"

  msg2 'Cleaning up deprecated files...'
  rm -r "$pkgdir/opt/$pkgname/tools"

  msg2 'Packaging license...'
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # "TODO: A PHP ninja will hopefully recommend a more effective way of doing this!"
  msg2 "Configure PHP to run properly with GAE..."
  cd "$srcdir"
  install -Dm644 "google-appengine-php.ini" "$pkgdir/etc/php/conf.d/google-appengine-php.ini"
}
