# Maintainer: archtux <antonio dot arias99999 at gmail dot com>
 
pkgname=deadbeef-git
pkgver=20130225
pkgrel=1
pkgdesc="Music player based on GTK2(GIT branch)"
url="http://deadbeef.sourceforge.net"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('alsa-lib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme')
makedepends=('curl' 'faad2' 'flac' 'git' 'intltool' 'libcddb' 'libcdio' 'libmad' 'libpulse' 'libsamplerate' 'libvorbis' 'libx11' 'libzip' 'wavpack' 'yasm')
optdepends=('libsamplerate: for Resampler plugin'
            'libvorbis: for Ogg Vorbis playback'
            'libmad: for MP1/MP2/MP3 playback'
            'flac: for FLAC playback'
            'curl: for Last.fm scrobbler, SHOUTcast, Icecast, Podcast support'
            'imlib2: for artwork plugin'
            'wavpack: for WavPack playback'
            'libsndfile: for Wave playback'
            'libcdio: audio cd plugin'
            'libcddb: audio cd plugin'
            'faad2: for AAC/MP4 support'
            'dbus: for OSD notifications support'
            'pulseaudio: for PulseAudio output plugin'
            'libx11: for global hotkeys plugin'
            'zlib: for Audio Overload plugin'
            'alsa-oss: for OSS output plugin'
            'libsidplay: for SID player plugin'
            'libzip: for vfs_zip plugin')
install='deadbeef.install'
options=('!libtool')
conflicts=('deadbeef')
provides=('deadbeef')
 
_gitname=deadbeef
_gitroot=git://deadbeef.git.sourceforge.net/gitroot/deadbeef/deadbeef
 
build() {
  cd $srcdir
  msg "Connecting to GIT server...."
  rm  -rf $srcdir/$_gitname-build
 
  if [ -d $_gitname ]; then
   cd $_gitname
   git pull origin
    else
   git clone $_gitroot
     fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."
 
  cd $srcdir
  cp -r $_gitname $_gitname-build
 
  cd $_gitname-build
 
  ./autogen.sh
  ./configure --prefix=/usr --disable-ffmpeg --enable-gtk2 --disable-gtk3
  make
}

package () {
  cd $srcdir/$_gitname-build

  make DESTDIR=$pkgdir install
}