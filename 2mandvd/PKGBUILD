# Maintainer: t3ddy  <t3ddy1988 "at" gmail {dot} com>
# Contributor: moostik <mooostik_at_gmail.com>

pkgname=2mandvd
pkgver=1.7.3
pkgrel=1
pkgdesc="DVD Authoring Maker In Linux Environnement"
url="http://2mandvd.tuxfamily.org/"
license=('GPL')
arch=('i686' 'x86_64')
install=2mandvd.install
depends=('qt>=4.6' 'sox' 'mplayer' 'mencoder' 'perl' 'netpbm' 'transcode' 'ffmpegthumbnailer' 'dvdauthor' 'mjpegtools' 'ttf-dejavu' 'dvd+rw-tools' 'exif')
optdepends=('xine-ui: to preview the result')
source=(http://download.tuxfamily.org/2mandvd/2ManDVD-${pkgver}.tar.gz
	2mandvd.desktop
	2mandvd.sh)
md5sums=('5ca9fcbcb366a9119da09a2217cb8ff2'
         'bd59ad69e6ed089d07adbb1ec3d59d66'
         '0112971f170034aff6efaafb9508b010')

build() {
  cd ${srcdir}/2ManDVD

  qmake 2ManDVD.pro

  make
}

package(){
  cd $srcdir/2ManDVD

  install -d -m755 ${pkgdir}/opt/2ManDVD ${pkgdir}/usr/bin
  cp -R Bibliotheque Interface 2ManDVD fake.pl *.qm README.txt ${pkgdir}/opt/2ManDVD/
  install -D -m755 ${srcdir}/2mandvd.sh ${pkgdir}/usr/bin/2ManDVD

  # Fixing some right problems
  cd ${pkgdir}/opt/2ManDVD
  find . -type f -print0 | xargs -0 -n1 chmod 644

  chmod 755 Bibliotheque Interface
  chmod +x 2ManDVD
  cd ${pkgdir}/opt/2ManDVD/Bibliotheque
  chmod -R 777 *

  # Creating menu item
  install -D -m644 ${srcdir}/2ManDVD/Interface/mandvd.png ${pkgdir}/usr/share/pixmaps/2mandvd.png
  install -D -m644 ${srcdir}/2mandvd.desktop ${pkgdir}/usr/share/applications/2mandvd.desktop
}