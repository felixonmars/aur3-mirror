# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=gcr
pkgname=$_pkgname-git
pkgver=3.9.1.9.g7924b75
pkgrel=1
_realver=3.9.2
pkgdesc="A library for bits of crypto UI and parsing"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
depends=('dconf' 'desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'libgcrypt' "p11-kit>=0.19.0")
makedepends=('intltool' 'gobject-introspection')
options=(!libtool)
install=gcr.install
conflicts=('gcr')
replaces=('gcr')
provides=('gcr' "gcr=$_realver")
source=('git://git.gnome.org/gcr'
        '10-gcr.conf')
sha256sums=('SKIP'
            '5f2eda7175ae9f23ee0e09d2beceb24fd2f6daafd7bddfcc1c1f5a3734eb60fc')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr \
    --libexec=/usr/lib/gcr \
    --disable-static \
    --disable-update-mime \
    --disable-schemas-compile \
    --enable-gtk-doc
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install

  # gcr wants to lock some memory to prevent swapping out private keys
  # https://bugs.archlinux.org/task/32616
  # https://bugzilla.gnome.org/show_bug.cgi?id=688161
  install -Dm644 ../10-gcr.conf "$pkgdir/etc/security/limits.d/10-gcr.conf"
}

# vim:set ts=2 sw=2 et:
