# $Id$
# Maintainer: Allen Zhong <moeallenz@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Miroslaw Szot <mss@czlug.icis.pcz.pl>

_cfgdir=/etc/tengine
_tmpdir=/var/lib/tengine

pkgname=tengine-dev
_pkgname=tengine
pkgver=1.4.5
pkgrel=1
pkgdesc='A web server based on Nginx and has many advanced features, originated by Taobao.'
arch=('i686' 'x86_64')
depends=('pcre' 'zlib' 'openssl' 'geoip')
makedepends=('passenger')
url="http://tengine.taobao.org"
license=('custom')
install=tengine.install
backup=(${_cfgdir:1}/fastcgi.conf
	${_cfgdir:1}/fastcgi_params
	${_cfgdir:1}/koi-win
	${_cfgdir:1}/koi-utf
	${_cfgdir:1}/mime.types
	${_cfgdir:1}/tengine.conf
	${_cfgdir:1}/scgi_params
	${_cfgdir:1}/uwsgi_params
	${_cfgdir:1}/win-utf
	etc/logrotate.d/tengine)
source=(http://tengine.taobao.org/download/tengine-$pkgver.tar.gz
	service
	logrotate)
sha256sums=('8681015e3c252fb13c6937f3687c717e78a0b563c3c18b1070086fbb7ecbeb07'
            'eee1114ee09c7e3526ec0ad441f32d324a2537f010a2984a64a17a666961527c'
            '245c23906db2a9642fed996ff969e127989a6dcae865c1dd8b422e23ebc94f42')

build() {
	cd "$srcdir"/$_pkgname-$pkgver

	./configure \
        --prefix=$_cfgdir \
        --conf-path=$_cfgdir/tengine.conf \
        --sbin-path=/usr/sbin/tengine \
        --pid-path=/var/run/tengine.pid \
        --lock-path=/var/lock/tengine.lock \
        --user=http --group=http \
        --http-log-path=/var/log/tengine/access.log \
        --error-log-path=/var/log/tengine/error.log \
        --http-client-body-temp-path=$_tmpdir/client-body \
        --http-proxy-temp-path=$_tmpdir/proxy \
        --http-fastcgi-temp-path=$_tmpdir/fastcgi \
        --http-scgi-temp-path=$_tmpdir/scgi \
        --http-uwsgi-temp-path=$_tmpdir/uwsgi \
        --with-imap --with-imap_ssl_module \
        --with-ipv6 --with-pcre-jit \
        --with-file-aio \
        --with-http_dav_module \
        --with-http_geoip_module \
        --with-http_gzip_static_module \
        --with-http_realip_module \
        --with-http_ssl_module \
        --with-http_stub_status_module \
        --add-module=/usr/lib/passenger/ext/nginx
        #--with-http_mp4_module \
        #--with-http_addition_module \
        #--with-http_xslt_module \
        #--with-http_image_filter_module \
        #--with-http_sub_module \
        #--with-http_flv_module \
        #--with-http_random_index_module \
        #--with-http_secure_link_module \
        #--with-http_degradation_module \
        #--with-http_perl_module \

	make
}

package() {
	cd "$srcdir"/$_pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	sed -e 's|\<user\s\+\w\+;|user html;|g' \
		-e '44s|html|/usr/share/tengine/html|' \
		-e '54s|html|/usr/share/tengine/html|' \
		-i "$pkgdir"/etc/tengine/tengine.conf
	rm "$pkgdir"/etc/tengine/*.default

	install -d "$pkgdir"/$_tmpdir
	install -dm700 "$pkgdir"/$_tmpdir/proxy

	chmod 750 "$pkgdir"/var/log/tengine
	chown http:log "$pkgdir"/var/log/tengine

	install -d "$pkgdir"/usr/share/tengine
	mv "$pkgdir"/etc/tengine/html/ "$pkgdir"/usr/share/tengine

        install -Dm644 "$srcdir"/logrotate "$pkgdir"/etc/logrotate.d/tengine
	install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/tengine.service
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/tengine/LICENSE
	rm -rf "$pkgdir"/var/run
}
