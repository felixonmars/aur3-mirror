# Maintainer: Remy Marquis <remy.marquis@gmail.com>

pkgname=pacaur
pkgver=4.1.7
pkgrel=1
pkgdesc="A fast workflow AUR helper using cower as backend"
arch=('any')
url="https://github.com/Spyhawk/pacaur"
license=('GPL')
depends=('cower' 'expac' 'sudo' 'pacman')
makedepends=('perl')
backup=('etc/xdg/pacaur/config')
source=("https://github.com/Spyhawk/$pkgname/archive/$pkgver.tar.gz")
md5sums=('4e7493ae35701d2f60cc4bfa11d44ef4')

build() {
  cd "$pkgname-$pkgver"
  pod2man --utf8 --section=8 --center="Pacaur Manual" --name="PACAUR" --release="$pkgname $pkgver"\
  	./README.pod > ./pacaur.8
}

package() {
  cd "$pkgname-$pkgver"
  mkdir -p $pkgdir/etc/xdg/pacaur
  install -D -m644 ./config $pkgdir/etc/xdg/pacaur/config
  mkdir -p $pkgdir/usr/bin
  install -D -m755 ./$pkgname $pkgdir/usr/bin/$pkgname
  mkdir -p $pkgdir/usr/share/bash-completion/completions
  install -D -m755 ./bash.completion\
        $pkgdir/usr/share/bash-completion/completions/$pkgname
  mkdir -p $pkgdir/usr/share/man/man8
  install -m 644 ./pacaur.8 $pkgdir/usr/share/man/man8/pacaur.8 
  mkdir -p $pkgdir/usr/share/locale/{de,es,fr,ja}/LC_MESSAGES/
  msgfmt ./po/de.po -o $pkgdir/usr/share/locale/de/LC_MESSAGES/pacaur.mo
  msgfmt ./po/es.po -o $pkgdir/usr/share/locale/es/LC_MESSAGES/pacaur.mo
  msgfmt ./po/fr.po -o $pkgdir/usr/share/locale/fr/LC_MESSAGES/pacaur.mo
  msgfmt ./po/ja.po -o $pkgdir/usr/share/locale/ja/LC_MESSAGES/pacaur.mo
}

