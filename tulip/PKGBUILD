# $Id: PKGBUILD,v 1.12 2003/11/06 08:26:13 dorphell Exp $
# Contributor: Thomas Aynaud <taynaud at gmail dot com>
# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=tulip
pkgver=3.8.0
pkgrel=1
pkgdesc="An information visualization framework dedicated to the analysis and visualization of relational data"
arch=('i686' 'x86_64')
url="http://www.tulip-software.org"
license=('LGPL')
depends=('python2' 'qt' 'libxml2' 'glew' 'freetype2' 'doxygen' 'libxml2' 'qtwebkit' 'ftgl')
optdepends=('python-sphinx: generates docucentation for python bindings')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/project/auber/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
	"${pkgname}.profile"
	"${pkgname}-config.patch")
md5sums=('6ad90c2f058be9f01dc17099ab0f8c4c'
         '68f8136db3326713fdf12254049426ef'
         '81a77ae32d853c636bbf1041a60bf7e0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-src"
  patch -p0 < "${srcdir}/${pkgname}-config.patch"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_SKIP_RPATH=ON -DCMAKE_INSTALL_PREFIX="${pkgdir}/opt/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src/build"
  make install
  install -D "${srcdir}/${pkgname}.profile" "${pkgdir}/etc/profile.d/${pkgname}.sh"
}

