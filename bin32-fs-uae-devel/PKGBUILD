# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae
pkgname=bin32-fs-uae-devel
pkgver=1.3.19
pkgrel=1
pkgdesc="An Amiga multi-platform emulator based on UAE/WinUAE, with a focus on emulating games (development/32-bit version)."
arch=("x86_64")
url="http://fengestad.no/wp/fs-uae/download-beta"
license=("GPL2")
depends=("lib32-sdl" "lib32-libpng" "lib32-glib2" "lib32-openal" "lib32-zlib" "lib32-mesa" "lib32-gettext" "xdg-utils")
makedepends=("libtool-multilib")
provides=("fs-uae" "fs-uae-devel")
conflicts=("fs-uae" "fs-uae-devel")
install="${pkgname}.install"
source=(http://fengestad.no/${_pkgname}/files/${pkgver}/${_pkgname}-${pkgver}.tar.gz pl.patch)
md5sums=('7c9107182e9a8d5ad1ae99f8cb3d2140'
         'c8b6d2bf017ee2c7a36f396696cd01af')






build() {
   cd ${_pkgname}-${pkgver}

   patch -Np0 -i ../pl.patch
   mv share/fs-uae/shaders/{Scale4xHQ,scale4xhq}.shader

   sed 's|\(sdl-config\)|\1-32|g' -i Makefile libfsemu/Makefile

   make CC="gcc -m32" CXX="g++ -m32" CFLAGS="${CFLAGS/x86-64/i686}" CXXFLAGS="${CXXFLAGS/x86-64/i686}"
}


package() {
   cd ${_pkgname}-${pkgver}

   make install prefix="${pkgdir}/usr"
}


