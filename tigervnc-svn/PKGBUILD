# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc-svn
pkgver=4993
pkgrel=1
_xorgver=1.12.4
pkgdesc="suite of VNC servers and clients. Based on the VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('pam' 'gnutls' 'libjpeg-turbo' 'libpng' 'libxft' 'libxinerama'
         'libxcursor' 'libxtst' 'libxfont' 'pixman' 'xorg-xauth'
         'xorg-xsetroot' 'xkeyboard-config' 'libgl')
makedepends=('cmake' 'nasm' 'xorg-font-util' 'xorg-util-macros' 'bigreqsproto'
             'compositeproto' 'damageproto' 'randrproto' 'resourceproto'
             'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xf86vidmodeproto'
             'xtrans' 'mesa' 'subversion' 'imagemagick')
options=(!libtool)
conflicts=('tigervnc' 'tightvnc')
provides=('tigervnc')
source=(fltk.patch
        icon_path.patch
        cmakelists.patch
        gethomedir.patch
        ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2)
md5sums=('bd4b76aed566f4db848a064bc52e386b'
         '3e831d90e6376c0de86804f024fdbb83'
         '7e9fa3165c1c9f83bed7b0b3d32576bd'
         'fa059baabe72308bf82622bca73cb3e0'
         'f87d830aa69885275e26dd6327d76a44')

_fltksvn=http://svn.easysw.com/public/fltk/fltk/branches/branch-1.3
_fltkmod=fltk-1.3.0r9619

_vncsvn=https://tigervnc.svn.sourceforge.net/svnroot/tigervnc/trunk
_vncmod=tigervnc

build() {
  cd ${srcdir}

  export LDFLAGS="$LDFLAGS -Wl,--copy-dt-needed-entries"

  if [ ! -d  ${_fltkmod} ]; then
    msg "Fetching FLTK..."
    svn co ${_fltksvn} --config-dir ./ -r 9619 ${_fltkmod}
  fi

  msg "Fetching TigerVNC..."
  if [ -d ${_vncmod}/.svn ]; then
    (cd ${_vncmod} && svn up -r ${pkgver})
  else
    svn co ${_vncsvn} --config-dir ./ -r ${pkgver} ${_vncmod}
  fi

  if [ -d ${_fltkmod}-build ]; then
    msg "Deleting old FLTK build directory..."
    rm -rf ${_fltkmod}-build
  fi

  if [ -d ${_vncmod}-build ]; then
    msg "Deleting old TigerVNC build directory..."
    rm -rf ${_vncmod}-build
  fi

  msg "Starting FLTK build..."
  svn export -q ${_fltkmod} ${_fltkmod}-build
  cd ${srcdir}/${_fltkmod}-build
  patch -Np1 -i ${srcdir}/fltk.patch
  cmake -G "Unix Makefiles" -DOPTION_USE_GL=no -DOPTION_BUILD_EXAMPLES=no
  make

  cd ${srcdir}

  msg "Starting TigerVNC build..."
  svn export -q ${_vncmod} ${_vncmod}-build
  cd ${srcdir}/${_vncmod}-build
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  patch -Np1 -i ${srcdir}/gethomedir.patch
  patch -Np1 -i ${srcdir}/cmakelists.patch
  patch -Np1 -i ${srcdir}/icon_path.patch

  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr \
        -DFLTK_FLUID_EXECUTABLE=${srcdir}/${_fltkmod}-build/bin/fluid \
        -DFLTK_INCLUDE_DIR=${srcdir}/${_fltkmod}-build
  make

  cd unix/xserver
  patch -Np1 -i ../xserver110.patch
  autoreconf -fiv
  ./configure --prefix=/usr \
	--disable-static --disable-xinerama --without-dtrace \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--disable-unit-tests --disable-devel-docs --disable-dri \
	--enable-dri2 --enable-glx --enable-glx-tls
  make
}

package() {
  cd ${srcdir}/${_vncmod}-build
  make DESTDIR=${pkgdir} install
  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  sed -i 's/iconic/nowin/' ${pkgdir}/usr/bin/vncserver
}
