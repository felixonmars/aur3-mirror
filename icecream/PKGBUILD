#Contributor: Isaac C. Aronson <isaac@pingas.org>

pkgname=icecream
pkgver=0.9.7
pkgrel=5
pkgdesc="Icecream takes compile jobs from your build and distributes it to remote machines allowing a parallel build on several machines."
url="http://en.opensuse.org/Icecream"
license="GPL"
depends=('gcc')
provides=('icecream' 'icecream1')
optdepends=('icemon-svn' 'systemd')
conflicts=('icecream1')
backup=('etc/icecream.conf')
arch=('i686' 'x86_64')
install=icecream.install
source=(ftp://ftp.suse.com/pub/projects/icecream/icecc-$pkgver.tar.bz2 icecream-daemon icecream-scheduler-daemon icecream.conf icecream.service icecream-scheduler.service icecreamd icecream-schedulerd http://people.profusion.mobi/~lucas/fix-rusage.diff)
md5sums=('c06900c2f4011428d0d48826a04f74fb'
         'ada145106134e30b6b22dfb5e982f7e3'
         '7fc4e0b1b56600673c0c1c3087928cb9'
         '51aae781751a11d108e174352d6e739d'
         '256dbfd08c2c7b51cbb62de3ae830afd'
         'b97228cf444282a995fa228b9969d7da'
         'aad41fb7f4b7dacf4321a64f55e65e39'
         '62295c0d75d9fd67a8dcf88ab3d04d7a'
         '8013059c69484716bb74638cb2ba418c')

build() {
        cd "$srcdir/icecc-$pkgver"
        patch -p1 < $srcdir/fix-rusage.diff
        ./configure --prefix=/usr/lib/icecream
        make
}

package() {
        cd "$srcdir/icecc-$pkgver"
        make DESTDIR="$pkgdir" install
        install -D -m755 ../icecream-daemon "$pkgdir/etc/rc.d/icecream"
        install -D -m755 ../icecream-scheduler-daemon "$pkgdir/etc/rc.d/icecream-scheduler"
        install -D -m755 ../icecream.conf "$pkgdir/etc/icecream.conf"
        install -D -m755 ../icecreamd "$pkgdir/usr/lib/icecream/icecreamd"
        install -D -m755 ../icecream-schedulerd "$pkgdir/usr/lib/icecream/icecream-schedulerd"
        install -D -m644 ../icecream-scheduler.service "$pkgdir/usr/lib/systemd/system/icecream-scheduler.service"
        install -D -m644 ../icecream.service "$pkgdir/usr/lib/systemd/system/icecream.service"
}
