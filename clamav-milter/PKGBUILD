# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: xjpvictor Huang <ke AT xjpvictor dot info>

pkgname=clamav-milter
_pkgname=clamav
pkgver=0.97.8
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix with libmilter built in'
url='http://www.clamav.net/'
license=('GPL')
options=('!libtool')
arch=('i686' 'x86_64')
depends=('bzip2' 'libltdl')
makedepends=('libmilter')
conflicts=('clamav')
backup=('etc/clamav/clamd.conf' 'etc/clamav/freshclam.conf' 'etc/clamav/clamav-milter.conf')
source=("http://downloads.sourceforge.net/project/${_pkgname}/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        'service'
        'service.fresh'
        'service.milter'
        'logrotate'
        'tmpfiles.d'
        'config.patch')

install=install

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	patch -p1 -i ../config.patch
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav \
		--disable-clamav \
                --enable-milter \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Make sure conf files get installed, because make install
	# doesn't do that if clamav is already installed upon building.
	install -Dm644 etc/clamd.conf "${pkgdir}"/etc/clamav/clamd.conf
	install -Dm644 etc/freshclam.conf "${pkgdir}"/etc/clamav/freshclam.conf
	install -Dm644 etc/clamav-milter.conf "${pkgdir}"/etc/clamav/clamav-milter.conf

	install -Dm644 ../service.milter "${pkgdir}"/usr/lib/systemd/system/clamav-milter.service
	install -Dm644 ../service.fresh "${pkgdir}"/usr/lib/systemd/system/freshclamd.service
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/clamd.service
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/clamav.conf
	install -Dm644 ../logrotate "${pkgdir}"/etc/logrotate.d/clamav

	install -d -o 64 -g 64 "${pkgdir}"/run/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/log/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/lib/clamav
}
sha1sums=('078c0ac2b4e69d27eecd7544a8361abcd859e73c'
          'b6ac0c5de1e5b41232b10d4f84e86201c370eaeb'
          '509c4ad618f699a6817c23a17b21474f03c8e482'
          '4e96f7c565d0f3b1b1b94fae66aeaa654498bc2d'
          'e389d42120495446037c46bf5e1ef9f18805fc23'
          'a224ea9b4d0f4f196827347d54bed51e11c197ea'
          'ab9aa898b71e250f9cad0530a0bdc5644bb22cd3')
