# Contributor: linuxSEAT <--put_my_name_here--@gmail.com>

pkgname=zotero-git
_pkgname=zotero
pkgver=20121031
pkgrel=2
pkgdesc='Zotero Standalone'
arch=('i686' 'x86_64')
url='http://www.zotero.org/'
license=('GPL3')
makedepends=('git' 'zip')
provides=('zotero')
conflicts=('zotero')
install=$_pkgname.install
source=($_pkgname.install $_pkgname.desktop)
md5sums=('0b6211b511da48346cecdc1d2f963c76'
         '532cb781a46843b1507766e2e342e130')

_gitroot='https://github.com/zotero/zotero-standalone-build'
_gitname='zotero'

build() {
  cd -- "$srcdir"
  msg "Connecting to GIT server..."
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --recursive --depth=1 $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"

  sed -i -e 's|BUILD_MAC=1|BUILD_MAC=0|g;s|BUILD_WIN32=1|BUILD_WIN32=0|g' "$srcdir"/$_pkgname/config.sh

  "$srcdir"/$_pkgname/fetch_xulrunner.sh
  "$srcdir"/$_pkgname/build.sh
}

package() {
  mkdir -p -- "$pkgdir"/usr/{bin,lib/zotero}
  cp -Rt "$pkgdir"/usr/lib/zotero -- "$srcdir"/$_gitname/staging/Zotero_linux-$CARCH/*
  sed -i -e 's|MOZ_PROGRAM=""|MOZ_PROGRAM="/usr/lib/zotero/zotero"|g' "$pkgdir"/usr/lib/zotero/run-zotero.sh
  ln -Ts /usr/lib/zotero/run-zotero.sh "$pkgdir"/usr/bin/zotero
  install -Dm644 "$srcdir"/$_gitname.desktop "$pkgdir"/usr/share/applications/$_gitname.desktop
  install -Dm644 "$pkgdir"/usr/lib/zotero/chrome/icons/default/default48.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png
}
