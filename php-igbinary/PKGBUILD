# Maintainer: Patrick Palka <patrick@parcs.ath.cx>
# Development: http://closure.ath.cx/aur-dev

_name=igbinary
pkgname="php-$_name"
pkgver=1.1.1
pkgrel=3
pkgdesc="Efficient replacement for the standard PHP serializer"
arch=('i686' 'x86_64')
url="https://github.com/igbinary/igbinary/"
license=('BSD')
depends=('php')
_ininame="$_name.ini"
_inifile="etc/php/conf.d/$_ininame"
backup=("$_inifile")
_filename="$_name-$pkgver"
source=("http://pecl.php.net/get/$_filename.tgz")
md5sums=('4ad53115ed7d1d452cbe50b45dcecdf2')
_builddir="$srcdir/$_filename"

build() {
    cd "$_builddir"
    phpize
    ./configure --prefix=/usr --enable-igbinary
    make
    #make test
}

package() {
    cd "$_builddir"
    make INSTALL_ROOT="$pkgdir" install
    echo ";extension=$_name.so" > "$_ininame"
    echo ";session.serialize_handler=$_name" >> "$_ininame"
    echo ";$_name.compact_strings=On" >> "$_ininame"
    echo ";apc.serializer=$_name" >> "$_ininame"
    install -vDm644 "$_ininame" "$pkgdir/$_inifile"
    install -vDm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
