# Maintainer:  cuihao <cuihao dot leo at gmail dot com>
# Contributor: Guten <ywzhaifei at gmail dot com> 

pkgname=goagent-git
pkgver=20120820
pkgrel=1
pkgdesc="A gae proxy forked from gappproxy/wallproxy"
arch=("i686" "x86_64")
url="http://goagent.googlecode.com"
license=("GPL2")
depends=('python2' 'python2-pyopenssl')
optdepends=(
    'python26: (AUR) upload GAE code using /opt/server/uploader.zip'
)
conflicts=('goagent' 'systemd-goagent-units')
replaces=('systemd-goagent-units')
provides=('goagent')
source=(
  "goagent.daemon"
  "goagent.service"
)
md5sums=('94e66bb9673157bc7fcfc2e7d01be389'
         '1369618bb20df7b2f09c9df69d560e87')
backup=('etc/goagent')
install='goagent-git.install'

_gitroot="git://github.com/goagent/goagent.git"
_gitname="goagent"

build() {
    cd ${srcdir}
    msg "Connecting to GIT server...."

    if [ -d ${_gitname}/.git ] ; then
        cd ${_gitname}
        # Change remote url to anongit
        if [ -z $( git branch -v | grep anongit ) ] ; then
            git remote set-url origin ${_gitroot}
        fi
        git pull origin
        msg "The local files are updated."
    else
        git clone ${_gitroot} ${_gitname}
        msg "GIT checkout done or server timeout"
    fi

    rm -rf "$srcdir/${_gitname}-build"
    cp -r "$srcdir/${_gitname}" "$srcdir/${_gitname}-build"
}

package() {
    cd "$srcdir/${_gitname}-build"

    # python2 fix
    sed -i -re "1s/python2?/python2/" local/*.py
    chmod +x local/proxy.py

    mkdir -p "$pkgdir/opt/goagent"
    cp -r local server "$pkgdir/opt/goagent"
  
    # remove windows-only files
    rm -f "$pkgdir/opt/goagent/"*/*.{vbs,dll,exe,manifest,bat}

    # config file
    install -Dm644 "${pkgdir}/opt/goagent/local/proxy.ini" "${pkgdir}/etc/goagent"
    ln -sf "/etc/goagent" "${pkgdir}/opt/goagent/local/proxy.ini"

    # rc.d daemon
    install -Dm755 "${srcdir}/goagent.daemon" "${pkgdir}/etc/rc.d/goagent"

    # systemd service
    install -Dm644 "${srcdir}/goagent.service" "${pkgdir}/usr/lib/systemd/system/goagent.service"
}

# vim:set ts=2 sw=2 et:
