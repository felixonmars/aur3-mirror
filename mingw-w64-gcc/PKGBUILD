# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com
pkgname=mingw-w64-gcc
pkgver=4.6.2
pkgrel=1
pkgdesc="Cross GCC for the MinGW-w64 cross-compiler"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('mingw-w64')
depends=('zlib' 'libmpc' 'ppl' 'cloog')
makedepends=('mingw-w64-binutils' 'mingw-w64-headers' 'mingw-w64-gcc-base' 'mingw-w64-winpthreads' 'libmpc' 'cloog' 'ppl')
#checkdepends=('dejagnu') # Windows executables could run on Arch through bin_mft and Wine
optdepends=()
provides=('mingw-w64-gcc-base')
conflicts=()
replaces=()
backup=()
options=('!strip' '!libtool' '!emptydirs' '!buildflags')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2)
md5sums=('028115c4fbfb6cfd75d6369f4a90d87e')

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/gcc-${pkgver}"

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  echo ${pkgver} > gcc/BASE-VER

  for _target in ${_targets}; do
    mkdir -p ${srcdir}/gcc-build-${_target} && cd ${srcdir}/gcc-build-${_target}
    
    ${srcdir}/gcc-${pkgver}/configure --prefix=/usr \
        --target=${_target} \
        --enable-languages=c,lto,c++,objc,obj-c++,fortran \
        --enable-shared --enable-static \
        --enable-threads=win32 --enable-fully-dynamic-string \
        --with-system-zlib --with-ppl --enable-cloog-backend=isl \
        --enable-lto --enable-sjlj-exceptions --enable-libgomp \
        --disable-nls \
        --disable-multilib --enable-checking=release
    make all
  done
}

package() {
  for _target in ${_targets}; do
    cd ${srcdir}/gcc-build-${_target}
    make prefix=${pkgdir}/usr install
  done
  # remove unnecessary files
  rm -r ${pkgdir}/usr/share/man
}