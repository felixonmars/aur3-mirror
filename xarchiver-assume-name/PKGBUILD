# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=xarchiver-assume-name
pkgver=0.5.2
pkgrel=4
pkgdesc="Xarchiver with a sensible default archive name in batch mode"
arch=('i686' 'x86_64')
url="http://xarchiver.sourceforge.net/"
license=('GPL')
groups=('xfce4-goodies')
depends=('gtk2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
replaces=('xarchiver')
provides=('xarchiver')
optdepends=('tar: TAR support'
            'gzip: GZIP support'
            'bzip2: BZIP2 support'
            'zip: ZIP support'
            'unzip: ZIP support'
            'unrar: RAR support'
            'p7zip: 7z support'
            'arj: ARJ support'
            'lha: LHA support'
            'lzma-utils: LZMA support'
            'lzop: LZOP support')
options=('!libtool')
install=xarchiver.install
source=(http://downloads.sourceforge.net/xarchiver/xarchiver-$pkgver.tar.bz2 assume_name.patch explicit_extension.patch)

build() {
  cd "$srcdir/xarchiver-$pkgver"
  cd src
  patch -p0 -i "$srcdir"/assume_name.patch
  patch -p0 -i "$srcdir"/explicit_extension.patch
  cd ..
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4
  make
}

package() {
  cd "$srcdir/xarchiver-$pkgver"
  make DESTDIR="$pkgdir" install
}
md5sums=('2bc7f06403cc6582dd4a8029ec9d038d' '29e468d9ad60d078b8670eac2177a5e7' '2ca70379a37f1614565366ac3f757b8e')
