# Maintainer: Hubert Maraszek <marach5 at gmail dot com>
pkgname=mp3tag
pkgver=2.57
pkgrel=1
pkgdesc="A universal tag editor"
arch=('i686' 'x86_64')
url="http://www.mp3tag.de/en/"
license=('custom')
depends=('wine')
makedepends=('p7zip')
source=(mp3tag
        LICENSE
        mp3tag.desktop
        mp3tag.png
        http://download.mp3tag.de/mp3tagv257setup.exe)
md5sums=('b2e8aa8e4748b2668e0504264eacc6a9'
         'ecafef682bedadde129aeb1a3e762c30'
         '6c8cf49f81f247bea965db3cb5a29f45'
         '7146546bd343649d68e983e841bd4cb3'
         'c271db98c6a7fd589cfa0e6f2eaa2e66')

options=('!strip')

package() {
  install -d -m755 "$pkgdir/usr/share/$pkgname"
  7z -y -o"$pkgdir/usr/share/$pkgname" x "$srcdir/mp3tagv257setup.exe"
  rm -rf "$pkgdir/usr/share/mp3tag/\$PLUGINSDIR" "$pkgdir/usr/share/mp3tag/\$R0"
  find "$pkgdir/usr/share/$pkgname" -type d -exec chmod 755 "{}" \;
  find "$pkgdir/usr/share/$pkgname" -type f -exec chmod 644 "{}" \;
  
  install -D -m755 mp3tag "$pkgdir/usr/bin/mp3tag"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 mp3tag.png "$pkgdir/usr/share/pixmaps/mp3tag.png"
  install -D -m644 mp3tag.desktop "$pkgdir/usr/share/applications/mp3tag.desktop"
}

