# Maintainer: rubenvb vanboxem <dottie> ruben <attie> gmail <dottie> com
pkgname=mingw-w64-tools
pkgver=2.0.4
pkgrel=1
pkgdesc='MinGW-w64 gendef and genidl tools'
arch=('i686' 'x86_64')
url='http://mingw-w64.sourceforge.net'
license=('custom')
groups=('mingw-w64')
depends=()
makedepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=('!libtool' '!emptydirs')
source=(http://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.gz)
md5sums=('2dcd2085a29a75e99a077e23699b8928')


build() {
  cd ${srcdir}
  msg "Building MinGW-w64-tools"
  mkdir -p ${srcdir}/gendef-build && cd ${srcdir}/gendef-build
    ${srcdir}/mingw-w64-v${pkgver}/mingw-w64-tools/gendef/configure --prefix=/usr
    make
  mkdir -p ${srcdir}/genidl-build && cd ${srcdir}/genidl-build
    ${srcdir}/mingw-w64-v${pkgver}/mingw-w64-tools/genidl/configure --prefix=/usr
    make
}

package() {
  msg "Installing MinGW-w64-tools"
  cd ${srcdir}/gendef-build
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/genidl-build
  make DESTDIR=${pkgdir} install
  msg "Installing licenses"
  install -Dm644 ${srcdir}/mingw-w64-v${pkgver}/mingw-w64-tools/gendef/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.gendef
  install -Dm644 ${srcdir}/mingw-w64-v${pkgver}/mingw-w64-tools/genidl/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.genidl
}

