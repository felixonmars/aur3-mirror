# Maintainer: Rustam Tsurik <rustam.tsurik@gmail.com>

pkgname=memsource-editor
_pkg=MemSourceEditor
_platform=ubuntu-12.04
pkgver=3.140
pkgrel=1
pkgdesc="A lightweight CAT tool"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.memsource.com/"

if [ "$CARCH" = "i686" ]; then
  depends=('qt4' 'bzip2' 'libsm' 'libx11' 'libxrender' 'freetype2' 'fontconfig' 'libpng12')
elif [ "$CARCH" = "x86_64" ]; then
  depends=('lib32-qt4' 'lib32-bzip2' 'lib32-libsm' 'lib32-libx11' 'lib32-libxrender' 'lib32-freetype2' 'lib32-fontconfig' 'lib32-libpng12')
fi

options=('!strip')
source=(http://download.memsource.com/production/updates/memsource-editor/linux/${_platform}/archive/install/${_pkg}-${pkgver}-${_platform}.run
	memsource-editor.desktop)
sha256sums=('c1c9825aa1fc895b60a009d3a7e7cca72bd5bbba17aa2f0f3a468e00518e6432'
            '745685e38802a03b02a5ec2257fd41b060818dee27eaedc64758642b2ad4b599')

build() {
  mkdir -p "${srcdir}/build"
  cd "${srcdir}/build"
  chmod +x ../${_pkg}-${pkgver}-${_platform}.run

  mkdir -p "${srcdir}/build/Desktop"
  echo "XDG_DESKTOP_DIR=\"${srcdir}/build/Desktop\"" > "${srcdir}/build/user-dirs.dirs"
  export XDG_CONFIG_HOME="${srcdir}/build"
  export HOME="${srcdir}/build"

  ../${_pkg}-${pkgver}-${_platform}.run --mode unattended --prefix "${srcdir}/build/memsource-editor"
}

package() {
  cd "${srcdir}/build/memsource-editor"

  mkdir -p "${pkgdir}/opt/memsource-editor"
  cp -a fonts "${pkgdir}/opt/memsource-editor"
  cp -a lib "${pkgdir}/opt/memsource-editor"
  cp -a spelling "${pkgdir}/opt/memsource-editor"
  cp arch "${pkgdir}/opt/memsource-editor"
  cp cacert.pem "${pkgdir}/opt/memsource-editor"
  cp run.sh "${pkgdir}/opt/memsource-editor"
  chmod +x "${pkgdir}/opt/memsource-editor/run.sh"
  cp TranslationEditor "${pkgdir}/opt/memsource-editor"
  chmod +x "${pkgdir}/opt/memsource-editor/TranslationEditor"

  mkdir -p "${pkgdir}/usr/share/icons"
  cp app.png "${pkgdir}/usr/share/icons/memsource-editor.png"

  mkdir -p "${pkgdir}/usr/share/applications"
  cp "${srcdir}/memsource-editor.desktop" "${pkgdir}/usr/share/applications"
}
