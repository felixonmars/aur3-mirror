# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libdc1394
pkgname=lib32-${_pkgbase}
pkgver=2.2.1
pkgrel=1
pkgdesc="High level programming interface to control IEEE 1394 based cameras (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://sourceforge.net/projects/libdc1394/"
depends=('lib32-libraw1394' 'lib32-libusb' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('5c4b78bb8265d6dc971433ec1da381ab')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
