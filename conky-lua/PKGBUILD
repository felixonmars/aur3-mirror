# Maintainer: Renato Garcia <fgar.renato@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=conky-lua
_pkgname=conky
pkgver=1.8.1
pkgrel=3
pkgdesc='An advanced, highly configurable system monitor for X based on torsmo, with lua bindings enabled.'
arch=('i686' 'x86_64')
url='http://conky.sourceforge.net/'
license=('custom')
replaces=('torsmo')
makedepends=('pkg-config' 'toluapp')
depends=('alsa-lib' 'libxml2' 'curl' 'wireless_tools' 'libxft' 'glib2' 'libxdamage' 'imlib2' 'lua' 'cairo')
provides=('conky=1.8.1')
conflicts=('conky')
backup=('etc/conky/conky.conf' 'etc/conky/conky_no_x11.conf')
source=("http://downloads.sourceforge.net/project/${_pkgname}/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        'current-power.patch')
sha1sums=('06bbe9b7eed8603fa7a971b27c5c61f38424ac6f'
          '2eeef0cc5a0b612826b2a7f0b43b7a3b7e10bf0b')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    patch -p1 < ../current-power.patch # See FS#21984

    sed -i '
    /#include <curl\/types.h>/ c\
    //#include <curl/types.h>' src/eve.c

    sed -i '
    /#include <curl\/types.h>/ c\
    //#include <curl/types.h>' src/ccurl_thread.c

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-ibm \
        --enable-curl \
        --enable-rss \
        --enable-weather-xoap \
        --enable-imlib2 \
        --enable-wlan \
        --enable-lua \
        --enable-lua-cairo \
        --enable-lua-imlib2

    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
