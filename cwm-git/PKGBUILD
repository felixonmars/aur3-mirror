# Maintainer: Christian Neukirchen <chneukirchen@gmail.com>
pkgname=cwm-git
pkgver=20121128
pkgrel=1
pkgdesc="OpenBSD fork of calmwm, a clean and lightweight window manager"
arch=('i686' 'x86_64')
url="http://www.openbsd.org/cgi-bin/man.cgi?query=cwm&sektion=1"
license=('custom')
depends=('libxft' 'libxrandr' 'libxinerama')
makedepends=('git' 'pkg-config')
replaces=('cwm' 'cwm-cvs')
conflicts=('cwm' 'cwm-cvs')
source=('cwm.desktop')
md5sums=('42530ce5a23fc5f9088d891e325d99bd')

_gitroot="git://github.com/chneukirchen/cwm.git"
_gitname=cwm
_gitbranch=linux

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git fetch origin
    msg "The local files are updated."
  else
   git clone --mirror  $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  git checkout $_gitbranch

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -D -m644 "${srcdir}/cwm.desktop" "${pkgdir}/usr/share/xsessions/cwm.desktop"
  install -D -m644 <(sed -n '/Copyright/,/PERFORMANCE/p' <calmwm.c) "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
