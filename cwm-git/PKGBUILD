# Maintainer: Christian Neukirchen <chneukirchen@gmail.com>
pkgname=cwm-git
pkgver=20101124
pkgrel=3
pkgdesc="OpenBSD fork of calmwm, a clean and lightweight window manager"
arch=(i686 x86_64)
url="http://www.openbsd.org/cgi-bin/man.cgi?query=cwm&sektion=1"
license=('custom')
depends=(libxft libxrandr libxinerama)
makedepends=(git)
replaces=(cwm cwm-cvs)
conflicts=(cwm cwm-cvs)
source=(cwm.desktop)
md5sums=('42530ce5a23fc5f9088d891e325d99bd')

_gitroot="git://github.com/chneukirchen/cwm.git"
_gitname=cwm
_gitbranch=linux

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git fetch origin
    msg "The local files are updated."
  else
   git clone --mirror  $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  git checkout $_gitbranch

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  install -D -m755 cwm "${pkgdir}/usr/bin/cwm"
  install -D -m644 cwm.1 "${pkgdir}/usr/share/man/man1/cwm.1"
  install -D -m644 cwmrc.5 "${pkgdir}/usr/share/man/man5/cwmrc.5"
  install -D -m644 "${srcdir}/cwm.desktop" "${pkgdir}/usr/share/xsessions/cwm.desktop"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
