# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=karlyriceditor
pkgver=1.6
pkgrel=1
pkgdesc="A karaoke lyrics editor supporting LRC, LRCv2, Ultrastar, CD+G."
arch=(i686 x86_64)
url="http://www.ulduzsoft.com/linux/karaoke-lyrics-editor/"
license=('GPL3')
depends=('qt4' 'ffmpeg')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('26894f79e64a3d1dd663851baa746b35')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  qmake-qt4
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # bin
  install -Dm755 bin/$pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 packages/$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 src/images/application_icon.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
