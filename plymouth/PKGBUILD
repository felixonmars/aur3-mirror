# Maintainer:  M0Rf30 <morfeo89@hotmail.it>
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Det <nimetonmaili at gmail dot com>
# Contributor: Biginoz <biginoz _at_ free _point_ fr>
# Contributor: Ryon Sherman <ryon.sherman@gmail.com>
# Based on plymouth-git: http://aur.archlinux.org/packages.php?ID=26117

pkgname=plymouth
pkgver=0.8.8
pkgrel=11
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('libdrm' 'pango' 'systemd' 'gtk2')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
backup=('etc/plymouth/plymouthd.conf')
provides=('plymouth')
conflicts=('plymouth-git')
source=("http://www.freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.bz2"
	'arch-logo.png'
        'encrypt_hook'
        'encrypt_install'
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'lightdm-plymouth.service'
        'lxdm-plymouth.service'
	'slim-plymouth.service'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-pid.patch'
        'plymouth-quit.service'
        'plymouth-quit-wait.service'
        'plymouth-set-default-theme.in.patch'
        'plymouth-update-initrd.patch'
        'plymouthd.conf'
        'system-release'
        'systemd-unit-dir.patch'
	'udevadm.patch'
)


build() {
    
  cd $srcdir/$pkgname-$pkgver
  msg2 "Setting a human-readable error for '# plymouth-set-default-theme -R'"
  patch -p0 -i ../plymouth-set-default-theme.in.patch

  msg2 "Fixing updating initrd"
  patch -p0 -i ../plymouth-update-initrd.patch

  msg2 "Changing PID file to /run/plymouth.pid"
  patch -p1 -i ../plymouth-pid.patch

  msg2 "Changing udevadm path"
  patch -p1 -i ../udevadm.patch  

  msg2 "Changing SYSTEMD_UNIT_DIR to /usr/lib/systemd/system/"
  patch -p0 -i ../systemd-unit-dir.patch

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  msg "Starting make..."

    ./configure --prefix=/usr --exec-prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib --libexecdir=/usr/lib --sbindir=/usr/bin\
    --enable-tracing \
    --without-rhgb-compat-link \
    --with-gdm-autostart-file=yes \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D \
    --enable-systemd-integration \
    --without-system-root-install

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/arch-logo.png" "$pkgdir/usr/share/plymouth/arch-logo.png"
  install -Dm644 "$srcdir/encrypt_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "$srcdir/encrypt_install" "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "$srcdir/plymouth.functions" "$pkgdir/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "$srcdir/plymouth.initcpio_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
  install -Dm644 "$srcdir/plymouthd.conf" "$pkgdir/etc/plymouth/plymouthd.conf"
  install -Dm644 "$srcdir/system-release" "$pkgdir/etc/system-release"

  # Plymouth<->systemd integration, including units for DMs
  for i in {{gdm,kdm{,-unpatched},lightdm,lxdm,slim}-plymouth,plymouth-quit-wait,plymouth-quit}.service; do
    install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
  done
}

md5sums=('38f5e613e5ab17806b950cee2d0d0d4e'
         '65fa2763d5c9bb9f80973ea5e6e3db3e'
         '51a17e5bfaccf79760bffee19e6679cb'
         '65eb269910f1b81e3f0970af456394ca'
         '30e6c6cf8728e6a6886b436dbd67def2'
         '75ec0ac73eb9a1e3f6104df64520d431'
         'ceec65ce58accdb270f3eaa23d794b4a'
         '7e8537274bb768d500b50683f8684598'
         '279698faad118b7b72979884fa0471e9'
         '225b953976e1c824f62ba406a63c18c7'
         'c0c5102e0c129fc3eba13a0e372363d9'
         'f13a37a307665014fe05039bdf077b9b'
         'd5a74db1e9a528d291f3cdf655997db9'
         'e4d804825708c4ae555acd6489925b35'
         '9418535bacbc27dabd4d15fce9e7cf0c'
         'a8bab4f948ab553f3123a72a5b2dea74'
         '42fddd683720dbacbe6e30b4b7892f3c'
         'c2a0cf5a499d6c14723461166a24ede7'
         '37479b134827dc307b3c55bfb2711e99'
         'ab8a557d10f74bec5c94ed6cae34729c'
         '30acbfc7722fb62ee464b8fec32b5cfd'
         '89014b04513d61e0085e7a0c9924c183')
