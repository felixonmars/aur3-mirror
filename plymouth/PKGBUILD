# Maintainer:  M0Rf30 <morfeo89@hotmail.it>
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Det <nimetonmaili at gmail dot com>
# Contributor: Biginoz <biginoz _at_ free _point_ fr>
# Based on plymouth-git: http://aur.archlinux.org/packages.php?ID=26117

pkgname=plymouth
pkgver=0.8.6.1
pkgrel=2
pkgdesc="A graphical boot splash screen with kernel mode-setting support (Git version)"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('libdrm' 'pango' 'systemd')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
backup=('etc/plymouth/plymouthd.conf')
provides=('plymouth')
conflicts=('plymouth-git')
source=("http://www.freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.bz2"
	'arch-logo.png'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-$CARCH/encrypt_hook"
        'encrypt_hook.patch'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-$CARCH/encrypt_install"
        'encrypt_install.patch'
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'lxdm-plymouth.service'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-update-initrd.patch'
        'plymouthd.conf'
        'system-release'
        'systemd-unit-dir.patch'
)


build() {
  cd $srcdir
  
   msg "Applying Patches..."

  patch -p0 -i encrypt_hook.patch
  patch -p0 -i encrypt_install.patch

  cd $pkgname-$pkgver
  patch -p0 -i "$srcdir/plymouth-update-initrd.patch"

  # Change SYSTEMD_UNIT_DIR to /usr/lib/systemd/system
  patch -p0 -i "$srcdir/systemd-unit-dir.patch"

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  msg "Starting make..."

    ./configure --prefix=/usr --exec-prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib --libexecdir=/usr/lib \
    --enable-tracing \
    --without-rhgb-compat-link \
    --with-gdm-autostart-file=yes \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D \
    --enable-systemd-integration \
    --without-system-root-install

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
  install -Dm644 "$srcdir/plymouth.initcpio_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "$srcdir/encrypt_install" "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "$srcdir/encrypt_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "$srcdir/plymouth.functions" "$pkgdir/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "$srcdir/arch-logo.png" "$pkgdir/usr/share/plymouth/arch-logo.png"
  install -Dm644 "$srcdir/system-release" "$pkgdir/etc/system-release"
  install -Dm644 "$srcdir/plymouthd.conf" "$pkgdir/etc/plymouth/plymouthd.conf"

  # Plymouth<->systemd integration, units for DMs
  install -Dm644 "$srcdir/kdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/kdm-plymouth.service"
  install -Dm644 "$srcdir/gdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/gdm-plymouth.service"
  install -Dm644 "$srcdir/lxdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/lxdm-plymouth.service"
  install -Dm644 "$srcdir/kdm-unpatched-plymouth.service" "$pkgdir/usr/lib/systemd/system/kdm-unpatched-plymouth.service"
}

md5sums=('7c801a71bdd5d9aa4d628feec27a21ba'
         '65fa2763d5c9bb9f80973ea5e6e3db3e'
         '4dab1b0e23d81907b79b49c2d8d719b5'
         'c31bc1fb5c15b0e6e9723ec79098fe76'
         '21c45f9cab3e0b5165f68358884fbd0f'
         '5ad5fb82557ce5346307e977ee245ffc'
         'f4ab60fc4a9e5cbc13816a843c6bd4a1'
         '63357be7879122c608072963c6a0afd5'
         '9a07137f410bbff22c07d71826c9719b'
         '0ec399a5677576d6fa4fb9cda028fcd9'
         'f0110fba1e77cd22c32d1727399d0b8a'
         '9d449582883f0c72d3a3e381fe2b8c8c'
         'e606ce14ceace727c2f13758e371a4ed'
         'c2a0cf5a499d6c14723461166a24ede7'
         '37479b134827dc307b3c55bfb2711e99'
         'ab8a557d10f74bec5c94ed6cae34729c'
         '30acbfc7722fb62ee464b8fec32b5cfd')
