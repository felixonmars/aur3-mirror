pkgname=plymouth
pkgver=0.8.8.97.gf3d73e1
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support (Git verson for now)"
url="http://cgit.freedesktop.org/cgit/plymouth/"

arch=('i686' 'x86_64')
license=('GPL')

depends=('libdrm' 'pango' 'systemd')
makedepends=('git' 'docbook-xsl')
optdepends=('ttf-dejavu')
backup=('etc/plymouth/plymouthd.conf')
options=('!emptydirs')
conflicts=('plymouth-git')

source=(
'git://anongit.freedesktop.org/plymouth'
'plymouth.hook'
'plymouth.install'
'plymouth-encrypt.hook'
'plymouth-encrypt.install'
'arch-logo.png'
)

md5sums=(
'SKIP'
'SKIP'
'SKIP'
'SKIP'
'SKIP'
'SKIP'
)
pkgver() {
	cd "$srcdir"/plymouth
	git describe --always | sed 's/-/./g'
}

build() {
	cd "$srcdir"/plymouth

	#export LDFLAGS+=" -ludev"

	./autogen.sh --prefix=/usr \
		--exec-prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--sbindir=/usr/bin \
		--enable-drm-renderer \
		--enable-systemd-integration \
		--enable-pango \
		--with-logo=/usr/share/plymouth/arch-logo.png \
		--without-system-root-install

	make
}

package() {
	cd "$srcdir"/plymouth
	make DESTDIR="$pkgdir" install

	# logo
	install -Dm644 "$srcdir"/arch-logo.png "$pkgdir/usr/share/plymouth/arch-logo.png"

	# remove non-arch script
	rm "$pkgdir/usr/lib/plymouth/"*-initrd
	rm "$pkgdir/usr/bin/plymouth-set-default-theme"

	# initcpio
	install -Dm644 "$srcdir/plymouth.hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth"
	install -Dm644 "$srcdir/plymouth.install" "$pkgdir/usr/lib/initcpio/install/plymouth"
	install -Dm644 "$srcdir/plymouth-encrypt.hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
	install -Dm644 "$srcdir/plymouth-encrypt.install" "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
}
