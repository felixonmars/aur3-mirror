# Maintainer: Jonne Ha√ü <me@mrzyx.de>
pkgname=crystal
pkgver=0.5.0
pkgrel=2
pkgdesc="The Crystal Programming Language"
arch=('x86_64')
url="http://crystal-lang.org"
license=('Apache')
depends=('libunwind' 'gc' 'llvm' 'libatomic_ops' 'pcre')
checkdepends=('libyaml')
optdepends=('libyaml: For YAML support'
            'gmp: For BigInt support'
            'libpcl: For coroutine (Fiber) support')
conflicts=('crystal-git')
source=("https://github.com/manastech/crystal/releases/download/$pkgver/$pkgname-$pkgver-1-linux-x86_64.tar.gz"
        "https://github.com/manastech/crystal/archive/$pkgver.tar.gz"
        "crystal.sh")

prepare() {
  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  cp "$srcdir/$pkgname-$pkgver-1/embedded/bin/crystal" "$srcdir/$pkgname-$pkgver/deps/crystal"
  sed -e "s/#{tag}/$pkgver/" -e "s/#{sha}/release/" -i "$srcdir/$pkgname-$pkgver/src/compiler/crystal/version.cr"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make release=1
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make spec
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # /usr/bin/crystal wrapper script ensuring right environment, ideally the executable would have compiled in defaults. Maybe provide profile.d file instead
  # /usr/lib/crystal/crystal compiled executable
  # /usr/lib/crystal/src compiler src & core libs
  # /usr/lib/cystal/libs stdlib
  # /usr/share/doc/crystal/samples samples
  install -Dm755 "$srcdir/crystal.sh" "$pkgdir/usr/bin/crystal"
  install -dm755 "$pkgdir/usr/lib/crystal"
  install -Dm755 ".build/crystal" "$pkgdir/usr/lib/crystal/crystal"
  cp -rv src "$pkgdir/usr/lib/crystal/"
  cp -rv libs "$pkgdir/usr/lib/crystal/"
  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -rv samples "$pkgdir/usr/share/doc/crystal/"
}
sha256sums=('17071df3c7b975463591778b5baeb652a40aa539635668be1b4391857bcc1d8d'
            '526b00a228c1345cfd4a118009be4d6cb002292ba0c95afd832e2c84cbc86b02'
            'f6c44b98aa5a08749ef5fa349f9414543a7a5719b020becc22782a2a440d73d4')
