# Maintainer: ava1ar <mail(at)ava1ar(dot)info>

pkgname=system76-driver
pkgver=13.10.3
pkgrel=1
pkgdesc="System76 Laptops Driver. Tested on System76 Galago UltraPro."
arch=('any')
url="https://www.system76.com/"
license=('GPL')
depends=('python' 'python-gobject' 'dmidecode' 'python-dbus')
optdepends=(
	'pm-utils: To enable powersaving features'
	'pulseaudio: To apply microphone fix'
)
install="${pkgname}.install"
source=(
	"https://launchpad.net/~system76-dev/+archive/stable/+files/${pkgname}_${pkgver}.tar.gz"
	'arch_galu1.patch'
	'system76.service'
)
sha1sums=(
	'f90ce2e93263cc1715de4ec6e17b6c6c1274b016'
	'89999dc6d47a239cbb42d6e5533e2960493cc8ec'
	'cf763432441c3da0563eee041145a02109f2bb3a'
)

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	# system76-daemon
	install -m644 -D system76-daemon ${pkgdir}/usr/lib/${pkgname}/system76-daemon
	# system76-driver-pkexec
	install -m755 -D system76-driver-pkexec ${pkgdir}/usr/bin/system76-driver-pkexec
	# systemd service
	install -m644 -D ${startdir}/system76.service ${pkgdir}/usr/lib/systemd/system/system76.service
	# python module
	python setup.py install --prefix=/usr --root=${pkgdir} --optimize=1
	cd ${pkgdir} &&	patch --no-backup-if-mismatch -Np1 -i ${srcdir}/arch_galu1.patch 
	# some cleanup
	rm -rf ${pkgdir}/usr/lib/python3.3/site-packages/system76driver/tests/
} 
