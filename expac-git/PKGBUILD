# Contributor: Dave Reisner <d@falconindy.com>

pkgname=expac-git
pkgver=20110308
pkgrel=1
pkgdesc="pacman database extraction utility"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/expac"
license=('MIT')
depends=('pacman')
makedepends=('git' 'perl')
conflicts=('expac')
provides=('expac')

_gitroot="git://github.com/falconindy/expac.git"
_gitname="expac"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  readelf -s /usr/lib/libalpm.so | grep -q alpm_find_satisfier &&
    PMCHECK="-D_HAVE_ALPM_FIND_SATISFIER"
  readelf -s /usr/lib/libalpm.so | grep -q [^_]alpm_db_register_local &&
    PMCHECK+=" -D_HAVE_ALPM_DB_REGISTER_LOCAL"

  make PMCHECK="$PMCHECK"
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
