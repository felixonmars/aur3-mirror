pkgname=lib32-libx11-nokeyboardgrab
pkgver=1.6.2
pkgrel=1
pkgdesc='Core X11 protocol client library (32-bit)'
arch=(x86_64)
url=http://www.x.org
license=(custom)
depends=(lib32-libxcb libx11)
makedepends=(gcc-multilib inputproto xextproto xorg-util-macros xtrans)
provides=(lib32-libx11=$pkgver)
options=(!libtool)
source=(http://x.org/releases/individual/lib/libX11-$pkgver.tar.bz2)
sha1sums=(0dd74854e6f6bb8a322e88ba3e89f87c3dcf9e08)
sha256sums=(2aa027e837231d2eeea90f3a4afe19948a6eb4c8b2bec0241eba7dbc8106bd16)

prepare() {
  cd libX11-$pkgver

  sed -i '/xGrabKeyboardReply/i return (GrabSuccess);' src/GrKeybd.c
}

build() {
  cd libX11-$pkgver

  export CC='gcc -m32' PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure \
    --disable-specs \
    --disable-static \
    --disable-xf86bigfont \
    --libdir=/usr/lib32 \
    --prefix=/usr

  make
}

check() {
  cd libX11-$pkgver

  make check
}

package() {
  cd libX11-$pkgver

  make DESTDIR=$pkgdir install

  rm -r $pkgdir/usr/{include,share}

  install -d $pkgdir/usr/share/licenses/$pkgname
  install -m644 COPYING $pkgdir/usr/share/licenses/$pkgname
}
