# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Patch by Procyon

_pkgbasename=libx11
pkgname=lib32-$_pkgbasename-nokeyboardgrab
pkgver=1.5.0
pkgrel=2
pkgdesc="X11 client-side library with no keyboard grab patch (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
depends=('lib32-libxcb' $_pkgbasename)
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto' 'gcc-multilib')
provides=(lib32-libx11=$pkgver)
conflicts=(lib32-libx11)
options=('!libtool')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2)
sha256sums=('c382efd7e92bfc3cef39a4b7f1ecf2744ba4414a705e3bc1e697f75502bd4d86')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libX11-${pkgver}"

  # Patch keyboard grab prevention.
  sed -i '/xGrabKeyboardReply/i return (GrabSuccess);' src/GrKeybd.c

  # Mouse grab prevention. Doesn't work with some games.
  #sed -i '/xGrabPointerReply/i return (GrabSuccess);' src/GrPointer.c

  ./configure --prefix=/usr --disable-static --disable-xf86bigfont \
      --libdir=/usr/lib32 --disable-specs
  make
}

check() {
  cd "${srcdir}/libX11-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
