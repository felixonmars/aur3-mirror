# Maintainer: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Quentin Foussette <quentinf7@gmail.com>

pkgname=epson-inkjet-printer-escpr
pkgver=1.2.0
pkgrel=3
pkgdesc="A common Linux printer driver for Epson Multifonction Inkjet Printers."
arch=('i686' 'x86_64')
url="https://www.epson.com"
license=('GPL')
depends=('cups' 'gutenprint')
source=(http://download.ebz.epson.net/dsc/op/stable/SRPMS/$pkgname-$pkgver-1lsb3.2.src.rpm)
md5sums=('6981748275c8f88d369f5b9e1998aef2')

build() {
  cd "$srcdir"
  tar xzvf "$pkgname-$pkgver-1lsb3.2.tar.gz"
  cd "$pkgname-$pkgver"
  sed -i "s:/opt/epson-inkjet-printer-escpr/cups/lib/filter:/usr/lib/cups/filter:g" src/Makefile.in
  sed -i "s:/opt/epson-inkjet-printer-escpr/cups/lib/filter:/usr/lib/cups/filter:g" src/Makefile.am
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib --libdir=/usr/lib
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
