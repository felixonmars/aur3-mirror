# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
pkgname=aurqt-git
_pyname=aurqt
_gitname=aurqt
pkgver=20130504
pkgrel=2
pkgdesc='A graphical AUR manager.  (git version)'
arch=('any')
url='https://github.com/Kwpolska/aurqt'
license=('BSD')
depends=('python' 'pyalpm>=0.5.1-1' 'python-requests' 'pkgbuilder>=3.0.1-1'
         'pyqt' 'python-beautifulsoup4')
makedepends=('git')
options=(!emptydirs)
provides=('aurqt')
conflicts=('aurqt')
source=("git://github.com/Kwpolska/${_gitname}.git"
        "aurqt.desktop")
md5sums=('SKIP'
         '24dd5d405c4c35f5ac14d95332795209')

package() {
  cd "${srcdir}/${_gitname}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 ../aurqt.desktop "${pkgdir}/usr/share/applications/aurqt.desktop"
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --always | sed 's/-/_/g;s/v//'
}

# vim:set ts=2 sw=2 et:
