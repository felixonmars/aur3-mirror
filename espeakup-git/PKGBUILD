# Maintainer: Storm Dragon <stormdragon2976@gmail.com>

_gitname='espeakup'
pkgname="$_gitname"-git
pkgver=v0.71.115.gd95ee07
pkgrel=1
pkgdesc='allows the Speakup screen review system to use the ESpeak synthesizer'
arch=('i686' 'x86_64')
url='http://github.com/williamh/espeakup'
license=('GPL3')
depends=('speakup-utils' 'espeak')
provides=('espeakup')
conflicts=('espeakup')
install="$pkgname".install
source=("git://github.com/williamh/$_gitname.git"
        'espeakup@.service'
        'speakup.conf')
sha256sums=('SKIP'
            '5ee89ab5ef90e8a7538c82cebed3a8155b3c2bf06d7e814e0211b5a4ba029642'
            '4c39f77f682335dee0daf7d12013bdbd695c4740acead9c7414dd3189269472e')

pkgver() {
  cd "$srcdir/$_gitname"
  local ver="$(git describe --tags)"
  echo "${ver//-/.}"
}

prepare() {
  cd "$srcdir/$_gitname"
  sed -i 's/const int rateMultiplier = 41;/const int rateMultiplier = 120;/' espeak.c
  make
}

build() {
  cd "$srcdir/$_gitname"
  make
}

package() {
  cd "$srcdir/$_gitname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D "$srcdir/espeakup@.service" "$pkgdir/usr/lib/systemd/system/espeakup@.service"
  install -m644 -D "$srcdir/speakup.conf" "$pkgdir/etc/modules-load.d/speakup.conf"
}

# vim: set ts=2 sw=2 et:
