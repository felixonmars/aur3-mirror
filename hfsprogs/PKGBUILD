# Maintainer: Prashant Vaibhav <mercurysquad+arch@gmail.com>
# Previous Maintainer: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Rubin Simons <rubin@xs4all.nl>

pkgname=hfsprogs
pkgver=332.25
pkgrel=4
pkgdesc="HFS/HFS+ user space utils" 
arch=('i686' 'x86_64')
depends=('libbsd')
license=('custom:APSL')
url="http://www.opensource.apple.com/"
source=(http://ftp.de.debian.org/debian/pool/main/h/hfsprogs/hfsprogs_332.25.orig.tar.gz \
	http://ftp.de.debian.org/debian/pool/main/h/hfsprogs/hfsprogs_332.25-9.debian.tar.gz \
        license
	makefile.patch)
md5sums=('261c3de5ec0dcf5244e3f60d79c1d6f1'
	 '5bc5c56041291db36fcbcbc2e4636638'
         '771f0a6036a80d929c07df49f9a3d909'
	 'e2429944c0192d817c700b8f0dffce6e')

build() {
    cd $srcdir/debian/patches/
    rm $srcdir/debian/patches/series
    cd $srcdir/diskdev_cmds-${pkgver}
    for i in $srcdir/debian/patches/*; do patch -p1 < $i
    done
    patch -Np0 -i "${srcdir}/makefile.patch" || return 1
    make -f Makefile.lnx
}

package() {
    cd $srcdir/diskdev_cmds-${pkgver}
    install -d -m 755 "$pkgdir/usr/share/hfsprogs"
    install -m 644 newfs_hfs.tproj/hfsbootdata.img "$pkgdir/usr/share/hfsprogs/hfsbootdata"
    install -D -m755 newfs_hfs.tproj/newfs_hfs "$pkgdir/sbin/mkfs.hfsplus"
    install -D -m755 fsck_hfs.tproj/fsck_hfs "$pkgdir/sbin/fsck.hfsplus"
    install -D -m644 $startdir/src/license "$pkgdir/usr/share/licenses/hfsprogs/APSL"
    install -d -m 755 "$pkgdir/usr/share/man/man8/"
    install -m 644 newfs_hfs.tproj/newfs_hfs.8 "$pkgdir/usr/share/man/man8/mkfs.hfsplus.8"
    install -m 644 fsck_hfs.tproj/fsck_hfs.8 "$pkgdir/usr/share/man/man8/fsck.hfsplus.8"
}
