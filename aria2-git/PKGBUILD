pkgname=aria2-git
pkgver=20120502
pkgrel=1
pkgdesc="Download utility that supports HTTP(S), FTP, BitTorrent, and Metalink"
arch=(i686 x86_64)
url="http://aria2.sourceforge.net/"
license=(GPL)
depends=(gnutls libxml2 sqlite3 c-ares ca-certificates)
makedepends=(git cppunit asciidoc)
provides=(aria2)
conflicts=(aria2 aria2-daemon-svn)
source=(aria2.conf aria2.d)
md5sums=(4ac35c66fb40ef8f35e8e280ad6f30f3 d22416c1463a26d3c4642dfbd60fd96b)
install=aria2-git.install
_gitroot="https://github.com/tatsuhiro-t/aria2.git"
_gitname="aria2"

build(){
  cd $srcdir
  if [[ -d "$_gitname" ]]; then
    cd "$_gitname"
    git reset --hard
    git clean -dxf
    git pull 
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi

  autoreconf -i
  ./configure --prefix=/usr --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "$srcdir/$_gitname"
  install -D -m755 $srcdir/aria2.d $pkgdir/etc/rc.d/aria2
  install -D -m644 $srcdir/aria2.conf $pkgdir/etc/aria2.conf
  mkdir -p $pkgdir/srv/aria2

  make install DESTDIR="$pkgdir" || true

  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 ${srcdir}/$_gitname/doc/bash_completion/aria2c ${pkgdir}/usr/share/bash-completion/completions
  rm -rf ${pkgdir}/usr/share/doc/aria2/bash_completion
} 

