# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-system-monitor
pkgver=20120706
pkgrel=1
pkgdesc="A system monitor for MATE"
arch=(i686 x86_64)
license=('GPL')
depends=('libmatewnck' 'libgtop' 'gtkmm' 'librsvg' 'mate-icon-theme')
makedepends=('git' 'mate-doc-utils' 'pkgconfig' 'intltool')
groups=('mate-extras')
options=(!emptydirs !libtool)
url="http://matsusoft.com.ar/projects"
install=mate-system-monitor.install
source=()
sha256sums=()

_gitroot=git://github.com/mate-desktop/mate-system-monitor.git
_gitname=mate-system-monitor

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"

	export LIBS='-lgmodule-2.0'

	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--libexecdir=/usr/lib/${pkgname} \
		--localstatedir=/var --disable-static \
		--disable-scrollkeeper \
		--disable-schemas-install || return 1

	# there is some pre-compiled files on the tar
	make || return 1
}

package() {
	cd "$srcdir/$_gitname-build"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
	#rm -f ${pkgdir}/usr/lib/*.la # can be used options=(!libtools)

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
