# Maintainer: Tom Vincent <http://tlvince.com/contact>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Juergen Hoetzel <juergen@hoetzel.info>
# Contributor: basilburn (basilburn), Paul Bredbury (brebs)

_pkgname=privoxy
pkgname=freedombox-$_pkgname-git
_pkgver=3.0.19
pkgver=20121129
pkgrel=1
pkgdesc='Privoxy with HTTPS Everywhere and EasyList support'
arch=('i686' 'x86_64')
url='https://github.com/jvasile/freedombox-privoxy'
license=('GPL')
depends=('pcre' 'zlib')
makedepends=('autoconf' 'git' 'python2')
provides=($_pkgname)
conflicts=($_pkgname)
backup=('etc/conf.d/privoxy'
        'etc/privoxy/'{config,trust,default.action,user.filter,default.filter}
        'etc/privoxy/'{match-all,user}.action
        'etc/logrotate.d/privoxy')
install='privoxy.install'
source=("http://downloads.sourceforge.net/ijbswa/${_pkgname}-${_pkgver}-stable-src.tar.gz"
        'privoxy'
        'privoxy.logrotate.d'
        'privoxy.conf.d'
        'privoxy.service'
        'freedombox-privoxy-config.patch')
md5sums=('57acc79059565cc42eda67982842785d'
         '1b4ed8c4e7e5b04b10ef41b597dc3f3b'
         'fc3046377137003726a151ee27b142a2'
         '27830ef79418e277b90c1c1fa933f876'
         '4c154a126ec17b02c623e3d451e062d9'
         '8aa1ecad356a9aa9fda3c86ae99d2a9c')

_gitroot=https://github.com/jvasile/freedombox-privoxy.git
_gitname=freedombox-privoxy

build() {
  # Build freedombox-privoxy
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i "s_#!/usr/bin/env python_&2_" *.py

  make privoxy/easyprivacy.action privoxy/easylist.action \
    privoxy/https_everywhere.action

  # Build privoxy
  privoxy="${srcdir}/${_pkgname}-${_pkgver}-stable"
  rm -rf "${privoxy}-build"
  cp -R "${privoxy}" "${privoxy}-build"
  cd "${privoxy}-build"

  mv "${srcdir}/$_gitname-build/privoxy/filters.c" .

  patch < "${srcdir}/freedombox-privoxy-config.patch"

  autoheader
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc/privoxy --enable-compression

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgver}-stable-build"

  make prefix="${pkgdir}/usr" MAN_DEST="${pkgdir}/usr/share/man/man1" GROUP=42 \
    CONF_BASE="${pkgdir}/etc/privoxy" VAR_DEST="${pkgdir}/var" install

  # Don't overwrite existing log files!
  rm "${pkgdir}/var/log/privoxy/"*

  install -Dm0755 "${srcdir}/privoxy" "${pkgdir}/etc/rc.d/privoxy"
  install -Dm0644 "${srcdir}/privoxy.logrotate.d" "${pkgdir}/etc/logrotate.d/privoxy"
  install -Dm0644 "${srcdir}/privoxy.conf.d" "${pkgdir}/etc/conf.d/privoxy"

  # Fix config paths.
  sed -i -e 's#^confdir.*$#confdir /etc/privoxy#' -e 's#^logdir.*$#logdir /var/log/privoxy#' \
    -e '/^user-manual/s#.*#\#user-manual /usr/share/doc/privoxy/user-manual/#' \
    "${pkgdir}/etc/privoxy/config"

  # Fix group ownership and permissions.
  chgrp -R 42 "${pkgdir}/etc/privoxy/"
  chgrp -R 42 "${pkgdir}/var/log/privoxy/"

  find "${pkgdir}/etc/privoxy/" -type d | xargs chmod 0770
  find "${pkgdir}/etc/privoxy/" -type f | xargs chmod 0660
  chmod 0700 "${pkgdir}/var/log/privoxy"

  # avoid conflict with filesystem>=2012.06
  rmdir "$pkgdir/var/run"

  # systemd unit file
  install -Dm0644 "${srcdir}/privoxy.service" "${pkgdir}/usr/lib/systemd/system/privoxy.service"

  # Install freedombox-privoxy actions
  for i in easylist easyprivacy https_everywhere; do
    install -Dm0644 "${srcdir}/${_gitname}-build/privoxy/$i.action" "${pkgdir}/etc/privoxy/$i.action"
  done
}
