#Maintainer: Bhoppi Chaw <bhoppi#outlook,com>

pkgname=audenc
pkgver=3.1
pkgrel=2
pkgdesc='a batch script for encoding directories with audio files from one format to another.'
arch=(any)
url='http://sourceforge.net/projects/audenc/'
license=(GPL2)
depends=(mediainfo
         mplayer)
optdepends=('lame: MP3 encoder'
            'flac: FLAC encoder'
            'vorbis-tools: OGG encoder'
            'aften: A/52 encoder'
            'neroaacenc: Nero AAC encoder'
            'neroaactag: Nero AAC tagger'
            'neroaac: Nero AAC encoder and tagger within one package'
            'faac: FAAC encoder'
            'aacplusenc: AAC+ encoder'
            'gpac: MP4 container for AAC+'
            'amrenc: AMR encoder'
            'dcaenc: DTS encoder')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")

prepare()
{
    cd $srcdir/$pkgname-$pkgver
    sed -i -e "s,/usr/local,$pkgdir/usr,g" \
           -e '/^MANDIR=/s,usr/man,usr/share/man,' install
}

package()
{
    cd $srcdir/$pkgname-$pkgver
    ./install
    rm $pkgdir/usr/share/doc/audenc/uninstall
}

md5sums=('47f9de9f8df8be18146ea45377baa3ce')
