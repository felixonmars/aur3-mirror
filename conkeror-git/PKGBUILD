# Maintainer: Ivy Foster <joyfulgirl@archlinux.us>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Heeru Kiyura <M8R-p9i5nh@mailinator.com>

pkgname=conkeror-git
pkgver=20121213
pkgrel=1
pkgdesc="A highly programmable web browser based on Mozilla XULRunner."
arch=('i686' 'x86_64')
url="http://conkeror.mozdev.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('xulrunner' 'desktop-file-utils')
makedepends=('git' 'imagemagick')
provides=(conkeror)
source=(conkeror_gimpfile.xpm conkeror.sh)
md5sums=('b592582a5b923db1707615564a95737f'
         '11c6c76a2639254754b34a542f1965a1')
changelog=ChangeLog
install=conkeror-git.install

_gitroot="git://repo.or.cz/conkeror.git"
_gitname="conkeror"

build() {
  cd "$srcdir"

 ## Git checkout
  if [ -d "$srcdir"/${_gitname} ] ; then
    msg "Git checkout:  Updating existing tree"
    cd ${_gitname} && git pull origin master
    msg "Git checkout:  Tree has been updated"
  else
    msg "Git checkout:  Retrieving sources"
    git clone ${_gitroot} && cd ${_gitname}
  fi
  msg "Checkout completed"

 ## Build
  [ -d "$srcdir"/${_gitname}-build ] && rm -rf "$srcdir"/${_gitname}-build
  #git clone "$srcdir"/${_gitname} "$srcdir"/${_gitname}-build
  cp -a "$srcdir"/${_gitname} "$srcdir"/${_gitname}-build
  cd "$srcdir"/${_gitname}-build
  make
}

package() { 
  cd "$srcdir"/${_gitname}-build

  install -d "$pkgdir"/usr/share/{conkeror,man/man1,pixmaps}
  cp -a "$srcdir"/${_gitname}-build/* "$pkgdir"/usr/share/conkeror

  install -Dm644 "$pkgdir"/usr/share/conkeror/contrib/man/conkeror.1 \
    "$pkgdir"/usr/share/man/man1/conkeror.1
  install -Dm644 "$srcdir"/${_gitname}-build/debian/conkeror.desktop \
    "$pkgdir"/usr/share/applications/conkeror.desktop
  install -Dm644 "$srcdir"/conkeror_gimpfile.xpm "$pkgdir"/usr/share/pixmaps

  install -Dm755 "$srcdir"/conkeror.sh "$pkgdir"/usr/bin/conkeror

  mv "$pkgdir"/usr/share/conkeror/conkeror-spawn-helper "$pkgdir"/usr/bin
  rm "$pkgdir"/usr/share/conkeror/conkeror-spawn-helper.c
  rm -r "$pkgdir"/usr/share/conkeror/contrib/man
  rm -r "$pkgdir"/usr/share/conkeror/debian
} 
