# Maintainer: Adam Reichold <adamreichold@myopera.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=qpdfview-bzr
pkgver=309
pkgrel=1
pkgdesc='A tabbed PDF viewer using the poppler library. (development version)'
arch=('i686' 'x86_64')
url='https://launchpad.net/qpdfview'
license=('GPL3')
depends=('qt' 'poppler-qt' 'libcups')
makedepends=('bzr')
conflicts=('qpdfview')
_bzrtrunk='lp:qpdfview'
_bzrmod='qpdfview'

build() {
  cd "$srcdir"

  msg 'Obtaining source code...'

  if [ -d $_bzrmod ] ; then
    cd $_bzrmod && bzr pull $_bzrtrunk
  else
    bzr checkout --lightweight $_bzrtrunk && cd $_bzrmod
  fi

  msg 'Building program...'

  qmake
  make
}

package() {
  cd "$srcdir/$_bzrmod"

  make INSTALL_ROOT=$pkgdir install
}
