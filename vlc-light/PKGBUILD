# Contributor: Zsolt Udvari <udvzsolt gmail com>
# Based on vlc-mini by anrxc

basename=vlc
pkgname=vlc-light
pkgver=2.0.8.a
pkgrel=1
pkgdesc="A MPEG, VCD, DVD and DivX player with extra minimal dependencies"
arch=("i686" "x86_64")
url="http://www.videolan.org/vlc/"
license=("GPL2")
depends=("libmad" "ffmpeg" "sysfsutils" "qt4"
         "libdvdread" "libxml2" "a52dec" "x264"
         "libmpeg2" "libxcb"
         "libdvdnav")
makedepends=("pkgconfig" "ncurses")
optdepends=("libdvdcss: for decoding encrypted DVDs"
            "ncurses: for ncurses interface support")
provides=("vlc")
conflicts=("vlc")
options=("!libtool")
install="${pkgname}.install"
source=("http://download.videolan.org/pub/videolan/${basename}/${pkgver/.a}/${basename}-${pkgver/.a/a}.tar.xz"
        vlc-2.0.7-vaapi-compat.patch
        vlc-2.0.8-Fix-live555-breakage-in-last-update.patch)
md5sums=('e5000677181406d026ffe448633d1ca0'
         '6df10774bb7acf20e09d6139e5c7839e'
         'e592efe7bb65291a84f99cfaa3a9f8a2')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver/.a}"

  patch -Np1 -i ../vlc-2.0.7-vaapi-compat.patch
  patch -Np1 -i ../vlc-2.0.8-Fix-live555-breakage-in-last-update.patch

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c
  sed -i -e 's:truetype/ttf-dejavu:TTF:g' modules/visualization/projectm.cpp
}

build() {
  cd "${srcdir}/${basename}-${pkgver/.a}"


  rm -rf ../build
  mkdir "../build"
  cd ../build

  CFLAGS+="-I/usr/include/samba-4.0" CPPFLAGS+="-I/usr/include/samba-4.0" \
  PKG_CONFIG_PATH+="/usr/lib/ffmpeg-compat/pkgconfig" \
  ../${basename}-${pkgver}/configure --prefix=/usr \
              --enable-{alsa,avcodec,avformat,a52,dvdnav,dvdread,fontconfig,freetype,glx} \
              --enable-{libxml2,libmpeg2,loader,mad,ncurses} \
              --enable-{optimize-memory,optimizations,qt4,sout} \
              --enable-{vlc,xvideo,xcb,x264} \
              --disable-{aa,atmo,bluray,bonjour,caca,coverage,cprof} \
              --disable-{dbus,dbus-control,dca,dc1394,debug,decklink,dirac,directfb,directx,dshow,dv,faad,fbosd,flac,fluidsynth,fribidi} \
              --disable-{gme,gnomevfs,gnutls,goom,gprof,growl,httpd,jack,kate} \
              --disable-{libass,libcddb,libgcrypt,libproxy,libtar,linsys,lirc,live555,loader,lua} \
              --disable-{macosx,macosx-audio,mkv,mod,mpc,mtp,nls,notify,ogg,opencv,oss} \
              --disable-{png,portaudio,postproc,projectm,pulse,pvr,quicktime,real,realrtsp,rpath,run-as-root} \
              --disable-{schroedinger,screen,sdl,sdl-image,sftp,shine,shout,sid,skins2,smb,speex,sqlite,svg,switcher,swscale} \
              --disable-{taglib,telepathy,telx,theora,tiger,tremor,twolame} \
              --disable-{udev,upnp,update-check,vcd,vcdx,visual,vlm,vorbis,v4l2} \
              --disable-{waveout,wingdi,wma-fixed,xosd,zvbi} \
              RCC=/usr/bin/rcc-qt4
  make
}

package() {
  cd ${srcdir}/${basename}-${pkgver/.a}/../build
  make DESTDIR="${pkgdir}" install

# Install icons
  for res in 16 32 48 128; do
    install -D -m644 ${srcdir}/${basename}-${pkgver/.a}/share/icons/${res}x${res}/vlc.png \
        "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
}
