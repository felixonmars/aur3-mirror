# Contributor: Zsolt Udvari <udvzsolt gmail com>
# Based on vlc-mini by anrxc

basename=vlc
pkgname=vlc-light
pkgver=2.0.1
pkgrel=1
pkgdesc="A MPEG, VCD, DVD and DivX player with extra minimal dependencies"
arch=("i686" "x86_64")
url="http://www.videolan.org/vlc/"
license=("GPL2")
depends=("libmad" "ffmpeg" "sysfsutils" "qt"
         "libdvdread" "libxml2" "a52dec" "x264"
         "libmpeg2" "libv4l" "libxcb"
         "libdvdnav")
makedepends=("pkgconfig" "ncurses")
optdepends=("libdvdcss: for decoding encrypted DVDs"
            "ncurses: for ncurses interface support")
provides=("vlc")
conflicts=("vlc")
options=("!libtool")
install="${pkgname}.install"
source=("http://download.videolan.org/pub/videolan/${basename}/${pkgver}/${basename}-${pkgver}.tar.xz")
md5sums=("5ad114755670e4881a2b35354e2f79bc")


build() {
  cd "${srcdir}/${basename}-${pkgver}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  rm -rf ../build
  mkdir "../build"
  cd ../build

  ../${basename}-${pkgver}/configure --prefix=/usr \
              --enable-{alsa,avcodec,avformat,a52,dvdnav,dvdread,fontconfig,freetype,glx} \
              --enable-{libxml2,libmpeg2,loader,mad,ncurses} \
              --enable-{ogg,optimize-memory,optimizations,qt4,sout} \
              --enable-{vlc,vorbis,theora,xvideo,xcb,x264} \
              --disable-{aa,atmo,bluray,bonjour,caca,coverage,cprof} \
              --disable-{dbus,dbus-control,dca,dc1394,debug,decklink,dirac,directfb,directx,dshow,dv,faad,fbosd,flac,fluidsynth,fribidi} \
              --disable-{gme,gnomevfs,gnutls,goom,gprof,growl,httpd,jack,kate} \
              --disable-{libass,libcddb,libgcrypt,libproxy,libtar,linsys,lirc,live555,loader,lua} \
              --disable-{macosx,macosx-audio,mkv,mod,mpc,mtp,nls,notify,opencv,oss} \
              --disable-{png,portaudio,postproc,projectm,pulse,pvr,quicktime,real,realrtsp,rpath,run-as-root} \
              --disable-{schroedinger,screen,sdl,sdl-image,sftp,shine,shout,sid,skins2,smb,speex,sqlite,svg,switcher,swscale} \
              --disable-{taglib,telepathy,telx,tiger,tremor,twolame} \
              --disable-{udev,upnp,update-check,vcd,vcdx,visual,vlm,v4l2} \
              --disable-{waveout,wingdi,wma-fixed,xosd,zvbi}
  CFLAGS="-fPIC"
  make
  make DESTDIR=${pkgdir}/ install

# Install icons
  for res in 16 32 48 128; do
    install -D -m644 ${srcdir}/${basename}-${pkgver}/share/icons/${res}x${res}/vlc.png \
        ${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png
  done
}
