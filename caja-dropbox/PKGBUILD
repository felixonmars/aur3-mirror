# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>
# Contributor: fazibear <fazibear@gmail.com>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>
# Contributor: Gordin <9ordin @t gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=caja-dropbox
pkgver=0.7.1
pkgrel=3
pkgdesc="Dropbox for Linux - Caja extension"
url="http://forums.dropbox.com/topic.php?id=21111"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('custom:CC-BY-ND-3' 'GPL')
depends=('glib2' 'libnotify' 'mate-file-manager' 'polkit' 'procps' 'pygtk' )
makedepends=('mate-common' 'python2-docutils')
optdepends=('dropbox: Dropbox support')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.4/${pkgname}-${pkgver}.tar.xz")
sha1sums=('559e09ecebb8bcd8e059710b171ffecc0ed4c2e3')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i "s/python/python2/" configure.in dropbox.in Makefile.am rst2man.py
    ./autogen.sh \
        --prefix=/usr \
        --disable-static
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    # Remove the cruft
    rm -f "${pkgdir}"/usr/lib/caja/extensions-*/libcaja-dropbox.a
    rm -f "${pkgdir}/usr/bin/dropbox"
    rm -f "${pkgdir}/usr/share/applications/dropbox.desktop"
    rm -rf "${pkgdir}/usr/share/icons/"*
    rm -rf "${pkgdir}/usr/share/man/"*
}
