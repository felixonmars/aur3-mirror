# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=obconf
pkgname=$_pkgname-git
pkgver=20121119
pkgrel=1
pkgdesc="A gtk2 based configuration tool for the Openbox windowmanager"
arch=(i686 x86_64)
license=(GPL)
url=http://openbox.org/wiki/ObConf
depends=(openbox libglade desktop-file-utils)
provides=($_pkgname=2.0.3)
conflicts=($_pkgname)
install=$_pkgname.install
source=(http://icculus.org/openbox/mw/images/8/80/Obconf-72.png rrtheme.patch)
sha256sums=('ed62f78069a2d4b588e4cc387f8c5663c8a1ea1c2c35959ea0b17ef652ebeabe'
    '0d6fe4b051ce501cbbb6d3435a2f11fa385d53650b002b698bc409fed7a350ed')
sha512sums=('6392d0b16cdeaa36f32f3dc0baa897042934a04996494728c1b4d17858b2e6ab413ce0014bbeeaa1c0985ea50baaffa8f2441e42d537087c44e717c65e893566'
    '040558a866e16c29b219c008f8f6184aa47a9ced02830f28ef76bc835ad30d959abef73bf92fcff5cf9b2609d19aa7f0fd2cb772f8622cb4b102ea8cded62111')

_gitroot=git://git.openbox.org/dana/$_pkgname
_gitname=obconf

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    patch -Np1 -i ../rrtheme.patch

    ./bootstrap
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    sed -i 's:=openbox:=obconf:' $_pkgname.desktop
    make DESTDIR="$pkgdir" install
    install -Dm644 ../Obconf-72.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png
}
