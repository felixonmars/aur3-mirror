# Maintainer: Giuseppe Borzi <gborzi@ieee.org>

pkgname=midori-passwordmanager
_pkgname=midori
pkgver=0.3
_pkgver=0.4.7
pkgrel=8
pkgdesc="A password manager for Midori"
arch=('i686' 'x86_64')
url="http://www.twotoasts.de/index.php?/pages/midori_summary.html"
license=('GPL2')
depends=('midori' 'gnome-keyring' 'libgnome-keyring')
makedepends=('intltool' 'vala')
optdepends=('seahorse: to edit/delete passwords')
source=(http://archive.xfce.org/src/apps/$_pkgname/${_pkgver%.*}/$_pkgname-$_pkgver.tar.bz2 passwordmanager.c passwordmanager.js wscript.diff README lpins.c)
md5sums=('06db7b88a41e9b2265728960d5e98f35'
         '520d2a0929cb4153149915ebaa4fb46a'
         'f9a91b8d929d4cb6407a769603fca619'
         '432c250c1dd157d4c5f723fca8557883'
         'defadec00ebd02506fb1106f7fe5f2ae'
         'd11b1f035e4a044e2727ef226db9482b')

build() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"

  patch -Np1 -i "${srcdir}/wscript.diff"
  # python2 
  export PYTHON=/usr/bin/python2
  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" wscript waf
  cp "$srcdir/passwordmanager.c" extensions

  [ -z $MAKEFLAGS ] && JOBS=1 || JOBS=${MAKEFLAGS/-j/}
  ./configure --prefix=/usr --jobs=$JOBS \
	--enable-addons \
	--disable-hildon \
	--enable-unique \
	--enable-nls 
  make
  cc `pkg-config --cflags --libs gnome-keyring-1` "$srcdir/lpins.c" -o lpins
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  install -Dm644 _build/default/extensions/libpasswordmanager.so \
     "${pkgdir}/usr/lib/midori/libpasswordmanager.so"
  install -Dm644 "${srcdir}/passwordmanager.js" \
     "${pkgdir}/usr/share/midori/res/passwordmanager.js"
  install -Dm755 lpins "${pkgdir}/usr/bin/lpins"
  install -Dm644 "${srcdir}/README" "${pkgdir}/usr/share/doc/$pkgname/README"
}
