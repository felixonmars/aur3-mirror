# Maintainer: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad-bzr
pkgver=4190
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('mesa' 'shared-mime-info' 'wxgtk')
makedepends=('cmake' 'boost' 'bzr' 'bzrtools')
optdepends=('kicad-doc-bzr' 'kicad-library-bzr')
conflicts=('kicad')
provides=('kicad')
install=kicad.install
source=()
md5sums=()
_bzrtrunk=lp:kicad
_bzrmod=kicad

build() {
  cd ${srcdir}
  msg "Connecting to BZR server..." 
  if [ ! -d ${srcdir}/${_bzrmod} ] ; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi
  msg "BZR checkout done or server timeout"

  cd ${srcdir}/${_bzrmod}
  # build and install kicad
  mkdir -p build/Release && cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/${_bzrmod}/build/Release

  make DESTDIR=${pkgdir} install
}
