# Contributor: Antonio Rojas

pkgname=kmenuedit
pkgver=5.0.2
pkgrel=1
pkgdesc='KDE menu editor'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kmenuedit'
license=('LGPL')
depends=('khotkeys')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kdebase-workspace')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('ddfb950816e3e51f2358a32a36986b6c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLOCALE_INSTALL_DIR=share/locale/kf5 \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
