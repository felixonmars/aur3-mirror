# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=stirfs
pkgver=1.3
pkgrel=1
pkgdesc='Secure, transparent and irresistible filesystem'
url='http://www.normalesup.org/~bisson/src/'
license=('ISC')
arch=('i686' 'x86_64')
depends=('fuse' 'openssl')
source=("${url}/${pkgname}-${pkgver}.c")
sha1sums=('7b7e96896ccd12653c5e76bab21411f4191e4657')

build() {
	cd "${srcdir}"
	cc -lfuse -lcrypto \
		${CFLAGS} ${LDFLAGS} \
		-o ${pkgname} ${pkgname}-${pkgver}.c
}

package() {
	cd "${srcdir}"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	head -n 22 "${pkgname}-${pkgver}.c" > "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
