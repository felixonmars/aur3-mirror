# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>

pkgname=gitlab
pkgver=6.8.1
pkgrel=2
pkgdesc="Project management and code hosting application"
arch=('any')
url="http://gitlab.org/gitlab-ce"
license=('MIT')
depends=('ruby>=1.9.3' 'git>=1.7.10' 'ruby-bundler' 'gitlab-shell>=1.9.3' 'openssh' 'redis>=2.0' 'libxslt' 'icu')
optdepends=(
	'mariadb: database backend'
	'postgresql: database backend'
	'python2-docutils: reStructuredText markup language support'
	'postfix: mail server in order to receive mail notifications'
	)
backup=(
	etc/webapps/gitlab/gitlab.yml
	etc/webapps/gitlab/database.yml
	etc/webapps/gitlab/unicorn.rb
	etc/logrotate.d/gitlab)
_server=()
if [[ -n $(which httpd 2> /dev/null) ]]; then
	backup+=('etc/httpd/conf/extra/gitlab.conf'
		'etc/httpd/conf/extra/gitlab-ssl.conf')
	_server+=('apache')
fi
if [[ -n $(which nginx 2> /dev/null) ]]; then
	backup+=('etc/nginx/sites-available/gitlab-ssl')
	_server+=('nginx')
fi
if [[ -n $(which lighttpd 2> /dev/null) ]]; then
	backup+=('etc/lighttpd/conf.d/10-gitlab.conf')
	_server+=('lighttpd')
fi
#source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/gitlab-org/gitlab-ce/repository/archive.tar.gz?ref=v${pkgver}"
source=("$pkgname-$pkgver.tar.gz::https://github.com/gitlabhq/gitlabhq/archive/v${pkgver}.tar.gz"
	gitlab.target
	gitlab-unicorn.service
	gitlab-sidekiq.service
	gitlab.conf
	gitlab-ssl.conf
	gitlab-ssl
	10-gitlab.conf
	gitlab.tmpfiles.d
	gitlab.logrotate)
install='gitlab.install'
sha512sums=('c0d5c8656f39b7f95bf3bb8b302c2ef886841f8320fdb61ba7862f7b76447a3dee59e0d6346210a1504306a8cb2a7f31acc5693e61c5d9622bd943c6ddedf5f9'
	'c88b7b500225c9a2bfaf10912fc09e5a3beb7c28890dcc1d49d55dfe92df02cef3f1865a138e7e2802b4e795c9a45cb130819be0a1c115eca9b566b8b9fb4395'
	'b6724b4171a1a62dee10bea6c9e1fd20eb6788ba61622c58ced80bef9cf7401b7c6a17a5a5a38ad677a0b224b09ff2b0be3737d89e5718b4f1391a57d91148e9'
	'df653dbe2f47bf8f4adaaa274574cc94c1e66055415b58ba31e753e23805a9e3e4fce984076c76c9ccf86887c77a3af80a8a006744b2409dda31f3671f7e39d2'
	'f021ddcaaf8f4e92db7684f3a7eb34d4d00911efe5a3d57bc78ebb0b9636b2639b1990047c8da3f6d7339ad7a89574cfbfeb59a79c30b099e6f44ecfbf3166f7'
	'12b1775f9457d62657e388125b6f6a24a87577632696d3f7b8d5765deb57c2feba13fe8037e93fc362f6d328f3cf00183ff563631ef6b4b8badf104af126854d'
	'4a27e26f309063b615045f4ae0a789d4221389539d69aedafde81a7cde0e29f56aeaff2b17d2da51c6d615a9f6c2c087971d3565b3687c1e8023b498c2ec1202'
	'c78b6f46abcf603d8db6e38cf50868e14145928422ddfe17c88e2f006b5b910dddf456ec5d6d724b250994530643963809688a98f7e12ebd5b5dabf7f96f0e06'
	'b98ba6cbd8745a649046386f4aad6ee3d869b8e14820560e25b3207683dcd0841e1af8afb5537e6e06bf19d9436489420db59348c57f4c096c69b0ea4f2e7978'
	'8b0094a4f7bffb909a0ded0d86fbe93c96dd684902354e191844361e36a61109c4696b2003734d57d2d4fe65ba4ebafb87d211c1a9ee532cdc87a1a8abeec0f6')

options=('!strip')
_homedir='/var/lib/gitlab'
_datadir='/usr/share/webapps/gitlab'
_wo=()
_srcdir="gitlabhq-${pkgver}"

prepare() {
	if [[ `pacman -T libmariadbclient` != '' ]]; then
		_wo+=('mysql')
	fi
	if [[ `pacman -T postgresql-libs` != '' ]]; then
		_wo+=('postgres')
	fi
	if [[ ${#_wo[@]} == 2 ]]; then
		error "Usable DB libs not found"
		msg2 "Install at least libmariadbclient or postgresql-libs"
		return 1
	fi

	cd "${srcdir}/${_srcdir}"

	# Patching config files:
	sed -e "s|# user: git|user: gitlab|" \
		-e "s|/home/git/repositories|${_homedir}/repositories|" \
		-e "s|/home/git/gitlab-satellites|${_homedir}/satellites|" \
		-e "s|/home/git/gitlab-shell|/usr/share/gitlab-shell|" \
		config/gitlab.yml.example > config/gitlab.yml
	sed -e "s|/home/git/gitlab/tmp/.*/|/var/run/gitlab/|g" \
		-e "s|/home/git/gitlab|${_datadir}|g" \
		-e "s|timeout 30|timeout 300|" \
		config/unicorn.rb.example > config/unicorn.rb
	sed -e "s|username: git|username: gitlab|" \
		config/database.yml.mysql > config/database.yml
}

build() {
	cd "${srcdir}/${_srcdir}"
	msg "Fetching bundled gems..."
	# Gems will be installed into vendor/bundle
	bundle install --no-cache --no-prune --deployment --without development test aws ${_wo[@]}
}

package() {
	cd "${srcdir}/${_srcdir}"
	install -d "${pkgdir}/usr/share/webapps"
	cp -r "${srcdir}/${_srcdir}" "${pkgdir}${_datadir}"

	# Creating directories
	install -d \
		"${pkgdir}/etc/webapps/gitlab" \
		"${pkgdir}/usr/share/webapps" \
		"${pkgdir}/usr/share/doc/${pkgname}" \
		"${pkgdir}${_homedir}/www" \
		"${pkgdir}${_datadir}/www" \
		"${pkgdir}${_datadir}/public/uploads"
	ln -fs /run/gitlab "${pkgdir}${_homedir}/pids"
	ln -fs /run/gitlab "${pkgdir}${_homedir}/sockets"
	ln -fs /usr/share/webapps/gitlab/log "${pkgdir}${_homedir}/log"

	# Install config files
	for _file in gitlab.yml unicorn.rb database.yml; do
		mv "config/${_file}" "${pkgdir}/etc/webapps/gitlab/"
		[[ -f "${pkgdir}${_datadir}/config/${_file}" ]] && rm "${pkgdir}${_datadir}/config/${_file}"
		ln -fs "/etc/webapps/gitlab/${_file}" "${pkgdir}${_datadir}/config/"
	done

	# Install license and help files
	mv README.md MAINTENANCE.md CONTRIBUTING.md CHANGELOG config/*.{example,mysql,postgresql} "${pkgdir}/usr/share/doc/${pkgname}"
	install -D "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	rm "${pkgdir}/usr/share/webapps/gitlab/LICENSE"

	# Install systemd service files
	for _file in gitlab.target gitlab-unicorn.service gitlab-sidekiq.service; do
		install -Dm0755 "${srcdir}/${_file}" "${pkgdir}/usr/lib/systemd/system/${_file}"
	done

	install -Dm644 "${srcdir}/gitlab.tmpfiles.d" "${pkgdir}/usr/lib/tmpfiles.d/gitlab.conf"
	install -Dm644 "${srcdir}/gitlab.logrotate" "${pkgdir}/etc/logrotate.d/gitlab"

	# Install apache, nginx and lighttpd template files (if they are installed)
	for __server in ${_server[@]}; do
		case ${__server} in
			apache)
				install -d "${pkgdir}/etc/httpd/conf/extra"
				install -m 644 "${srcdir}/gitlab.conf" "${pkgdir}/etc/httpd/conf/extra/"
				install -m 644 "${srcdir}/gitlab-ssl.conf" "${pkgdir}/etc/httpd/conf/extra/"
				;;
			nginx)
				install -d "${pkgdir}/etc/nginx/sites-enabled"
				install -m 644 "${srcdir}/gitlab-ssl" "${pkgdir}/etc/nginx/sites-enabled/"
				;;
			lighttpd)
				install -d "${pkgdir}/etc/lighttpd/conf.d"
				install -m 644 "${srcdir}/10-gitlab.conf" "${pkgdir}/etc/lighttpd/conf.d/"
				;;
		esac
	done
}

# vim:set ts=4 sw=4 et:
