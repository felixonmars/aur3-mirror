# Maintainer: Scott Lawrence <bytbox@gmail.com>

# Forked from pommed. Previous header:
# Maintainer: Corrado Primier <ilbardo gmail com>
# Contributor: Other contributors existed but lost information
# Contributor: dpevp <daniel.plaza.espi@gmail.com>
# Contributor: jordi Cerdan (jcerdan) <jcerdan@tecob.com>

pkgname=pommed-light
pkgver=1.43lw
pkgrel=1
pkgdesc="Handles the hotkeys of Apple MacBook (Pro) laptops - lightweight edition"
arch=('i686' 'x86_64')
url="https://github.com/bytbox/pommed-light"
license=('GPL2')
install=$pkgname.install
makedepends=('pkgconfig')
depends=('alsa-lib' 'audiofile' 'confuse' 'pciutils')
conflicts=('pommed')
optdepends=('eject: disc ejection support')
#backup=('etc/pommed.conf')
source=(https://github.com/bytbox/${pkgname}/tarball/v${pkgver} pommed.rc)
dfname="bytbox-${pkgname}-29c3ba4"
md5sums=('6acbe806a44c7974af324d6078849e6a'
         'c024728673d20aef3a9d8dff3fc5b388')

build() {
  cd ${srcdir}/${dfname}
  make pommed
}

package() {
  install -Dm755 ${srcdir}/${dfname}/pommed/pommed ${pkgdir}/usr/sbin/pommed
  install -Dm644 ${srcdir}/${dfname}/pommed.conf.mactel ${pkgdir}/etc/pommed.conf.mactel
  install -Dm644 ${srcdir}/${dfname}/pommed.conf.pmac ${pkgdir}/etc/pommed.conf.pmac
  install -Dm755 ${srcdir}/pommed.rc ${pkgdir}/etc/rc.d/pommed

  # Man page
  install -Dm644 ${srcdir}/${dfname}/pommed.1 ${pkgdir}/usr/share/man/man1/pommed.1

  # Sounds
  install -Dm644 ${srcdir}/${dfname}/pommed/data/goutte.wav ${pkgdir}/usr/share/pommed/goutte.wav
  install -Dm644 ${srcdir}/${dfname}/pommed/data/click.wav ${pkgdir}/usr/share/pommed/click.wav

  # Systemd
  install -Dm644 ${srcdir}/${dfname}/pommed.service ${pkgdir}/usr/lib/systemd/system/pommed.service
}

# vim:set ts=2 sw=2 et:
