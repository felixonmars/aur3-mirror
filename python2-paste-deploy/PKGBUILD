# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: David Campbell <davekong@archlinux.us>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

_pkgname=PasteDeploy
pkgname=python2-paste-deploy
pkgver=1.5.0
pkgrel=1
pkgdesc="Load, configure, and compose WSGI applications and servers"
arch=(any)
url=http://pythonpaste.org/deploy/
license=(custom)
depends=(python2)
makedepends=(python2-distribute)
source=(http://pypi.python.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('61c205633adae996cd0e306451c8a28deca0499524e2a38c65ff1570f35a8a53')
sha512sums=('55e54916c720ffa828c573f4c692bd68fcccee42006d70d8a23d8ac4ddbf89c7f46e337885e81a369622b4724b5e07dc30bdcccd9fb9cb4a756f857b91c60f77')

prepare() {
    find $_pkgname-$pkgver -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $_pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $_pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1
    install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
