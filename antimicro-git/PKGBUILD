# Maintainer: Earnestly <zibeon@gmail.com>

pkgname=antimicro-git
pkgver=2.0.1.r8.g8efd865
pkgrel=1

pkgdesc="Map keyboard and mouse actions to gamepad buttons, inspired by qjoypad."
arch=('i686' 'x86_64')
url="https://github.com/Ryochan7/antimicro"
license=('GPL3')

depends=('glibc' 'libxtst' 'qt4' 'sdl')
makedepends=('git')

provides=('antimicro')
conflicts=('antimicro')

install='antimicro-git.install'
source=('git+https://github.com/Ryochan7/antimicro')
md5sums=('SKIP')

pkgver() {
    cd antimicro
    git describe | sed 's/-/.r/; s/-/./'
}

build() {
    cd antimicro/src
    qmake-qt4 INSTALL_PREFIX=/usr
    make
}

package() {
    cd antimicro/src
    make INSTALL_ROOT="$pkgdir" install
}
