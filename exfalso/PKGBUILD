# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=exfalso
pkgver=2.3.2
pkgrel=1
pkgdesc="GTK+ audio tag editor"
arch=('any')
url="http://code.google.com/p/quodlibet/"
license=('GPL2')
depends=('pygtk' 'mutagen')
makedepends=('intltool')
conflicts=('quodlibet')
source=(http://quodlibet.googlecode.com/files/quodlibet-$pkgver.tar.gz)
md5sums=('bf93b2945589d9b53cfb9fa854e56e97')

build () {
  cd "$srcdir"/quodlibet-$pkgver

  # Use Python 2
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' exfalso.py quodlibet/util/fmps.py

  python2 setup.py build
}

package() {
  cd "$srcdir"/quodlibet-$pkgver

  python2 setup.py install --root="$pkgdir"

  install -D -m644 quodlibet/images/exfalso.png "$pkgdir"/usr/share/pixmaps/exfalso.png

  # Remove Quod Libet
  rm -f "$pkgdir"/usr/bin/quodlibet
  rm -f "$pkgdir"/usr/lib/python2.*/site-packages/quodlibet/_mmkeys.so
  rm -f "$pkgdir"/usr/lib/python2.*/site-packages/quodlibet/_trayicon.so
  rm -f "$pkgdir"/usr/share/applications/quodlibet.desktop
  rm -f "$pkgdir"/usr/share/man/man1/quodlibet.1
}
