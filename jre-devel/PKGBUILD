# Maintainer: Det
# Based on jre: https://aur.archlinux.org/packages.php?ID=51908

pkgname=jre-devel
_major=8
_build=b62
_date=25_oct_2012
pkgver=$_major$_build
pkgrel=1
pkgdesc="Java $_major Runtime Environment Snapshot"
arch=('i686' 'x86_64')
url=http://jdk$_major.java.net/
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound support'
            'ttf-dejavu: fonts')
provides=("java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major")
conflicts=("${provides[@]}" "${provides[@]/$_major/7}")
backup=('etc/profile.d/jre.sh'
        'etc/profile.d/jre.csh')
options=('!emptydirs')
install=$pkgname.install
_arch=i586  # Workaround for the AUR Web interface Source parser
_arch2=i386; [ "$CARCH" = 'x86_64' ] && _arch=x64 && _arch2=amd64
source=("http://download.java.net/jdk$_major/archive/$_build/binaries/jre-$_major-ea-bin-$_build-linux-$_arch-$_date.tar.gz"
        'java-policy-settings.desktop'
        'javaws-launcher'
        'jre.csh'
        'jre.sh')
md5sums=(`curl -Ls ${source/.t*}.md5 | cut -d " " -f4` # jre-$_major-ea-bin-$_build-linux-$_arch-$_date.tar.gz
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         '92dec202858f2bf7729c6805b1bd3ae4'  # jre.csh
         '85ba1d2b39d5e6efad89ef98d0f19909') # jre.sh

PKGEXT=".pkg.tar"

package() {
  msg2 "Creating required dirs"
  cd jre1.$_major.0
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{lib/mozilla/plugins,share/licenses/jre},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Removing the redundancies"
  rm -r plugin/ man/ja

  msg2 "Moving stuff in place"
  mv lib/desktop/{applications,icons,mime} man "$pkgdir"/usr/share/
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/jre/
  mv * "$pkgdir"/opt/java/jre/

  msg2 "Symlinking the plugin"
  ln -s /opt/java/jre/lib/$_arch2/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing the scripts, confs and .desktops of our own"
  cd "$srcdir"
  install -m755 jre.{,c}sh "$pkgdir"/etc/profile.d/
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -m644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/

  msg2 "Tweaking the javaws .desktop file"
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}