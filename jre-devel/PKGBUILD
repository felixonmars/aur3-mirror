# Maintainer: Det
# Based on jre: https://aur.archlinux.org/packages.php?ID=51908

pkgname=jre-devel
_major=8
_build=b58
_date=27_sep_2012
pkgver=$_major$_build
pkgrel=1
pkgdesc="Java $_major Runtime Environment Snapshot"
url=http://jdk$_major.java.net/
arch=('i686' 'x86_64')
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound support'
            'ttf-dejavu: fonts')
provides=("java-runtime=$_major" "java-runtime-headless=$_major")
conflicts=("${provides[@]}" "java-runtime=7" "java-runtime-headless=7")
backup=('etc/profile.d/jre.sh'
        'etc/profile.d/jre.csh')
install=$pkgname.install
_arch=i386; [ "$CARCH" = 'x86_64' ] && _arch=amd64
source=("http://download.java.net/jdk$_major/archive/$_build/binaries/jre-$_major-ea-bin-$_build-linux-i586-$_date.tar.gz"
        'java-policy-settings.desktop'
        'javaws-launcher'
        'jre.csh'
        'jre.sh')
[ "$CARCH" = 'x86_64' ] && source[0]="http://download.java.net/jdk$_major/archive/$_build/binaries/jre-$_major-ea-bin-$_build-linux-x64-$_date.tar.gz"
md5sums=(`curl -Ls ${source/.t*}.md5 | cut -d " " -f4` # jre-$_major-ea-bin-$_build-linux-i586/x64-$_date.tar.gz
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         '92dec202858f2bf7729c6805b1bd3ae4'  # jre.csh
         '85ba1d2b39d5e6efad89ef98d0f19909') # jre.sh

package() {
  msg2 "Creating required dirs"
  cd jre1.$_major.0
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{share/{,licenses/jre},lib/mozilla/plugins},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Re-ordering folders a bit"
  mv lib/desktop/{applications,icons,mime} "$pkgdir"/usr/share/

  msg2 "Removing empty and redundant dirs"
  rm -r lib/{applet,desktop}/ man/ja plugin/

  msg2 "Moving stuff in place"
  mv COPYRIGHT LICENSE THIRDPARTYLICENSEREADME.txt "$pkgdir"/usr/share/licenses/jre/
  mv man "$pkgdir"/usr/share/
  mv * "$pkgdir"/opt/java/jre/

  msg2 "Symlinking the plugin"
  cd "$srcdir"
  ln -s /opt/java/jre/lib/$_arch/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing scripts and the .desktop file"
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -Dm644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/
  install -m755 jre.{c,}sh "$pkgdir"/etc/profile.d/

  msg2 "Tweaking the javaws .desktop file"
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}