# Maintainer: Philip A Reimer <antreimer@gmail.com>
pkgname=mingw-w64-libmariadbclient
pkgver=5.5.33.a
_pkgver=${pkgver/.a/a}
pkgrel=2
pkgdesc="MariaDB client libraries (mingw-w64)"
arch=('any')
url="http:/mariadb.org"
license=('GPL')
makedepends=('mingw-w64-gcc' 'wine')
depends=('mingw-w64-crt')
options=('!strip' '!buildflags' 'staticlibs')
source=("http://ftp.osuosl.org/pub/mariadb/mariadb-${_pkgver}/win32-packages/mariadb-${_pkgver}-win32.zip"
        "http://ftp.osuosl.org/pub/mariadb/mariadb-${_pkgver}/winx64-packages/mariadb-${_pkgver}-winx64.zip"
        "http://downloads.sourceforge.net/sourceforge/mingw/mingw-utils-0.4-1-mingw32-src.tar.lzma")
md5sums=('7eb1f673dae6489be98334f4b29bee56'
         'ac97429e9288419c1b357c5c84e95fc2'
         '4f9d358f50ad66159271c431c56bb210')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS

  ln -s mariadb-${_pkgver}-win32 i686-w64-mingw32
  ln -s mariadb-${_pkgver}-winx64 x86_64-w64-mingw32

  unset WINEARCH
  export WINEPREFIX="${srcdir}/wineprefix"

  for _arch in ${_architectures}; do
    cd "${srcdir}/mingw-utils-0.4/reimp"
    ${_arch}-gcc -g -I . reimp.c util.c ar.c -o reimp.exe
    cd "${srcdir}/${_arch}/lib"
    wine "${srcdir}/mingw-utils-0.4/reimp/reimp.exe" -d libmysql.lib
    ${_arch}-dlltool -k --input-def libmysql.def --dllname libmysql.dll --output-lib libmysql.dll.a
  done
}

package() {
  for _arch in ${_architectures} ; do
    cd "${srcdir}/${_arch}"
    mkdir -p "${pkgdir}/usr/${_arch}/"{bin,include,lib}
    cp -R include "${pkgdir}/usr/${_arch}"
    install -Dm644 lib/libmysql.dll "${pkgdir}/usr/${_arch}/bin/libmysql.dll"
    install -Dm644 lib/libmysql.dll.a "${pkgdir}/usr/${_arch}/lib/libmysql.dll.a"
    ${_arch}-ranlib "${pkgdir}/usr/${_arch}/lib/libmysql.dll.a"
    # calling strip on libmysql.dll does not work
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/lib/"*.dll.a
  done
}
