# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=hawaii-swordfish-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Hawaii: File manager"
arch=('i686' 'x86_64')
url="http://www.maui-project.org"
license=('GPL')
depends=('hawaii-vibe-git')
makedepends=('git' 'cmake')

_gitroot="git://github.com/hawaii-desktop/swordfish.git"
_gitname=swordfish
_gitbranch=gio
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${srcdir}/${_gitname}

	export PATH=/opt/hawaiide/bin:$PATH
	export PKG_CONFIG_PATH=/opt/hawaiide/lib/pkgconfig

	mkdir build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
		-DCMAKE_INSTALL_PLUGINSDIR=/usr/lib/hawaii/plugins \
		-DCMAKE_INSTALL_QMLDIR=/usr/lib/hawaii/qml \
		-DCMAKE_BUILD_TYPE=Release ..
	make
}

package() {
	cd ${srcdir}/${_gitname}/build
	make DESTDIR="${pkgdir}" install

	# Remove D-Bus service which conflicts with nautilus
	rm -f $pkgdir/usr/share/dbus-1/services/org.freedesktop.FileManager1.service
}
