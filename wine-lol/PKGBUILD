# Maintainer: arakmar <yoann.laissus@gmail.com"
# Based on the wine package in the community repository and wine-git of AUR

pkgname=wine-lol
pkgver=20120617
pkgrel=1

source=(0001-ntdll-Improve-performace-of-heap-allocation-v2.patch 0002-imm32-Avoid-deadlock-when-destroying-thread-data.patch libgcrypt_thread_safe.patch)
md5sums=('8bf50a144ec9bf54b10c6ef5d79936b2'
         'b74f20e5c1f7bec942d0a2607883bf16'
         '54dd6f086426d8845ed53c3af75433df')

_pkgbasever=${pkgver/rc/-rc}

_gitroot="git://source.winehq.org/git/wine.git"
_gitname="wine"

pkgdesc="A compatibility layer for running Windows programs. Latest GIT version with patches for running League of Legends."
url="http://www.winehq.com"
arch=(i686 x86_64)
license=(LGPL)
install='wine-lol.install'

depends=(
  fontconfig      lib32-fontconfig
  mesa            lib32-mesa
  libxcursor      lib32-libxcursor
  libxrandr       lib32-libxrandr
  libxdamage      lib32-libxdamage
  libxi           lib32-libxi
  gettext         lib32-gettext
  desktop-file-utils
)

makedepends=(autoconf ncurses bison perl fontforge flex prelink
  'gcc>=4.5.0-2'  'gcc-multilib>=4.5.0-2'
  giflib          lib32-giflib
  libpng          lib32-libpng
  gnutls          lib32-gnutls
  libxinerama     lib32-libxinerama
  libxcomposite   lib32-libxcomposite
  libxmu          lib32-libxmu
  libxxf86vm      lib32-libxxf86vm
  libxml2         lib32-libxml2
  libldap         lib32-libldap
  lcms            lib32-lcms
  mpg123          lib32-mpg123
  openal          lib32-openal
  v4l-utils       lib32-v4l-utils
  alsa-lib        lib32-alsa-lib
  oss
  samba
  git
)
  
optdepends=(
  giflib          lib32-giflib
  libpng          lib32-libpng
  libldap         lib32-libldap
  gnutls          lib32-gnutls
  lcms            lib32-lcms
  libxml2         lib32-libxml2
  mpg123          lib32-mpg123
  openal          lib32-openal
  v4l-utils       lib32-v4l-utils
  libpulse        lib32-libpulse
  alsa-plugins    lib32-alsa-plugins
  alsa-lib        lib32-alsa-lib
  oss             cups
  samba
)

if [[ $CARCH == i686 ]]; then
  # Strip lib32 etc. on i686
  depends=(${depends[@]/*32-*/})
  makedepends=(${makedepends[@]/*32-*/})
  makedepends=(${makedepends[@]/*-multilib*/})
  optdepends=(${optdepends[@]/*32-*/})
  provides=('wine=_pkgbasever' )
  conflicts=('wine')
else
  provides=('bin32-wine=_pkgbasever' 'wine-wow64=_pkgbasever' 'wine=_pkgbasever' )
  conflicts=('bin32-wine' 'wine-wow64' 'wine')
fi

build() {
  cd "$srcdir"
  
  # Get rid of old build dirs
  rm -rf $pkgname-{32,64}-build
  mkdir "$pkgname-32-build"

  # These additional CFLAGS solve FS#27662
  export CFLAGS="${CFLAGS/-D_FORTIFY_SOURCE=2/} -D_FORTIFY_SOURCE=0"
  export CXXFLAGS="${CFLAGS/-D_FORTIFY_SOURCE=2/} -D_FORTIFY_SOURCE=0"

  msg "Connecting to GIT server...."

  if [ -d "${srcdir}/${pkgname}" ] ; then
    cd "${srcdir}/$pkgname"
    # Remove previously applied patches
    git reset --hard HEAD
    git pull $_gitroot master
    msg "Local repository updated."
  else
    git clone $_gitroot $pkgname
  fi
  
    
  msg2 "Patching..."
  (
    cd "${srcdir}/${pkgname}"
    
    patch -Np1 < ../../0001-ntdll-Improve-performace-of-heap-allocation-v2.patch
    patch -Np1 < ../../0002-imm32-Avoid-deadlock-when-destroying-thread-data.patch
    patch -Np1 < ../../libgcrypt_thread_safe.patch
  )

  cd "$srcdir"

  if [[ $CARCH == x86_64 ]]; then

    msg2 "Building Wine-64..."

    mkdir $pkgname-64-build
    cd "$srcdir/$pkgname-64-build"
    ../$pkgname/configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --libdir=/usr/lib \
      --with-x \
      --enable-win64

    make -j`grep processor /proc/cpuinfo | wc -l`

    _wine32opts=(
      --libdir=/usr/lib32
      --with-wine64="$srcdir/$pkgname-64-build"
    )

    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  msg2 "Building Wine-32..."
  cd "$srcdir/$pkgname-32-build"
  ../$pkgname/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-x \
    "${_wine32opts[@]}"

  # These additional CFLAGS solve FS#27560
  make CFLAGS+="-mstackrealign" CXXFLAGS+="-mstackrealign" -j`grep processor /proc/cpuinfo | wc -l`
}

package() {
  msg2 "Packaging Wine-32..."
  cd "$srcdir/$pkgname-32-build"

  if [[ $CARCH == i686 ]]; then
    make prefix="$pkgdir/usr" install
  else
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib32" \
      dlldir="$pkgdir/usr/lib32/wine" install

    msg2 "Packaging Wine-64..."
    cd "$srcdir/$pkgname-64-build"
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib" \
      dlldir="$pkgdir/usr/lib/wine" install
  fi
}
