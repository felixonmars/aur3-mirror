# Contributor: bubla <matej.tyc@gmail.com>
pkgname=mingw32-libtiff
pkgver=4.0.2
pkgrel=1
pkgdesc="Library for manipulation of TIFF images (mingw32)"
arch=(any)
url="http://www.remotesensing.org/libtiff/"
license=("custom")
depends=(mingw32-runtime "mingw32-libjpeg>=7" mingw32-zlib)
makedepends=(mingw32-gcc)
options=(!libtool !buildflags !strip)
optdepends=("mingw32-xz: LZMA2 support")
source=("ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz")
md5sums=('04a08fa1e07e696e820a0c3f32465a13')

build() {
  cd "${srcdir}/tiff-${pkgver}"

  ./configure \
  --prefix=/usr/i486-mingw32 \
  --host=i486-mingw32
  make
}

package() {
  cd "${srcdir}/tiff-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 COPYRIGHT "${pkgdir}/usr/i486-mingw32/share/licenses/${pkgname}/LICENSE"
  
  i486-mingw32-strip "$pkgdir/usr/i486-mingw32/bin/"*.exe
  i486-mingw32-strip -x -g "$pkgdir/usr/i486-mingw32/bin/"*.dll
  i486-mingw32-strip -g "$pkgdir/usr/i486-mingw32/lib/"*.a
  rm -rf "$pkgdir/usr/i486-mingw32/share"
}
