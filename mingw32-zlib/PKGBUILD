# Maintainer: Mikael Eriksson <mikael_eriksson@miffe.org>

pkgname=mingw32-zlib
pkgver=1.2.7
pkgrel=1
pkgdesc="A compression/decompression Library (mingw32)"
arch=('any')
license=('custom:zlib')
depends=(mingw32-runtime)
makedepends=(mingw32-gcc)
url="http://www.zlib.net/"
source=("http://zlib.net/zlib-${pkgver}.tar.gz")
provides=('mingw32-zlib-static')
conflicts=('mingw32-zlib-static')
options=(!strip)
md5sums=('60df6a37c56e7c1366cca812414f7b85')

build() {
  cd "$srcdir/zlib-$pkgver"

  unset LDFLAGS

  sed -ie "s,dllwrap,i486-mingw32-dllwrap," win32/Makefile.gcc

  ./configure --prefix=/usr/i486-mingw32 -shared -static
  make -f win32/Makefile.gcc \
    CC=i486-mingw32-gcc \
    AR=i486-mingw32-ar \
    RC=i486-mingw32-windres \
    STRIP=i486-mingw32-strip \
    IMPLIB=libz.dll.a \
    CFLAGS="-DNO_FSEEKO -Wall -O3" \
    || return 1

  install -d "$pkgdir/usr/i486-mingw32/"{bin,include,lib}
  install -m644 -t "$pkgdir/usr/i486-mingw32/include" zlib.h zconf.h
  install -m644 -t "$pkgdir/usr/i486-mingw32/lib" libz.a libz.dll.a
  install -m644 -t "$pkgdir/usr/i486-mingw32/bin" zlib1.dll

  install -d "$pkgdir/usr/i486-mingw32/lib/pkgconfig"

  sed "s,@prefix@,/usr/i486-mingw32,;s,@exec_prefix@,\${prefix},;s,@libdir@,\${exec_prefix}/lib,;s,@sharedlibdir@,\${libdir},;s,@includedir@,\${prefix}/include,;s,@VERSION@,$pkgver," <zlib.pc.in >"$pkgdir/usr/i486-mingw32/lib/pkgconfig/zlib.pc"
}

# vim:set ts=2 sw=2 et:
