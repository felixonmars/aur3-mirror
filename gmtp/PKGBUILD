# Maintainer: yugrotavele <yugrotavele at archlinux dot us>

pkgname=gmtp
pkgver=1.3.2
pkgrel=1
pkgdesc="A simple MP3 player client for MTP based devices"
arch=('i686' 'x86_64')
url="http://gmtp.sourceforge.net/"
license=('BSD')
depends=('dconf' 'flac' 'gtk3' 'libmtp' 'libid3tag' 'libusb' 'libvorbis')
install=$pkgname.install
source=($pkgname-$pkgver-i386.tar.gz::http://sourceforge.net/projects/gmtp/files/gMTP-$pkgver/$pkgname-$pkgver-i386.tar.gz/download)
md5sums=('8070922a189f0f3aed1ed7d158da2529')

build() {
  cd "$srcdir/gMTP"
  sed -i 's:PREFIX ?= /usr/local:PREFIX ?= /usr:g' Makefile
  make gtk3  
}

package() {
  cd "$srcdir/gMTP"
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  make DESTDIR="$pkgdir" install-gtk3
  make DESTDIR="$pkgdir" register-gsettings-schemas
  rm "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"
}


