# Maintainer : Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Gen2ly <toddrpartridge@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

_pkgname="parted"
pkgname="${_pkgname}-git"

pkgver=20111104
pkgrel=1
pkgdesc="A program for creating, destroying, resizing, checking and copying partitions"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/parted/parted.html"
makedepends=('git' 'rsync' 'gperf' 'bc')
depends=('device-mapper' 'util-linux' 'gettext' 'e2fsprogs' 'ncurses' 'readline')
optdepends=('reiserfsprogs: for reiserfs partitions')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
options=(!libtool)
source=()
md5sums=()

_gitroot="git://git.debian.org/git/parted/parted.git"
_gitname="${_pkgname}"
_gitbranch="master"

_update_git() {
	
	cd "${srcdir}/"
	
	msg "Connecting to GIT server...."
	
	if [ -d "${srcdir}/${_gitname}/" ]
	then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout "${_gitbranch}"
		git merge "remotes/origin/${_gitbranch}"
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
		cd "${srcdir}/${_gitname}/"
		git checkout "${_gitbranch}"
		msg "GIT checkout done or server timeout"
	fi
	
	echo
	
}

build() {
	
	_update_git
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build/"
	
	./bootstrap
	echo
	
	mkdir -p "${srcdir}/${_gitname}_build/BUILD"
	cd "${srcdir}/${_gitname}_build/BUILD"
	
	CFLAGS="" ../configure --host="${CARCH}-unknown-linux-gnu" \
							--prefix="/usr" \
							--bindir="/usr/bin" \
							--sbindir="/usr/bin" \
							--libexecdir="/usr/lib" \
							--with-readline \
							--disable-silent-rules \
							--enable-mtrace \
							--enable-device-mapper \
							--enable-dynamic-loading \
							--enable-nls \
							--enable-debug \
							--enable-pc98
	echo
	
	make clean || true
	echo
	
	CFLAGS="" LDFLAGS="" make
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build/BUILD"
	
	make DESTDIR="${pkgdir}/" install
	echo
	
}
