# Maintainer: Tom Vincent <http://tlvince.com/contact/>
# Contributor: Anton S <fluffylime at gmail dot com>
# Contributor: crequill <crequill at worldonline dot fr>

pkgname=redsocks-git
pkgver=20120911
pkgrel=2
pkgdesc='Transparent redirector of any TCP connection to proxy using your firewall'
arch=('i686' 'x86_64')
url='http://darkk.net.ru/redsocks/'
license=('GPL3')
depends=('libevent')
makedepends=('git')
conflicts=('redsocks')
provides=('redsocks')
install=redsocks.install
backup=('etc/redsocks.conf'
        'etc/conf.d/redsocks'
        'etc/iptables/redsocks.rules')
source=('redsocks.rc'
        'redsocks.conf'
        'redsocks.confd'
        'redsocks.tmpfiles'
        'redsocks.rules')
md5sums=('af62e44c059972314c56ec2fadb56f44'
         'ed8b0090ded9de0940b1cd7539c78422'
         '174129e652467b8880ef2c2ec18d36b0'
         '9be5e8bea768b1a8fdec55a8e8af33fd'
         '04702a7faf31d707d3df4c116bd58a6d')

_gitroot='https://github.com/darkk/redsocks.git'
_gitname=redsocks

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  install -Dm755 "$srcdir/redsocks.rc" "$pkgdir/etc/rc.d/redsocks"
  install -Dm644 "$srcdir/redsocks.conf" "$pkgdir/etc/redsocks.conf"
  install -Dm644 "$srcdir/redsocks.confd" "$pkgdir/etc/conf.d/redsocks"
  install -Dm644 "$srcdir/redsocks.rules" "$pkgdir/etc/iptables/redsocks.rules"
  install -Dm644 "$srcdir/redsocks.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/redsocks.conf"

  install -Dm755 "redsocks" "$pkgdir/usr/bin/redsocks"
  install -Dm644 "README" "$pkgdir/usr/share/doc/redsocks/README"
  install -Dm644 "redsocks.service" "$pkgdir/usr/lib/systemd/system/redsocks.service"
  install -Dm644 "redsocks.conf.example" "$pkgdir/usr/share/doc/redsocks/redsocks.conf.example"
}

