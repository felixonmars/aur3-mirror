# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: science4sail <science4sail at archlinux dot us>
# Contributor: fana-m <geminin at gmx dot net>

pkgname=freeorion
pkgver=0.3.15
pkgrel=1
pkgdesc="Free Clone of Master of Orion. Static linked version."
arch=('i686')
url="http://freeorion.org/"
license=('GPL')
depends=('sdl')
source=(http://freeorion.psitronic.de/download/FreeOrion-$pkgver-Linux-i386.tar.gz
        freeorion.png 
        freeorion.desktop)
md5sums=('150b5d24f71ec0efb0d976f6dbfd675c'
         '7064e5b16d222322ce90cd4598eec64e'
         '13e3e8621b157f8a895521f847a8d80c')

build() {
    cd $srcdir
    
    # Copy everything in /usr/share/freeorion and fix permissions
    install -d $pkgdir/usr/{bin,share/$pkgname}
    cp -r freeorion/* $pkgdir/usr/share/$pkgname/
    find $pkgdir/usr/share/$pkgname/ -type f -exec chmod 644 {} +
    find $pkgdir/usr/share/$pkgname/ -type d -exec chmod 755 {} +
    chmod 755 $pkgdir/usr/share/$pkgname/{freeorion,application/{freeorion,freeorionca,freeoriond}}

    # Install bin, icon and .desktop 
    ln -s /usr/share/$pkgname/freeorion $pkgdir/usr/bin/freeorion
    install -Dm 644 $srcdir/freeorion.png $pkgdir/usr/share/pixmaps/freeorion.png
    install -Dm 644 $srcdir/freeorion.desktop $pkgdir/usr/share/applications/freeorion.desktop
}
