# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Dany Martineau <dany.luc.martineau gmail com>

pkgname=chroma
pkgver=1.13
pkgrel=1
pkgdesc="Abstract puzzle game."
arch=('i686' 'x86_64')
url="http://www.level7.org.uk/chroma/"
license=('GPL')
depends=('sdl_image' 'ncurses' 'freetype2')
makedepends=('make' 'gettext')
source=("http://www.level7.org.uk/${pkgname}/download/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop")
md5sums=('284a12fc110892cd2896c3f1c5052003'
         '90932d9a42f1545c3ba59ae38afb7732')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # removing the next line causes ncurses linking errors
    export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
    ./configure --prefix=/usr
    make
}
package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make DESTDIR=${pkgdir} install
    install -Dm644 ../${pkgname}.desktop \
        ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
