# Maintainer: Muflone <webreg@vbsimple.net>

pkgname=remmina-plugin-url
pkgver=1.0.0.0
pkgrel=1
pkgdesc="A protocol plugin for Remmina to open URLs."
arch=(i686 x86_64)
url="https://github.com/muflone/remmina-plugin-url"
license=('GPL')
depends=('gtk3' 'zlib' 'remmina')
makedepends=('intltool' 'pkgconfig' 'cmake')
optdepends=()
install=remmina-plugin-url.install
source=(
  "https://github.com/downloads/FreeRDP/Remmina/Remmina-1.0.0.tar.gz"
  "https://github.com/FreeRDP/Remmina/commit/569d9bb702d06c061dfbbf3c590d57ea83a04369.diff"
  "https://github.com/FreeRDP/Remmina/commit/f7d1038ba358a295fb46c681db5c47e4ead97306.diff"
  "${pkgname}_${pkgver}.tar.gz::https://github.com/muflone/${pkgname}/archive/${pkgver}.tar.gz"
  "${pkgname}_${pkgver}.diff"
)
md5sums=(
  '701c540acaab961bc3adf130a2ddb8b1'
  'a979c898d5e53446cec9d01912ad9243'
  'b2a9c0e80ac7b0c8a26935bbe289aa3b'
  'e44992380dfc0ee8052cd3d5356c765a'
  'e5b663f1d1cb2261f151736817edbc85'
)

build() {
  #cd ${srcdir}/FreeRDP-Remmina-356c033/
  [ -d build ] && rm -rf build
  cp -r FreeRDP-Remmina-356c033 build
  cp -r "${srcdir}/${pkgname}-${pkgver}/remmina-plugin-url" build
  cd build
  
  patch -p1 -i "../569d9bb702d06c061dfbbf3c590d57ea83a04369.diff"
  patch -p1 -i "../f7d1038ba358a295fb46c681db5c47e4ead97306.diff"
  patch -p1 -i "../${pkgname}_${pkgver}.diff"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd ${srcdir}/build/
  make DESTDIR=${pkgdir} install
}
