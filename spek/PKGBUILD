# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=spek
pkgver=0.7
pkgrel=4
pkgdesc="Analyse your audio files by showing their spectrogram."
arch=('i686' 'x86_64')
url="http://www.spek-project.org/"
license=('GPL3')
depends=('gtk2' 'ffmpeg' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('vala' 'avifile' 'intltool')
install="$pkgname.install"
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.bz2"
        "https://github.com/alexkay/spek/commit/fec0114.diff")
md5sums=('c60e1e9f9a62c41dbbd61c3bb52af4f8'
         '3f6a89b71428953d5c1e8680712a29bf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # ffmpeg 0.11.1 patch ( remove deprecated ) and patch fix :)
  patch -p1 --ignore-whitespace -i ../fec0114.diff
  sed -i 's/avcodec_register/av_register/' src/spek-audio.c

  # vala 0.16 fix
  sed -i "s/ActionEntry/Gtk.&/" src/spek-window.vala

  # ffmpeg 0.11.1 fix ( SAMPLE_FMT_* -> AV_SAMPLE_FMT_* )
  sed -i "s/SAMPLE_FMT/AV_&/" src/spek-audio.c
  export LIBS=" -laviplay"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
