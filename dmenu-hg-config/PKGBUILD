# Maintainer: Tom Vincent <http://tlvince.com/contact/>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Andreas B. Wagner <AndreasBWagner@pointfree.net>
# Contributor: v2punkt0 <v2punkt0@gmail.com>

pkgname=dmenu-hg-config
pkgver=489
pkgrel=2
pkgdesc="A generic menu for X, latest hg pull, sourced config patch"
url="http://tools.suckless.org/dmenu/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama')
makedepends=('mercurial')
conflicts=('dmenu')
provides=('dmenu')
source=('config.patch')
md5sums=('05eb06d0a1746a5d4676d8c8b7456ca4')

_hgroot='http://hg.suckless.org'
_hgrepo='dmenu'

build() {
  cd $srcdir
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u || return 1
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  patch < "$srcdir/config.patch"

  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$_hgrepo-build"
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
