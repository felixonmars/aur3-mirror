# Maintainer: Mitch Bigelow <ipha00@gmail.com>

pkgname=masscan-git
pkgver=100.236f8e0
pkgrel=1
pkgdesc="TCP port scanner, spews SYN packets asynchronously, scanning entire Internet in under 5 minutes"
arch=('i686' 'x86_64')
url="https://github.com/robertdavidgraham/masscan"
license=('AGPL')
depends=('libpcap')
makedepends=('git')
source=("$pkgname"::'git://github.com/robertdavidgraham/masscan.git')
md5sums=('SKIP')

pkgver() {
	cd "$pkgname"
	printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$pkgname"
	make
}

package() {
	cd "$pkgname"

	install -d ${pkgdir}/usr/bin
	cp bin/masscan ${pkgdir}/usr/bin/

	install -d ${pkgdir}/usr/share/man/man8
	gzip -c doc/masscan.8 > ${pkgdir}/usr/share/man/man8/masscan.8.gz

	install -d ${pkgdir}/usr/share/licenses/masscan
	cp LICENSE ${pkgdir}/usr/share/licenses/masscan/
}
