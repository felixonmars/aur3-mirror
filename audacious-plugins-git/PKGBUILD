# Maintainer: Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins-git
pkgver=20110702
pkgrel=1
pkgdesc="Plugins for Audacious-git"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL')
depends=('mpg123')

makedepends=('audacious-git>=${pkgver}'
             'lame' 'libvorbis' 'flac'
             'faad2' 'ffmpeg' 'fluidsynth' 'libcdio' 'libsidplay' 'wavpack'
             'libnotify' 'lirc-utils' 'curl' 'libmtp'
             'neon' 'libmms' 'libcue' 'git')

optdepends=('lame: FileWriter MP3 output'
            'libvorbis: Vorbis input, FileWriter Vorbis output'
            'flac: FLAC input, FileWriter FLAC output'

            'faad2: AAC input'
            'ffmpeg: ffaudio input'
            'fluidsynth: MIDI FluidSynth backend input'
            'libcdio: CD Digital Audio input'
            'libsidplay: Commodore 64 audio input'
            'wavpack: WavPack input'

            'libnotify: libnotify OSD'
            'lirc-utils: LIRC'
            'curl: AudioScrobbler Client'
            'libmtp: Upload to MTP device'

            'neon: neon-based http transport'
            'libmms: libmms-based mms transport'
            'libcue: CUE playlist format')

provides=('audacious-plugins')
conflicts=('audacious-plugins' 'audacious-plugins-hg')

_gitroot="git://git.atheme.org/audacious-plugins"
_gitname="audacious-plugins"

build() {
    msg "Connecting to GIT server..."

    if [[ -d ${_gitname} ]]; then
        ( cd ${_gitname} && git pull origin )
        msg "The local files are updated."
    else
        git clone ${_gitroot}
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf ${_gitname}-build
    git clone ${_gitname}{,-build}
    cd ${_gitname}-build

    ./autogen.sh

    sed 's/libavcore >= 0.12.0//g' -i configure # FS#23549

    [[ ${CARCH} == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
    ./configure \
        --prefix=/usr \
        --enable-chardet \
        --enable-amidiplug \
        --enable-mp3 \
        --disable-adplug \
        ${extraopts}

    make
}

package() {
    cd "${srcdir}/${_gitname}-build"
    make DESTDIR="${pkgdir}" install
}
