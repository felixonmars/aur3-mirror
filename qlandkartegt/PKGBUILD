# Maintainer:  Matthias Maennich <arch@maennich.net>
# Contributor: <boenki@gmx.de>

pkgname=qlandkartegt
pkgver=1.0.1
pkgrel=1
pkgdesc="Use your GPS with Linux"
arch=('i686' 'x86_64')
url="http://www.qlandkarte.org"
license=('GPL')
depends=('proj>=4.6' 'gdal>1.5.0' 'qt>=4.6'  'mesa' 'cfitsio' 'postgresql-libs' 'libmysqlclient')
optdepends=('garmindev: to connect garmin devices'
            'libexif:   to create wayoints from geotagged images'
            'libdmtx:   to summarize waypoints as  2D barcode'
            'gpsd:      to enable gpsd support')
makedepends=('cmake>=2.6')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz" 
        "$pkgname.desktop" 
        "$pkgname.menu"
        "$pkgname-mimetypes.xml")
md5sums=('640f67c194e6dcaf0b58fbeb539b7c88'
         'c1b431fa7694f491e653621b47b68d3f'
         'b82cab9c878d0d090b12e84cdbdb1705'
         'e4808690a9b5381880c573abd81cffdd')

build() {

    mkdir -p $srcdir/build

    cd $srcdir/build
	
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../$pkgname-$pkgver
    make

}

package(){

    cd $srcdir/build

    make DESTDIR=$pkgdir install

    install -D -m 644 $srcdir/$pkgname-$pkgver/src/icons/orig.backGlobe128x128.png $pkgdir/usr/share/pixmaps/$pkgname.png
    install -D -m 644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop  
    install -D -m 644 $srcdir/$pkgname.menu $pkgdir/usr/share/menu/$pkgname.menu
    install -D -m 644 $srcdir/$pkgname-mimetypes.xml $pkgdir/usr/share/mime/packages/$pkgname-mimetypes.xml
}
