# Maintainer: Pritunl <contact@pritunl.com>

pkgname=pritunl-client
pkgver=1.0.497.86
pkgrel=1
pkgdesc="Pritunl VPN Client"
arch=("any")
license=("custom")
url="https://github.com/pritunl/${pkgname}"
depends=(
    "python2"
    "net-tools"
    "iproute2"
    "openvpn"
)
makedepends=(
    "python2-distribute"
)
provides=("${pkgname}")
conflicts=(
    "pritunl-client"
    "pritunl-client-dev"
    "pritunl-client-gtk"
    "pritunl-client-gtk-dev"
)
install=${pkgname}.install
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=("6d246f3b35b699ccf334fdaf03d7ba4b509716e711a08c644d5c65ac70e8583d")
options=("emptydirs")
backup=(
    "var/log/${pkgname}.log"
    "var/log/${pkgname}.log.1"
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p "${pkgdir}/etc/pritunl_client"
    python2 setup.py install --root="${pkgdir}" --prefix=/usr --no-upstart --no-gtk
}
