# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=ropper
_pkgname=Ropper
pkgver=1.4.1
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
arch=('any')
url="https://github.com/sashs/Ropper"
license=('GPL2')
depends=('python2-capstone')
makedepends=('python2-setuptools')
source=(https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('c40d6c022a262eb3b35d28b99ebe9670f07244b2e184e9ff9341eaf7a147a14a17366dc94b5f31d3dc8746f81b89dffa249133446b17442e717027ff20b63603')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -ri 's/(ropper)\.py/\1/g' ropperapp/options.py
  sed -ri 's/env (python)/\12/g' ropperapp/printer/rawprinter.py ropperapp/loaders/raw.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
