# Maintainer: Ner0

pkgname=files-bzr
pkgver=809
pkgrel=1
pkgdesc="A simple, powerful and sexy elementary file manager (Marlin successor)"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-files"
license=('GPL3')
depends=('granite' 'glib2' 'pango' 'gtk3' 'libgee' 'sqlite3' 'dbus-glib' 'gtk-update-icon-cache'
         'libnotify' 'gnome-icon-theme' 'dconf' 'cairo' 'atk' 'desktop-file-utils' 'varka-bzr')
makedepends=('bzr' 'vala' 'cmake' 'pkg-config')
optdepends=('tumbler: for generating thumbnails'
            'contractor-bzr: plugins integration')
conflicts=('marlin-bin' 'marlin-bzr')
install=$pkgname.install

_bzrtrunk=lp:pantheon-files
_bzrmod=files

if [ -f /usr/lib/libunity.so.[0-9] ]; then
  _UNITY=ON
else
  _UNITY=OFF
fi

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build

  #sed -i '/varka/d' src/CMakeLists.txt

  [ -d build ] && rm -rf build
  mkdir build
  cd build

  CFLAGS="$CFLAGS -Wno-unused-but-set-variable -Wno-unused-function -Wno-deprecated-declarations -Wno-unused-variable"

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_EXE_LINKER_FLAGS="$CFLAGS -lm" \
           -DGSETTINGS_COMPILE=OFF -DGSETTINGS_LOCALINSTALL=OFF -DWITH_UNITY=$_UNITY

  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
