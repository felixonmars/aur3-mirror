pkgname=kdebase4-workspace-git
pkgver=20111026
pkgrel=1
pkgdesc="KDE4 Base Workspace Programs"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')
depends=('kdepim4-runtime-git' 'lm_sensors' 'libraw1394'  'libqalculate' 'qimageblitz'
         'polkit-kde-git' 'consolekit' 'xorg-xprop' 'libxdamage' 'libxklavier' 'libdmtx'
         'xorg-xsetroot' 'libxcomposite' 'libxinerama' 'prison-git' 'kactivities-git')
makedepends=('git' 'pkgconfig' 'cmake' 'automoc4' 'boost' 'kdebindings-python')
source=('kdm-zsh-profile.patch' 'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam'
        'fixpath.patch' 'terminate-server.patch')
md5sums=('721e97031b62aee8914e8617e86f9235'
         '363a36b63f84bac79959cfbefdb8309e'
         '10a490653b002e6f9e7476ff9d37c011'
         '552337fd9a3982d809ea16c7f0033d42'
         '367a3538f54db71f108b34cfa31088ac'
         '47a1f12673f66e62e2463efd7037a26a'
         '814350c52c135d6f7bdada1e29223d38')
install="kdebase4-workspace-git.install"
provides=('kdebase-workspace')
conflicts=('kdebase-workspace')
groups=('kde4-git')

_gitroot="git://anongit.kde.org/kde-workspace"
_gitname="kde-workspace"

build() {
  cd $srcdir
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi
  git clone $srcdir/$_gitname $srcdir/build
  cd $srcdir/build
  patch -p0 -i ${srcdir}/kdm-zsh-profile.patch
  patch -p0 -i ${srcdir}/fixpath.patch
  patch -p0 -i ${srcdir}/terminate-server.patch
  mkdir $srcdir/build/build
  cd $srcdir/build/build
  cmake ../ -DCMAKE_BUILD_TYPE=debugfull \
	-DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_Xmms=OFF \
        -DWITH_Googlegadgets=OFF \
        -DWITH_libgps=OFF \
        -DWITH_OpenGLES=OFF \
        -DKWIN_BUILD_WITH_OPENGLES=OFF
  make
}

package() {
  cd ${srcdir}/build/build
  make DESTDIR=${pkgdir} install
  install -D -m755 ${srcdir}/kdm ${pkgdir}/etc/rc.d/kdm
  install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
  install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
  install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
  install -d -m755 ${pkgdir}/usr/share/xsessions/
  ln -sf /usr/share/apps/kdm/sessions/kde-plasma{,-safe}.desktop ${pkgdir}/usr/share/xsessions/
  install -d -m755 ${pkgdir}/etc/kde/{env,shutdown}
  install -d -g 135 -o 135 ${pkgdir}/var/lib/kdm
}