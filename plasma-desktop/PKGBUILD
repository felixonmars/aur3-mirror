# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.0.2
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-desktop'
license=('LGPL')
depends=('kemoticons' 'kitemmodels' 'qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings' 'ksysguard' 'powerdevil' 
'oxygen' 'kmenuedit' 'kinfocenter' 'polkit-kde-frameworks-git')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('plasma-nm: Network manager applet')
conflicts=('kdebase-workspace' 'kdebase-kdepasswd<5')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('79047f0ae75df09ffa6cec13772b7ba8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DLOCALE_INSTALL_DIR=share/locale/kf5 \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
