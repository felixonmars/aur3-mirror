# Maintainer: nil0x42 <http://goo.gl/kb2wf>
pkgname=clide
pkgver=0.9
pkgrel=1
pkgdesc="Color and style highlighting program for text"
arch=('i686' 'x86_64')
url="http://suso.suso.org/xulu/Clide"
license=('GPL3')
depends=('perl')
source=("http://suso.suso.org/programs/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('f578b8c5f1b7f81c7280fcb549ca93c8')

_docs='CHANGELOG COPYING GOALS IDEAS README WARNING'

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    pod2man clide > clide.1
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm755 clide "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 clide.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
    for doc in $_docs; do
        install -Dm644 "${doc}" "${pkgdir}/usr/share/doc/${pkgname}/${doc}"
    done
}
