# Contributors: Flamelab, Ner0
# Maintainer: Ner0

pkgname=marlin-bzr
pkgver=798
pkgrel=1
pkgdesc="A sleek and fast GTK3 file manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/marlin"
license=('GPL2' 'GPL3')
depends=('varka-bzr' 'glib2' 'pango' 'gtk3' 'libgee' 'sqlite3' 'dbus-glib' 'gtk-update-icon-cache'
         'libnotify' 'gnome-icon-theme' 'dconf' 'cairo' 'atk' 'desktop-file-utils')
makedepends=('bzr' 'vala' 'cmake' 'gettext')
optdepends=('tumbler: For generating thumbnails'
            'extended-actions-bzr: Plugins integration [AUR]'
            'light-themes-marlinesque: Themes designed for Marlin [AUR]'
            'marlin-dropbox-plugin-bzr: Dropbox integration [AUR]')
install=$pkgname.install

_bzrtrunk=lp:marlin
_bzrmod=marlin

if [ -f /usr/lib/libunity.so.[0-9] ]; then
  _UNITY=ON
else
  _UNITY=OFF
fi

if [[ $(sed -n 's/Version: //p' /usr/lib/pkgconfig/gtk+-3.0.pc) > 3.3.0 ]]; then
  _bzrtrunk=lp:marlin/pangolin
fi

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build
  [ -d build ] && rm -rf build
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_EXE_LINKER_FLAGS="$CFLAGS -lm" -DGSETTINGS_COMPILE=OFF -DGSETTINGS_LOCALINSTALL=OFF -DWITH_UNITY=${_UNITY}
  make
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
