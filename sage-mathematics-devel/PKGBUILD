# Maintainer: Antonio Rojas < nqn1976 @ gmail.com >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Osman Ugus <ugus11@yahoo.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Special thanks to Nareto for moving the compile from the .install to the PKGBUILD

# use external ATLAS, comment out to compile internal version (warning: very long)
# If you compile the internal version you should disable CPU freq scaling
USE_EXTERNAL_ATLAS=1

# build the documentation by default, comment out to skip (saves ~300MB of disk space)
BUILD_DOCS=1

pkgname=sage-mathematics-devel
pkgver=5.1.beta1
pkgrel=1
pkgdesc='Open Source Mathematics Software, a viable free alternative to Magma, Maple, Mathematica, and Matlab. Development version'
url='http://www.sagemath.org/download-latest.html'
arch=('i686' 'x86_64')
license=('GPL')
depends=()
[[ $USE_EXTERNAL_ATLAS ]] && depends=('atlas-lapack')
makedepends=('gcc-fortran' 'desktop-file-utils')
optdepends=('ffmpeg: to show animations' 'imagemagick: to show animations and some LaTeX output in the notebook'
  'texlive-core: to view LaTeX output in the notebook and to use SageTeX'
  'jsmath-fonts: native TeX fonts for the notebook'
  'openssh: to use the notebook in secure mode')
install="$pkgname.install"
source=("http://boxen.math.washington.edu/home/release/sage-$pkgver/sage-$pkgver.tar"
  "http://boxen.math.washington.edu/home/leif/Sage/spkgs/gfan-0.4plus.p3.spkg"
  "http://boxen.math.washington.edu/home/jdemeyer/spkg/givaro-3.2.13.p0.spkg"
  "http://boxen.math.washington.edu/home/jdemeyer/spkg/linbox-1.1.6.p10.spkg"
  'SAGE-devel-notebook.desktop')
noextract=("gfan-0.4plus.p3.spkg" "givaro-3.2.13.p0.spkg" "linbox-1.1.6.p10.spkg")
md5sums=('6f27ef7ea48f3186c4e8acf8fab09fc0'
         '2bb0b5f4cd0081c1af939294ba2e1859'
         '5b2418891b75adbf6e4fd2716bd4141b'
         '3e14ffa3e7036c196508d7931b718df7'
         'cc47a29bc32fbdea48f8a19e2ad70441')

build() {
  cd sage-$pkgver

  # fix gfan build with GCC 4.7 http://trac.sagemath.org/sage_trac/ticket/12760
  mv ../gfan-0.4plus.p3.spkg spkg/standard
  rm spkg/standard/gfan-0.4plus.p1.spkg 

  # fix sage library build with GCC 4.7 http://trac.sagemath.org/sage_trac/ticket/12761
  mv ../givaro-3.2.13.p0.spkg spkg/standard
  rm spkg/standard/givaro-3.2.13.rc1.p3.spkg

  # fix linbox build with GCC 4.7 http://trac.sagemath.org/sage_trac/ticket/12762
  mv ../linbox-1.1.6.p10.spkg spkg/standard
  rm spkg/standard/linbox-1.1.6.p8.spkg

  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  # don't build GCC
  export SAGE_INSTALL_GCC='no'

  # don't mess with user's .sage dir during build
  export DOT_SAGE='/tmp/.sage'

  # parallel build, replace 2 with the number of available CPU cores
  export MAKE="make -j2"

  # use archlinux's fortran
  export SAGE_FORTRAN='/usr/bin/gfortran'
  export SAGE_FORTRAN_LIB='/usr/lib/libgfortran.so'

  # use external ATLAS
  [[ $USE_EXTERNAL_ATLAS ]] && export SAGE_ATLAS_LIB='/usr/lib'

  # disable building with debugging support
  export SAGE_DEBUG='no'

  # enable fat binaries (disables processor specific optimizations)
  # comment out if you're only building it for yourself
  # export SAGE_FAT_BINARY='yes'
  
  if [[ $BUILD_DOCS ]]; then
    make
  else
    make build
  fi
}

check() {
  cd sage-$pkgver

  make ptest || /bin/true

  # uncomment if you want to run all the tests (warning: very long)
  # make ptestlong || /bin/true
}

package() {
  cd sage-$pkgver

  # remove source packages
  rm -fr spkg/{base,build,standard}

  # remove empty dirs
  rm -fr {tmp,devel/old}

  # remove development stuff
  rm -fr devel/sage-main/sage
  rm -fr devel/sage-main/build/{lib.*,temp.*}
  rm -fr devel/sagenb-main/{build,dist,sagenb.egg-info,sass}
  rm -fr {,devel/sage-main/,devel/sagenb-main/,data/extcode/,local/bin/}.hg*

  # make install is experimental
  # make install DESTDIR=$pkgdir/opt/  
  install -d $pkgdir/opt/sage-devel
  cp -r * $pkgdir/opt/sage-devel

  # install .desktop file
  desktop-file-install $srcdir/SAGE-devel-notebook.desktop --dir $pkgdir/usr/share/applications 

  # install scripts
  install -d $pkgdir/usr/bin
  ./sage -c "install_scripts('$pkgdir/usr/bin', ignore_existing=True)"

  # rename scripts to avoid conflicts
  for _i in $(ls $pkgdir/usr/bin); do
    mv $pkgdir/usr/bin/$_i $pkgdir/usr/bin/sage-devel-$_i
  done

  # create link to main binary
  ln -s /opt/sage-devel/sage $pkgdir/usr/bin/sage-devel

  # copy license file to its proper location (keep it in /opt/sage so that license() works)
  install -d $pkgdir/usr/share/licenses/sage-mathematics-devel
  cp $pkgdir/opt/sage-devel/COPYING.txt $pkgdir/usr/share/licenses/sage-mathematics-devel

  # move SageTeX files to a more appropriate directory (will conflict with sage-mathematics)
  # mv $pkgdir/opt/sage-devel/local/share/texmf $pkgdir/usr/share

  # remove build logs
  rm $pkgdir/opt/sage-devel/*.log
  rm -r $pkgdir/opt/sage-devel/spkg/logs
}
