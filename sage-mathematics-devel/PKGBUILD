# Maintainer: Antonio Rojas < nqn1976 @ gmail.com >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Osman Ugus <ugus11@yahoo.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Special thanks to Nareto for moving the compile from the .install to the PKGBUILD

# use external ATLAS, comment out to compile internal version (warning: very long)
# If you compile the internal version you should disable CPU freq scaling
USE_EXTERNAL_ATLAS=1

# build the documentation by default, comment out to skip (saves ~300MB of disk space)
BUILD_DOCS=1

pkgname=sage-mathematics-devel
sagename=sage # change to allow installation alongside stable version
pkgver=5.3.beta1
pkgrel=1
pkgdesc='Open Source Mathematics Software, a viable free alternative to Magma, Maple, Mathematica, and Matlab. Development version'
url='http://www.sagemath.org/download-latest.html'
arch=('i686' 'x86_64')
license=('GPL')
conflicts=('sage-mathematics')
provides=('sage-mathematics')
depends=('openssl')
[[ $USE_EXTERNAL_ATLAS ]] && depends+=('atlas-lapack')
makedepends=('gcc-fortran' 'desktop-file-utils')
optdepends=('ffmpeg: to show animations' 'imagemagick: to show animations and some LaTeX output in the notebook'
  'texlive-core: to view LaTeX output in the notebook and to use SageTeX'
  'openssh: to use the notebook in secure mode')
install="$pkgname.install"
source=("http://boxen.math.washington.edu/home/release/sage-$pkgver/sage-$pkgver.tar"
  'sage-devel-notebook.desktop')
noextract=()
md5sums=('2f657bf190afaf35bc68e73f739d78a6'
         'f1b56e3376a49537fb2c8037b0e3f039')

build() {
  cd sage-$pkgver

  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  # don't mess with user's .sage dir during build
  export DOT_SAGE='/tmp/.sage'

  # parallel build, replace 2 with the number of available CPU cores
  export MAKE="make -j2"

  # use archlinux's fortran
  export SAGE_FORTRAN='/usr/bin/gfortran'
  export SAGE_FORTRAN_LIB='/usr/lib/libgfortran.so'

  # use external ATLAS
  [[ $USE_EXTERNAL_ATLAS ]] && export SAGE_ATLAS_LIB='/usr/lib'

  # disable building with debugging support
  export SAGE_DEBUG='no'

  # enable fat binaries (disables processor specific optimizations)
  # comment out if you're only building it for yourself
  # export SAGE_FAT_BINARY='yes'
  
  if [[ $BUILD_DOCS ]]; then
    make
  else
    make build
  fi
}

check() {
  cd sage-$pkgver

  make ptest || /bin/true

  # uncomment if you want to run all the tests (warning: very long)
  # make ptestlong || /bin/true
}

package() {
  cd sage-$pkgver

  # remove source packages
  rm -fr spkg/{base,build,standard}

  # remove empty dirs
  rm -fr {tmp,devel/old}

  # remove development stuff
  rm -fr devel/sage-main/build/{lib.*,temp.*}
  rm -fr {,devel/sage-main/,devel/sagenb-main/,data/extcode/,local/bin/}.hg*

  # make install is experimental
  # make install DESTDIR=$pkgdir/opt/  
  install -d $pkgdir/opt/$sagename
  cp -r * $pkgdir/opt/$sagename

  # install .desktop file
  desktop-file-install $srcdir/sage-devel-notebook.desktop --dir $pkgdir/usr/share/applications 

  # install scripts
  install -d $pkgdir/usr/bin
  ./sage -c "install_scripts('$pkgdir/usr/bin', ignore_existing=True)"

  # rename scripts to avoid conflicts
  for _i in $(ls $pkgdir/usr/bin); do
    mv $pkgdir/usr/bin/$_i $pkgdir/usr/bin/$sagename-$_i
  done

  # create link to main binary
  ln -s /opt/$sagename/sage $pkgdir/usr/bin/$sagename

  # copy license file to its proper location (keep it in /opt/sage so that license() works)
  install -d $pkgdir/usr/share/licenses/sage-mathematics-devel
  cp $pkgdir/opt/$sagename/COPYING.txt $pkgdir/usr/share/licenses/sage-mathematics-devel

  # move SageTeX files to a more appropriate directory (will conflict with sage-mathematics)
  mv $pkgdir/opt/$sagename/local/share/texmf $pkgdir/usr/share

  # remove build logs
  rm $pkgdir/opt/$sagename/*.log
  rm -r $pkgdir/opt/$sagename/spkg/logs
}
