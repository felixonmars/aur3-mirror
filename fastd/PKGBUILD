pkgname=fastd
arch=(i686 x86_64)
pkgver=8
pkgrel=1
pkgdesc="Fast and secure tunneling daemon"
makedepends=('cmake' 'flex' 'bison' 'libuecc>=3' 'libnacl' 'pkg-config')
depends=('glibc')
url="https://projects.universe-factory.net/projects/fastd"
license=('BSD')
source=("https://projects.universe-factory.net/attachments/download/46/${pkgname}-${pkgver}.tar.xz")
md5sums=('44957c7c656c7123813c01b8f52aaf3b')

build() {
  mkdir -p "$srcdir/build-${pkgver}"
  cd "$srcdir/build-${pkgver}"

  cmake -D CMAKE_BUILD_TYPE=RELEASE -D CMAKE_INSTALL_PREFIX=/usr "../${pkgname}-${pkgver}"
  make
}

package() {
  cd "$srcdir/build-${pkgver}"
  make install DESTDIR="${pkgdir}"

  install -dm755 "${pkgdir}/etc/fastd/"

  install -dm755 "${pkgdir}/usr/lib/systemd/system/"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/examples/fastd@.service" "${pkgdir}/usr/lib/systemd/system/"
}
