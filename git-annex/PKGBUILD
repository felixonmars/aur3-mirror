# Maintainer: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Elena ``of Valhalla'' Grandi <elena.valhalla@gmail.com>

# In _cabal_mode=1 cabal install is used to install the missing dependencies
# for git-annex in a temporary local location to not interfer with you
# installation.
_cabal_mode=0
#_cabal_mode=1

# Some of these features require packages not available in the [haskell] repo.
# The _cabal_mode=1 is here for that reason.
FEATURES='-DWITH_S3'
#FEATURES='-DWITH_ASSISTANT -DWITH_S3 -DWITH_WEBAPP -DWITH_NEW_YESOD -DWITH_PAIRING'

# Building the documentation requires ikiwiki.
# If you don't want to build the documentation turns this to 0.
_build_docs=1

pkgname=git-annex
pkgver=3.20121001
pkgrel=1
pkgdesc='manage files with git, without checking their contents into git'
arch=('i686' 'x86_64')
url="http://git-annex.branchable.com/"
license=(GPL3)
depends=(git util-linux openssh rsync pcre curl)
options=(!makeflags)
if (( _cabal_mode )); then
  makedepends=(ghc cabal-install)
else
  makedepends=(ghc
              haskell-bloomfilter
              haskell-dataenc
              haskell-edit-distance
              haskell-hinotify
              haskell-hs3
              haskell-hslogger
              haskell-http
              haskell-ifelse
              haskell-json
              haskell-lifted-base
              haskell-missingh
              haskell-monad-control
              haskell-pcre-light
              haskell-quickcheck
              haskell-sha
              haskell-stm
              haskell-transformers-base
              haskell-testpack
              haskell-utf8-string)
fi
(( ! _build_docs )) || makedepends=(ikiwiki "${makedepends[@]}")
_gitroot=git://git.kitenet.net/git-annex
_gitrepo=git-annex
_ignore='IGNORE=-ignore-package monads-fd -ignore-package monads-tf'

asking() {
  read -p "$@ (Y/n)" answer
  case "$answer" in
    n|N|no|NO|No|nO) false;;
    *) true;;
  esac
}

build() {
  cd $srcdir

  if [ -d $_gitrepo ] ; then
    cd $_gitrepo
    git fetch origin || return 1
    git reset --hard origin/master || return 1
    msg "The local files are updated."
  else
    # --depth=1 should only be used when no merge is done
    git clone "${_gitroot}" $_gitrepo || return 1
  fi
  cd "${srcdir}/$_gitrepo"
  git checkout $pkgver || return 1
  rsync -aH --del "${srcdir}/$_gitrepo/" "${srcdir}/build/" || return 1
  cd "${srcdir}/build"

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  ORIG_HOME="$HOME"
  ORIG_TMPDIR="$TMPDIR"
  export HOME="${srcdir}/build"
  export TMPDIR="${srcdir}/build/tmp"
  mkdir -p "$HOME" "$TMPDIR"

  # Thanks to the local HOME this is harmless
  git config --global user.email test@example.com
  git config --global user.name  'John Doe'

  if (( _cabal_mode )); then
    cabal update
    cabal install --user --only-dependencies
  fi

  make "$_ignore" FEATURES="$FEATURES" || return 1
  # haskell-testpack is required only to run the tests
  make "$_ignore" FEATURES="$FEATURES" test ||
    asking "Tests failed. Continue?"
  export HOME="$ORIG_HOME"
  export TMPDIR="$ORIG_TMPDIR"
}

package() {
  cd $srcdir/build
  make "$_ignore" DESTDIR=$pkgdir install
}
