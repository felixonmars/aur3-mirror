# Maintainer: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>

_target="arm-linux-gnueabi"
pkgname=${_target}-eglibc-headers
pkgver=2.15
pkgrel=8
pkgdesc="Embedded GNU C Library. Stage 1 - headers only. Needed for initialy build toolchain (${_target})"
arch=(any)
url="http://www.eglibc.org"
license=('GPL')
depends=("${_target}-linux-api-headers")
makedepends=("${_target}-gcc-stage1" 'subversion')
options=(!strip)
source=("eglibc-2.15-LIBC_TRY_CC_OPTION.patch")
md5sums=("b5e1e3caa485a216dae43df024986f4b")

build() {
  cd "$srcdir"

  if [ -x "eglibc-${pkgver}" ]; then
    rm -rf eglibc-${pkgver}
  fi
  svn co svn://svn.eglibc.org/branches/eglibc-${pkgver/./_}/libc eglibc-${pkgver}
  cd eglibc-${pkgver}
  svn co svn://svn.eglibc.org/branches/eglibc-${pkgver/./_}/ports ports

  # Don not build timezones
  sed -i 's/timezone rt/rt/' Makeconfig

  patch -Np1 -i "$srcdir"/eglibc-2.15-LIBC_TRY_CC_OPTION.patch

  rm -rf build
  mkdir build
  cd build

  unset CFLAGS
  export CFLAGS="${CFLAGS} -U_FORTIFY_SOURCE  -mlittle-endian -msoft-float -O2"

  export BUILD_CC=gcc
  export CC=${_target}-gcc
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

  ../configure \
    --target=${_target} \
    --host=${_target} \
    --build=${CHOST} \
    --prefix=/  \
    --with-headers=/usr/${_target}/include \
    --enable-add-ons=libidn,ports,nptl \
    --disable-profile \
    --without-gd \
    --without-cvs

  # make some libs and stubs
  make csu/subdir_lib
}

package()
{
  cd "$srcdir/eglibc-$pkgver/build"

  #
  make install-headers install_root=${pkgdir}/usr/${_target} install-bootstrap-headers=yes

  #
  mkdir -p ${pkgdir}/usr/${_target}/lib
  cp csu/crt1.o csu/crti.o csu/crtn.o ${pkgdir}/usr/${_target}/lib

  # create stub lib
  ${_target}-gcc -nostdlib -nostartfiles -shared -x c /dev/null -o ${pkgdir}/usr/${_target}/lib/libc.so

  #
  mkdir -p ${pkgdir}/usr/${_target}/usr
  (
   cd ${pkgdir}/usr/${_target}/usr
   ln -s ../{include,lib} .
  )

}

# vim:set ts=2 sw=2 et:
