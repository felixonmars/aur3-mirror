# Contributor: Kyle Keen <keenerd@gmail.com>
pkgname=cataclysm-git
pkgver=20111122
pkgrel=1
pkgdesc="A post-apocalyptic roguelike."
url="http://cataclysmrl.blogspot.com/"
arch=('i686' 'x86_64')
license="copyright"
depends=('ncurses')
makedepends=('git')
install=cataclysm-git.install
source=(cataclysm-git.install keymap.txt)
md5sums=('9b048cbaa2588b9e81ff91f6fc6f665c'
         'af7b6a0873b18135c0b648a6f98f9387')

_gitroot="git://github.com/Whales/Cataclysm.git"
_gitname="Cataclysm"

build() {
  cd "$srcdir"
  msg "Connecting to github..."

  if [ -d "$srcdir"/${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd "$srcdir/$_gitname"

  sed -i 's|"data/|"/usr/share/cataclysm/data/|g' *.cpp
  sed -i 's|"./data/|"/usr/share/cataclysm/data/|g' *.cpp
  sed -i 's|"data"|"/usr/share/cataclysm/data"|g' *.cpp
  sed -i 's|"save"|"/var/games/cataclysm/save"|g' *.cpp
  sed -i 's|"save/|"/var/games/cataclysm/save/|g' *.cpp

  # someday...
  #sed -i '1i #include <stdio.h>' {game,map,overmap}.cpp
  #sed -i 's|"save/|getenv("HOME") << "/.cataclysm/save/|g' *.cpp

  make PREFIX=/usr
}

package() {
  cd "$srcdir/$_gitname"
  install -Dm755 cataclysm "$pkgdir/usr/bin/cataclysm"
  install -dm755  "$pkgdir/usr/share/cataclysm/data"
  cp data/* "$pkgdir/usr/share/cataclysm/data"
  install -dm777 -g games "$pkgdir/var/games/cataclysm/save"
  install -Dm764 -g games ../keymap.txt "$pkgdir/usr/share/cataclysm/data"
}
