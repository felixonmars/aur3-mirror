# Maintainer: Michael Mansell <michael.mansell@gmail.com>
# Contributor: Joe Davison <joedavison.davison@gmail.com>

pkgname=wxlauncher
_pkgtar=wxlauncher
pkgver=0.9.4
pkgrel=4

source=("http://wxlauncher.googlecode.com/files/${_pkgtar}-$pkgver.tar.gz"
	"wxlauncher_homedir.patch")
md5sums=('daa392649cdb28d9b41b4e89d0923c94'
	'828542570e508e069489f32a70432974')

pkgdesc="wxLauncher is a cross-platform launcher for the FreeSpace 2 Open engine"
url="http://www.hard-light.net/forums/index.php?topic=67950.0"
arch=(i686 x86_64)
license=(GPL2)
# change below if you want to modify the resources intall directory.  The default installs it along side fs2_open resources.
resourcesdir=/opt/fs2_open/wxlauncher

depends=('wxpython2.8' 'python2' 'python2-markdown' 'openal')
optdepends=('fs2_open: Well that is what this launcher is for')
makedepends=(cmake)

prepare() {
	# prepare environment
	cd $srcdir/${_pkgtar}-$pkgver

	# needed to fix linking bug mentioned in Issue 93 (http://code.google.com/p/wxlauncher/issues/detail?id=93)
	# Thanks to Samsagax for pointing me in the direction of this patch
	patch -Np3 < $srcdir/wxlauncher_homedir.patch
}


build() {
	cd $srcdir/${_pkgtar}-$pkgver

		if [ ! -d build ]; then
			mkdir build
		fi

	cd build/
	
	# commence build		

	#cmake -DPYTHON_EXECUTABLE=python2 -DUSE_OPENAL=1 -DRESOURCES_PATH=$resourcesdir -DCMAKE_INSTALL_PREFIX=/usr ../
	# Make sure to build against wxpython2.8
	cmake -DPYTHON_EXECUTABLE=python2 -DUSE_OPENAL=1 -DRESOURCES_PATH=$resourcesdir -DCMAKE_INSTALL_PREFIX=/usr ../ -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8'
	make
		

}

package() {
	cd "$srcdir/$pkgname-$pkgver/build"
	make DESTDIR="$pkgdir/" install
}
