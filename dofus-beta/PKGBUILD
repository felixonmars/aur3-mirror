# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>
# Contributor: Schtroumpfette <fpeterschmitt@voila.fr>
pkgname=dofus-beta
pkgver=2.9.0
pkgrel=1
pkgdesc="A manga inspired, Massively Multiplayer Online Role-playing Game (MMORPG) for Adobe AIR (beta version)."
url="http://www.dofus.com/"
license=("custom:Dofus License")
arch=('i686' 'x86_64')
source=(DofusInstall.run::http://dl.ak.ankama.com/games/dofus2-beta/setup/setup-base-beta.run
        air-generic-launcher.sh
        UpLauncher.arch.sh)
if [ "$CARCH" == "x86_64" ];then
  depends=('adobe-air-sdk' 'lib32-gtk2' 'lib32-libxt'
           'lib32-libjpeg6' 'lib32-libpng12' 'lib32-openssl098'
           'lib32-libldap' 'lib32-nss' 'lib32-libxslt'
           'lib32-dbus-glib' 'lib32-alsa-lib')
else
  depends=('adobe-air-sdk' 'gtk2' 'libxt'
           'libjpeg6' 'libpng12' 'openssl098'
           'nss' 'libxslt' 'alsa-lib')
fi
install=dofus.install
md5sums=('b8e815180786bfd40435eef55e684fc6'
         'f179eaa5e6e6674b1853cf826fc33c3a'
         'ae460da240d653d796e5c8f703d572c0')

build() {
  cd "$srcdir"

  msg2 "Extracting installer..."
  sh ./DofusInstall.run --noexec --keep --target dofus_install
}

package() {
  cd "$srcdir/dofus_install"
  install_dir="$pkgdir/opt/dofus-beta"
  mkdir -p "$install_dir"

  msg2 "Installing main applications..."
  cp -R Dofus/* "$install_dir"
  chmod 0775 "$install_dir/share"
  install "$srcdir/air-generic-launcher.sh" "$install_dir/bin/Dofus"
  cp -R Reg "$install_dir/share/reg"
  chmod 0775 "$install_dir/share/reg" "$install_dir/share/reg/share"
  install "$srcdir/air-generic-launcher.sh" "$install_dir/share/reg/bin/Reg"

  msg2 "Installing additional content..."
  mv "$install_dir/share/reg/share/Reg.swf" "$install_dir/share/reg/Reg.swf"
  ln -s ../Reg.swf "$install_dir/share/reg/share/Reg.swf"
  ln -s ../content "$install_dir/share/reg/share/content"
  ln -s ../config.xml "$install_dir/share/reg/share/config.xml"
  install UpLauncher "$install_dir/share/UpLauncher"
  install -m 644 uplauncherComponents.xml "$install_dir/share/uplauncherComponents.xml"

  msg2 "Installing launcher script..."
  install "$srcdir/UpLauncher.arch.sh" "$install_dir/share/UpLauncher.arch.sh"
  mkdir -p "$pkgdir/usr/bin"
  ln -s /opt/dofus-beta/share/UpLauncher.arch.sh "$pkgdir/usr/bin/dofus-beta"

  msg2 "Installing menu icon..."
  mkdir -p "$pkgdir/usr/share/applications"
  cat > "$pkgdir/usr/share/applications/Dofus-beta.desktop" <<EOF
[Desktop Entry]
Encoding=UTF-8
Type=Application
Name=Dofus
GenericName=Dofus
Comment=Dofus
Icon=/opt/dofus-beta/share/icon/dofus-icon-48.png
Exec=/opt/dofus-beta/share/UpLauncher.arch.sh
Path=/opt/dofus-beta/share/
Categories=Game;RolePlaying;Ankama-Games
EOF

  msg2 "Installing licenses..."
  mkdir -p "$pkgdir/usr/share/licenses/dofus-beta"
  cp -R licenses/*.txt "$pkgdir/usr/share/licenses/dofus-beta"
  chmod 0644 "$pkgdir/usr/share/licenses/dofus-beta"/*
}
# vim:set ts=2 sw=2 et:
