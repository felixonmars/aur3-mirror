# Based on extra/trayer

pkgname=trayer-dwm
_pkgname=trayer
pkgver=1.0
pkgrel=1
pkgdesc="A lightweight GTK2-based systray, for DWM"
arch=('i686' 'x86_64')
url="https://gna.org/projects/fvwm-crystal/"
license=('GPL' 'MIT')
depends=('gtk2')
makedepends=('libxmu')
provides=('trayer')
conflicts=('trayer')
options=('!makeflags')
source=("http://download.gna.org/fvwm-crystal/trayer/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        'trayer.diff' 'manpage.diff' 'dwm.diff')
md5sums=('e1b0b6464f991f2c296561e377585867'
         '7f66eee885563871f9f76646bca6b14d'
         '3f1c92bbe275f186b613c04a47e8e977'
         '501303702b13c53da4c3fb5fbd837aeb')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p0 -i "$srcdir/trayer.diff"
  patch -p1 -i "$srcdir/manpage.diff"
  patch -p1 -i "$srcdir/dwm.diff"
  sed -i 's/$(LIBS) $(OBJ) $(SYSTRAYOBJ)/$(OBJ) $(SYSTRAYOBJ) $(LIBS)/' Makefile
  sed -i 's/-lXmu/-lXmu -lX11/' Makefile.common
  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make PREFIX="${pkgdir}/usr" install
  install -D -m644 trayer.1 "${pkgdir}/usr/share/man/man1/trayer.1"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
