# Maintainer: Muflone <webreg@vbsimple.net>
# Contributor: jose <jose1711 [at] gmail (dot) com>

pkgname=gwakeonlan
pkgver=0.6
pkgrel=2
pkgdesc="GTK+ utility to awake machines using the Wake on LAN."
url="http://gwakeonlan.muflone.com/"
arch=('any')
license=('GPL2')
depends=('gtk3' 'gobject-introspection' 'python2-xdg' 'python2-gobject' 'gtk-update-icon-cache')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/muflone/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('45308dc3df2acb26968f3bedcc0c9a52')
sha1sums=('dc41dd0ef95870aa281e5c2967140c85dbfa4fe3')
sha256sums=('89f00865f61d0e63f2ad4eaaf127705aba595945f7c468c6fb217b9eaf7e509a')
install="${pkgname}.install"

build() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --root "${pkgdir}"
}

