# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=csync
pkgver=0.44.0
pkgrel=3
pkgdesc="A bidirectional file synchronizer especially designed for you, the normal user"
arch=("i686" "x86_64")
license=('GPL2')
url="http://www.csync.org"
depends=('libssh' 'smbclient' 'log4c' 'sqlite3' 'iniparser')
makedepends=('cmake')
backup=(
	etc/csync/csync.conf
	etc/csync/csync_exclude.conf
	etc/csync/csync_log.conf
)
source=("http://www.csync.org/files/$pkgname-$pkgver.tar.gz")

build() {
	cd "$srcdir"
	rm -rf build
	mkdir build
	cd build
	cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONF_INSTALL_DIR=/etc "$srcdir/$pkgname-$pkgver"
	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir" install
}

sha256sums=('37f40a49d03dc96c111369df99eb45de1fd0c7b725d4077f0e82e6719fdfb9cc')
