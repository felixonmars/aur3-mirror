# Maintainer: SanskritFritz (gmail)
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias T. <OldShatterhand at gmx-topmail dot de>

pkgname=bibletime-git
_gitname="bibletime"
pkgver=20130311
pkgrel=1
epoch=1
pkgdesc="Bible study tool for KDE4 - sword-svn compatible branch"
arch=('x86_64' 'i686')
url="http://www.bibletime.info/"
license=('GPL2')
depends=('sword-svn=2793' 'openssl' 'clucene' 'qtwebkit')
makedepends=('cmake' 'git')
provides=('bibletime')
conflicts=('bibletime')
source=("git://gitorious.org/bibletime/bibletime.git#branch=sword-svn-compat")
md5sums=('SKIP')
install=$pkgname.install

pkgver() {
	cd "$srcdir/$_gitname"
	git log -1 --format="%cd" --date=short | sed 's|-|.|g'
}

build() {
	cd "$srcdir/$_gitname"
	CXXFLAGS+=" -fpermissive"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DQT_QMAKE_EXECUTABLE=qmake-qt4 \
		-DBT_FORCE_USE_QT4=TRUE \
		"$srcdir/$_gitname"

	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
