# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributors: atommixz, beroal

pkgname=libtorrent-rasterbar-svn
pkgver=r10163
pkgrel=1
pkgdesc="C++ library that aims to be a good bittorrent implementation (development version)"
arch=('i686' 'x86_64')
url="http://www.rasterbar.com/products/libtorrent/"
license=('BSD')
depends=('boost-libs' 'python2' 'geoip')
makedepends=('boost' 'subversion')
conflicts=("${pkgname%-*}")
provides=("${pkgname%-*}")
source=(${pkgname%-*}::"svn+http://svn.code.sf.net/p/libtorrent/code/trunk")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd ${pkgname%-*}

  export PYTHON="/usr/bin/python2"
  ./bootstrap.sh --prefix=/usr --enable-python-binding --with-libgeoip=system
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
