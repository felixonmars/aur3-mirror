# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: Todd Partridge <toddrpartridge@gmail.com>

pkgname=worldofgoo
_gamepkg="WorldOfGooSetup.1.41.tar.gz"
pkgver=1.41
pkgrel=4
pkgdesc="A physics based puzzle/construction game (requires copy of the full game)"
arch=('i686' 'x86_64')
url="http://www.2dboy.com"
license=('custom')
depends=('mesa' 'sdl_mixer' 'hicolor-icon-theme')
optdepends=('worldofgoo-gootool: for creating and installing fan levels')
install="worldofgoo.install"
source=("$_gamepkg"::"local://$_gamepkg"
        'WorldOfGoo.desktop')
md5sums=('f5afa40893d0fbcc37885191404f6d8c'
         '25080af1fe369a296e15e568f2480124')
options=(!strip)
PKGEXT='.pkg.tar'
DLAGENTS=("${DLAGENTS[@]}" 'local::/bin/true')

build() {
  cd "$srcdir"

  # data
  install -d "$pkgdir/opt"
  cp -a WorldOfGoo "$pkgdir/opt"

  # patch and link to startup script
  sed -e 's|export LD_LIBRARY_PATH|#export LD_LIBRARY_PATH|' -i "$pkgdir/opt/WorldOfGoo/WorldOfGoo"
  install -d "$pkgdir/usr/bin"
  ln -s /opt/WorldOfGoo/WorldOfGoo "$pkgdir/usr/bin/worldofgoo"

  # icons and desktop launcher
  install -Dm644 WorldOfGoo.desktop "$pkgdir/usr/share/applications/WorldOfGoo.desktop"
  for i in 16x16 22x22 32x32 48x48 64x64 128x128; do
    install -d "$pkgdir/usr/share/icons/hicolor/$i/apps"
    ln -s /opt/WorldOfGoo/icons/$i.png "$pkgdir/usr/share/icons/hicolor/$i/apps/WorldOfGoo.png"
  done
  install -d "$pkgdir/usr/share/icons/hicolor/scalable/apps"
  ln -s /opt/WorldOfGoo/icons/scalable.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/WorldOfGoo.svg"

  # license
  install -d "$pkgdir/usr/share/licenses/worldofgoo"
  ln -s /opt/WorldOfGoo/eula.txt "$pkgdir/usr/share/licenses/worldofgoo"
}
