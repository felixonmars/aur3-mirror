# Maintainer:  Daniel YC Lin <dlin.tw at gmail>
# Contributor : Tdy  <tdy@gmx.com>, Biginoz <biginoz at free.fr>
pkgname=sweethome3d
pkgver=3.6
pkgrel=5
pkgdesc="An interior design application"
arch=('any')
url="http://www.sweethome3d.eu/"
license=('GPL')
depends=('bash' 'java-environment' 'java3d')
source=($pkgname.desktop $pkgname.png sweethome3d
 http://downloads.sourceforge.net/$pkgname/SweetHome3D-$pkgver-linux-x86.tgz)

package() {
  cd "$srcdir"
  mkdir -p "$pkgdir/usr/share/java/$pkgname" \
    "$pkgdir/usr/share/licenses/$pkgname"
  install SweetHome3D-$pkgver/lib/*.jar "$pkgdir/usr/share/java/$pkgname"
  local n
  for n in j3dcore.jar j3dutils.jar vecmath.jar ; do
    rm "$pkgdir/usr/share/java/$pkgname/$n"
  done
  install SweetHome3D-$pkgver/*.{TXT,HTML} "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm755 ../sweethome3d "$pkgdir/usr/bin/sweethome3d"
}
md5sums=('5d5f1b5d390b71d545e09b3afc66ac38'
         '4fa1604285c1d8c474e3a914b6a4e6ce'
         '325b6ef353c91cedc4db6ada30844247'
         '73ab4df93a4add51ec319501d45b2a50')
