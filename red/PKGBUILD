# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
pkgname=red
pkgver=0.4.3
pkgrel=2
pkgdesc="Red Programming Language - An open source, native code compiled, dialect of Rebol"
arch=('i686' 'x86_64')
url="http://www.red-lang.org"
license=('custom:3-clause BSD' 'custom:BSL')
groups=('devel')
conflicts=('ed')
makedepends=('wget' 'rebol=2.7.8' 'bash')
source=("https://github.com/dockimbel/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('ff052ff74699c827ae0843af967e0ffa')

if test "$CARCH" == x86_64; then
  depends+=(lib32-readline)
fi

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  rebol -qw red.r tests/hello.red
  rebol -qw red.r runtime/console/console.red
  msg2 "Generating docs..."
  cd docs
  rebol -qw makedoc2.r red-system-specs.txt
  rebol -qw makedoc2.r red-system-quick-test.txt
}

check() {
   msg2 "Skipping tests..."
#  cd "$srcdir/$pkgname-$pkgver"
#  rebol -qws run-all.r --batch
}

package() {
  cd "$srcdir"
  install -Dm755 ./"${pkgname}-${pkgver}"/console "$pkgdir"/usr/bin/$pkgname
  install -d "$pkgdir"/opt/$pkgname
  cp -R $pkgname-$pkgver/* "$pkgdir"/opt/$pkgname/
  rm -rf "$pkgdir"/opt/$pkgname/{tests,quick-test,console}
  cd $pkgname-$pkgver
  install -Dm644 BSD-3-License.txt "$pkgdir"/usr/share/licenses/$pkgname/BSD-3-License.txt
  install -Dm644 BSL-License.txt "$pkgdir"/usr/share/licenses/$pkgname/BSL-License.txt
  install -Dm644 docs/red-system-quick-test.html "$pkgdir"/usr/share/doc/$pkgname/red-system-quick-test.html
  install -Dm644 docs/red-system-specs.html "$pkgdir"/usr/share/doc/$pkgname/red-system-specs.html
  rm "$pkgdir"/opt/$pkgname/docs/red-system-{quick-test,specs}.html
}

# vim:set ts=2 sw=2 et:
