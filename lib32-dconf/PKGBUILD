# Maintainer: jtts
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=dconf
pkgname=lib32-$_pkgbasename
pkgver=0.18.0
pkgrel=2
pkgdesc="A low-level configuration system (32-bit)."
arch=(x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('lib32-glib2' $_pkgbasename) # 'lib32-gtk-update-icon-cache'
makedepends=('gcc-multilib' 'intltool') # 'gtk3' 'vala' 'docbook-xsl' 'python' 'python2'
#optdepends=('gtk3: for dconf-editor')
install=dconf.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('109b1bc6078690af1ed88cb144ef5c5aee7304769d8bdc82ed48c3696f10c955')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "$_pkgbasename-$pkgver"

  # Remove compilation of man-pages  
  sed -i 's/docs //' Makefile.am Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32/dconf \
    --disable-editor \
    --disable-gtk-doc \
    --disable-gtk-doc-html \
    --disable-gtk-doc-pdf \
    --disable-schemas-compile \
    --disable-nls
  make
}

package() {
  cd "$_pkgbasename-$pkgver"
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  # cleanup for lib32 package
  rm -rf $pkgdir/{etc,bin,sbin,usr/{bin,lib,include,share},var}
}

# vim:set ts=2 sw=2 et:
