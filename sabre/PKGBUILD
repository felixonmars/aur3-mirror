# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=sabre
pkgver=0.2.4b
pkgrel=1
pkgdesc="A flight simulator with fighter planes of the Korean War era"
arch=('i686' 'x86_64')
url="http://www.happypenguin.org/show?Sabre"
license=('GPL')
depends=('dialog' 'gcc-libs' 'svgalib' 'svgalib-helper')
install="${pkgname}.install"
source=("http://archive.debian.org/debian/pool/main/s/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        "000_cflags.diff"
        "010_rebootstrap.diff"
        "020_bugfixes.diff"
        "025_endianness.diff"
        "030_launch_scripts_arch.diff"
        "035_gcc43_1.diff"
        "040_gcc43_2.diff"
        "041_gcc43.diff")
md5sums=('ebab5cab09e6fdf3059a72525693d05b'
         'fe02a6e4b135ced8145362f46fc8f5ba'
         '6fa776b1f65b67ae6175ea03e8bbf4a4'
         'c560955f64472cbf978e2b4a5d42e7bf'
         'c9e7d1529c9df1ba6124194190c8d092'
         '7937b8f5bb2c0f637ef5b23e9869726b'
         '1019e5264e1554dc35b78ed83ea81363'
         'a7a03d98ac38084c90303c287b9a102e'
         'bdec5c04a80e201cbdfcab847344c876')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}.orig"

  # Apply Debian patches
  patch -Np1 -i ../000_cflags.diff
  patch -Np1 -i ../010_rebootstrap.diff
  patch -Np1 -i ../020_bugfixes.diff
  patch -Np1 -i ../025_endianness.diff
  patch -Np1 -i ../030_launch_scripts_arch.diff
  patch -Np1 -i ../035_gcc43_1.diff
  patch -Np1 -i ../040_gcc43_2.diff
  patch -Np1 -i ../041_gcc43.diff

  # Add missing #include and fix path for data files
  sed -i "26i#include <stdint.h>" src/txtrmap.h
  sed -i "s_/usr/share/games/sabre/lib_/usr/share/sabre/lib_" src/globals.C

  # Compile SVGAlib version
  ./configure
  make

  # Compile SDL version
#  cp src/main.C sdlsrc
#  cd sdlsrc
#  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}.orig"

  # Install data files
  rm {scenarios,lib,lib/tzp}/{Makefile,distrib}*
  mkdir -p "${pkgdir}/usr/share/${pkgname}"/{scenarios,lib/tzp}
  install -m755 scenarios/* "${pkgdir}/usr/share/${pkgname}/scenarios"
  install -m644 lib/tzp/* "${pkgdir}/usr/share/${pkgname}/lib/tzp"
  install -m644 lib/*.* "${pkgdir}/usr/share/${pkgname}/lib"

  # Install a binary and launchers
  install -Dm755 src/${pkgname} "${pkgdir}/usr/bin/${pkgname}-bin"
  install -m755 RunSabre "${pkgdir}/usr/bin/${pkgname}"
#  install -m755 sdlsrc/${pkgname}sdl "${pkgdir}/usr/bin/sdl${pkgname}-bin"
#  install -m755 RunSabreSDL "${pkgdir}/usr/bin/sdl${pkgname}"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 \
    doc/*.txt \
    CONTRIBUTORS COPYING HISTORY INSTALL JOYSTICK.README \
    README REQUIREMENTS TODO TROUBLE_SHOOTING WHATSNEW \
    "${pkgdir}/usr/share/doc/${pkgname}"
}
