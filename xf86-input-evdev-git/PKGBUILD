# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-evdev

_pkgname=xf86-input-evdev
pkgname=$_pkgname-git
pkgver=20121009
pkgrel=1
pkgdesc="X.Org evdev Input driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/$_pkgname/"
license=('custom')
depends=('glibc' 'systemd-tools' 'mtdev')
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=18' 'resourceproto' 'scrnsaverproto' 'git')
provides=("$_pkgname=99")
conflicts=("$_pkgname" 'xorg-server<1.13.0' 'X-ABI-XINPUT_VERSION<18' 'X-ABI-XINPUT_VERSION>=19')
options=('!libtool' '!makeflags')
groups=('xorg-drivers' 'xorg')

_gitroot=git://anongit.freedesktop.org/xorg/driver/$_pkgname
_gitname=$_pkgname

build() {
  msg "Connecting to anongit.freedesktop.org GIT server...."

  if [ -d $_gitname ] ; then
     cd $_gitname && git pull origin && cd ..
     msg "The local files are updated."
  else
     git clone --depth 1 $_gitroot
  fi

  msg "GIT checkout done or server timeout"

  msg "Starting make..."
  cd $_gitname
  ./autogen.sh --prefix=/usr
  make
}

package(){
  cd $_gitname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}