# Maintainer: Danilo Kuehn <dk at nogo-software dot de>
# Git: https://github.com/nogo/archlinux-pkgbuild
# Brackets v0.34

_shell=brackets-shell
_brackets=brackets

# Package
pkgname=brackets-git
pkgver=10678.fd686b4
pkgrel=1
pkgdesc="Adobe Brackets - An open source code editor for the web, written in JavaScript, HTML and CSS."
arch=('i686' 'x86_64')
url="http://brackets.io"
license=('MIT')
depends=('nodejs' 'gtk2' 'gtkglext' 'brackets-lib' 'gconf' 'atk' 'cairo' 'libcups' 'fontconfig' 'freetype2' )
optdepends=('chromium: for live view' 'google-chrome: for live view')
makedepends=('git' 'p7zip' 'gyp-svn' 'nodejs-grunt-cli')
provides=('brackets' 'adobe-brackets')
install=${pkgname}.install
#source=("${_shell}::git+https://github.com/adobe/brackets-shell.git#branch=irichter/fix-5294"
source=("${_shell}::git+https://github.com/adobe/brackets-shell.git"
        "${_brackets}::git+https://github.com/adobe/brackets.git")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd ${srcdir}/${_brackets}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd ${srcdir}/${_brackets}
  git submodule update --init --recursive
}

build() {
  cd ${srcdir}/${_shell}
  npm install
  grunt setup
  make
}

package() {
  cd ${srcdir}/${_shell}
  
  install -dm755 "${pkgdir}/opt/brackets"
  cp -R out/Release/lib "${pkgdir}/opt/brackets/lib"
  cp -R out/Release/locales "${pkgdir}/opt/brackets/locales"
  cp -R out/Release/node-core "${pkgdir}/opt/brackets/node-core"
  install -Dm644 out/Release/cef.pak "${pkgdir}/opt/brackets/cef.pak"
  install -Dm644 out/Release/devtools_resources.pak "${pkgdir}/opt/brackets/devtools_resources.pak"
  install -Dm755 out/Release/Brackets "${pkgdir}/opt/brackets/Brackets"
  install -Dm755 out/Release/Brackets-node "${pkgdir}/opt/brackets/Brackets-node"
  install -Dm755 installer/linux/debian/brackets "${pkgdir}/opt/brackets/brackets"
  for size in 32 48 128 256; do
    install -Dm644 "out/Release/appshell${size}.png" "${pkgdir}/opt/brackets/appshell${size}.png"
  done

  install -dm755 "${pkgdir}/usr/bin"
  ln -s /opt/brackets/brackets "$pkgdir/usr/bin/brackets"

  install -dm755 "${pkgdir}/usr/share"
  install -Dm644 installer/linux/debian/brackets.desktop "${pkgdir}/usr/share/applications/brackets.desktop"
  install -Dm644 installer/linux/debian/package-root/usr/share/icons/hicolor/scalable/apps/brackets.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/brackets.svg"
  for size in 32 48 128 256; do
    install -Dm644 "out/Release/appshell${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/brackets.png"
  done
  
  cd ${srcdir}/${_brackets}
  # Copy samples
  cp -R "samples" "${pkgdir}/opt/brackets/samples"
  # Copy www
  cp -R "src" "${pkgdir}/opt/brackets/www"
}
