# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: Sebastian Benvenuti (santodelaespada) <sebastianbenvenuti@gmail.com>

pkgname=osx-aluminium-icontheme
pkgver=1.2
pkgrel=7
pkgdesc="Mac OSX Aluminium icon theme"
url="http://3rik.free.fr/Linux/Themes/"
license=(GPL)
arch=(any)
source=(http://3rik.free.fr/Linux/Themes/Xfce/OSX_Aluminium-$pkgver.tar.bz2)
sha256sums=('595b23f8354fe1e39d533735723ef4ca582fbb39fff1c43adfe3fc9fcc259bdf')
sha512sums=('7bc62e396e3f0dfd40c758b4bfd8762637f9509fbf1e0679acbe3383a896c006384f2fb728ee308a1e0b506884ddbeedd5301a01d200de9e1465155698345a43')

build() {
    cd $srcdir/
    find -type f -exec chmod 644 '{}' \;
    cd OSX_Aluminium/128x128/apps/

    # adds in extra missing links
    install -d ../categories/
    _targets=({gnome-{session-{hibernate,suspend,reboot,halt},network-preferences,monitor,help},gnometris,system-config-securitylevel,abiword,kmail}.png)
    _links=({xfsm-{hibernate,suspend,reboot},system-shutdown,preferences-system-network,xfce4-taskmanager,"../categories/gtk-help",gnome-quadrapassel,system-log-out,abiword_48,exo-mail-reader}.png)
    # ranges from 0 to 1 less than the length of $_targets, and symlinks the links to the targets
    for i in $(seq 0 $(expr ${#_targets[*]} - 1)); do
	ln -sf ${_targets[$i]} ${_links[$i]}
    done
    for i in xfsm*; do
	ln -sf $(readlink $i) xfpm${i:4}
    done
    ln -sf $(readlink ../categories/gtk-help.png) ../gtk/gtk-help.png

    # switches reboot and grsync icons
    mv gnome-session-reboot.png TMPFILE; mv grsync.png gnome-session-reboot.png; mv TMPFILE grsync.png
}

package() {
    cd $srcdir
    install -d $pkgdir/usr/share/icons/
    cp -r OSX_Aluminium/ $pkgdir/usr/share/icons/
}
