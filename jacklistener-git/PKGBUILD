# Maintainer: M0Rf30
pkgname=jacklistener-git
pkgver=20121025
pkgrel=1
pkgdesc="Jack Listener Daemon (jack-sensing)"
arch=('i686' 'x86_64')
url="https://github.com/gentoo-root/jacklistener"
license=('GPL3')
provides=('jacklistener')
depends=('dbus')
optdepends=("jackevencmd-git: Run custom commands when headphones are (un)plugged"
	    "jacknotifier-git: Headphones Jack Notification Daemon"
	    "musicpaused-git: Pause music when headphones are unplugged"
)
makedepends=('git' 'pkgconfig')

_gitroot="git://github.com/gentoo-root/jacklistener.git"
_gitname="jacklistener"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  libtoolize
  aclocal -I m4
  autoheader
  automake --add-missing
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc --enable-systemd --enable-arch
  make
}

package() {
   cd "$srcdir/$_gitname-build"
   make DESTDIR="$pkgdir" install
   install -D init.d/jacklistener-runscript $pkgdir/usr/sbin/jacklistenerd-runscript
   install -D jacklistenerd $pkgdir/usr/lib/jacklistener/jacklistenerd
}
