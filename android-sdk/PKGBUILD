# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
pkgname=android-sdk
pkgver=r20
pkgrel=2
pkgdesc='Google Android SDK'
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
depends=('java-runtime' 'alsa-lib' 'openal' 'libstdc++5' 'libxv'
         'ncurses' 'sdl' 'swt' 'zlib')
if [[ $CARCH = x86_64 ]]; then
  depends=('java-runtime' 'lib32-alsa-lib' 'lib32-openal' 'lib32-libstdc++5'
           'lib32-libxv' 'lib32-ncurses' 'lib32-sdl' 'lib32-zlib' 'swt')
fi
install=$pkgname.install
optdepends=('android-udev: udev rules for Android devices'
            'android-sdk-platform-tools: adb, aapt, aidl, dexdump and dx')
source=(http://dl.google.com/android/${pkgname}_$pkgver-linux.tgz
        $pkgname.png
        $pkgname.desktop
        $pkgname.sh
        $pkgname.csh
        license.html
        adb.service)
sha1sums=('ab10577d91e5b66e4e40a504b2d1be90d0729767'
          'c249134310ae91dccce1ad839b3985211674ab71'
          'e834f53e13d5926ec7be3fd775fa80aa6f328eb6'
          '78f8574e651c9bf8b7515ecb30c7ef93edbc4a96'
          '08c85aab7523e22b298891c7047bc0e7adbf3437'
          'bfb91be7e0b602d765b7a1fcaf0ce1b7e1a93faa'
          '7338275fcafb9fe930139dc91f7c20891867f6d4')

package() {
  cd "$pkgdir"
  install -Dm755 "$srcdir/$pkgname.sh" etc/profile.d/$pkgname.sh
  install -Dm755 "$srcdir/$pkgname.csh" etc/profile.d/$pkgname.csh
  mkdir -p opt
  cp -a "$srcdir/$pkgname-linux" opt/$pkgname
  install -Dm655 "$srcdir/$pkgname.png" usr/share/pixmaps/$pkgname.png
  install -Dm655 "$srcdir/$pkgname.desktop" \
    usr/share/applications/$pkgname.desktop
  install -Dm644 "$srcdir/license.html" \
    usr/share/licenses/$pkgname/license.html
  install -Dm644 "$srcdir/adb.service" usr/lib/systemd/system/adb.service
  [[ $CARCH = "i686" ]] && rm -rf opt/$pkgname/tools/lib/{monitor-,}x86_64
  # Fix broken permissions
  chmod -R o=g opt/$pkgname
}
