# Maintainer: Sam Stuewe <halosghost at archlinux dot info>
# Contributor: Stefan Seering

pkgname="hashcat"
pkgver=0.46
pkgrel=1
pkgdesc='A multithreaded cross platform hash cracker.'
arch=('i686' 'x86_64')
url='http://hashcat.net/hashcat/'
license=('custom:proprietary')
makedepends=('p7zip')
depends=('glibc')
install="${pkgname}.install"
source=("https://hashcat.net/files/${pkgname}-${pkgver}.7z"
        "${install}")
sha256sums=('bf8ba2ef197199b3be156aa85fb5d085ebf5e29e76f18cf66e81830fe654e84f'
            '22d30b179f3f4e287c1e757ddf371941f1e934d0356a1a3a4fde1333dfbf7045')
noextract=("${pkgname}-${pkgver}.7z")

prepare() {
  7z x "${pkgname}-${pkgver}.7z"

  cd "${srcdir}/${pkgname}-${pkgver}"

  if [[ "$CARCH" == 'x86_64' ]]; then
    mv hashcat-cli64.bin hashcat
  else
    mv hashcat-cli32.bin hashcat
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 
  rm *.{exe,bin,app}
 
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  mkdir -p "${pkgdir}/etc/${pkgname}"
  
  install -Dm755 hashcat "${pkgdir}/usr/bin/${pkgname}"

  cp -dpr --no-preserve=ownership "${srcdir}/${pkgname}-${pkgver}"/{rules,salts,tables} "${pkgdir}"/etc/"${pkgname}"/
  cp -dpr --no-preserve=ownership "${srcdir}/${pkgname}-${pkgver}"/examples "${pkgdir}"/usr/share/doc/"${pkgname}"/

  # Needed because 7z fails to preserve the correct permissions
  cd "${pkgdir}"
  find . -type d -exec chmod 755 {} \+
  find . -type f -exec chmod 644 {} \+
  chmod a+x "${pkgdir}/usr/bin/hashcat"
}

# vim:set ts=2 sw=2 et:
