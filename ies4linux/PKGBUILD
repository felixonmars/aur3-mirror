# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jarek Sedlacek <jareksedlacek@gmail.com>

pkgname=ies4linux
pkgver=2.99.0.1
pkgrel=5
pkgdesc="The simpler way to run Microsoft Internet Explorer on Wine"
arch=(any)
url=http://www.tatanka.com.br/ies4linux/page/Main_Page
license=(GPL)
depends=(cabextract pygtk unzip wine)
options=(!emptydirs)
source=(${url/pa*}/downloads/$pkgname-$pkgver.tar.gz
    remove-wineprefixcreate.patch
    wine-version.patch)
sha256sums=('f12685793b8978b1cf418b823724382aaac238adbb6bb4721e6d001ab8afa88e'
    '123ce8ff236ccac20ed22d577fc3fca81d684e4adf78bd6223571f0163426f94'
    '39c592a8200ee7dc94072bad2f742dbfa7aab0fe98ca4adcfcfe5272da298adf')
sha512sums=('beae6f1d03cd6a004eddd7426bcc655d468aa95e3a2e7588f676cac74b94a7a4c19c27cdc64695b869e560790aaa1947bb78f3954e3bc24ef07ca1bdd43b5c21'
    '067072dca40745376d7237f928aa781a2907a4c1fd9919372f8e285423d034f28e02cb260e438d5df5f6eadcb41da4b9454fdc38b773b23f0da051d64bce43db'
    '6a05bb9ca31dd4d9718be2fce8465b9d264ebac395d541433006e9a73779e463d9de8bd7ea7e8158b9940c4ead77023286a6853eb0eb36efaae49a08702920e0')

build() {
    cd "$srcdir"/$pkgname-$pkgver/
    patch -Np1 -i ../remove-wineprefixcreate.patch
    patch -Np1 -i ../wine-version.patch
    sed -i -e '/IES4LINUX=/s|`dirname "$0"`|/usr/lib/ies4linux|' -e 's/python -c/python2 -c/' $pkgname
    sed -i 's/python /python2 /' ui/pygtk/python-gtk.sh
}

package(){
    cd "$srcdir"
    install -Dm755 $pkgname-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
    rm $pkgname-$pkgver/$pkgname
    install -d "$pkgdir"/usr/lib/
    cp -a $pkgname-$pkgver "$pkgdir"/usr/lib/$pkgname
}
