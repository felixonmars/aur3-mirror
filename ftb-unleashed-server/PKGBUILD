# Maintainer: Justin Dray <justin@dray.be>
# Previous Maintainer: Isaac C. Aronson <isaac at pingas dot org>

pkgname=ftb-unleashed-server
pkgver=1.1.7
pkgrel=1
pkgdesc="Feed the Beast Unleashed server unit files, script, and jar"
arch=('any')
url="http://www.feed-the-beast.com/"
license=('custom')
depends=('java-runtime-headless' 'screen' 'expect')
backup=("etc/conf.d/$pkgname")
options=(emptydirs)
install=install
source=(Unleashed_Server.zip::http://www.creeperrepo.net/direct/FTB2/18606e7928709218bd2a4386fb84ada9/modpacks%5EUnleashed%5E1_1_7%5EUnleashed-server.zip
        bin
        service
	config)
noextract=('Unleashed_Server.zip')
md5sums=('dc1d1badcbae224b2d0cfb347369725f'
         'fd8dc3a0a4154c459bb797813454b258'
         '003b749899919d3da08901192bd9add8'
         '13e1af3a0ee77d26122ec1bb0f98d928')
PKGEXT='.pkg.tar.gz'

build() {
	cd "$srcdir"
	unzip -qo "Unleashed_Server.zip" -d "$srcdir/$pkgname"
}

package() {
	cd "$srcdir"
	
	install -d "$pkgdir/srv/$pkgname"
	cp -r "$srcdir/$pkgname/"* "$pkgdir/srv/$pkgname/"
	install -Dm744 "$srcdir/bin" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
	install -Dm644 "$srcdir/config" "$pkgdir/etc/conf.d/$pkgname"
	rm -f "$pkgdir"/srv/$pkgname/ServerStart.{bat,sh}
}
