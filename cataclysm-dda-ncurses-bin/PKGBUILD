# Maintainer: Nick Hu <nickhu00@gmail.com>
pkgname=cataclysm-dda-ncurses-bin
pkgver=0.A
pkgrel=2
pkgdesc="Cataclysm: Dark Days Ahead is an actively maintained roguelike set in a post-apocalyptic world, ncurses only."
arch=('i686' 'x86_64')
url="http://www.cataclysmdda.com/"
license=('CCPL:by-sa')

depends=('lua51' 'sdl' 'sdl_ttf' 'sdl_image' 'gcc-libs' 'glibc' 'freetype2'
         'zlib' 'bzip2' 'libpng')
if test "$CARCH" == x86_64; then
  depends=('lib32-lua51' 'lib32-sdl' 'lib32-sdl_ttf' 'lib32-sdl_image'
          'lib32-gcc-libs' 'lib32-glibc' 'lib32-freetype2' 'lib32-zlib'
          'lib32-bzip2' 'lib32-libpng')
fi

install='cataclysm-dda.install'
source=('http://assets.cataclysmdda.com/downloads/linux_binaries/cddacurses-current.tar.gz')
md5sums=('d3e1823e2c9d749e8af20b9a8f05e504')

prepare() {
  cd "$srcdir/cataclysmdda-${pkgver}"
  sed -i 's/\x6C\x69\x62\x74\x69\x6E\x66\x6F\x2E\x73\x6F\x2E\x35/\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00/' cataclysm
  sed -i 's|DIR=.*|DIR=/usr/share/cataclysm-dda\n|' cataclysm-launcher
}

package() {
  cd "$srcdir/cataclysmdda-${pkgver}"
  install -Dm755 cataclysm-launcher "$pkgdir/usr/bin/cataclysm-dda"
  install -Dm755 cataclysm "$pkgdir/usr/share/cataclysm-dda/cataclysm"
  install -dm775 -g games "$pkgdir/usr/share/cataclysm-dda/data"
  cp -r data/* "$pkgdir/usr/share/cataclysm-dda/data"
  cp -r lang lua "$pkgdir/usr/share/cataclysm-dda"
  install -dm775 -g games "$pkgdir/var/games"
  install -dm775 -g games "$pkgdir/var/games/cataclysm-dda"
  install -dm775 -g games "$pkgdir/var/games/cataclysm-dda/save"
  ln -s "/var/games/cataclysm-dda/save" "$pkgdir/usr/share/cataclysm-dda/save"
}
