# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=solr
pkgver=4.0.0
pkgrel=1
pkgdesc="Popular, blazing fast open source enterprise search platform from the Apache Lucene project"
arch=(any)
license=('Apache')
url="http://lucene.apache.org/solr/"
depends=(java-environment)
makedepends=(unzip)
source=(
	"http://www.apache.org/dist/lucene/solr/$pkgver/apache-solr-$pkgver.tgz"
	jetty-solr.xml
	solr.service
)
install=solr.install
backup=(
	etc/solr/{web,solr,jetty}.xml
	etc/solr/zoo.cfg
	etc/solr/core{0,1}/conf/{schema,solrconfig}.xml
)

package() {
	cd "$srcdir/apache-solr-$pkgver/example"

	install -dm755 "$pkgdir/etc/solr"
	install -dm755 "$pkgdir/usr/share/solr/webapps/solr"
	install -dm755 "$pkgdir/usr/lib/solr"
	install -dm755 "$pkgdir/var/lib/solr"
	install -Dm644 start.jar lib/*.jar "$pkgdir/usr/lib/solr/"
	install -Dm644 "$srcdir/solr.service" "$pkgdir/usr/lib/systemd/system/solr.service"
	install -Dm644 "$srcdir/jetty-solr.xml" "$pkgdir/usr/share/solr/contexts/jetty-solr.xml"

	unzip webapps/solr.war -d "$pkgdir/usr/share/solr/webapps/solr/"
	rm -rf "$pkgdir/usr/share/solr/webapps/solr/META-INF"

	sed -e 's|<dataDir>.*|<dataDir>/var/lib/solr/core0</dataDir>|' \
	    -i multicore/core0/conf/solrconfig.xml
	sed -e 's|<dataDir>.*|<dataDir>/var/lib/solr/core1</dataDir>|' \
	    -i multicore/core1/conf/solrconfig.xml

	cp -R multicore/* "$pkgdir/etc/solr"

	install -Dm0644 etc/jetty.xml "$pkgdir/etc/solr/jetty.xml"
	rm -rf "$pkgdir"/etc/solr/{exampledocs,README.txt}

	mv "$pkgdir/usr/share/solr/webapps/solr/WEB-INF/web.xml" "$pkgdir/etc/solr"
	ln -s /etc/solr/web.xml "$pkgdir/usr/share/solr/webapps/solr/WEB-INF/web.xml"

	ln -s /etc/solr "$pkgdir/usr/share/solr/conf"
	ln -s /usr/lib/solr "$pkgdir/usr/share/solr/lib"
}

sha256sums=('4b7ac4fdc1f1610f2d13e8dddf310e9d61620c00c463ba24e0f385a4c5c6f3ec'
            '672b64e05b26ca3434d9ef4cf6f02b06358609b72a483340682f70a080c7770f'
            'f64a58df96f3fa5e07e6a4902797cf1691ec29686d5462c69979d4590f8b4b99')
