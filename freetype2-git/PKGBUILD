# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2-git
pkgver=20121128
pkgrel=1
pkgdesc="TrueType font rendering library (from Git)"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh')
options=('!libtool')
provides=("freetype2=2.4.10")
conflicts=("freetype2")
source=(freetype-2.3.0-enable-spr.patch
        freetype-2.4.11-enable-sph.patch
        freetype-2.2.1-enable-valid.patch)
md5sums=('816dc8619a6904a7385769433c0a8653'
         '2422f8902224fedb46ae9eec08c8ceac'
         '214119610444c9b02766ccee5e220680')

_gitroot="git://git.sv.gnu.org/freetype/freetype2.git"
_gitname="freetype2"


build() {
  if [[ -d $_gitname ]]; then
    ( cd $_gitname && git pull ) || warning "Git pull failed!"
  else
    git clone $_gitroot $_gitname
  fi

  rm -rf $_gitname-build
  git clone -s $_gitname $_gitname-build
  cd $_gitname-build

  patch -Np1 -i ../freetype-2.3.0-enable-spr.patch
  patch -Np1 -i ../freetype-2.4.11-enable-sph.patch
  patch -Np1 -i ../freetype-2.2.1-enable-valid.patch

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

check() {
  cd $_gitname-build
  make -k check
}

package() {
  cd $_gitname-build
  make DESTDIR="$pkgdir" install
}
