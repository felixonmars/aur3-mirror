# Maintainer: speps <speps at aur dot archlinux dot org>

_name=ardour
pkgname=$_name-bin
pkgver=3.0beta4a_12406
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=(i686 x86_64)
url="http://$_name.org/"
license=('GPL')
depends=('jack' 'desktop-file-utils')
options=('!strip')
install="$pkgname.install"
if [ "$CARCH" = 'i686' ]; then
_arch=_32 && md5sums=('d6a5a0a4351474329d281217ef7a08f9'); else
_arch=_64 && md5sums=('d2de2a85931842d49e2a0de0080ce01d'); fi
source=("http://$_name.org/files/${_name^}${_arch}bit-$pkgver.tar"
        $pkgname.{desktop,sh})
md5sums+=('e0f71bdc1510054106d7c9db0bd76109'
          '21886cd2d1ac140aa29fbba886deb5b5')

package() {
  cd "$srcdir/${_name^}${_arch}bit-$pkgver"

  # unpack
  install -d "$pkgdir/opt"
  bsdtar --no-same-owner -jxvf \
    ${_name^}_x86${_arch/_32}-$pkgver.tar.bz2 \
    -C "$pkgdir/opt"

  # install to /opt as does not conflict
  # with other ardour installations and libs
  cd "$pkgdir/opt"
  mv ${_name^}_x86${_arch/_32}-$pkgver $_name

  # rm uninstaller and sanity check
  rm $_name/bin/{*.uninstall.sh,sanityCheck}

  # launcher
  install -Dm755 "$srcdir/$pkgname.sh" \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
