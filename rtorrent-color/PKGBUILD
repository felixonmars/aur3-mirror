# Maintainer:   Ashren <edgar [at] archlinux [dot] us>
# Contributors:  Lucky <archlinux@builds.lucky.li> and Hleem
# Based on rtorrent [community]

pkgname=rtorrent-color
_pkgname="rtorrent"
#_pkgname="${pkgname%-*}" not supported by AUR ;)
pkgver=0.9.2
pkgrel=1
pkgdesc="Ncurses BitTorrent client based on libTorrent with color patch."
url="http://libtorrent.rakshasa.no"
arch=("i686" "x86_64")
license=("GPL")
depends=("curl" "libtorrent=0.13.2" "xmlrpc-c")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
install="${pkgname}.install"
source=("${url}/downloads/${_pkgname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}_canvas_color.patch")
sha1sums=("2a642d722745091265037ed8929a23c237a3b99f"
          "5752820e7a10df927e2e50858a60a5b2eb7e68c3")

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -uNp1 -i "${srcdir}/${_pkgname}-${pkgver}_canvas_color.patch"

  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  ./configure \
    --prefix=/usr \
    --enable-debug \
    --with-xmlrpc-c \

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 doc/faq.xml      "${pkgdir}/usr/share/doc/rtorrent/faq.xml"
  install -Dm644 doc/rtorrent.1   "${pkgdir}/usr/share/man/man1/rtorrent.1"
  install -Dm644 doc/rtorrent.rc  "${pkgdir}/usr/share/doc/rtorrent/rtorrent.rc"
}
