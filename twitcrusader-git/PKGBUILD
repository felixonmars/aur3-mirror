# Maintainer: Patryk Rzucidlo (@PTKDev) <ptkdev@gmail.com>
# Contributor: Alessio Sergi (@al3hex) <asergi@archlinux.us>
# Contributor: Alessio Biancalana (@dottorblaster) <dottorblaster@archlinux.us>
# Contributor: Andrea Scarpino (@bashlnx) <andrea@archlinux.org>

# SUPPORT:
# Follow on Twitter: @teamtwc / @PTKDev / @dottorblaster / @bashlnx / @al3hex
# IRC: chat.freenode.net at #teamtwc
# E-mail: teamtwc@twitcrusader.org

# DEPENDENCIES INFO:
# liboauth -> curl -> openssl
# gtk2 -> libxml2

pkgname=twitcrusader-git
pkgver=20110610
pkgrel=1
pkgdesc="A Twitter client for Linux (Nightly Release)"
arch=('i686' 'x86_64')
url="http://www.twitcrusader.org"
license=('GPL')
depends=('gtk2' 'liboauth')
makedepends=('git')
provides=('twitcrusader')
conflicts=('twitcrusader')

_gitroot="git://github.com/PTKDev/ClientTwitter-TwitCrusader.git"
_gitname="twitcrusader"
_gitbranch="nightly"

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname} -b ${_gitbranch}
  fi

  cd "${srcdir}/${_gitname}"

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  if [ -d "${srcdir}/${_gitname}-build" ] ; then
    rm -rf "${srcdir}/${_gitname}-build"
  fi

  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  make DESTDIR="${pkgdir}" install
}
