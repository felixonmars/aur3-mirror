# $Id$
# Maintainer: Yamashita Ren <lemaitre dot lotus at gmail dot com>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-vapoursynth
pkgver=0.7.1
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2, with vapoursynth filter'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=(
  'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libquvi' 'libgl' 'enca'
  'libxinerama' 'mpg123' 'libxv' 'libxkbcommon' 'libva' 'lirc-utils' 'wayland'
  'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua' 'libdvdnav'
  'libguess' 'portaudio' 'vapoursynth'
)
makedepends=('mesa' 'python-docutils' 'ladspa')
provides=('mpv')
conflicts=('mpv')
options=('!emptydirs' '!buildflags')
install=mpv.install
source=(https://github.com/mpv-player/mpv/archive/v$pkgver.tar.gz)
md5sums=('8112478d360135b2c78ea08dac3b9b22')

prepare() {
  cd mpv-$pkgver
  ./bootstrap.py
  sed -i 's/vendor-completions/site-functions/' wscript_build.py
}

build() {
  cd mpv-$pkgver

  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-joystick \
    --enable-zsh-comp \
    --enable-libmpv-shared \
    --enable-cdda \
    --enable-portaudio

  ./waf build
}

package() {
  cd mpv-$pkgver
  ./waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
