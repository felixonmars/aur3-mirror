# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms

_pkgname=caffeine
pkgname=$_pkgname-bzr
pkgver=466
pkgrel=2
pkgdesc="A system applet that allows you to inhibit the screensaver and the sleep power saving mode in an easy way"
arch=(any)
url=https://launchpad.net/caffeine
license=(GPL3)
depends=(dconf gtk2 hicolor-icon-theme kaa-metadata python-xlib python2-dbus python2-notify pyxdg)
makedepends=(bzr)
options=(!libtool !emptydirs)
install=$_pkgname.install
source=($_pkgname.patch)
sha256sums=('875ec25a0eafa12eb9419ad44f55d3831016d3e5d75f041a975df75eb2bbc7fa')
sha512sums=('75cd08cc605c35586ee466a9d1cddbc1fe683056f0958d906fe74b61d892fa2ad82bc6ecf478a36daaa712817c59af74722208e6ee8000dcb2533031fb37f75e')

_bzrtrunk=lp:caffeine
_bzrmod=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to Bazaar server...."
    if [[ -d $_bzrmod/.bzr ]]; then
        pushd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver
        msg2 "The local files are updated."
        popd
    else
        bzr branch $_bzrtrunk $_bzrmod -q -r $pkgver
    fi
    msg2 "Bazaar checkout done or server timeout"

    rm -rf $_bzrmod-build/
    bzr export $_bzrmod-build/ -qd $_bzrmod/
    cd $_bzrmod-build/

    # Thanks to Andr√© Ericson for the patch!
    # https://bugs.launchpad.net/caffeine/+bug/892663
    patch -Np1 -i ../$_pkgname.patch

    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    chmod +x setup.py
}

package() {
    cd "$srcdir"/$_bzrmod-build/
    ./setup.py install --root="$pkgdir"
}
