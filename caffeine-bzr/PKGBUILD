# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Fork Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms

_pkgname=caffeine
pkgname=$_pkgname-bzr
pkgver=464
pkgrel=1
pkgdesc="A system applet that allows you to inhibit the screensaver and the sleep power saving mode in an easy way"
arch=(any)
url=https://launchpad.net/caffeine
license=(GPL)
depends=(dbus-python dconf gtk2 hicolor-icon-theme kaa-metadata python-xlib python2-notify pyxdg)
makedepends=(bzr)
options=(!libtool !emptydirs)
install=$_pkgname.install
source=($_pkgname.diff)
sha256sums=('6c7927a6349f64f4784380779cd65fbd12d244ca207b4788a4922661a40aff53')
sha512sums=('6d65c64d08f3e39a1be2a45ed74990fd901c2e9c9b527f9480941fffdb81e30acf5148194b6bac87181d613757e4adcf431083d23cfae35a731842b4b7fd8b71')

_bzrtrunk=lp:caffeine
_bzrmod=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to Bazaar server...."
    if [[ -d $_bzrmod/.bzr ]]; then
        pushd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver
        msg2 "The local files are updated."
        popd
    else
        bzr branch $_bzrtrunk $_bzrmod -q -r $pkgver
    fi
    msg2 "Bazaar checkout done or server timeout"

    rm -rf $_bzrmod-build/
    bzr export $_bzrmod-build/ -qd $_bzrmod/
    cd $_bzrmod-build/

    # Thanks to Andr√© Ericson for the patch!
    # https://bugs.launchpad.net/caffeine/+bug/892663
    patch -Np1 -i ../$_pkgname.diff

    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    chmod +x setup.py
}

package() {
    cd "$srcdir"/$_bzrmod-build/
    ./setup.py install --root="$pkgdir"
}
