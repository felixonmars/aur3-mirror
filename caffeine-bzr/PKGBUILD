# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Fork Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms

_pkgname=caffeine
pkgname=$_pkgname-bzr
pkgver=466
pkgrel=1
pkgdesc="A system applet that allows you to inhibit the screensaver and the sleep power saving mode in an easy way"
arch=(any)
url=https://launchpad.net/caffeine
license=(GPL)
depends=(dconf gtk2 hicolor-icon-theme kaa-metadata python-xlib python2-dbus python2-notify pyxdg)
makedepends=(bzr)
options=(!libtool !emptydirs)
install=$_pkgname.install
source=($_pkgname.patch)
sha256sums=('d52ebeda2d1d0e3e1c6c1ea11b4f310297c5df1e37367f69bbdd41878e60f7c3')
sha512sums=('4d8d358d21b7330d2b2d067800053a05947be7ae9fc0a070809cf09e8ec3712615f1c343bfcaa7a79769cdc3a53142bc414e0f3603aefe1891a7828d3f1ddb6b')

_bzrtrunk=lp:caffeine
_bzrmod=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to Bazaar server...."
    if [[ -d $_bzrmod/.bzr ]]; then
        pushd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver
        msg2 "The local files are updated."
        popd
    else
        bzr branch $_bzrtrunk $_bzrmod -q -r $pkgver
    fi
    msg2 "Bazaar checkout done or server timeout"

    rm -rf $_bzrmod-build/
    bzr export $_bzrmod-build/ -qd $_bzrmod/
    cd $_bzrmod-build/

    # Thanks to Andr√© Ericson for the patch!
    # https://bugs.launchpad.net/caffeine/+bug/892663
    patch -Np1 -i ../$_pkgname.patch

    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    chmod +x setup.py
}

package() {
    cd "$srcdir"/$_bzrmod-build/
    ./setup.py install --root="$pkgdir"
}
