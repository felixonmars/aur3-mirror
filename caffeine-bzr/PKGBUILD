# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms

pkgname=caffeine-bzr
pkgver=2.4.1.483.338f514
pkgrel=1
pkgdesc="A system applet that allows you to inhibit the screensaver and the sleep power saving mode in an easy way"
arch=(any)
url=https://launchpad.net/caffeine
license=(GPL3)
depends=(dconf gtk2 hicolor-icon-theme kaa-metadata python2-dbus python2-gobject python2-notify
    python2-xdg python2-xlib)
makedepends=(bzr)
options=(!emptydirs !libtool)
install=$pkgname.install
source=($pkgname::bzr+http://bazaar.launchpad.net/~caffeine-developers/${pkgname%-*}/main/
    $pkgname.patch)
sha256sums=('SKIP'
    '875ec25a0eafa12eb9419ad44f55d3831016d3e5d75f041a975df75eb2bbc7fa')
sha512sums=('SKIP'
    '75cd08cc605c35586ee466a9d1cddbc1fe683056f0958d906fe74b61d892fa2ad82bc6ecf478a36daaa712817c59af74722208e6ee8000dcb2533031fb37f75e')

pkgver() {
    cd $pkgname/
    echo $(cat VERSION).$(bzr revno).$(bzr testament | awk '/sha1/ { print $2 }' | head -c7)
}

prepare() {
    cd $pkgname/
    # Thanks to Andr√© Ericson for the patch!
    # https://bugs.launchpad.net/caffeine/+bug/892663
    patch -p1 -i ../$pkgname.patch

    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    chmod +x setup.py
}

package() {
    cd $pkgname/
    ./setup.py install --root="$pkgdir"
}
