# $Id$
# Contributor:	Gergely Dar√≥czi <gergely@snowl.net>
# Based on [r-devel@AUR](http://aur.archlinux.org/packages.php?ID=52561) by Florian Breitwieser <florian.bw@gmail.com>

pkgname=r-devel-svn
pkgver=62195
pkgrel=1
pkgdesc="Language and environment for statistical computing and graphics - development version (SVN)"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('blas' 'lapack' 'bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'tk' 'libxt' 'libxmu' 'pango' 'xz')
makedepends=('gcc-fortran')
backup=('etc/R/Makeconf' 'etc/R/Renviron' 'etc/R/ldpaths' 'etc/R/repositories')
options=('!makeflags')
source=('r-devel.desktop'
	'r-devel.png')
install=r-devel.install
_svntrunk=https://svn.r-project.org/R/trunk/
_svnmod=R-devel


build() {
    svn co $_svntrunk $_svnmod
    cd ${srcdir}/$_svnmod
    ./tools/rsync-recommended
    ./configure --prefix=/opt/r-devel \
		--enable-R-shlib \
		--with-lapack \
		--with-blas \
		F77=gfortran \
		LIBnn=lib
    make
}

package() {
    cd ${srcdir}/$_svnmod
    make -j1 DESTDIR=${pkgdir} install

    # install some freedesktop.org compatibility
    install -Dm644 ${srcdir}/r-devel.desktop ${pkgdir}/usr/share/applications/r-devel.desktop
    install -Dm644 ${srcdir}/r-devel.png ${pkgdir}/usr/share/pixmaps/r-devel.png

    # move the config directory to /etc and create symlinks
    install -d ${pkgdir}/etc/$_svnmod
    cd ${pkgdir}/opt/r-devel/lib/R/etc
    for i in *; do
	mv -f ${i} ${pkgdir}/etc/$_svnmod
	ln -s /etc/$_svnmod/${i} ${i}
    done

    # links
    install -d ${pkgdir}/usr/bin
    ln -s /opt/r-devel/bin/R ${pkgdir}/usr/bin/R-devel
    ln -s /opt/r-devel/bin/Rscript ${pkgdir}/usr/bin/Rscript-devel

    # clear up stuffs
    rm -rf ${srcdir}/$_svnmod
}

md5sums=('db4aaeb54dec28f831e3fac64705d4be'
         '8862f998939a07a2391840f99ab4a4f6')
