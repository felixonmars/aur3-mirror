# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: unikum <unikum dot pm at gmail.com>
# Contributor: speed145a <jonathan at tagchapter dot com>

pkgname=firewalld
pkgver=0.2.5
pkgrel=5
pkgdesc="firewalld is a firewall service daemon that provides a dynamic customizable firewall with a D-BUS interface"
arch=('any')
url="http://fedorahosted.org/firewalld"
license=('GPL2')
depends=('dbus-python' 'iptables' 'ebtables' 'gtk2' 'hicolor-icon-theme' 'pygtk' 'python2-slip' 'python2-decorator')
makedepends=('gettext' 'gettext' 'intltool')
install=$pkgname.install
source=("http://fedorahosted.org/released/$pkgname/$pkgname-$pkgver.tar.bz2" "$pkgname")
md5sums=('f831d74efb4ca9cdddb0f577079de4ed'
         '5e15aa877b0bb1c5f93dd1c4cd435528')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=python2
  sed -i '1 s|^|#!/usr/bin/python\n|' $(find "$srcdir" -name '*.py') 
  sed -i 's|#![ ]*/usr/bin/python|#!/usr/bin/python2|' $(grep -rl '#!/usr/bin/python' "$srcdir")
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf $pkgdir/etc/rc.d/init.d
  mv "$pkgdir/etc/sysconfig" "$pkgdir/etc/conf.d"
  mv "$pkgdir/lib/systemd" "$pkgdir/usr/lib/"
  rm -rf "$pkgdir/lib"
  sed -i 's/sysconfig/conf.d/g' "$pkgdir/usr/lib/systemd/system/firewalld.service"
  install -Dm 755 "$srcdir/firewalld" "$pkgdir/etc/rc.d/firewalld"
  chmod 755 -R "$pkgdir/usr/share/icons/hicolor"
} 
