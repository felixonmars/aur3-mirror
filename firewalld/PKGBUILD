# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: unikum <unikum dot pm at gmail.com>
# Contributor: speed145a <jonathan at tagchapter dot com>

pkgname=firewalld
pkgver=0.2.10
pkgrel=4
pkgdesc="Firewall service daemon that provides a dynamic customizable firewall with a D-BUS interface"
arch=('any')
url="http://fedorahosted.org/firewalld"
license=('GPL2')
depends=('dconf' 'dbus-python' 'iptables' 'ebtables' 'gtk2' 'hicolor-icon-theme' 'pygtk' 'python2-slip' 'python2-decorator' 'python2-gobject')
makedepends=('gettext' 'intltool')
install=$pkgname.install
source=("http://fedorahosted.org/released/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('abed942a209b189b75c8b5308dfcb95a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PYTHON=python2
  sed -i '1 s|^|#!/usr/bin/python\n|' $(find "${srcdir}" -name '*.py') 
  sed -i 's|#![ ]*/usr/bin/python|#!/usr/bin/python2|' $(grep -rl '#!/usr/bin/python' "${srcdir}")
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/etc/rc.d"
  mv "${pkgdir}/etc/sysconfig" "${pkgdir}/etc/conf.d"
  mv "${pkgdir}/lib/systemd" "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
  sed -i 's|sysconfig|conf.d|g' "$pkgdir/usr/lib/systemd/system/firewalld.service"
  find "${pkgdir}" -type d | xargs chmod 755
  find "${pkgdir}" -type f | xargs chmod 644
  chmod -R 755 "${pkgdir}/usr/bin"
  chmod -R 755 "${pkgdir}/usr/sbin"
  install -dm 755 "${pkgdir}/sbin"
  ln -s /usr/sbin/iptables "${pkgdir}/sbin/iptables"
  ln -s /usr/sbin/iptables-save "${pkgdir}/sbin/iptables-save"
  ln -s /usr/sbin/iptables-restore "${pkgdir}/sbin/iptables-restore"
  ln -s /usr/sbin/ip6tables "${pkgdir}/sbin/ip6tables"
  ln -s /usr/sbin/ip6tables-save "${pkgdir}/sbin/ip6tables-save"
  ln -s /usr/sbin/ip6tables-restore "${pkgdir}/sbin/ip6tables-restore"
  ln -s /usr/sbin/xtables-multi "${pkgdir}/sbin/xtables-multi"
  ln -s /usr/sbin/ebtables "${pkgdir}/sbin/ebtables"
  ln -s /usr/sbin/ebtables-save "${pkgdir}/sbin/ebtables-save"
  ln -s /usr/sbin/ebtables-restore "${pkgdir}/sbin/ebtables-restore"
}
