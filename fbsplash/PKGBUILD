# Maintainer: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Kurt J. Bosch <kjb-temp-2009 at alpenjodel.de>

pkgname=fbsplash
pkgver=1.5.4.3
pkgrel=34
pkgdesc="A userspace implementation of a splash screen for Linux (formerly known as gensplash)"
arch=('i686' 'x86_64')
url="http://fbsplash.berlios.de"
license=('GPL')
depends=('miscsplashutils' 'freetype2' 'libjpeg' 'libpng' 'libmng' 'lcms' 'gpm') 
optdepends=('kernel26-fbcondecor: enable console background images'
            'fbsplash-extras: additional functionality like daemon icons'
            'uswsusp-fbsplash: suspend to disk with fbsplash'
            'python: convert themes from splashy to fbsplash')
conflicts=('fbsplash-scripts' 'initscripts-extras-fbsplash')
options=('!makeflags')
backup=('etc/conf.d/fbcondecor' 'etc/conf.d/splash')
install=fbsplash.install
source=(http://dev.gentoo.org/~spock/projects/gensplash/archive/splashutils-${pkgver}.tar.bz2
        fbsplash.git.patch
        splash.conf
        fbsplash-basic.sh
        fbsplash.initcpio_install
        fbsplash.initcpio_hook
        fbcondecor.daemon
        fbcondecor.conf)
md5sums=('c722cd4148817b9c50381d9bdc1ea6ef'
         '3a338c60ed0710c8b7e3e08929db521a'
         'dd6d7ecad8e953754f90de683f0d5a33'
         'e69753dac753b8ba4e2e29ef5a9c46ca'
         'e29b3db6ee059b9950febe976a0f2881'
         'ac2351bc918b101bb9249ce8940722be'
         '715926469d05eecd345c052e6248ca32'
         'b3db9d4fd902b62ac9e38589677e2d16')

build() {
  cd ${srcdir}/splashutils-${pkgver}

  # patch for building splashutils with glibc and dynamical linking
  patch -Np0 -i ${srcdir}/fbsplash.git.patch
  autoreconf

  # fix fbcondecor_ctl splash type
  sed -e 's,fbsplash_lib_init(fbspl_bootup),fbsplash_lib_init(fbspl_undef),' -i src/fbcon_decor_ctl.c

  ./configure --prefix=/usr --sysconfdir=/etc --without-klibc --enable-fbcondecor --with-gpm --with-mng --with-png --with-ttf --with-ttf-kernel
  make
}

package() {
  cd ${srcdir}/splashutils-${pkgver}

  make DESTDIR=${pkgdir} install

  cd ${pkgdir}

  # fix duplicate slashes to get splash_cache_cleanup grep to work
  sed -r -e 's,^(export spl_.*="/)/+,\1,' -i sbin/splash-functions.sh

  # fix the path to splash_util
  sed -r -e 's,^(export spl_util=)\"/bin/,\1"/sbin/,' -i sbin/splash-functions.sh

  # provide the mountpoint needed by splash-functions.sh
  mkdir -p lib/splash/{cache,tmp}

  # Install fbsplash scripts and config file
  install -D -m644 ${srcdir}/splash.conf etc/conf.d/splash
  install -D -m644 ${srcdir}/fbsplash-basic.sh etc/rc.d/functions.d/fbsplash-basic.sh
  install -D -m644 ${srcdir}/fbsplash.initcpio_install lib/initcpio/install/fbsplash
  install -D -m644 ${srcdir}/fbsplash.initcpio_hook lib/initcpio/hooks/fbsplash

  # Install fbcodecor script and config file
  install -D -m644 ${srcdir}/fbcondecor.conf etc/conf.d/fbcondecor
  install -D -m755 ${srcdir}/fbcondecor.daemon etc/rc.d/fbcondecor
}
