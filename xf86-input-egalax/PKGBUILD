# Maintainer: Padfoot <padfoot at exemail dot com dot au>
# Contributor: neolith2099 <mdovermann at ceruleanmicrosystems dot com>

pkgname=xf86-input-egalax
pkgver=2.5.1918
pkgrel=1
_reldate=20120719
pkgdesc="Touchscreen driver for eGalax (eeti) devices"
arch=('i686' 
      'x86_64')
url="http://home.eeti.com.tw/web20/eGalaxTouchDriver/linuxDriver.htm"
license=('custom:EULA')
groups=('xorg-input-drivers')
depends=('linux' 
         'libpng12' 
         'gksu' 
         'alsa-lib' 
         'hicolor-icon-theme'
         'xorg-server')
makedepends=('gtk-update-icon-cache')
backup=(etc/eGTouchL.ini)
install='xf86-input-egalax.install'
source=("eGTouch_v${pkgver}.L-x.tar.gz::http://home.eeti.com.tw/web20/drivers/touch_driver/Linux/${_reldate}/eGTouch_v${pkgver}.L-x.tar.gz" 
        "eGalaxConfig.desktop" 
        "eGTouchD")
md5sums=('fe35ac0ca54595909535e25cf00bcf0d' 
         '0ee99c83fd74bdb8357427eae9b2dc7d' 
         '50414a9d86692c88ee65b43db55d41ec')

_architect="32"
[ "$CARCH" = "x86_64" ] && _architect="64"

package() {
    mkdir -p \
        "$pkgdir/etc/X11/xorg.conf.d" \
        "$pkgdir/etc/rc.d" \
        "$pkgdir/usr/share/eGTouch/sample" \
        "$pkgdir/usr/share/applications" \
        "$pkgdir/usr/share/icons/hicolor/scalable/apps" \
        "$pkgdir/usr/share/licenses/$pkgname" \
        "$pkgdir/usr/bin" || return 1

    cd $srcdir/eGTouch_v${pkgver}.L-x/eGTouch${_architect}/eGTouch${_architect}withX

    install -D -m755 eGTouchU $pkgdir/usr/bin
    install -D -m755 eGTouchD $pkgdir/usr/bin
    install -D -m755 eCalib $pkgdir/usr/bin
    install -D -m755 eGTouchL.ini $pkgdir/etc
    
    cd ../../Rule
    
    install -D -m644 52-egalax-virtual.conf $pkgdir/etc/X11/xorg.conf.d
    install -D -m644 eGTouchU.png $pkgdir/usr/share/icons/hicolor/scalable/apps

    install -D -m755 serio_raw.sh $pkgdir/usr/share/eGTouch/sample

    cd ../Patent_Declaration

    install -D -m644 eula.pdf $pkgdir/usr/share/licenses/$pkgname
    install -D -m644 "EETI_Declaration_and_Disclaimer.pdf" $pkgdir/usr/share/licenses/$pkgname

    cd ../Guide
    
    install -D -m644 "EETI_eGTouch_Utility_Guide_for Linux_v1.03.pdf" $pkgdir/usr/share/eGTouch
    install -D -m644 "EETI_eGTouch_Linux_Programming_Guide_v2.5a.pdf" $pkgdir/usr/share/eGTouch

    install -D -m644 GetEvent.c $pkgdir/usr/share/eGTouch/sample

    cd ../../../

    install -D -m755 eGalaxConfig.desktop $pkgdir/usr/share/applications
    install -D -m755 eGTouchD $pkgdir/etc/rc.d
}