# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>
# Contributor: Ray Kohler <ataraxia937@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=veusz
pkgver=1.13
pkgrel=1
pkgdesc="A scientific plotting and graphing package, designed to create publication-ready Postscript or PDF output"
arch=('i686' 'x86_64')
url="http://home.gna.org/${pkgname}/"
license=('GPL')
depends=('python2-qt' 'python2-numpy>=1.0' 'lapack')
optdepends=('python-pyfits: for reading files in FITS format'
            'python2-emf: for EMF export')
install='veusz.install'
source=("http://download.gna.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('081eab85b51fcc7e1ad1d27cd37d53ef'
         '3823fe5961f3975a4dff2967aadaa14f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py build 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --prefix=/usr
  for _i in 16 32 48 64 128; do
    install -D -m644 "windows/icons/veusz_${_i}.png" \
        "${pkgdir}/usr/share/icons/hicolor/${_i}x${_i}/apps/veusz.png"
  done
  install -D -m644 "${srcdir}/veusz.desktop" \
      "${pkgdir}/usr/share/applications/veusz.desktop"
}


