pkgname=mingw-w64-sdl_ttf
pkgver=2.0.11
pkgrel=3
pkgdesc="A library that allows you to use TrueType fonts in your SDL applications (mingw-w64)"
arch=(any)
url="http://libsdl.org/projects/SDL_ttf"
license=("zlib")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-sdl
mingw-w64-freetype
mingw-w64-bzip2)
options=(!libtool !strip !buildflags)
source=("$url/release/SDL_ttf-$pkgver.tar.gz")
md5sums=('61e29bd9da8d245bc2471d1b2ce591aa')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/SDL_ttf-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-sdltest
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
