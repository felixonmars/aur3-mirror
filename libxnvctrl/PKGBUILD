# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=libxnvctrl
pkgver=302.17
pkgrel=1
pkgdesc="NVIDIA X Ctrl library"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('GPL2')
makedepends=('libx11' 'libxext' 'xextproto')
source=(http://us.download.nvidia.com/XFree86/nvidia-settings/nvidia-settings-$pkgver.tar.bz2)
sha1sums=('ad3ba9f931621c399c70b0d6b1cbc2c749828ad8')

build() {
  cd "nvidia-settings-$pkgver/src/libXNVCtrl"

  export CFLAGS="$CFLAGS -fPIC"

  make clean
  make
}

package() {
  cd "nvidia-settings-$pkgver/src/libXNVCtrl"

  # install libXNVCtrl and headers
  install -d -m 755 "$pkgdir"/usr/{include/NVCtrl,lib}
  install -m 644 nv_control.h NVCtrl{,Lib}.h "$pkgdir"/usr/include/NVCtrl/
  install -m 644 libXNVCtrl.a "$pkgdir"/usr/lib/libXNVCtrl.a
}

# vim:set ts=2 sw=2 et:
