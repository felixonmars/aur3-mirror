pkgname=mate-screensaver
pkgver=2011.11.06
pkgrel=1
pkgdesc='Screensaver for MATE'
arch=('i686' 'x86_64')
url="https://github.com/perberos/mate-screensaver"
license=('GPL')
depends=('gtk2' 'dbus' 'mate-conf' 'mate-desktop')
makedepends=('pkgconfig' 'intltool' 'mate-doc-utils')
options=('!libtool' '!emptydirs')
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download
		mate-screensaver.pam)
install=mate-screensaver.install
sha256sums=('1dd5cbca9481c0f92b7a3337e05b42b49fd439e3a9a94ee1477046ae19fa884b'
            '2744d1fc39da46fc681b7cf1c9230d2035b2bb8d9f510213fbe439c114eb76a2')

build() {
	# this is a hack for tarballs on github
	cd ${pkgname}

	./autogen.sh \
		--prefix=/usr --sysconfdir=/etc \
		--mandir=/usr/share \
		--disable-static \
		--with-xscreensaverdir=/usr/share/xscreensaver/config \
		--with-xscreensaverhackdir=/usr/lib/xscreensaver \
		--with-mit-ext \
		--libexecdir=/usr/lib/${pkgname} || return 1

	make || return 1
}

package() {
	# this is a hack for tarballs on github
	cd ${pkgname}

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
	
	install -m644 "${srcdir}/mate-screensaver.pam" "${pkgdir}/etc/pam.d/mate-screensaver"
	
	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
