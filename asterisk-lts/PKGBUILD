# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=asterisk-lts
pkgver=1.8.15.0
pkgrel=1
pkgdesc="A complete PBX solution (Long Term Support)"
arch=('i686' 'x86_64')
groups=()
url="http://www.asterisk.org"
license=('GPL2')
depends=('libusb' 'libxml2' 'openssl' 'curl' 'gmime' 'iksemel' 'libldap' 'net-snmp' 'postgresql-libs' 'speex' 'sqlite' 'libcap' 'libvorbis' 'unixodbc' 'libnewt' 'libresample' 'alsa-lib' 'popt' 'lua' 'neon' 'jack' 'sdl' 'gsm' 'dahdi' 'libpri')
optdepends=('radiusclient-ng' 'dahdi-tools')
makedepends=('linux-headers')
conflicts=('asterisk' 'asterisk16' 'asterisk18' 'asterisk10' 'asterisk-svn')
provides=('asterisk')
options=()
install=${pkgname}.install
source=(http://downloads.asterisk.org/pub/telephony/asterisk/asterisk-${pkgver}.tar.gz
  asterisk
  asterisk-lts.logrotated)
sha1sums=('416b611b560cfcf6cab3e8769180493149f67960'
  '19ca30d5dde7ba90e56c21abfe4fb9fcf2529e98'
  '38a772e20c9a1987faf433ca38c8536012fbada0')
build() {
  cd $srcdir/asterisk-${pkgver}

  msg "Configuring..."
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var

  msg "Building..."
  make
}

package() {
  cd ${srcdir}/asterisk-${pkgver}
  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} samples
  cd ${pkgdir}/etc/asterisk/ && for i in *; do mv $i $i.sample; done
  install -D -m 755 ${srcdir}/asterisk ${pkgdir}/etc/rc.d/asterisk
}

# vim:set ts=2 sw=2 et:
