# Contributor: Greg Fitzgerald <greg@gregf.org>

pkgname=gpodder3
_pkgname=gpodder
pkgver=3.2.0
pkgrel=2
pkgdesc='A podcast receiver/catcher'
license=('GPL3')
arch=('any')
url='http://gpodder.org/'
conflicts=('gpodder' 'gpodder2' 'gpodder-git')
depends=('pygtk' 'python2-feedparser' 'dbus-python' 'python2-mygpoclient')
makedepends=('intltool' 'imagemagick' 'help2man')
optdepends=('libgpod: for ipod support'
            'pymad: for ipod support'
            'mplayer: for ipod video support'
            'python-imaging: rockbox cover art support'
            'vorbis-tools: for ogg tag updating and converting to mp3'
            'lame: for converting ogg to mp3'
            'gnome-bluetooth: transfer podcast via bluethooth'
            'pywebkitgtk: render shownotes'
            'python-eyed3: for ipod support')
            # pymtp: for mtp support (AUR only)
source=(${_pkgname}-${pkgver}.tar.gz::http://${_pkgname}.org/source/${pkgver})
md5sums=('29587ee5d88c9802a862ec41686bdd52')
install=gpodder.install

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  PYTHON=python2 make DESTDIR=${pkgdir}/ install

  sed -i -e 's|#!/usr/bin/python$|#!/usr/bin/python2|' \
         -e 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' \
    $(find $pkgdir/usr/lib/python2.7/site-packages/gpodder/ -name '*.py')
}
