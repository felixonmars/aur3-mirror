# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>

_gemname=chef
pkgname=ruby-$_gemname
pkgver=10.14.4
pkgrel=1
pkgdesc="A systems integration framework, built to bring the benefits of configuration management to your entire infrastructure."
arch=(any)
url="http://wiki.opscode.com/display/chef"
license=('GPL')
depends=('ruby'
  'ruby-bunny>0.6.0'
  'ruby-erubis'
  'ruby-highline'
  'ruby-mixlib-log>=1.3.0'
  'ruby-mixlib-authentication>=1.1.0'
  'ruby-mixlib-cli>=1.1.0'
  'ruby-mixlib-shellout'
  'ruby-moneta'
  'ruby-net-ssh-2.2'
  'ruby-net-ssh-multi>=1.1'
  'ruby-ohai>=0.6.0'
  'ruby-rest-client<1.7.0'
  'ruby-treetop>=1.4.9'
  'ruby-uuidtools'
  )
makedepends=(rubygems)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
md5sums=('afb959e7766894a431f7591260d428e0')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"
}

# vim:set ts=2 sw=2 et:
