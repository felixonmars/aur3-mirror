# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Matthias Maennich <arch@maennich.net>
pkgname=shiboken
pkgver=1.1.1
pkgrel=1
_pyver=2.7
pkgdesc="Support library for Python bindings"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.pyside.org"
depends=("python2>=${_pyver}" "qt>=4.8" 'openssl')
optdepends=("python-sphinx: Documentation")
makedepends=('cmake' 'automoc4' 'generatorrunner>=0.6.16')
conflicts=("libshiboken")
provides=("libshiboken" "shiboken")
source=("http://www.pyside.org/files/shiboken-$pkgver.tar.bz2")
md5sums=('fa451b6c4f3e06cce283a84550a96fd2')

build(){
    cd $srcdir/shiboken-$pkgver
    mkdir -p build && cd build
    cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
              -DCMAKE_BUILD_TYPE=Release   \
              -DBUILD_TESTS=OFF            \
              -DPYTHON_EXECUTABLE=/usr/bin/python${_pyver}
    make
}

package(){
    cd $srcdir/shiboken-$pkgver/build
    make DESTDIR=$pkgdir install

    cd generator
    install -D -m 755 ../data/ShibokenConfig.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -D -m 755 ../data/ShibokenConfigVersion.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -D -m 755 ../data/ShibokenConfig-python${_pyver}.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -D -m 755 ../data/shiboken.pc $pkgdir/usr/lib/pkgconfig/
}

