# Maintainer: OmeGa <omega [U+0040] mailoo [.] org>
# Based on https://aur.archlinux.org/packages/scid_vs_pc

pkgname=scid-vs-pc-svn
pkgver=r1702
pkgrel=1
pkgdesc="A usability and bug-fix fork of Shane's Chess Information Database (SVN version)"
arch=('i686' 'x86_64')
url="http://scidvspc.sourceforge.net/"
license=('GPL')
depends=('snack' 'tdom-git' 'tkimg')
makedepends=('svn')
conflicts=('scid' 'scid_vs_pc')
install=scid-vs-pc.install
source=("$pkgname"::'svn+svn://svn.code.sf.net/p/scidvspc/code'
        'scid'
        'scid-vs-pc.desktop'
        'scidlet')
sha1sums=('SKIP'
          'fa8f37281becfda890256ec409046a51c8d9f3ff'
          '8614d5471456a718eeff41ff3b198fb0972c8d43'
          'e5938454f8c29e85d94bdf6d23b2b4adede5ffc2')

pkgver() {
  cd "$srcdir/$pkgname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "$srcdir/$pkgname"

  # Fix for tcl 8.6
  sed -i 's/8.5/8.6/g' configure
    
  ./configure BINDIR=/usr/bin/ \
    SHAREDIR=/usr/share/scid/ \
    TCL_VERSION="8.6" \
    OPTIMIZE="-O2 -funroll-loops"
}

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname"
   
  # Directories
  install -d "$pkgdir/usr/bin"
  install -d "$pkgdir"/usr/share/scid/{bases,bin,bitmaps,books,data,html,sounds}
  install -d "$pkgdir/usr/share/fonts/scid"
     
  # Data
  install -m755 scid sc_* scidpgn pgnfix spliteco pgnscid tkscid tcscid \
    scmerge scidlet* scidt "$pkgdir/usr/share/scid/bin"
  install -m644  scid.eco "$pkgdir/usr/share/scid/data"
  install -m644  spelling.ssp "$pkgdir/usr/share/scid"

  # Start files
  install -m755 "$srcdir"/{scid,scidlet} "$pkgdir/usr/bin"
 
  # Opening books
  install -m666  books/* "$pkgdir/usr/share/scid/books"

  # Sounds
  install -m644 sounds/* "$pkgdir/usr/share/scid/sounds"
  
  # Bases
  install -m666  bases/* "$pkgdir/usr/share/scid/bases"

  # Bitmaps
  install -m644 bitmaps/* "$pkgdir/usr/share/scid/bitmaps"
  
  # Docs
  cp -R html/* "$pkgdir/usr/share/scid/html"

  # Timeseal
  install -Dm755 timeseal/timeseal.Linux-i386 "$pkgdir/usr/share/scid/timeseal/timeseal-linux"

  # Engines
  install -m755 engines/toga/src/fruit "$pkgdir/usr/bin"
  install -m755 engines/phalanx/phalanx "$pkgdir/usr/bin"
  install -Dm644 engines/phalanx/pbook.phalanx "$pkgdir/usr/share/scid/engines/phalanx/pbook.phalanx"
  install -Dm666 engines/phalanx/eco.phalanx "$pkgdir/usr/share/scid/engines/phalanx/eco.phalanx"

  # Fonts
  install -m644 fonts/*.ttf "$pkgdir/usr/share/fonts/scid"

  # Desktop icon
  install -Dm644 icons/scid.png "$pkgdir/usr/share/pixmaps/scid.png"
  install -Dm644 "$srcdir/scid-vs-pc.desktop" "$pkgdir/usr/share/applications/scid-vs-pc.desktop"
}

# vim:set ts=2 sw=2 et:
