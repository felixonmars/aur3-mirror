# Maintainer: Chad Versace <chad.versace@linux.intel.com>
pkgname='waffle-1'
pkgver='0.1'
pkgrel=1
pkgdesc='a library for choosing window system and OpenGL API at runtime'
arch=('i686' 'x86_64')
url='http://people.freedesktop.org/~chadversary/waffle'
license=('BSD')

depends=(
  'libgl' # for GLX
  'libegl'
  'libx11'
  'libxcb'
  )
makedepends=(
  'cmake'
  'xcb-proto'
  )
source=("http://people.freedesktop.org/~chadversary/waffle/files/release/$pkgname.$pkgver/$pkgname.$pkgver.tar.xz")
sha256sums=('295a2884e0e28a28405df05f14b4c722a261078d6730bc524a0dcebbaca30ab9')

build() {
  cd "$srcdir/$pkgname.$pkgver"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -Dwaffle_has_glx=1 \
    -Dwaffle_has_x11_egl=1 \
    -Dwaffle_has_wayland=0 \
    -Dwaffle_build_examples=0
  make
}

package() {
  cd "$srcdir/$pkgname.$pkgver"
  make DESTDIR="$pkgdir/" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$pkgdir/usr/share/doc/waffle-1/LICENSE.txt" \
                "$pkgdir/usr/share/licenses/waffle-1/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
