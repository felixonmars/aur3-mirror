# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kdenlive-frameworks-git
pkgver=v0.9.10.182.gdb7214d
pkgrel=1
pkgdesc="A non-linear video editor for Linux using the MLT video framework. KF5 Frameworks (GIT Version)"
arch=('i686' 'x86_64')
url="http://http://www.kdenlive.org/"
license=('GPL')
depends=('knewstuff' 'kplotting' 'knotifyconfig' 'glu' 'mlt-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'v4l-utils')
optdepends=('ffmpeg: for FFmpeg plugin'
            'cdrkit: for creation of DVD ISO images'
            'dvdauthor: for creation of DVD'
            'dvgrab: for firewire capture'
            'libdv: for webcam capture (if FFmpeg is not installed)'
            'recordmydesktop: for screen capture'
            'xine-ui: for DVD preview')
provides=('kdenlive')
conflicts=('kdenlive')
source=("git://anongit.kde.org/kdenlive#branch=frameworks")
md5sums=('SKIP')
install=kdenlive-frameworks-git.install
_gitname="kdenlive"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare(){
  mkdir -p build
}

build() {

  cd build
  cmake "../${_gitname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
}

