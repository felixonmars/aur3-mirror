# Maintainer: SrÄ‘an Tot

pkgname=weston-rbp
pkgver=1.1.0
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor for Raspberry Pi'
arch=('armv6h')
url='http://wayland.freedesktop.org'
license=('MIT')
options=(!libtool)
depends=('libxkbcommon' 'libunwind' 'wayland' 'mesa' 'cairo' 'poppler-glib' 'mtdev' 'libxcursor' 'glu'
         'pango' 'xkeyboard-config')
conflicts=('weston')
provides=('weston')
source=("http://wayland.freedesktop.org/releases/weston-$pkgver.tar.xz"
	"bcm_host.pc"
	"egl.pc"
	"glesv2.pc")
sha1sums=('0929afa86d795e0e970936623d86bdd950d48e8d'
	  'f40a7a2e5ea69e4ec0327e3ba1d1fc31d70b01b0'
	  'dc64eb1bc41a85f32ddea5c006ae3181bd8222bb'
	  '01c011605b0590c4aa140cff1bf273cb03b1cfeb')

build() {
	cd weston-$pkgver
	PKG_CONFIG_PATH="$srcdir" ./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/weston \
		--disable-setuid-install \
		--with-cairo-glesv2 \
		--disable-drm-compositor \
		--disable-wayland-compositor \
		--disable-weston-launch \
		--disable-fbdev-compositor \
		WESTON_NATIVE_BACKEND="rpi-backend.so"
	make
}

package() {
	cd weston-$pkgver
	make DESTDIR="$pkgdir" install
	# license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/weston/COPYING"
	# embed more clients
	for _c in calibrator clickdot cliptest dnd editor eventdemo flower image \
	 resizor simple-egl simple-shm simple-touch smoke transformed view; do
		install -Dm755 "clients/$_c" "$pkgdir/usr/bin/weston-$_c"
	done
	for _c in info keyboard screenshooter tablet-shell terminal; do
		install -Dm755 "clients/weston-$_c" "$pkgdir/usr/bin/weston-$_c"
	done
}
