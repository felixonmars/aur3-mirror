# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=libtxc_dxtn
pkgname=lib32-$_pkgbasename
pkgver=070518
pkgrel=2
arch=(x86_64)
pkgdesc="Texture compression library for Mesa (32-bit)"
url="http://homepage.hispeed.ch/rscheidegger/dri_experimental/s3tc_index.html"
license=(custom:BSD)
depends=(lib32-mesa)
makedepends=(gcc-multilib)
source=(http://pkgbuild.com/~heftig/${_pkgbasename}${pkgver}.tar.gz)
md5sums=('03beb907c13df6484cde210ce219f4b8')

build() {
  cd "$srcdir/$_pkgbasename"
  sed -i 's:usr/lib:usr/lib32:g' Makefile
  make CC="gcc -m32"
}

package() {
  cd "$srcdir/$_pkgbasename"
  make DESTDIR="$pkgdir" install

  # License
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '5,22{s|^ \* \?||;p}' txc_dxtn.h \
    > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
