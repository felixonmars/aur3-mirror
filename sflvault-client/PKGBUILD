# Maintainer: Sandy Carter <bwrsandman@gmail.com>
# Contributor: Gr√©gory Starck <gregory.starck@savoirfairelinux.com>

pkgname=sflvault-client
pkgver=0.8.0
pkgrel=3
pkgdesc="SFLvault is a Networked credentials store and authentication manager"
arch=(i686 x86_64)
url="http://www.sflvault.org"
license=('GPL')

depends=('python2-pysqlite' 'python2-simplejson' 'python2-crypto' 'python2-pexpect'
         'python2-urwid' 'python2-pylons' 'python-webhelpers' 'python2-paste-script' 
         'ipython2' 'python2-sqlalchemy' 'sflvault-common')
source=(https://github.com/savoirfairelinux/${pkgname%-client}/archive/v${pkgver}-rc1.tar.gz
        'python-2.7.9.patch')
sha256sums=('497e0f14a07de4d766071bb4aaa060a10baf7c57e509c969bc5f05def0a6869b'
            '90ebf9677573af293a32974e27765da6eb94111eda6e456939540a185f0dbb49')

prepare() {
  cd ${srcdir}/${pkgname%-client}-${pkgver}-rc1/
  sed -i 's,pexpect-u==2.5.1,pexpect==3.1,g' requirements.freeze
  sed -i 's,pexpect-u,pexpect,g' client/setup.py
  patch -p0 < ${srcdir}/python-2.7.9.patch
}

package() {
  cd ${srcdir}/${pkgname%-client}-${pkgver}-rc1/client
  python2 ./setup.py install --prefix=/usr --root="${pkgdir}" -O1
}
