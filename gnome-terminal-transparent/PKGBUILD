# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Boyan Ding <stu_dby@126.com>

pkgname=gnome-terminal-transparent
_realname=gnome-terminal
pkgver=3.14.2
pkgrel=1
pkgdesc="The GNOME Terminal Emulator - Applied patch to allow transparent background"
arch=(i686 x86_64)
license=(GPL)
provides=('gnome-terminal')
conflicts=('gnome-terminal')
depends=(vte3 gsettings-desktop-schemas dconf)
makedepends=(gnome-doc-utils intltool itstool docbook-xsl desktop-file-utils
             libnautilus-extension appdata-tools gnome-shell gconf vala)
optdepends=('gconf: settings migration when upgrading from older version')
options=('!emptydirs')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-terminal-transparent.install
source=(http://ftp.gnome.org/pub/gnome/sources/$_realname/${pkgver:0:4}/$_realname-$pkgver.tar.xz
        '0001-Restore-transparency-gnome-3-14.patch')
sha256sums=('886bf9accb863d59791c5d8f2078d0fb022245a79854ad4a131b7b2186c27d2b'
	    'ae62bc139a40066d624cee13befd0a0a747fed5e3acfb98782ab7a31796f2510')

prepare() {
  cd $_realname-$pkgver
  patch -Np1 -i ../0001-Restore-transparency-gnome-3-14.patch
}

build() {
  cd $_realname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static --with-nautilus-extension
  make
}

check() {
  cd $_realname-$pkgver
  make check
}

package() {
  cd $_realname-$pkgver
  make DESTDIR="$pkgdir" install
}
