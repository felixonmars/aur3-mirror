# Maintainer: mathieui <mathieui[at]mathieui.net>
# Contributor: Baptiste Jonglez <zerstorer at free dot fr>

pkgname=poezio-git
pkgrel=1
pkgver=20120325
pkgdesc="A full-featured command-line irc-like (muc-centered) jabber client (git)"
arch='any'
url="http://poezio.eu"
replaces=('poezio' 'poezio-hg')
provides=('poezio' 'poezio-hg')
license=('zlib')
depends=('python' 'python-sleekxmpp-poezio')
makedepends=('git')
optdepends=('python-dnspython: SRV records support (also, gmail)'
            'libopenotr-git: OTR plugin'
            'gnupg: GPG plugin'
            'figlet: ASCII art plugin')

_gitroot="http://git.louiz.org/poezio"
_gitname="poezio"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" prefix=/usr install
}
