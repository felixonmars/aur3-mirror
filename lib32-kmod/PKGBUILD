# Maintainer: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=lib32-kmod
pkgver=8
pkgrel=1
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL2')
depends=('lib32-zlib' 'kmod')
makedepends=('gcc-multilib')
options=('!libtool')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/kmod/kmod-$pkgver.tar.xz"
	'0001-split-usr-read-configs-from-lib-depmod.d-modprobe.d.patch'
	'0002-config-hardcode-the-path-to-modules-to-be-lib-module.patch')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/kmod-${pkgver}"

  patch -p1 -i ../0001-split-usr-read-configs-from-lib-depmod.d-modprobe.d.patch
  patch -p1 -i ../0002-config-hardcode-the-path-to-modules-to-be-lib-module.patch

  ./configure \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-tools \
    --with-zlib \
    --with-rootprefix=/usr

  make
}

package() {
  cd "${srcdir}/kmod-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # nuke manpages and headers
  rm -rf "${pkgdir}"/usr/{share,include,bin}
}


md5sums=('d4e3d038b5370b1e8ff237c55666aa64'
         'cfa4125b3f60fb27c0e7f6361b5d03a4'
         '434fc9e6ab4b95e84a7838685850a2ca')
