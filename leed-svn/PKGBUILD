# Maintainer: Tetsumaki <http://goo.gl/YMBdA>

pkgname=leed-svn
pkgver=99
pkgrel=1
pkgdesc="Leed (Light Feed) is a free RSS aggregator and minimalist consultation allows RSS feeds quickly and non-intrusively. Very similar to Google Reader, Tiny Tiny RSS, rssLounge and selfoss but leed use the principles of philosophy kiss"
arch=('any')
url="http://projet.idleman.fr/leed/"
license=('CC by-nc-sa')
depends=('php' 'mysql' 'wget')
makedepends=('subversion')
optdepends=('apache: is depedency if you do not use lighttpd or nginx'
            'lighttpd'
            'nginx')
install=${pkgname}.install

_svntrunk=http://projet.idleman.fr/leed.svn/
_svnmod=leed

build() {
	cd "${srcdir}"
	msg "Connecting to SVN server...."

	if [[ -d "${_svnmod}/.svn" ]]; then
		cd "${_svnmod}" && svn up -r ${pkgver}
	else
		svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
	fi

	msg "SVN checkout done or server timeout"
	msg "Starting build..."

	rm -rf "${srcdir}/${_svnmod}-build"
	svn export "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"

	if [ -e "${srcdir}/${_svnmod}-build/logs" ]; then
		cd "${srcdir}/${_svnmod}-build/logs"
		rm -rf *
	fi
}

package() {
	install -dm755 "${pkgdir}/usr/share/webapps/${_svnmod}"
	cd "${srcdir}/${_svnmod}-build"
	cp -rf * "${pkgdir}/usr/share/webapps/${_svnmod}"
	chmod -R 755 "${pkgdir}/usr/share/webapps/${_svnmod}"
}
