pkgname=grads
pkgver=2.0.2
pkgrel=5
pkgdesc="The Grid Analysis and Display System (GrADS) is an interactive \
desktop tool that is used for easy access, manipulation, and visualization \
of earth science data. The format of the data may be either binary, GRIB, \
NetCDF, or HDF-SDS (Scientific Data Sets)."
url="http://www.iges.org/grads"
license="GPL"
depends=(netcdf libx11 udunits gd libdap)
makedepends=(libjpeg libpng shapelib jasper libtiff libgeotiff g2clib hdf4-nonetcdf gadap)
arch=(i686 x86_64)
provides=(grads)
conflicts=(grads)
source=(ftp://cola.gmu.edu/grads/2.0/${pkgname}-${pkgver}-src.tar.gz time_unit.patch)
md5sums=('d6ed37066fa2d78e5582df5193917a7d'
         '4267574ffb319dfef225a923fa7d6975')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 -i ${srcdir}/time_unit.patch
  sed -i configure -e 's/png12/png/g'
  sed 's!/usr/local/lib/grads!/usr/share/grads!' -i src/gx.h
  sed 's!/usr/local/lib/grads!/usr/share/grads!' -i src/gxsubs.c
  mkdir -p supplibs/{bin,lib,include}
  mkdir -p supplibs/include/shp
  ln -sf /usr/include/libshp/shapefil.h supplibs/include/shp
  ln -sf /usr/lib/libshp.a supplibs/lib
  ln -sf /usr/lib/libgd.so supplibs/lib/libgd.a
  ln -sf /usr/lib/libgrib2c.a supplibs/lib
  ln -sf /usr/lib/libjasper.a supplibs/lib
  ln -sf /usr/lib/libpng.so supplibs/lib/libpng.a
  ln -sf /usr/lib/libz.a supplibs/lib
  ln -sf /usr/lib/libtiff.a supplibs/lib
  ln -sf /usr/lib/libgeotiff.a supplibs/lib
  ln -sf /usr/lib/libjpeg.a supplibs/lib
  ln -sf /usr/lib/libmfhdf.a supplibs/lib
  ln -sf /usr/lib/libdf.a supplibs/lib
  ln -sf /usr/lib/libsz.a supplibs/lib
  ln -sf /usr/lib/liblzma.a supplibs/lib
  ln -sf /usr/lib/libgadap.a supplibs/lib
  ln -sf /usr/lib/libdapclient.so supplibs/lib/libdapclient.a
  ln -sf /usr/lib/libdap.so supplibs/lib/libdap.a
  ln -sf /usr/lib/libxml2.so supplibs/lib/libxml2.a
  ln -sf /usr/lib/libudunits2.so supplibs/lib/libudunits.a
  ln -sf /usr/lib/libnetcdf.so supplibs/lib/libnetcdf.a
  ln -sf /usr/lib/libhdf5.so supplibs/lib/libhdf5.a
  ln -sf /usr/lib/libhdf5_hl.so supplibs/lib/libhdf5_hl.a
  ln -sf /usr/lib/libcurl.so supplibs/lib/libcurl.a
  ln -sf /usr/bin/nc-config supplibs/bin/nc-config
  CFLAGS="$CFLAGS -Wno-trigraphs" \
  ./configure --enable-dyn-supplibs --prefix=/usr 
  sed -i src/Makefile -e 's/\$(supp_lib_dir)\/libtiff.a/\$(supp_lib_dir)\/libtiff.a \$(supp_lib_dir)\/liblzma.a/'
  sed -i src/Makefile -e 's/-lgd/-lgd -lpng -ljpeg/'
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make prefix=${pkgdir}/usr install
  mkdir -p ${pkgdir}/usr/share/grads
  cp -a data/* ${pkgdir}/usr/share/grads
}
