# Maintainer: Federico Cinelli <cinelli.federico@gmail.com>

pkgname=audacious-gtk2
_pkgalias=audacious
pkgver=3.2.4
pkgrel=1
pkgdesc='Lightweight, GTK-based advanced audio player focused on audio quality'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL3')
depends=('gtk2' 'dbus-glib' 'libguess' 'libsm' 'desktop-file-utils'
         'hicolor-icon-theme')
optdepends=('audacious-plugins-gtk2: proper functioning'
            'unzip: zipped skins support')
conflicts=('audacious')
install=${_pkgalias}.install
source=("http://distfiles.audacious-media-player.org/${_pkgalias}-${pkgver}.tar.bz2")
md5sums=('7d1cabdaf5188abb4deddf628535a955')

build()
{
    cd "${srcdir}/${_pkgalias}-${pkgver}"
    LDFLAGS="${LDFLAGS} -Wl,--copy-dt-needed-entries"
    ./configure --prefix=/usr --disable-gtk3
    make
}

package()
{
    cd "${srcdir}/${_pkgalias}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
