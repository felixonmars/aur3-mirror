# $Id: PKGBUILD 188683 2013-06-17 19:05:53Z andrea $
# Maintainer: Yichao Yu <yyc1992@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com> Ben <ben@benmazer.net>

pkgbase=pyqt5
pkgname=pyqt5
true && pkgname=('pyqt5-common' 'python-pyqt5' 'python2-pyqt5')
pkgver=5.0
pkgrel=2
arch=('i686' 'x86_64')
url="http://riverbankcomputing.co.uk/software/pyqt/intro"
license=('GPL')
_qt5_module_depends=(
  'qt5-multimedia' # QtMultimedia
  'qt5-tools' # QtHelp, QtDesigner
  'qt5-svg' # QtSvg
  'qt5-webkit' # QtWebKit, QtWebKitWidgets
  'qt5-xmlpatterns' # QtXmlPatterns
)
makedepends=('python-sip' 'python2-sip' 'mesa'
             'python-opengl' 'python2-opengl' 'python2-dbus' 'python-dbus'
             'qt5-base'
             # QtGui, QtNetwork, QtOpenGL, QtPrintSupport, QtSql, QtTest,
             # QtWidgets, QtDBus
             "${_qt5_module_depends[@]}")
source=("http://downloads.sourceforge.net/project/pyqt/PyQt5/PyQt-${pkgver}/PyQt-gpl-${pkgver}.tar.gz"
  dbus-mainloop.patch)
md5sums=('771b3d680a905ab5b6a13fa8707dc70a'
         '38cc8201e52ec98f19208745baf10b86')
options=('debug' 'strip')

build() {
  cd PyQt-gpl-${pkgver}
  patch -Np1 < ../dbus-mainloop.patch

  cd ..
  cp -r PyQt-gpl-${pkgver} Py2Qt-gpl-${pkgver}

  cd PyQt-gpl-${pkgver}
  python3 configure.py \
    --confirm-license \
    --qsci-api \
    -v /usr/share/sip/PyQt5/ \
    -q /usr/lib/qt/bin/qmake

  # Thanks Gerardo for the rpath fix
  find -name 'Makefile' | \
    xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make

  ### Python2 version ###
  cd ../Py2Qt-gpl-${pkgver}
  python2 configure.py \
    --confirm-license \
    --qsci-api \
    -v /usr/share/sip/PyQt5/ \
    -q /usr/lib/qt/bin/qmake

  # Thanks Gerardo for the rpath fix
  find -name 'Makefile' | \
    xargs sed -i 's|-Wl,-rpath,/usr/lib||g;s|-Wl,-rpath,.* ||g'

  make
}

package_pyqt5-common(){
  pkgdesc="Common PyQt files shared between python-pyqt5 and python2-pyqt5"
  depends=('qt5-base')

  cd PyQt-gpl-${pkgver}

  rm -rf tmp_pkg
  make DESTDIR="${PWD}/tmp_pkg" INSTALL_ROOT="${PWD}/tmp_pkg" install
  install -dm755 "${pkgdir}/usr/share/sip"
  mv "${PWD}/tmp_pkg/usr/share/sip/PyQt5" "${pkgdir}"/usr/share/sip
  rm -rf tmp_pkg

  make -C pyrcc DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
  make -C pylupdate DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  install -Dm644 PyQt5.api "${pkgdir}"/usr/share/qt/qsci/api/python/PyQt5.api
}

package_python-pyqt5(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=('python-sip' 'pyqt5-common'
    'python>=3.3' 'python<3.4')
  optdepends=('python-opengl: enable OpenGL 3D graphics in PyQt applications'
    "${_qt5_module_depends[@]}"
    'python-dbus: for python-dbus mainloop support')

  cd PyQt-gpl-${pkgver}
  # INSTALL_ROOT is needed for the QtDesigner module,
  # the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  # Provided by pyqt-common
  rm "${pkgdir}"/usr/bin/{pylupdate5,pyrcc5}
  rm "${pkgdir}"/usr/share/qt/qsci/api/python/PyQt5.api
  rm -rf "${pkgdir}"/usr/share/sip
}

package_python2-pyqt5(){
  pkgdesc="A set of Python 2.x bindings for the Qt5 toolkit"
  depends=('python2-sip' 'pyqt5-common' 'python2>=2.7'
    'python2<2.8' # ... well, there should be one though...
  )
  optdepends=('python2-opengl: enable OpenGL 3D graphics in PyQt applications'
    "${_qt5_module_depends[@]}"
    'python2-dbus: for python2-dbus mainloop support')

  cd Py2Qt-gpl-${pkgver}
  # INSTALL_ROOT is needed for the QtDesigner module,
  # the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  # Fix conflicts with python-pyqt5
  mv "${pkgdir}"/usr/bin/{,python2-}pyuic5

  rm "${pkgdir}"/usr/bin/{pylupdate5,pyrcc5}
  rm "${pkgdir}"/usr/lib/qt/plugins/designer/libpyqt5.so
  rm "${pkgdir}"/usr/share/qt/qsci/api/python/PyQt5.api
  rm -rf "${pkgdir}"/usr/share/sip
}
