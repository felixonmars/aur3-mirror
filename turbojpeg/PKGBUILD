# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=turbojpeg
pkgver=1.1.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://www.libjpeg-turbo.org/About/TurboJPEG"
license=('GPL' 'custom')
depends=('glibc')
makedepends=('nasm')
options=('!libtool')
source=(http://sourceforge.net/projects/libjpeg-turbo/files/$pkgver/libjpeg-turbo-$pkgver.tar.gz)
md5sums=('03b9c1406c7bfdc204313c2917ce6962')

build() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  ./configure --prefix=/usr --without-jpeg8 --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  make DESTDIR="$pkgdir/" install
  
  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h $pkgdir/usr/include/
  
  # distribute libturbojpeg
  rm -rf $pkgdir/usr/bin
  rm -rf $pkgdir/usr/share
  rm $pkgdir/usr/include/j*.h
  rm $pkgdir/usr/lib/libj*
  
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/turbojpeg/LICENSE"
}

