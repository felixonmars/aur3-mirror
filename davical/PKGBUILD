# Maintainer: Andre Wayand <aur-davical@awayand.sleepmail.com>
# Contributor: <zenlord@gmail.com>
# Contributor: Michael Seiwald <michael@mseiwald.at>
pkgname=davical
pkgver=1.1.1
pkgrel=1
pkgdesc="CalDAV CardDAV Server"
arch=('any')
url="http://davical.org/"
license=('GPL')
depends=('php>=5.1' 'awl>=0.53' 'php-pgsql' 'perl-dbd-pg' 'perl-yaml' 'perl-dbi')
optdepends=(
	'postgresql>=8.1: Run postgresql locally'
	'apache>=1.3.0: Web server to run davical'
	'nginx: Web server to run davical'
)
backup=(
	"etc/httpd/conf/extra/httpd-davical.conf"
	"etc/davical/localhost-conf.php"
)
install="$pkgname.install"
changelog=ChangeLog
source=(
	"http://debian.mcmillan.net.nz/packages/$pkgname/$pkgname-$pkgver.tar.gz"
	httpd-davical.conf
	README.archlinux
)

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  install -d "${pkgdir}/srv/http"
  cp -a . "${pkgdir}/srv/http/davical"

  install -d "${pkgdir}/etc/davical"
  install -D "config/example-config.php" "${pkgdir}/etc/davical/localhost-conf.php"
  install -D "${srcdir}/README.archlinux" "${pkgdir}/srv/http/davical/"

  install -d "${pkgdir}/etc/httpd/conf/extra"
  install -t "${pkgdir}/etc/httpd/conf/extra" "${srcdir}/httpd-davical.conf"
}
md5sums=('242de458d22d793b297751ff67b8324f'
         'd268017114ce997488fc2f2978b4528d'
         '22d7a93915a796a6fb8b509582abf188')
sha1sums=('680ca71e54fa3c3dfa026692c7c434572029731b'
          'a3d7860a6da5966d53b3e4b798c17d0d8785952b'
          '5040b5dfa278cf2a5b6a097a318a50086f55602a')
