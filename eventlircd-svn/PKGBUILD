# Maintainer: JÃ¶rg Hansen (joerg dot hansen at gmx dot net)

pkgname=eventlircd-svn
pkgver=44
pkgrel=1
pkgdesc="Eventlircd daemon providing functions for LIRC devices"
url="https://code.google.com/p/eventlircd"
license=('GPL2')
arch=(i686 x86_64)
depends=('udev')
makedepends=('subversion' 'gawk' 'pkg-config')
source=(eventlircd.rc eventlircd.conf)
md5sums=(94e7334d42b0344354427a726c33ab1d 072492af07cbf54c06d38b033cdc4e30)
install=eventlircd.install

_svntrunk=http://eventlircd.googlecode.com/svn/trunk
_svnname=eventlircd

build() {

    cd $srcdir

    msg "SVN checking out..."

    if [ -d $_svnname/.svn ]; then
        (cd $_svnname && svn up -r $pkgver)
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnname
    fi

    msg "SVN checkout done"

    if [[ -d $srcdir/$_svnname-build ]]; then
        msg "Removing previous build directory..."
        rm -rf $srcdir/$_svnname-build
    fi

    msg "Creating build directory..."
    cp -r $srcdir/$_svnname $srcdir/$_svnname-build

    msg "Configuring..."
    cd $srcdir/$_svnname-build
    sed -i 's/\s\-Werror//' configure.ac
    autoreconf -vfi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-dependency-tracking \
        --with-lircd-socket=/var/run/lirc/lircd \
        --with-evmap-dir=/etc/eventlircd.d \
        --with-udev-dir=/lib/udev

    msg "Compiling..."
    make
}

package() {

    cd $srcdir/$_svnname-build
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/eventlircd.conf" "$pkgdir/etc/conf.d/eventlircd.conf"
    install -Dm755 "$srcdir/eventlircd.rc" "$pkgdir/etc/rc.d/eventlircd"

}
