# Maintainer: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: metzen <metzen@gmail.com>

pkgname=logmein-hamachi
pkgver=2.1.0.119
pkgrel=1
pkgdesc="A zero-configuration VPN service"
arch=("i686" "x86_64" "arm" "armv7h")
url="https://secure.logmein.com/AU/labs/#HamachiforLinux"
license=("custom")
conflicts=("hamachi")
replaces=("hamachi")
options=("!strip")
[ "${CARCH}" == "i686" ] && _filearch_=x86 && sha1sums=('1973dec2d7280af384828f15d43717dd2053280b')
[ "${CARCH}" == "x86_64" ] && _filearch_=x64 && sha1sums=('a4706e9075859ae771ca05344c175f3f6b426448')
[ "${CARCH}" == "arm" ] && _filearch_=armel && sha1sums=('7b147c9e8b232d196a04ccaec10b0eba0ee60c8a')
[ "${CARCH}" == "armv7h" ] && _filearch_=armhf && sha1sums=('34ca84dc3343019ca2a478857b98327cb53e8506')

source=("https://secure.logmein.com/labs/${pkgname}-${pkgver}-${_filearch_}.tgz")

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}-${_filearch_}

	# Directories
	install -d "${pkgdir}"/opt/${pkgname}/bin
	install -d "${pkgdir}"/usr/bin "${pkgdir}"/usr/lib/systemd/system
	install -d "${pkgdir}"/usr/share/licenses/${pkgname}

	# Files
	install -m 755 hamachid "${pkgdir}"/opt/${pkgname}/bin
	install -m 755 dnsup "${pkgdir}"/opt/${pkgname}/bin
	install -m 755 dnsdown "${pkgdir}"/opt/${pkgname}/bin
	install -m 755 uninstall.sh "${pkgdir}"/opt/${pkgname}
	install -m 444 README "${pkgdir}"/opt/${pkgname}
	install -m 444 LICENSE "${pkgdir}"/opt/${pkgname}
	install -m 444 CHANGES "${pkgdir}"/opt/${pkgname}
	ln -sf "/opt/${pkgname}/bin/hamachid" "${pkgdir}"/usr/bin/hamachi
	install -m 644 "${startdir}"/systemd "${pkgdir}"/usr/lib/systemd/system/${pkgname}.service

	# License
	ln -sf "/opt/${pkgname}/LICENSE" "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
