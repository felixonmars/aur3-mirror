# Maintainer: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: metzen <metzen@gmail.com>

pkgname=logmein-hamachi
pkgver=2.1.0.101
pkgrel=1
pkgdesc="A zero-configuration VPN service"
arch=("i686" "x86_64" "arm")
url="https://secure.logmein.com/AU/labs/#HamachiforLinux"
license=("custom")
conflicts=("hamachi")
replaces=("hamachi")
options=("!strip")
[ "${CARCH}" == "i686" ] && filearch=x86 && sha1sums=('4493113af22f2ebd50d8be42fc280a2c583c31f8')
[ "${CARCH}" == "x86_64" ] && filearch=x64 && sha1sums=('998bd7b0d4ec709f47d17cb706fd7fc912f657a1')
[ "${CARCH}" == "arm" ] && filearch=armel && sha1sums=('475f5bb249a39061960de5776b4b6d665fe7f4df')

source=("https://secure.logmein.com/labs/${pkgname}-${pkgver}-${filearch}.tgz")

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}-${filearch}

	# Directories
	install -d "${pkgdir}"/opt/${pkgname}/bin
	install -d "${pkgdir}"/usr/bin "${pkgdir}"/usr/lib/systemd/system
	install -d "${pkgdir}"/usr/share/licenses/${pkgname}

	# Files
	install -m 755 hamachid "${pkgdir}"/opt/${pkgname}/bin
	install -m 755 dnsup "${pkgdir}"/opt/${pkgname}/bin
	install -m 755 dnsdown "${pkgdir}"/opt/${pkgname}/bin
	install -m 755 uninstall.sh "${pkgdir}"/opt/${pkgname}
	install -m 444 README "${pkgdir}"/opt/${pkgname}
	install -m 444 LICENSE "${pkgdir}"/opt/${pkgname}
	install -m 444 CHANGES "${pkgdir}"/opt/${pkgname}
	ln -sf "/opt/${pkgname}/bin/hamachid" "${pkgdir}"/usr/bin/hamachi
	install -m 644 "${startdir}"/systemd "${pkgdir}"/usr/lib/systemd/system/${pkgname}.service

	# License
	ln -sf "/opt/${pkgname}/LICENSE" "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

