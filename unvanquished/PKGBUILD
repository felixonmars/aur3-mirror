# Maintainer: Viech
# Contributor: Martin F. Schumann <mfs@mfs.name>

pkgname=unvanquished
pkgver=0.14.0
pkgrel=1
pkgdesc="A team-based fps/rts hybrid game which pits aliens against humans. Monthly release that can be played on official servers."
arch=('x86_64' 'i686')
url="http://www.unvanquished.net"
license=('GPL3')
depends=('curl' 'freetype2' 'glew' 'gmp' 'libjpeg-turbo' 'ncurses' 'libogg' 'libpng' 'sdl' 'libvorbis' 'zlib' 'libwebp>=0.2.0' 'libtheora' 'nettle' 'speex' 'xvidcore' 'openal' 'xdg-utils' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished-git')
options=('emptydirs')
backup=('srv/unvanquished/main/server.cfg' 'srv/unvanquished/main/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=('unvanquished.install' 'unvanquished.sh' 'unvanquished-server.sh' 'unvanquished-update-paks.sh' 'unvanquished-rc.sh' 'unvanquished.service' 'unvanquished.conf' 'unvanquished.desktop' 'ChangeLog')

md5sums=('ce5398923fa900f2f18ff58aca7bbe35'
         'f049a8c71d6b1e3e8415f7f5ec348e17'
         'd25d57c778fc627a95f41b082cffef52'
         '1da9acdc7e8f83610dd6398ba2b38e72'
         '1f2ffbe1482e1450cb77451bfe208246'
         'd272a70f58778affeabac13cc8a880da'
         '9d576da480e18b7f7352c49e537d2005'
         'ac69d49b3c665d274d0ab58870220522'
         'fb752b6412b8097801576ec7e0ca0739')

# I'm using _name for compliance with the *-git version of this package
_name=$pkgname

# I'm using __git* instead of _git* so makepkg leaves pkgver alone
__gitroot="https://github.com/Unvanquished/Unvanquished.git"
__gitver="v$pkgver"

build() {
	cd $srcdir
	
	msg "Connecting to GIT server ..."

	if [[ -d $_name ]]; then
		msg "Updating local files ..."
		cd $_name
		git fetch
		git checkout $__gitver
	else
		msg "Cloning repository ..."
		git clone $__gitroot $_name
		
		msg "Checking out $__gitver ..."
		cd $_name
		git checkout $__gitver
	fi

	msg "The local files are up to date."
	
	msg "Starting build ..."
	cd $srcdir/$_name
	cmake -D CMAKE_INSTALL_PREFIX="" . && make
}

package() {
	# create installation dirs
	cd $pkgdir

	install -d opt/$_name usr/bin etc/conf.d etc/rc.d usr/share/applications var/cache/$_name/update-paks usr/lib/systemd/system
	install -dm 775 srv/$_name/main

	# install starters
	cd $srcdir

	install -m 755 $_name.sh $pkgdir/opt/$_name/$_name
	install -m 755 $_name-server.sh $pkgdir/opt/$_name/$_name-server
	install -m 755 $_name-update-paks.sh $pkgdir/opt/$_name/$_name-update-paks
	install -m 755 $_name-rc.sh $pkgdir/etc/rc.d/$_name
	install -m 644 $_name.service $pkgdir/usr/lib/systemd/system/
	install -m 644 $_name.conf $pkgdir/etc/conf.d/
	install -m 644 $_name.desktop $pkgdir/usr/share/applications/
	
	# install base files
	cd $srcdir/$_name
	
	install -m 755 daemon daemonded *.so download-pk3.sh $pkgdir/opt/$_name/
	install -m 644 COPYING.txt GPL.txt README.txt debian/$_name.png $pkgdir/opt/$_name/
	
	cp -pr main $pkgdir/opt/$_name/
	chown -R root:root $pkgdir/opt/$_name/main
	
	# setup server directory 
	install -m 660 debian/config/* $pkgdir/srv/$_name/main/
	
	cd $pkgdir/srv/$_name/

	ln -s . .Unvanquished

	# symlink starters
	cd $pkgdir/usr/bin/
	
	ln -s /opt/$_name/$_name .
	ln -s /opt/$_name/$_name-server .
	ln -s /opt/$_name/$_name-update-paks .
}
