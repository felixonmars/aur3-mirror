# Maintainer: Viech
# Contributor: Martin F. Schumann <mfs@mfs.name>

pkgname=unvanquished
pkgver=0.5.1
pkgrel=2
pkgdesc="A team-based first-person shooter which pits the aliens against the humans. Monthly release that can be played on official servers."
arch=('x86_64' 'i686')
url="http://www.unvanquished.net"
license=('GPL3')
depends=('curl' 'freetype2' 'glew' 'gmp' 'libjpeg-turbo' 'ncurses' 'libogg' 'libpng' 'sdl' 'libvorbis' 'zlib' 'libwebp' 'libtheora' 'nettle' 'speex' 'xvidcore' 'hicolor-icon-theme' 'xdg-utils' 'openal')
makedepends=('git' 'cmake')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished-git')
backup=('srv/unvanquished/main/server.cfg' 'srv/unvanquished/main/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=('unvanquished.install' 'unvanquished.sh' 'unvanquished-server.sh' 'unvanquished-update-paks.sh' 'unvanquished-rc.sh' 'unvanquished.desktop' 'ChangeLog')

md5sums=('4a1df8c5cf7e74e4349f7a065a3a502a'
         '17ae1a4d793966104e23115417a0f42c'
         'b47705a01575c2c1c2ef7bb4a5bcf8d7'
         '1da9acdc7e8f83610dd6398ba2b38e72'
         '759173428e2d8686d2bb840a9948fb8b'
         '15c96cd0bf51f10d64fa1fe3eed409c5'
         '75a7b19ef00360d83b44b450a95db8c5')

# I'm using __git* instead of _git* so makepkg leaves pkgver where I want it
__gitroot="https://github.com/Unvanquished/Unvanquished.git"
__gitname=$pkgname
__gitver="v$pkgver-last-compat"

build() {
	cd $srcdir
	
	msg "Connecting to GIT server ..."

	if [[ -d $__gitname ]]; then
		msg "Updating local files ..."
		cd $__gitname
		git pull origin $__gitver
	else
		msg "Cloning repository ..."
		git clone $__gitroot $__gitname
		
		msg "Checking out $__gitver ..."
		cd $__gitname
		git checkout $__gitver
	fi

	msg "The local files are up to date."
	msg "Starting build ..."

	cd $srcdir/$pkgname
	
	cmake -D CMAKE_INSTALL_PREFIX="" . && make
}

package() {
	# create installation dirs
	cd $pkgdir

	install -d opt/$pkgname usr/bin etc/rc.d srv/$pkgname/main usr/share/applications var/cache/$pkgname/update-paks
	
	# install starters
	cd $srcdir

	install -m 755 $pkgname.sh $pkgdir/usr/bin/$pkgname
	install -m 755 $pkgname-server.sh $pkgdir/usr/bin/$pkgname-server
	install -m 755 $pkgname-update-paks.sh $pkgdir/usr/bin/$pkgname-update-paks
	install -m 755 $pkgname-rc.sh $pkgdir/etc/rc.d/$pkgname
	install -m 644 $pkgname.desktop $pkgdir/usr/share/applications/
	
	# install base files
	cd $srcdir/$pkgname
	
	install -m 755 daemon.* daemonded.* *.so *.a download-pk3.sh $pkgdir/opt/$pkgname/
	install -m 644 COPYING.txt GPL.txt README.txt debian/$pkgname.png $pkgdir/opt/$pkgname/
	
	# download and install game media
	mkdir -p update-paks-cache
	./download-pk3.sh main update-paks-cache
	install -m 644 update-paks-cache/* $pkgdir/var/cache/$pkgname/update-paks/
	rm -f main/navmesh-default-0.1.pk3 # only needed for bot branch
	cp -pvr main $pkgdir/opt/$pkgname/
	chown root:root $pkgdir/opt/$pkgname/main
	
	# setup server directory 
	install -m 644 debian/config/* $pkgdir/srv/$pkgname/main/
	
	cd $pkgdir/srv/$pkgname/

	ln -s . .Unvanquished
	
	# link architecture specific executables
	cd $pkgdir/opt/$pkgname
	
	if [ $CARCH == "i686" ]; then a="i386"; else a=$CARCH; fi
	ln -s daemon.$a $pkgname
	ln -s daemonded.$a ${pkgname}ded
}
