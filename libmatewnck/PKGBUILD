# Maintainer: Perberos <perberos@gmail.com>
pkgname=libmatewnck
pkgver=20120702
pkgrel=2
pkgdesc="Window Navigator Construction Kit for MATE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libxres')
makedepends=('git' 'mate-doc-utils' 'libxt' 'intltool' 'gobject-introspection')
options=(!emptydirs)
url="http://matsusoft.com.ar/projects/mate/"
source=()
sha256sums=()

_gitroot=git://github.com/mate-desktop/libmatewnck.git
_gitname=libmatewnck

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"


	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static || return 1
	make || return 1
}

package() {
	cd "$srcdir/$_gitname-build"

	make DESTDIR="${pkgdir}" install || return 1
}
