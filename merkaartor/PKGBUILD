# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Michael Kanis <mkanis gmx de>
# Contributor: Paulo Freire <paulofreire gmail com>
# Contributor: Brice Maron <brice bmaron net>

pkgname=merkaartor
pkgver=0.18.1
pkgrel=1
pkgdesc='OpenStreetMap editor'
url='http://merkaartor.be/'
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('boost' 'gdal' 'proj')
depends=('qtwebkit' 'exiv2')
optdepends=('gpsd' 'desktop-file-utils' 'gtk-update-icon-cache')
source=("http://merkaartor.be/attachments/download/301/merkaartor-0.18.1.tar.bz2")
sha1sums=('0c3884d6e04c3b43b7d898cc23693a9494259d32')
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	echo 'QMAKE_MOC = $$QMAKE_MOC -DBOOST_TT_HAS_OPERATOR_HPP_INCLUDED' >> src/src.pro
	lrelease src/src.pro
	qmake Merkaartor.pro \
		PREFIX="/usr" \
		GEOIMAGE=1 \
		NODEBUG=1 \
		RELEASE=1 \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make INSTALL_ROOT="${pkgdir}" install
}
