# Maintainer: Ner0

pkgname=elementary-icons-bzr
pkgver=985
pkgrel=1
pkgdesc="An icon theme designed to be smooth, sexy, clear, and efficient (Development branch)"
arch=('any')
url="https://launchpad.net/elementaryicons"
license=('GPL2')
depends=('hicolor-icon-theme')
makedepends=('bzr')
optdepends=('egtk-bzr: Development branch of the elementary GTK theme. [AUR]')
conflicts=('elementary-icons')
options=('!strip')
source=('user-desktop.patch' 'chrome-chromium.patch')
md5sums=('d2abb087040d62a1b0850614c1a77379'
         'f1ac694e313d9fe991c036aac62ee425')

_bzrtrunk=lp:elementaryicons
_bzrmod=3.0

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
}

package() {
  install -dm755 "$pkgdir/usr/share/icons/"
  cp -Prf "$srcdir/$_bzrmod" "$pkgdir/usr/share/icons/elementary"
  install -Dm644 "$srcdir/$_bzrmod/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  rm -rf "$pkgdir"/usr/share/icons/elementary/{.bzr*,AUTHORS,CONTRIBUTORS,COPYING}

  cd "$pkgdir/usr/share/icons/elementary"
  # Add Desktop icon
  patch -Np0 -i "$srcdir/user-desktop.patch"
  # Add Chrome/Chromium icons (http://spg76.deviantart.com/art/Chrome-Chromium-elementary-201289232)
  patch -Np0 -i "$srcdir/chrome-chromium.patch"
}
