# Contributor: Spyros Stathopoulos <foucault.online@gmail.com>
pkgname=pkgupdate-git
pkgver=20121017
pkgrel=1
pkgdesc="Simple monitor server for pacman and AUR updates"
arch=('i686' 'x86_64')
url="https://github.com/foucault/pkgupdate"
license=('GPL3')
depends=('python')
conflicts=('pkgupdate')
backup=('etc/pkgupdate.conf')
optdepends=('pygtk: for the gtk+ client'
            'gtk3: for the gtk+ 3 client'
	          'python-gobject: for the gtk+ 3 client'
            'gnome-icon-theme: for the gtk+ clients')
makedepends=('git' 'python-docutils')
install='pkgupdate.install'

_gitroot="git://github.com/foucault/pkgupdate.git"
_gitname="pkgupdate"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  python setup.py build
}

package() {

  cd "${srcdir}/$_gitname-build"
  python setup.py install --prefix=/usr --root="${pkgdir}"
  # gtk client is a python2 script
  if [ `grep '#!/usr/bin/python$' "${pkgdir}/usr/bin/pkgupdate_gtk.py"` ]; then
    sed -i -e "s|/usr/bin/python|/usr/bin/python2|" "${pkgdir}/usr/bin/pkgupdate_gtk.py"
  fi
  install -D -m755 "${srcdir}/$_gitname-build/pkgupdate.rc" "${pkgdir}/etc/rc.d/pkgupdate"
  install -D -m644 "${srcdir}/$_gitname-build/pkgupdate.service" "${pkgdir}/usr/lib/systemd/system/pkgupdate.service"
}

