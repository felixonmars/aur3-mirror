# $Id$
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Maintainer: tobias [ tobias at archlinux org ]
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gvim-python
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor (with advanced features, such as a GUI) (Compiled with Python 3).'
_topver=7.3
_patchlevel=709
__hgrev=06e088d016ad
_versiondir="vim${_topver//./}"
pkgver=${_topver}.${_patchlevel}
pkgrel=1
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
depends=("vim-runtime=${pkgver}-${pkgrel}" 'gpm' 'ruby' 'libxt'
         'desktop-file-utils' 'gtk2' 'lua' 'python')
provides=("vim=${pkgver}-${pkgrel}")
conflicts=('vim')
install=gvim.install
source=("ftp://ftp.archlinux.org/other/vim/vim-${pkgver}.tar.xz"
        "ftp://ftp.archlinux.org/other/vim/vim-${pkgver}.tar.xz.sig"
        'pythoncomplete.vim::http://www.vim.org/scripts/download_script.php?src_id=10872'
        'vimrc'
        'archlinux.vim'
        'gvim.desktop')
md5sums=('7a42c72b07e8bfe6795197a303b21d53'
         '5c5723f3c39aaa716bd868d7edf9f563'
         '6e7adfbd5d26c1d161030ec203a7f243'
         '20fceda56badf201695c57999b0bc609'
         '10353a61aadc3f276692d0e17db1478e'
         'd90413bd21f400313a785bb4010120cd')

# source PKGBUILD && mksource
mksource() {

  [[ -x /usr/bin/hg ]] || (echo "hg not found. Install mercurial." && return 1)

  __hgroot='http://vim.googlecode.com/hg/'
  __hgrepo='vim'
  __hgbranch='default'

  hg clone -b ${__hgbranch} -u ${__hgrev} "${__hgroot}${__hgrepo}" ${__hgrepo}

  pushd ${__hgrepo}
  if (( $(hg id -n) < $(hg id -nr ${__hgbranch}) )); then
    printf 'You are not building the latest revision!\n'
    printf "Consider updating __hgrev to $(hg id -r ${__hgbranch}).\n"
  fi
  popd

  mv vim vim-${pkgver}
  find vim-${pkgver} -depth -type d -name .hg -exec rm -rf {} \;
  rm vim-${pkgver}/{.hgignore,.hgtags}
  tar -cJf vim-${pkgver}.tar.xz vim-${pkgver}/*
  rm -r vim-${pkgver}
  #gpg --detach-sign --use-agent -u ${GPGKEY} vim-${pkgver}.tar.xz
}

build() {
  # remove -O2 because there is a crash with python because of it
  # fedora br: https://bugzilla.redhat.com/show_bug.cgi?id=817196
  export CFLAGS="${CFLAGS/-O2 /}"

  cd "${srcdir}"

  cp -a vim-${pkgver} gvim-build

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    gvim-build/src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    gvim-build/src/feature.h
  # fix python name
  #sed -i -e 's|vi_cv_path_python, python|vi_cv_path_python, python2|' \
    #gvim-build/src/configure.in
  (cd gvim-build/src && autoconf)

  cd "${srcdir}"/gvim-build

  ./configure --prefix=/usr --localstatedir=/var/lib/vim \
    --with-features=big --with-compiledby=ArchLinux \
    --enable-gpm --enable-acl --with-x=yes \
    --enable-gui=gtk2 --enable-multibyte --enable-cscope \
    --enable-netbeans --enable-perlinterp --disable-pythoninterp \
    --enable-python3interp --enable-rubyinterp --enable-luainterp

  make
}

check() {
  # disable tests because they seem to freeze

  cd "${srcdir}"/gvim-build

  #make test
}

package() {

  cd "${srcdir}"/gvim-build
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}"/usr/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
  done

  # remove run-time
  rm -r "${pkgdir}"/usr/share/vim
 
  # freedesktop links
  install -Dm644 "${srcdir}"/gvim.desktop \
    "${pkgdir}"/usr/share/applications/gvim.desktop
  install -Dm644 runtime/vim48x48.png "${pkgdir}"/usr/share/pixmaps/gvim.png

  # license
   install -Dm644 "${srcdir}"/vim-${pkgver}/runtime/doc/uganda.txt \
     "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}

# vim:set sw=2 sts=2 et:
