pkgname=tile-molester
pkgver=0.15a
pkgrel=1
pkgdesc="Tile Molester is the next generation of tile editors"
arch=(any)
url="http://www.zophar.net/utilities/graphutil/tile-molester.html"
license=("GPL")
makedepends=(java-runtime)
depends=(java-runtime)
source=("TileMolester_${pkgver//./}_src.zip::http://www.zophar.net/download_file/2737"
"fig1.png"
"fig2.png"
"fig3.png"
"fig4.png"
"fig5.png"
"help.htm"
"language.properties"
"TMIcon.png.in")
md5sums=('a664f4fe532f03522f8d3ac2ab54af8a'
         '992f5c830724d597d60c1ea0038815db'
         'd44bf0e1e143520cfcf0cae1daaa0239'
         'dbc27a22cd2a899597f5fd243fe04175'
         'bf33b58761069f4df69b39824d50e860'
         'da22f711669b8af87a38a5b80e3d92ab'
         'ae921e18903f9730d9b7a9a29e5871ab'
         'e0204644fecf63dfcf1bed0d2cb53197'
         'd6fff3a9990c2b307e4b42bc82b0b9ed')

build() {
  cd $srcdir
  jar xvf bmpLibrary.jar
  mkdir -p ".build/languages"
  find . -name '*.java' | xargs -rtl1 -I {} javac {} -d .build
  cp splash.gif .build
  cp -r icons .build
  find . -name '*.properties' | xargs -rtl1 -I {} cp {} ".build/languages/"
  cd .build
  jar cvef TileMolester tm.jar colorcodecs fileselection icons languages modaldialog osbaldeston \
    threads tilecodecs treenodes utils *.class splash.gif
}

package() {
  cd $srcdir
  install -Dm755 ".build/tm.jar" "$pkgdir/usr/share/$pkgname/tm.jar"
  install -Dm644 "resources/tmres.dtd" "$pkgdir/usr/share/$pkgname/resources/tmres.dtd"
  install -m644 settings.dtd "$pkgdir/usr/share/$pkgname/"
  install -m644 tmspec.dtd "$pkgdir/usr/share/$pkgname/"
  install -m644 tmspec.xml "$pkgdir/usr/share/$pkgname/"
  mkdir "$pkgdir/usr/share/$pkgname/docs"
  find . -name '*.png' | xargs -rtl1 -I {} install -m644 {} "$pkgdir/usr/share/$pkgname/docs/"
  install -m644 help.htm "$pkgdir/usr/share/$pkgname/docs/"
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/applications"
  install -Dm644 TMIcon.png.in "$pkgdir/usr/share/icons/$pkgname.png"
  echo '#!/bin/sh
if [ ! -e $HOME/settings.dtd ]; then
  cp /usr/share/tile-molester/settings.dtd $HOME/
fi
if [ ! -e $HOME/tmspec.dtd ]; then
  cp /usr/share/tile-molester/tmspec.dtd $HOME/
fi
if [ ! -e $HOME/tmspec.xml ]; then
  cp /usr/share/tile-molester/tmspec.xml $HOME/
fi
exec java -jar /usr/share/tile-molester/tm.jar $@' > "$pkgdir/usr/bin/$pkgname"
  chmod +x "$pkgdir/usr/bin/$pkgname"
  echo '[Desktop Entry]
Categories=Utility;Game
Exec=tile-molester
GenericName=Tile Editor
Name=Tile Molester
Icon=tile-molester
Type=Application' > "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
