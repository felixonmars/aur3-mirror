# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=sssd
pkgver=1.9.3
_pkgver=$pkgver
pkgrel=1
pkgdesc="System Security Services Daemon"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3')
depends=(
  'bind'          # for nsupdate
  'c-ares'
  'dbus-core'
  'ding-libs'
  'krb5'
  'ldb'
  'libldap'
  'libnl'
  'libsasl'
  'libunistring'
  'libxml2'
  'nspr'
  'nss'
  'pcre'
  'talloc'
  'tdb'
  'tevent'
)
makedepends=(
  'docbook-xsl'
  'doxygen'
  'python2'
)
source=("https://fedorahosted.org/released/$pkgname/$pkgname-$_pkgver.tar.gz"
        "https://fedorahosted.org/released/$pkgname/$pkgname-$_pkgver.tar.gz.asc"
        'sssd.service')
sha1sums=('a91c973153fb30a1f0f3180797eca4bd5138a7bd'
          'b49f48d4fb108bdf73a4349afed3d755db0860a2'
          'd1100a2428d691251c3b226766e0ebc9897bcd69')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=/usr/bin/python2
  ./configure \
    --prefix=/usr                                 \
    --sysconfdir=/etc                             \
    --localstatedir=/var                          \
    --libexecdir=/usr/lib/sssd                    \
    --datadir=/usr/share                          \
    --enable-pammoddir=/usr/lib/security          \
    --with-autofs                                 \
    --with-initscript=systemd                     \
    --with-os=fedora                              \
    --with-pid-path=/run                          \
    --with-python-bindings                        \
    --without-selinux                             \
    --without-semanage                            \
    --with-ssh                                    \
    --with-sudo                                   \
    --with-systemdunitdir=/usr/lib/systemd/system \
    ;
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/etc/rc.d"
  rm -rf "$pkgdir/lib"
  rm -rf "$pkgdir/run"
  rm -f "$pkgdir/usr/modules/ldb/memberof.la"
  find "$pkgdir/usr" -depth -type d \
    -exec rmdir --ignore-fail-on-non-empty {} \;

  cd "$srcdir"
  install -Dm0644 sssd.service  "$pkgdir/usr/lib/systemd/system/sssd.service"
} 

# vim: ts=2:sw=2:et
