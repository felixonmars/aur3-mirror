# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=sssd
pkgver=1.8.4
pkgrel=1
pkgdesc="System Security Services Daemon"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3')
depends=(
  'bind'          # for nsupdate
  'c-ares'
  'dbus-core'
  'ding-libs'
  'krb5'
  'ldb'
  'libldap'
  'libnl'
  'libsasl'
  'libunistring'
  'libxml2'
  'nspr'
  'nss'
  'pcre'
  'talloc'
  'tdb'
  'tevent'
)
makedepends=(
  'docbook-xsl'
  'doxygen'
  'python2'
)
source=("https://fedorahosted.org/released/$pkgname/$pkgname-$pkgver.tar.gz"
        "https://fedorahosted.org/released/$pkgname/$pkgname-$pkgver.tar.gz.asc"
        'sssd.service'
        'sssd.rcd')
sha1sums=('0ce53f121db741c1baaa11f645b9a83ee7b369f4'
          'f5ec76f6b212335ddff15651e14eded14aba233f'
          '8d92cba4b2491eb677efe59d1088bb83f36569f5'
          '43784f1820a27e426df68e7121f29bc900b5b662')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=/usr/bin/python2
  ./configure \
    --prefix=/usr                             \
    --sysconfdir=/etc                         \
    --localstatedir=/var                      \
    --libexecdir=/usr/lib/sssd                \
    --enable-pammoddir=/usr/lib/security      \
    --with-autofs                             \
    --with-initscript=systemd                 \
    --with-os=fedora                          \
    --with-pid-path=/run                      \
    --with-python-bindings                    \
    --without-selinux                         \
    --without-semanage                        \
    --with-ssh                                \
    --with-sudo                               \
    --with-systemdunitdir=/lib/systemd/system \
    ;
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/etc/rc.d"
  rm -rf "$pkgdir/lib/systemd"
  rm -f "$pkgdir/usr/modules/ldb/memberof.la"
  find "$pkgdir/usr/share/man" -depth -type d -exec rmdir --ignore-fail-on-non-empty {} \;

  cd "$srcdir"
  install -Dm0644 sssd.service  "$pkgdir/usr/lib/systemd/system/sssd.service"
  install -Dm0755 sssd.rcd      "$pkgdir/etc/rc.d/sssd"
} 

# vim: ts=2:sw=2:et
