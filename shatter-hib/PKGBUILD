# Maintainer: Sam S. <smls75@gmail.com>

pkgname=shatter-hib
pkgver=1.0.20120917
_hibver=1347954459
pkgrel=1
pkgdesc='A retro-inspired brick-breaking game with unique twists (Humble Bundle version)'
url='http://www.shattergame.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('glibc' 'gcc-libs' 'libgl' 'libdrm' 'libidn' 'zlib' 'libx11'
         'libxfixes' 'libxdamage' 'libxext' 'libxinerama' 'libxrender' 'libxau'
         'libxft' 'libxxf86vm' 'libxdmcp' 'fontconfig' 'sdl' 'alsa-lib'
         'nvidia-cg-toolkit')
optdepends=('alsa-plugins: PulseAudio support'
            'libpulse: PulseAudio support')
if [ $CARCH == x86_64 ]; then
  depends=('lib32-glibc' 'lib32-gcc-libs' 'lib32-libgl' 'lib32-libdrm'
           'lib32-libidn' 'lib32-zlib' 'lib32-libx11' 'lib32-libxfixes'
           'lib32-libxdamage' 'lib32-libxext' 'lib32-libxinerama'
           'lib32-libxrender' 'lib32-libxau' 'lib32-libxft' 'lib32-libxxf86vm'
           'lib32-libxdmcp' 'lib32-fontconfig' 'lib32-sdl' 'lib32-alsa-lib'
           'lib32-nvidia-cg-toolkit')
  optdepends=('lib32-alsa-plugins: PulseAudio support'
              'lib32-libpulse: PulseAudio support')
fi
source=('shatter-hib.desktop')
md5sums=('00bf87930abd0d75382ab2d33f6fb66a')
PKGEXT='.pkg.tar'

_gamepkg="shatter-linux-${_hibver}.sh"


package() {
  cd $srcdir

  # Get installer
  _get_humblebundle_source "${_gamepkg}" || {
    error "Unable to find the game archive. Please download it from your Humble
           Bundle page, and place it into one of the above locations."
    exit 1; }

  # Execute installer
  msg "Starting setup..."
  [[ -d "./temp" ]] && rm -r "./temp"
  sh "./${_gamepkg}" --unattended --no-register --keep --overwrite \
                     --target  "${srcdir}/temp" \
                     --bindir  "{$pkgdir}/opt/Shatter" \
                     --datadir "${pkgdir}/opt"

  # Remove bundled libraries (to force usage of system libs instead)
  rm -rf "${pkgdir}/opt/Shatter/lib"{Cg.so,CgGL.so,SDL-1.2.so.0}

  # Fix permissions
  find "${pkgdir}" -type f -exec chmod 644 "{}" +

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "${pkgdir}/opt/Shatter/Shatter.png" \
                 "${pkgdir}/usr/share/pixmaps/shatter.png"

  # Install launcher symlink
  chmod 755 "${pkgdir}/opt/Shatter/Shatter.bin.x86"
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/Shatter/Shatter.bin.x86" "${pkgdir}/usr/bin/shatter"
}


# Locate a game archive from one of the Humble Bundles, and symlink it into $srcdir
_get_humblebundle_source() {
  _get_local_source "$1" || return 1;
  # TODO: Implement automatic downloading from Humble Bundle site
}

# Locate a file or folder provided by the user, and symlink it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    if [ -z "$_dir" ]; then _dir="<undefined>"; fi
    echo -n "    - in $_key ['$_dir'] ... ";
    if [ -e "$_dir/$1" ]; then
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
    echo "NOT FOUND"
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}
