# Maintainer: Andreas B. Wagner <AndreasBWagner@pointfree.net>
pkgname=ccnx
pkgver=0.6.2
pkgrel=1
pkgdesc="an experimental implementation of content-centric networking"
arch=('i686' 'x86_64')
url="http://www.ccnx.org"
license=('GPLv2' 'LGPLv2.1')
depends=('openssl' 'expat' 'libpcap' 'libxml2' 'apache-ant' 'java-environment' 'java-runtime' 'athena-jot')
provides=('ccnx')
conflicts=('ccnx')
options=('zipman')
source=("http://www.ccnx.org/releases/$pkgname-$pkgver.tar.gz")
md5sums=('f24ca7c00dd9bc7607609637195ff6f2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  echo INSTALL_BASE=/usr > $srcdir/$pkgname-$pkgver/csrc/conf/local.mk
  ./configure
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p $pkgdir{/etc/profile.d,/usr/{include,lib,bin,share/{man/man1,doc/$pkgname/technical}}}
  make INSTALL_BASE=${pkgdir}/usr install
  export CCN_HOME=/usr
  echo "export CCN_HOME=/usr/lib/ccn" > ${pkgdir}/etc/profile.d/${pkgname}.sh
  chmod +x ${pkgdir}/etc/profile.d/${pkgname}.sh

  install -Dm644 doc/manpages/*.1 $pkgdir/usr/share/man/man1/
  install -Dm644 doc/technical/*.txt $pkgdir/usr/share/doc/ccnx/technical/
  install -Dm644 README NOTICES NEWS $pkgdir/usr/share/doc/ccnx/
} 
# vim:set ts=2 sw=2 et:
