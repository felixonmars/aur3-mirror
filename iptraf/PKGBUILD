# $Id: PKGBUILD 32907 2009-04-02 15:33:32Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=iptraf
pkgver=3.0.0
pkgrel=3
pkgdesc="An IP network monitor."
arch=('i686' 'x86_64')
url="http://iptraf.seul.org/"
depends=('ncurses>=5.6-7')
license=('GPL')
options=('!makeflags')
source=("ftp://iptraf.seul.org/pub/$pkgname/$pkgname-$pkgver.tar.gz"
        "iptraf-$pkgver-headerfix.patch")
md5sums=('377371c28ee3c21a76f7024920649ea8'
         'd88c8a5387870e18f2be9bdff28fffac')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  patch -Np1 -i "$srcdir"/$pkgname-$pkgver-headerfix.patch

  cd src/
  sed -e 's|/var/local/iptraf|/var/lib/iptraf|' \
      -e 's|/usr/local/bin|/usr/sbin|' \
      -i dirs.h

  make \
    CFLAGS="$CFLAGS" \
    DEBUG="" \
    TARGET="/usr/sbin" \
    WORKDIR="/var/lib/iptraf" \
    clean all
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/src

  for sbin in iptraf rvnamed ; do
    install -D -m755 $sbin "$pkgdir"/usr/sbin/$sbin
  done

  cd "$srcdir"/$pkgname-$pkgver/Documentation
  for man in *.8; do
    install -D -m644 $man "$pkgdir"/usr/share/man/man8/$man
  done

  mkdir -p "$pkgdir"/var/{lib,log,run}/iptraf
}
