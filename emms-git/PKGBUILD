# Contributor: Joyfulgirl <joyfulgirl (at) archlinux.us>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=emms-git
pkgver=20120408
pkgrel=1
pkgdesc="Emacs MultiMedia System -- git version"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emms/"
makedepends=('git')
depends=('emacs' 'taglib' 'emacs-w3m-cvs')
optdepends=('vorbis-tools: ogg support' 
            'mpg123: mp3 support' 
            'mp3info: mp3 tag support'
            'mpd: mpd support'
            'mplayer: mplayer support'
            'xine: xine support')
license=('GPL')
install=emms.install
conflicts=('emms')
provides=('emms')
source=()
md5sums=()

_gitname="emms"
_gitroot="git://git.savannah.gnu.org/emms.git"

build() {
    msg "Connecting to ${_gitroot}..."

    if [ -d ${srcdir}/${_gitname} ]; then
        cd ${srcdir}/${_gitname} && git pull origin master
        msg "Update complete!"
    else
        cd ${srcdir}
        git clone $_gitroot
    fi

    [ -d ${srcdir}/${_gitname}-build ] && rm -rf ${srcdir}/${_gitname}-build
    git clone ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
    msg "GIT checkout done or server timeout."
    msg "Starting make..."

    cd ${srcdir}/${_gitname}-build 
    mv lisp/emms-setup.el lisp/emms-setup.e!
    PREFIX=/usr make
    PREFIX=/usr make emms-print-metadata 
    mv lisp/emms-setup.e! lisp/emms-setup.el
}

package() {
  cd ${srcdir}/${_gitname}-build
  install -d ${pkgdir}/usr/share/{emacs/site-lisp/emms,man/man1,info}
  install -m 644 -t ${pkgdir}/usr/share/emacs/site-lisp/emms lisp/*.el
  install -m 644 -t ${pkgdir}/usr/share/emacs/site-lisp/emms lisp/*.elc
  install -m 644 -t ${pkgdir}/usr/share/info doc/emms.info 
  install -m 644 -t ${pkgdir}/usr/share/man/man1 emms-print-metadata.1 
  install -Dm755 src/emms-print-metadata ${pkgdir}/usr/bin/emms-print-metadata
}
