# Maintainer: David Scholl <djscholl at gmail dot com>
pkgname=elog
pkgver=2.9.0.2
pkgrel=1
pkgdesc="Electronic logbook w/ attached file support, doesn't use a database."
arch=('i686')
url="http://midas.psi.ch/elog/"
license=('GPL')
depends=('openssl')
optdepends=('imagemagick: inline thumbnail support'
            'ghostscript: inline thumbnail support')
install=elog.install
backup=('etc/elog/elogd.cfg')
source=(https://midas.psi.ch/elog/download/tar/elog-2.9.0-2.tar.gz elogd)
md5sums=('55cde7b713428cccbfee3b424c817c39'
         '746fc2d7ea204473ef95136b99fc7bb0')
build() {
  cd $srcdir/$pkgname-2.9.0
  # make things more Arch-friendly
  sed -i '/^ELOGDIR/s/$(PREFIX)\/elog/\/etc\/elog/' Makefile || return 1
  sed -i '/^BINOWNER/s/bin/root/' Makefile || return 1
  sed -i '/^BINGROUP/s/bin/root/' Makefile || return 1
  # make and install
  make PREFIX=/usr || return 1 
  make PREFIX=/usr ROOT=$pkgdir MANDIR=$pkgdir/usr/share/man install || return 1
  # substitute an Arch-friendly init script in place of the default
  cp $srcdir/elogd $pkgdir/etc/rc.d/
  rm -Rf $pkgdir/etc/rc.d/init.d
  # move data to standard Arch locations
  install -d $pkgdir/usr/share/elog
  mv $pkgdir/etc/elog/resources $pkgdir/usr/share/elog
  mv $pkgdir/etc/elog/scripts $pkgdir/usr/share/elog
  mv $pkgdir/etc/elog/themes $pkgdir/usr/share/elog
  mkdir -p $pkgdir/var/lib/elog
  mv $pkgdir/etc/elog/logbooks $pkgdir/var/lib/elog
  # tell elogd about the new locations
  sed -i '/\[global\]/s/$/\nLogbook dir = \/var\/lib\/elog\/logbooks/' \
    $pkgdir/etc/elog/elogd.cfg || return 1
  sed -i '/\[global\]/s/$/\nResource dir = \/usr\/share\/elog/' \
    $pkgdir/etc/elog/elogd.cfg || return 1
}
