# Maintainer: Mike WB2FKO <mph at sportscliche dot com>
pkgname=wspr-svn
pkgver=2460
pkgrel=1
pkgdesc="Weak Signal Propagation Reporter"
arch=('i686')
url="http://physics.princeton.edu/pulsar/K1JT/wspr.html"
license=('GPL')
depends=('python2' 'python-imaging' 'python2-numpy' 'libsamplerate' 'portaudio' 'fftw' 'g95')
makedepends=('subversion')
source=($pkgname.png
        $pkgname.desktop)
md5sums=('dd21318d4aa3423f1fbefc53061cc40d'
         '56e94de2f4fded54a6a199caf8ea65ab')

_svntrunk="svn://svn.berlios.de/wsjt/branches/wspr"
_svnmod="wspr"

build() {
# Set the build environment to python2
 export PYTHON="/usr/bin/python2"
 export F2PY="/usr/bin/python2 f2py.py"
 unset LDFLAGS
 cd "$srcdir"

 if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

# Make problems can sometimes be fixed by replacing g95 with gfortran
  ./configure --prefix=/usr --enable-g95
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  rm -rf build/
  python2 setup.py install --root=$pkgdir/ 
  sed -i 's:python -O wspr.py:python2 -O /usr/bin/wspr.py:' wspr
  install -Dm755 wspr $pkgdir/usr/bin/wspr

# Install a menu item with icon
  cd $srcdir
  mkdir -p $pkgdir/usr/share/pixmaps
  mkdir -p $pkgdir/usr/share/applications
  mkdir -p $pkgdir/usr/share/doc/$pkgname
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644  $pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
  cp -ra $srcdir/$_svnmod-build/*.TXT $pkgdir/usr/share/doc/$pkgname/
}
