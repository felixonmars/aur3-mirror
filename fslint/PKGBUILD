# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=fslint
pkgver=2.44
pkgrel=1
pkgdesc="A utility to find and clean various forms of lint on a filesystem"
arch=('any')
url="http://www.pixelbeat.org/fslint/"
license=('GPL')
depends=('pygtk' 'python2')
conflicts=('rpm')
install=$pkgname.install
source=(http://www.pixelbeat.org/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('8491a9cd76725e652ac8fe07288ba1f896d9331e30593c0ff3ab9e82e863f0ae')

package() {
  cd "${srcdir}"/$pkgname-$pkgver

#install directories
  install -d "${pkgdir}"/usr/share/{man/man1,doc/$pkgname}
  install -d "${pkgdir}"/usr/share/$pkgname/fslint/{fstool,supprt/rmlint}

  sed -i -e s_^liblocation=.*_liblocation=\'/usr/share/fslint\'_ fslint-gui

  install -D -m755 $pkgname-gui "${pkgdir}"/usr/bin/$pkgname-gui
  install -D -m644 $pkgname.glade "${pkgdir}"/usr/share/$pkgname/$pkgname.glade
  install -D -m755 -t "${pkgdir}"/usr/share/$pkgname/$pkgname $pkgname/find* $pkgname/zipdir $pkgname/$pkgname
  install -D -m755 -t "${pkgdir}"/usr/share/$pkgname/$pkgname/fstool $pkgname/fstool/*
  install -D -m755 -t "${pkgdir}"/usr/share/$pkgname/$pkgname/supprt $pkgname/supprt/get* $pkgname/supprt/fslver
  install -D -m755 -t "${pkgdir}"/usr/share/$pkgname/$pkgname/supprt/rmlint fslint/supprt/rmlint/*

#icons
  install -D -m644 fslint_icon.png "${pkgdir}"/usr/share/pixmaps/fslint_icon.png
  install -D -m644 fslint_icon.png "${pkgdir}"/usr/share/fslint/fslint_icon.png

#shortcut
  install -D -m644 $pkgname.desktop "${pkgdir}"/usr/share/applications/$pkgname.desktop

#locales
  make -C po DESTDIR="${pkgdir}"/usr DATADIR=share install

#man pages
  cp -r "${srcdir}"/$pkgname-$pkgver/man/* "${pkgdir}"/usr/share/man/man1

#doc files
  cp -r "${srcdir}"/$pkgname-$pkgver/doc/* "${pkgdir}"/usr/share/doc/$pkgname

#make it use Python2
  sed -i 's/env python/env python2/g' "${pkgdir}"/usr/bin/fslint-gui
  sed -i 's/env python/env python2/g' "${pkgdir}"/usr/share/fslint/fslint/supprt/rmlint/merge_hardlinks
  sed -i 's/env python/env python2/g' "${pkgdir}"/usr/share/fslint/fslint/supprt/rmlint/fixdup
  sed -i 's/env python/env python2/g' "${pkgdir}"/usr/share/fslint/fslint/fstool/dupwaste
}
