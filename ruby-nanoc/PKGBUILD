# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: Lucky <archlinux@builds.lucky.li>

pkgname=ruby-nanoc
_gemname="${pkgname#ruby-}"
pkgver=3.6.7
pkgrel=1
pkgdesc="A simple, flexible static site generator written in Ruby."
url="http://nanoc.ws"
license=("MIT")
arch=("any")
depends=("ruby-cri")
optdepends=(
    "ruby-adsf: Provides 'nanoc view' command."
    "ruby-listen: Provides 'nanoc watch' command."
)
provides=("ruby-nanoc3")
conflicts=("ruby-nanoc3")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem" 'LICENSE')
noextract=("${_gemname}-${pkgver}.gem")
md5sums=('f3909caa4a069bffabdf5f5b7adaf02a'
         'edf05ed229b286bb405abca067ba91cf')
sha1sums=('dc61c591de33e7b235361c39767596724cb77ee1'
          'e71123f873b48b29eaad9ab141d5a15a57d4e11a')

package() {
  cd "${srcdir}"
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  HOME=/tmp gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"
  install -Dm 644 \
    "LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
