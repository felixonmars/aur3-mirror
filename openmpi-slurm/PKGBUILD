# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>
pkgname=openmpi-slurm
pkgver=1.6.5
pkgrel=1
pkgdesc="High performance message passing library (MPI) - compiled for slurm"
arch=('i686' 'x86_64')
url="http://www.open-mpi.org"
license=('custom')
depends=('gcc-fortran' 'openssh' 'valgrind' 'libltdl' 'slurm-llnl')
makedepends=('inetutils')
options=(!libtool)
conflicts=('openmpi')
provides=('openmpi')
replaces=('openmpi')
source=(http://www.open-mpi.org/software/ompi/v1.6/downloads/openmpi-${pkgver}.tar.bz2)
sha1sums=('93859d515b33dd9a0ee6081db285a2d1dffe21ce')

build() {
   cd openmpi-${pkgver}

   # Make sure we use the system ltdl librariry rather than the ones in the tarball
   rm -r opal/libltdl

   ./configure --prefix=/usr \
               --sysconfdir=/etc/openmpi \
               --mandir=/usr/share/man \
               --enable-mpi-f90 \
               --libdir=/usr/lib/openmpi \
               --with-threads=posix \
               --enable-smp-locks \
               --with-valgrind \
               --enable-memchecker \
               --enable-debug \
               --enable-pretty-print-stacktrace \
               --with-slurm \
               --with-hwloc=/usr \
               --with-libltdl=/usr  \
               --enable-contrib-no-build=vt \
               FC=/usr/bin/gfortran \
               LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

   make
}

package() {
   cd openmpi-${pkgver}
   make DESTDIR="${pkgdir}" install

   # FS#28583
   install -d -m 755 "${pkgdir}"/usr/lib/pkgconfig
   for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
      ln -sf /usr/lib/openmpi/pkgconfig/${i} "${pkgdir}"/usr/lib/pkgconfig/
   done

   # Remove dangling symlink and useless file
   rm "${pkgdir}"/usr/share/man/man1/orteCC.1

   install -d -m 755 "${pkgdir}"/etc/ld.so.conf.d
   echo "/usr/lib/openmpi" > "${pkgdir}"/etc/ld.so.conf.d/openmpi.conf

   install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
