# Maintainer: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>

_target="arm-linux-gnueabi"
pkgname=${_target}-eglibc
pkgver=2.15
pkgrel=8
pkgdesc="Embedded GNU C Library (${_target})"
arch=(any)
url="http://www.eglibc.org"
license=('GPL')
depends=("${_target}-linux-api-headers")
makedepends=("${_target}-gcc-stage2" 'subversion')
provides=("${_target}-eglibc-headers=${pkgver}")
conflicts=("${_target}-eglibc-headers")
replaces=("${_target}-eglibc-headers")
options=(!strip)
source=("eglibc-2.15-LIBC_TRY_CC_OPTION.patch")
md5sums=("b5e1e3caa485a216dae43df024986f4b")
build()
{
  cd "$srcdir"

  if [ -x "eglibc-${pkgver}" ]; then
    rm -rf eglibc-${pkgver}
  fi
  svn co svn://svn.eglibc.org/branches/eglibc-2_15/libc eglibc-${pkgver}
  cd eglibc-${pkgver}
  svn co svn://svn.eglibc.org/branches/eglibc-2_15/ports ports

  # Don not build timezones
  sed -i 's/timezone rt/rt/' Makeconfig

  patch -Np1 -i "$srcdir"/eglibc-2.15-LIBC_TRY_CC_OPTION.patch

  rm -rf build
  mkdir build
  cd build

  unset CFLAGS
  export CFLAGS="${CFLAGS} -U_FORTIFY_SOURCE  -mlittle-endian -msoft-float -O2"

  export BUILD_CC=gcc
  export CC=${_target}-gcc
  export CXX=${_target}-g++
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

  ../configure \
    --target=${_target} \
    --host=${_target} \
    --build=${CHOST} \
    --prefix=/ \
    --with-headers=/usr/${_target}/include \
    --enable-kernel=2.6.27 \
    --enable-add-ons=libidn,ports,nptl \
    --enable-shared \
    --without-fp \
    --with-tls \
    --with-__thread \
    --without-cvs \
    --disable-profile \
    --without-gd

  make
}

package()
{
  cd "$srcdir/eglibc-$pkgver/build"
  make install_root=${pkgdir}/usr/${_target} install

  #
  mkdir -p ${pkgdir}/usr/${_target}/usr
  (
   cd ${pkgdir}/usr/${_target}/usr
   ln -s ../{include,lib} .
  )

  # Remove unneeded for compilation files
  (
   cd ${pkgdir}/usr/${_target}
   rm -rf {bin,sbin,etc,share,var}
  )
}

# vim:set ts=2 sw=2 et:
