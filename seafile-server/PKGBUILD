# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

# Contributor: Aaron Lindsay <aaron@aclindsay.com>

pkgname=seafile-server
pkgver=3.1.5
pkgrel=1
pkgdesc="Next-generation open source cloud storage with advanced features on privacy protection and teamwork. (Server components, without seahub)"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/haiwen/seafile/"
license=('GPL3')
depends=('python2-mako' 'python2-dateutil' 'python2-webpy' 'python2-simplejson' 'python2-pillow' 'python2-chardet' 'python2-pip' 'python2-virtualenv' 'gunicorn-python2==18.0' 'python2-djblets' 'python2-flup' 'python2-six' 'libevhtp' 'fuse' 'seafile-shared>=3.1.5')
makedepends=('vala' 'intltool')
optdepends=()
provides=()
conflicts=('django' 'django-rest-framework', 'seafile')
options=('!libtool' '!emptydirs')
install=seafile-server.install
source=("https://github.com/haiwen/seafile/archive/v${pkgver}-server.tar.gz"
        "seafile-admin.patch"
        "seafile-server.install"
        "seafile-server@.service"
        "seahub-preupgrade"
        "0001-Revert-server-put-pids-folder-out-of-seafile-data.patch")

prepare ()
{
	cd "${srcdir}/seafile-${pkgver}-server"
	patch -p1 -i ${srcdir}/seafile-admin.patch
	patch -p1 -i ${srcdir}/0001-Revert-server-put-pids-folder-out-of-seafile-data.patch
}

build ()
{
	cd "${srcdir}/seafile-${pkgver}-server"
	./autogen.sh
	./configure --enable-server --enable-httpserver --disable-client --disable-python --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	# Install library and header files
	cd "${srcdir}/seafile-${pkgver}-server"
	make DESTDIR="${pkgdir}/" install

	# Remove files already installed by seafile-shared
	# to maintain compatibility
	rm -rf "${pkgdir}/usr/lib"
	rm -rf "${pkgdir}/usr/include"

	# Install upgrade scripts
	mkdir -p "${pkgdir}/usr/share/$pkgname/scripts"
	cp -r -p "${srcdir}/seafile-${pkgver}-server/scripts/upgrade" "${pkgdir}/usr/share/$pkgname/scripts/upgrade"
	rm -rf "${pkgdir}/usr/share/$pkgname/scripts/upgrade/win32"
	# Fix upgrade script's  python 2 requirement
	sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' "${pkgdir}/usr/share/seafile-server/scripts/upgrade/db_update_1.3_1.4.py"
	# Fix upgrade script's invald use of $INSTALLPATH to find seafile binaries
	sed -i 's|${INSTALLPATH}/seafile/bin|/usr/bin|g' "${pkgdir}/usr/share/seafile-server/scripts/upgrade/upgrade_2.2_3.0.sh"

	# Workaround for strange behaviour in the Makefile, which
	# installs python bindings in the package root directory,
	# even though they were disabled
	rm -rf "${pkgdir}/seaserv"
	rm -rf "${pkgdir}/seafile"

	# Install license
	install -D -m644 "${srcdir}/seafile-${pkgver}-server/LICENCE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt"

	# Install systemd service
	install -D -m644 "${srcdir}/seafile-server@.service" "${pkgdir}/usr/lib/systemd/system/seafile-server@.service"

	# Install seahub preupgrade script
	install -D -m755 "${srcdir}/seahub-preupgrade" "${pkgdir}/usr/bin/seahub-preupgrade"
}
sha256sums=('7e2d7c361f822fe49a17cb6cb1ac9d1a185d6a55fd0ae2b8278bb9ec51f4febd'
            'c23ba3810a392c97e7087dd3c5ef665616c2576746edd8ca9044f81b61f6d2f2'
            '44889af3e5dd861caa1bb0b0c63ad739856d9ac22b327b2c2b3d5e5929cf0896'
            '943ed1bb7d4908b452ce8b7abff26e56f3f24582f6a2cf6e0dc4bf2bffd0a6d8'
            '6a04e89eee4030d06d79f21912c7aa531dfc7beb5d3f51b66ec2bd62f95401ec'
            'b1748e826d8e7cccdd825b99864b74dfb5795312f8878d63e9a87105f4382e29')
