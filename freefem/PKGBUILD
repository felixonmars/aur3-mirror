# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>

pkgname=freefem
pkgver=3.19.1
_pkgver=3.19-1
pkgrel=2
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'gsl' 'umfpack')
makedepends=('gcc-fortran' 'wget')
source=("http://www.freefem.org/ff++/ftp/${pkgname}++-${_pkgver}.tar.gz")
md5sums=('74c09591d35ffc7d50de6cd820ebad01')

build() {
  cd "${srcdir}/${pkgname}++-${_pkgver}"

  sed -i "1i#include <unistd.h>" src/Graphics/getprog-unix.hpp

  ./configure \
    --prefix=/usr \
    --enable-download
  make
}

package() {
  cd "${srcdir}/${pkgname}++-${_pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install documentation
  install -Dm644 DOC/freefem++doc.pdf "${pkgdir}/usr/share/doc/${pkgname}/freefem++doc.pdf"
}
