pkgname=znc-git
pkgver=20120702
pkgrel=1
pkgdesc='An IRC bouncer with modules & scripts support'
url='http://znc.in/'
license=('GPL2')
arch=('i686' 'x86_64')
provides=('znc')
conflicts=('znc')
depends=('openssl')
makedepends=('swig' 'tcl' 'python' 'perl' 'cyrus-sasl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')

_gitroot="https://github.com/znc/znc.git"
_gitname="znc"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"

  cd "$srcdir/$_gitname"
  ./autogen.sh
  ./configure --prefix=/usr \
    --enable-cyrus \
    --enable-tcl \
    --enable-perl \
    --enable-python
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}
