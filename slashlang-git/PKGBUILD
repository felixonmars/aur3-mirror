# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=slashlang-git
pkgver=r1120.cd51d18
pkgrel=1
pkgdesc="A language for the web. It's designed for the cases when you just want to throw up a simple dynamic page and don't want to have to bother setting up and maintaining application servers."
arch=(i686 x86_64)
url='https://github.com/slash-lang/slash'
license=(MIT)
depends=(gmp pcre curl libgcrypt libmariadbclient yajl discount)
makedepends=(git ruby flex)
source=(git://github.com/slash-lang/slash)
sha1sums=(SKIP)

pkgver() {
  cd slash
  # the repo does not have any git tags yet
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd slash
  sed 's/yajl_s/yajl/' -i ext/json/configure.pl
}

build() {
  cd slash
  ./configure --prefix=/usr
  make
}

check() {
  cd slash
  make test
}

package() {
  cd slash
  make DESTDIR="$pkgdir" install
}
