# Maintainer: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-quazip-qt5
pkgver=0.5.1
pkgrel=2
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package (mingw-w64)"
url="http://sourceforge.net/projects/quazip/"
arch=('any')
depends=('mingw-w64-crt' 'mingw-w64-qt5-base')
makedepends=('mingw-w64-gcc')
source=("http://downloads.sourceforge.net/project/quazip/quazip/$pkgver/quazip-$pkgver.tar.gz")
options=('!strip' '!buildflags')
license=('LGPL')
md5sums=('eec6b9b6f19654230dfcd158f29ea9d0')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  cd "$srcdir/quazip-$pkgver/quazip"
  
  for _arch in ${_architectures}; do
    mkdir -p "$srcdir/${_arch}-build"
    cd "$srcdir/${_arch}-build"
    ${_arch}-qmake-qt5 "$srcdir/quazip-$pkgver/quazip/quazip.pro" \
       PREFIX="$pkgdir/usr/${_arch}" \
       CONFIG+=dll \
       LIBS+=-lz
    make
    # crate static library
    mkdir -p "$srcdir/${_arch}-static"
    cd "$srcdir/${_arch}-static"
    ${_arch}-qmake-qt5 "$srcdir/quazip-$pkgver/quazip/quazip.pro" \
       PREFIX="$pkgdir/usr/${_arch}" \
       CONFIG+=staticlib \
       LIBS+=-lz
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/${_arch}-build"
    mkdir -p ${pkgdir}/usr/${_arch}/{bin,lib,include}
    make install
    mv ${pkgdir}/usr/${_arch}/lib/libquazip.a ${pkgdir}/usr/${_arch}/lib/libquazip.dll.a
    mv ${pkgdir}/usr/${_arch}/lib/*.dll ${pkgdir}/usr/${_arch}/bin/

    # install static
    cd "$srcdir/${_arch}-static"
    make install

    # strip libraries
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/bin/quazip.dll
    ${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/libquazip.a
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/lib/libquazip.dll.a
  done
}
