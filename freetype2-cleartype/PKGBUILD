# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Estevao Valadao <estevao@archlinux-br.org>
# Maintainer: Chirantan Ekbote <ekbotec at chromium.org>
# Contributor: Tetsumaki <http://goo.gl/YMBdA>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: freedom

pkgname=freetype2-cleartype
pkgver=2.5.0.1
pkgrel=1
pkgdesc="TrueType font rendering library with ClearType patch"
arch=('i686' 'x86_64')
license=('GPL')
url="http://freetype.sourceforge.net/"
depends=('zlib')
conflicts=('freetype2')
provides=("freetype2=$pkgver")
options=('!libtool')
changelog=CHANGELOG
source=("http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2"
    "http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2.sig"
    freetype-2.5.0.1-subpixel-disable-quantization.patch
    freetype-2.5.0.1-bytecode.patch
    freetype-2.5.0.1-enable-spr.patch
    freetype-2.5.0.1-enable-valid.patch
    freetype-2.5.0.1-memcpy-fix.patch)
md5sums=('c72e9010b1d986d556fc0b2b5fcbf31a'
         'SKIP'
         'a8d7d2991333a02bdea5b4a2e51df574'
         '676dd419d9d6f10d40e0c9a0454c2502'
         'b3855fdea5051fa76afb10e09bfb7b33'
         'ec0c8776974d193ae126ab6a3f5587f3'
         '559be35652420b4d80b160cbd853b71a')

prepare() {
    cd "freetype-$pkgver"

    for p in $(find "$srcdir" -maxdepth 1 -name '*.patch'); do
	msg2 "Applying $p"
	patch -Np1 -i "$p"
    done
}

build() {
    cd "freetype-$pkgver"
    ./configure --prefix=/usr
    make
}

package() {
    cd "freetype-$pkgver"
    make DESTDIR="$pkgdir" install
}
