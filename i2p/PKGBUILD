# Maintainer: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Build multilanguage translations
_multilang=0

# Download sources from within i2p
_i2p_fetch=0
###########################################################

pkgname=i2p
pkgver=0.9.2
pkgrel=1
pkgdesc="A distributed anonymous network"
url="http://www.i2p2.de"
license=('GPL2')
arch=('x86_64' 'i686')
depends=('java-runtime' 'bash')
makedepends=('apache-ant' 'java-environment')
conflicts=('i2p-bin' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
install='i2p.install'
source=(#"https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2"
        #"https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2.sig"
        "https://launchpad.net/i2p/trunk/${pkgver}/+download/i2psource_${pkgver}.tar.bz2"
        "https://launchpad.net/i2p/trunk/${pkgver}/+download/i2psource_${pkgver}.tar.bz2.sig"
        "i2prouter.service")
sha256sums=('ac0262120868a01d11b27ce56a7fea5ea243e261d0d7ff6e6dd59e18987a1be5'
            'f31b6b25046cf2443834508d085c7c485b5de12f7f151ab3c1419fcaaaacaa82'
            '77105f3a4c52467fa5e70c3b2cf6a8435744760c53feb1358773191e746207b1')

[[ "$_i2p_fetch" = 1 ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2"
            "http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2.sig"
            "i2prouter.service")
}
[[ "$_multilang" = 1 ]] &&
    depends+=('gettext')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    if [[ "$_multilang" != 1 ]]; then
        sed "s:require.gettext=true:require.gettext=false:" -i build.properties
        # this hack will be fixed in a future release
        for file in ./{apps/{desktopgui,i2psnark/java,i2ptunnel/java,routerconsole/java,susidns/src,susimail},installer/resources/locale}/bundle-messages.sh
        do
            sed -i $file \
                -e 's:xgettext -f:false:' \
                -e 's:msgmerge -U:false:' \
                -e 's:msgfmt .* --statistics:false:'
        done
    fi

    source /etc/profile.d/apache-ant.sh
    ant installer-linux
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    echo "INSTALL_PATH=${pkgdir}/opt/i2p" >install.properties
    java -jar i2pinstall_${pkgver}*.jar -options install.properties

    sed -i $pkgdir/opt/i2p/{i2prouter,wrapper.config,runplain.sh,eepget} \
        -e "s:$pkgdir/opt/i2p:/opt/i2p:g"
    sed -i $pkgdir/opt/i2p/i2prouter \
        -e 's:#RUN_AS_USER=.*:RUN_AS_USER=i2p:' \
        -e 's:I2P_CONFIG_DIR=.*:I2P_CONFIG_DIR="$I2P/.i2p":'

    install -d "${pkgdir}/usr/share/licenses/i2p/"
    install -Dm644 ${srcdir}/i2prouter.service       "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1    "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/man/i2prouter.1 "${pkgdir}/usr/share/man/man1/i2prouter.1"
    mv ${pkgdir}/opt/i2p/LICENSE.txt                 "${pkgdir}/usr/share/licenses/i2p/"
    mv ${pkgdir}/opt/i2p/licenses/*                  "${pkgdir}/usr/share/licenses/i2p/"

    rm -r ${pkgdir}/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,licenses,man}
    chmod -R 750 ${pkgdir}/opt/i2p
    chmod -x ${pkgdir}/opt/i2p/*.config
}
