# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Comment out if you want to build all language translations
export LG2=en

# Download sources from within i2p
#_i2p_fetch=1
###########################################################

pkgname=i2p
pkgver=0.9.10
pkgrel=1
pkgdesc="A distributed anonymous network"
url="http://www.i2p2.de"
license=('GPL2')
arch=('any')
depends=('java-runtime' 'java-service-wrapper')
makedepends=('apache-ant' 'java-environment')
optdepends=('robert: I2P BitTorrent client') #'gtk2: for rrd graphs'
conflicts=('i2p-bin' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
backup=('opt/i2p/wrapper.config')
install='i2p.install'

#_url='https://i2p.googlecode.com/files'
#_url='https://googledrive.com/host/0B4jHEq5G7_EPWV9UeERwdGplZXc/${pkgver}'
_url='https://launchpad.net/i2p/trunk/${pkgver}/+download'

source=("${_url}/i2psource_${pkgver}.tar.bz2"
        "${_url}/i2psource_${pkgver}.tar.bz2.sig"
        'i2prouter.service' 'i2prouter.sh' 'wrapper.config')

[[ $_i2p_fetch ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2"
            "http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2.sig"
            'i2prouter.service' 'i2prouter.sh' 'wrapper.config')
}
sha256sums=('20bcd1a62101b15b423ae6dd81a76b1806d67a28a5bf0f341803a7a61178d494'
            'SKIP'
            '842b529ae23bc82fd78e986dd7cc683bd7b671e93421de57f279dc3f4d5d0fd2'
            '30f60e516d13f9e0f94881f5c313dab4589f977d20536117f67ed2ee47ec1a97'
            '5c7df12c302b7f7e8d285cc22afe696eb71cf5660d1cdff58d25b4f8a47d3b77')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    source /etc/profile.d/apache-ant.sh
    source /etc/profile.d/jdk.sh

    ant preppkg-linux-only
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -dm755 "$pkgdir/usr/lib/tmpfiles.d"
    install -dm700 "$pkgdir/run/i2p"
    install -dm755 "$pkgdir/usr/bin"
    install -dm755 "$pkgdir/opt/i2p"

    cp -r pkg-temp/* "$pkgdir/opt/i2p"
    cp "$srcdir/wrapper.config" "$pkgdir/opt/i2p"

    install -Dm755 "$srcdir/i2prouter.sh"         "$pkgdir/opt/i2p/i2prouter"
    install -Dm644 "$srcdir/i2prouter.service"    "$pkgdir/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 "$pkgdir/opt/i2p/man/eepget.1" "$pkgdir/usr/share/man/man1/eepget.1"
    install -Dm644 "$pkgdir/opt/i2p/LICENSE.txt"  "$pkgdir/usr/share/licenses/i2p/LICENSE"
    mv "$pkgdir"/opt/i2p/licenses/*               "$pkgdir/usr/share/licenses/i2p/"

    ln -s /opt/i2p/{eepget,i2prouter} "$pkgdir/usr/bin/"
    chmod +x "$pkgdir"/opt/i2p/{eepget,i2prouter}

    echo 'router.updateDisabled=true' >"$pkgdir/opt/i2p/router.config"
    echo 'd /run/i2p 0700 i2p i2p'    >"$pkgdir/usr/lib/tmpfiles.d/i2prouter.conf"

    sed -i "$pkgdir"/opt/i2p/eepget \
        -e 's:%INSTALL_PATH:/opt/i2p:g'
    sed -i "$pkgdir"/opt/i2p/clients.config \
        -e "s:clientApp.4.startOnLoad=.*:clientApp.4.startOnLoad=false:"
    rm -r "$pkgdir"/opt/i2p/{osid,postinstall.sh,runplain.sh,INSTALL-headless.txt,LICENSE.txt,licenses,man,lib/wrapper*}
}
