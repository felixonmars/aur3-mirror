# Maintainer : Skydrome <irc.freenode.net>
# Contributor: veox <veox at wre dot ath dot cx>
# Contributor: Rorschach <r0rschach@lavabit.com>
# Contributor: Cedric Chabanois <cchabanois@gmail.com>

pkgname=i2p
pkgver=0.9
pkgrel=1
pkgdesc="A distributed anonymous network"
url="http://www.i2p2.de"
license=('GPLv2')
arch=('x86_64' 'i686')
depends=('java-runtime' 'gettext')
makedepends=('apache-ant' 'java-environment')
conflicts=('i2p-bin' 'i2p-git' 'i2p-portable' 'i2p-portable-source')
install='i2p.install'
source=("http://mirror.i2p2.de/i2psource_${pkgver}.tar.bz2"
        "http://mirror.i2p2.de/i2psource_${pkgver}.tar.bz2.sig"
        #"https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2"
        #"https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2.sig"
        "i2prouter"
        "i2prouter.service")
sha256sums=('8a3654a13781a9aacf9db94081e057be73322f88db2931eba4f2cfa467ead429'
            'a806a4a9de7549fba4aea41d5fd40ac71a90493e5d68c087ebf81d08671801d6'
            'f40c0996b4b0d83b443f1f616bc54bb18735872020e33a50b0f1fbad59c97d7d'
            'dee85e9a856b18bac257cd06e70f42e88edce3f48b92762410359a69d5c4284f')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    source /etc/profile.d/apache-ant.sh
    ant installer-linux
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    echo "INSTALL_PATH=$pkgdir/opt/i2p" > install.properties
    java -jar i2pinstall_${pkgver}*.jar -options install.properties

    sed -i "s|$pkgdir/opt/i2p|/opt/i2p|g"       $pkgdir/opt/i2p/{i2prouter,wrapper.config,runplain.sh,eepget}
    sed -i "s|#RUN_AS_USER=.*|RUN_AS_USER=i2p|" $pkgdir/opt/i2p/i2prouter

    install -Dm755 ${srcdir}/i2prouter               "${pkgdir}/etc/rc.d/i2prouter"
    install -Dm644 ${srcdir}/i2prouter.service       "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1    "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/man/i2prouter.1 "${pkgdir}/usr/share/man/man1/i2prouter.1"
    install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
    mv ${pkgdir}/opt/i2p/licenses/* LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/"
    rm -r $pkgdir/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,licenses,man}
}

