# $Id: PKGBUILD, 2009/06/6$
# Maintainer: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Xyne <ac xunilhcra enyx, backwards>
# Contributor: speeddefrost
# Contributor: stefanr
pkgname=alsaequal
pkgver=0.6
pkgrel=10
pkgdesc="A real-time adjustable equalizer plugin for ALSA sound server."
arch=(i686 x86_64)
url="http://www.thedigitalmachine.net/alsaequal.html"
license=('GPL2')
depends=('alsa-lib' 'ladspa')
optdepends=('caps: to use the Eq CAPS equalizer (10-band)'
'swh-plugins: to use the mbeq equalizer (15-band)')
install='alsaequal.install'
source=("http://www.thedigitalmachine.net/tools/alsaequal-${pkgver}.tar.bz2"
        'alsaequal.patch'
        'false_error.patch'
        'alsaequal_caps_9.x.patch')

build() {
  cd "$srcdir/$pkgname"
  patch < ../alsaequal.patch
  patch < ../false_error.patch
  patch < ../alsaequal_caps_9.x.patch
  make
}

package() {
  mkdir -p "$pkgdir/usr/lib/alsa-lib/"
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}

sha256sums=('916e7d152added24617efc350142438a46099efe062bd8781d36dbf10b4e6ff0'
            '2f3d928b0c0665c39ace25fd727bd12b6e81b2406811bd0744c09f804b6d25bd'
            'c1a02bfed6be6385e8ab4ebe7c9c3844e9c0937fbafe0f8d948842c2b7c9f7f0'
            'e6be205b818c32b68eaab1fa2e083b9f43e1d70cec4c0e1f78d1f2f84441270c')
