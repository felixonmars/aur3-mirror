# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=hodoku-build
pkgver=2.1.1
pkgrel=2
pkgdesc="A sudoku helper written in Java"
arch=('any')
url=('http://hodoku.sourceforge.net')
license=('GPL3')
depends=('java-runtime')
makedepends=('apache-ant' 'java-environment')
conflicts=('hodoku')

_PKGNAME=hodoku
_PKGCLASS=HoDoKu

source=("http://downloads.sourceforge.net/project/$_PKGNAME/$_PKGNAME/${_PKGNAME}_${pkgver}/$_PKGNAME-${pkgver//./-}-src.zip"
        "$_PKGNAME.sh"
        "$_PKGNAME.desktop")
md5sums=('c257b941d219ca3ae84c3bd6237e9a5e'
         'd29a1647df264d1379ae48783ff2eb38'
         '557ca158902fddf06cbda7c7c9480c08')

build()
{
    cd "$srcdir/$_PKGCLASS"
    ant jar
}

package()
{
    cd "$srcdir/$_PKGCLASS"
    install -m 644 -D "dist/$_PKGCLASS.jar" \
        "$pkgdir/usr/share/java/$_PKGNAME/$_PKGNAME.jar"
    install -m 644 -D "src/img/${_PKGNAME}02-256.png" \
        "$pkgdir/usr/share/pixmaps/$_PKGNAME.png"
    install -m 755 -D "$srcdir/$_PKGNAME.sh" \
        "$pkgdir/usr/bin/$_PKGNAME"
    install -m 644 -D "$srcdir/$_PKGNAME.desktop" \
        "$pkgdir/usr/share/applications/$_PKGNAME.desktop"
}
