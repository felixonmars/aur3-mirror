pkgname=pony-git
pkgver=0.1.5.r26.gc1af3d2
pkgrel=1

pkgdesc='An actor model, capabilities, high performance programming language.'
url='http://ponylang.org/'
arch=('x86_64')
license=('BSD')

options=('staticlibs')

depends=('zlib' 'ncurses' 'gcc-libs')
makedepends=('git' 'llvm' 'pcre2')

provides=('pony')
conflicts=('pony')

source=('git://github.com/CausalityLtd/ponyc')
install='pony-git.install'

md5sums=('SKIP')

pkgver() {
    cd ponyc
    git describe --tags | sed 's/-/.r/; s/-/./'
}

build() {
    cd ponyc
    make config=release prefix=/usr
}

check() {
    cd ponyc
    make config=release prefix=/usr test
}

package() {
    cd ponyc
    #make config=release prefix=/usr destdir="$pkgdir" install
    install -Dm755 build/release/ponyc "$pkgdir"/usr/bin/ponyc
    install -Dm644 build/release/libponyrt.a "$pkgdir"/usr/lib/libponyrt.a
    install -Dm644 build/release/libponyc.a "$pkgdir"/usr/lib/libponyc.a
    install -Dm644 src/libponyrt/pony.h "$pkgdir"/usr/include/pony.h
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    install -dm755 "$pkgdir"/usr/lib/pony
    cp -dr --no-preserve=ownership packages/* "$pkgdir"/usr/lib/pony

    install -dm755 "$pkgdir"/usr/share/doc/pony
    cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony
}
