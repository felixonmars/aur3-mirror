# Contributor: Marios Abatis <marios.archlinux@gmail.com>
pkgname=gnome-chemistry-utils
pkgver=0.12.11
_majorver=$(echo ${pkgver}|cut -c1-4)
pkgrel=1
pkgdesc="Gnome Chemistry Utilities (gcu). Includes gchempaint, a tool to draw molecules."
arch=('i686' 'x86_64')
url="http://www.nongnu.org/gchemutils/"
license=('GPL')

provides=('gchempaint' 'gchemcalc' 'gcrystal' 'gchem3d' 'gchemtable' 'gspectrum')
conflicts=('gnome-chemistry-utils-devel')
replaces=('gcu')   #Old package name
depends=('bodr' 'libglade' 'goffice>=0.8.0' 'gtkglext' 'openbabel' 'chemical-mime-data' 'hicolor-icon-theme')
	##Already fulfilled dependencies:
	#libgsf required by goffice
	#gconf required by goffice
	#cairo required by gtk2 required by gconf
	#shared-mime-info required by gtk2 required by gconf
makedepends=('pkgconfig' 'intltool' 'gettext' 'gnome-doc-utils' 'boost' 'desktop-file-utils' 'xulrunner')
optdepends=('yelp: Viewing help manuals' \
	    'gnome-doc-utils')
source=(http://download.savannah.gnu.org/releases/gchemutils/${_majorver}/${pkgname}-${pkgver}.tar.bz2)
options=(!libtool)
install=gnome-chemistry-utils.install
md5sums=('d819fc4a7bfc77f15d6c69e958e83018')

build() {

	cd ${srcdir}/${pkgname}-${pkgver}

	./configure	--prefix=/usr \
			--sysconfdir=/etc \
			--libexecdir=/usr/lib/${pkgname} \
			--disable-update-databases \
			--enable-mozilla-plugin \
			--with-mozilla-libdir=/usr/lib/mozilla

	make || return 1

	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1

	## Handle gconf schemas
	mkdir -p ${pkgdir}/usr/share/gconf/schemas
        gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas
        rm -rf ${pkgdir}/etc/gconf
}
