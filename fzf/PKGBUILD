# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf
pkgver=0.9.4
pkgrel=1
pkgdesc="Fuzzy finder for your shell"
arch=('i686' 'x86_64')
depends=('ncurses')
makedepends=('git' 'go' 'make')
optdepends=('ctags: fzf && select tags'
            'fish: fish compatible keybindings'
            'git: fzf && checkout git commits, branches'
            'tmux: fzf && select tmux sessions'
            'vim-fzf: fzf vim plugin'
            'xdg-utils: fzf && open files'
            'zsh: zsh compatible keybindings')
url="https://github.com/junegunn/fzf"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/junegunn/$pkgname/tar.gz/$pkgver
        fzf.fish
        fzf.sh
        fzf.zsh)
sha256sums=('1c4d2847a1793755f2e2fa31e80b95ddfe56f9b925e2bf804174534de000bedd'
            '8e514e349751ee355ad7b352f1373030a5218972b8c983555b594f01d16a8956'
            '2ee6c07e301a1bb8fad18038ab1439e39487bc993c128cfd823c80cd2d9a9835'
            '7d5e03b13d95f5ab0c22aa20f007393db0382003a1ff7b4fe5beed31498f0db0')
install=fzf.install

#_no_bash=1 # uncomment to save optional bash keybindings as fzf.sh.pacaur
#_no_bash=2 # uncomment to remove support for bash
#_no_fish=1 # uncomment to save optional fish keybindings as fzf.fish.pacaur
#_no_fish=2 # uncomment to remove support for fish
#_no_zsh=1 # uncomment to save optional zsh keybindings as fzf.zsh.pacaur
#_no_zsh=2 # uncomment to remove support for zsh

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building...'
  GOPATH="$srcdir" TMPDIR=/tmp go get -u github.com/junegunn/fzf/...
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing documentation...'
  for _doc in install uninstall README.md; do
    install -Dm 644 $_doc "$pkgdir/usr/share/doc/fzf/$_doc"
  done

  msg 'Installing executables...'
  install -Dm 755 "$srcdir/bin/fzf" "$pkgdir/usr/bin/fzf"
  #install -Dm 755 "$srcdir/bin/fzf-tmux" "$pkgdir/usr/bin/fzf-tmux"

  msg 'Placing optional completion and keybindings...'

  # bash
  if [[ "$_no_bash" == 1 ]]; then
    install -Dm 644 fzf-completion.bash "$pkgdir/usr/share/bash-completion/completions/fzf.pacaur"
    install -Dm 755 "$srcdir/fzf.sh" "$pkgdir/etc/profile.d/fzf.sh.pacaur"
  elif [[ "$_no_bash" == 2 ]]; then
    msg2 'Removing bash support'
  else
    install -Dm 644 fzf-completion.bash "$pkgdir/usr/share/bash-completion/completions/fzf"
    install -Dm 755 "$srcdir/fzf.sh" "$pkgdir/etc/profile.d/fzf.sh"
  fi

  # fish
  if [[ "$_no_fish" == 1 ]]; then
    install -Dm 644 "$srcdir/fzf.fish" "$pkgdir/usr/share/fish/functions/fzf.fish.pacaur"
  elif [[ "$_no_fish" == 2 ]]; then
    msg2 'Removing fish support'
  else
    install -Dm 644 "$srcdir/fzf.fish" "$pkgdir/usr/share/fish/functions/fzf.fish"
  fi

  # zsh
  if [[ "$_no_zsh" == 1 ]]; then
    install -Dm 755 "$srcdir/fzf.zsh" "$pkgdir/etc/profile.d/fzf.zsh.pacaur"
  elif [[ "$_no_zsh" == 2 ]]; then
    msg2 'Removing zsh support'
  else
    install -Dm 755 "$srcdir/fzf.zsh" "$pkgdir/etc/profile.d/fzf.zsh"
  fi
}
