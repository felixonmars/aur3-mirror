# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>

pkgname=fzf
pkgver=0.8.7
pkgrel=2
pkgdesc="Fuzzy finder for your shell"
arch=('any')
depends=('ruby' 'ruby-curses')
url="https://github.com/junegunn/fzf"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/junegunn/$pkgname/tar.gz/$pkgver
        fzf.sh)
sha256sums=('cd979113f1df2cd33d462dffc3e35e4a56926da8c3241d09bb2f64268d96f6cb'
            '06283acaad022455ef79fc43e744863c3cfac4956cdbdd1baaa1823dac290b41')
install=fzf.install

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing documentation...'
  for _doc in install uninstall README.md; do
    install -Dm 644 $_doc "$pkgdir/usr/share/doc/fzf/$_doc"
  done

  msg 'Installing executable...'
  install -Dm 755 fzf "$pkgdir/usr/bin/fzf"

  msg 'Installing bash completion...'
  install -Dm 644 fzf-completion.bash "$pkgdir/etc/bash_completion.d/fzf"

  msg 'Placing optional key bindings in /etc/profile.d...'
  install -Dm 755 "$srcdir/fzf.sh" "$pkgdir/etc/profile.d/fzf.sh"
}
