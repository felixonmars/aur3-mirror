# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>

pkgname=fzf
pkgver=0.8.7
pkgrel=3
pkgdesc="Fuzzy finder for your shell"
arch=('any')
depends=('ruby' 'ruby-curses')
optdepends=('ctags: fzf && select tags'
            'git: fzf && checkout git commits, branches'
            'tmux: fzf && select tmux sessions'
            'vim-fzf: fzf vim plugin'
            'xdg-utils: fzf && open files'
            'zsh: zsh compatible keybindings')
url="https://github.com/junegunn/fzf"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/junegunn/$pkgname/tar.gz/$pkgver
        fzf.sh
        fzf.zsh)
sha256sums=('cd979113f1df2cd33d462dffc3e35e4a56926da8c3241d09bb2f64268d96f6cb'
            '06283acaad022455ef79fc43e744863c3cfac4956cdbdd1baaa1823dac290b41'
            '7d5e03b13d95f5ab0c22aa20f007393db0382003a1ff7b4fe5beed31498f0db0')
install=fzf.install

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing documentation...'
  for _doc in install uninstall README.md; do
    install -Dm 644 $_doc "$pkgdir/usr/share/doc/fzf/$_doc"
  done

  msg 'Installing executable...'
  install -Dm 755 fzf "$pkgdir/usr/bin/fzf"

  msg 'Installing bash completion...'
  install -Dm 644 fzf-completion.bash "$pkgdir/usr/share/bash-completion/completions/fzf"

  msg 'Placing optional key bindings in /etc/profile.d...'
  install -Dm 755 "$srcdir/fzf.sh" "$pkgdir/etc/profile.d/fzf.sh"
  install -Dm 755 "$srcdir/fzf.zsh" "$pkgdir/etc/profile.d/fzf.zsh"
}
