# $Id: PKGBUILD 164246 2012-07-28 09:50:19Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=binx32-tcl
pkgver=8.5.12
pkgrel=1.2
pkgdesc="The Tcl scripting language (x32 ABI)"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}")
makedepends=("gcc-multilib-x32")
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
md5sums=('174b2b4c619ba8f96875d8a051917703')

build() {
  cd ${srcdir}/tcl${pkgver}/unix
  
  export CC="gcc -mx32"
  export CXX="g++ -mx32"
  export PKG_CONFIG_PATH="/usr/libx32/pkgconfig"

  sed -i '/sprintf(installLib/s:"lib:&x32:' tclUnixInit.c
  sed -i -e '/TCL_PACKAGE_PATH=/s:=.*:="/usr/libx32":' \
         -e "/^TCL_LIBRARY=/s:=.*:='/usr/libx32/tcl\$(VERSION)':" configure

  ./configure --prefix=/usr --mandir=/usr/share/man --libdir=/usr/libx32 --enable-threads
  
  make
}

check() {
  cd ${srcdir}/tcl${pkgver}/unix
  make test
}

package()
{
  cd ${srcdir}/tcl${pkgver}/unix
  
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  mv ${pkgdir}/usr/bin/tclsh8.5 ${pkgdir}/usr/bin/tclsh8.5-x32
  ln -sf tclsh8.5-x32 ${pkgdir}/usr/bin/tclsh-x32
  
  # install license
  install -Dm644 ../license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces
  sed -i \
    -e "s,^TCL_BUILD_LIB_SPEC='-L.*/unix,TCL_BUILD_LIB_SPEC='-L/usr/libx32," \
    -e "s,^TCL_SRC_DIR='.*',TCL_SRC_DIR='/usr/include'," \
    -e "s,^TCL_BUILD_STUB_LIB_SPEC='-L.*/unix,TCL_BUILD_STUB_LIB_SPEC='-L/usr/libx32," \
    -e "s,^TCL_BUILD_STUB_LIB_PATH='.*/unix,TCL_BUILD_STUB_LIB_PATH='/usr/libx32," \
    -e "s,^TCL_LIB_FILE='libtcl8.5..TCL_DBGX..so',TCL_LIB_FILE=\"libtcl8.5\$\{TCL_DBGX\}.so\"," \
    -e "s,^TCL_CC_SEARCH_FLAGS='\(.*\)',TCL_CC_SEARCH_FLAGS='\1:/usr/libx32'," \
    -e "s,^TCL_LD_SEARCH_FLAGS='\(.*\)',TCL_LD_SEARCH_FLAGS='\1:/usr/libx32'," \
    ${pkgdir}/usr/libx32/tclConfig.sh

  # remove duplicate include and manual files
  rm -rf ${pkgdir}/usr/{share,include}
}
