# $Id: pkgbuild-mode.el,v 1.23 2007/10/20 16:02:14 juergen Exp $
# Maintainer: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: dmotd <inaudible@simplesuperlativ.es>
pkgname=pd-extended-svn
pkgver=14664
pkgrel=1
pkgdesc="Pure Data Extended SVN version"
url="http://puredata.info/"
arch=('i686' 'x86_64' 'ppc')
license=('BSD')
depends=('libv4l' 'fftw' 'jack-audio-connection-kit' 'tk' 'freeglut' \
  'libquicktime' 'libdv' 'gsl' 'imagemagick' \
  'ftgl' 'libgl' 'dssi')
makedepends=('subversion' 'swig' 'automake' 'curl')
conflicts=('pd' 'pd-gem' 'pdp' 'zexy')
provides=('pd' 'pd-extended' 'pd-gem=0.91.3' 'pdp' 'zexy')
replaces=(pd-extended)
backup=()
options=('!makeflags')
install=
source=()
md5sums=()

# MAIN SVN TRUNK
_svntrunk=https://pure-data.svn.sourceforge.net/svnroot/pure-data/trunk/
_svnmod=pd-extended
# PD SOURCES
_svnpdvanilla=https://pure-data.svn.sourceforge.net/svnroot/pure-data/trunk/pd/
_svnextendedbranch=https://pure-data.svn.sourceforge.net/svnroot/pure-data/branches/pd-extended/0.42/pd/
_svnguirewritebranch=https://pure-data.svn.sourceforge.net/svnroot/pure-data/branches/pd-gui-rewrite/0.43/

build() {
  unset CFLAGS
  unset LDFLAGS
  unset INCLUDES
  
  msg "Begin SVN checkout"
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up && svn switch $_svnextendedbranch pd)
  else
    (svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod && \
      cd $_svnmod && svn switch $_svnextendedbranch pd && cd ..)
  fi

  msg "SVN checkout done or server timeout"

  msg "Starting make..."
  cp -r $_svnmod $_svnmod-build 
  cd $startdir/src/$_svnmod-build

# hdspm_mixer disabled for now - doesn`t build
  sed -e "s|gem2pdp hdspm_mixer iem16|gem2pdp iem16|g" \
    -i externals/Makefile || return 1
# build gem with v4l2-support
  sed -e "s|--disable-NV|--disable-NV --with-v4l2|g" \
    -i packages/Makefile || return 1
# fix tcl version detection in tclpd Makefile
  sed -e "s|/usr/include/tcl8.|/usr/lib/tcl8.|g" \
    -i externals/tclpd/Makefile || return 1

# fix for x86_64
if [ "$CARCH" = "x86_64" ]; then
# fix -fPIC issue in PDP
  sed -e "s|CFLAGS =|CFLAGS = -fPIC|" \
    -i externals/pdp/opengl/Makefile.config || return 1
# fix -fPIC issue in pddp
  sed -e "s|DEFINES =|DEFINES = -fPIC|" \
    -i externals/miXed/Makefile.common || return 1
# setting additional variable
  FPIC_FLAG="-fPIC"
  else FPIC_FLAG=""
fi

# build and install
  cd packages/linux_make || return 1

  make \
    -C $srcdir/$_svnmod-build/packages \
    GEM_EXTRA_CXXFLAGS="$FPIC_FLAG" \
    BUILDLAYOUT_DIR=$srcdir/$_svnmod-build/packages \
    cvs_root_dir=$srcdir/$_svnmod-build \
    DESTDIR=$pkgdir \
    prefix=/usr \
    UNAME=Linux \
    install || return 1

  cd $srcdir/$_svnmod-build
# PD License
  install -Dm644 pd/LICENSE.txt \
    $pkgdir/usr/share/licenses/pd-extended-svn/LICENSE.txt
  cd packages/
  install -p linux_make/default.pdextended $pkgdir/usr/lib/pd-extended/
# Gnome menu support
  install -d $pkgdir/usr/share/icons/hicolor/128x128/apps
  install -p -m0644 linux_make/pd.png \
    $pkgdir/usr/share/icons/hicolor/128x128/apps/
  install -d $pkgdir/usr/share/icons/hicolor/48x48/apps
  install -p -m0644 linux_make/pd-48x48.png \
    $pkgdir/usr/share/icons/hicolor/48x48/apps/pd.png
  install -d $pkgdir/usr/share/applications/ 
  install -p linux_make/pd-extended.desktop \
    $pkgdir/usr/share/applications/
# files for /etc
  cd $srcdir/$_svnmod-build/
  install -d $pkgdir/etc/bash_completion.d/
  install -p scripts/bash_completion/pd $pkgdir/etc/bash_completion.d
# emacs mode for .pd files
  install -d $pkgdir/usr/share/emacs/site-lisp/
  install -p scripts/pd-mode.el $pkgdir/usr/share/emacs/site-lisp/
# Pd-related scripts
  install -p scripts/pd-diff $pkgdir/usr/bin/
  install -p scripts/config-switcher.sh $pkgdir/usr/bin/

  rm -rf $srcdir/$_svnmod-build
}
