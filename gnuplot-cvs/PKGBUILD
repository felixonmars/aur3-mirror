# Contributor: Stephen Caraher <moskvax@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=gnuplot-cvs
pkgver=20121208
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A command-line driven interactive function and data plotting utility -- cvs version"
url="http://www.gnuplot.info/"
license=('custom')
depends=('gd' 'pango' 'lua' 'wxgtk')
makedepends=('cvs' 'libpng' 'freetype2' 'libjpeg-turbo')
conflicts=('gnuplot')
provides=('gnuplot=4.5')
install=$pkgname.install
options=('zipman')
source=()
md5sums=()

_cvsmod="gnuplot"
_cvsroot=":pserver:anonymous:@gnuplot.cvs.sourceforge.net:/cvsroot/$_cvsmod"

build() {
  cd $srcdir
  msg "Connecting to gnuplot.cvs.sourceforge.net..."
  if [ -d $_cvsmod/CVS ]; then
    echo "fetching only new files"
    cd $_cvsmod
    cvs -z3 update -d
  else
    echo "fetching whole branch"
    cvs -z3 -d$_cvsroot co -D $pkgver -P $_cvsmod
  fi

  msg "CVS checkout done or server timeout"
  [[ -d $srcdir/$_cvsmod-build ]] && rm -fr $srcdir/$_cvsmod-build
  cp -r $srcdir/$_cvsmod $srcdir/$_cvsmod-build

  cd $srcdir/$_cvsmod-build/gnuplot

  msg "Running prepare script..."
  ./prepare
  # fix default source location; use the GDFONTPATH variable 
  # to modify at runtime 
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' \
    src/variable.c
  sed -i 's|/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' \
    src/variable.c
  
  msg "Running configure script..."
  ./configure --prefix=/usr --with-gihdir=/usr/share/gnuplot --with-readline=gnu \
    --with-kpsexpand --enable-thin-spline --with-lua 
  make
}
package() {
  cd $srcdir/$_cvsmod-build/gnuplot
  make DESTDIR=$pkgdir X11_DRIVER_DIR=$pkgdir/usr/bin install

  install -D ../$_cvsmod/Copyright \
    $pkgdir/usr/share/licenses/$pkgname/Copyright
}
