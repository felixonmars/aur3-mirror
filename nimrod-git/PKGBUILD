pkgname=nimrod-git
pkgver=20120621
pkgrel=2
pkgdesc="Nimrod is a new statically typed, imperative programming language, that supports procedural, object oriented, functional and generic programming styles while remaining simple and efficient. (git)"
arch=(i686 x86_64)
url="http://nimrod-code.org"
depends=(glibc readline)
license=("GPL" "LGPL")
makedepends=(git unzip)
conflicts=(nimrod)
provides=(nimrod)
backup=("etc/nimdoc.cfg"
"etc/nimdoc.tex.cfg"
"etc/nimrod.cfg")
_gitroot="https://github.com/Araq/Nimrod.git"
_gitname="nimrod"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i -e 's/^OS:.*$/OS: "linux"/' -e 's/^CPU:.*$/CPU: "i386;amd64"/' "compiler/nimrod.ini"
  cd build
  unzip "csources.zip"
  cd ..
  ./build.sh
  ./bin/nimrod c koch.nim
  ./koch boot -d:release
  PATH=./bin:$PATH ./koch csource
  cd compiler
  ../bin/nimrod c -d:release c2nim/c2nim.nim
  ../bin/nimrod c -d:release pas2nim/pas2nim.nim
  cd ..
  cd lib
  ../bin/nimrod c --app:lib -d:createNimRtl -d:release nimrtl.nim
}

package() {
  cd "$srcdir/$_gitname-build"
  ./install.sh "$pkgdir/"
  mkdir -p "$pkgdir/usr/share/doc/nimrod"
  mkdir -p "$pkgdir/usr/lib/nimrod"
  mkdir -p "$pkgdir/etc"
  mv "$pkgdir/nimrod/lib/"* "$pkgdir/usr/lib/nimrod"
  mv "$pkgdir/nimrod/config/"* "$pkgdir/etc"
  mv "$pkgdir/nimrod/doc/"* "$pkgdir/usr/share/doc/nimrod"
  rmdir "$pkgdir/nimrod/config"
  rmdir "$pkgdir/nimrod/doc"
  rmdir "$pkgdir/nimrod/lib"
  mv "$pkgdir/nimrod/bin" "$pkgdir/usr/"
  rmdir "$pkgdir/nimrod"
  install -m755 "compiler/c2nim/c2nim" "$pkgdir/usr/bin/c2nim"
  install -m755 "compiler/pas2nim/pas2nim" "$pkgdir/usr/bin/pas2nim"
  install -m755 "lib/libnimrtl.so" "$pkgdir/usr/lib/libnimrtl.so"
} 
