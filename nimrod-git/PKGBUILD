pkgname=nimrod-git
pkgver=v0.9.0.888
pkgrel=1
pkgdesc="Nimrod is a new statically typed, imperative programming language, that supports procedural, object oriented, functional and generic programming styles while remaining simple and efficient. (git)"
arch=(i686 x86_64)
url="http://nimrod-code.org"
depends=(glibc readline zlib)
license=("GPL" "LGPL")
makedepends=(git)
conflicts=(nimrod)
provides=(nimrod)
backup=("etc/nimdoc.cfg"
"etc/nimdoc.tex.cfg"
"etc/nimrod.cfg")
source=("${pkgname%-*}::git+https://github.com/Araq/Nimrod.git"
"https://github.com/nimrod-code/csources.git")
md5sums=('SKIP' 'SKIP')

pkgver () {
	cd "$srcdir/${pkgname%-*}"
	git describe | sed 's|\(.*-.*\)-.*|\1|;s|-|.|'
}

build() {
	cd "${srcdir}/${pkgname%-*}/"
	cp -r "$srcdir/csources/"* "build/"
	chmod +x "build/build.sh"
	cd build
	./build.sh
	cd ..
	./bin/nimrod c koch.nim
	./koch boot -d:release -d:useGnuReadline
	export PATH="$PATH:../bin/nimrod"
	cd compiler
	../bin/nimrod c -d:release c2nim/c2nim.nim
	../bin/nimrod c -d:release pas2nim/pas2nim.nim
	cd ../lib
	../bin/nimrod c --app:lib -d:createNimRtl -d:release nimrtl.nim
	cd ../tools
	../bin/nimrod c -d:release nimgrep.nim
}

package() {
	cd "${srcdir}/${pkgname%-*}"
	export PATH="$PATH:$PWD/bin"
	./koch install $pkgdir
	mkdir -p "$pkgdir/usr/share/nimrod/doc" "$pkgdir/usr/lib/nimrod" "$pkgdir/etc" "$pkgdir/usr/bin"
	mv "$pkgdir/nimrod/lib/"* "$pkgdir/usr/lib/nimrod/"
	mv "$pkgdir/nimrod/config/"* "$pkgdir/etc/"
	cp -a "lib/packages" "$pkgdir/usr/lib/nimrod/"
	mv "$pkgdir/nimrod/doc/"* "$pkgdir/usr/share/nimrod/doc/"
	mv "$pkgdir/nimrod/bin/"* "$pkgdir/usr/bin/"
	rm -r "$pkgdir/nimrod"
	install -m755 "compiler/c2nim/c2nim" "$pkgdir/usr/bin/"
	install -m755 "compiler/pas2nim/pas2nim" "$pkgdir/usr/bin/"
	install -m644 "lib/libnimrtl.so" "$pkgdir/usr/lib/libnimrtl.so"
	install -m755 "tools/nimgrep" "$pkgdir/usr/bin/"
}
