pkgname=nodejs-unstable
pkgver=0.11.8
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl')
makedepends=('python2')
checkdepends=('curl')
provides=('nodejs')
conflicts=('nodejs')
options=('!emptydirs')
source=(http://nodejs.org/dist/v$pkgver/node-v$pkgver.tar.gz)
sha256sums=('87c809dea764d5d66f925626fba403fb2fb0c0ccfad408bf79fdb62dc246d65b')

prepare() {
  cd node-v$pkgver

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd node-v$pkgver

  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --shared-openssl

  make
}

check() {
  cd node-v$pkgver
  make test || true
}

package() {
  cd node-v$pkgver

  make DESTDIR="$pkgdir" install
}
