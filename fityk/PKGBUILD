# Maintainer: masutu < masutu dot arch at gmail dot com >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Cuneyt Unlu <unlucu AT gmail.com>

pkgname=fityk
pkgver=1.2.1
pkgrel=2
pkgdesc="A program for nonlinear fitting of analytical functions to data."
arch=(i686 x86_64)
url="http://fityk.nieto.pl/"
license=('GPL')
depends=('lua' 'wxgtk' 'xylib')
makedepends=('boost' 'python2-sphinx' 'swig')
options=(!libtool)
install=fityk.install
source="https://github.com/downloads/wojdyr/fityk/fityk-1.2.1.tar.bz2"
md5sums=('ba09bd91d08d125e8c6b0069e5c56ea5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's#sphinx-build#sphinx-build2#' doc/Makefile
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
