# Maintainer: Stefan J. Betz <info@stefan-betz.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=dar
pkgver=2.4.5
pkgrel=1
pkgdesc='A full featured command-line backup tool, short for Disk ARchive'
arch=('i686' 'x86_64')
url='http://dar.linux.free.fr/'
license=('GPL')
depends=('bzip2' 'lzo2' 'libgcrypt')
optdepends=('perl: for running some samples')
makedepends=('doxygen')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
options=('!libtool')
backup=('etc/darrc')
md5sums=('f799a39202c4c15ae0d3cca80a089338')
sha256sums=('2badd2e7de9cf56554431f6f99bb93be1a26395fd9829603e63aa799108f28e1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d $pkgdir/usr/share/doc/$pkgname
  make DESTDIR="$pkgdir" install 
  cd $pkgdir/usr/share/$pkgname
  mv *.html  $pkgdir/usr/share/doc/$pkgname
  rm -r man
  for _i in html mini-howto samples 
  do
    mv ${_i} $pkgdir/usr/share/doc/$pkgname
  done
}
