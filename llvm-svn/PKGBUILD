# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Anders Bergh <anders@archlinuxppc.org>
# Contributor: Tomas Wilhelmsson <tomas.wilhelmsson@gmail.com>

pkgname=llvm-svn
pkgver=154261
pkgrel=1
pkgdesc='Low Level Virtual Machine - Compiler infrastructure.'
arch=('i686' 'x86_64')
url="http://llvm.org"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('libffi' 'python2' 'ocaml')
makedepends=('subversion')
provides=('llvm')
conflicts=('llvm')

# this is always the latest svn so debug info can be useful
options=('!strip')

_svntrunk='http://llvm.org/svn/llvm-project/llvm/trunk'
_svnmod='llvm'

build() {
  if [ -d ${_svnmod} ]; then
    cd ${_svnmod}
    svn up -r ${pkgver}
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi

  msg 'SVN checkout done or server timeout'
  msg 'Starting make...'

  cd ${srcdir}

  sed -i 's/python$/python2/' ${_svnmod}/utils/llvm-build/llvm-build

  rm -rf build
  mkdir -p build
  cd build

  # Apply strip option to configure
  _optimized_switch="enable"
  [[ $(check_option strip) == n ]] && _optimized_switch="disable"

  # Include location of libffi headers in CPPFLAGS
  export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags libffi)"

  # Force the use of GCC instead of clang
  PYTHON=python2 CC=gcc CXX=g++ \
  ../${_svnmod}/configure \
    --prefix=/usr \
    --libdir=/usr/lib/llvm \
    --sysconfdir=/etc \
    --enable-shared \
    --enable-libffi \
    --enable-targets=all \
    --disable-expensive-checks \
    --with-binutils-include=/usr/include \
    --$_optimized_switch-optimized

  make REQUIRES_RTTI=1
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install

  # Fix permissions of static libs
  #chmod -x "$pkgdir"/usr/lib/llvm*.a

  #install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
