# Maintainer: Christian Babeux <christian.babeux@0x80.ca>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Anders Bergh <anders@archlinuxppc.org>
# Contributor: Tomas Wilhelmsson <tomas.wilhelmsson@gmail.com>

pkgname=llvm-svn
_pkgname='llvm'
pkgver=183418
pkgrel=1
pkgdesc='Low Level Virtual Machine - Compiler infrastructure.'
arch=('i686' 'x86_64')
url="http://llvm.org"
license=('custom:University of Illinois')
makedepends=('subversion' 'libffi' 'python2' 'ocaml')
provides=('llvm')
conflicts=('llvm')
source=("${_pkgname}::svn+http://llvm.org/svn/llvm-project/llvm/trunk")
sha1sums=('SKIP')
# this is always the latest svn so debug info can be useful
options=('!strip')

pkgver()
{
    cd "$SRCDEST/${_pkgname}"
    svnversion | tr -d [A-z]
}

build()
{
    cd "${srcdir}/${_pkgname}"

    # Apply strip option to configure
    _optimized_switch="enable"
    [[ $(check_option strip) == n ]] && _optimized_switch="disable"

    # Include location of libffi headers in CPPFLAGS
    export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags libffi)"

    # Force the use of GCC instead of clang
    PYTHON=/usr/bin/python2 CC=gcc CXX=g++ \
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib/llvm \
        --sysconfdir=/etc \
        --enable-shared \
        --enable-libffi \
        --enable-targets=all \
        --disable-expensive-checks \
        --with-binutils-include=/usr/include \
        --$_optimized_switch-optimized

    make REQUIRES_RTTI=1
}

package()
{
    cd "${srcdir}/${_pkgname}"
    make install DESTDIR=${pkgdir}
}
