# Maintainer: Jan Dolinar <dolik.rce@gmail.com>

pkgname=theide-nogtk-svn
pkgver=5390
pkgrel=1
pkgdesc="Modern IDE designed for developping large U++/C++ applications (nogtk version)"
arch=('i686' 'x86_64')
url="http://www.ultimatepp.org"
license=('BSD')
groups=()
depends=('libxft' 'libpng' 'desktop-file-utils')
makedepends=('subversion')
provides=('theide')
conflicts=('theide')
replaces=()
backup=()
options=(!makeflags emptydirs)
install='theide.install'
source=('license.txt')
noextract=()

_svntrunk="http://upp-mirror.googlecode.com/svn/trunk/"
# many users have already working copy of U++ on their system, so they
# can use it for building (e.g. to save network traffic or to speed up
# things) by setting $UPPSVN environment variable
if [ "x$UPPSVN" != "x" ]
then
  _svnmod="$UPPSVN"
else
  _svnmod="$srcdir/uppsvn"
fi
build() {
  cd "$srcdir"
  #get sources
  for n in uppsrc uppbox/lpbuild2
  do
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod/$n && svn up -r $pkgver)
    else
      svn co $_svntrunk$n/ --config-dir ./ -r $pkgver $_svnmod/$n
    fi
  done
  msg2 "SVN checkout done (or server timeout)"
  #build
  mv "$_svnmod/uppsrc/ide/version.h" "$_svnmod/uppsrc/ide/version.h~"
  echo "#define IDE_VERSION \"$pkgver-Arch-$(uname -m)\"" > "$_svnmod/uppsrc/ide/version.h"
  msg2 "Building $pkgname..."
  make -f "$_svnmod/uppbox/lpbuild2/mkfile" PKG=umk FLAGS="GCC" NESTS="$_svnmod/uppsrc" TIME= COLOR=0 USEMAINCFG=0 JOBS=5 ECHO=0 TARGET="$srcdir/umk"
  make -f "$_svnmod/uppbox/lpbuild2/mkfile" PKG=ide FLAGS="GCC GUI NOGTK" NESTS="$_svnmod/uppsrc" TIME= COLOR=0 USEMAINCFG=0 JOBS=5 ECHO=0 TARGET="$srcdir/theide"
  mv "$_svnmod/uppsrc/ide/version.h~" "$_svnmod/uppsrc/ide/version.h"
}

package(){
  #license
  install -D "$srcdir/license.txt" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
  #man page
  install -D "$_svnmod/uppbox/lpbuild2/umk.1" "$pkgdir/usr/share/man/man1/umk.1"
  install -D "$_svnmod/uppbox/lpbuild2/theide.1" "$pkgdir/usr/share/man/man1/theide.1"
  #desktop entry
  install -D "$_svnmod/uppbox/lpbuild2/theide.desktop" "$pkgdir/usr/share/applications/theide.desktop"
  #icon
  install -D "$_svnmod/uppsrc/ide/theide-48.png" "$pkgdir/usr/share/pixmaps/theide.png"
  #fix permissions
  find "$pkgdir/usr/" -type f -exec chown root:root {} \; -exec chmod 644 {} \;
  #install applications
  install -D "$srcdir/umk" "$pkgdir/usr/bin/umk"
  install -D "$srcdir/theide" "$pkgdir/usr/bin/theide"
}


md5sums=('b214709f096e4f50d61f50988359241e')
