pkgname=mingw-w64-openjpeg
pkgver=1.5.1
pkgrel=3
pkgdesc="An open source JPEG 2000 codec (mingw-w64)"
arch=(any)
url="http://www.openjpeg.org"
license=("BSD")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-lcms2
mingw-w64-libtiff
mingw-w64-libpng)
options=(!libtool !strip !buildflags)
source=("http://openjpeg.googlecode.com/files/openjpeg-${pkgver}.tar.gz"
"cdecl.patch")
md5sums=('b5f74cec2688fb918331bb014061be6f'
         '673edf457f9b4f3fdc9a083e1857a76a')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/openjpeg-$pkgver"
  rm -r thirdparty
  patch -Np1 < $srcdir/cdecl.patch
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-jpwl
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}