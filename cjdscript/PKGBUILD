# Maintainer: Prurigro

pkgname=cjdscript
pkgver=20140617.r15.0cad511
pkgrel=2
pkgdesc="Script to maintain and execute the collection of (working) contrib features included with cjdns"
url="https://github.com/prurigro/${pkgname}"
license=('GPL3')
arch=('any')
depends=('bash')
makedepends=('git')
optdepends=('python2: needed for access to the python2 scripts and building binaries'
            'nodejs: needed for access to the nodejs scripts and building binaries')
source=("git://github.com/prurigro/${pkgname}.git#branch=master" "${pkgname}.1")
sha512sums=('SKIP' '67fd4f0ad736c7acc226a7b0a392e4e718e3188ce88153072f878478a830b60c3cec97d1b096a9916902c33e18cac963c90ab05525eba90e9a78a44e8578788a')

pkgver() {
    cd ${pkgname}
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    install -Dm755 ${pkgname}/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 ${pkgname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
