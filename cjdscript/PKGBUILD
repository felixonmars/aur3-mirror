# Maintainer: Prurigro

pkgname=cjdscript
pkgver=20140613.r13.af0e2ec
pkgrel=1
pkgdesc="Script to maintain and execute the collection of (working) contrib features included with cjdns"
url="https://github.com/prurigro/${pkgname}"
license=('GPL3')
arch=('any')
depends=('bash')
makedepends=('git')
optdepends=('python2: needed for access to the python2 scripts and building binaries'
            'nodejs: needed for access to the nodejs scripts and building binaries')
source=("git://github.com/prurigro/${pkgname}.git#branch=master" "${pkgname}.1.gz")
sha512sums=('SKIP' 'b1af8144f829b5ea8f0dab6a7c65293d875b6ee759f4cc6b1032a6d87e14923e410d20db2300d73fcad8046fe87af7a095ca183952d2d683afe64c2374a2b122')

pkgver() {
    cd ${pkgname}
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    install -Dm755 ${pkgname}/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 ${pkgname}.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
}
