# Maintainer: Ner0

pkgname=elementary-icon-theme-bzr
pkgver=1136
pkgrel=1
pkgdesc="An icon theme designed to be smooth, sexy, clear, and efficient (Development branch)"
arch=('any')
url="https://launchpad.net/elementaryicons"
license=('GPL3')
groups=('pantheon')
depends=('gtk-update-icon-cache' 'hicolor-icon-theme')
makedepends=('bzr')
conflicts=('elementary-icon-theme' 'elementary-icons-bzr')
provides=('elementary-icon-theme')
options=('!strip' '!emptydirs')
install=elementary-icon-theme.install
source=('bzr+lp:elementaryicons')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/elementaryicons"
  bzr revno
}

package() {
  install -dm755 "$pkgdir/usr/share/icons"
  cp -dr --no-preserve=ownership "$srcdir/elementaryicons" "$pkgdir/usr/share/icons/elementary"
  rm -rf "$pkgdir"/usr/share/icons/elementary/{.bzr*,AUTHORS,CONTRIBUTORS,COPYING}
}
