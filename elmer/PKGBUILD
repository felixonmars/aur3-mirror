# Maintainer: sudokode <sudokode@gmail.com>

pkgname=elmer
pkgver=67.2021ad3
pkgrel=2
pkgdesc="Robust frontend to wget and/or curl for various pastebin services"
arch=('any')
url="http://github.com/sudokode/elmer"
license=('custom:SPL')
makedepends=('git')
depends=('perl>=5.10' 'perl-uri')
optdepends=('wget: pasting with wget'
            'curl: pasting with curl and uploading files'
            'xclip: reading from and printing to X buffer')
source=("$pkgname::git://github.com/sudokode/elmer.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

package() {
  cd $pkgname

  install -D -m755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -D -m755 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  find "$pkgdir" -type d -name '.git' -exec rm -r '{}' +
}

# vim:set ts=2 sw=2 et:
