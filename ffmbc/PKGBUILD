# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=ffmbc
pkgver="0.7_rc8"
pkgrel=1
pkgdesc="FFmpeg customized for broadcast and professional usage"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmbc/"
license=('GPL2' 'custom')
depends=('faac' 'lame' 'libass' 'x264-git' 'zlib')
makedepends=('yasm')
source=("http://ffmbc.googlecode.com/files/FFmbc-${pkgver//_/-}.tar.bz2")
sha1sums=('85a9673ac82a698bb96057fe027222efe6ebae28')

build() {
  cd "${srcdir}/FFmbc-${pkgver//_/-}"

  ./configure \
    --prefix=/usr \
    --incdir=/usr/include/ffmbc \
    --libdir=/usr/lib/ffmbc \
    --shlibdir=/usr/lib/ffmbc \
    --disable-ffplay \
    --disable-ffprobe \
    --enable-gpl \
    --enable-libass \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libx264 \
    --enable-nonfree

 
  make
  make doc/ffmbc.1
}

package() {
  cd "${srcdir}/FFmbc-${pkgver//_/-}"
  make DESTDIR="${pkgdir}" install install-man
}
