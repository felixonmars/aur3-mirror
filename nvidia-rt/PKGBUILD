# $Id: PKGBUILD 146818 2012-01-18 17:17:23Z tpowa $
# Maintainer:  Morgan Cox <morgancoxuk@gmail.com>
# Contributor: Joakim Hernberg <jbh@alchemy.lu>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-rt
pkgver=295.40
_extramodules=extramodules-3.2-rt
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=2
pkgdesc="NVIDIA drivers for linux-rt."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-rt>=3.2' 'linux-rt<3.3' "nvidia-utils=${pkgver}")
makedepends=('linux-rt-headers>=3.2' 'linux-rt-headers<3.3')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
            "patch-3.0-rt-nvidia.patch")
    md5sums=('0bc1561b405443c6d1331934093137fd'
             'f09b5fb3edea108ddacd15d8fd4fc6d3')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
            "patch-3.0-rt-nvidia.patch")
    md5sums=('dde8347cde142c329b6bb923747672a3'
             'f09b5fb3edea108ddacd15d8fd4fc6d3')
fi

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    patch -Np1 -i $srcdir/patch-3.0-rt-nvidia.patch
    make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-nvidia-rt.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
