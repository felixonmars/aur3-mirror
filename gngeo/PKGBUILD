# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=gngeo
pkgver=0.8
pkgrel=4
pkgdesc="A command-line NeoGeo Emulator."
url="http://gngeo.googlecode.com"
license="custom"
arch=('i686' 'x86_64')

if [ $CARCH == i686 ]; then
  depends=('zlib' 'sdl' 'libgl')
  makedepends=('nasm')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-zlib' 'lib32-sdl' 'lib32-libgl')
  makedepends=('nasm' 'gcc-multilib')
fi

backup=(usr/share/${pkgname}/sample_gngeorc)
install=gngeo.install
source=(${url}/files/${pkgname}-${pkgver}.tar.gz
        4dec1ccfb85d.patch)
md5sums=('21055885694e9f6cdbaa20be069a4258'
         'cfa16491e9e5185afec9342be49d55d1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -p2 -i "${srcdir}/4dec1ccfb85d.patch"
  sed 's#games/lib#share#' -i sample_gngeorc
  
  if [ $CARCH == i686 ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man  
  elif [ $CARCH == x86_64 ]; then
    sed 's#/usr/lib/libGL.so#/usr/lib32/libGL.so#' -i sample_gngeorc
    ./configure --prefix=/usr --mandir=/usr/share/man \
        CC='gcc -m32' SDL_CONFIG='/usr/bin/sdl-config-32'
  fi

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 sample_gngeorc "${pkgdir}/usr/share/${pkgname}/sample_gngeorc"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
