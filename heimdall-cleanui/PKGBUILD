# Contributor: Todd Musall
# Contributor: dront78 (Ivan)
# Contributor: Victor Noel
# Contributor (Maintainer of source package): ngoonee
# Maintainer: Prurigro

_pkgname="heimdall"
pkgname="${_pkgname}-cleanui"
pkgver=1.4.0
_pkgrev="910ae1d"
pkgrel=1
pkgdesc="A cross-platform utility to flash firmware (aka ROMs) onto Samsung devices - patched to remove donation menu"
arch=('i686' 'x86_64')
url="http://www.glassechidna.com.au/products/heimdall/"
license=('MIT')
depends=('libusb' 'qt4')
optdepends=('android-udev: Udev rules to connect Android devices to your linux box')
makedepends=('gcc')
conflicts=('heimdall')
provides=('heimdall')
instal="heimdall-cleanui.install"
source=("heimdall-${pkgver}.tar.gz::http://github.com/Benjamin-Dobell/Heimdall/tarball/910ae1d1fed7b608239b344b6d92c9165a0b8d52"
        'no-donation.patch'
        'heimdall.desktop'
        'heimdall.png')
md5sums=('72c203839571d9c4e4adf798cc89fe4c'
         'fee1580ad99bdffdc1c109c96cd7f3a7'
         '6c4de9e74c0d9e7ab4d50af21303b78a'
         '8dd4b61053db6826e970aa0b21168fb7')

prepare() {
    cd "${srcdir}/Benjamin-Dobell-Heimdall-${_pkgrev}"

    # Remove all that donation spam from the GUI - please consider donating @ http://glassechidna.com.au/donate/
    patch -p1 < ../no-donation.patch
}

build() {
    cd "${srcdir}/Benjamin-Dobell-Heimdall-${_pkgrev}"

    # Build libpit which is needed for compiling heimdall
    pushd libpit > /dev/null 2>&1
        ./configure --prefix=/usr
        make
    popd > /dev/null 2>&1

    # Build heimdall command line tool
    pushd heimdall > /dev/null 2>&1
        ./configure --prefix=/usr
        make
    popd > /dev/null 2>&1

    # Build heimdall GUI front end
    pushd heimdall-frontend > /dev/null 2>&1
        env OUTPUTDIR="/usr/bin" qmake-qt4 heimdall-frontend.pro
        make
    popd
}

package() {
    cd "${srcdir}/Benjamin-Dobell-Heimdall-${_pkgrev}"

    # Install license file
    install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"

    # Install heimdall command line tool
    pushd heimdall > /dev/null 2>&1
        # Prevent make install from trying to reload udev
        # We'll do this the Arch way at package install time
        mv Makefile Makefile.orig
        sed -e 's/sudo service udev restart/echo sudo service udev restart/' <Makefile.orig >Makefile

        make DESTDIR="${pkgdir}" install
        rm -rf "${pkgdir}/lib/"
    popd > /dev/null 2>&1

    # Install heimdall GUI front end
    pushd heimdall-frontend > /dev/null 2>&1
        # hack to place heimdall-frontend in /usr/bin
        sed -i 's|local\/||g' Makefile
        make INSTALL_ROOT="${pkgdir}/" install
    popd > /dev/null 2>&1

    install -m644 -D "../heimdall.desktop" "${pkgdir}/usr/share/applications/heimdall.desktop"
    install -m644 -D "../heimdall.png" "${pkgdir}/usr/share/pixmaps/heimdall.png"
}
