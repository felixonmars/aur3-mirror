# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=gmpc
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="A gnome based GUI client for MPD"
arch=(i686 x86_64)
url=http://www.gmpclient.org
license=(GPL)
depends=(glyr-git hicolor-icon-theme lib{mpd-git,soup,unique} sqlite)
makedepends=(git gnome-doc-utils gob2 intltool vala)
provides=($_pkgname=11.8.16)
conflicts=($_pkgname{,-svn})
options=(!libtool !makeflags)
install=$pkgname.install
source=($pkgname-{ref-error,treeview-undefined}.patch)
sha256sums=('fb3fc96ac823745347a71405f2179ef0ad824382fed3d4e10726f570e7836181'
    '97004814e796b504d0a7aeed5ba715933bd3716981718dbf06c8b509d4765ef0')
sha512sums=('7a0c55084ae17cca1d78ee8fb3c75757035fddda21bb7b8ecf2b94cafb2e0fc851b604a7a26bd24f0dc5fb9701edffc4190b8031ab465720eb9bbfac4f1c72af'
    '9e360dab2768c0a75374bbe08d920ba7d5a3260a41196c711999b4faba155962e33dbfe485bf133a02ca9bb7b5fb1f1270fa63bc3e812f5fdd3f705064be9656')

_gitroot=git://repo.or.cz/$_pkgname.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    for p in ref-error treeview-undefined; do
        patch -Np1 -i ../$pkgname-$p.patch
    done

    msg "Building..."
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
