# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=1.5
pkgrel=2
pkgdesc="Stunt Rally game with track editor, based on VDrift and OGRE"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
depends=('libvorbis' 'mygui' 'sdl' 'enet' 'bullet' 'boost')
makedepends=('cmake')
source=(src.tgz::https://nodeload.github.com/stuntrally/stuntrally/tarball/${pkgver}
	tracks.tgz::https://nodeload.github.com/stuntrally/tracks/tarball/${pkgver})
md5sums=('b47562662ff5a2518595778117460e68'
		 'f546bec51de1faa29f0d35356eab1394')

build() {
	# build the sources
	cd "${srcdir}/stuntrally-stuntrally-f7ef0ef/"
	
	rm -rf build
	mkdir build && cd build
	
	cmake -DCMAKE_INSTALL_PREFIX="/usr" -DSHARE_INSTALL="share/stuntrally/" ..
	make
	
	
	# build the tracks
	cd "${srcdir}/stuntrally-tracks-2123ed6/"
	
	cmake -DCMAKE_INSTALL_PREFIX="/usr" .
	make
}

package() {
	#install the sources
	cd "${srcdir}/stuntrally-stuntrally-f7ef0ef/build/"
	
	make DESTDIR="${pkgdir}" install
	
	
	# install the tracks
	cd "${srcdir}/stuntrally-tracks-2123ed6/"

	make DESTDIR="${pkgdir}/usr/share/stuntrally/" install
}
