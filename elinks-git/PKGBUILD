# Contributor: joyfulgirl <joyfulgirl@archlinux.us>
# Contributor: test1000 <kfs1@online.no>
pkgname=elinks-git
pkgver=20110711
pkgrel=1
pkgdesc="A featureful text mode web browser"
arch=("i686" "x86_64")
url="http://elinks.or.cz"
license=('GPL2')
conflicts=('elinks')
provides=('elinks')
depends=('bzip2' 'gzip' 'expat' 'openssl' 
         'zlib' 'lua' 'libidn' 'tre'
         'js' 'xz')
makedepends=('git')
optdepends=('gpm: console mouse support')
changelog=ChangeLog
source=("elinks.desktop")
md5sums=('ef70f6bb7457799a979fb201615369e6')

_gitroot="git://repo.or.cz/elinks.git"
_gitname="elinks"

build() {
  cd $srcdir
  msg "Begin checkout..."

  if [ -d $_gitname ]; then
      cd $_gitname && git pull origin master
  else
      git clone $_gitroot
  fi

  msg "Checkout complete or server timeout"
  msg "Begin make..."

  rm -rf ${srcdir}/${_gitname}-build
  cp -a ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build

  ./autogen.sh 
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-88-colors \
              --enable-256-colors \
              --enable-exmode \
              --with-lzma
  make
}

package() {
  cd ${srcdir}/${_gitname}-build
  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/elinks.desktop \
                   ${pkgdir}/usr/share/applications/elinks.desktop
  # Prevent glibc conflicts
  rm -f ${pkgdir}/usr/share/locale/locale.alias
}

# End of file
