# Maintainer: K. Hampf <khampf@users.sourceforge.net>
# Contributor: Kevin Sullivan <ksullivan@archlinux.us>

pkgname=prey-tracker
pkgver=0.5.9
pkgrel=1
pkgdesc="Lightweight application that will help you track and find your laptop if it ever gets stolen."
arch=('i686' 'x86_64')
url="http://preyproject.com/"
license=('GPL3')
install=prey-tracker.install

# DEPENDENCY NOTE: the project says curl *or* wget can be used.
#   Also, it says scrot *or* imagemagick can be used. You may adjust the
#   dependencies as needed with this information.
depends=('wget' 'imagemagick' 'perl-io-socket-ssl' 'perl-net-ssleay' 'python2')
optdepends=('pygtk: for gui configuration'
            'xawtv: webcam features'
            'mpg123: needed for audible alarm'

            # NOTE: if you want to exclusively use one of the below, feel free to delete the original dependency above 
            'curl: alternative to wget'
            'scrot: alternative to imagemagick')

source=("http://preyproject.com/releases/$pkgver/prey-$pkgver-linux.zip")
md5sums=('1ff6cb6bb0de36415fde3382e72a8a6d')
backup=('usr/share/prey-tracker/config')

package() {
  # make directories and move files into /usr/share/prey-tracker/
  mkdir -p $pkgdir/usr/share
  cp -r $srcdir/prey $pkgdir/usr/share/$pkgname
  cd $pkgdir/usr/share/$pkgname/

  # prey-config.py needs to know the new name for the directory.  This package
  # sticks to "prey-tracker" for naming conventions to reduce conflicts with 
  # other packages.
  sed -i 's/share\/prey/share\/prey-tracker/g' ./platform/linux/prey-config.py
  
  # it also needs to use Python 2.x
  sed -i 's/env\ python/env\ python2/'         ./platform/linux/prey-config.py

  # the path to iwlist has to be fixed
  sed -i 's/sbin/usr\/sbin/g' ./modules/geo/platform/linux/functions

  # fix a bunch of permissions manually
  chmod -R a-x LICENSE README version pixmaps/*
}

# vim:set ts=2 sw=2 et:
