# Contributor: pressh <pressh funnysymbol gmail dot com>
# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=convertall
pkgver=0.5.2
_varpkgver=0.5.2a
pkgrel=2
pkgdesc="Unit conversion program (Qt)"
arch=('any')
url="http://convertall.bellz.org/"
license=('GPL')
depends=('python2-pyqt')
source=("http://download.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz" 
	"http://download.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}-i18n-${_varpkgver}.tar.gz"
	"${pkgname}.desktop"
	"${pkgname}.sh"
	"correctpaths.patch")
md5sums=('9378cb3ab40cf944570d53bcc5718ee8'
         '37a884b332e8b56c55d3ee2a44ef7007'
         'f07eb5f02aa80f54c6122a481ead5934'
         'bea570696ef313f9abe97d73e6295791'
         '30c4ece9ebfc70a85c1dbfeb19d63920')

package() {
  #fix python2 executable
  patch -Np0 < ${srcdir}/correctpaths.patch
  find "${srcdir}/ConvertAll" -name "*py" -exec sed "s/'env python'/'env python2'/" -i {} \;

  cd "${srcdir}/ConvertAll"

  python2 install.py -p "${pkgdir}/usr" -x
  rm "${pkgdir}/usr/share/doc/${pkgname}"/{INSTALL,LICENSE}
  
  #install launcher
  install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  # install desktop file
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
