# Maintainer: 3ED <krzysztof1987@gmail.com>

pkgname=napi-bash
pkgver=0.50.3
pkgrel=1
pkgdesc="Lightweight, easy and fast polish subtitle downloader from the napiprojekt.pl"
arch=('any')
url="http://3ed.github.io/napi-bash/"
license=('GPL')
depends=('bash>=4.0' 'curl' 'p7zip' 'coreutils' 'findutils')
optdepends=('notification-daemon: dla wersji GUI/GTK'
            'sed: konwertowanie znaku konca linii dos/unix'
            'subotage: konwertowanie format√≥w (nie wymagane dla detekcji)'
            'ffmpeg: detektor FPS dla subotage'
            'mediainfo: detektor FPS dla subotage'
            'mplayer: detektor FPS dla subotage'
            'bc: kalkulator dla ffmpeg')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/3ed/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('f6c530a6d04e0f66b6e7503cfe215d804ab315ee786686131538603096d58a83')

# Mirror: http://napi-bash.googlecode.com/archive/v${pkgver}.tar.gz (sums will not mach)

package() {
  cd "$pkgname-$pkgver"
  make install PREFIX="/usr" DESTDIR="$pkgdir"
}
