# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: vogo <vogo(at)seznam(dot)cz>

pkgname=pacman-color-testing
pkgver=4.0.0
pkgrel=3
pkgdesc="A color patched command-line frontend for libalpm (Pacman) - prefers [testing]"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman>=4.0' 'pacman<4.1')
conflicts=('pacman-color')
provides=('pacman-color')
backup=('etc/pacman.d/color.conf')
source=(http://mirrors.kernel.org/archlinux/other/pacman/pacman-${pkgver}.tar.gz
# source=(ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz
        ${pkgname}-${pkgver}-${pkgrel}.patch
        color.conf)
# sha1sums=(`wget ${source/pacman-*}sha1sums -qO - | grep ${pkgver}.tar.gz | cut -d " " -f1`
sha1sums=('097e4671ccf02d0995101798613b27de6b0f8866'
          '4c8d293a18798b78a1ecf2a74a32ccb4d2b08749'
          '6e36b8720de0363f95b2ba7e06beb0770915c5c3')

build() {
  cd pacman-${pkgver}
  patch -p1 -i ../${pkgname}-${pkgver}-${pkgrel}.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
  make
}

package() {
  install -Dm755 pacman-${pkgver}/src/pacman/.libs/pacman "${pkgdir}/usr/bin/pacman-color"
  install -Dm644 color.conf "${pkgdir}/etc/pacman.d/color.conf"
}
