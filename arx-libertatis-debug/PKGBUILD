# Maintainer: Daniel Scharrer <daniel@constexpr.org>

pkgname=arx-libertatis-debug
_installname=arx-libertatis
pkgver=1.0
pkgrel=1
pkgdesc='Cross-platform port of Arx Fatalis, a first-person role-playing game (debug build)'
url='http://arx-libertatis.org/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('sdl' 'devil' 'openal' 'freetype2' 'zlib' 'boost' 'glew' 'libgl')
optdepends=('qt: enable built-in crash handler (recompile needed)'
            'gdb: generate detailed crash reports')
makedepends=('cmake')
provides=('arx' 'arx-libertatis')
conflicts=('arx-git' 'arx-libertatis')
options=(!strip)
source=("https://github.com/downloads/arx/ArxLibertatis/${_installname}-${pkgver}.tar.xz")
sha256sums=('5e410ca14dae05910162e1052cd4a693dce763ebe3158ca78e44d9dacb289108')
install='arx-libertatis.install'

build() {
	cd "${srcdir}/${_installname}-${pkgver}"
	
	cmake . -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Debug \
		-DUNITY_BUILD=OFF
	
	# (Note: UNITY_BUILD speeds up the compilation and produces a more
	# optimized binary, but uses a lot of RAM - disable if low on memory.)
		
	make -j`getconf _NPROCESSORS_ONLN`
}

package() {
	cd "${srcdir}/${_installname}-${pkgver}"
	
	make DESTDIR="${pkgdir}" install
	
	install -Dm644 README.md \
		"${pkgdir}/usr/share/doc/${_installname}/README.md"
	install -Dm644 CHANGELOG \
		"${pkgdir}/usr/share/doc/${_installname}/CHANGELOG"
	install -Dm644 AUTHORS \
		"${pkgdir}/usr/share/doc/${_installname}/AUTHORS"
	_license_file='ARX_PUBLIC_LICENSE.txt'
	install -Dm644 "${_license_file}" \
		"${pkgdir}/usr/share/licenses/${_installname}/${_license_file}"
}
