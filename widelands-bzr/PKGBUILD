#Contributor: matthiaskrgr <matthias · krueger _strange_curved_character_ famsike · de

pkgname=widelands-bzr
pkgver=6346
pkgrel=1
pkgdesc="A realtime strategy game with emphasis on economy and transport - development version"
arch=(i686 x86_64)
license=('GPL2')
depends=('boost' 'glew' 'lua' 'python' 'python2' 'sdl_gfx' 'sdl_image' 'sdl_mixer' 'sdl_net' 'sdl_ttf')
makedepends=('bzr' 'cmake')
provides=('widelands')
conflicts=('widelands')
url="http://wl.widelands.org"
source=('fix_build.patch')
sha1sums=('d1194223062f9e370de11bc845aec138c2f4e8ae')

_bzrtrunk=lp:widelands
_bzrmod=widelands

build() {
	cd ${srcdir}
	msg "Connecting to BZR server..."

	if [ -d ${srcdir}/${_bzrmod} ] ; then
		cd ${_bzrmod}
		msg "Updating files..."
		bzr pull "$_bzrtrunk" -r "$pkgver"
		msg "Local repository updated."
	else
		msg "Cloning repo..."
		bzr co ${_bzrtrunk}
		msg "Cloning done"
	fi
	msg "BZR checkout done or server timeout"

	msg "Removing old build directory..."
	rm -Irf ${srcdir}/${_bzrmod}-build

	msg "Creating new build directory..."
	mkdir ${srcdir}/${_bzrmod}-build
	cp -r ${srcdir}/${_bzrmod}/[^.bzr]* ${srcdir}/${_bzrmod}-build

	msg "Patching files.."
	cd ${srcdir}/${_bzrmod}-build
	patch -p0 < ${srcdir}/fix_build.patch

	msg "Beginning build..."
	if [ ! -d build ] ; then
		mkdir build
	fi
	cd build

	msg "Running cmake..."
	cmake .. -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWL_INSTALL_PREFIX=/usr \
		-DWL_INSTALL_DATADIR=share/$_bzrmod \
		-DWL_INSTALL_BINDIR=bin
	msg "Running make..."
	make
}

package() {
	cd  ${srcdir}/${_bzrmod}-build/build
	msg "Compiling into pkgdir this time..."
	make DESTDIR="${pkgdir}" install
}
