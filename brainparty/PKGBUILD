# Maintainer: Joao Cordeiro <jlcordeiro at gmail dot com>
# Contributor: mosra <mosra@centrum.cz>
# Contributor: Xyne <xyne@archlinux.ca>
pkgname=brainparty
pkgver=0.61
pkgrel=1
pkgdesc="36 puzzle games for all the family"
arch=('i686' 'x86_64')
url="http://www.tuxradar.com/brainparty"
license=('GPL3')
depends=('sdl_gfx' 'sdl_ttf' 'sdl_mixer' 'sdl_image' 'mesa' 'glu' 'ttf-freefont')
makedepends=('sdl_gfx' 'sdl_ttf' 'sdl_mixer' 'sdl_image' 'mesa' 'glu' 'gcc')
source=('https://launchpad.net/brainparty/trunk/0.61/+download/brainparty0.61.tar.gz'
        'path2data.patch'
        'change-config-file-path.patch')
md5sums=('d6bcdf6261697d206dbbda3362632002'
         'ef8be0d4160c7c9dc936698d6c6aa672'
         '3bef8710cb6ef57291361a03d34a556f')

build()
{
  cd "$srcdir/$pkgname"
  patch -p1 -i "$srcdir/path2data.patch"
  patch -p1 -i "$srcdir/change-config-file-path.patch"
  sed -i "s|Content/freesans.ttf|/usr/share/fonts/TTF/FreeSans.ttf|g" BPGame.cpp
  make
}

package()
{
  cd "$srcdir/$pkgname"
  install -D -m 755 brainparty "$pkgdir"/usr/bin/brainparty
  install -d "$pkgdir"/usr/share/games/brainparty/
  install -m 644 Content/* "$pkgdir"/usr/share/games/brainparty/
}