# Contributor: TryA <tryagainprod@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_pkgbasename=libftdi
pkgname=lib32-libftdi
pkgver=0.19
pkgrel=1
pkgdesc="A library to talk to FTDI chips (32-bit)"
arch=('i686' 'x86_64')
url="http://www.intra2net.com/en/developer/libftdi/download.php"
license=('GPL2' 'LGPL2.1')
depends=('lib32-libusb-compat' 'lib32-gcc-libs' $_pkgbasename)
makedepends=('boost' 'gcc-multilib')
options=('!libtool')
source=("http://www.intra2net.com/en/developer/libftdi/download/$_pkgbasename-$pkgver.tar.gz")
md5sums=('e6e25f33b4327b1b7aa1156947da45f3')
sha1sums=('0f08caf8e754ace69cd682489fae3f7f09920fe1')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "$srcdir/$_pkgbasename-$pkgver"
  sed -i '/no-install/d' examples/Makefile.in
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include}
}
