# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan Fader <jan.fader@web.de>
# Contributor: SanskritFritz (gmail)

pkgname=fish-shell-git
pkgver=20120721
pkgrel=1
pkgdesc="User friendly shell intended mostly for interactive use, forked by ridiculousfish."
arch=('i686' 'x86_64' 'arm')
url="http://ridiculousfish.com/shell/"
license=("GPL" "LGPL" "BSD" "MIT")
depends=('ncurses' 'bc')
optdepends=('python2: create_manpage_completions.py and other tools')
makedepends=('doxygen' 'git' 'python2')
provides=('fish')
conflicts=('fish')
install=fish-shell.install
source=()

_gitroot="git://github.com/fish-shell/fish-shell.git"
_gitname="fish-shell"

build() {
	cd "$srcdir"

	if [ -d "$srcdir/$_gitname" ] ; then
		cd $_gitname && git pull origin
	else
		git clone $_gitroot
	fi
	cd "$srcdir/$_gitname"
	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

	cd "$srcdir/$_gitname-build"

	sed -i "s/python/python2/" "internalize_scripts.py"
	sed -i "s/python/python2/" "make_completions.py"
	sed -i "s/python/python2/" "create_wajig_completions.py"
	autoconf
	./configure --prefix=/usr --sysconfdir=/etc --docdir=/usr/share/doc/fish --without-xsel
	make
}

package() {
	cd "$srcdir/$_gitname-build"
	make DESTDIR="$pkgdir" install

}
