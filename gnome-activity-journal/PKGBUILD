# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gnome-activity-journal
pkgver=0.8.0
pkgrel=1
pkgdesc="A tool for easily browsing and finding files on your computer"
arch=('any')
url="https://launchpad.net/gnome-activity-journal"
license=('GPL')
depends=('python2-libgnome' 'python2-gconf' 'zeitgeist>=0.6.0' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('python-distutils-extra')
optdepends=('gstreamer0.10-python: add multimedia tooltip to multimedia files'
            'python2-pygments: get a thumbnail of a text file')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('46e493f090b41a49cfce422203791bc0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/config.py gnome-activity-journal

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}
