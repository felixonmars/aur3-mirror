# Maintainer: Thomas Sowell <tom@fancydriving.org>

buildarch=1

pkgname=vboot-utils
pkgdesc="Chromium OS verified boot utilities"
pkgver=R30.4537
pkgrel=2
arch=('any')
url="http://git.chromium.org/gitweb/?p=chromiumos/platform/vboot_reference.git;a=summary"
license=('custom:chromiumos')
_gitname='vboot_reference'
depends=('trousers' 'libyaml' 'util-linux')
makedepends=('git')

source=(vboot-utils-ld.patch
        vboot-utils-shared-cgpt.patch
        "$_gitname::git+http://git.chromium.org/git/chromiumos/platform/vboot_reference.git#branch=release-R30-4537.B")

md5sums=('5ada2164058f94492e665081f217150a'
         '61bc68fbe27e55ec6e9464084e1f90bc'
         'SKIP')

prepare() {
  cd "$srcdir/$_gitname"

  git apply ${srcdir}/vboot-utils-ld.patch
  git apply ${srcdir}/vboot-utils-shared-cgpt.patch
}

build() {
  cd "$srcdir/$_gitname"

  ARCH=arm CFLAGS="-Wno-error=format -I/usr/include/yaml-cpp" make
}

package() {
  make -C "$srcdir/$_gitname" DESTDIR="${pkgdir}/usr/bin" install

  install -m 644 -D ${srcdir}/${_gitname}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
