# Maintainer: veox <veox ta wemakethings dot net>
# Contributor: Nick Ã˜stergaard <oe.nick at gmail dot com>

pkgname=openocd-git
pkgver=20130102
pkgrel=1
pkgdesc="Debugging, in-system programming and boundary-scan testing for embedded target devices (git version)"
arch=('i686' 'x86_64')
url="http://openocd.sourceforge.net/"
license=('GPL')
depends=('libftdi')
makedepends=('git' 'automake>=1.11' 'tcl')
options=(!strip)
install=openocd-git.install
provides=('openocd')
conflicts=('openocd')
source=()
md5sums=()

_gitroot=git://git.code.sf.net/p/openocd/code
_gitname=openocd
_features=(amtjtagaccel arm-jtag-ew buspirate ep93xx ep93xxat91rm9200 gw16012 ft2232_libftdi ftdi jlink oocd_trace opendous osbdm parport presto_libftdi remote-bitbang rlink stlink sysfsgpio ti-icdi ulink usb_blaster_libftdi usbprog vsllink)

build() {
  cd ${srcdir}

  msg "Connecting to GIT server..."
  if [[ -d $_gitname ]]; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth=1 $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"

  ./bootstrap
  ./configure --prefix=/usr ${_features[@]/#/--enable-} \
    --enable-maintainer-mode \
    --disable-werror

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR=${pkgdir} install 
}
