pkgname=amule-git
pkgver=20120505
pkgrel=1
pkgdesc='An eMule-like client for the eD2k and Kademlia p2p networks ( Daemon Function )'
url='http://www.amule.org'
arch=('i686' 'x86_64')
license=('GPL')
backup=('etc/conf.d/amule.conf')
depends=('wxgtk' 'gd' 'binutils' 'geoip')
makedepends=('crypto++' 'git')
provides=('amule')
conflicts=('amule')
source=('amuled' 'amule.conf')

_gitroot="git://github.com/adaptee/amule.git"
_gitname="amule"

build() {

  cd "$srcdir"
  msg "Connecting to GIT server...."

  # temporary hack needed for switching git mirror 
  if [[ -d "$_gitname" ]]; then
      rm "$_gitname" -rf
  fi

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  if [[ -d "$srcdir/$_gitname-build" ]]; then
      rm -rf "$srcdir/$_gitname-build"
  fi

  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  ./configure --prefix=/usr \
      --mandir=/usr/share/man \
      --enable-utf8-systray \
      --enable-cas \
      --enable-alc \
      --disable-debug \
      --disable-upnp \
      --enable-optimize \
      --enable-webserver \
      --enable-amule-daemon \
      --enable-geoip \
      --enable-amule-gui \
      --enable-fileview \
      --enable-amulecmd

    make
}

package() {
    cd "$srcdir/$_gitname-build"
    make DESTDIR=${pkgdir} install
    install -D -m644 ${srcdir}/amule.conf ${pkgdir}/etc/conf.d/amule.conf
    install -D -m755 ${srcdir}/amuled ${pkgdir}/etc/rc.d/amuled
}

md5sums=('f710777ed5fa52e56f2c247fbeb4a281'
         '6652f53c665776faab107c6ba573ce26')
