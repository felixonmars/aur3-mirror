# Mantainer: Franco Tortoriello

pkgname=openglide-cvs
pkgver=20120820
pkgrel=1
pkgdesc="Glide wrapper, useful for DOSBox with Glide support"
arch=('i686' 'x86_64')
url="http://openglide.sourceforge.net "
license=('GPL')
options=(!libtool)
depends=('mesa' 'sdl')
makedepends=('cvs')

_cvsroot="anonymous@openglide.cvs.sourceforge.net:/cvsroot/openglide"
_cvsmod="openglide"

build() {
  cd "$srcdir"

  if [[ -d "$_cvsmod/CVS" ]]; then
    cd "$_cvsmod"
    cvs -z3 update -d
  else
    cvs -z3 -d:pserver:"$_cvsroot" co -D "$pkgver" -f "$_cvsmod"
  fi

  msg "CVS checkout done or server timeout"

  rm -rf "$srcdir/$_cvsmod-build"
  cp -r "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"
  cd "$srcdir/$_cvsmod-build"

  sed -e '/install-data-hook/d' -e '/ln/d' -i Makefile.am
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_cvsmod-build"
  make DESTDIR="$pkgdir" install
}
