# Maintainer: Vasil Yonkov <bustervill@gmail.com>
pkgname=libebur128-git
pkgver=20120906
pkgrel=2
pkgdesc="A library implementing the EBU R128 loudness standard."
arch=('x86_64')
url="https://github.com/jiixyj/libebur128"
license=('MIT')
makedepends=('git' 'cmake')
optdepends=('gtk2: scanner dependency'
            'qt: scanner dependency'
            'taglib: scanner dependency'
            'libsndfile: SNDFILE input module support'
            'mpg123: MPG123 input module support'
            'libmpcdec: MPCDEC input module support'
            'ffmpeg: FFMPEG input module support'
            'gstreamer0.10: GStreamer  input module support')

_gitroot="git://github.com/jiixyj/libebur128.git"
_gitname="libebur128"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  git submodule init
  git submodule update
  mkdir build
  cd build

  cmake -DCMAKE_INSTALL_LIBDIR:path=lib -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}/
  ln -sf /usr/share/doc/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}

# vim:set ts=2 sw=2 et:
