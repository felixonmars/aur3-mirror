# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-blog
pkgver=0.9.2
pkgrel=7
pkgdesc="A gnome application that allows you to post entries to many different blog formats"
arch=('any')
license=('GPL')
depends=('python2-gconf' 'python-gdata' 'python2-gtkspell' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'desktop-file-utils')
install=$pkgname.install
url="http://projects.gnome.org/gnome-blog/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.9/$pkgname-$pkgver.tar.bz2
        drop-gnomevfs.patch
        drop-gnome.patch)
md5sums=('39ec54600a6b8dd786729c401fcfdf27'
         '930e8783f5238af36b695e02a8b5617c'
         '768ec8be9f59e25f3fc8fd9b32c0bced')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.gnome.org/show_bug.cgi?id=612630
  patch -Np1 -i "$srcdir/drop-gnomevfs.patch"

  # https://bugzilla.gnome.org/show_bug.cgi?id=671579
  patch -Np1 -i "$srcdir/drop-gnome.patch"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mv "$pkgdir"/usr/share/gconf/schemas/{gnomeblog.schemas,$pkgname.schemas}

  # Remove gnomeapplet
  rm -R "$pkgdir"/usr/lib/{bonobo,gnome-blog}
  rm -R "$pkgdir"/usr/lib/python2.7/site-packages/gnomeblog/aligned_window.py*
}
