# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qtbase-git
pkgver=v5.1.0.beta1.17.g3a770f5
pkgrel=1
pkgdesc="A cross-platform application and UI framework"
arch=('i686' 'x86_64')
url="http://qt-project.org/"
license=('GPL3' 'LGPL')
depends=('dbus' 'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-image'
         'libxext' 'inputproto' 'libgl' 'libxkbcommon' 'systemd'
         'libpng' 'sqlite' 'fontconfig' 'icu' 'libxrender')
makedepends=('git' 'gdb' 'mesa-libgl' 'postgresql-libs' 'libmariadbclient'
             'unixodbc' 'libfbclient')
optdepends=('qtchooser: set the default Qt toolkit'
            'postgresql-libs: PostgreSQL driver'
            'libmariadbclient: MariaDB driver'
            'unixodbc: ODBC driver'
            'libfbclient: Firebird/iBase driver')
options=('!libtool' 'debug')
source=(git://gitorious.org/qt/qtbase.git#branch=dev
        'qtchooser.conf')
md5sums=('SKIP'
         '386d7cdfc83c6772a5bf33a047d40784')

pkgver() {
  cd qtbase
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd qtbase
  sed -i "s|-O2|${CXXFLAGS}|" mkspecs/common/{g++,gcc}-base.conf
  sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" mkspecs/common/gcc-base-unix.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS}|g" mkspecs/common/gcc-base.conf
}

build() {
  cd qtbase
  ./configure -prefix /opt/qt-git \
    -opensource -confirm-license \
    -developer-build \
    -system-sqlite \
    -openssl-linked \
    -nomake examples \
    -nomake tests \
    -no-rpath \
    -dbus \
    -no-separate-debug-info \
    -xcb \
    -qpa xcb \
    -no-gtkstyle \
    -opengl es2
  make
}

package() {
  cd qtbase
  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 "${srcdir}"/qtchooser.conf \
    "${pkgdir}"/etc/xdg/qtchooser/git.conf
}
