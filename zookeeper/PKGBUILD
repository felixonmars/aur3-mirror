# Maintainer: Scott Lawrence <bytbox@gmail.com>
pkgname=zookeeper
pkgver=3.3.6
pkgrel=1
pkgdesc="An open-source server which enables highly reliable distributed coordination"
arch=('any')
url="https://zookeeper.apache.org/"
license=('APACHE')
depends=(java-runtime)
install=
source=("http://www.eng.lsu.edu/mirrors/apache/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('68157fc9bc80a3bc78bb440191e3b32e')

package() {
	_optdir=$pkgdir/opt/${pkgname}-${pkgver}
	cd ${pkgname}-${pkgver}
	install -d $pkgdir/usr/bin $pkgdir/usr/share/doc $pkgdir/etc $_optdir/lib
	for n in zkServer zkCli zkCleanup; do
		install -D bin/$n.sh $_optdir/bin/$n.sh
		ln -s /opt/${pkgname}-${pkgver}/bin/$n.sh $pkgdir/usr/bin/$n
	done	
	install -D bin/zkEnv.sh $_optdir/bin/zkEnv.sh

	cp -r docs $pkgdir/usr/share/doc/zookeeper

	install -t $_optdir/lib dist-maven/*.jar lib/*.jar
	cp -r conf $_optdir/conf
	install conf/zoo_sample.cfg $_optdir/conf/zoo.cfg
	ln -s /opt/${pkgname}-${pkgver}/conf $pkgdir/etc/zookeeper
}

