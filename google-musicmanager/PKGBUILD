# Maintainer: Patrick Mullaney  <pajmullaney@gmail.com>
# Contributor: Techlive Zheng <techlivezheng@gmail.com>

pkgname=google-musicmanager
pkgver=1.0.35.1577_r0
pkgrel=1
pkgdesc="A simple application for adding the music files on your computer to your Google Music library."
arch=('i686' 'x86_64')
url="http://music.google.com"
license=('custom:musicmanager')
depends=('flac' 'qt' 'fontconfig' 'libogg' 'freetype2' 'libvorbis' 'xdg-utils' 'libidn' 'qtwebkit')
optdepends=('log4cxx')
options=(!strip)
install=${pkgname}.install

if [ "$CARCH" = "x86_64" ]; then
    _arch='amd64'
    md5sums=('85faff026eec1ca7661a1ecbc52fc929')
elif [ "$CARCH" = "i686" ]; then
    _arch='i386'
    md5sums=('b74912536e58a54882fa100adef21d5e')
fi

source=("google-musicmanager-beta-${_arch}-${pkgver}.deb::http://dl.google.com/linux/direct/google-musicmanager-beta_current_${_arch}.deb")

build() {
    #Get binary sources.
    ar -xv google-musicmanager-beta-${_arch}-${pkgver}.deb
    tar -xvf data.tar.lzma
    tar -xvf control.tar.gz

    #Auto fetch the latest version.
    :;pkgver=`grep "Version" control | cut -d: -f2 | sed 's/^ *//g' | sed 's/ *$//g' | sed 's/-/_/g'`

    #Debian package source files cleaning.
    rm -r $srcdir/etc
    rm -r $srcdir/usr
    rm -f $srcdir/opt/google/musicmanager/product_logo_32.xpm
}
package() {
    #Auto fetch the latest version.
    :;pkgver=`grep "Version" control | cut -d: -f2 | sed 's/^ *//g' | sed 's/ *$//g' | sed 's/-/_/g'`

    install -d $pkgdir/{opt,usr/{bin,share/applications}}

    #Install files.
    cd $srcdir/opt/google/musicmanager
    find . -type d -exec install -d {,${pkgdir}/opt/google/musicmanager/}{} \;
    find . -type f -exec install -D -m 644 {,${pkgdir}/opt/google/musicmanager/}{} \;

    #Make google-musicmanager executable.
    chmod 755 ${pkgdir}/opt/google/musicmanager/{google-musicmanager,minidump_upload,xdg-settings,xdg-mime,MusicManager}

    #Installing icons to /usr/share/icons/hicolor/
    for i in 16 32 48 128
    do
        install -D -m644 $pkgdir/opt/google/musicmanager/product_logo_${i}.png $pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/google-musicmanager.png
    done

    install -D -m644 $pkgdir/opt/google/musicmanager/google-musicmanager.desktop $pkgdir/usr/share/applications

    ln -s /opt/google/musicmanager/google-musicmanager $pkgdir/usr/bin/
}
