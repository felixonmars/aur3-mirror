# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=(fastbreeder)
pkgver=1.0.1
pkgrel=3
pkgdesc="An experimental genetic programming synthesiser."
arch=('i686' 'x86_64')
url="http://www.pawfal.org/Software/fastbreeder/"
license=('GPL')
depends=('jack' 'liblo' 'python2')
makedepends=('scons')
source=("http://www.pawfal.org/Software/fastbreeder/files/$pkgname-$pkgver.tar.gz")
md5sums=('9d475f99545006df1bcbd52f807245aa')

build() {

  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i "s_python_&2_" "scripts/$pkgname"

  # Prefix fix
  sed -i "s_\(/usr/local\)_$pkgdir/usr_" SConstruct

  scons
}

package() {

  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir"/usr/{bin,share/$pkgname}

  install -Dm 755 "$pkgname"server "$pkgdir/usr/bin"
  install -Dm 755 scripts/* "$pkgdir/usr/share/$pkgname"

  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin"
}