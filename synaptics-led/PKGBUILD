# Maintainer: Matthew Monaco <net 0x01b dgbaley27>

_kver=3.6
_gitroot=git://repo.or.cz/linux.git
_gitcommit=linux-$_kver.y
_cur_kernel="$(uname -r)"

pkgname=synaptics-led
pkgver=$_kver
pkgrel=3
arch=(i686 x86_64)
license=(GPL2)
url="https://github.com/mmonaco/PKGBUILDs"
pkgdesc="Synaptics LED enabled psmouse kernel module"
depends=("linux")
makedepends=("git" "linux-headers")
install=$pkgname.install

source=(
	"SHA256SUMS"
	"synled.patch"
	"$pkgname.install"
)

sha256sums=(
	"f67d599a1cc96e1e2e01e805a87ee49361f646251e8f409886e227a9a102fc7e"
	"f3bf548854d5aa7b2b35eee7cf5e565f909331efee204d22410d4666f2303047"
	"b46af61822e8ec8639faa1b60dd3b6b1a64e24854611902499b9f81d2691e22c"
)

build() {

	cd "$srcdir"

	msg2 "Getting source from $_gitroot"
	git archive --remote="$_gitroot" "$_gitcommit" drivers/input/mouse | tar -x
	
	cd "drivers/input/mouse"

	msg2 "Performing Integrity Check"
	sha256sum --quiet -c "$srcdir/SHA256SUMS"

	msg2 "Patching source"
	patch -p4 -i "$srcdir/synled.patch"

	msg2 "Building psmouse.ko"
	make -C "/lib/modules/$_cur_kernel/build" M="$PWD" psmouse.ko

	msg2 "Compressing psmouse.ko.gz"
	gzip -9 psmouse.ko
}

package() {

	cd "$srcdir/drivers/input/mouse"

	install -Dm 0644 psmouse.ko.gz \
		"$pkgdir/lib/modules/$_cur_kernel/extramodules/psmouse.ko.gz"
}
