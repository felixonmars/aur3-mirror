# Maintainer: Matthew Monaco <net 0x01b dgbaley27>

_kver=3.4
_kver_extra=
_gitroot=git://repo.or.cz/linux-2.6.git

pkgname=synaptics-led
pkgver=$_kver
pkgrel=1
arch=(i686 x86_64)
license=(GPL2)
url="https://github.com/mmonaco/PKGBUILDs"
pkgdesc="Synaptics LED enabled psmouse kernel module"
depends=("linux>=$_kver" "linux<3.5")
makedepends=("git" "linux-headers>=$_kver" "linux-headers<3.5")

install=$pkgname.install

source=(
	"MD5SUMS"
	"synled.patch"
)

md5sums=(
	deb2d3b31e260e00b18c92ee413e009f
	0563ac5e30a5661a677e7fd4fe6951df
)

build() {

	local arch_kernel

	arch_kernel=$(< /lib/modules/extramodules-$_kver-ARCH/version)

	cd "$srcdir"

	msg2 "Getting source from $_gitroot"
	git archive --remote=$_gitroot v$_kver$_kver_extra drivers/input/mouse | tar -x
	cd "drivers/input/mouse"

	msg2 "Performing Integrity Check"
	md5sum --quiet -c $srcdir/MD5SUMS

	msg2 "Patching source"
	patch -p4 -i "$srcdir/synled.patch"

	msg2 "Building psmouse.ko"
	make -C "/lib/modules/$arch_kernel/build" M="$PWD" psmouse.ko

	msg2 "Installing psmouse.ko.gz"
	gzip -9 psmouse.ko
}

package() {

	cd "$srcdir/drivers/input/mouse"

	install -Dm 0644 psmouse.ko.gz \
		"$pkgdir/lib/modules/extramodules-$_kver-ARCH/psmouse.ko.gz"
}
