# Maintainer: ThePierrezou <thepierrezou@openmailbox.org>
# Contributor: polyzen <polycitizen@gmail.com>

pkgname=firefox-extension-ublock-gorhill
pkgver=0.9.8.2
pkgrel=1
pkgdesc='Finally, an efficient blocker. Easy on CPU and memory. Gorhill version.'
url=https://github.com/gorhill/uBlock
arch=('any')
license=('GPL3')
depends=('firefox')
conflicts=('firefox-extension-ublock-gorhill-git' 'firefox-ublock-origin' )
source=("https://github.com/gorhill/uBlock/releases/download/$pkgver/uBlock0.firefox.xpi")

package() {
  local GLOBIGNORE=*.xpi:LICENSE.txt
  local version="$(grep -Pom1 '(?<=<id>).*(?=</id>)' install.rdf)"
  local dstdir="$pkgdir/usr/lib/firefox/browser/extensions/$version"
  install -d "$dstdir"
  cp -dpr --no-preserve=ownership * "$dstdir"
  chmod -R 755 "$dstdir"
}

# vim:set ts=2 sw=2 et:
sha256sums=('170886e024653888bbfb0279c98b601ab71b93c9443a498f53b31619f1e0c641')

