# Maintainer: ThePierrezou <thepierrezou@openmailbox.org>
# Contributor: polyzen <polycitizen@gmail.com>

pkgname=firefox-extension-ublock-gorhill
pkgver=0.9.4.5
pkgrel=1
pkgdesc='Finally, an efficient blocker. Easy on CPU and memory. Gorhill version.'
url=https://github.com/gorhill/uBlock
arch=('any')
license=('GPL3')
depends=('firefox')
conflicts=('firefox-extension-ublock' 'firefox-extension-ublock-git' 'firefox-extension-ublock-gorhill-git')
source=("https://github.com/gorhill/uBlock/releases/download/$pkgver/uBlock0.firefox.xpi")
sha256sums=('7c6f125a64e77a3257552425ba136f9a418c052bd44f0ee0d31baa2c7795debd')

package() {
  local GLOBIGNORE=*.xpi:LICENSE.txt
  local version="$(grep -Pom1 '(?<=<id>).*(?=</id>)' install.rdf)"
  local dstdir="$pkgdir/usr/lib/firefox/browser/extensions/$version"
  install -d "$dstdir"
  cp -dpr --no-preserve=ownership * "$dstdir"
  chmod -R 755 "$dstdir"
}

# vim:set ts=2 sw=2 et:
