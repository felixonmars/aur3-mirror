# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=cinnamon-git
pkgver=20120909
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/Cinnamon"
license=('GPL')
depends=('gnome-menus' 'libpulse' 'gnome-desktop' 'python2' 'gnome-bluetooth' 'gjs' 'libcroco' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'gnome-session' 'muffin-git>=20120507' 'python2-gconf' 'libgnomekbd' 'gnome-menus2' 'python2-lxml')
makedepends=('git' 'intltool' 'gnome-common')
optdepends=('nautilus: Default GNOME file manager'
            'nemo-git: Experimental Linux Mint fork of Nautilus')
provides=('cinnamon')
conflicts=('cinnamon' 'cinnamon-session-git' 'cinnamon-session' 'cinnamon<20120118')
options=('!libtool' '!emptydirs')
install=${pkgname}.install

_gitroot='https://github.com/linuxmint/Cinnamon.git'
_gitname='Cinnamon'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Hack to make cinnamon-settings work. The proper fix is to install
  # gnome-settings-daemon-ubuntu
  if ! pacman -Q gnome-settings-daemon-ubuntu &>/dev/null; then
    sed -i '/sidePage.add_widget(self.changeTimeWidget)/d;/self.changeTimeWidget\ =\ ChangeTimeWidget()/d' files/usr/lib/cinnamon-settings/cinnamon-settings.py
  fi

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon --localstatedir=/var --disable-static --disable-schemas-compile --disable-schemas-install --enable-compile-warnings=yes
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}/" install

  # Python 2 fix
  sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' \
    "${pkgdir}/usr/bin/cinnamon-extension-tool" \
    "${pkgdir}/usr/lib/cinnamon-settings/cinnamon-settings.py" \
    "${pkgdir}/usr/bin/cinnamon-settings" \
    "${pkgdir}/usr/bin/cinnamon-menu-editor"
}

# vim:set ts=2 sw=2 et:
