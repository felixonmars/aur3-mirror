# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=cinnamon-git
pkgver=20120429
pkgrel=2
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/Cinnamon"
license=('GPL')
depends=('gnome-menus' 'libpulse' 'gnome-desktop' 'python2' 'gnome-bluetooth' 'gjs' 'libcroco' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'gnome-session' 'muffin-git>=20120429' 'python2-gconf' 'libgnomekbd' 'gnome-menus2')
makedepends=('git' 'intltool' 'gnome-common')
provides=('cinnamon')
conflicts=('cinnamon' 'cinnamon-applets-git' 'cinnamon-applets' 'cinnamon-session-git' 'cinnamon-session' 'cinnamon<20120118')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=('0001-Fix-build-with-gnome-bluetooth-3.4.patch'
        'cinnamon-settings-hack.patch::http://pastebin.com/download.php?i=EaQ4fpcA')
sha512sums=('7d718ad5be0e65ccef1015e4f338406df546f58932077b7675c6b3c338af58c4b1cf474b5ea30aae0f047f86b508bcc11892f341965cd087b94f26f64d0f6808'
            '2abe53be568ef2bdef3a17525d4ed1d27b242abcf631c32103796b5ac1ed171429b9a6c67b72a15b1c2e8568af104eee74eb70c91fba70f1a2dd756e7002f6bb')

_gitroot='https://github.com/linuxmint/Cinnamon.git'
_gitname='Cinnamon'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Fix build with GNOME 3.4
  patch -Np1 -i "${srcdir}/0001-Fix-build-with-gnome-bluetooth-3.4.patch"

  # Hack to make cinnamon-settings work. The proper fix is to install gnome-settings-daemon-ubuntu
  if ! pacman -Q gnome-settings-daemon-ubuntu &>/dev/null; then
    patch -Np0 -i "${srcdir}/cinnamon-settings-hack.patch"
  fi

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon --localstatedir=/var --disable-static --disable-schemas-compile --disable-schemas-install --enable-compile-warnings=yes
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}/" install

  # GConf2 stuff
  install -dm755 "${pkgdir}/usr/share/gconf/schemas/"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain cinnamon "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -r "${pkgdir}/etc/gconf/"

  # Python 2 fix
  sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' \
    "${pkgdir}/usr/bin/cinnamon-extension-tool" \
    "${pkgdir}/usr/lib/cinnamon-settings/cinnamon-settings.py" \
    "${pkgdir}/usr/bin/cinnamon-settings" \
    "${pkgdir}/usr/bin/cinnamon-menu-editor"
}

# vim:set ts=2 sw=2 et:
