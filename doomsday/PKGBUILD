# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Dmitry Shilov <stormblast@land.ru>

pkgname=doomsday
pkgver=1.9.0_beta6.9
pkgrel=1
pkgdesc="An advanced port of the Doom game engine"
url="http://dengine.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl_net' 'sdl_mixer' 'curl' 'libgl' 'libpng' 'mesa' 'openal')
makedepends=('cmake>=2.4')
install=$pkgname.install
source=(http://downloads.sourceforge.net/deng/deng-1.9.0-beta6.9.tar.gz
        launchers.tar.gz)
md5sums=('907ef41b70e2dbf148ef7e4a0350c6bd'
         '9ffc152d8031da40f8047424b46c478e')

build() {
    cd $srcdir/deng-${pkgver//_/-}/$pkgname/build

    # Compile and install
    cmake	.. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILDOPENAL:BOOL=1 \
        -DBUILDWOLFTC:BOOL=1 \
        -DBUILDDOOM64TC:BOOL=1 \
        || return 1

    make || return 1
    make DESTDIR=$pkgdir install || return 1

    # Install launchers
    cd $srcdir/launchers
    install -d $pkgdir/usr/share/{applications,desktop-directories}
    install -m 644 j*.desktop $pkgdir/usr/share/applications/
    install -m 644 Doomsday.directory $pkgdir/usr/share/desktop-directories/
    install -m 755 {jdoom*,jheretic,jhexen} $pkgdir/usr/bin/
    install -Dm 644 $srcdir/deng-${pkgver//_/-}/$pkgname/engine/data/graphics/logo.png \
        $pkgdir/usr/share/pixmaps/$pkgname.png
}
