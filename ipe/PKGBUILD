#Maintainer: Dmitriy Morozov <archlinux@foxcub.org>

pkgname=ipe
_sfproject=ipe7
pkgver=7.0.14
pkgrel=3
pkgdesc="The extensible drawing editor"
url="http://tclab.kaist.ac.kr/ipe/"
depends=('lua' 'qt' 'freetype2' 'zlib' 'poppler' 'python2')
arch=('i686' 'x86_64')
license=("GPL")
conflicts=('ipe')
ftiversion_=20091205
ptiversion_=20110517
stiversion_=20100608
ipepresenter_version_=6f17855970fc
source=(http://downloads.sourceforge.net/project/$_sfproject/$pkgname/$pkgname-$pkgver-src.tar.gz 
        http://downloads.sourceforge.net/project/$_sfproject/tools/figtoipe-$ftiversion_.tar.gz
        http://downloads.sourceforge.net/project/$_sfproject/tools/pdftoipe-$ptiversion_-src.tar.gz
        http://downloads.sourceforge.net/project/$_sfproject/tools/svgtoipe-$stiversion_.tar.gz
        http://hg.mrzv.org/IpePresenter/archive/${ipepresenter_version_}.tar.gz
        ipe.bash-completion
        svgtoipe-python2.patch
        config.patch)
md5sums=('13b1790813304ac888402d9c6c40a6ec'
         'a19e0712df137939c37c194b551da6b8'
         '7f382805288ad4a6652f7d090ef32529'
         'da05b41f9ce7be0f0dbd14c5ffe08e45'
         '708dfd9e09962ffbf1bda3ffd0462bd1'
         '694f0d5402655901be385647e5d8d6e3'
         '967a191ca673aec93546640002c794ab'
         'f295a7c25a1dba6d197207ab35e5f867')

build() {
 # Build Ipe
 cd $srcdir/$pkgname-$pkgver/src
 patch config.mak < $srcdir/config.patch
 cd $srcdir/$pkgname-$pkgver/src

 mkdir -p $pkgdir/usr/bin

 make IPEPREFIX=/usr
 INSTALL_ROOT=$pkgdir make install IPEPREFIX=/usr

 # Make QVoronoi Ipelet
 # cd $srcdir/$pkgname-$pkgnamever/src/ipelets/qvoronoi
 # qmake IPEPREFIX=/usr INCLUDEPATH+=/usr/include/qhull
 # make || return 1
 # INSTALL_ROOT=$pkgdir make install

 # Build figtoipe
 cd $srcdir/figtoipe-$ftiversion_
 make
 install -m 755 figtoipe $pkgdir/usr/bin

 # Build pdftoipe
 cd $srcdir/pdftoipe-$ptiversion_-src
 make
 install -m 755 pdftoipe $pkgdir/usr/bin

 # Copy svgtoipe
 cd $srcdir/svgtoipe-$stiversion_
 patch svgtoipe < $srcdir/svgtoipe-python2.patch
 install -m 755 svgtoipe $pkgdir/usr/bin

 # Build IpePresenter
 cd $srcdir/IpePresenter-$ipepresenter_version_
 CPPFLAGS+=" -I ../$pkgname-$pkgver/src/include -I ../$pkgname-$pkgver/src/ipeqtcanvas" \
 LIBS=" -L ../$pkgname-$pkgver/build/lib" \
 make
 install -m 755 ipepresenter $pkgdir/usr/bin

 # make lua binding available
 mkdir -p $pkgdir/usr/lib/lua/5.1
 ln -s /usr/lib/libipelua.so $pkgdir/usr/lib/lua/5.1/ipe.so

 # install bash completion
 mkdir -p $pkgdir/etc/bash_completion.d
 install $srcdir/ipe.bash-completion $pkgdir/etc/bash_completion.d/ipe
} 
