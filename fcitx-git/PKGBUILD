# Maintainer: Yichao Yu <yyc1992 AT gmail.com>
# Contributor: Weng XueTian <wengxt AT gmail com>
# Contributor: lh <jarryson AT gmail com>

pkgname=fcitx-git
pkgver=20120902
pkgrel=1
pkgdesc="A Flexible Input Method Framework"
arch=("i686" "x86_64")
license=('GPL')
url="http://fcitx-im.org/wiki/Fcitx"
depends=('pango' 'gtk2' 'qt' 'gtk3' 'dbus-core' 'libxinerama'
  'gtk-update-icon-cache' 'shared-mime-info' 'hicolor-icon-theme'
  'desktop-file-utils' 'icu' 'libxkbfile' 'iso-codes' 'libxml2'
)
makedepends=('git' 'intltool' 'cmake' 'gobject-introspection' 'enchant')
conflicts=('fcitx' 'fcitx-qt' 'fcitx-gtk2' 'fcitx-gtk3' 'fcitx-keyboard')
provides=("fcitx=4.2.6" 'fcitx-qt=4.2.6' 'fcitx-gtk2=4.2.6'
  'fcitx-gtk3=4.2.6' 'fcitx-keyboard=0.1.3')
replaces=('fcitx-keyboard-git')
optdepends=(
  'enchant: for word predication using enchant'
  'presage: for word predication using presage')
install="$pkgname.install"

_gitname="fcitx"
_gitroot="https://github.com/fcitx/fcitx.git"

build() {
  cd "$srcdir"
  msg "Connecting to the GIT server...."

  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git checkout master
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  cd "$srcdir/$_gitname"
  git pull

  msg "Creating make environment..."

  mkdir -p "$srcdir/$_gitname-build"

  msg "Starting make..."
  cd "$srcdir/$_gitname-build"
  cmake ../$_gitname -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_QT_IM_MODULE=On \
    -DENABLE_GTK2_IM_MODULE=On \
    -DENABLE_GTK3_IM_MODULE=On \
    -DENABLE_OPENCC=Off \
    -DFORCE_PRESAGE=On
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
