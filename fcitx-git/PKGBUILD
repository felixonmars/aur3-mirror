# Maintainer: Yichao Yu <yyc1992 AT gmail.com>
# Contributor: Weng XueTian <wengxt AT gmail com>
# Contributor: lh <jarryson AT gmail com>

pkgbase=fcitx
pkgname=$pkgbase-git
pkgver=20121202
_realver=4.2.7
pkgrel=2
pkgdesc="A Flexible Input Method Framework"
arch=("i686" "x86_64")
license=('GPL')
url="http://fcitx-im.org/wiki/Fcitx"
depends=('pango' 'libxinerama' 'shared-mime-info' 'hicolor-icon-theme'
  'desktop-file-utils' 'icu' 'libxkbfile' 'iso-codes' 'libxml2'
  'gtk2' 'gtk3' 'dbus-core')
makedepends=('git' 'gettext' 'cmake' 'gobject-introspection' 'coreutils'
  'sh' 'qt')
conflicts=('fcitx' 'fcitx-qt' 'fcitx-gtk2' 'fcitx-gtk3' 'fcitx-keyboard')
provides=("fcitx=${_realver}" 'fcitx-qt=${_realver}' 'fcitx-gtk2=${_realver}'
  'fcitx-gtk3=${_realver}' 'fcitx-keyboard=0.1.3')
replaces=('fcitx-keyboard-git')
optdepends=(
  'enchant: for word predication using enchant'
  'presage: for word predication using presage'
  'opencc: for simplified and traditional chinese translation'
  'qt: for qt input method module')
install="$pkgbase.install"

_gitname="fcitx"

_gitroot="git://github.com/fcitx/fcitx.git"
_gitbranch="master"

# _gitroot="git://github.com/yuyichao/fcitx.git"
# _gitbranch="master"

# _gitroot="ssh://git@bitbucket.org/yuyichao/fcitx.git"
# _gitbranch="python"

build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname/.git" ]; then
    cd "$_gitname"
    msg "Reset current branch"
    git reset --hard HEAD
    msg "Fetching branch $_gitbranch from $_gitroot..."
    git fetch --force --update-head-ok \
      "$_gitroot" "$_gitbranch:$_gitbranch"
    msg "Checking out branch $_gitbranch..."
    git checkout "$_gitbranch"
    git reset --hard "$_gitbranch"
    msg "The local files are updated."
  else
    msg "Cloning branch $_gitbranch from $_gitroot to $_gitname..."
    git clone --single-branch --branch "$_gitbranch" \
      "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  msg "Creating make environment..."
  mkdir -p "$srcdir/$_gitname-build"

  msg "Starting make..."
  cd "$srcdir/$_gitname-build"
  cmake ../$_gitname -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_QT_IM_MODULE=On \
    -DENABLE_GTK2_IM_MODULE=On \
    -DENABLE_GTK3_IM_MODULE=On \
    -DFORCE_OPENCC=On \
    -DFORCE_ENCHANT=On \
    -DFORCE_PRESAGE=On
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
