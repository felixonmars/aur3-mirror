# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=nftables-git
pkgver=0.r165.a6931eb
pkgrel=1
pkgdesc='Netfilter nf_tables infrastructure library'
arch=(i686 x86_64)
url='http://netfilter.org/projects/nftables/'
license=(GPL2)
depends=(libmnl gmp libnftables-git ncurses)
source=('nftables::git://git.netfilter.org/nftables')
md5sums=('SKIP')

pkgver() {
  cd nftables
  # repo does not have any git tags yet
  echo 0.r$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd nftables
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd nftables
  make DESTDIR="$pkgdir" install
}
