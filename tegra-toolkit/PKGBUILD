#Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=tegra-toolkit
pkgver=1.0r4
_pkgver=1.0
_pkgdate=2012-01-20
_pkgbuild=11617556
pkgrel=2
pkgdesc="The Tegra Android Toolkit, tools, samples and documentation"
license=('custom')
arch=('i686' 'x86_64')
url="http://developer.nvidia.com/category/zone/mobile-development"

depends=('oprofile' 'gprof2dot-git' 'graphviz' 'android-sdk-platform-tools' 'android-udev' 'qt')
optdepends=('eog: default image viewer for oprofile-analyze')

[ "$CARCH" = "i686"   ] && _pkgarch="linux"
[ "$CARCH" = "x86_64" ] && _pkgarch="linux-x64"
source=("http://developer.nvidia.com/sites/default/files/akamai/tools/files/Tegra/$pkgname-$_pkgver-$_pkgarch-$_pkgdate-$_pkgbuild.run"
        "oprofile-analyze.patch" "license")
[ "$CARCH" = "i686"   ] && md5sums=('b692d76a32de4780a9f47e51bd4c8a3e')
[ "$CARCH" = "x86_64" ] && md5sums=('ca3eb386315e9fb927d0d78affd4e675')
md5sums+=('65ae4180dfea679bd498394fdc73d691'
          '2ea4dc7733201cd7dbc5552342fa1d26')

options=(!strip)

build() {
  cd "${srcdir}"
  if [ -e ~/NVPACK ]
  then
    echo "Sorry, Tegra Toolkit installer is broken"
    echo "it needs to extract to ~/NVPACK and the file is already present"
    echo "rename or delete the file and try again"
    return 1
  fi
  msg "Extracting Tegra Toolkit..."
  chmod a+x $pkgname-$_pkgver-$_pkgarch-$_pkgdate-$_pkgbuild.run
  rm -rf "fakebin"
  mkdir "fakebin"
  cd "fakebin"
  touch apt-cache
  chmod a+x apt-cache
  touch apt-get
  chmod a+x apt-get
  echo "#!/bin/bash" > sudo
  echo "\$@" >> sudo
  chmod a+x sudo
  echo "#!/bin/bash" > xterm
  echo "\$2 \$3 \$4" >> xterm
  chmod a+x xterm
  echo "#!/bin/bash" > dpkg
  echo "cp \$2 \"$srcdir/\${2##*/}\"" >> dpkg
  chmod a+x dpkg
  cd ..
  oldPATH=$PATH
  export PATH="$srcdir/fakebin:$PATH"
  unset LD_PRELOAD
  ./$pkgname-$_pkgver-$_pkgarch-$_pkgdate-$_pkgbuild.run --mode unattended
  export PATH="$oldPATH"
  rm -rf "$srcdir/NVPACK"
  mv ~/NVPACK "$srcdir/NVPACK"
  msg "Finished extracting Tegra Toolkit"
  cd "$srcdir/NVPACK/oprofile"
  patch -p0 < ../../oprofile-analyze.patch
  install -m 644 -D "abi/arm_abi" "$pkgdir/usr/share/tegra/abi/arm_abi"
  install -m 755 -D "oprofile-analyze.py" "$pkgdir/usr/bin/oprofile-analyze"
  cd "$srcdir/NVPACK/TDK_Samples"
  install -d "$pkgdir/usr/share/tegra/samples"
  cp -rf Android_NVIDIA_samples_2_public_RC2/* "$pkgdir"/usr/share/tegra/samples
  cp -rf NVIDIA_Android_Lifecycle_samples_2_0_public/* "$pkgdir"/usr/share/tegra/samples
  cd "$srcdir/NVPACK"
  install -d "$pkgdir/usr/share/tegra/docs"
  rm -f "docs/NVIDIA Debug Manager for Android NDK.pdf"
  cp -rf docs/* "$pkgdir/usr/share/tegra/docs"
  cd "$srcdir/NVPACK/perf/PERF"
  install -d "$pkgdir/usr/share/tegra/docs/perf-patches"
  install -m 644 -D perf "$pkgdir/usr/share/tegra/perf"
  cp -rf patch/* "$pkgdir"/usr/share/tegra/docs/perf-patches
  cd "$srcdir/NVPACK/perfhud_switch"
  install -m 755 -D enable_perfhud.sh "$pkgdir/usr/bin/enable_perfhud"
  install -m 755 -D disable_perfhud.sh "$pkgdir/usr/bin/disable_perfhud"
  cd "$srcdir"
  [ "$CARCH" = "i686"   ] && ar -x perfhudes_1.9-8_i386.deb
  [ "$CARCH" = "x86_64" ] && ar -x perfhudes_1.9-8_amd64.deb
  tar -xzf data.tar.gz -C "$pkgdir"
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib"
  install -m 644 -D license "$pkgdir/usr/share/licenses/$pkgname/license"
}

