# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=sshfp
pkgver=1.2.2
pkgrel=1
pkgdesc="Generates DNS SSHFP-type records from SSH public keys from public keys from a known_hosts file or from scanning the host's sshd daemon."
arch=('i686' 'x86_64')
license=('GPL')
depends=('python2' 'python2-dnspython')
url="http://www.xelerance.com/software/sshfp/"
source=("$pkgname-$pkgver.tar.gz::https://github.com/xelerance/$pkgname/tarball/$pkgver")
noextract=("$pkgname-$pkgver.tar.gz") # see build()
md5sums=('ebf3abb952ffa90914913ef2060ac125')

build() {
  # the tarball comes with part of the commit sha1
  # forming part of the directory name, so to have
  # a deterministic tree when building, we'll extract
  # the tarball ourselves, stripping the leading
  # directory name
  mkdir "$srcdir"/$pkgname-$pkgver/ || rm -Rf "$srcdir"/$pkgname-$pkgver/*
  cd "$srcdir"/$pkgname-$pkgver/
  tar xzf "$srcdir"/$pkgname-$pkgver.tar.gz --strip-components=1
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/
  make DESTDIR="$pkgdir" install

  sed -e 's|^#!/usr/bin/python$|#!/usr/bin/python2|g' \
      -i "$pkgdir"/usr/bin/sshfp
}

# vim:set ts=2 sw=2 et:
