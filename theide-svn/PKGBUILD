# Maintainer: Jan Dolinar <dolik.rce@gmail.com>

pkgname=theide-svn
pkgver=4365
pkgrel=1
pkgdesc="Modern IDE designed for developping large U++/C++ applications"
arch=('i686' 'x86_64')
url="http://www.ultimatepp.org"
license=('BSD')
groups=()
depends=('libnotify')
makedepends=('subversion')
provides=('theide')
conflicts=('theide')
replaces=()
backup=()
options=(!makeflags emptydirs)
install=
source=('license.txt')
noextract=()

_svntrunk="http://upp-mirror.googlecode.com/svn/trunk/"
# many users have already working copy of U++ on their system, so they
# can use it for building (e.g. to save network traffic or to speed up
# things) by setting $UPPSVN environment variable
if [ "x$UPPSVN" != "x" ]
then
  _svnmod=$UPPSVN
else
  _svnmod="$srcdir/uppsvn"
fi

build() {
  cd "$srcdir"
  #get sources
  for n in uppsrc uppbox/lpbuild
  do
    if [ -d $_svnmod/$n/.svn ]; then
      (cd $_svnmod/$n && svn up -r $pkgver)
    else
      svn co $_svntrunk$n/ --config-dir ./ -r $pkgver $_svnmod/$n
    fi
  done
  msg2 "SVN checkout done (or server timeout)"
  #prepare copy for building
  rm -rf "$srcdir/build"
  mkdir "$srcdir/build"
  cp -a "$_svnmod/uppsrc" "$srcdir/build/"
  cd "$srcdir/build"
  if [ -d "$srcdir/_out" ]
  then
    _fast="FAST=N" # to be safe if no header files changed
  else
    _fast="FAST=Y"
  fi
  #build
  echo "#define IDE_VERSION \"$pkgver-Arch\"" > uppsrc/ide/version.h
  msg2 "Building $pkgname..."
  make -f "$_svnmod/uppbox/lpbuild/Makefile" PKG=ide FLAGS="GCC GUI" BINPREFIX="$srcdir/_out/bin/the" BINEXT="" NESTS="uppsrc" OBJDIR="$srcdir/_out" $_fast
  #clean-up
  rm -rf "$srcdir/build"
}

package(){
  #install
  mkdir -p "$pkgdir/usr/bin"
  cp "$srcdir/_out/bin/theide" "$pkgdir/usr/bin/theide"
  #license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  cp "$srcdir/license.txt" "$pkgdir/usr/share/licenses/$pkgname"
  #man page
  mkdir -p "$pkgdir/usr/share/man/man1"
  cp "$_svnmod/uppbox/lpbuild/theide.1" "$pkgdir/usr/share/man/man1/theide.1"
  #desktop entry
  mkdir -p "$pkgdir/usr/share/applications"
  cp "$_svnmod/uppbox/lpbuild/theide.desktop" "$pkgdir/usr/share/applications"
  #icon
  mkdir -p "$pkgdir/usr/share/pixmaps"
  cp "$_svnmod/uppsrc/ide/theide-48.png" "$pkgdir/usr/share/pixmaps/theide.png"
  #fix permissions
  find "$pkgdir/usr/" -type f -exec chown root:root {} \; -exec chmod 644 {} \;
  chmod a+x "$pkgdir/usr/bin/theide"
}


md5sums=('b214709f096e4f50d61f50988359241e')
