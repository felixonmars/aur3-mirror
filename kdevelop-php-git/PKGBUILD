# Contributor: Harley Laue <losinggeneration@gmail.com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=(kdevelop-php-git)
pkgver=20121027
pkgrel=1
pkgdesc="PHP language plugin for KDevelop/Quanta - Git build"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
groups=('kde' 'kdevelop-plugins')
depends=('kdevplatform-git')
optdepends=('kdevelop-git')
makedepends=('cmake' 'automoc4' 'git' 'kdevelop-pg-qt-git')
provides=('kdevelop-php' 'kdevelop-extra-plugins-php-git')
conflicts=('kdevelop-php' 'kdevelop-extra-plugins-php-git')
replaces=('kdevelop-extra-plugins-php-git')
source=()
md5sums=()

_gitroot="git://anongit.kde.org/kdev-php"
_gitname="php"

build() {
    cd "$srcdir"
    msg "Connecting to Git server..."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "Git checkout done."
    msg "Starting make..."

    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake ../${_gitname} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr

    make
}

package() {
    cd "$srcdir/build"

    make DESTDIR="${pkgdir}" install
}
