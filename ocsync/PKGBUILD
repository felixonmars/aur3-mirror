# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is RelWithDebInfo to help with debugging.

_buildtype="RelWithDebInfo"

##############################################################

pkgname=ocsync
pkgver=0.60.1
pkgrel=1
pkgdesc="A file synchronizer especially designed for you, the normal user."
arch=("i686" "x86_64")
url="http://www.csync.org"
license=('GPL2')
depends=('log4c' 'sqlite3' 'iniparser' 'neon' 'smbclient' 'libssh')
makedepends=('cmake')
#optdepends=('samba: smb support' 'libssh: sftp support')
provides=('csync' 'csync-owncloud')
conflicts=('csync' 'csync-owncloud')
options=(!strip)
source=("http://download.owncloud.com/download/${pkgname}-${pkgver}.tar.bz2")
md5sums=('82784c9dbad695b314256025ab9567e4')

# Clean options array to strip pkg if release buildtype is chosen
if [[ ${_buildtype} == "Release" ]] || [[ ${_buildtype} == "release" ]]; then
  options=()
fi

build() {
  if [[ -e ${srcdir}/${pkgname}-${pkgver}-build ]]; then rm -rf ${srcdir}/${_name}-${pkgver}-build; fi
  mkdir ${srcdir}/${pkgname}-${pkgver}-build
  cd ${srcdir}/${pkgname}-${pkgver}-build
  
  cmake -DCMAKE_BUILD_TYPE=${_buildtype} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONF_INSTALL_DIR=/etc \
        -DLOG_TO_CALLBACK=on \
        ../${pkgname}-${pkgver}
  make
}
package() {
  cd ${srcdir}/${pkgname}-${pkgver}-build
  make DESTDIR=${pkgdir} install
}
