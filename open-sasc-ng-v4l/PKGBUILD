# Submitter and Maintainer:  Wessel Dirksen "p-we" <wdirksen at gmail dot com>
# Contributor: bas-t (responsible for hosting and further development of open-sasc-ng forked from sasc-ng rev-570)
# Contributor: Petr Vacek "vaca" (contributing patch and code from sascng-linux3-patch and providing cardslot.conf for serial port SC readers)
# Contributor: J.P. van Best (coding workaround for procfs changes on kernels >= 3.10)

pkgname=open-sasc-ng-v4l
pkgver=uname_gitsasc_gitv4l
pkgrel=1
pkgdesc="A SoftCAM creating virtual DVB interface - Compiled with V4L driver GIT library"
url="https://github.com/bas-t/sasc.git"
arch=('i686' 'x86_64')
license=('GPL')
depends=('linux-headers')
makedepends=('git' 'patchutils' 'perl-proc-processtable' 'linux-headers>=3.10' 'linux-headers<3.11')
conflicts=(sasc-ng)
provides=(sasc-ng)
backup=(etc/camdir/cardclient.conf etc/conf.d/sasc-ng etc/camdir/cardslot.conf)
install='sasc-ng.install'

_basekernel=3.10

source=('git://linuxtv.org/media_build.git' \
	'git://github.com/bas-t/sasc.git' \
	'cardclient.conf' 'sasc-ng.rc' 'sasc-ng.conf' \
	'sasc-ng.lr' 'cardslot.conf' 'sasc-ng.service' \
	'sasc-ng.install' 'v4l.patch')

sha256sums=('SKIP'
            'SKIP'
            '7caba03e515fe55aced4aad831e72ef3c0e59a3cdcea7bcdf79f7bfff6fcec75'
            '99ad8ff3733d5eca5885081ad6fec65d6b3febd79f992456a3415b17db9d7aa0'
            '0576abd9001ac17437d7613cadd51a000f8f78423b27f63af089a091dba1bd95'
            '620da70c775ce055a3f04041cf90e6d2acf7f7a57b0eecd07f240456d0069cf4'
            '436eb5a612aa3cb9e45bb2031429f3d41eb596ed65d18659d3bd708919c61253'
            '4e759fc72d2f54a8c797cadfba0719c872cfe797f30a94433a3c52c69f02b7af'
            'ebdc05878806c35f3915c0fe59904cef2fc39d7154e4fe6162e569b9e4d26b95'
            'eb64024d654308b80f0ec13596fddb7ca2a394e1e012702fb7614d31f2a04bfd')

pkgver() {

	cd $srcdir/sasc
 	_gitsasc=`git describe --always | sed 's|-|.|g'`

	cd $srcdir/media_build 
	_gitv4l=`git describe --always | sed 's|-|.|g'`

	_kernel=`uname -r | sed -r 's/-/_/g'`

	echo "$_kernel"_"$_gitsasc"_"$_gitv4l"
}

prepare() {

	cd $srcdir/media_build
	sed -i 's/system ("make") == 0 or die "build failed";/#system ("make") == 0 or die "build failed";/' build
	sed -i "s/Compilation finished. Use 'make install' to install them/Preparation finished. You can now patch the source/" build
	./build

	msg "Applying V4l patch..."
	cd $srcdir/media_build
	patch -p1 < $srcdir/v4l.patch
	make || return 1
}

build() {

	cd $srcdir/sasc/contrib/sasc-ng      
	./configure --compiletype=release --dvb_dir=$srcdir/media_build/linux
}

package() {

	mkdir -p "$pkgdir/usr/bin"
	mkdir -p "$pkgdir/usr/lib/modules/`uname -r`/extra"
	mkdir -p "$pkgdir/etc/conf.d"
	mkdir -p "$pkgdir/etc/rc.d"
	mkdir -p "$pkgdir/etc/camdir"
	mkdir -p "$pkgdir/etc/logrotate.d/"
	mkdir -p "$pkgdir/usr/lib/systemd/system/"
      
	install -m0755 "$srcdir/sasc/contrib/sasc-ng/sasc-ng" "$pkgdir/usr/bin/"
	install -m0644 "$srcdir/sasc/contrib/sasc-ng/dvbloopback.ko" "$pkgdir/usr/lib/modules/`uname -r`/extra"
	install -m0755 "$srcdir/sasc-ng.rc" "$pkgdir/etc/rc.d/sasc-ng"
	install -m0644 "$srcdir/sasc-ng.conf" "$pkgdir/etc/conf.d/sasc-ng"
	install -m0644 "$srcdir/cardclient.conf" "$pkgdir/etc/camdir/cardclient.conf"
	install -m0644 "$srcdir/cardslot.conf" "$pkgdir/etc/camdir/cardslot.conf"
	install -m0644 "$srcdir/sasc-ng.lr" "$pkgdir/etc/logrotate.d/open-sasc-ng.lr"
	install -m0644 "$srcdir/sasc-ng.service" "$pkgdir/usr/lib/systemd/system/sasc-ng.service"
}


