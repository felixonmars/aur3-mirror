# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-devel-stacktrace
pkgver=1.31
pkgrel=1
pkgdesc="Stacktrace object w/ info form caller()"
arch=('any')
url="http://search.cpan.org/~DROLSKY/Devel-StackTrace"
license=('PerlArtistic')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/D/DR/DROLSKY/Devel-StackTrace-$pkgver.tar.gz)
md5sums=('f7d3ba1371eac6b6a5855ba3dc1dbcd4')
sha512sums=('1412ba5b2223cfe79e4482b4cdbd8e5d3b81059043a2f4e17923e486f97650bb1ce0d2018c661efd7a3248f01316ccdcc35bb96499cf22cf1c78d131e9ad8905')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/Devel-StackTrace-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
