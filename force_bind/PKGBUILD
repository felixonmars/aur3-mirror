# Maintainer: Prurigro
# Contributor: Prurigro

pkgname=force_bind
pkgver=0.12
pkgrel=1
pkgdesc="Force binding on a specific IP and/or port, change TTL/TOS/KA/MSS/REUSEADDR/FWMARK/PRIORITY. Works with both IPv4 and IPv6. Also, you can enforce bandwidth per process or per socket"
url="http://kernel.embedromix.ro/us/"
license=('GPL3')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('glibc')

source=("http://kernel.embedromix.ro/us/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.1.gz"
        "${pkgname}_null-to-strcmp-fix.patch")
sha512sums=('a208c359ebbc71a01c6ae551f9793cb152046225923385f676751ab7427590291bcc9b78dd250ff99567d44def7ff0335dc95a36e1d553c24c67fabd15419ff4'
            '6818df0c63f8d7179e865539c55340372ae4ecbc9990bd09d3d60b6c52783b4ea659935263a4590a0dfa74c050672f4a36921109b70f1a112a25e7977fb12cc8'
            '3e33eb1158dd89737fa62c018ff5af0419a3900315875363f30194c8ae0cceecd943d2c8e1f139127f5b092c428597f1a570ff072c0c29a60082bc6b990f4be9')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 < ../${pkgname}_null-to-strcmp-fix.patch
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

package() {
    install -Dm644 ${pkgname}.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
}
