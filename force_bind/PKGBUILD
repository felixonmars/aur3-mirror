# Maintainer: Prurigro
# Contributor: Prurigro

pkgname=force_bind
pkgver=0.11
pkgrel=5
pkgdesc="Force binding on a specific IP and/or port, change TTL/TOS/KA/MSS/REUSEADDR/FWMARK/PRIORITY. Works with both IPv4 and IPv6. Also, you can enforce bandwidth per process or per socket"
url="http://kernel.embedromix.ro/us/"
license=('GPL3')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('glibc')

source=("http://kernel.embedromix.ro/us/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.1.gz"
        "${pkgname}_null-to-strcmp-fix.patch")
sha512sums=('4671554bcee3a3ac9879e6cfcc5ee1621451c02dbc2afca00da337b500e9bd9b071cde8edcf1fb08cb039a7f3833b0c913e92cc7cc1b0b9d4c20b711bbb197df'
            '6818df0c63f8d7179e865539c55340372ae4ecbc9990bd09d3d60b6c52783b4ea659935263a4590a0dfa74c050672f4a36921109b70f1a112a25e7977fb12cc8'
            '3e33eb1158dd89737fa62c018ff5af0419a3900315875363f30194c8ae0cceecd943d2c8e1f139127f5b092c428597f1a570ff072c0c29a60082bc6b990f4be9')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 < ../${pkgname}_null-to-strcmp-fix.patch
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

package() {
    install -Dm644 ${pkgname}.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
}
