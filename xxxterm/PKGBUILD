# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=xxxterm
pkgver=1.8.0
pkgrel=1
pkgdesc="A minimalists web browser, vi-like while maintaining traditional web browser behavior."
arch=('i686' 'x86_64')
url="http://opensource.conformal.com/wiki/XXXTerm"
license=('custom:ISC')
depends=('libwebkit' 'gtk2' 'libbsd')
optdepends=()
source=(http://opensource.conformal.com/snapshots/$pkgname/$pkgname-$pkgver.tgz \
        LICENSE \
        xxxterm.conf)
md5sums=('783062d6c2d5a08b0bcf924431e95f18'
         'f3eeb6e8b70a3dcccb8ee57daf584c9e'
         'ab57148880673bd4a8affe7d8b326da3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # no spam
  sed -i 's|https://www.cyphertite.com|https://archlinux.org|' xxxterm.{c,conf}
  cd linux
  sed -i 's/gnutls/& libbsd/' Makefile
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/linux"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  cd ..
  install -Dm644 xxxterm.conf "$pkgdir/etc/skel/xxxterm.conf"
  cd ../..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

