# Contributor: Jerome Berger <jeberger@free.fr>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>

# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

# See http://gcc.gnu.org/install/prerequisites.html

pkgname=gdc-git
pkgver=20120905
pkgrel=1
_gccver=4.8-20120902
pkgdesc="GDC, The D Programming Language (D2) frontend for GCC. GIT master branch compiled with GCC trunk snapshots (necessary)."
arch=('i686' 'x86_64')
url="http://bitbucket.org/goshawk/gdc/wiki/Home"
license=('GPL')
provides=('gdc')
depends=(perl libmpc)
makedepends=('binutils>=2.22' 'git')
conflicts=('gdc' 'gdc-bin' 'gdc1-hg')
options=('!libtool' '!emptydirs')
source=(ftp://gcc.gnu.org/pub/gcc/snapshots/$_gccver/gcc-$_gccver.tar.bz2)
md5sums=('45f5a25d3b03121da5317cc922960be5')

_gitroot=https://github.com/D-Programming-GDC/GDC.git
_gitname=gdc
# _gitrev=316ad9c558450c8826c7ae4c8d9a6a0fed2a0b0d

_basedir="$srcdir/gcc-$_gccver"

build()
{
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname"
    git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname"
    # patch -p1 -i "$srcdir/gdc-gcc-patches.patch"
    # git checkout "$_gitrev"
  fi

  msg "GIT checkout done or server timeout"

  msg "Starting build..."

  cd "$_basedir"

  cd "$srcdir/$_gitname"
  ./update-gcc.sh ../gcc-${_gccver} --setup

  cd "$srcdir"
  if [ ! -d "gcc-build" ]; then
    mkdir gcc-build
  fi
  cd gcc-build

  "$_basedir"/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --with-bugurl=https://github.com/D-Programming-GDC/GDC/issues/ \
      --enable-languages=d \
      --disable-multilib --disable-nls --disable-bootstrap \
      --disable-libgomp --disable-libmudflap --disable-libquadmath \
      --disable-shared
  make 2>&1 | tee build.log
}

package()
{
  cd "$srcdir"/gcc-build

  # Easier to just do a full install and remove the excess later
  make DESTDIR=$pkgdir install

  # Delete all the stuff we don't need
  # rm -rf $pkgdir/usr/lib/gcc/$CHOST/$_gccver/{*.a,*.so*,lto-wrapper,lto1,plugin*}
  # rm -rf $pkgdir/usr/lib/gcc/$CHOST/$_gccver/{crt*,cc1,collect2,include*,install*}
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/lib/{libgcc*,libiberty*,libitm*,libstdc++*,libsupc++*}
  rm -rf $pkgdir/usr/share/man/man1/{cpp,gc,g++}*
  rm -rf $pkgdir/usr/share/man/man7/{fsf,gfdl,gpl}*
  rm -rf $pkgdir/usr/bin/{gcc,gcov,cpp,$CHOST-gcc,$CHOST-gcc-$_gccver,c++,g++,$CHOST-c++,$CHOST-g++}
  rm -rf $pkgdir/usr/bin/{gcc-ar,gcc-nm,gcc-ranlib,$CHOST-gcc-ar,$CHOST-gcc-nm,$CHOST-gcc-ranlib}

  # Fix permissions
  chmod 644 $pkgdir/usr/lib/*
  chmod 755 $pkgdir/usr/lib/gcc
}
