_pkgname=nyancat
pkgname=$_pkgname-git
pkgver=20121012
pkgrel=1
pkgdesc="Terminal-based Pop Tart Cat animation"
arch=(i686 x86_64)
url=http://miku.acm.uiuc.edu
license=(NCSA)
provides=($_pkgname)
conflicts=($_pkgname)
replaces=($_pkgname)
source=($_pkgname.rc)
md5sums=(6401df9f5d579bf6cecaaeea898c77e6)
optdepends=('socat: Telnet Daemon initscript')

_gitroot=https://github.com/klange/$_pkgname.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]] ; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    make
}

package() {
    cd "$srcdir"/$_gitname-build/

    install -Dm755 src/$_pkgname "$pkgdir"/usr/bin/$_pkgname
    install -Dm755 "$srcdir"/$_pkgname.rc "$pkgdir"/etc/rc.d/$_pkgname
    install -Dm644 $_pkgname.1 "$pkgdir"/usr/share/man/man1/$_pkgname.1
}
