# Maintainer: Alexander Rødseth <rodseth@gmail.com>
pkgname=cuse
pkgver=0.51
pkgrel=1
pkgdesc="MIDI sequencer for the terminal"
arch=('x86_64' 'i686')
url="http://pi4.informatik.uni-mannheim.de/~haensel/cuse/index_en.html"
license=('GPL')
depends=('libcdk')
md5sums=('6c40c56f1076f66f9e0d5f4185562793')
source=("http://pi4.informatik.uni-mannheim.de/~haensel/cuse/files/$pkgname-$pkgver-src.tar.gz")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Configuring..."
  ./configure --prefix=/usr CXXFLAGS="-fpermissive" 

  msg2 "Building..."
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Packaging..."
  make DESTDIR="$pkgdir/" install

  msg2 "Packaging the documentation..."
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cp doc/*_en.html "$pkgdir/usr/share/doc/$pkgname/"
  cp -r doc/images "$pkgdir/usr/share/doc/$pkgname/"
  mv "$pkgdir/usr/share/doc/$pkgname/index_en.html" \
    "$pkgdir/usr/share/doc/$pkgname/index.html"
  find "$pkgdir/usr/share/doc" -name "*~" -exec rm -r {} +

  msg2 "Packaging the license..."
  install -Dm644 COPYING \
    $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
