# Maintainer: Viech <viech unvanquished net>
# Contributor: Gereon Schomber
# Contributor: Martin F. Schumann <mfs@mfs.name>

# "branch" or "tag", depending on what you want to build
_type=branch

# the branch or tag to check out
_checkout=master

pkgname=unvanquished-git
pkgver=v0.21.0.0.ge5c3538
pkgrel=1
pkgdesc='A team-based fps/rts hybrid game which pits aliens against humans. Bleeding edge version that might be too new for official servers.'
arch=('x86_64' 'i686')
url='http://www.unvanquished.net'
license=('GPL3')
makedepends=('cmake')
depends=('curl'               'freetype2' 'glew'               'gmp'
         'libjpeg-turbo'      'ncurses'   'libogg'             'libpng'
         'sdl2'               'libvorbis' 'zlib'               'libwebp>=0.2.0'
         'libtheora'          'nettle'    'speex'              'xvidcore'
         'openal'             'xdg-utils' 'desktop-file-utils' 'shared-mime-info'
         'hicolor-icon-theme' 'geoip')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished')
options=('emptydirs')
backup=('etc/unvanquished/main/server.cfg' 'etc/unvanquished/main/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=("unvanquished::git+https://github.com/Unvanquished/Unvanquished.git#${_type}=${_checkout}"
        'ChangeLog'                   'unvanquished.install' 'unvanquished.sh'   'unvanquished-tty.sh'
        'unvanquished-update-paks.sh' 'unvanquished.service' 'unvanquished.conf' 'unvanquished.desktop')

pkgver() {
	cd $srcdir/unvanquished
	local ver="$(git describe --long)"
	printf "%s" "${ver//-/.}"
}

build() {
	cd $srcdir/unvanquished
	cmake -D GAME_QVM=OFF -D BUILD_TTY_CLIENT=ON .
	make
}

package() {
	# create installation directories
	cd $pkgdir

	install -d     etc/conf.d \
	               etc/unvanquished/main \
	               usr/bin \
	               usr/lib/systemd/system \
	               usr/lib/unvanquished/main \
	               usr/share/applications \
	               usr/share/icons/hicolor/128x128/apps \
	               usr/share/licenses/unvanquished \
	               var/cache/unvanquished/update-paks \
	               var/lib/unvanquished/main \
	               var/lib/unvanquished-server/main

	# install content
	cd $srcdir/unvanquished

	install -m 755 daemon                      \
	               daemon-tty                  \
	               daemonded                   \
	               *.so                        \
		       download-pk3.sh             $pkgdir/usr/lib/unvanquished/
	install -m 755 main/*.so                   $pkgdir/usr/lib/unvanquished/main/
	install -m 644 debian/unvanquished.png     $pkgdir/usr/share/icons/hicolor/128x128/apps/
	install -m 644 COPYING.txt                 $pkgdir/usr/share/licenses/unvanquished/
	install -m 644 debian/configfiles/*        $pkgdir/etc/unvanquished/main/

	# install starters
	cd $srcdir

	install -m 644 unvanquished.conf           $pkgdir/etc/conf.d/
	install -m 755 unvanquished.sh             $pkgdir/usr/bin/unvanquished
	install -m 755 unvanquished-tty.sh         $pkgdir/usr/bin/unvanquished-tty
	install -m 755 unvanquished-update-paks.sh $pkgdir/usr/bin/unvanquished-update-paks
	install -m 644 unvanquished.service        $pkgdir/usr/lib/systemd/system/
	install -m 644 unvanquished.desktop        $pkgdir/usr/share/applications/
	
	# setup server home directory
	cd $pkgdir/var/lib/unvanquished-server/main

	ln -s ../../../../etc/unvanquished/main configuration.pk3dir
}

md5sums=('SKIP'
         '6865da658a1700fbb3c5b50bf3c7c835'
         '7d8d5e1d05978745abdcaa624be7c47b'
         'ac9c54f828edbbf236156431991f6c82'
         '7a10dca92e352e6f47d99a0434681f4e'
         '58a22722faca197c5017ef26fa622826'
         '1842789de67de55b8027c51cad22f778'
         'ce04c7e8423b2da0ea0d8df8be4c8ea4'
         'ac69d49b3c665d274d0ab58870220522')
