# Maintainer: Martin F. Schumann <mfs@mfs.name>
pkgname=unvanquished-git
pkgver=20120603
pkgrel=2
pkgdesc="A unique team-based first-person shooter, which pits the aliens against the humans."
arch=('x86_64' 'i686')
url="http://www.unvanquished.net"
license=('GPL3')
groups=()
depends=('curl' 'freetype2' 'glew' 'gmp' 'libjpeg-turbo' 'ncurses' 'libogg' 'openal' 'libpng' 'sdl' 'libvorbis' 'zlib' 'libwebp' 'libtheora' 'nettle' 'speex')
makedepends=('git' 'cmake')
optdepends=('unvanquished-maps: The unvanquished maps package, needed to run a server')
provides=('unvanquished')
conflicts=()
replaces=()
backup=('etc/unvanquished-server/maprotation.cfg' 'etc/unvanquished-server/server.cfg')
options=()
install='unvanquished.install'
changelog='changelog'
source=('unvanquished.sh' 'unvanquished-download-paks.sh' 'unvanquished.desktop' 'unvanquished.install' 'unvanquished-server.sh' 'changelog')
noextract=()
md5sums=('ac17e7744e1bb0e759e7f63e6634376f'
         '40dd5f8958bee9764815f8fbd2208167'
         '9128f8aaa682de2adb6258789cf83cb1'
         '13218ff55aee0039c34e66a6a593a3f0'
         '999d6be8043fa9ecda19710f5c7ad919'
         '5bc7b1e3137c65924f7d60cbfc26764d')

_gitroot="https://github.com/Unvanquished/Unvanquished.git"
_gitname="unvanquished"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "$srcdir/$_gitname"

  cmake -D CMAKE_INSTALL_PREFIX="/usr" . && make
}

package() {
  # Create pkg dirs
  cd "$pkgdir"
  install -d opt/unvanquished/main var/lib/unvanquished usr/share/icons/hicolor/128x128/apps usr/share/applications
  
  # install files
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
  mv "$pkgdir"/usr/bin/* "$pkgdir"/opt/unvanquished/
  install -m 644 debian/unvanquished.png "$pkgdir"/usr/share/icons/hicolor/128x128/apps/
  install -m 755 download-pk3.sh "$pkgdir"/opt/unvanquished/
  cp -r main/* "$pkgdir"/opt/unvanquished/main/
  install *.so "$pkgdir"/opt/unvanquished/

  cd "$srcdir"
  install -m 755 unvanquished*.sh "$pkgdir"/usr/bin/
  install -m 644 unvanquished.desktop "$pkgdir"/usr/share/applications/

  # Download and install pk3 files
  "$_gitname"/download-pk3.sh dl/main dl/cache
  if [ -e dl/main/navmesh-default-0.1.pk3 ]; then rm -f dl/main/navmesh-default-0.1.pk3; fi
  cp -r dl/main "$pkgdir"/opt/unvanquished/
  cp -r dl/cache/* "$pkgdir"/var/lib/unvanquished/

  # link arch-specific executables
  cd "$pkgdir"/opt/unvanquished
  if [ "$CARCH" == "i686" ]; then a="i386"; else a="$CARCH"; fi
  ln -s daemon.$a unvanquished
  ln -s daemonded.$a unvanquishedded

  # install server config
  cd "$srcdir/$_gitname"
  install -d "$pkgdir"/etc/unvanquished-server
  cp -r debian/config/* "$pkgdir"/etc/unvanquished-server/
  
  msg 'The unvanquished client should be started with /usr/bin/unvanquished.sh'
  msg 'The server is supposed to be started as the user "unvanquished-server" (which will be created now), i.e. by issuing sudo -u unvanquished-server /usr/bin/unvanquished-server.sh'
}

# vim:set ts=2 sw=2 et:
