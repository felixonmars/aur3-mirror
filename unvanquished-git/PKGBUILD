# Maintainer: Viech
# Contributor: Martin F. Schumann <mfs@mfs.name>

pkgname=unvanquished-git
pkgver=20121105
pkgrel=1
pkgdesc="A team-based fps/rts hybrid game which pits aliens against humans. Bleeding edge version that might be too new for official servers."
arch=('x86_64' 'i686')
url="http://www.unvanquished.net"
license=('GPL3')
depends=('curl' 'freetype2' 'glew' 'gmp' 'libjpeg-turbo' 'ncurses' 'libogg' 'libpng' 'sdl' 'libvorbis' 'zlib' 'libwebp>=0.2.0' 'libtheora' 'nettle' 'speex' 'xvidcore' 'openal' 'xdg-utils' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished')
options=('emptydirs')
backup=('srv/unvanquished/main/server.cfg' 'srv/unvanquished/main/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=('unvanquished.install' 'unvanquished.sh' 'unvanquished-server.sh' 'unvanquished-update-paks.sh' 'unvanquished-rc.sh' 'unvanquished.desktop' 'ChangeLog')

md5sums=('106ef331d5889bff18fe099b2e7bbcf8'
         '09ed4b00045453b68a0a6fda07299b0e'
         'b47705a01575c2c1c2ef7bb4a5bcf8d7'
         '1da9acdc7e8f83610dd6398ba2b38e72'
         '759173428e2d8686d2bb840a9948fb8b'
         'ac69d49b3c665d274d0ab58870220522'
         '7329a91bb8c14ab1e93008a1da4e6ab5')

_name=unvanquished

_gitroot="https://github.com/Unvanquished/Unvanquished.git"
_gitname=$_name

build() {
	cd $srcdir
	
	msg "Connecting to GIT server ..."

	if [[ -d $_gitname ]]; then
		msg "Updating local files ..."
		cd $_gitname
		git pull
	else
		msg "Cloning repository ..."
		git clone $_gitroot $_gitname
	fi

	msg "The local files are up to date."
	msg "Starting build ..."

	cd $srcdir/$_name
	
	cmake -D CMAKE_INSTALL_PREFIX="" . && make
}

package() {
	# create installation dirs
	cd $pkgdir

	install -d opt/$_name usr/bin etc/rc.d usr/share/applications var/cache/$_name/update-paks
	install -dm 775 srv/$_name/main

	# install starters
	cd $srcdir

	install -m 755 $_name.sh $pkgdir/usr/bin/$_name
	install -m 755 $_name-server.sh $pkgdir/usr/bin/$_name-server
	install -m 755 $_name-update-paks.sh $pkgdir/usr/bin/$_name-update-paks
	install -m 755 $_name-rc.sh $pkgdir/etc/rc.d/$_name
	install -m 644 $_name.desktop $pkgdir/usr/share/applications/
	
	# install base files
	cd $srcdir/$_name
	
	install -m 755 daemon daemonded *.so download-pk3.sh $pkgdir/opt/$_name/
	install -m 644 COPYING.txt GPL.txt README.txt debian/$_name.png $pkgdir/opt/$_name/
	
	cp -pr main $pkgdir/opt/$_name/
	chown root:root $pkgdir/opt/$_name/main
	
	# setup server directory 
	install -m 660 debian/config/* $pkgdir/srv/$_name/main/
	
	cd $pkgdir/srv/$_name/

	ln -s . .Unvanquished

	# link game binarys under suitable name
	cd $pkgdir/opt/$_name/
	
	ln -s daemon unvanquished
	ln -s daemonded unvanquishedded
}
