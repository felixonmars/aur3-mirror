# Maintainer: Martin F. Schumann <mfs@mfs.name>
pkgname=unvanquished-git
pkgver=20120714
pkgrel=1
pkgdesc="A unique team-based first-person shooter, which pits the aliens against the humans."
arch=('x86_64' 'i686')
url="http://www.unvanquished.net"
license=('GPL3')
groups=()
depends=('curl' 'freetype2' 'glew' 'gmp' 'libjpeg-turbo' 'ncurses' 'libogg' 'libpng' 'sdl' 'libvorbis' 'zlib' 'libwebp' 'libtheora' 'nettle' 'speex' 'xvidcore' 'hicolor-icon-theme' 'xdg-utils' 'openal')
makedepends=('git' 'cmake')
optdepends=()
provides=('unvanquished')
conflicts=('unvanquished-maps')
replaces=()
backup=('etc/unvanquished-server/maprotation.cfg' 'etc/unvanquished-server/server.cfg')
options=()
install='unvanquished.install'
changelog='changelog'
source=('unvanquished.sh' 'unvanquished-download-paks.sh' 'unvanquished.desktop' 'unvanquished.install' 'unvanquished-server.sh' 'changelog' 'cmake_install-i686.patch' 'cmake_install-x86_64.patch')
noextract=()
md5sums=('fe56a773c62f74a9ad4ede92757c505a'
         '40dd5f8958bee9764815f8fbd2208167'
         '9128f8aaa682de2adb6258789cf83cb1'
         '1df68792a60ae85fd7fa3f9b14bde024'
         '82f011ae79a87e1220dfb9f4650bc1bf'
         'e1808858a671b5daf4b9dcf16bb8d880'
         '1b18bf9a0513a5b03792c1e8da8643ea'
         'a6d3e2cbd195c4d3083335e08543d357')

_gitroot="https://github.com/Unvanquished/Unvanquished.git"
_gitname="unvanquished"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "$srcdir/$_gitname"

  cmake -D CMAKE_INSTALL_PREFIX="" . && make
  
  patch cmake_install.cmake -i ../cmake_install-$CARCH.patch
}

package() {
  # Create pkg dirs
  cd "$pkgdir"
  install -d var/lib/unvanquished usr/share/applications usr/bin opt
  
  # install files
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
  mv "$pkgdir"/Unvanquished "$pkgdir"/opt/$pkgname
  install -m 644 debian/unvanquished.png "$pkgdir"/opt/$pkgname/
  install -m 755 download-pk3.sh "$pkgdir"/opt/$pkgname/
#  cp -r main/* "$pkgdir"/opt/$pkgname/main/
#  install *.so "$pkgdir"/opt/$pkgname/

  cd "$srcdir"
  install -m 755 unvanquished*.sh "$pkgdir"/usr/bin/
  install -m 644 unvanquished.desktop "$pkgdir"/usr/share/applications/

  # Download and install pk3 files
  "$_gitname"/download-pk3.sh dl/main dl/cache
  if [ -e dl/main/navmesh-default-0.1.pk3 ]; then rm -f dl/main/navmesh-default-0.1.pk3; fi
  cp -r dl/main "$pkgdir"/opt/$pkgname/
  cp -r dl/cache/* "$pkgdir"/var/lib/unvanquished/

  # link arch-specific executables
  cd "$pkgdir"/opt/$pkgname
  if [ "$CARCH" == "i686" ]; then a="i386"; else a="$CARCH"; fi
  ln -s daemon.$a unvanquished
  ln -s daemonded.$a unvanquishedded

  # install server config
  cd "$srcdir/$_gitname"
  install -d "$pkgdir"/etc/unvanquished-server
  cp -r debian/config/* "$pkgdir"/etc/unvanquished-server/
  
  msg 'The unvanquished client should be started with /usr/bin/unvanquished.sh'
  msg 'The server is supposed to be started as the user "unvanquished-server" (which will be created now), i.e. by issuing sudo -u unvanquished-server /usr/bin/unvanquished-server.sh'
}

# vim:set ts=2 sw=2 et:
