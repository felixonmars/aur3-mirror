# Maintainer:  Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=qemu-git
pkgver=20110327
pkgrel=1
pkgdesc="Processor emulator and virtual machine with with expansive multi-arch support and fast operation (with an accelerator), dev tree version."
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2')
url="http://wiki.qemu.org/Index.html"

makedepends=('git' 'texi2html' 'perl')
depends=('gnutls>=2.4.1' 'bluez' 'vde2' 'util-linux-ng' 'curl' 'libsasl'
         'sdl' 'alsa-lib' 'esound')
conflicts=('qemu' 'kvm')
provides=('qemu')

source=('65-kvm.rules'
        'qemu.install')
sha256sums=('c16a8dc7855880b2651f1a3ff488ecc54d4ac1036c71fffd5007021d8d18a7c5'
            '910e845f3da5c4ea38b3f548dab8c0cfbcdd62822cd38afa2175ba23ac2c31f2')
install='qemu.install'

_gitroot="git://repo.or.cz/qemu.git"
_gitname=qemu


options=('!strip')
build()
{
	msg2 "Performing source checkout..."
	if [ -d $_gitname ]; then
		(cd $_gitname && git pull origin)
	else
		git clone "$_gitroot"
	fi
	msg2 "Source checkout finished."
	rm -rf $_gitname-build
	git clone $_gitname $_gitname-build
	cd $_gitname-build

	sed -i -e 's/lib64/lib/g' x86_64.ld

	# http://www.mail-archive.com/qemu-devel@nongnu.org/msg40524.html
	./configure --prefix=/usr --audio-drv-list=alsa,oss,sdl,esd \
	            --audio-card-list=ac97,sb16,es1370,adlib --enable-docs \
		    --extra-cflags=-D__EXPORTED__HEADERS__ --disable-werror \
	            --disable-spice
	make
}
package() {
	cd "$srcdir"/$_gitname-build

	make DESTDIR="$pkgdir" install

	install -Dm644 "$srcdir"/65-kvm.rules \
	               "$pkgdir"/lib/udev/rules.d/65-kvm.rules
}
