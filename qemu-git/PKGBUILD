# Maintainer:  Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Frederic Bezies <fredbezies@gmail.com>

pkgname=qemu-git
_gitname=qemu
pkgver=2.0.r30771.ga455044
pkgrel=1
pkgdesc="A generic and open source processor emulator which achieves a good emulation speed by using dynamic translation. Git version."
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2')
url="http://wiki.qemu.org/Index.html"
depends=('pixman' 'libjpeg' 'libpng' 'sdl' 'alsa-lib' 'nss' 'glib2'
         'gnutls>=2.4.1' 'bluez-libs' 'vde2' 'util-linux' 'curl' 'libsasl'
         'libgl' 'libpulse' 'libcap-ng' 'libaio' 'libseccomp'
         'libiscsi' 'libcacard' 'spice' 'seabios-git')
makedepends=('git' 'texi2html' 'perl' 'python2' 'dtc' 'spice-protocol')

optdepends=('ovmf-svn: UEFI Firmware (OVMF) with Secure Boot Support - for Virtual Machines (QEMU)')
conflicts=('qemu' 'kvm' 'kvm-git' 'qemu-spice' 'libcacard')
provides=('qemu' 'qemu-kvm' 'qemu-spice' 'libcacard')
backup=('etc/qemu/target-x86_64.conf')
source=('git://git.qemu.org/qemu.git'
        '65-kvm.rules'
        'qemu.install')
sha256sums=('SKIP'
            '9c8a15c34461a9481a34ca9e0ab4ae3825eabe8fd863227f2445325413cd755c'
            '537eb07baa44bd846a01311f538fa89540e40e6360b8a794fbfa0dea4ccaa955')
install='qemu.install'
options=(!strip)

pkgver() {
	cd "${srcdir}/$_gitname"
	echo "2.0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
}

build()
{
	cd $_gitname
	# qemu vs. make 4 == bad
  	export ARFLAGS="rv"
  	# http://permalink.gmane.org/gmane.comp.emulators.qemu/238740
  	# gtk gui breaks keymappings at the moment

 	./configure --prefix=/usr --sysconfdir=/etc --audio-drv-list='pa alsa sdl' \
              --python=/usr/bin/python2 --smbd=/usr/bin/smbd \
              --enable-docs --libexecdir=/usr/lib/qemu \
              --disable-gtk --enable-linux-aio --enable-seccomp \
              --enable-spice --localstatedir=/var --disable-werror
  	make V=99
}

package() {
 
  cd $_gitname

  make DESTDIR="${pkgdir}" libexecdir="/usr/lib/qemu" install
  # provided by seabios package
  rm "${pkgdir}/usr/share/qemu/bios.bin"
  rm "${pkgdir}/usr/share/qemu/acpi-dsdt.aml"
  rm "${pkgdir}/usr/share/qemu/q35-acpi-dsdt.aml"
  # remove conflicting /var/run directory
  rm -r "${pkgdir}/var"
  install -D -m644 "${srcdir}/65-kvm.rules" \
                   "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"
  # bridge_helper needs suid
  # https://bugs.archlinux.org/task/32565
  chmod u+s "${pkgdir}/usr/lib/qemu/qemu-bridge-helper"
  # add sample config
  echo "allow br0" > ${pkgdir}/etc/qemu/bridge.conf.sample
  # strip scripts directory
    find "${pkgdir}/usr/src/linux-${_kernver}/scripts"  -type f -perm -u+w 2>/dev/null | while read binary ; do
      case "$(file -bi "$binary")" in
        *application/x-executable*) # Binaries
        /usr/bin/strip $STRIP_BINARIES "$binary";;
      esac
    done
  # remove libcacard files
  # rm -rf ${pkgdir}/usr/include/cacard
  # rm -rf ${pkgdir}/usr/lib/libcacard*
  # rm -rf ${pkgdir}/usr/lib/pkgconfig/libcacard.pc
  # rm -rf ${pkgdir}/usr/bin/vscclient
}

