# Maintainer: Yuval Adam <yuv dot adm at gmail dot com> PGP-Key: CC2115C12D99D2F0
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Art Gramlich <art@gramlich-net.com>

pkgname=icu-staticlibs
pkgver=53.1
pkgrel=1
pkgdesc="International Components for Unicode library with staticlibs enabled"
arch=(i686 x86_64)
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('gcc-libs>=4.7.1-5' 'sh')
options=(staticlibs)
#makedepends=('clang')
_orig_pkgname=icu
source=(#http://download.icu-project.org/files/${_orig_pkgname}4c/${pkgver}/${_orig_pkgname}4c-${pkgver/./_}-src.tgz
	    http://download.icu-project.org/files/${_orig_pkgname}4c/${pkgver}/${_orig_pkgname}4c-${pkgver//./_}-src.tgz
	    icu.8198.revert.icu5431.patch
	    icu.fix.dtfmttst.patch)
md5sums=('b73baa6fbdfef197608d1f69300919b9'
         'ebd5470fc969c75e52baf4af94a9ee82'
	 '98d536340e2ac76d99cc910c478333d0')

prepare() {
  cd icu/source
  # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
  patch -Rp3 -i ${srcdir}/icu.8198.revert.icu5431.patch
  # fix date-sensitive year formatting test (fixed upstream)
  patch -p2 -i ${srcdir}/icu.fix.dtfmttst.patch
}

build() {
  cd icu/source
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--mandir=/usr/share/man \
	--sbindir=/usr/bin \
	--enable-static
  make
}

check() {
  cd icu/source
  make -k check # passes all
}

package() {
  cd icu/source
  make -j1 DESTDIR=${pkgdir} install

  # Install license
  install -Dm644 ${srcdir}/icu/license.html ${pkgdir}/usr/share/licenses/icu/license.html
}
