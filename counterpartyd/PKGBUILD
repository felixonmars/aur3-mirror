# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=counterpartyd
pkgver=9.42.0
pkgrel=1
pkgdesc="An open marketplace, built on Bitcoin"
arch=('any')
depends=('leveldb' 'python' 'python-appdirs' 'python-apsw' 'python-crypto' 'python-dateutil' 'python-flask' 'python-flask-httpauth' 'python-json-rpc' 'python-prettytable' 'python-pycoin' 'python-pytest' 'python-pyzmq' 'python-requests' 'python-tornado' 'sqlite' 'zeromq')
makedepends=('python-sphinx')
optdepends=('bitcoin-core: Bitcoin core headless P2P node'
            'insight-bitcore: Bitcoin blockchain API'
            'pyrpcwallet-git: same commands and same result as bitcoin-core JSON-RPC API')
groups=('counterparty')
url="https://www.counterparty.co"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/CounterpartyXCP/$pkgname/tar.gz/v$pkgver
        counterpartyd.conf
        counterpartyd.logrotate
        counterpartyd.service)
sha256sums=('954b0eb6c09ac0561bdddbea1f656c4b58f040648f070717e31386028697e0ca'
            'adc50cb97b31f7b4bb5d8657322369b7d015364c1561aca14ff29c3286ccc12b'
            '0977538447f05ef133f76784fb88e02410ad4f7b0e01b4525efca1cd39cc15d1'
            '1ab7d2ec9f43887e4cc1971e8493f273f6214528f70565b6358bb98d0b618fc8')
install=counterpartyd.install

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building man pages...'
  make man -C docs/
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/counterpartyd/LICENSE"

  msg 'Installing user manual...'
  for _manpage in `find docs/_build/man/ -type f -name "*.1" -printf '%f\n'`; do
    install -Dm 644 docs/_build/man/$_manpage "$pkgdir/usr/share/man/man1/$_manpage"
  done

  msg 'Installing documentation...'
  install -Dm 644 CHANGELOG.md "$pkgdir/usr/share/doc/counterpartyd/CHANGELOG.md"
  install -Dm 644 README.md    "$pkgdir/usr/share/doc/counterpartyd/README.md"

  msg 'Installing appdirs...'
  install -dm 755 "$pkgdir/usr/share/counterpartyd"
  install -dm 700 "$pkgdir/var/log/counterpartyd"
  for _appdir in lib test; do
    cp -dpr --no-preserve=ownership $_appdir "$pkgdir/usr/share/counterpartyd/$_appdir"
  done

  msg 'Installing appfiles...'
  for _appfile in __init__.py version.json; do
    install -Dm 644 $_appfile "$pkgdir/usr/share/counterpartyd/$_appfile"
  done

  msg 'Installing counterpartyd executable...'
  install -Dm 755 counterpartyd.py "$pkgdir/usr/share/counterpartyd/counterpartyd.py"

  msg 'Installing counterpartyd.conf...'
  install -Dm 600 "$srcdir/counterpartyd.conf" "$pkgdir/etc/counterpartyd/counterpartyd.conf"

  msg 'Installing counterpartyd.service'
  install -Dm 644 "$srcdir/counterpartyd.service" "$pkgdir/usr/lib/systemd/system/counterpartyd.service"

  msg 'Installing counterpartyd.logrotate...'
  install -Dm 644 "$srcdir/counterpartyd.logrotate" "$pkgdir/etc/logrotate.d/counterpartyd"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
