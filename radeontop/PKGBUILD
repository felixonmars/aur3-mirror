# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=radeontop
pkgver=0.5.2
pkgrel=1
pkgdesc="Radeon cards monitoring utility"
arch=('i686' 'x86_64')
url="https://github.com/clbr/radeontop"
license=('GPL3')
depends=('ncurses' 'xf86-video-ati')
source=("https://github.com/clbr/radeontop/tarball/v$pkgver"
        'Makefile.patch')
sha256sums=('9a5091f65adb6231d528e141c8e5f94d43c9555048ce0541900578f4f3795677'
            '8ec2c50e5b3951a8b753597b43a205dad1cf826d89e757c9d343f8233b1063d6')

build() {
  cd "$srcdir/clbr-$pkgname-"*

  patch Makefile < "$srcdir/Makefile.patch"
  cat > version.h << EOF
#ifndef VER_H
#define VER_H

#define VERSION "$pkgver"

#endif
EOF

  LDFLAGS='' # radeontop doesn’t seem to support Arch’s standard LDFLAGS
  make
}

package() {
  cd "$srcdir/clbr-$pkgname-"*
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
