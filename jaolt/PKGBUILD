# Maintainer: Max Roder <maxroder@web.de>

pkgname=jaolt
pkgver=0.6.9
pkgrel=1
pkgdesc="Java auction management tool for ebay, auvito and hood"
arch=('any')
url="http://code.google.com/p/jaolt/"
license=('LGPL' 'CDDL'  'APACHE' 'BSD' 'CCPL:by' 'custom:H2' 'custom:jdom' 'custom:jgoodies')
depends=('java-environment' 'bash')
conflicts=('jaolt-svn')
source=("http://jaolt.googlecode.com/files/${pkgname}_${pkgver}-noarch-1154.tar.gz")
sha256sums=('2bf68f823c607a4b46fd2c3af8dfcb7ba1aa2ec38610cb4ae474bec00197ac04')

package() {
	cd "${srcdir}/${pkgname}"

	# Patch loader
	sed -i 's/jaolt\/launcher.jar/java\/jaolt\/launcher.jar/g' jaolt
	install -Dm755 jaolt ${pkgdir}/usr/bin/jaolt

	# Copy license files
	install -d ${pkgdir}/usr/share/licenses/jaolt
	mv license-instructions.txt ${pkgdir}/usr/share/licenses/jaolt/readme
	mv jgoodies-uiflite.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jgoodies-uiflite
	mv h2.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.h2
	mv jdom.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jdom
	mv jgoodies-forms.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jgoodies-forms
	mv jgoodies-looks.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jgoodies-looks

	install -d ${pkgdir}/usr/share/java/jaolt/ ${pkgdir}/usr/share/applications/ ${pkgdir}/usr/share/pixmaps/
	mv jaolt.desktop ${pkgdir}/usr/share/applications/
	mv jaolt.xpm ${pkgdir}/usr/share/pixmaps/
	
	# Remove unnecessary files, copy anything else
	rm -rf build.xml lgpl.txt INSTALLING jaolt
	cp -r * ${pkgdir}/usr/share/java/jaolt/
}

# vim:set ts=2 sw=2 et:
