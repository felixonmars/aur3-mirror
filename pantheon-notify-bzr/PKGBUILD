# Maintainer: Ner0

pkgname=pantheon-notify-bzr
pkgver=22
pkgrel=1
pkgdesc="A stylish notification daemon from the elementary project"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-notify"
license=('GPL3')
depends=('clutter-gtk' 'granite-bzr' 'glib2' 'gtk3')
makedepends=('bzr' 'cmake' 'vala' 'pkg-config')

_bzrtrunk=lp:pantheon-notify
_bzrmod=pantheon-notify

build() {
  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  [ -d "$srcdir/$_bzrmod-build" ] && rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"
  [ -d build ] && rm -rf build
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
