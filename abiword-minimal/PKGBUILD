# Maintainer: megadriver <megadriver at gmx dot com>

pkgname=abiword-minimal
pkgver=2.8.6
pkgrel=4
pkgdesc="A fully-featured word processor (GNOME free!)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.abisource.com"
makedepends=('boost')
depends=('fribidi' 'wv' 'librsvg' 'enchant' 'desktop-file-utils' 'goffice-light')
conflicts=('abiword' 'abiword-plugins')
provides=('abiword')
install=abiword-minimal.install
options=('!makeflags' '!libtool')
source=("http://www.abisource.com/downloads/abiword/${pkgver}/source/abiword-${pkgver}.tar.gz"
	     abiword-2.8.6-no-undefined.patch)
md5sums=('f883b0a7f26229a9c66fd6a1a94381aa'
         '42e2614b6e0aad457d91f6ce20b6f225')

build() {
  cd "${srcdir}/abiword-${pkgver}"
  # fix a gcc 4.6.x build issue, patch from Fedora
  patch -Np1 -i "${srcdir}/abiword-2.8.6-no-undefined.patch"
  libtoolize --force
  autoreconf
  ./configure --prefix=/usr --enable-clipart --enable-templates --without-gnomevfs --with-gio \
              --disable-default-plugins --disable-builtin-plugins --disable-static --with-psiconv-config=/nothere \
              --with-goffice --enable-plugins="opendocument openwriter openxml pdf goffice"
  make
}

package() {
  cd "${srcdir}/abiword-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Change the default font from "Times New Roman" to "Sans" (i.e. the default Sans font on our system)
  cd ${pkgdir}/usr/share/abiword-2.8/templates
  sed -i 's/Times New Roman/Sans/g' normal.awt*
}
