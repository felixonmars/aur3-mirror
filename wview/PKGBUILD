# Maintainer: Thomas Techinus ttech@mostlynothing.info
# Contributor: Romain Schmitz <slopjong .at. slopjong .dot. de>

pkgname=wview
pkgver=5.20.2
pkgrel=3
pkgdesc=" colletion of daemons to provide support to retrieve weather station information."
arch=('i686' 'x86_64')
url="http://www.wviewweather.com/"
license=('GPL')
depends=('zlib' 'libzip' 'libpng' 'gd' 'libusb' 'radlib') # radlib is needed but doesn't work here?
makedepends=('curl' 'gawk' 'wget' 'file' 'sqlite') # we need sqlite to update a column during the packaging
optdepends=('apache2: To view weather station site'
	   'ntp: time sync'
	   'php: web admin interface'
	   'php-gd: For generating web interface images'
	   'php-sqlite: to access weather datababases')
backup=(etc/wview/{graphics,images,images-user,arcrec-header}.conf
	etc/wview/wview-conf.sdb 
	etc/wview/wview-binary
	etc/wview/html/{html-templates,graphics}.conf 
	var/wview/archive/wview-{archive,hilo,history,noaa}.sdb)
options=(!libtool)
source=(http://downloads.sourceforge.net/project/wview/wview/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz wview wview.service)
#install=('wview.install')
sha512sums=('165f81a21ceae0b971a82a370439035bf2ccdb9863c1508aebe537ab38cb3ce309b5bb9cca04eb0dd6f2837fb4dbedb716aa6ef30ed21674356a9982a90ab846'
	    'd1d115cc82269dac1f05f859a2f288da64636beb2bd546c91f436073df1c309dc68d83fae7169e122f27f065ecdf6a446ad02f6d6017fdb85f079532d05443a6'
	    'ac3b0602b83fadb351496a68ff75afd3ce60352faceee0635e2a5a92cb0999eadddeb2c46b0be320ade87e13865c6d468954d401986e3274bdde2c7011e4a085')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install || return 1
  echo "update config set value='/var/wview/img' where name='HTMLGEN_IMAGE_PATH';" | sqlite3 "$pkgdir"/etc/wview/wview-conf.sdb
  install -D -m755 "${srcdir}/wview" "${pkgdir}/bin/wviewctl" || return 1
  install -D -m644 "${srcdir}/wview.service" "${pkgdir}/usr/lib/systemd/system/wview.service"
  echo "Make sure to run wviewconfig and wviewhtmlconfig if this is your first time installing!"
}
