# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=gocode
pkgver=r60
pkgrel=3
pkgdesc="An autocompletion/refactoring daemon for the Go programming language"
arch=(i686 x86_64)
url="https://github.com/nsf/gocode"
license=('GPL')
depends=('go')
makedepends=('git')
optdepends=('auto-complete: emacs autocompletion')
options=(!strip)

# Extra underscore to stop the automatic pkgver update
__gitroot="git://github.com/nsf/gocode.git"
__gitname="gocode"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $__gitname ]] ; then
    ( cd "$__gitname" && git pull origin; )
    msg "The local files are updated."
  else
    git clone "$__gitroot" "$__gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$__gitname-build"
  cd "$__gitname"
  git clone "$srcdir/$__gitname" "$srcdir/$__gitname-build"
  cd "$srcdir/$__gitname-build"
  git checkout compatible-with-go-release.r60

  make
}

package() {
  cd "$srcdir/$__gitname-build"

  install -Dm755 gocode "$pkgdir/usr/bin/gocode"
  install -Dm644 vim/autoload/gocomplete.vim "$pkgdir/usr/share/vim/vimfiles/autoload/gocomplete.vim"
  install -Dm644 vim/ftplugin/go.vim "$pkgdir/usr/share/vim/vimfiles/ftplugin/go.vim"
  install -Dm644 emacs/go-autocomplete.el "$pkgdir/usr/share/emacs/site-lisp/go-autocomplete.el"
}
