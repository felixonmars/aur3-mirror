# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=grive-git
pkgver=20120429
pkgrel=1
pkgdesc='Open source Linux client for Google Drive'
arch=('i686' 'x86_64')
url='http://match065.github.com/grive/'
license=('GPL')
depends=('gcc-libs' 'json-c')
makedepends=('cmake' 'cppunit' 'git')
provides=('grive')
conflicts=('grive')

_gitroot="https://github.com/match065/grive.git"
_gitname="grive"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}"/build
  mkdir "${srcdir}"/build
  cd "${srcdir}"/build
  cmake ../${_gitname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}
 
package() {
  cd "${srcdir}"/build
  install -Dm755 grive "${pkgdir}"/usr/bin/grive
}
