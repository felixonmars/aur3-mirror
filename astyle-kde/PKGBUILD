# $Id: PKGBUILD 103213 2013-12-30 12:53:05Z lfleischer $
# Maintainer: Teo Mrnjavac <teo@kde.org>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Thomas Mader <thezema@gmail.com>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=astyle-kde
_pkgname=astyle
provides=astyle
conflicts=astyle
pkgver=2.04
pkgrel=1
pkgdesc='A free, fast and small automatic formatter for C, C++, C sharp and Java source code, with KDE patches.'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/astyle/'
license=('LGPL')
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/sourceforge/astyle/${_pkgname}_${pkgver}_linux.tar.gz"
        "astyle_qt.diff"
        "astyle_comma.diff")

build() {
  cd "$srcdir/$_pkgname"
  patch -p1 < ../astyle_qt.diff
  patch -p1 < ../astyle_comma.diff
  cd "$srcdir/$_pkgname/build/gcc"
  make
}

package() {
  cd "$srcdir/$_pkgname/build/gcc"

  install -Dm0755 bin/astyle "$pkgdir/usr/bin/astyle"

  # install documentation (implements FS#23290)
  install -d "$pkgdir/usr/share/doc/$_pkgname/"
  for f in ../../doc/*.html; do
    install -m0644 "$f" "$pkgdir/usr/share/doc/$_pkgname/"
  done
}
md5sums=('30b1193a758b0909d06e7ee8dd9627f6'
         '3f5005275a1b083d9ff07e64fa7fa9cf'
         'a8702d1f4b5770c7d34cfc414ab3202c')
