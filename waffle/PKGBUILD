# Maintainer: Chad Versace <chad.versace@linux.intel.com>

pkgname='waffle'
pkgver='1.4.1'
pkgrel=1
pkgdesc='a library for choosing window system and OpenGL API at runtime'
arch=('i686' 'x86_64')
url='http://www.waffle-gl.org'
license=('BSD')

depends=(
  'libgl' # for GLX
  'libegl'
  'libgbm'
  'libx11'
  'libxcb'
  'wayland'
  )
makedepends=(
  'cmake'
  'xcb-proto'

  # For building the docs.
  'libxslt'
  'docbook-xsl'
  )
source=("http://www.waffle-gl.org/files/release/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('66b3240b94abc30cd18c644ffc145024ad60e5b8a8a7d1e718c265b4c24351c9')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -Dwaffle_has_gbm=1 \
    -Dwaffle_has_glx=1 \
    -Dwaffle_has_x11_egl=1 \
    -Dwaffle_has_wayland=1 \
    -Dwaffle_build_manpages=1 \
    -Dwaffle_build_htmldocs=1 \
    -Dwaffle_build_examples=0
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$pkgdir/usr/share/doc/waffle1/LICENSE.txt" \
                "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
