# Get help on the forums
# http://forum.diyefi.org/viewtopic.php?f=41&t=2124

# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>
pkgname=emstudio-git
_gitname=emstudio
pkgver=475.6f23c52
pkgrel=1
pkgdesc="Cross Platform FreeEMS tuning application, written in c++ with Qt"
arch=("i686" "x86_64")
url="https://github.com/malcom2073/emstudio"
license=('GPL')
groups=()
depends=("qt4" "qconf" "qwt" "qjson" "icu")
makedepends=('git')
source=("$_gitname::git+git://github.com/malcom2073/emstudio.git"
        "0001-Do-not-treat-warnings-as-errors.patch")
md5sums=('SKIP'
         'cf0f3b1437a3ad29dcca6709cc66b866')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$_gitname"

  git apply "$srcdir/0001-Do-not-treat-warnings-as-errors.patch"

  qmake-qt4
  make
}

package() {
  cd "$_gitname"

  make INSTALL_ROOT="$pkgdir/" install
  mkdir -p "$pkgdir/etc/emstudio"
  install -Dm644 "core/freeems.config.json" "$pkgdir/etc/emstudio"
}

# vim:set ts=2 sw=2 et:
