# Maintainer: VÃ­tor Ferreira <vitor.dominor at gmail dot com>
# Contributor: Andrei Gutu
# text : The new mediaplayer from ROSA(based on SMPlayer)

pkgname=rosa-media-player
pkgver=1.6
pkgrel=1
pkgdesc="The new multimedia player(based on SMPlayer) with clean and elegant UI."
arch=(i686 x86_64)
url=http://www.koryavov.net/2012/09/rosa-media-player-15.html
# Alternative url is that of official git repo for the rosa media player project:
# https://abf.rosalinux.ru/uxteam/ROSA_Media_Player
# or rosalinux mirror
# http://mirror.yandex.ru/rosa/rosa2012.1/repository/SRPMS/main/release/

_upstream_rel=1

source=(https://github.com/Dominor/aur/raw/master/$pkgname/$pkgname-$pkgver.tar.gz)
# end source URL with sha1sum taken from https://abf.rosalinux.ru/import/rosa-media-player/raw/rosa2012.1/.abf.yml
#source=(http://file-store.rosalinux.ru/download/22d80592f0e8b6813add28306caa2d8573d06664)
license=(GPL)
depends=('qt4' 'mplayer' 'wildmidi' 'timidity-freepats' 'wget' 'qjson')
optdepends=('mencoder: video splitting support'
            'ffmpeg: audio cutting and video recording support'
            'rosa-media-player-plugin: mozilla plugin')
makedepends=('qt4' 'wildmidi' 'qjson')
install="$pkgname".install
sha1sums=('9f3097de076547287e501ad56a81f67e909e15b7')

build()
{
  cd "$srcdir/$pkgname"
  ./get_romp_version.sh $pkgver $_upstream_rel
  make PREFIX=/usr QMAKE=/usr/bin/qmake-qt4 LRELEASE=/usr/lib/qt4/bin/lrelease
}

package()
{
  cd "$srcdir/$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
