# Maintainer: TDY <tdy@archlinux.info>

pkgname=foxitreader
pkgver=1.1
pkgrel=5
pkgdesc="A small, fast PDF viewer"
arch=('i686' 'x86_64')
url="http://www.foxitsoftware.com/pdf/desklinux/"
license=('custom')
depends=('desktop-file-utils' 'gtk2' 'sqlite3')
[[ $CARCH = x86_64 ]] && depends=('desktop-file-utils' 'lib32-gtk2' 'lib32-sqlite3')
install=fr.install
source=(http://cdn04.foxitsoftware.com/pub/foxit/reader/desktop/linux/1.x/$pkgver/enu/FoxitReader-$pkgver.0.tar.bz2
        http://www.foxitsoftware.com/pdf/rd_eula.htm
        fr.desktop
        fr-i686.sh
        fr-x86_64.sh
        fr.png)
sha256sums=('02f1ad5ad2fd516754254f349c94b8e81b57b9eb2a0315f30debdc5d7fb8dbf4'
            '37a774e150fb7996ff2ec1c4d2f788b392052b1376a2773229b3d7ba80837117'
            '3f1c233ed359153c093eab8194981646b473763042ad672d1dde94b756173cdc'
            'cd70582074710360cb667d91dd677b4f074f308bc13517a879d2735a08266d96'
            '13856414068d9e374eed86a5a99c665b8450f7d07dd27fd9106b73d20fdd5e87'
            '01d9197a2387bf007a8ebc6159dad1238a66aeb65cf0851b484ed0dcbf9779af')

package() {
  cd "$srcdir/$pkgver-release"
  install -Dm755 FoxitReader "$pkgdir/usr/lib/$pkgname/FoxitReader"
  install -Dm644 fum.fhd "$pkgdir/usr/lib/$pkgname/fum.fhd"
  install -Dm644 fpdfcjk.bin "$pkgdir/usr/lib/$pkgname/fpdfcjk.bin"

  for _lng in po/*; do
    install -Dm644 $_lng/FoxitReader.mo \
      "$pkgdir/usr/share/locale/${_lng#*/}/LC_MESSAGES/FoxitReader.mo"
  done

  install -Dm644 ../fr.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 ../fr.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ../rd_eula.htm "$pkgdir/usr/share/licenses/$pkgname/EULA.htm"
  install -Dm755 ../fr-$CARCH.sh "$pkgdir/usr/bin/$pkgname"
  ln -sf $pkgname "$pkgdir/usr/bin/fr"
}

# vim:set ts=2 sw=2 et:
