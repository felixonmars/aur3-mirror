# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=gmp-ecm
pkgver=6.4.2
pkgrel=1
pkgdesc='Elliptic Curve Method for Integer Factorization'
url='http://ecm.gforge.inria.fr/'
license=('GPL' 'LGPL')
depends=('gmp')
options=('!libtool')
arch=('i686' 'x86_64' 'armv7h')
source=("http://gforge.inria.fr/frs/download.php/30448/ecm-${pkgver}.tar.gz")
sha1sums=('f0c556d83a86b54ca0ff27e49f77252a796c4ab8')

build() {
	cd "${srcdir}/ecm-${pkgver}"
	[[ $CARCH = *86* ]] && export CFLAGS+=' -msse'
	./configure --prefix=/usr --enable-shared --enable-openmp
	# make ecm-params
	make
}

check() {
	cd "${srcdir}/ecm-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/ecm-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
