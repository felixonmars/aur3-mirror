# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=boxer
pkgver=0.3.1
pkgrel=1
pkgdesc="A graphical user interface for the Box vector graphics language"
arch=('any')
url="http://boxc.sourceforge.net"
license=('GPL')
depends=('box' 'pygtk')
provides=('box-gui')
conflicts=('box-gui')
replaces=('box-gui')
install="$pkgname.install"
source=("http://switch.dl.sourceforge.net/project/boxc/Boxer%20-%20the%20Box%20GUI/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz"
        "http://boxc.sourceforge.net/_images/box-logo.png"
        "$pkgname.desktop")
md5sums=('b02eb6c163c737cc5d972d3c63b6ffcc'
         '2ad95dafb22037b74cac8c4736c73674'
         '350cf47d8ed7851efa2001fea086ca89')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i "s|\(env python\).*|\12|" `grep -rl "env python" .`

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir/"

  # desktop file and pixmaps
  install -Dm644 ../$pkgname.desktop \
                 "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ../box-logo.png \
                 "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
