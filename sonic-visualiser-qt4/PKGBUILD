# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

_name=sonic-visualiser
pkgname=$_name-qt4
pkgver=2.1
pkgrel=1
pkgdesc="A viewer and analyser of music audio files."
arch=('i686' 'x86_64')
url="http://www.sonicvisualiser.org/"
license=('GPL')
depends=('libfishsound' 'libid3tag' 'liblo' 'liblrdf' 'libmad'
         'liboggz' 'libpulse' 'portaudio' 'qt4' 'rubberband' 'sord')
provides=("$_name=$pkgver")
conflicts=("$_name")
install="$pkgname.install"
#source=("http://downloads.sourceforge.net/sv1/$_name-$pkgver.tar.gz")
source=("http://code.soundsoftware.ac.uk/attachments/download/711/$_name-$pkgver.tar.gz")
md5sums=('d86cfc1c26385d8f7702f913ca8fa379')

build() {
  cd "$srcdir/$_name-$pkgver"

  # build against qt4
  export QMAKE=qmake-qt4

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"

  # bin
  install -Dm755 $_name "$pkgdir/usr/bin/$_name"

  # desktop file
  install -d "$pkgdir/usr/share/applications"
  install -Dm644 *.desktop "$pkgdir/usr/share/applications"

  # icons
  for _s in 16 22 24 32 48 64 128; do
    install -Dm644 icons/sv-${_s}x${_s}.png \
            "$pkgdir/usr/share/icons/hicolor/${_s}x${_s}/apps/sv-icon.png"
  done
  install -Dm644 icons/sv-icon.svg \
          "$pkgdir/usr/share/icons/hicolor/scalable/apps/sv-icon.svg"

  # samples
  install -d "$pkgdir/usr/share/$_name/samples"
  install -Dm644 samples/* "$pkgdir/usr/share/$_name/samples"

  # templates
  install -d "$pkgdir/usr/share/$_name/templates"
  install -Dm644 templates/* "$pkgdir/usr/share/$_name/templates"
}
