#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pm2ml
pkgver=2012.8
pkgrel=1
pkgdesc='Generate metalinks for downloading Pacman packages and databases.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pm2ml"
depends=(python3 pyalpm)
optdepends=('python3-aur: AUR support' 'reflector: Reflector support')
backup=(etc/ppl.conf)
source=(
  http://xyne.archlinux.ca/projects/pm2ml/archives/pm2ml-2012.8.tar.xz
  http://xyne.archlinux.ca/projects/pm2ml/archives/pm2ml-2012.8.tar.xz.sig
)
sha512sums=(
  d8274c83a8f70e1e5202d67c1c3c95a618688a55009a3a7e73f2a5c0b6f95f1fe0519c202115bd04c1a9c43782194af91e1ab06a40ed7106d4192346a530ef16
  d8ee4f3b2c9867fca8805e18d8bb95ba8149c0f181bb67bbdba91cb39a705d1adc28d281aef6f3a190e6c89bdafa4f784e493f349325f0c396d17a9752c91536
)
md5sums=(
  47e84b90119c2bd8b65cbe8267a9dfc2
  533d02c851876922ea555216c99bb99d
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m755 "$srcdir/$pkgname-$pkgver/$pkgname" "$pkgdir/usr/bin/$pkgname"
  for foo_ in ppl pplsyu ppls; do
    install -D -m755 "$srcdir/$pkgname-$pkgver/$foo_" "$pkgdir/usr/bin/$foo_"
  done
  install -D -m755 "$srcdir/$pkgname-$pkgver/ppl.conf" "$pkgdir/etc/ppl.conf"
}

# vim: set ts=2 sw=2 et:
