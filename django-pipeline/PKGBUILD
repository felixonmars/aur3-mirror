# Maintainer: Steven Allen <steven@stebalien.com>

pkgname='django-pipeline'
pkgver=1.2.14
pkgrel=1
pkgdesc="An asset packaging library for Django, providing both CSS and JavaScript concatenation and compression, built-in JavaScript template support, and optional data-URI image embedding."
arch=('any')
url="https://github.com/cyberdelia/django-pipeline"
license=('custom')
depends=('python2' 'django')
makedepends=('python2-sphinx')
optdepends=('yuicompressor: for css and js compression')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cyberdelia/${pkgname}/tarball/${pkgver}")
md5sums=('3214f69436a128626985bc6491558682')

build() {
  cd "$srcdir/cyberdelia-$pkgname-"*
  python2 setup.py build_sphinx -b man

  # Compress the man pages without generating .gz.gz... on rebuild
  find build/sphinx/man/ -name '*.[0-8n]' -exec gzip -f '{}' \;
}

package() {
  cd "$srcdir/cyberdelia-$pkgname-"*
  python2 setup.py install --root="$pkgdir/" --optimize=1
  # Remove test cases
  rm -rf "$pkgdir"/usr/lib/python*/site-packages/tests/

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Slightly more complicated but flexable.
  find build/sphinx/man/ -name '*.[0-8n].gz' | while read doc; do
      install -D -m 644 "$doc" $pkgdir/usr/share/man/man"$(echo $doc|sed 's/.*\.\([0-8n]\)\.gz/\1/')"/"$(basename $doc)"
  done
}
