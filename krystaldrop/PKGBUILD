# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Piotr Beling <qwak@stud.ics.p.lodz.pl>

pkgname=krystaldrop
pkgver=0.7.2
pkgrel=2
pkgdesc="A free clone of the excellent NeoGeo's Magical Drop puzzle game"
arch=('i686' 'x86_64')
url="http://krystaldrop.sourceforge.net/"
license=('GPL2')
depends=('libgl' 'libxml2' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
source=("http://downloads.sourceforge.net/$pkgname/src_$pkgver.tgz"
        "http://downloads.sourceforge.net/$pkgname/art_$pkgver.tgz"
        "00-assert.patch" "01-deps.patch" "02-gcc41.patch"
        "03-gcc43.patch" "04-glibc2.10.patch" "05-icu.patch"
        "$pkgname.desktop")
sha256sums=('d0e42cf05f241e409649de6a48045c026c42417cdabd233c8fef8783096946e5'
            '7cd9dbd1ad12eb7e26e57e2a52b59e7137e20979d4fc8cb21a48faff983f6e27'
            'ac4a526be3335f7aab6fd00f0af5a79556aedc66704277704a9bb41030983df2'
            'bc8121c186af89aa1116d0eed5b5743c91a691434965f4128b2e9ac4ade2ea84'
            '413d56f90d196ecfdc7ec3210b78ee54cd7348cf451e23c519ffcd2e3913a70a'
            '37e14f887e34628d6675f060423c46cd54855f5623a4de0c8c9458fc841104cf'
            '3bfa2d948193f7807eeb404994a924bb7c5b29a12d518744b8de537e014386ea'
            '2a4ea65242e1b07a1ace9f0a0fba9a43e51e3ccce34b2c4c637faf69de41ab9a'
            'bb84fcc97daac6accc93f89e7d60f8046bd54e3f4749904577130ee354a9c45c')

prepare() {
  cd $pkgname

  # copy data files
  cp -r ../art .

  # apply gentoo patches
  patch -Np1 < ../00-assert.patch
  patch -Np1 < ../01-deps.patch
  patch -Np1 < ../02-gcc41.patch
  patch -Np1 < ../03-gcc43.patch
  patch -Np0 < ../04-glibc2.10.patch
  patch -Np0 < ../05-icu.patch

  # fix installation paths
  sed -e "s|/etc|/etc/$pkgname|" \
    -e "s|/usr/games|/usr/bin|" \
    -e "s|/usr/share/games|/usr/share|" \
    -i Makefile
}

build() {
  make -C $pkgname
}

package() {
  cd $pkgname

  make INSTALL_PREFIX="$pkgdir" install
  # install desktop entry
  install -Dm644 art/drop.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  # install man page and readme
  install -Dm644 doc/kdrop.6 "$pkgdir"/usr/share/man/man6/$pkgname.6
  install -Dm644 README "$pkgdir"/usr/share/doc/$pkgname/README
}
