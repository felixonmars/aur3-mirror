# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libgphoto2
pkgname=lib32-${_pkgbase}
pkgver=2.4.14
pkgrel=1
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs. (32 bit)"
arch=('x86_64')
url="http://www.gphoto.org"
license=(LGPL)
depends=('lib32-libexif' 'lib32-libjpeg-turbo' 'lib32-gd' 'lib32-libltdl' 'lib32-libusb-compat' 'lib32-libusb')
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.sourceforge.net/gphoto/${_pkgbase}-${pkgver}.tar.gz)
md5sums=('492bec63dd610906c3a28030be77e650')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --disable-rpath --libdir=/usr/lib32 --without-hal
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share,lib32/udev}
}
