# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Here you choose whether you want a PGO (Profile-Guided Optimization) build. See: http://en.wikipedia.org/wiki/Profile-guided_optimization
_pgo="0"  # "1" to enable

pkgname=thunderbird-hg
pkgver=8323
pkgrel=1
pkgdesc="Standalone Mail/News reader - Mercurial version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/thunderbird"
depends=('gtk2' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell>=1.3' 'fontconfig' 'freetype2' 'cairo' 'desktop-file-utils' 'libjpeg' 'libpng' 'nspr' 'libmng' 'libevent')
makedepends=('unzip' 'zip' 'libgnomeui' 'python2' 'libidl2' 'yasm' 'libnotify' 'gconf' 'yasm' 'mesa' 'mercurial' 'autoconf2.13')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$pkgname.install
source=(mozconfig
        $pkgname.desktop)
md5sums=('b8fd97ab4bfb2e4538b1e1340d70f915'
         '19ea9f30321b65fa8de67f0cf7456be3')

_hgroot="http://hg.mozilla.org/"
_hgrepo="comm-central"

build() {
  cd comm-central
  python2 client.py checkout

  export LDFLAGS="-Wl,-rpath,/opt/$pkgname -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
  export PYTHON="/usr/bin/python2"

  if [[ "$_pgo" = "1" ]]; then
     sed -i '/^NO_PROFILE_GUIDED_OPTIMIZE = 1$/d' mozilla/memory/jemalloc/Makefile.in
     echo 'LDFLAGS += -lX11 -lXrender' >> mozilla/layout/build/Makefile.in
     LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
     LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
  else
     make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  fi
}

package() {
  cd comm-miramar/obj-*
  make -j1 package MOZ_MAKE_FLAGS="$MAKEFLAGS"

  cd mozilla/dist/
  bsdtar -x -f thunderbird-*.tar.bz2
  install -m755 -d "$pkgdir"/{opt,usr/bin}
  cp -r thunderbird/ "$pkgdir/opt/$pkgname/"
  ln -sf /opt/$pkgname/thunderbird "$pkgdir/usr/bin/$pkgname"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -D "$srcdir/comm-miramar/other-licenses/branding/thunderbird/mailicon${i/x*/}.png" "$pkgdir/usr/share/icons/hicolor/$i/apps/$pkgname.png"
  done

  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  ln -sf /usr/share/myspell/dicts "$pkgdir/opt/$pkgname/dictionaries"

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "$pkgdir/opt/$pkgname/defaults/pref/channel-prefs.js"
}