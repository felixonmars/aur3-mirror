# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Here you choose whether you want a PGO (Profile-Guided Optimization) build
_pgo="0"  # Change to "1" to do so - every other number/mark disables PGO

pkgname=thunderbird-hg
pkgver=7888
pkgrel=1
pkgdesc="Standalone Mail/News reader - Mercurial version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell>=1.3' 'fontconfig' 'freetype2' 'cairo' 'desktop-file-utils' 'libjpeg' 'libpng' 'nspr' 'libmng')
makedepends=('zip' 'libgnomeui' 'python2' 'libidl2' 'yasm' 'autoconf2.13' 'libnotify' 'mercurial')
optdepends=('libcanberra: for sound support'
            'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
install=${pkgname}.install
source=(mozconfig
        ${pkgname}.desktop)
md5sums=('ac1edd41e214742421c7f917948a9ad6'
         'e74678baaeaebde3ce0d9a33b6c53518')

_hgroot="http://hg.mozilla.org/"
_hgrepo="comm-central"

build() {
  cd comm-central
  python2 client.py checkout

  cp ../mozconfig .mozconfig
  export LDFLAGS="-Wl,-rpath,/opt/${pkgname}"
  export CXXFLAGS="-fpermissive"

  if [[ "${_pgo}" = "1" ]]; then
     [[ "$CARCH" = "i686" ]] && export CFLAGS="-march=i686 -mtune=generic"
     [[ "$CARCH" = "x86_64" ]] && export CFLAGS="-march=x86-64 -mtune=generic"
     export CXXFLAGS="${CXXFLAGS} ${CFLAGS}"
     make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  else
     unset CFLAGS
     make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  fi
}

package() {
  cd comm-central/obj-*
  make package

  cd mozilla/dist/
  bsdtar -x -f thunderbird-*.tar.bz2
  install -m755 -d "${pkgdir}"/{/opt,/usr/{share/applications,bin}}
  cp -r thunderbird/ "${pkgdir}/opt/${pkgname}/"
  ln -sf /opt/${pkgname}/thunderbird "${pkgdir}/usr/bin/${pkgname}"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -D "${srcdir}/comm-miramar/other-licenses/branding/thunderbird/mailicon${i/x*/}.png" "${pkgdir}/usr/share/icons/hicolor/${i}/apps/${pkgname}.png"
  done

  install -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "${pkgdir}/opt/${pkgname}/defaults/pref/channel-prefs.js"
}