# $Id: PKGBUILD 102090 2010-12-05 20:57:04Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=claws-mail-cvs
pkgver=3.7.9cvs35
pkgrel=1
pkgdesc="A GTK+ based e-mail client."
arch=(i686 x86_64)
license=('GPL3')
url="http://www.claws-mail.org"
depends=('gtk2>=2.18.5' 'gnutls>=2.8.5' 'startup-notification>=0.10' 'pilot-link>=0.12.4' 'enchant>=1.5.0-2'
         'gpgme>=1.2.0' 'libetpan>=1.0' 'libsm>=1.1.1' 'db' 'dbus-glib>=0.82-2' 'hicolor-icon-theme')

optdepends=('python2:           needed for some tools'
            'perl:              needed for some tools'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering'
            'html2ps:		AUR pkg - adds support for printing html mails together with html plugins')
replaces=('sylpheed-claws')
provides=('claws')
options=(!libtool)
conflicts=('claws-mail')
install=claws-mail.install
source=(http://www.claws-mail.org/snapshots/claws-mail-${pkgver}.tar.bz2)
md5sums=('bd6f8c70d24ab983783856814a4f81ec')

build() {
  cd ${srcdir}/claws-mail-${pkgver}
  sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
  ./configure --prefix=/usr --disable-static \
    --enable-enchant \
    --enable-gnutls \
    --enable-ldap \
    --disable-dillo-viewer-plugin \
    --enable-crash-dialog \
    --enable-pgpmime-plugin \
    --enable-spamassassin-plugin \
    --enable-bogofilter-plugin \
    --enable-jpilot

  make
}

package() {
  cd ${srcdir}/claws-mail-${pkgver}
  make DESTDIR=${pkgdir} install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 ${i} \
        ${pkgdir}/usr/lib/claws-mail/tools/${i}
  done
}
