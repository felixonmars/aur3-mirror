# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Based on r8168 [community] & r8168-ck (https://aur.archlinux.org/packages.php?ID=56657)

pkgname=r8168-openvz-testing
_realname=r8168
pkgver=8.029.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards (for linux-openvz-testing kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=("i686" "x86_64")
depends=("glibc" "linux-openvz-testing>=2.6.32" "linux-openvz-testing<2.6.33")
makedepends=("linux-openvz-testing-headers")
source=("http://r8168.googlecode.com/files/${_realname}-${pkgver}.tar.bz2")
install="${_realname}.install"
md5sums=("5dc15a976950250b7b543876cc3350a8")
sha256sums=("211a2994c9caf57321dafdb3acdfaa877f0b3f447798dc2abd2e417db3b0e515")

_extramodules="extramodules-2.6.32-openvz-testing"
_kernver="$(cat "/lib/modules/${_extramodules}/version" || true)"

build() {
	cd "${srcdir}/${_realname}-${pkgver}"
	make clean BASEDIR="/lib/modules/${_kernver}" modules
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}/src"
	install -Dm644 "${_realname}.ko" "${pkgdir}/lib/modules/${_extramodules}/${_realname}.ko"
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}
