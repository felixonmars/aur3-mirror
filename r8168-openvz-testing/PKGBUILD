# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Based on r8168 [community]

pkgname=r8168-openvz-testing
_realname=r8168
pkgver=8.032.00
pkgrel=2
pkgdesc="A kernel module for Realtek 8168 network cards for linux-openvz-testing"
url="http://www.realtek.com.tw"
license=("GPL")
arch=("i686" "x86_64")
depends=("glibc" "linux-openvz-testing>=2.6.32" "linux-openvz-testing<2.6.33")
makedepends=("linux-openvz-testing-headers")
source=("http://r8168.googlecode.com/files/${_realname}-${pkgver}.tar.bz2")
install="${_realname}.install"
md5sums=("942ce02459a181093f77bb4d5cc75d45")
sha256sums=("f24ed6ba1a0a6e8cae27c49b0bddbecbd430ad1d7ddd1c5d28f2091202aee9c3")

_extramodules="extramodules-2.6.32-openvz-testing"

build() {
	cd "${_realname}-${pkgver}"
  local _kernver="$(cat /usr/lib/modules/${_extramodules}/version || true)"

  # avoid using the Makefile directly -- it doesn't understand
  # any kernel but the current.
  make -C "/usr/lib/modules/${_kernver}/build" \
      SUBDIRS="${srcdir}/${_realname}-${pkgver}/src" \
      EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
	cd "${_realname}-${pkgver}"
	install -Dm644 "src/${_realname}.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/${_realname}.ko"
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} +
}
