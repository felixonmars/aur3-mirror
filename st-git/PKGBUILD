# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
epoch=1
pkgver=20130407
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
license=('MIT')
depends=('libxft')
makedepends=('git' 'ncurses')
arch=('i686' 'x86_64' 'armv7h')
source=('git://git.suckless.org/st')
sha1sums=('SKIP')

provides=('st')
conflicts=('st')
replaces=('st-hg')

prepare() {
	cd "${srcdir}/st"
	sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
	sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
	sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk
}

build() {
	cd "${srcdir}/st"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/st"
	make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
