# $Id: PKGBUILD 73307 2012-07-06 03:48:24Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

# -----------  NOTE TO ALL USERS ------------
# Go read http://www.courier-mta.org/install.html b4 running or building courier

pkgname=courier-mta
pkgver=0.71
pkgrel=4
pkgdesc="IMAP(s)/POP3(s) and SMTP Server with ML-manager, webmail and webconfig"
arch=(i686 x86_64)
license=('GPL2')
backup=('etc/courier/imapd.cnf' 'etc/courier/pop3d.cnf' \
        'etc/courier/imapd' 'etc/courier/imapd-ssl' \
        'etc/courier/pop3d' 'etc/courier/pop3d-ssl' \
        'etc/courier/courierd' 'etc/courier/sqwebmaild' \
        'etc/courier/esmtpd' 'etc/courier/esmtpd-ssl' \
        'etc/courier/esmtpd.cnf' 'etc/courier/esmtpd-msa' \
        'etc/courier/webadmin/password' 'etc/courier/esmtpauthclient' \
        'etc/conf.d/courier-mta')
url="http://courier-mta.org"
depends=('courier-authlib>=0.63.0' 'gamin' 'gcc-libs' 'gdbm' 'pcre' 'mime-types' 'ca-certificates')
optdepends=('libldap')
makedepends=('apache' 'pam' 'expect' 'gnupg' 'libldap' 'gamin')
provides=('smtp-server' 'smtp-forwarder' 'imap-server' 'pop3-server' 'courier-imap' 'courier-maildrop')
conflicts=('courier-imap' 'smtp-forwarder' 'smtp-server' 'imap-server' 'courier-maildrop' 'ucspi-tcp')
options=('!libtool')
install=courier-mta.install
source=(http://downloads.sourceforge.net/project/courier/courier/${pkgver}/courier-${pkgver}.tar.bz2
	courier-imapd.service
	courier-imapd-ssl.service
	courier-pop3d.service
	courier-pop3d-ssl.service
	courier-esmtpd.service
	courier-esmtpd-ssl.service
	courier-esmtpd-msa.service
	courier.service
	webmaild.service
	courier-courierfilter.service
	courier-imapd.conf)
sha512sums=('08b02b006c978c4e1d12241d3f687a201e65afbd6a7ca49f10c77c4ddad0b1d89bb43169b1eb7cd90af30117ffa13911794da3f7dec3429c36b4bc3a9cf3cac8'
	    '424a46871cf85432427a5bc7714d338cb773d603e71a95910373b323b85c9ca75e6e57e4c4fa55adda43978c1092ac555672a91ba19815790d4b80e62502609e'
	    '23968b8f1ae71a2f3e69811f230513ff6ea704fe6de042ef6be3ffebc593ebe3526f3ac3ace567565b7d5df5dc12b7c532e8e623d2422af821ecd95ad0ad80c5'
	    '51b3855a036e66cceee4617a64e505b9bd3b33c4e623cc2aeb00e91f1c06fd98d577de24e055cb84843f3891da94de883142d1133b74d8481b9feacc81a28f86'
	    'c452e8bf89d73345537052b012157d32fdde58b9d50a731881c7726f3a3774d491e0d16921a17e881d522c67fae7997e429dc34f1104e6efe4a2e6d7ae615d58'
	    'e3a60c6fafebb608478cc4ca42e8f139f4171c634e1cfa869a9b831e1998437d98a78dbd3510e0c4365b11d153e53a47f6c0e1b6c0f5420d2abb591626457128'
	    'f0fcf8329a30b7e7f2602165c54d8d163b107424d1ce9a98e57dbe2b893720e9c21b6210d1a794267d781186660c0362f9739f91d3946174e71efd0d68c6223b'
	    'fc49c2d4136375ed8e0297fabe97a09d20018474b381a500405e0e91fd88066e9438f9dc1e6270ea866c1d411eeb38c906b8fbff8e577056a144a2e325bd59cb'
	    '0cb769e0799583fc0710642f79f29c2b964c614a96d8046d73a54be00d9d170d9a508227012b353e7206b8c7ea92fdc0f7c4e747d2b305b65767501ab90f1468'
	    'e219f07ab91d1a1cbdc59872042de07f4f1b4ca1d78f26dd0b315b1a57dd9ae8e9465b3fbb56791af39823dbc0747c17422782e1ed6522442d74d3bc4c32e74e'
	    '1bee292fd4012ec75f3d3b7f9da1078fb7cded341bcf6fc248da8399127bca8bcc6465b461b6812ff6bf40e77dcb0ed8f7c552509d2dc803b32077380095f601'
	    '01cdaa929bad1fe73a665f9b528dfe046920d4575539165a20201267b6950882738012cdaa6e1a24b25018bc1a6fa6ec23dd8ffd9e11e2e02f6423eb0932a99d')

build() {
  cd ${srcdir}/courier-${pkgver}

  LDFLAGS+=",-L /usr/lib/courier-authlib -lcourierauth"
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/courier \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --localstatedir=/var/spool/courier \
    --disable-root-check \
    --enable-unicode \
    --enable-workarounds-for-imap-client-bugs \
    --enable-mimetypes=/etc/mime.types \
    --with-piddir=/var/run/courier \
    --with-locking-method=lockf \
    --with-trashquota \
    --with-db=gdbm \
    --with-trashquota \
    --with-random=/dev/urandom --without-ispell \
    --with-mailuser=courier --with-mailgroup=courier \
    --with-certdb=/etc/ssl/certs/
  make
}

package() {
  cd ${srcdir}/courier-${pkgver}

  #chown mail.mail ${pkgdir}/var/spool/courier
  make DESTDIR=${pkgdir} install
  # docs say we can get rid of those after make
  find ${pkgdir} -name '*\.a' -exec -rm -f {} \;
  # install the perftest-script for testings
  install -Dm755 courier/perftest1 ${pkgdir}/usr/lib/courier/perftest1
  ###############################################################################
  # this is what usually "make install-configure" does
  # *.dist files get rid of "dist"
  for distfile in ${pkgdir}/etc/courier/*.dist; do
    mv ${distfile} ${pkgdir}/etc/courier/$(basename ${distfile} .dist)
  done
  # install pam files according to the layout used in Archlinux
  for pamfile in ${pkgdir}/etc/courier/*.authpam; do
    sed -i 's|/lib/security/pam_pwdb\.so|pam_unix.so|' ${pamfile}
    #echo "password  required  pam_unix.so" >> $pamfile
    install -Dm 644 ${pamfile} \
      ${pkgdir}/etc/pam.d/$(basename ${pamfile} .authpam | sed "s/d$//")
    rm -f ${pamfile}
  done

  ###############################################################################
  # Arch Linux specific tweaks to make things easier for the user
  # create passwordfile for webadmin -> standard archwebadmin
  sed -i 's|/etc/courier/webadmin/password|$(DESTDIR)/etc/courier/webadmin/password|g' Makefile
  yes "archwebadmin" | make DESTDIR=${pkgdir} install-webadmin-password

  # Install systemd service files
  install -Dm 644 "${srcdir}/courier-imapd.service" 	"${pkgdir}/usr/lib/systemd/system/courier-imapd.service"
  install -Dm 644 "${srcdir}/courier-imapd-ssl.service" "${pkgdir}/usr/lib/systemd/system/courier-imapd-ssl.service"
  install -Dm 644 "${srcdir}/courier-pop3d.service" 	"${pkgdir}/usr/lib/systemd/system/courier-pop3d.service"
  install -Dm 644 "${srcdir}/courier-pop3d-ssl.service" "${pkgdir}/usr/lib/systemd/system/courier-pop3d-ssl.service"
  install -Dm 644 "${srcdir}/courier-esmtpd.service"	"${pkgdir}/usr/lib/systemd/system/courier-esmtpd.service"
  install -Dm 644 "${srcdir}/courier-esmtpd-ssl.service" "${pkgdir}/usr/lib/systemd/system/courier-esmtpd-ssl.service"
  install -Dm 644 "${srcdir}/courier-esmtpd-msa.service" "${pkgdir}/usr/lib/systemd/system/courier-esmtpd-msa.service"
  install -Dm 644 "${srcdir}/courier.service" 		"${pkgdir}/usr/lib/systemd/system/courier.service"
  install -Dm 644 "${srcdir}/courier.service" 		"${pkgdir}/usr/lib/systemd/system/courier.service"
  install -Dm 644 "${srcdir}/courier-courierfilter.service" "${pkgdir}/usr/lib/systemd/system/courier-courierfilter.service"
  install -Dm 644 "${srcdir}/webmaild.service" 		"${pkgdir}/usr/lib/systemd/system/webmaild.service"
  
  # Install systemd configuration files
  install -Dm 644 "$srcdir/courier-imapd.conf" "$pkgdir/usr/lib/tmpfiles.d/courier-imapd.conf"

   #install -Dm 655 ${srcdir}/courier-webmail-cleancache.cron.hourly \
   # ${pkgdir}/etc/cron.hourly/courier-webmail-cleancache
  # bug http://bugs.archlinux.org/task/5154
   find ${pkgdir}/usr/lib -name '*\.a' -exec rm -f {} \;
  # fixing some permissions
   chown -R courier:courier ${pkgdir}/usr/lib/courier/modules
   rm -r ${pkgdir}/var/run
   #chown -R courier:courier ${pkgdir}/var/run/courier
   chown root:root ${pkgdir}/usr/{.,bin,lib,share}
}
