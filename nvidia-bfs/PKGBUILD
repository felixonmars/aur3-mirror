# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: fsckd <fsckdaemon at gmail dot com>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-bfs
pkgver=295.33
_extramodules=extramodules-3.3-bfs
pkgrel=2
pkgdesc="NVIDIA drivers for linux-bfs."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-bfs>=3.3' 'linux-bfs<3.4' "nvidia-utils=${pkgver}")
makedepends=('linux-bfs-headers>=3.3' 'linux-bfs-headers<3.4')
conflicts=('nvidia-bfs-96xx' 'nvidia-bfs-173xx')
provides=("nvidia=${pkgver}")
license=('custom')
install=nvidia.install
options=(!strip)
[ "$CARCH" = "i686" ] && _arch='x86' && _pkg="NVIDIA-Linux-${_arch}-${pkgver}" && md5sums=('1634fb3115526caeae5eb8227282bf17')
[ "$CARCH" = "x86_64" ] && _arch='x86_64' && _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32" && md5sums=('04e814843bcaf23efa0b4b3df4ea2700')
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

build() {
    _kernver="$(cat /lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC="/lib/modules/${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/lib/modprobe.d/nvidia_bfs.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
