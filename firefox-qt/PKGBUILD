# $Id: PKGBUILD 73155 2010-03-23 10:23:39Z jgc $
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox-qt
pkgver=3.6.3
pkgrel=2
_xulver=1.9.2.3
pkgdesc="Standalone web browser from mozilla.org"
arch=(i686 x86_64)
license=('MPL' 'GPL' 'LGPL')
depends=("xulrunner-qt=${_xulver}" 'desktop-file-utils')
makedepends=('zip' 'pkgconfig' 'diffutils' 'qt' 'python' 'wireless_tools' 'patch')
replaces=('firefox3')
install=firefox.install
url="http://www.mozilla.org/projects/firefox"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
        mozconfig
        firefox-qt.desktop
        firefox-qt-safe.desktop
        mozilla-firefox-1.0-lang.patch
        browser-defaulturls.patch
        firefox-version.patch)

md5sums=('5e4541ab23084b7b14ba228291ce2688'
         'c3a62a73278a069fff0f16e391f79670'
         '55d930b5bec8778d222d0a7946bdd676'
         'a2958ad90a26e16f66508d91b43d2683'
         'bd5db57c23c72a02a489592644f18995'
         '8bf21158090c89fd834bb711aae0602e'
         '78b9b0bded64cff20a73d8a3ad3fc38b')

build() {
  cd "${srcdir}/mozilla-1.9.2"
  patch -Np1 -i "${srcdir}/mozilla-firefox-1.0-lang.patch"
  patch -Np0 -i "${srcdir}/browser-defaulturls.patch"
  patch -Np1 -i "${srcdir}/firefox-version.patch"

  cp "${srcdir}/mozconfig" .mozconfig
  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,/opt/ffqt/lib/firefox-3.6"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}" || return 1
  make -j1 DESTDIR="${pkgdir}" install || return 1

  rm -f ${pkgdir}/opt/ffqt/lib/firefox-3.6/libjemalloc.so

  install -m755 -d ${pkgdir}/opt/ffqt/share/applications
  install -m755 -d ${pkgdir}/opt/ffqt/share/pixmaps
  install -m644 ${srcdir}/mozilla-1.9.2/browser/branding/unofficial/default48.png ${pkgdir}/opt/ffqt/share/pixmaps/firefox.png || return 1
  install -m644 ${srcdir}/firefox-qt.desktop ${pkgdir}/opt/ffqt/share/applications/ || return 1
  install -m644 ${srcdir}/firefox-qt-safe.desktop ${pkgdir}/opt/ffqt/share/applications/ || return 1
}
