# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>
# Contributor: Franco Tortoriello

pkgname=firefox-qt
pkgver=12.0
pkgrel=1
pkgdesc="Web browser from mozilla.org, using Qt (experimental)"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('qt' 'mozilla-common' 'nss>=3.13.3' 'libxt' 'hunspell' 'startup-notification' 'mime-types'
	 'dbus-glib' 'libnotify' 'libvpx' 'libevent' 'pango')
makedepends=('unzip' 'zip' 'python2' 'wireless_tools' 'yasm' 'mesa' 'autoconf2.13' 'libidl2' 'imake' 'upx')
optdepends=('firefox-oxygen-kde')
provides=("firefox=$pkgver")
conflicts=('firefox')
url="http://www.mozilla.org/projects/firefox"
options=(upx)
install=$pkgname.install
source=("ftp://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2"
	'mozconfig' 'firefox.desktop' 'firefox-install-dir.patch' 'vendor.js' 'libvpx.patch')
md5sums=('80c3e5927274de7f181fb5f931ac5fd4'
	 '7c919ae862eb31a5094974c39b801d47'
	 'bdeb0380c7fae30dd0ead6d2d3bc5873'
	 '594523c536a9be951b52c90dd85aff18'
	 '0d053487907de4376d67d8f499c5502b'
	 '5d418ecdbdb9f40597df6b978b0b5ee5')

build() {
  cd "$srcdir/mozilla-release"

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch
  patch -Np1 -i ../libvpx.patch

  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  # Fix compiling with GCC 4.7
  sed 's:#if defined(ANDROID):& || defined(OS_POSIX):' -i ipc/chromium/src/base/file_util.cc
  sed '/fcntl.h/a#include <unistd.h>' -i ipc/chromium/src/base/file_util_linux.cc
  sed '/fcntl.h/a#include <unistd.h>' -i ipc/chromium/src/base/message_pump_libevent.cc
  sed '/sys\/time\.h/a#include <unistd.h>' -i ipc/chromium/src/base/time_posix.cc
  sed '/nsAutoPtr.h/a#include <unistd.h>' -i widget/src/qt/nsSound.cpp

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"

  autoconf-2.13
  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "$srcdir/mozilla-release"
  make -j1 -f client.mk DESTDIR="$pkgdir" install

  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/pref"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 browser/branding/official/default${i/x*/}.png \
        "$pkgdir/usr/share/icons/hicolor/$i/apps/firefox.png"
  done

  install -Dm644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -sf /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
