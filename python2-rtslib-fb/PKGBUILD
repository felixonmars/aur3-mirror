# Contributor: Johannes Dewender  arch at JonnyJD dot net
pkgname=python2-rtslib-fb
_pkgname=rtslib-fb
pkgver=2.1.fb25
pkgrel=1
epoch=
pkgdesc="free branch version of the LIO target API"
arch=('any')
url="https://github.com/agrover/rtslib-fb"
license=('AGPL3')
depends=('python2-ipaddr' 'python2-ethtool')
# this is incompatible to python2-rtslib
provides=()
conflicts=('python2-rtslib')
backup=()
options=()
install=
source=($_pkgname-$pkgver.tar.gz::https://github.com/agrover/$_pkgname/tarball/v$pkgver)
md5sums=('85ac8bc55517e40431ad3bb38878cfbc')

_githubname=agrover-rtslib-fb
_gitversion=43341e9

build() {
  cd "$srcdir/$_githubname-$_gitversion"
  python2 setup.py build
}

package() {
  cd "$srcdir/$_githubname-$_gitversion"
  python2 setup.py install --skip-build --root="$pkgdir/" --optimize=1

  install -d "$pkgdir/var/lib/target/fabric"
  for file in specs/*; do
    install -m 644 "$file" "$pkgdir/var/lib/target/fabric/"
  done
}

# vim:set ts=2 sw=2 et:
