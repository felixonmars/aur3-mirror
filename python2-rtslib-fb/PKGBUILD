# Contributor: Johannes Dewender  arch at JonnyJD dot net
pkgname=python2-rtslib-fb
_pkgname=rtslib-fb
pkgver=2.1.fb11
pkgrel=1
epoch=
pkgdesc="free branch version of RTS generic SCSI target API"
arch=('any')
url="https://github.com/agrover/rtslib-fb"
license=('AGPL3')
depends=('python2-ipaddr' 'python2-ethtool' 'python-configobj')
provides=('python2-rtslib')
conflicts=('python2-rtslib')
backup=()
options=()
install=
source=($_pkgname-$pkgver.tar.gz::https://github.com/agrover/$_pkgname/tarball/v$pkgver)
md5sums=('5ed6d81acf6cbb04e685194665e84d82')

_githubname=agrover-rtslib-fb
_gitversion=e86a611

build() {
  cd "$srcdir/$_githubname-$_gitversion"
  python2 setup.py build
}

package() {
  cd "$srcdir/$_githubname-$_gitversion"
  python2 setup.py install --skip-build --root="$pkgdir/" --optimize=1

  install -d "$pkgdir/var/lib/target/fabric"
  for file in specs/*; do
    install -m 644 "$file" "$pkgdir/var/lib/target/fabric/"
  done
}

# vim:set ts=2 sw=2 et:
