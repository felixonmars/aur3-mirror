# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-libsvm
pkgver=3.17
pkgrel=1
pkgdesc="A library for Support Vector Machines classification (mingw-w64)"
arch=(any)
url="http://www.csie.ntu.edu.tw/~cjlin/libsvm/"
license=('LGPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!strip')
source=("http://www.csie.ntu.edu.tw/~cjlin/libsvm/libsvm-${pkgver}.tar.gz")
md5sums=('67f8b597ce85c1f5288d7838e57ea28a')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"      

build()
{
  cd "$srcdir/libsvm-$pkgver"
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ${_arch}-g++ -c ../svm.cpp
    ${_arch}-g++ -shared -o libsvm.dll svm.o -Wl,--out-implib,libsvm.dll.a 
    ${_arch}-ar cru libsvm.a svm.o 
    ${_arch}-ranlib libsvm.a
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/libsvm-$pkgver/build-${_arch}"
    install -d $pkgdir/usr/${_arch}/include
    install -d $pkgdir/usr/${_arch}/lib
    install -d $pkgdir/usr/${_arch}/bin
    install libsvm.dll.a $pkgdir/usr/${_arch}/lib
    install libsvm.dll $pkgdir/usr/${_arch}/bin
    install libsvm.a $pkgdir/usr/${_arch}/lib
    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
  done
}
