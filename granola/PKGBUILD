# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: Paul Cox <pauldcox@gmail.com>
# Contributor: Matthew Bauer

pkgname=granola
pkgver=5.0.11
pkgrel=2
pkgdesc="MiserWare intelligent power management for PCs"
arch=(i686 x86_64)
if [[ $CARCH = "x86_64" ]]; then
    _arch=$CARCH
else
    _arch=i386
fi
url=http://grano.la/
license=(custom)
[[ $CARCH = "x86_64" ]] && depends=(gcc-libs-multilib)
optdepends=('python2: MiserWare customer support via the mw-feedback script'
    'granola-connect: daemon to upload energy stats'
    'granola-gui: Granola graphical interface')
backup=(etc/$pkgname.conf)
options=(!strip)
install=$pkgname.install
source=(https://download.miserware.com/linux/tar/$_arch/$pkgname-$pkgver-$_arch.tar.gz
    $pkgname.confd
    $pkgname.rcd.sh
    $pkgname.service)
if [[ $CARCH = "x86_64" ]]; then
    sha256sums=('8144a196f9de3e4f996d9ec4b0d4f9ac1a09d1de2464151c454da80b4fd7fbf0')
    sha512sums=('09908ac4bf06b231527c9a7ae991e7bbc13006ee45ffda12756fd0761ab1c4b97017e6ece2c7d4d14372123c8a0328e1462e92068981376239742eba4c90632a')
else
    sha256sums=('4a4b2ccea5ce232d59e0a3f756d51f6e8777eb5ae42993fb05d7d98ec76d9846')
    sha512sums=('846f5cfe70cd32a50cb3cb24b30a4109ba32dae8e1f5f0bbf9bc88f7273120b2fe1f4867b3800f0373beac654c7e3e15c00ee4e234d11862e39053ad9efec250')
fi
sha256sums+=('b2f5e32c820dc692d37568883a4e8ef254b0feafa8da0952b4904596bbc192c4'
    'f1bd47b8a6624ef7412ca88a99c464d1931f8351a014cb5256893bae45970ec2'
    '519da90556348052f82625fff37951633e632c3794e6adbeb2294576673dbb31')
sha512sums+=('c9a5d92a9af069ad6b4c2a65ad7d8c840d1679db7e9f87cd65c5b23f57de2001dd27856c8f4bd0b14b34cc5c99b3e5b4bcefb4029f235714873e9ddf205c6ed2'
    '2e6d7c99f90858fb030098a1d0be55f8419d3e3eecc4ebe8d6ff0d7825fe83110e2ffe9623140bae2fe7c16a17e2867a114c3ae1fd1cbde3640d2c1f3ddbc8e3'
    '1f2fab3d6bbce2180de0680c8ce1a87965456ae1808707b9deb4af59a819616a83dfefac7d8156207efdc570546634bc08d72710db6c37bd828d5122894cdafb')

build() {
    sed -i "s:^#!/usr/bin/env python$:&2:" "$srcdir"/$pkgname-$pkgver-$_arch/usr/bin/mw-feedback
}

package() {
    cd "$srcdir"/$pkgname-$pkgver-$_arch/

    cp -r usr var "$pkgdir"
    chmod 755 "$pkgdir"/var/lib/miserware
    chmod 644 "$pkgdir"/var/lib/miserware/*
    install -Dm644 etc/$pkgname.conf "$pkgdir"/etc/$pkgname.conf

    install -Dm644 ../$pkgname.confd "$pkgdir"/etc/conf.d/$pkgname.conf
    install -Dm755 ../$pkgname.rcd.sh "$pkgdir"/etc/rc.d/$pkgname
    install -Dm644 ../$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service

    rm -rf "$pkgdir"/usr/share/doc/
    install -Dm644 usr/share/doc/$pkgname-$pkgver/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
