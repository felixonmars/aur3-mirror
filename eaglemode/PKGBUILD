# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=eaglemode
pkgver=0.84.0
pkgrel=1
pkgdesc="Visit almost everything in your PC simply by zooming in"
arch=('i686' 'x86_64')
url="http://eaglemode.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'libtiff' 'libpng' 'librsvg' 'poppler-glib' 'xdg-utils' 'xine-lib' 'xz')
makedepends=('perl>=5.8')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2 \
        $pkgname.desktop \
        $pkgname.sh)
sha256sums=('9a816d0f6699889e356715696cd3d31ea899801d6e6892e58187029902594058'
            '927e77b2cacc29ea0db13fbbf506b1d87515ea20849a3f990ff42f9f2565daa8'
            'e7c372c754b00ddddf17f2236b1edbf899b82dbac62365be257aaff9a7fc214e')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  install -d ${pkgdir}/opt

  perl make.pl build
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  perl make.pl install dir=${pkgdir}/opt/$pkgname
  install -D -m755 ${srcdir}/$pkgname.sh ${pkgdir}/usr/bin/$pkgname

# .desktop + icon file
  install -D -m644 ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop
  install -D -m644 ${srcdir}/$pkgname-$pkgver/res/icons/${pkgname}32.png \
    ${pkgdir}/usr/share/icons/hicolor/32x32/apps/$pkgname.png
  install -D -m644 ${srcdir}/$pkgname-$pkgver/res/icons/${pkgname}48.png \
    ${pkgdir}/usr/share/icons/hicolor/48x48/apps/${pkgname}.png
}
