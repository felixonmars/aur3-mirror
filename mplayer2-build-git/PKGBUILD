# Maintainer: Gustavo Alvarez <s1lpkn07@gmail.com>

pkgname=mplayer2-build-git
pkgver=20120514
pkgrel=1
pkgdesc="A movie player for linux (uses statically linked libav; git version). lachs0r's Fork"
arch=('i686' 'x86_64')
depends=('libgl' 'libxss' 'fribidi' 'libxxf86dga' 'ttf-dejavu' 'desktop-file-utils' 'libbluray-git'
         'libdvdnav-svn' 'libdvdread-svn' 'libdv' 'jack' 'speex' 'enca' 'a52dec' 'libmng' 'libdca'
         'aalib' 'mpg123' 'directfb' 'libpulse' 'rsound' 'smbclient' 'xvidcore' 'harfbuzz-git' 'libmad'
         'libtheora' 'libcaca' 'faad2' 'libxxf86vm' 'libvdpau' 'libxinerama' 'libcdio' 'lcms2' 'libquvi-git')
license=('GPL')
url="http://www.mplayer2.org/"
makedepends=('git' 'mesa' 'python2' 'yasm' 'live-media')
backup=('etc/mplayer/codecs.conf'
        'etc/mplayer/input.conf')
provides=('mplayer' 'mplayer2')
conflicts=('mplayer' 'mplayer2')
replaces=('mplayer' 'mplayer2')
options=('!buildflags' '!emptydirs')
source=('http://ftp.mplayer2.org/pub/release/mplayer2-2.0.tar.xz'
        'mplayer2-build-git.install')
noextract=('mplayer2-2.0.tar.xz')
md5sums=('b880ae4be0e5b9693cdecf97c84b74f3'
         'cbc234e5e789e30c624741173992a225')
install="mplayer2-build-git.install"

_gitroot="git://git.mplayer2.org/mplayer2-build.git"
_gitname="mplayer2-build"

build() {
  cd "${srcdir}"

  # Use python2
  rm -fr python2
  mkdir python2
  ln -s /usr/bin/python2 python2/python
  export PATH=""${srcdir}"/python2:$PATH"

  if [ -d "${_gitname}" ]; then
    msg "Clean and Update repository"
    cd "${_gitname}"
    ./clean
    ./update
  else
    msg "Clone Repository"
    git clone "${_gitroot}" && cd "${_gitname}"
    # Change mplayer2 repo to lachs0r's mplayer2 repo
    sed 's|git://repo.or.cz/mplayer.git|git://github.com/lachs0r/mplayer2-lachs0r.git|g' -i .gitmodules
    ./init --shallow
  fi

  msg "Merge lachs0r's mplayer2 Testing branch"
  cd mplayer
  git fetch --depth=1 origin
  git checkout testing
  cd "${srcdir}"
  msg2 "Done"

  msg "Prepare Build folders"
  rm -fr "${_gitname}"-build
  cp -R "${srcdir}"/"${_gitname}" "${srcdir}"/"${_gitname}"-build
  cd "${_gitname}"-build
  msg2 "Done"

  msg "Add language PO files from mplayer2-2.0.tar.xz"
  bsdtar -xf "${srcdir}"/mplayer2-2.0.tar.xz mplayer2-2.0/po
  mv mplayer2-2.0/po mplayer && rm -fr mplayer2-2.0
  msg2 "Done"

  msg "Configure Mplayer2"
  # Make Mplayer2 build flags
  echo "--confdir=/etc/mplayer
--disable-arts
--disable-esd
--disable-lirc
--disable-lircc
--disable-live
--disable-mga
--disable-vstream
--enable-translation
--language=all
--language-doc=all
--language-man=all
--language-msg=all
--prefix=/usr
" > mplayer_options
  msg2 "Done"

  msg "Starting Make Mplayer2"
  make
  msg2 "Done"
}

package() {
  msg "Install Mplayer2"
  cd "${srcdir}"/"${_gitname}"-build
  make DESTDIR="${pkgdir}" install
  install -Dm664 mplayer/etc/{codecs,input,example}.conf "${pkgdir}"/etc/mplayer/
  install -d "${pkgdir}"/usr/share/{mplayer,applications}
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "${pkgdir}"/usr/share/mplayer/subfont.ttf
  install -Dm644 mplayer/etc/mplayer.{desktop,xpm} "${pkgdir}"/usr/share/applications/
  sed -e 's|gmplayer|mplayer|' -e 's|GTK|X11|' -i "${pkgdir}"/usr/share/applications/mplayer.desktop
  msg2 "Done"
}