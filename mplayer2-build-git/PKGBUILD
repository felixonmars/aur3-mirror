# Maintainer: Gustavo Alvarez <s1lpkn07@gmail.com>

pkgname=mplayer2-build-git
pkgver=20120410
pkgrel=1
pkgdesc="A movie player for linux (uses statically linked libav; git version)"
arch=('i686' 'x86_64')
depends=('libgl' 'libxss' 'fribidi' 'libxxf86dga' 'ttf-dejavu' 'desktop-file-utils' 'libnemesi-git' 'libbluray-git' 'libdvdnav-svn' 'libdvdread-svn' 'libdv' 'jack' 
'speex' 'enca' 'a52dec' 'libmng' 'libdca' 'aalib' 'mpg123' 'directfb' 'libpulse' 'rsound' 'smbclient' 'xvidcore' 'harfbuzz-git' 
'cdparanoia' 'libmad' 'libtheora' 'libcaca' 'faad2' 'libxxf86vm' 'libvdpau' 'libxinerama')
license=('GPL')
url="http://www.mplayer2.org/"
makedepends=('git' 'mesa' 'python2' 'unzip' 'yasm' 'live-media')
backup=('etc/mplayer/codecs.conf'
        'etc/mplayer/input.conf')
provides=('mplayer' 'mplayer2')
conflicts=('mplayer' 'mplayer2')
replaces=('mplayer' 'mplayer2')
options=('!buildflags' '!emptydirs')
source=('http://ftp.mplayer2.org/pub/release/mplayer2-2.0.tar.xz'
        'mplayer2-build-git.install')
md5sums=('b880ae4be0e5b9693cdecf97c84b74f3' 
         'cbc234e5e789e30c624741173992a225')
install="mplayer2-build-git.install"

_gitroot="git://git.mplayer2.org/mplayer2-build.git"
_gitname="${pkgname}"

build() {

  cd "${srcdir}"

  # Use python2
  [ -d "${srcdir}"/python2-path ] && rm -fr "${srcdir}"/python2-path
  mkdir "${srcdir}"/python2-path
  ln -s /usr/bin/python2 "${srcdir}"/python2-path/python
  export PATH=""${srcdir}"/python2-path:$PATH"
 
  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}"
    msg "Clean and Update repository"
    ./clean  
    ./update
  else
    msg "Clone Repository"
    git clone "${_gitroot}" "${srcdir}"/"${_gitname}"
    cd "${_gitname}"
    ./init --shallow
  fi
  msg2 "Done"  
  
  msg "Prepare Build folders"
  rm -fr "${srcdir}"/"${_gitname}"-build
  cp -r "${srcdir}"/"${_gitname}" "${srcdir}"/"${_gitname}"-build
  cd "${srcdir}"/"${_gitname}"-build

  # Add language PO files from "ZIP" file
  
  [ ! -d mplayer/po ] && cp -r "${srcdir}"/mplayer2-2.0/po "${srcdir}"/"${_gitname}"-build/mplayer
  rm -fr "${srcdir}"/mplayer2-2.0
  msg2 "Done"

  msg "Configure Mplayer2"

  # Make Mplayer2 build flags

  echo "--prefix=/usr
--disable-arts
--disable-esd
--disable-mga
--disable-lirc
--disable-lircc
--enable-translation
--language=all
--language-doc=all
--language-man=all
--language-msg=all
--confdir=/etc/mplayer
--enable-vstream
--disable-live
" > mplayer_options
  msg2 "Done"

  msg "Starting Make Mplayer2"

  make ${MAKEFLAGS}
  
  msg2 "Done"
}

package() {
  msg "Install Mplayer2"
  cd "${_gitname}"-build
  make DESTDIR="${pkgdir}" install
  cp mplayer/etc/{codecs,input,example}.conf "${pkgdir}"/etc/mplayer/
  mkdir -p "${pkgdir}"/usr/share/mplayer
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "${pkgdir}"/usr/share/mplayer/subfont.ttf
  install -Dm644 "${srcdir}"/"${pkgname}"-build/mplayer/etc/mplayer.desktop "${pkgdir}"/usr/share/applications/mplayer.desktop
  install -Dm644 "${srcdir}"/"${pkgname}"-build/mplayer/etc/mplayer.xpm "${pkgdir}"/usr/share/pixmaps/mplayer.xpm
  sed -e 's|TryExec=gmplayer|TryExec=mplayer|' -e 's|Exec=gmplayer %F|Exec=mplayer -really-quiet %F|' -e 's|GTK|X11|' -i "${pkgdir}"/usr/share/applications/mplayer.desktop
  msg2 "Done"
}
