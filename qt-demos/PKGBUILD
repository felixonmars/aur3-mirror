# Maintainer: Vojtech Kral <kral.vojtech at gmail dot com>

pkgname=qt-demos
pkgver=4.8.2
pkgrel=1
pkgdesc='Qt ships with lots of example programs that teach you how to implement various tasks with Qt'
arch=('i686' 'x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL')
depends=("qt>=${pkgver}")
makedepends=("qt>=${pkgver}")
optdepends=('qt-doc: Examples fully documented')
options=('!libtool')

_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://releases.qt-project.org/qt4/source/${_pkgfqn}.tar.gz"
        'shadow-build-plugandpaint.patch'
        'qtdemo.desktop')

sha256sums=('921b2a2d060934ceda65ae4615edec474cea13d3c893e7df19ad1262e7dc2379'
            '731de0f2f8747564be3bc33b4d03ae072c442887aaecbcb8525317d7ced7f0a7'
            '415fd225e671a36d9a4d64e57b78ae943c8f9af0cabad052ce30ec3e4e7dc8c2')

build()
{
  cd $srcdir/$_pkgfqn

  cp -r include/QtGui/private demos/shared/

  #This patch fixes linking error during shadow build
  patch -p0 < $srcdir/shadow-build-plugandpaint.patch

  cd $srcdir/$_pkgfqn/demos
  qmake
  make

  cd $srcdir/$_pkgfqn/examples
  qmake
  make

  cd $srcdir/$_pkgfqn/demos/qtdemo
  qmake
  make
}

package()
{
  cd $srcdir/$_pkgfqn

  find demos/* -not -name "*.*" -type f -executable -exec install -p -D -m755 '{}' ${pkgdir}'/usr/share/doc/qt/{}' \;
  find examples/* -not -name "*.*" -type f -executable -exec install -p -D -m755 '{}' ${pkgdir}'/usr/share/doc/qt/{}' \;
  install -p -D -m755 bin/qtdemo ${pkgdir}/usr/bin/qtdemo
  install -p -D -m644 ${srcdir}/qtdemo.desktop ${pkgdir}/usr/share/applications/qtdemo.desktop
}
