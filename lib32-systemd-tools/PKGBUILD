#Maintainer:	Jesse	Jaara		<gmail.com: jesse.jaara>
#Contributor:	Aaron	Griffin		<archlinux.org: aaron>
#Contributor:	Tobias	Powalowski	<archlinux.org: tpowa>
#Contributor:	Thomas	BÃ¤chler		<archlinux.org: thomas>

pkgname=lib32-systemd-tools
pkgver=189
pkgrel=1
arch=('x86_64')
pkgdesc="The userspace dev tools (udev) (32-bit)"
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
options=(!libtool)
depends=('lib32-glib2')
makedepends=('gcc-multilib' 'gperf' 'lib32-kmod' 'lib32-util-linux' 'lib32-libcap' 'intltool')
provides=('lib32-udev')
conflicts=('lib32-udev')
source=("http://www.freedesktop.org/software/systemd/systemd-$pkgver.tar.xz"
        '0001-Reinstate-TIMEOUT-handling.patch'
	'use-split-usr-path.patch'
	'gudev-1.0.pc'
	'libudev.pc')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/systemd-${pkgver}"

  patch -p1 -i ../0001-Reinstate-TIMEOUT-handling.patch
#  patch -p1 -i ../use-split-usr-path.patch

  ./configure \
      --libexecdir=/usr/lib32 \
      --libdir=/usr/lib32 \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-split-usr \
      --disable-ima \
      --disable-selinux \
      --disable-xz \
      --disable-tcpwrap \
      --disable-pam \
      --disable-acl \
      --disable-audit \
      --disable-libcryptsetup \
      --disable-gcrypt \
      --disable-qrencode \
      --disable-binfmt \
      --disable-vconsole \
      --disable-readahead \
      --disable-quotacheck \
      --disable-randomseed \
      --disable-logind \
      --disable-hostnamed \
      --disable-timedated \
      --disable-localed \
      --disable-coredump \
      --disable-keymap \
      --disable-manpages \
      --with-distro=arch \
      --with-usb-ids-path=/usr/share/hwdata/usb.ids \
      --with-pci-ids-path=/usr/share/hwdata/pci.ids \
      --with-firmware-path=/usr/lib/firmware/updates:/lib/firmware/updates:/usr/lib/firmware:/lib/firmware

  make libudev.la libgudev-1.0.la
}

package() {
  cd "${srcdir}/systemd-${pkgver}"

  mkdir -p "${pkgdir}/usr/lib32/pkgconfig"

  cp .libs/libgudev-1.0.a .libs/libgudev-1.0.so.0.1.2 "${pkgdir}/usr/lib32/"
  cp .libs/libudev.a .libs/libudev.so.1.1.0 "${pkgdir}/usr/lib32/"

  ln -s libgudev-1.0.so.0.1.2 "${pkgdir}/usr/lib32/libgudev-1.0.so.0"
  ln -s libgudev-1.0.so.0.1.2 "${pkgdir}/usr/lib32/libgudev-1.0.so"

  ln -s libudev.so.1.1.0 "${pkgdir}/usr/lib32/libudev.so.1"
  ln -s libudev.so.1.1.0 "${pkgdir}/usr/lib32/libudev.so"

  cp "${srcdir}/libudev.pc" "${pkgdir}/usr/lib32/pkgconfig/"
  cp "${srcdir}/gudev-1.0.pc" "${pkgdir}/usr/lib32/pkgconfig/"
}

md5sums=('ac2eb313f5dce79622f60aac56bca66d'
         'd9827d244813e1cf29e17f6e6626db30'
         'fd9501032828ac40816d79e743eacfea'
         '62b6261ff41de344b1a8ecba087917e6'
         'cab512d8f5d41cf6888cf90eecb38012')
