#Maintainer:	Jesse	Jaara		<gmail.com: jesse.jaara>
#Contributor:	Aaron	Griffin		<archlinux.org: aaron>
#Contributor:	Tobias	Powalowski	<archlinux.org: tpowa>
#Contributor:	Thomas	BÃ¤chler		<archlinux.org: thomas>

pkgname=lib32-systemd-tools
pkgver=185
pkgrel=2
arch=('x86_64')
pkgdesc="The userspace dev tools (udev) (32-bit)"
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
options=(!libtool)
depends=('lib32-glib2')
makedepends=('gcc-multilib' 'gperf' 'lib32-kmod' 'lib32-util-linux')
source=("http://www.freedesktop.org/software/systemd/systemd-$pkgver.tar.xz"
        '0001-Reinstate-TIMEOUT-handling.patch')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/systemd-${pkgver}"

  patch -p1 -i ../0001-Reinstate-TIMEOUT-handling.patch

  ./configure \
      --libexecdir=/usr/lib32 \
      --libdir=/usr/lib32 \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-split-usr \
      --disable-ima \
      --disable-selinux \
      --disable-xz \
      --disable-tcpwrap \
      --disable-pam \
      --disable-acl \
      --disable-audit \
      --disable-libcryptsetup \
      --disable-binfmt \
      --disable-vconsole \
      --disable-readahead \
      --disable-quotacheck \
      --disable-randomseed \
      --disable-logind \
      --disable-hostnamed \
      --disable-timedated \
      --disable-localed \
      --disable-coredump \
      --disable-keymap \
      --disable-manpages \
      --with-distro=arch \
      --with-usb-ids-path=/usr/share/hwdata/usb.ids \
      --with-pci-ids-path=/usr/share/hwdata/pci.ids \
      --with-firmware-path=/usr/lib/firmware/updates:/lib/firmware/updates:/usr/lib/firmware:/lib/firmware

  make
}

package() {
  cd "${srcdir}/systemd-${pkgver}"

  mkdir -p "${pkgdir}/usr/lib32/pkgconfig"

  cp .libs/libgudev-1.0.a .libs/libgudev-1.0.so.0.1.1 "${pkgdir}/usr/lib32/"
  cp .libs/libudev.a .libs/libudev.so.1.0.1 "${pkgdir}/usr/lib32/"

  ln -s libgudev-1.0.so.0.1.1 "${pkgdir}/usr/lib32/libgudev-1.0.so.0"
  ln -s libgudev-1.0.so.0.1.1 "${pkgdir}/usr/lib32/libgudev-1.0.so"

  ln -s libudev.so.1.0.1 "${pkgdir}/usr/lib32/libudev.so.1"
  ln -s libudev.so.1.0.1 "${pkgdir}/usr/lib32/libudev.so"

  cp src/libudev/libudev.pc "${pkgdir}/usr/lib32/pkgconfig/"
  cp src/gudev/gudev-1.0.pc "${pkgdir}/usr/lib32/pkgconfig/"
}

md5sums=('a7dbbf05986eb0d2c164ec8e570eb78f'
         'd9827d244813e1cf29e17f6e6626db30')
