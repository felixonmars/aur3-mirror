# Maintainer: cantabile <cantabile dot desu at gmail dot com>
# Contributor: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>

pkgname=atris
pkgver=1.0.7
pkgrel=8
pkgdesc='A tetris clone with a lot of cool features'
arch=('i686' 'x86_64')
url='http://www.wkiri.com/projects/atris/'
license=('GPL')
depends=('desktop-file-utils' 'sdl_ttf' 'ttf-dejavu')
makedepends=('sed')
install=atris.install
source=("http://ftp.debian.org/debian/pool/main/a/${pkgname}/${pkgname}_${pkgver}.dfsg.1.orig.tar.gz"
        "http://ftp.de.debian.org/debian/pool/main/a/${pkgname}/${pkgname}_${pkgver}.dfsg.1-7.1.diff.gz"
        'use-dejavu.diff'
        'add-delay-when-paused.patch')
backup=('var/games/atris/Atris.Players'
        'var/games/atris/Atris.Scores')

build() {
  cd "${srcdir}"
  
  mkdir -p "${srcdir}/debian-patch"
  
  patch -N -d "${srcdir}/debian-patch" -i "${srcdir}/${pkgname}_${pkgver}.dfsg.1-7.1.diff"
  
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -Np0 -i "${srcdir}/debian-patch/00-atris-1.0.7.configure.in_pathfix.diff"
  patch -Np0 -i "${srcdir}/debian-patch/01-atris-1.0.7.Makefile.am_pathfix.diff"
  patch -Np0 -i "${srcdir}/debian-patch/02-atris-1.0.7.proper_statedir.diff"
  # don't use the freefont patch - it has the wrong dir and I want dejavu.
  patch -Np0 -i "${srcdir}/use-dejavu.diff"
  patch -Np0 -i "${srcdir}/debian-patch/04-atris-1.0.7.window_mode_fix.diff"
  patch -Np0 -i "${srcdir}/debian-patch/05-atris-1.0.7.atrisrc.perms.fix.diff"
  # without this patch, atris would use 100% cpu when paused (without flames).
  # thanks to Janno for solving the problem.
  patch -Np2 -i "${srcdir}/add-delay-when-paused.patch"
  
  # thanks to this, it builds on x86_64:
  # http://linux.derkeiler.com/Newsgroups/linux.redhat/2005-03/0373.html
  libtoolize \
     --force \
     --automake \
     --copy

  aclocal \
     --force

  autoheader \
     --force

  automake \
     --gnu \
     --force-missing \
     --add-missing \
     --copy

  autoconf \
     --force

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --datadir=/usr/share \
    --mandir=/usr/share/man \
    --localstatedir=/var

  make

  sed -i '/TryExec/iIcon=atris' "$srcdir/debian-patch/atris.desktop"
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  
  # we create var/games/ and var/games/atris because the default
  # permissions and ownership won't do
  install -d -m 775 -g games "$pkgdir/var/games"
  install -d -m 775 -g games "$pkgdir/var/games/atris"

  make DESTDIR="$pkgdir" install

  # we make usr/bin/atris setgid and group 'games' so it can
  # write in var/games/atris
  chgrp games "$pkgdir/usr/bin/atris"
  chmod 2755 "$pkgdir/usr/bin/atris"
  
  make install_gamedocs target="$pkgdir/usr/share/doc/atris/"
  
  # install desktop and icon files
  install -D -m 644 "$srcdir/debian-patch/atris.desktop" \
    "$pkgdir/usr/share/applications/atris.desktop"
  install -D -m 644 "$srcdir/${pkgname}-${pkgver}/icon.xpm" \
    "$pkgdir/usr/share/pixmaps/atris.xpm"
  
  # install the manuals â€” makepkg compresses them for us
  install -D -m 644 "$srcdir/debian-patch/atris.1" \
    "$pkgdir/usr/share/man/man1/atris.1"
  install -D -m 644 "$srcdir/debian-patch/atris.de.1" \
    "$pkgdir/usr/share/man/de/man1/atris.1"
  
  # default would be 666, the debian package makes them 664
  # *shrug*
  chmod 664 "$pkgdir"/var/games/atris/Atris.{Players,Scores}
}

# vim:set ts=2 sw=2 et:
md5sums=('0634240c8dda338ae2a3ad81514ed2b5'
         '004b9586a525dfb2a870108c7dc8457f'
         '57b7aa9aec0b48c3921d44a2c05eb3bc'
         '23c8892dbb5162374be4376c6b8c4a4d')
