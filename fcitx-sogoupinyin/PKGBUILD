# Maintainer:  Jove Yu <yushijun110[AT]gmail.com>
# Contributor: csslayer <wengxt[AT]gmail.com>
#              Felix Yan <felixonmars@gmail.com>

pkgname=fcitx-sogoupinyin
pkgver=1.1.0.0037
_time=1408440412
pkgrel=3
pkgdesc="Sogou Pinyin for Linux"
arch=('x86_64' 'i686')
url="http://pinyin.sogou.com/linux/"
license=('custom')
depends=('fcitx' 'opencc' 'libidn' 'fcitx-qt4')


if [ "${CARCH}" = "i686" ]; then
    _LIB_DIR=i386-linux-gnu
    _ARCH=i386
    md5sums=('1f27859de8df10d60330597cdff947d2'
         '2b6cef8db26e9f1d1c300dba5de9c215')
else
    _LIB_DIR=x86_64-linux-gnu
    _ARCH=amd64
    md5sums=('7088f392d940144dc94a6cd34b0d23f3'
         '2b6cef8db26e9f1d1c300dba5de9c215')

fi

source=("http://download.ime.sogou.com/${_time}/sogou_pinyin_linux_${pkgver}_${_ARCH}.deb"
        "sogou-autostart")

package(){
    cd ${srcdir}
  
    tar xJvf data.tar.xz -C "${pkgdir}"
  
    mv "$pkgdir"/usr/lib/{$_LIB_DIR/,}fcitx
    rmdir "$pkgdir/usr/lib/${_LIB_DIR}"
    
    rm -r "$pkgdir"/etc/apt
    rm -r "$pkgdir"/usr/share/keyrings

    ln -s /usr/lib/libopencc.so "$pkgdir"/usr/lib/libopencc.so.1

    #sleep 5s to start 
    install -m755 sogou-autostart "$pkgdir"/usr/bin

    rm "$pkgdir"/etc/xdg/autostart/fcitx-ui-sogou-qimpanel.desktop
    cp "$pkgdir"/usr/share/applications/fcitx-ui-sogou-qimpanel.desktop "$pkgdir"/etc/xdg/autostart/fcitx-ui-sogou-qimpanel.desktop
    sed -i 's/sogou-qimpanel\ %U/sogou-autostart/g' "$pkgdir"/etc/xdg/autostart/fcitx-ui-sogou-qimpanel.desktop 
    
}


