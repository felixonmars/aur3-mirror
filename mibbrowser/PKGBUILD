# Maintainer: Garry Harthill <gazzerh@gmail.com>

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=mibbrowser
pkgver=9.6.3710
pkgrel=1
pkgdesc="A powerful and easy-to-use MIB browser. Supports standard & proprietary MIBs. Also issue SNMP requests to retrieve agent data. Built-in trap receiver can receive SNMP traps and handle trap storm."
arch=('any')
license=('custom')
depends=('java-runtime')
url="http://ireasoning.com/mibbrowser.shtml"
source=("$pkgname-$pkgver.zip::http://www.ireasoning.com/download/mibfree/$pkgname.zip")
md5sums=('ab0ee7c6dc1a2a8c118cd2f685da2a03')

package() {
  _dest="opt/${pkgname}"

  # Licenses first, before we move the whole directory to opt
  cd "$srcdir/ireasoning/${pkgname}"
  for F in license.txt THIRDPARTYLICENSEREADME.txt ; do
    # Install to /usr and remove from distribution dir
    install -D -m644 $F "${pkgdir}/usr/share/licenses/${pkgname}/$F"
    rm -f $F
    # Symlink the original file from /opt to /usr
    ln -sf /usr/share/licenses/${pkgname}/$F $F
  done

  # Install the compiled app to /opt
  cd "$srcdir/ireasoning"
  install -dm755 "$pkgdir/opt/"
  cp -r ${pkgname} $pkgdir/opt/

  # Create a symlink to /usr/bin
  install -dm755 "$pkgdir/usr/bin/"
  ln -sf /${_dest}/browser.sh "$pkgdir/usr/bin/$pkgname"
  chmod 755 "${pkgdir}/${_dest}/browser.sh"

  # Log file needs to be writeable by all users
  touch "$pkgdir/${_dest}/log/log.txt"
  chmod 666 "$pkgdir/${_dest}/log/log.txt"

  # Fix the run script
  sed -e "s|^DIRNAME=.*\$|DIRNAME='/${_dest}';|g" \
    -i "${pkgdir}/${_dest}/browser.sh"
}

# vim:set ts=2 sw=2 et:
