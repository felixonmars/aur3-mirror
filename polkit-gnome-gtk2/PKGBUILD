# Maintainer: Florian Dejonckheere <florian@floriandejonckheere.be>
# Taken from the polkit-gnome PKGBUILD
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=polkit-gnome-gtk2
_pkgname=polkit-gnome
pkgver=0.105
pkgrel=1
pkgdesc="PolicyKit integration for the GNOME desktop"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/PolicyKit"
license=('LGPL')
depends=('polkit' 'gtk3')
makedepends=('intltool' 'gobject-introspection')
conflicts=('polkit-gnome')
options=(!libtool)
source=(http://download.gnome.org/sources/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.xz
        polkit-gnome-authentication-agent-1.desktop)
sha256sums=('1784494963b8bf9a00eedc6cd3a2868fb123b8a5e516e66c5eda48df17ab9369'
            'ad19e73ae8eba00a8f66bd9afd3e0e1fd7ca22f716e29b97f097d9c01711e0de')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  
  # Enable GTK-2 build
  sed 's/GTK_REQUIRED=3.0.0/GTK_REQUIRED=2.0.0/' -i configure.ac
  sed 's/PKG_CHECK_MODULES(GTK, gtk+-3.0 >= $GTK_REQUIRED)/PKG_CHECK_MODULES(GTK, gtk+-2.0 >= $GTK_REQUIRED)/' -i configure.ac

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-gnome \
      --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/polkit-gnome-authentication-agent-1.desktop" \
      "${pkgdir}/etc/xdg/autostart/polkit-gnome-authentication-agent-1.desktop"
}
