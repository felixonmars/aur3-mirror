# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@gmail.com>

pkgname=apache-spark
pkgver=1.0.1
pkgrel=2
pkgdesc="fast and general engine for large-scale data processing"
arch=('any')
url="http://spark.apache.org"
license=('APACHE')
depends=('bash' 'java-environment>=6')
install=apache-spark.install
source=("http://d3kbcqa49mib13.cloudfront.net/spark-$pkgver-bin-hadoop1.tgz"
        'apache-spark-standalone.service'
        'spark-env.sh')
md5sums=('8ac185df2c73d48d2380d41ad9d65200'
         'SKIP'
         'SKIP')
backup=('etc/apache-spark/spark-env.sh')

PKGEXT=${PKGEXT:-'.pkg.tar.xz'}

prepare() {
  cd "$srcdir/spark-$pkgver-bin-hadoop1"

  sed -i 's|pid=$SPARK_PID_DIR/spark-$SPARK_IDENT_STRING-$command-$instance.pid|pid=/var/lib/apache-spark/spark-daemon.pid|' sbin/spark-daemon.sh
}

package() {
  cd "$srcdir/spark-$pkgver-bin-hadoop1"

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/share"

  rm -f bin/*.cmd bin/pyspark
  rm -rf examples/ ec2/ python/

  cp -r "$srcdir/spark-$pkgver-bin-hadoop1" "$pkgdir/usr/share/apache-spark/"

  cd "$pkgdir/usr/bin"
  ln -s ../share/apache-spark/bin/spark-{class,shell,submit} .


  install -Dm644 "$srcdir/apache-spark-standalone.service" "$pkgdir/usr/lib/systemd/system/apache-spark-standalone.service"
  install -Dm644 "$srcdir/spark-env.sh" "$pkgdir/etc/apache-spark/spark-env.sh"

  cd "$pkgdir/usr/share/apache-spark/conf"
  ln -sf "/etc/apache-spark/spark-env.sh" .
}
