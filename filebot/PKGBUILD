# Maintainer:  Kaurin <milos dot kaurin at gmail>
# Contributor: Nathan Owe <ndowens04 at gmail>
pkgname=filebot
pkgver=2.61
pkgrel=4
pkgdesc="The ultimate tool to rename TV/anime shows, download subtitles, and validate checksums"
arch=('any')
url="http://filebot.sourceforge.net/"
license=('GPL')
depends=('java-runtime')
source=(http://downloads.sourceforge.net/$pkgname/FileBot_$pkgver.jar
        $pkgname.sh $pkgname.png $pkgname.desktop)
noextract=(FileBot_$pkgver.jar)
md5sums=('724efb9f64d1564147e1d0cccee99d0d'
         '9d5bb93616ca27030ab6d45e00c96638'
         '05e6b476aa19f56b0ab14ae32718ad97'
         'f37edd0bba7570904d28ab1681c7a7f3')
optdepends=('mediainfo: episode naming / sublight usage' 'p7zip: adds reading/extracting functionality')
changelog=ChangeLog
package() {
  cd "$srcdir"
  install -Dm644 FileBot_$pkgver.jar \
    "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
