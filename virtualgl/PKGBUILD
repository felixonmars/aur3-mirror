# Contributor: FigoDaPaura <ffigoDaPaura>

pkgname=virtualgl
pkgver=2.3.1
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card."
arch=('i686' 'x86_64')
url="http://virtualgl.org"
license=('LGPL')
depends=('libgl' 'libxv' 'turbojpeg')
makedepends=('cmake' 'mesa')
provides=('virtualgl')
 
source=("http://downloads.sourceforge.net/project/virtualgl/VirtualGL/$pkgver/VirtualGL-$pkgver.tar.gz")
 
sha1sums=('087f2c8cab080e7d68d22ee5f08cb39437e63d8d')
 
build() {
  cd "$srcdir/VirtualGL-$pkgver"
 
  mkdir -p build
  cd build
 
  cmake .. -DCMAKE_INSTALL_PREFIX=/opt/VirtualGL -DTJPEG_INCLUDE_DIR=/usr/include -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
    -DVGL_LIBDIR=/usr/lib -DVGL_BINDIR=/usr/bin
  make
}
 
package() {
  cd "$srcdir/VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"
 
  mkdir -p "$pkgdir/opt/VirtualGL/bin"
  mv "$pkgdir"/usr/bin/glxinfo "$pkgdir/opt/VirtualGL/bin"
}
