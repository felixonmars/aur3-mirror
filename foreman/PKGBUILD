# Maintainer: Greg Sutcliffe <greg.sutcliffe@gmail.com>>

# Currently does a full bundle install, there are no split packages.

pkgname=foreman
pkgver=1.1
pkgrel=2
pkgdesc="Infrastruce management app built on Ruby-on_rails. Integrates with Puppet for complete lifecycle management or real or virtual servers"
arch=('any')
url="http://theforeman.org"
license=('GPL3')
depends=('ruby' 'ruby-bundler' 'libxslt' 'libmysqlclient' 'postgresql-libs' 'libvirt')
conflicts=('foreman-git')
backup=("etc/foreman/settings.yaml")
options=(emptydirs !strip)
install="foreman.install"
source=(https://github.com/theforeman/foreman/archive/${pkgver}.tar.gz
        foreman.systemd
        foreman.tmpfiles.conf
        foreman.cron.d
        foreman.logrotate)
noextract=()
md5sums=('defdfd2daec51abca4aee4ff6c68199c'
         '1c18ebbfc2a11f48f1fa727dd8bd833c'
         '9a24ac601fb92a388eda1367ca951a82'
         'c3a8ec014b4fee80304d5adf4cb65666'
         'a78a5b740b79f89d13dd0603dac97483')

package() {
  cd $srcdir/foreman-${pkgver}
  # Not needed in a 'production' server
  rm bundler.d/test.rb
  rm bundler.d/development.rb
  rm bundler.d/console.rb

  # Main codebase
  install -d -m0755 $pkgdir/usr/share/foreman/
  cp -r ./ $pkgdir/usr/share/foreman/

  # Symlink config file to etc
  install -Dp -m0644 config/settings.yaml.example $pkgdir/etc/foreman/settings.yaml
  ln -sn /etc/foreman/settings.yaml $pkgdir/usr/share/foreman/config/settings.yaml
  install -Dp -m0644 config/database.yml.example $pkgdir/etc/foreman/database.yml
  ln -sn /etc/foreman/database.yml $pkgdir/usr/share/foreman/config/database.yml

  # tmp
  install -d -m0755 $pkgdir/usr/share/foreman/tmp

  # logdirs
  install -d -m0755 $pkgdir/var/log/foreman
  rm -rf $pkgdir/usr/share/foreman/log
  ln -sn /var/log/foreman $pkgdir/usr/share/foreman/log
  touch $pkgdir/var/log/foreman/production.log
  chmod 0666 $pkgdir/var/log/foreman/production.log

  # system files
  install -Dm 644 $srcdir/foreman.systemd       $pkgdir/usr/lib/systemd/system/foreman.service
  install -Dm 644 $srcdir/foreman.tmpfiles.conf $pkgdir/usr/lib/tmpfiles.d/foreman.conf
  install -Dm 644 $srcdir/foreman.cron.d        $pkgdir/etc/cron.d/foreman
  install -Dm 644 $srcdir/foreman.logrotate     $pkgdir/etc/logrotate.d/foreman

}

# vim:set ts=2 sw=2 et:
