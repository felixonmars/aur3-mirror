# Maintainer: Kelvin Ng (qpalz) <kelvin9302104 at gmail dot com>

pkgname=mingw-w64-freeglut
pkgver=2.8.1
pkgrel=3
pkgdesc="freeglut allows the user to create and manage windows containing OpenGL contexts (mingw32-w64)"
arch=(any)
url="http://freeglut.sourceforge.net/"
license=('MIT')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!strip' '!buildflags' 'staticlibs')
source=("http://downloads.sourceforge.net/freeglut/freeglut-${pkgver}.tar.gz")
md5sums=('918ffbddcffbac83c218bc52355b6d5a')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
	for _arch in $_architectures; do
		rm -rf "${srcdir}/freeglut-${pkgver}/build-${_arch}"
		mkdir "${srcdir}/freeglut-${pkgver}/build-${_arch}"
		cd "${srcdir}/freeglut-${pkgver}/build-${_arch}"

		../configure \
			--prefix=/usr/${_arch} \
			--host=${_arch} \
			--without-x
		make
	done
}

package() {
	for _arch in $_architectures; do
		cd "${srcdir}/freeglut-${pkgver}/build-${_arch}"

		make DESTDIR="$pkgdir" install

		${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/"*.dll
		${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
	done

	rm "${pkgdir}/usr/i686-w64-mingw32/lib/libglut.a"
}

