# Maintainer: Christoph Wiechert <wio@psitrax.de>
pkgname=sflphone-kde
pkgver=1.2.0
pkgrel=1
pkgdesc="SIP/IAX2 softphone"
url="http://www.sflphone.org"
arch=('x86_64' 'i686')
license=('GPL3')
depends=('kdelibs' 'libpulse' 'gsm>=0.9.7' 'libzrtpcpp' 'log4c' 'check' 'libyaml' 'celt-0.7' 'dbus-c++' 'speex' 'libsamplerate' 'kdepimlibs')
makedepends=('autoconf' 'cmake' 'boost' 'automoc4')
conflicts=('sflphone')
source=("https://projects.savoirfairelinux.com/attachments/download/4306/sflphone-${pkgver}.tar.gz")
md5sums=('79486bfedeecd306a52a17c0ebcdb935')
install="generateStarter.install"
build() {

## pjsip
  cd ${srcdir}/sflphone-${pkgver}/daemon/libs/pjproject
  ./configure --disable-ilbc-codec
  make dep
  make
  
  
## daemon
  cd "${srcdir}/sflphone-${pkgver}/daemon"
  ./autogen.sh
  ./configure --prefix="/usr"
   make
   
## KDE client
  cd "${srcdir}/sflphone-${pkgver}/kde"
  ./config.sh --prefix="/usr"
   cd build
   make  
}

package() {

## daemon
  cd "${srcdir}/sflphone-${pkgver}/daemon"
  make DESTDIR="${pkgdir}" install
  
## KDE client
  cd "${srcdir}/sflphone-${pkgver}/kde/build"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et: