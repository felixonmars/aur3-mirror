# Maintainer: Tai Chi Minh Ralph Eastwood <tcmreastwood@gmail.com>
# Maintainer of physfs package: Eric BÃ©langer <eric@archlinux.org>

pkgname=physfs-hg
pkgver=1301
pkgrel=1
pkgdesc="A library to provide abstract access to various archives"
arch=('i686' 'x86_64')
url="http://icculus.org/physfs/"
license=('ZLIB')
depends=('zlib')
makedepends=('cmake' 'doxygen' 'mercurial')
options=('!makeflags')
source=()
sha1sums=()
provides=(physfs)
replaces=(physfs)
conflicts=(physfs)

_hgroot="http://hg.icculus.org/icculus/physfs"
_hgrepo="physfs"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."
  if [ -d $_hgrepo ]; then
    cd $_hgrepo
    hg pull -u || true
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || true
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"

  cd "${srcdir}/$_hgrepo-build"
  sed -i 's/-Werror//' CMakeLists.txt
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  export CXXFLAGS="$CXXFLAGS -fno-strict-aliasing"
  cmake . -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHYSFS_BUILD_TEST=OFF -DPHYSFS_BUILD_WX_TEST=OFF
  make all docs
}

package() {
  cd "${srcdir}/$_hgrepo-build"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}"/usr/share/{doc/physfs,man/man3}
  install -m644 docs/html/* "${pkgdir}/usr/share/doc/physfs"
  install -m644 docs/man/man3/* "${pkgdir}/usr/share/man/man3"

  for i in author Deinit description extension Free Init major Malloc minor opaque patch Realloc url ; do
    mv "${pkgdir}/usr/share/man/man3/$i.3" "${pkgdir}/usr/share/man/man3/PHYSFS_$i.3"
  done

  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
