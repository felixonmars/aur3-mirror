# Maintainer: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: David Moore <davidm@sjsoft.com>

pkgname=python2-xlwt
pkgver=0.7.4
pkgrel=1
pkgdesc="Library to create spreadsheet files compatible with MS Excel 97/2000/XP/2003 XLS files"
license=('BSD')
arch=(any)
url="http://pypi.python.org/pypi/xlwt"
depends=('python2')
makedepends=('python2-distribute')
# Conflicts until xlwt supports Python 3
conflicts=('python-xlwt')
source=("http://pypi.python.org/packages/source/x/xlwt/xlwt-$pkgver.tar.gz")
md5sums=('231f4ff30894fc70d142b4ed1ba71cc0')

build() {
  cd "$srcdir/xlwt-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/xlwt-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  find $pkgdir -name "*.py" -exec \
    sed -i '1s/python[[:space:]]*$/python2/' {} \;
  install -Dm644 licences.py \
    "$pkgdir/usr/share/licenses/$pkgname/licences.py"
}
