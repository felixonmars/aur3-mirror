# Contributor: David Rosenstrauch <darose@darose.net>
# Contributor: Erik Mank <erik@braindisorder.org>
# Maintainer: Clément Démoulins <clement@archivel.fr>

pkgname=tt-rss
pkgver=1.6.0
pkgrel=2
pkgdesc="Tiny Tiny RSS is an open source web-based news feed (RSS/Atom) aggregator, designed to allow you to read news from any location, while feeling as close to a real desktop application as possible."
arch=('any')
url="http://tt-rss.org/redmine/"
license=('GPL')
depends=('php')
optdepends=('apache' 'mysql' 'postgresql' 'php-curl')
backup=()
install=tt-rss.install

source=(http://tt-rss.org/download/$pkgname-$pkgver.tar.gz tt-rss-updated.rc tt-rss-updated.service)
md5sums=('5e3b389bf4547022b09f1c8b1aca102c'
         '162188e3206f2fe337a9d7090b28246b'
         '5eb8a3bafb1165dadb9489e1389d9979')

package() {
    cd "$srcdir/$pkgname-$pkgver"
    _instdir=${pkgdir}/usr/share/webapps/${pkgname}

    # install tt-rss
    mkdir -p ${_instdir}
    cp -ra * ${_instdir}/
    rm -rf ${_instdir}/debian

    # add a daemon
    install -D -m755 ${srcdir}/tt-rss-updated.rc ${pkgdir}/etc/rc.d/tt-rss-updated
    install -D -m644 ${srcdir}/tt-rss-updated.service ${pkgdir}/usr/lib/systemd/system/tt-rss-updated.service

    # make several directories writable by http user
    chown -R http:root ${_instdir}/{lock,feed-icons,cache}
}
