# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Mario Vazquez <mario_vazq@hotmail.com>
# Contributer: giacomogiorgianni@gmail.common

pkgname=realplayer
pkgver=11.0.2.2315
pkgrel=1
pkgdesc="Real Media Player produced by RealNetworks"
arch=('i686' 'x86_64')
url="http://it.real.com/realplayer/other_versions.html"
license=('custom')
depends=('gtk2' 'libxv' 'alsa-lib' 'pangox-compat')
makedepends=('rpmextract')
 if test "$CARCH" == x86_64; then
  makedepends+=('gcc-multilib' 'binutils-multilib')
 fi
source=(http://client-software.real.com/free/unix/RealPlayer11GOLD.rpm)
md5sums=('768cde61d80f3d60110d8bc92fcb156d')

build() {
    cd $srcdir
    rpmextract.sh RealPlayer11GOLD.rpm

    # copying files
    install -d  $pkgdir/opt/realplayer
    install -d $pkgdir/usr/{bin,share}

    cd opt/real/RealPlayer
    cp -R codecs common plugins share $pkgdir/opt/realplayer

    find $pkgdir/opt/realplayer/share -type f -exec chmod 644 {} +
    find $pkgdir/opt/realplayer -type d -exec chmod 755 {} +

    install -Dm 755 realplay realplay.bin $pkgdir/opt/realplayer
    ln -s /opt/realplayer/realplay $pkgdir/usr/bin/realplay

    # installing icons
    for res in 16 192 32 48; do
        install -Dm 644 $pkgdir/opt/realplayer/share/icons/realplay_${res}x${res}.png \
            $pkgdir/usr/share/icons/hicolor/${res}x${res}/apps/realplay.png
    done

    install -d $pkgdir/usr/share/icons/hicolor/{48x48,192x192}/mimetypes

    for mime in generic ogg ram rpm smil; do
        ln -s /opt/realplayer/share/icons/mime-application-${mime}_48x48.png \
            $pkgdir/usr/share/icons/hicolor/48x48/mimetypes/realplay-application-${mime}.png
        ln -s /opt/realplayer/share/icons/mime-application-${mime}_192x192.png \
            $pkgdir/usr/share/icons/hicolor/192x192/mimetypes/realplay-application-${mime}.png
    done

    for mime in aiff au generic ogg wav; do
        ln -s /opt/realplayer/share/icons/mime-audio-${mime}_48x48.png \
            $pkgdir/usr/share/icons/hicolor/48x48/mimetypes/realplay-audio-${mime}.png
        ln -s /opt/realplayer/share/icons/mime-audio-${mime}_192x192.png \
            $pkgdir/usr/share/icons/hicolor/192x192/mimetypes/realplay-audio-${mime}.png
    done

    ln -s /opt/realplayer/share/icons/mime-text-realtext_48x48.png \
        $pkgdir/usr/share/icons/hicolor/48x48/mimetypes/realplay-text-realtext.png
    ln -s /opt/realplayer/share/icons/mime-text-realtext_192x192.png \
        $pkgdir/usr/share/icons/hicolor/192x192/mimetypes/realplay-text-realtext.png

    for mime in generic ogg; do
        ln -s /opt/realplayer/share/icons/mime-video-${mime}_48x48.png \
            $pkgdir/usr/share/icons/hicolor/48x48/mimetypes/realplay-video-${mime}.png
        ln -s /opt/realplayer/share/icons/mime-video-${mime}_192x192.png \
            $pkgdir/usr/share/icons/hicolor/192x192/mimetypes/realplay-video-${mime}.png
    done

    # setting up locales
    for locale in $(ls share/locale); do
        install -d $pkgdir/usr/share/locale/${locale}/LC_MESSAGES
        ln -s /opt/realplayer/share/locale/${locale}/player.mo \
            $pkgdir/usr/share/locale/${locale}/LC_MESSAGES/realplay.mo
        ln -s /opt/realplayer/share/locale/${locale}/widget.mo \
            $pkgdir/usr/share/locale/${locale}/LC_MESSAGES/widget.mo
    done

    # installing pixmap, .desktop file, etc.
    install -d $pkgdir/usr/share/pixmaps
    ln -s /opt/realplayer/share/realplay.png $pkgdir/usr/share/pixmaps/realplay.png
    install -Dm 644 $pkgdir/opt/realplayer/share/realplay.applications \
        $pkgdir/usr/share/application-registry/realplay.applications
    install -Dm 644 $pkgdir/opt/realplayer/share/realplay.desktop \
        $pkgdir/usr/share/applications/realplay.desktop
    install -Dm 644 $pkgdir/opt/realplayer/share/realplay.keys \
        $pkgdir/usr/share/mime-info/realplay.keys
    install -Dm 644 $pkgdir/opt/realplayer/share/realplay.mime \
        $pkgdir/usr/share/mime-info/realplay.mime
    
    # installing mozilla plugin
    install -d $pkgdir/usr/lib/mozilla/plugins
    install -Dm 755 mozilla/nphelix.so $pkgdir/usr/lib/mozilla/plugins/npreal.so
    install -Dm 755 mozilla/nphelix.xpt $pkgdir/usr/lib/mozilla/plugins/npreal.xpt

    # installing licence
    install -Dm 644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE

    # installing docs for setup assistant
    install -Dm 644 README $pkgdir/opt/realplayer/README
    install -Dm 644 LICENSE $pkgdir/opt/realplayer/LICENSE 
}
