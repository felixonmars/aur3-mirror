# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=buildhosts
pkgname=${_pkgname}-git
pkgver=20140823.r5.8469adc
pkgrel=1
pkgdesc="Build and maintain /etc/hosts while including lists imported from local and remote hosts list sources"
url="https://github.com/prurigro/${_pkgname}"
license=('MIT')
arch=('any')
depends=('bash')
makedepends=('git')
source=("git://github.com/prurigro/${_pkgname}.git#branch=master")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd $_pkgname
    install -Dm755 $_pkgname "${pkgdir}"/usr/bin/${_pkgname}
    install -Dm755 p2p-to-hosts "${pkgdir}"/usr/bin/p2p-to-hosts
    install -Dm644 ${_pkgname}.1 "${pkgdir}"/usr/share/man/man1/${_pkgname}.1
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
