# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=pcmanfm-qt
pkgname=$_pkgname-git
pkgver=0.7.0.2.g86ba94c
pkgrel=1
pkgdesc="The LXQt file manager, Qt port of PCManFM"
arch=("i686" "x86_64" "armv6h")
url="http://lxqt.org"
license=("GPL2")
depends=("liblxqt-git" "libfm" "lxmenu-data")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
install="$_pkgname.install"
source=("git://git.lxde.org/git/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
