#~~~~~~~~~#
#NOTE: before building please download amd-driver-installer-catalyst-13-4-linux-x86.x86_64.zip using this site:
#http://support.amd.com/en-us/download/desktop?os=Linux%20x86_64
# and move it next to this PKGBUILD

# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: B. M. Kwapinski <lord.xml@web.de> (autofglrx daemon idea and most important code)
# Great Contributor: Shen Miren <dickeny@gmail.com> (build_module/hook idea and code)
# Contributor: aidanlinz
# Contributor: Rip-Rip
# Contributor: nob
# Contributor: Krzysztof Kolasa (patches to fix ttys bug)
# Contributor: lano1106 (patch to fix support of intel's iommu and to remove dependency of kernel's CONFIG_AGP option)
# Contributor: Emil Karlson from gentoo (3.10 kernel patch)

pkgname=catalyst-daemon
pkgver=13.4
pkgrel=16
_amdver=12.104
pkgdesc="AMD/ATI drivers. Auto re-compile the fglrx module (while system's start) if kernel was updated. DOES NOT SUPPORT SYSTEMD"
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
options=('staticlibs' 'libtool' '!strip' '!upx')
depends=('catalyst-utils' 'gcc-libs' 'gcc>4.0.0' 'make' 'patch')
conflicts=('catalyst-test' 'catalyst-hook' 'catalyst' 'catalyst-generator' 'catalyst-dkms')
provides=("catalyst=${pkgver}")
install=${pkgname}.install

source=(
    http://www2.ati.com/drivers/linux/amd-driver-installer-catalyst-${pkgver/./-}-linux-x86.x86_64.zip
    autofglrx.sh
    catalyst_build_module
    ati_make.sh
    makefile_compat.patch
    gentoo_linux-3.10-proc.diff
    kolasa_fglrx_fixing_ttys.patch
    lano1106_fglrx_intel_iommu.patch
    lano1106_kcl_agp_13_4.patch)

md5sums=('81eac924b950223ebf220bff1805b7a8'
         'e35dd2ffe748bb976cacab1846a02efd'
         'ec268cc09c49bc927fba7375209a0539'
	 'f9866a8e58665f9a91251e3f8e7edc72'
	 '3e1b82bd69774ea808da69c983d6a43b'
	 '5872d95907a93ada44982e355e91e59d'
	 '5f28dfc4c9fdb0fbfed69c8cb38659dd'
	 '5184b94a2a40216a67996999481dd9ee'
	 'c5156eddf81c8a1719b160d05a2e8d67')


build() {
  ## Unpack archive
     /bin/sh ./amd-catalyst-13.4-linux-x86.x86_64.run --extract archive_files
}

package() {
    # License
      install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
      install -m644 ${srcdir}/archive_files/LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}

      cd ${srcdir}/archive_files

      patch -Np1 -i ../makefile_compat.patch
      patch -Np0 -i ../gentoo_linux-3.10-proc.diff
      patch -Np1 -i ../kolasa_fglrx_fixing_ttys.patch
      patch -Np1 -i ../lano1106_fglrx_intel_iommu.patch
      patch -Np1 -i ../lano1106_kcl_agp_13_4.patch

    # Prepare modules source files
      _archdir=x86_64
      test "${CARCH}" = "i686" && _archdir=x86
      install -m755 -d ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/*.c \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/*.h \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 common/lib/modules/fglrx/build_mod/2.6.x/Makefile \
                ${pkgdir}/usr/share/ati/build_mod
      install -m644 arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a \
                ${pkgdir}/usr/share/ati/build_mod
      install -m755 -d ${pkgdir}/usr/bin
      install -m755 ${srcdir}/catalyst_build_module ${pkgdir}/usr/bin

    # modified ati's make.sh script
      install -m755 ${srcdir}/ati_make.sh ${pkgdir}/usr/share/ati/build_mod

    # fglrx daemon
      install -m755 -d ${pkgdir}/etc/rc.d
      install -m755 ${srcdir}/autofglrx.sh ${pkgdir}/etc/rc.d/autofglrx
}