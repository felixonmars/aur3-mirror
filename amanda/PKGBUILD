# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=amanda
pkgver=3.3.1
pkgrel=2
pkgdesc="Advanced Maryland Automatic Network Disk Archiver"
arch=('i686' 'x86_64')
license=('custom')
depends=('perl>=5.6.0' 'glib2>=2.2.0' 'gawk' 'gnuplot' 'tar>=1.12' 'xinetd')
optdepends=('dump: for creating backups in dump format' 'samba: for backing up Windows hosts')
install="$pkgname.install"
url="http://www.amanda.org"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz" "xinetd.$pkgname")
md5sums=('dae7631b4abcf7eac874df6e3740e75b'
         '910c9823073148c576b14d4a71bc6458')

build() {
  # the amanda user and group are required to build
  if [ -z `getent passwd amanda` ] ; then
    msg "Adding user/group amanda (temporarily)"
    groupadd -g 112 amanda
    useradd -u 112 -g amanda -G storage,disk -m -d /var/amanda -s /bin/bash -c "Amanda Backup Daemon"  amanda
    cleanup=1
  else
    cleanup=0
  fi
  
  cd $srcdir/$pkgname-$pkgver
  
	./configure --prefix=/usr \
    --with-configdir=/etc/amanda \
    --with-gnutar-listdir=/var/amanda/gnutar-lists \
    --mandir=/usr/share/man \
		--with-user=amanda \
    --with-group=amanda \
    --without-ipv6 \
    --with-ssh-security \
    --htmldir=/srv/http/docs/$pkgname

	make
	
  if [ $cleanup -eq 1 ]; then
    msg "Removing user/group amanda"
    userdel -r amanda
  fi
}

package() {
  _amandauser='amanda'
  _devgroup='disk'
  _amandahosts='var/amanda/.amandahosts'
  _amandadates='var/amanda/amandates'
  _amandadumpdates='var/amanda/dumpdates'
  _amandagnutarlists='var/amanda/gnutar-lists'
  
  # the amanda user and group are required to package
  if [ -z `getent passwd amanda` ]; then
    msg "Adding user/group amanda (temporarily)"
    groupadd -g 112 amanda
    useradd -u 112 -g amanda -G storage,disk -m -d /var/amanda -s /bin/bash -c "Amanda Backup Daemon"  amanda
    cleanup=1
  else
    cleanup=0
  fi
  
  cd $srcdir/$pkgname-$pkgver
  
  # Install the compiled output
  make DESTDIR=$pkgdir install
  
  # Install configuration examples
  install -dm755 $pkgdir/etc/$pkgname/
  cp -r example $pkgdir/etc/$pkgname/
  
  # Install xinetd configuration
  install -dm755 $pkgdir/etc/xinetd.d/
  cp $srcdir/xinetd.$pkgname $pkgdir/etc/xinetd.d/$pkgname
  
  # Create some files
  install -dm755 $pkgdir/var/$pkgname/
  chmod 700 $pkgdir/var/$pkgname/
  chown -R $_amandauser:$_devgroup $pkgdir/var/$pkgname

  touch $pkgdir/$_amandahosts
  chown $_amandauser:$_devgroup $pkgdir/$_amandahosts
  chmod 600 $pkgdir/$_amandahosts
  cat > $pkgdir/var/$pkgname/.amandahosts << EOT
# This is where amanda works out what remote connections to allow in the format
# of <host> <username> <command>
#
# server1.example.com   amanda  amdump
EOT
  
  touch $pkgdir/$_amandadates
  chown $_amandauser:$_devgroup $pkgdir/$_amandadates
  
  touch $pkgdir/$_amandadumpdates
  chown $_amandauser:$_devgroup $pkgdir/$_amandadumpdates
  chmod 664 $pkgdir/$_amandadumpdates

  install -dm755 $pkgdir/$_amandagnutarlists
  chown -R $_amandauser:$_devgroup $pkgdir/$_amandagnutarlists

  # Fix permissions
  chown -R $_amandauser:$_devgroup $pkgdir/etc/$pkgname/
  
  # Install the licence
  install -Dm444 COPYRIGHT $pkgdir/usr/share/licences/$pkgname/COPYRIGHT

  # Cleanup
  rm -Rf $pkgdir/usr/share/amanda/example/
  if [ $cleanup -eq 1 ]; then
    msg "Removing user/group amanda"
    userdel -r amanda
  fi
}

# vim:set ts=2 sw=2 et:
