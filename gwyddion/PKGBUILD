# on the base of Gentoo gwyddion ebuild
# http://packages.gentoo.org/package/sci-visualization/gwyddion

pkgname=gwyddion
pkgver=2.29
pkgrel=1
pkgdesc="A modular program for SPM (scanning probe microscopy) and other 2D (height field) data visualization and analysis"
url="http://gwyddion.net/"
license=("GPL")
arch=('i686' 'x86_64')
depends=('gtk2>=2.8' 'pango' 'cairo' 'gtkglext' 'desktop-file-utils' 'python2' 'pygtk'
         'hicolor-icon-theme' 'openexr' 'fftw' 'libunique')
#depends=('freeglut' 'gtksourceview')
makedepends=('pkgconfig>=0.16')
optdepends=('libxmu: for alternative "remote control" backend'
            'gconf: better GNOME and XFce integration (SPM file thumbnails)'
            'libxml2: SPML file support'
            'zlib: Matlab MAT5 file support'
            'perl: development of plug-in'
            'ruby: development of plug-in'
            'fpc: development of plug-in'
            'gtksourceview2: Pygwy console syntax highlighting')
install=gwyddion.install
source=(http://downloads.sourceforge.net/sourceforge/gwyddion/$pkgname-$pkgver.tar.xz
        gwyddion-fpc-2.6.0.patch)
sha256sums=('d7dd3b5bfb254cf158cfed80ebd2495fb1142f8189594c7d6f8f6a9944e5c64b'
            '1407d9d73f477b49f4977425109c17732ab2da94490d8d285cbda3a5af34c790')


build() {
  cd "${srcdir}/$pkgname-$pkgver"

#  ./configure --prefix=/usr --disable-desktop-file-update \
#		--disable-rpath \
#		--enable-library-bloat \
#		--enable-plugin-proxy \

  # python2 fix
  for file in $(find . -name '*.py' -print); do
      sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
      sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  patch -Np2 -i "${srcdir}/gwyddion-fpc-2.6.0.patch"

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --libexecdir=/usr/lib PYTHON=python2
  make PYTHON=python2
}


package() {
  cd "$srcdir"/$pkgname-$pkgver

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" PYTHON=python2 install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"

  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gwyddion ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}



