#Maintainer: kfgz <kfgz at interia pl>

pkgname=vche
pkgver=1.7.2
pkgrel=2
pkgdesc="A hex editor which lets you view/edit hard drives, CDROMs, RAM, and everything that can be read."
arch=('i686' 'x86_64')
url="http://www.grigna.com/diego/linux/vche/"
license=('GPL')
depends=('ncurses')
makedepends=('gcc')
source=(http://www.grigna.com/diego/linux/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('d2a21986a4d4a23820ff4862e96d061d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make
}

package() {
  install -dm755 ${pkgdir}/usr/{bin,share/man/{man1,man5}}
  install -dm755 ${pkgdir}/etc
  cd ${srcdir}/${pkgname}-${pkgver}/src
  
  # binaries
  install -m755 vche* ${pkgdir}/usr/bin
  
  # manpages
  cd ${srcdir}/${pkgname}-${pkgver}/doc
  sed "s/@version@/$pkgver/g;s/@releasedate@/November 1999/g;s%@libdir@%/etc%g;s%@configfile@%vche.conf%g" \
      vche.1.in > ${pkgdir}/usr/share/man/man1/vche.1
  sed "s/@version@/$pkgver/g;s/@releasedate@/November 1999/g;s%@libdir@%/etc%g;s%@configfile@%vche.conf%g" \
      vche.conf.5.in > ${pkgdir}/usr/share/man/man5/vche.conf.5
      
  # config
  install -m644 vche.conf /${pkgdir}/etc
}
