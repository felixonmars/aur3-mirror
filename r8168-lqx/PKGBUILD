# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com> and Perfect Gentleman <Perfect007Gentleman@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-lqx
_realname=r8168
pkgver=8.037.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards (for linux-lqx kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-lqx>=3.12' 'linux-lqx<3.13')
makedepends=('linux-headers')
source=(http://r8168.googlecode.com/files/$_realname-$pkgver.tar.bz2)
install=$_realname.install

_extramodules=extramodules-3.12-lqx
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
   cd "$srcdir"
   cd "$_realname-$pkgver"
   make -C /usr/lib/modules/$_kernver/build \
      SUBDIRS="$srcdir/$_realname-$pkgver/src" \
      EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
   cd "$srcdir/$_realname-$pkgver/src"
   install -D -m644 $_realname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_realname.ko"
   find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

sha256sums=('06aa1c6a58a878450798100eb5f9c3d87763c5f41e99b7d5f3b2fe78646d02b0')

