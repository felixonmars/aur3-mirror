# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [testing]'s xorg-server

pkgname=('xorg-server-dev' 'xorg-server-common-dev' 'xorg-server-devel-dev' 'xorg-server-xdmx-dev'
         'xorg-server-xephyr-dev' 'xorg-server-xvfb-dev' 'xorg-server-xnest-dev')
pkgver=1.15.99.904 # 1.16 RC4: http://lists.x.org/archives/xorg/2014-July/056734.html
pkgrel=1
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
makedepends=('bigreqsproto' 'compositeproto' 'dmxproto' 'dri2proto' 'dri3proto'
             'fontsproto>=2.1.3' 'glproto' 'inputproto' 'libdmx' 'libgcrypt' 'libgl'
             'libpciaccess' 'libx11' 'libxaw' 'libxfont' 'libxi' 'libxkbfile'
             'libxmu' 'libxrender' 'libxres' 'libxshmfence' 'libxtst' 'libxv'
             'mesa' 'pixman' 'presentproto' 'randrproto' 'recordproto' 'renderproto'
             'resourceproto' 'scrnsaverproto' 'videoproto' 'xcb-util' 'xcb-util-image'
             'xcb-util-keysyms' 'xcb-util-wm' 'xcmiscproto' 'xf86dgaproto'
             'xf86driproto' 'xineramaproto' 'xorg-font-util' 'xorg-util-macros'
             'xorg-xkbcomp' 'xproto>=7.0.26' 'xtrans') 
source=("${url}/releases/individual/xserver/xorg-server-${pkgver}.tar.bz2"
        '10-quirks.conf'
        'autoconfig-nvidia.patch'
        'autoconfig-sis.patch'
        'CVE-2013-6424.diff'
        'xvfb-run'
        'xvfb-run.1')
sha256sums=('3bff7df25363d53d730b353bc19d8fc5fd183b8875e35ac26c6466a542a64b6f'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166'
            '643b9f701e11af7ba7562fe00399f6197a0ddceea6d763ada6cf2c7d023f0acc'
            'e7db0b6736c4bd25868e5bc31a71d205acc5ed93d36acc6939cb0bdf9e083c83'
            '89948993afd4d9aec6a0bdbaf62f494e9956bb77a0386307d497f340a818ff61'
            'ff0156309470fc1d378fd2e104338020a884295e285972cc88e250e031cc35b9'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776')

prepare() {
  cd xorg-server-${pkgver}

  msg2 "Use nouveau/nv/nvidia drivers for Nvidia devices"
  patch -p1 -i ../autoconfig-nvidia.patch

  msg2 "Use unofficial imedia SiS driver for supported SiS devices"
  patch -p0 -i ../autoconfig-sis.patch

  msg2 "Fix CVE-2013-6424: https://bugs.archlinux.org/task/38401"
  patch -p1 -i ../CVE-2013-6424.diff
}

build() {
  cd xorg-server-${pkgver}

  msg2 "Starting autoreconf..."
  autoreconf -fi

  msg2 "Starting ./configure..."
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts \
      --enable-glamor
#      --without-dtrace \
#      --disable-linux-acpi \
#      --disable-linux-apm \

  msg2 "Starting make..."
  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-dev() {
  pkgdesc="Xorg X server - Bleeding edge version"
  groups=('xorg')
  depends=('libdrm' 'libgcrypt' 'libpciaccess' 'libxau' 'libxdmcp' 'libxfont' 'libxshmfence'
           'pixman' 'xf86-input-evdev' 'xorg-server-common-dev' 'libepoxy')
  # For ABI versions, see (major ones provided for drivers):
  #  - 'src/xorg-server-*/hw/xfree86/common/xf86Module.h'
  #  - 'pkg/xorg-server-devel/usr/lib/pkgconfig/xorg-server.pc'
  provides=('X-ABI-VIDEODRV_VERSION=18' 'X-ABI-XINPUT_VERSION=21' 'X-ABI-EXTENSION_VERSION=8.0'
            'x-server' 'xorg-server' 'glamor-egl')
  conflicts=('nvidia-utils<=331.20' 'xorg-server' 'glamor-egl' 'xwayland')
  backup=('etc/X11/xorg.conf.d/10-evdev.conf'
          'etc/X11/xorg.conf.d/10-quirks.conf')
  cd xorg-server-${pkgver}

  msg2 "Starting make install..."
  make DESTDIR="${pkgdir}" install

  # Confs
  install -Dm644 "${srcdir}"/10-quirks.conf \
                 "${pkgdir}"/etc/X11/xorg.conf.d/10-quirks.conf
  mv "${pkgdir}"/usr/share/X11/xorg.conf.d/* "${pkgdir}"/etc/X11/xorg.conf.d/

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}"/usr/lib/xorg/modules/extensions/libglx.so \
     "${pkgdir}"/usr/lib/xorg/modules/extensions/libglx.xorg

  # License
  install -d "${pkgdir}"/usr/share/licenses/xorg-server/
  ln -sf ../xorg-server-common/COPYING \
         "${pkgdir}"/usr/share/licenses/xorg-server/COPYING

  # Remove redundancies
  rm -r "${pkgdir}"/usr/include/
  rm -r "${pkgdir}"/usr/lib/pkgconfig/
  rm    "${pkgdir}"/usr/lib/xorg/protocol.txt
  rm -r "${pkgdir}"/usr/share/aclocal/
  rm    "${pkgdir}"/usr/share/man/man1/Xserver.1
  rm -r "${pkgdir}"/usr/share/X11/
  rm -r "${pkgdir}"/var/
}

package_xorg-server-common-dev() {
  pkgdesc="Xorg server common files - Bleeding edge version"
  depends=('xkeyboard-config' 'xorg-fonts-misc' 'xorg-setxkbmap' 'xorg-xkbcomp')
  provides=('xorg-server-common')
  conflicts=('xorg-server-common')
  cd xorg-server-${pkgver}
  
  msg2 "Starting make install-data..."
  make -C xkb DESTDIR="${pkgdir}" install-data

  # Man
  install -Dm644 man/Xserver.1 "${pkgdir}"/usr/share/man/man1/Xserver.1

  # Registry of protocol names
  install -Dm644 dix/protocol.txt "${pkgdir}"/usr/lib/xorg/protocol.txt

  # License
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/xorg-server-common/COPYING
}

package_xorg-server-devel-dev() {
  pkgdesc="Development files for the X.Org X server - Bleeding edge version"
  # - presentproto/resourceproto/scrnsaverproto: see; (FS#32146) https://bugs.archlinux.org/task/32146
  # - xorg-util-macros: not technically required but almost every Xorg pkg needs it to build
  depends=('dri2proto' 'dri3proto' 'fontsproto>=2.1.3' 'glproto' 'inputproto' 'kbproto'
           'libpciaccess' 'mesa' 'pixman' 'presentproto' 'randrproto' 'renderproto'
           'resourceproto' 'scrnsaverproto' 'videoproto' 'xextproto' 'xf86driproto'
           'xineramaproto' 'xorg-util-macros' 'xproto>=7.0.26')
  provides=('xorg-server-devel')
  conflicts=('xorg-server-devel' 'glamor-egl')
  cd xorg-server-${pkgver}

  msg2 "Starting make install..."
  make DESTDIR="${pkgdir}" install

  # License
  install -d "${pkgdir}"/usr/share/licenses/xorg-server-devel/
  ln -sf ../xorg-server-common/COPYING \
         "${pkgdir}"/usr/share/licenses/xorg-server-devel/COPYING

  # Remove redundancies
  rm -r "${pkgdir}"/usr/bin/
  rm -r "${pkgdir}"/usr/lib/xorg/
  #rm -r "${pkgdir}"/usr/share/doc/
  rm -r "${pkgdir}"/usr/share/man/
  rm -r "${pkgdir}"/usr/share/X11/
  rm -r "${pkgdir}"/var/
}

package_xorg-server-xdmx-dev() {
  pkgdesc="Distributed Multihead X Server and utilities - Bleeding edge version"
  depends=('libdmx' 'libgcrypt' 'libxaw' 'libxfixes' 'libxfont' 'libxi' 'libxrender'
           'pixman' 'xorg-server-common-dev')
  provides=('xorg-server-xdmx')
  conflicts=('xorg-server-xdmx')
  cd xorg-server-${pkgver}/hw/dmx

  msg2 "Starting make install..."
  make DESTDIR="${pkgdir}" install

  # License
  install -d "${pkgdir}"/usr/share/licenses/xorg-server-xdmx/
  ln -sf ../xorg-server-common/COPYING \
         "${pkgdir}"/usr/share/licenses/xorg-server-xdmx/COPYING
}

package_xorg-server-xephyr-dev() {
  pkgdesc="A nested X server that runs as an X application - Bleeding edge version"
  depends=('libgcrypt' 'libgl' 'libxfont' 'libxv' 'pixman' 'xcb-util-image'
           'xcb-util-keysyms' 'xcb-util-wm' 'xorg-server-common-dev')
  provides=('xorg-server-xephyr')
  conflicts=('xorg-server-xephyr')
  cd xorg-server-${pkgver}/hw/kdrive

  msg2 "Starting make install..."
  make DESTDIR="${pkgdir}" install

  # License
  install -d "${pkgdir}"/usr/share/licenses/xorg-server-xephyr/
  ln -sf ../xorg-server-common/COPYING \
         "${pkgdir}"/usr/share/licenses/xorg-server-xephyr/COPYING
}

package_xorg-server-xnest-dev() {
  pkgdesc="A nested X server that runs as an X application - Bleeding edge version"
  depends=('libgcrypt' 'libxext' 'libxfont' 'pixman' 'xorg-server-common-dev')
  provides=('xorg-server-xnest')
  conflicts=('xorg-server-xnest')
  cd xorg-server-${pkgver}/hw/xnest

  msg2 "Starting make install..."
  make DESTDIR="${pkgdir}" install

  # License
  install -d "${pkgdir}"/usr/share/licenses/xorg-server-xnest/
  ln -sf ../xorg-server-common/COPYING \
         "${pkgdir}"/usr/share/licenses/xorg-server-xnest/COPYING
}

package_xorg-server-xvfb-dev() {
  pkgdesc="Virtual framebuffer X server - Bleeding edge version"
  depends=('libgcrypt' 'libxau' 'libxdmcp' 'libxfont' 'pixman' 'xorg-server-common-dev')
  provides=('xorg-server-xvfb')
  conflicts=('xorg-server-xvfb')
  cd xorg-server-${pkgver}/hw/vfb

  msg2 "Starting make install..."
  make DESTDIR="${pkgdir}" install

  # Xvfb-run
  install -m755 "${srcdir}"/xvfb-run "${pkgdir}"/usr/bin/
  install -m644 "${srcdir}"/xvfb-run.1 "${pkgdir}"/usr/share/man/man1/

  # License
  install -d "${pkgdir}"/usr/share/licenses/xorg-server-xvfb/
  ln -sf ../xorg-server-common/COPYING \
         "${pkgdir}"/usr/share/licenses/xorg-server-xvfb/COPYING
}
