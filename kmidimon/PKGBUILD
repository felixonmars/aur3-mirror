# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Johannes Schriewer <jschriewer@arcor.de>

pkgname=kmidimon
pkgver=0.7.3
pkgrel=2
pkgdesc="KDE MIDI Metronome using the ALSA Sequencer"
arch=('i686' 'x86_64')
url="http://kmidimon.sourceforge.net/"
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('552d84352dc458704bbfc8854d8c598e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # doc build errors prevent success
  sed -i '/doc/d' CMakeLists.txt

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
