# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libquvi-scripts-git
pkgver=0.9.20130903
pkgrel=2
pkgdesc="Embedded lua scripts for libquvi. (GIT version)"
arch=('any')
url="http://quvi.sourceforge.net/"
license=('LGPL')
depends=('curl' 'lua' 'lua52-socket')
makedepends=('git' 'asciidoc')
optdepends=('lua-expat: subtitles')
options=('!libtool')
provides=('libquvi-scripts')
conflicts=('libquvi-scripts')
source=('git://repo.or.cz/libquvi-scripts.git')
sha256sums=('SKIP')

pkgver() {
  cd 'libquvi-scripts'
  git describe | sed 's/^v//;s/-/+/g'
}

build() {
  cd 'libquvi-scripts'

  ./bootstrap.sh

  ./configure \
    --prefix=/usr \
    --with-nlfy \
    --with-nsfw \
    --with-geoblocked

  make
}

package() {
  cd 'libquvi-scripts'

  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh sw=2 ts=2 et
