# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Arch Haskell Team

pkgname=haskell-snap-server
_hkgname=snap-server
_licensefile=LICENSE
pkgver=0.9.0
pkgrel=1
pkgdesc="A fast, iteratee-based, epoll-enabled web server for the Snap Framework"
url="http://snapframework.com/"
license=('BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-array'
         'haskell-attoparsec'
         'haskell-attoparsec-enumerator'
         'haskell-binary'
         'haskell-blaze-builder'
         'haskell-blaze-builder-enumerator'
         'haskell-bytestring'
         'haskell-bytestring-nums'
         'haskell-case-insensitive'
         'haskell-containers'
         'haskell-directory-tree'
         'haskell-enumerator'
         'haskell-filepath'
         'haskell-monadcatchio-transformers'
         'haskell-mtl'
         'haskell-murmur-hash'
         'haskell-network'
         'haskell-old-locale'
         'haskell-snap-core'
         'haskell-template-haskell'
         'haskell-text'
         'haskell-time'
         'haskell-transformers'
         'haskell-unix-compat'
         'haskell-vector'
         'haskell-vector-algorithms')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('6a0f085da3741c1e0529f31d7d5d1b670b360fb7ddeacbd648dcea6778f0b28a')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}
