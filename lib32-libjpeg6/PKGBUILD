# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=lib32-libjpeg6
pkgver=6b
pkgrel=4
pkgdesc="An older version for a Library of JPEG support functions (32 bit)"
arch=('x86_64')
url="http://www.ijg.org/"
license=('custom')
depends=('lib32-glibc' 'libjpeg6')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/libjpeg/libjpeg/${pkgver}/jpegsrc.v${pkgver}.tar.gz"
        'locale-fixes.patch')
md5sums=('dbd5f3b47ed13132f04c685d608a7547'
         'a0910de4ba7bb7526032d86bb0297de4')

build() {
  cd $srcdir/jpeg-$pkgver
  
  cp /usr/share/libtool/config/config.{guess,sub} . # Fix "unknown system"
  sed -i "s#./libtool#libtool#" configure
  patch -p1 -i "$srcdir/locale-fixes.patch"
  ./configure --prefix=/usr --enable-shared --enable-static \
                            CC='gcc -m32' --libdir=/usr/lib32
  make LIBTOOL="libtool --tag=CC"
}

check() {
  cd $srcdir/jpeg-$pkgver
  make check
}

package() {
  cd ${srcdir}/jpeg-${pkgver}
  
  mkdir -p $pkgdir/usr/{bin,lib32,include,share/man/man1}    
  make prefix=$pkgdir/usr \
  	mandir=$pkgdir/usr/share/man/man1 \
  	libdir=${pkgdir}/usr/lib32 \
  	install
  rm $pkgdir/usr/lib32/libjpeg.{a,so} # Avoid conflict with lib32-libjpeg-turbo
  rm -rf ${pkgdir}/usr/{bin,include,share}
  install -Dm644 README $pkgdir/usr/share/licenses/$pkgname/README
}
