# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=gitorious
pkgver=2.2.1
pkgrel=2
pkgdesc="Gitorious aims to provide a great way of doing distributed opensource code collaboration."
arch=(i686 x86_64)
url="http://gitorious.org/gitorious"
license=('AGPLv3')

depends=('sphinx' 'mysql' 'ruby-enterprise' 'ruby-enterprise-raspell' 'ruby-enterprise-bundler' 'ruby-enterprise-stompserver' 'imagemagick' 'nodejs-buster' 'libxslt')
makedepends=('git')
optdepends=('memcached: High performance cache')
options=('!strip')

install=gitorious.install
backup=(etc/webapps/gitorious/broker.yml etc/webapps/gitorious/database.yml etc/webapps/gitorious/gitorious.yml)

source=(gitorious-daemon.rc.d gitorious-poller.rc.d gitorious-ultrasphinx.rc.d gitorious-resque-worker.rc.d database.yml gitorious.yml sphinx-cmd.patch)

_gitroot="git://gitorious.org/gitorious/mainline.git"

build() {
	cd "$srcdir"

	# trick systemtimer build system into thinking we have gcc-4.6
	rm -rf gcc46-pathfix
	mkdir gcc46-pathfix
	ln -s /usr/bin/gcc gcc46-pathfix/gcc-4.6
	export PATH="$PATH:$srcdir/gcc46-pathfix"

	if [ ! -d "$srcdir/$pkgname-$pkgver" ]; then
		msg "Connecting to GIT server...."
		git clone $_gitroot $pkgname-$pkgver
		cd $pkgname-$pkgver
		git checkout v$pkgver
		git submodule update --init
	fi

	msg "GIT checkout done or server timeout"

	rm -rf "$srcdir/$pkgname-build"
	cp -r "$srcdir/$pkgname-$pkgver" "$srcdir/$pkgname-build"
	cd "$srcdir/$pkgname-build/"
	rm -rf .git .gitmodules
	find . -type f -name .gitignore -delete

	msg "Fetching bundled gems..."
	/opt/ruby-enterprise/bin/bundle install --deployment --binstubs
	rm -rf vendor/bundle/ruby/*/cache

	msg "Patching application..."
	# Activate production mode
	sed -i "s|^# ENV\['RAILS_ENV'\]|ENV['RAILS_ENV']|" config/environment.rb

	patch -p1 -i "$srcdir/sphinx-cmd.patch"

	find bin script data/hooks -type f -exec sed -i 's|^#!/usr/bin/env ruby|#!/opt/ruby-enterprise/bin/ruby|' {} \;
}

package() {
	cd "$srcdir"

	_gitorious="/usr/share/webapps/gitorious"
	_etc="$pkgdir/etc/webapps/gitorious"
	install -d "$pkgdir/usr/share/webapps"
	install -d "$_etc"

	mv "$pkgname-build" "${pkgdir}${_gitorious}"

	install -D -m0644 "$srcdir/database.yml" "$_etc/database.yml"
	install -D -m0644 "$srcdir/gitorious.yml" "$_etc/gitorious.yml"
	install -D -m0644 "${pkgdir}${_gitorious}/config/broker.yml.example" "$_etc/broker.yml"
	install -D -m0644 "${pkgdir}${_gitorious}/vendor/plugins/ultrasphinx/examples/ap.multi" "$pkgdir/usr/lib/aspell/ap.multi"

	ln -s "/etc/webapps/gitorious/database.yml" "${pkgdir}${_gitorious}/config/"
	ln -s "/etc/webapps/gitorious/gitorious.yml" "${pkgdir}${_gitorious}/config/"
	ln -s "/etc/webapps/gitorious/broker.yml" "${pkgdir}${_gitorious}/config/"

	install -d "$pkgdir/usr/bin"
	ln -s "${_gitorious}/script/gitorious" "$pkgdir/usr/bin/gitorious"

	# capillary
	ln -s /usr/lib/node_modules/buster/node_modules/ "${pkgdir}${_gitorious}/public/javascripts/lib/capillary/node_modules"

	install -D -m0755 "$srcdir/gitorious-daemon.rc.d" "$pkgdir/etc/rc.d/gitorious-daemon"
	install -D -m0755 "$srcdir/gitorious-poller.rc.d" "$pkgdir/etc/rc.d/gitorious-poller"
	install -D -m0755 "$srcdir/gitorious-resque-worker.rc.d" "$pkgdir/etc/rc.d/gitorious-resque-worker"
	install -D -m0755 "$srcdir/gitorious-ultrasphinx.rc.d" "$pkgdir/etc/rc.d/gitorious-ultrasphinx"
}

md5sums=('9d3a4476db211d0dd3a47368d21ea63a'
         'a5dca286ee2023fbd63ffda5c327bd85'
         '9e759e6b3e34bbd0410c7d976bc1bc02'
         '7693608913eb9d5f9cdec5459f528fd9'
         '6a6efd4a0c9deaaefe841c57176a1bee'
         'bd6987bada8e7067a2a7f3727e93dd61'
         'f46e84a467b492a6d5de936e1595e028')
