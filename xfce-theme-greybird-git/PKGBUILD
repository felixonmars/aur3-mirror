# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=xfce-theme-greybird-git
_pkgname=xfce-theme-greybird
pkgver=20120504
pkgrel=1
pkgdesc="Desktop suite for Xfce"
arch=('any')
url="https://github.com/shimmerproject/Greybird"
license=('GPL2' 'CCPL')
depends=('gtk-engine-murrine' 'gtk-engine-unico')
makedepends=('git')
optdepends=('elementary-xfce-icons: matching icon set'
            'emerald: emerald theme'
            'lightdm-gtk-greeter: lightdm theme')
provides=($_pkgname)
conflicts=($_pkgname)

_gitroot="git://github.com/shimmerproject/Greybird.git"
_gitname="Greybird"

package() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname" -b gtk3.3
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # install wallpaper
  install -d -m 755 "$pkgdir"/usr/share/backgrounds/xfce
  install -m 644 backdrops/greybird-wall-1920x1200.png \
    "$pkgdir"/usr/share/backgrounds/xfce/greybird.png

  # install emerald theme
  install -d -m 755 "$pkgdir"/usr/share/emerald/themes/Greybird
  tar zxf Greybird.emerald -C "$pkgdir"/usr/share/emerald/themes/Greybird/

  # create theme dirs
  install -d -m 755 "$pkgdir"/usr/share/themes/Greybird{,-compact/xfwm4,-lightdm/gtk-3.0}

  # install compact theme
  install -m 644 xfwm4_compact/* "$pkgdir"/usr/share/themes/Greybird-compact/xfwm4/

  # install lightdm theme
  install -m 644 lightdm/* "$pkgdir"/usr/share/themes/Greybird-lightdm/gtk-3.0/

  # clean up
  rm -rf {.git,.gitignore,backdrops,Greybird.emerald,LICENSE.{CC,GPL},lightdm,README,xfwm4_compact}

  # install theme
  cp -r . "$pkgdir"/usr/share/themes/Greybird/

  # fix permission and ownership
  find "$pkgdir"/usr/share/emerald/themes/Greybird -type f -exec chmod 644 {} \;
  chown -R root:root "$pkgdir"/usr/share/emerald/themes/Greybird
}

# vim:set ts=2 sw=2 et:
