# Contributor: icarus <lysergic2 at comcast.net>
pkgname=freediag
pkgver=1.04
pkgrel=1
pkgdesc="Freediag is a suite of vehicle diagnostic protocols and an OBD II (mostly) compliant ScanTool."
arch=('i686' 'x86_64')
url="http://freediag.sourceforge.net/"
license=('GNU')
makedepends=('cmake')
depends=('fltk')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/1.0/$pkgname-$pkgver-src.tar.gz"
         0001-cmake-force-HAVE_TIMERSUB.patch
         0002-cmake-fix-add_library-libdiag-libdyno-producing-libl.patch
         0003-cmake-link-libdiag-to-math-symbols-on-unix.patch
         0004-cmake-fix-missing-scantool_diag.c.patch  0005-cmake-build-scangui.patch)
md5sums=('cb6e73dab9f87fbc50ff180d97e950f3'
         '81fa610fabc7a4df6d7b18a65bc8aeef'
         '233c3c9c7bac6e419721f2c447b1d017'
         'b14c40cb4f08258c3b2ba3b92024eb25'
         'ba7363027af980ef77b77d794bcb7f36'
         '4f996495d6edf87f6332d38b81a1f011')

prepare () {
  cd "$pkgname-$pkgver-src"
  patch -p1 -i "${srcdir}"/0001-cmake-force-HAVE_TIMERSUB.patch
  patch -p1 -i "${srcdir}"/0002-cmake-fix-add_library-libdiag-libdyno-producing-libl.patch
  patch -p1 -i "${srcdir}"/0003-cmake-link-libdiag-to-math-symbols-on-unix.patch
  patch -p1 -i "${srcdir}"/0004-cmake-fix-missing-scantool_diag.c.patch
  rm -f scangui/CMakeLists.txt
  patch -p1 -i "${srcdir}"/0005-cmake-build-scangui.patch
}

build() {
  cd "$pkgname-$pkgver-src"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DBUILDGUI=ON 
  make
}


package () {
  cd "$pkgname-$pkgver-src"
  make DESTDIR="$pkgdir" install
  
  install -d "$pkgdir"/usr/share/doc/freediag
  cp OLD_CHANGES "$pkgdir"/usr/share/doc/freediag/CHANGES
  cp README "$pkgdir"/usr/share/doc/freediag
  cp -R doc/* "$pkgdir"/usr/share/doc/freediag
}

