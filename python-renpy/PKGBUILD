# Maintainer: AlexanderR <rvacheva at nxt dot ru>
# Contributor: zhn <zhangn1985 AT gmail com>
pkgname=python-renpy
pkgver=6.15.5
pkgrel=1
pkgdesc="Platform-dependant Ren'Py libraries."
arch=('i686' 'x86_64')
license=('MIT')
url='http://www.renpy.org'
depends=('fribidi' 'glew' 'python2-pygame>=1.9.1')
conflicts=("renpy<$pkgver" 'renpy64' 'renpy-bin')
# python-argparse is part of distribution since 2.7
makedepends=('python2>=2.7')
changelog=ChangeLog
source=("http://www.renpy.org/dl/$pkgver/renpy-${pkgver}-source.tar.bz2")

md5sums=('45e7c28a426c8d9ef470d09e7a689568')

export CFLAGS="$CFLAGS -Os"

build() {
  cd "$srcdir"/renpy-${pkgver}-source

  python2 module/setup.py build
}

package(){
  cd "$srcdir"/renpy-${pkgver}-source

  # install python-renpy
  python2 module/setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1
  install -D -m644 'LICENSE.txt' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
