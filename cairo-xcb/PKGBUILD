# Maintainer: xduugu
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=cairo-xcb
pkgver=1.10.2
pkgrel=2
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng>=1.4.0' 'libxrender' 'fontconfig>=2.8.0' 'pixman>=0.18.4' 'glib2>=2.24.0' 'sh' 'xcb-util>=0.3.6')
makedepends=('pkgconfig')
provides=("cairo=$pkgver")
conflicts=('cairo')
options=('!libtool')
source=(http://cairographics.org/releases/${pkgname%-*}-${pkgver}.tar.gz
        cairo-1.10.0-buggy_gradients.patch)
md5sums=('f101a9e88b783337b20b2e26dfd26d5f'
         '9b323790dab003e228c6955633cb888e')
sha256sums=('32018c7998358eebc2ad578ff8d8559d34fc80252095f110a572ed23d989fc41'
            '79085147567e6e080d5c1d7d2246f7d4be1efcc8e4b4fe44b6db1ebeae93c420')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  patch -Np1 -i "${srcdir}/cairo-1.10.0-buggy_gradients.patch"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee \
	--enable-xcb
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
