# Maintainer: Muflone <webreg@vbsimple.net>
# Contributor: György Balló <ballogy@freestart.hu>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=sbackup
pkgver=0.11.4
pkgrel=2
pkgdesc="Simple backup solution intended for desktop use"
arch=('any')
url="https://launchpad.net/sbackup"
license=('GPL')
depends=('python2-gconf' 'python2-libgnome' 'python2-notify' 'gvfs' 'dbus-python' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('python2-distribute')
optdepends=('python-pexpect: for ssh fuse plugin'
            'sshfs: for ssh plugin'
            'curlftpfs: for ftp and sftp fuse plugins')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/${pkgname}_$pkgver.tar.gz)
md5sums=('5e85502c233be387f58a5cdefffc8ab0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/env python2@'
  sed -i "s/dpkg --get-selections/pacman -Q/" src/sbackup/core/snapshot.py

  make PYTHON=python2 PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PYTHON=python2 \
       PREFIX=/usr \
       DESTDIR="$pkgdir/usr" \
       sysconf_dir=$pkgdir/etc \
       SETUP.PY_OPTS="--root=$pkgdir/" \
       DISABLE_MAKEFILE_GTK_UPDATE_ICON_CACHE=1 \
       DISABLE_MAKEFILE_DESKTOP_DATABASE_RELOAD=1 \
       DISABLE_MAKEFILE_DBUS_RELOAD=1 \
       GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
       DISABLE_MAKEFILE_CLEAN_DATA=1 \
       install
}
