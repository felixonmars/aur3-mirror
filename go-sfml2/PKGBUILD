# Maintainer: Alexandre Bury <alexandre.bury@gmail.com>

pkgname=go-sfml2
pkgver=20121205
pkgrel=2
pkgdesc="Go bindings for SFML."
arch=('x86_64' 'i686')
url="https://bitbucket.org/krepa098/gosfml2/"
license=('unknown')
depends=('go' 'csfml>=2.0')
makedepends=('git')
options=('!strip' '!emptydirs')
_gourl=bitbucket.org/krepa098/gosfml2/

build() {
  cd "$srcdir"
  GOPATH="$srcdir" go get -fix -v -x ${_gourl}

  # Apply a temporary patch
  FILENAME=src/bitbucket.org/krepa098/gosfml2/structs_test.go
  sed 's/Recti/IntRect/g' -i "${FILENAME}"
  sed 's/Rectf/FloatRect/g' -i "${FILENAME}"
}

check() {
  source /etc/profile.d/go.sh
  GOPATH="$GOPATH:$srcdir" go test -v -x ${_gourl}
}

package() {
  source /etc/profile.d/go.sh
  mkdir -p "$pkgdir/$GOPATH"
  cp -Rv --preserve=timestamps "$srcdir"/{src,pkg} "$pkgdir/$GOPATH"
  find "$pkgdir" -depth -type d -name .git -exec rm -r {} \;

  # Package license (if available)
  for f in LICENSE COPYING; do
    if [ -e "$srcdir/src/$_gourl/$f" ]; then
      install -Dm644 "$srcdir/src/$_gourl/$f" "$pkgdir/usr/share/licenses/$pkgname/$f"
    fi
  done
}

# vim:set ts=2 sw=2 et:
