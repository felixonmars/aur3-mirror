# Maintainer: Idares <idares@seznam.cz>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=zabbix-server
pkgver=1.8.11
pkgrel=1
pkgdesc="Software for monitoring of your applications, network and servers."
arch=('i686' 'x86_64')
url="http://www.zabbix.com"
license=('GPL')
depends=('apache' 'postgresql' 'php' 'php-pgsql' 'php-gd' 'fping' 'net-snmp' 'curl' 'iksemel')
backup=('etc/zabbix/zabbix_server.conf'
        'etc/conf.d/zabbix-server'
        )
install='zabbix-server.install'
options=('emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/zabbix/zabbix-$pkgver.tar.gz"
        'rc.zabbix-server'
        'conf.zabbix-server'
        'zabbix-server.install'
        )

_HTMLPATH='srv/http/zabbix'

build() {
  cd "$srcdir/zabbix-$pkgver"

  # Only one database can be compiled for; Uncomment/Comment the
  # --with-pgsql/--with-mysql lines below to suit your requirements
  ./configure \
    --prefix=/usr \
    --enable-server \
    --with-net-snmp \
    --with-jabber \
    --with-libcurl \
    --with-pgsql
    #--with-mysql

  make
}

package() {
  cd "$srcdir/zabbix-$pkgver"

  make DESTDIR="$pkgdir" install

  # Create data dirs required
  install -d -m0750 $pkgdir/var/run/zabbix
  install -d -m0750 $pkgdir/var/log/zabbix

  # sql templates
  for sql_file in ibm_db2.sql mysql.sql oracle.sql postgresql.sql sqlite.sql ; do
    install -D -m 0444 create/schema/$sql_file $pkgdir/usr/share/zabbix/dbms/create/schema/$sql_file
  done
  for sql_file in data.sql images_ibm_db2.sql images_mysql.sql images_oracle.sql images_pgsql.sql images_sqlite3.sql ; do
    install -D -m 0444 create/data/$sql_file $pkgdir/usr/share/zabbix/dbms/create/data/$sql_file
  done
  cp -r create/data/images/ $pkgdir/usr/share/zabbix/dbms/create/data/images

  # frontends (user:group = root:apache)
  mkdir -p $pkgdir/$_HTMLPATH/
  cp -r frontends/php/* $pkgdir/$_HTMLPATH/
  chown -R 0:33 $pkgdir/$_HTMLPATH/
  chmod -R u=rwX,g=rX,o= $pkgdir/$_HTMLPATH/

  # default configuration files
  install -D -m 0640 misc/conf/zabbix_server.conf $pkgdir/etc/zabbix/zabbix_server.conf

  # init script
  install -D -m 0755 $srcdir/rc.zabbix-server $pkgdir/etc/rc.d/$pkgname

  install -D -m 0622 $srcdir/conf.zabbix-server $pkgdir/etc/conf.d/$pkgname
}

md5sums=('6b6ced84024f18f22ce5e0e902e91936'
         'c56242d58e60c167fb31ac34b25ee64d'
         'cc60404fc2344545ff90546959342302'
         'd151af95826ea0ffac4281f54e2b76d6')
sha1sums=('2efb94e0b57f1ddbb38ef3f8da225d88effb0689'
          'ff7acb3012ff95a0b6b02496aab85eb0fc281581'
          '40af899208b89e51429d4274f7e57cf87fd76c56'
          '2adcf5f4a53a0a65a393db8815f763f9a1cf39c2')
