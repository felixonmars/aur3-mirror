# Maintainer: Benjamin Bukowski <benjamin.bukowski@googlemail.com>
pkgname=firebird-superserver
_pkgver=2.5.1.26351
_buildver=0
pkgver=${_pkgver}_${_buildver}
pkgrel=3
pkgdesc="A open source SQL relational database management system (RDMS)"
arch=(i686 x86_64)
url="http://www.firebirdsql.org/"
license=('IPL IDPL')
provides=("libfbclient=${_pkgver}")
depends=()
conflicts=('firebird-classicserver' 'libfbclient')
install=firebird-superserver.install

_arch=i686
[[ "$CARCH" == x86_64 ]] && _arch=amd64

_filename="FirebirdSS-${_pkgver}-${_buildver}.${_arch}"
source=("http://downloads.sourceforge.net/firebird/${_filename}.tar.gz"
        firebird)
md5sums=('84911d9e9a04f7edd26109abeb92038d'
         '0ad6fcab5610b337be2a4d7c00a29bb5')

[[ "$CARCH" == x86_64 ]] && md5sums[0]='337d00c6887941bcd1943dec94270fff'

package() {
  cd "$srcdir/${_filename}"

  msg "Extracting firebird"
  tar -xzof buildroot.tar.gz -C $pkgdir

  [[ "$CARCH" == x86_64 ]] &&  mv $pkgdir/usr/lib64/ $pkgdir/usr/lib/

  install -Dm744 $srcdir/firebird $pkgdir/etc/rc.d/firebird

  touch $pkgdir/opt/firebird/{fb_guard,firebird.log}
 
  chmod u=rw,go= $pkgdir/opt/firebird/{fb_guard,firebird.log,security2.fdb,examples/empbuild/*.fdb}

  cat << EOT > $pkgdir/opt/firebird/SYSDBA.password
# This is the default password
# You should change this password at the earliest oportunity
# Your password can be changed to a more suitable one using the
# /opt/firebird/bin/gsec utility
ISC_USER=sysdba
ISC_PASSWD=masterkey
EOT

  chmod u=r,go= $pkgdir/opt/firebird/SYSDBA.password

  if [ -x $pkgdir/opt/firebird/bin/fbmgr.bin ]; then
    cat > $pkgdir/opt/firebird/bin/fbmgr << EOT
#!/bin/sh
FIREBIRD=/opt/firebird
export FIREBIRD
exec /opt/firebird/bin/fbmgr.bin \$@
EOT
fi
chmod ugo+x $pkgdir/opt/firebird/bin/fbmgr

}

# vim:set ts=2 sw=2 et:
