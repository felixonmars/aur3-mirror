# Maintainer: Myles English <myles at rockhead dot biz>
pkgname=petsc
pkgver=3.1_p8
_config=linux-gnu-cxx-debug
pkgrel=1
pkgdesc="Portable, extensible toolkit for scientific computation"
arch=('any')
url="http://www.mcs.anl.gov/petsc/petsc-as"
license=('MIT compatible')
depends=('python2' 'gcc' 'mpich2' 'boost')
#conflicts=('umfpack')
install=(petsc.install)
source=(http://ftp.mcs.anl.gov/pub/petsc/release-snapshots/${pkgname}-${pkgver/_/-}.tar.gz)
md5sums=('936b56152f6eeaf3322b87683d757622')

_build_dir=${srcdir}/${pkgname}-${pkgver/_/-}
_install_dir=/usr/petsc/${_config}

build() {
    cd ${_build_dir}

    unset PETSC_ARCH
    export PETSC_DIR=${_build_dir}

    find ${srcdir} -name "*" -type f -exec \
	sed -i 's#\(/usr/bin/env \|/usr/bin/\)python[2-3]*#\1python2#' {} \;

    python2 ./config/configure.py --prefix=${pkgdir}${_install_dir} \
	--with-mpi-dir=/opt/mpich2 \
	--with-mpi-shared=1 \
	--with-sieve \
	--download-umfpack=1 \
	--with-clanguage=cxx \
        --with-boost-dir=/usr \
	--with-umfpack=1 --with-umfpack-dir=/usr/lib \
	--with-shared=1
    make
    sed -i 's/.\/config\/install.py/python2 .\/config\/install.py/' makefile
}

package() {
    cd ${_build_dir}

    make PETSC_DIR=${_build_dir} PETSC_ARCH=${_config} install

    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/variables"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/petscvariables"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/rules"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/include/petscconf.h"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/include/petscconfiginfo.h"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/petscrules"

    export PETSC_DIR=${_install_dir}
    
    # documentation
    mkdir -p ${pkgdir}/usr/share/doc/$pkgname/
    cp -r ${_build_dir}/docs ${pkgdir}/usr/share/doc/$pkgname/

    # tutorials
    mkdir -p ${pkgdir}/usr/share/doc/$pkgname/tutorials
    cp -r ${_build_dir}/tutorials ${pkgdir}/usr/share/doc/$pkgname/tutorials

    # install licenCe (even though there is no such word as licenSes)
    mkdir -p ${pkgdir}/usr/share/licenses/petsc
    cp ${_build_dir}/docs/copyright.html ${pkgdir}/usr/share/licenses/$pkgname/

    mkdir -p ${pkgdir}/etc/profile.d
    echo "export PETSC_DIR=${_install_dir}" > ${pkgdir}/etc/profile.d/petsc.sh
    chmod +x ${pkgdir}/etc/profile.d/petsc.sh

    # show where the shared libraries are
    install -d -m755 "${pkgdir}"/etc/ld.so.conf.d/
    echo '/usr/petsc/linux-gnu-cxx-debug/lib' > "${pkgdir}"/etc/ld.so.conf.d/petsc.conf
}
