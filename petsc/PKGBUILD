# Maintainer: Myles English <myles at rockhead dot biz>
# Some copied from from dorsal-1.0-beta2/FEniCS/packages/petsc.package
pkgname=petsc
pkgver=3.2_p7
_config=arch-linux2-cxx-opt
#_config=arch-linux2-cxx-debug
pkgrel=4
pkgdesc="Portable, extensible toolkit for scientific computation"
arch=('any')
url="http://www.mcs.anl.gov/petsc/petsc-as"
license=('MIT compatible')
depends=('python2' 'gcc' 'openmpi' 'blas' 'lapack')
# 'boost' 'umfpack') # 'parmetis') # 'ptscotch-openmpi' 'scotch' 'parmetis')
#conflicts=('umfpack')
install=petsc.install
source=(http://ftp.mcs.anl.gov/pub/petsc/release-snapshots/${pkgname}-${pkgver/_/-}.tar.gz)
md5sums=('b9b5b42ffb6c619e4f7ee6b29134dc5f')

_build_dir=${srcdir}/${pkgname}-${pkgver/_/-}
_install_dir=/usr/petsc/${_config}

build() {
    cd ${_build_dir}

    unset PETSC_ARCH
    export PETSC_DIR=${_build_dir}

    find ${srcdir} -name "*" -type f -exec \
	sed -i 's#\(/usr/bin/env \|/usr/bin/\)python[2-3]*#\1python2#' {} \;

    # Couldn't get parmetis from cower to work with this so download it instead
    # from Dorsal ./FEniCS/packages/petsc.package
    CONFOPTS="COPTFLAGS=-O2                                                                                                                     
        --with-debugging=0 --with-shared-libraries=1 --with-clanguage=cxx"
      # --with-parmetis=1 # --with-parmetis-dir=${PARMETIS_DIR}

    # Available in the AUR:
    #   aur/mumps
    #   aur/scalapack
    #   aur/blacs-mpi
    #   aur/ptscotch-openmpi
    #   aur/scotch

    # from Dorsal ./FEniCS/packages/petsc.package
    for external_pkg in hypre mumps scalapack blacs ptscotch scotch parmetis; do
	CONFOPTS="${CONFOPTS} --download-${external_pkg}=1"
    done

    # from Dorsal ./FEniCS/packages/petsc.package
    if [ "${TRILINOS_DIR}" ]; then
	CONFOPTS="${CONFOPTS} --with-ml=1 --with-ml-lib=${TRILINOS_DIR}/lib/libml.so
              --with-ml-include=${TRILINOS_DIR}/include" #trilinos
    fi
    
    # Arch specific
    CONFOPTS="${CONFOPTS} 
        --with-boost-dir=/usr --with-blas-lapack-dir=/usr/lib
        --with-umfpack=1 --with-umfpack-dir=/usr/include"

    python2 ./config/configure.py \
	--prefix=${pkgdir}${_install_dir} \
	${CONFOPTS}
    # --with-parmetis-include=/usr/include --with-parmetis-lib=/usr/lib/libparmetis.so
    # --with-parmetis-lib="-L/usr/lib -lparmetis -lmetis" --with-parmetis-include=/usr/include
    # --with-parmetis-lib=/usr/lib -lparmetis" --with-parmetis-include=/usr/include
    # sudo cp /usr/include/metis/metis.h /usr/include/metis.h
    # --with-parmetis-include=/sandbox/balay/parmetis/include \
    #   --with-parmetis-lib="-L/sandbox/balay/parmetis/lib -lparmetis -lmetis"
    # Specify either "--with-parmetis-dir" or "--with-parmetis-lib --with-parmetis-include". But not both!

    make all
}

package() {
    cd ${_build_dir}

    make PETSC_DIR=${_build_dir} PETSC_ARCH=${_config} install > /dev/null

    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/variables"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/petscvariables"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/rules"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/include/petscconf.h"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/include/petscconfiginfo.h"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/petscrules"

    export PETSC_DIR=${_install_dir}
    
    # documentation
    mkdir -p ${pkgdir}/usr/share/doc/$pkgname/
    cp -r ${_build_dir}/docs ${pkgdir}/usr/share/doc/$pkgname/

    # tutorials
    cp -r ${_build_dir}/tutorials ${pkgdir}/usr/share/doc/$pkgname/

    # install licenCe (even though there is no such word as licenSes)
    mkdir -p ${pkgdir}/usr/share/licenses/petsc
    cp ${_build_dir}/docs/copyright.html ${pkgdir}/usr/share/licenses/$pkgname/

    mkdir -p ${pkgdir}/etc/profile.d
    echo "export PETSC_DIR=${_install_dir}" > ${pkgdir}/etc/profile.d/petsc.sh
    chmod +x ${pkgdir}/etc/profile.d/petsc.sh

    # show where the shared libraries are
    install -d -m755 "${pkgdir}"/etc/ld.so.conf.d/
    echo "${_install_dir}/lib" > "${pkgdir}"/etc/ld.so.conf.d/petsc.conf
}
