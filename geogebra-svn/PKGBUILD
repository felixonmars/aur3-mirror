# Current i686 Maintainer: ifaigios <ifaigios_at_gmail_dot_com>
# Contributor: moostik <mooostik_at_gmail_dot_com>

pkgname=geogebra-svn
_realname=geogebra
pkgver=12988
_realver=4.9.4.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet - SVN version"
arch=('i686' 'x86_64')
url='http://www.geogebra.org/'
license=('custom')
        # Application and source code under GPLv3
        # Language files and documentation under CC-BY-SA 3.0
depends=('java-runtime' 'shared-mime-info' 'xdg-utils' 'desktop-file-utils' 'hicolor-icon-theme' 'gtk-update-icon-cache')
makedepends=('subversion' 'jdk' 'apache-ant')
optdepends=('geogebra-thumbnail-kde: KDE thumbnailer for GeoGebra')
install="$_realname.install"
provides=('geogebra' 'geogebra-beta' 'geogebra42-beta' 'geogebra5-beta')
conflicts=('geogebra' 'geogebra-beta' 'geogebra42-beta' 'geogebra5-beta')
source=("$_realname.sh")
md5sums=('a0b7c8314b54ff7e945b0c4e1ae83d48')

_svntrunk="http://www.geogebra.org/svn/trunk/$_realname/"
_svnmod="$_realname"

build() {
    # SVN checkout
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"

    # Creating working directory
  rm -rf $srcdir/$_svnmod-build
  cp -r $srcdir/$_svnmod $srcdir/$_svnmod-build
  cd $srcdir/$_svnmod-build

  msg "Starting make..."

    # Downloading external libraries
  ant -f kickstart.xml lib ecj

    # Actual build
  cd $srcdir/$_svnmod-build/scripts/autobuild
  touch autobuild.conf
  sh make-vanilla-jars -j /opt/java
}

package() {
    # Installing application
  cd $srcdir/$_svnmod-build/build
  install -dm755 $pkgdir/usr/share/java/$_realname
  install -Dm644 *.jar $pkgdir/usr/share/java/$_realname

    # Installing launcher
  install -Dm755 $srcdir/$_realname.sh $pkgdir/usr/bin/$_realname

    # Installing icons
  cd $srcdir/$_svnmod-build/icons/hicolor
  for i in *; do
      install -dm755 $pkgdir/usr/share/icons/hicolor/$i/{apps,mimetypes}
      install -Dm644 $i/apps/* $pkgdir/usr/share/icons/hicolor/$i/apps
      install -Dm644 $i/mimetypes/* $pkgdir/usr/share/icons/hicolor/$i/mimetypes
  done

    # Installing menu item
  install -Dm644 $srcdir/$_svnmod-build/scripts/autobuild/linux/generic/$_realname.desktop $pkgdir/usr/share/applications/$_realname.desktop

    # Installing pixmap
  install -dm755 $pkgdir/usr/share/pixmaps
  ln -s /usr/share/icons/hicolor/64x64/apps/$_realname.png $pkgdir/usr/share/pixmaps/$_realname.png

    # Installing mimetype
  install -Dm644 $srcdir/$_svnmod-build/scripts/autobuild/linux/generic/$_realname.xml $pkgdir/usr/share/mime/packages/$_realname.xml

    # Installing license
  install -Dm644 $srcdir/$_svnmod-build/_LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
