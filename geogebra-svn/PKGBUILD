# Current i686 Maintainer: ifaigios <ifaigios_at_gmail_dot_com>
# Contributor: moostik <mooostik_at_gmail_dot_com>

pkgname=geogebra-svn
_realname=geogebra
pkgver=18574
_realver=4.9.19.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet - SVN version"
arch=('i686' 'x86_64')
url="http://www.geogebra.org/"
license=('custom')
        # Application and source code under GPLv3
        # Language files and documentation under CC-BY-SA 3.0
        # Installer scripts under CC-BY-NC 3.0
depends=('java-runtime' 'shared-mime-info' 'xdg-utils' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('java-environment' 'subversion' 'apache-ant')
optdepends=('geogebra-thumbnail-kde: KDE thumbnailer for GeoGebra')
install="${_realname}.install"
provides=('geogebra' 'geogebra-beta' 'geogebra42-beta' 'geogebra5-beta')
conflicts=('geogebra' 'geogebra-beta' 'geogebra42-beta' 'geogebra5-beta')

_svntrunk="http://dev.geogebra.org/svn/trunk/${_realname}"
_svnmod="${_realname}"

build() {
    # SVN checkout
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"

    # Creating working directory
  rm -rf $srcdir/$_svnmod-build
  cp -r $srcdir/$_svnmod $srcdir/$_svnmod-build

  msg "Starting make..."

    # Downloading JOGL1
  cd $srcdir/$_svnmod-build/jogl1
  ant -f kickstart.xml

    # Downloading JOGL2
  cd $srcdir/$_svnmod-build/jogl2
  ant -f kickstart.xml

    # Downloading external libraries
  cd $srcdir/$_svnmod-build/desktop
  ant -f kickstart.xml geogebra5-packager-commandline

    # Build!
  cd scripts/autobuild
  ./make-vanilla-jars -tu
}

package() {
    # Installing application
  cd $srcdir/$_svnmod-build/desktop/build
  install -dm755 $pkgdir/usr/share/java/$_realname
  install -Dm644 * $pkgdir/usr/share/java/$_realname

    # Installing launcher
  install -dm755 $pkgdir/usr/bin
  sed -e "1 s:h:h\n\n\GG_PATH='/usr/share/java/geogebra'\n:" $srcdir/$_svnmod-build/desktop/scripts/autobuild/linux/geogebra > $pkgdir/usr/bin/$_realname
  chmod 755 $pkgdir/usr/bin/$_realname

    # Installing icons
  cd $srcdir/$_svnmod-build/desktop/icons/hicolor
  for i in *; do
    install -dm755 $pkgdir/usr/share/icons/hicolor/$i/{apps,mimetypes}
    install -Dm644 $i/apps/* $pkgdir/usr/share/icons/hicolor/$i/apps
    install -Dm644 $i/mimetypes/* $pkgdir/usr/share/icons/hicolor/$i/mimetypes
  done

    # Installing menu item
  install -Dm644 $srcdir/$_svnmod-build/desktop/scripts/autobuild/linux/generic/$_realname.desktop $pkgdir/usr/share/applications/$_realname.desktop

    # Installing mimetype
  install -Dm644 $srcdir/$_svnmod-build/desktop/scripts/autobuild/linux/generic/$_realname.xml $pkgdir/usr/share/mime/packages/$_realname.xml

    # Installing license
  install -Dm644 $srcdir/$_svnmod-build/desktop/_LICENSE.txt $pkgdir/usr/share/licenses/$_realname/LICENSE
}
