# Maintainer: RudolfPolzer <divVerent@xonotic.org>
# Based on the mplayer2-lite-git PKGBUILD

pkgname=mpv-git
pkgver=20121204
pkgrel=1
pkgdesc="A movie player and encoder for linux (uses dynamically linked ffmpeg; git version)"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/mpv-player/mpv"
depends=('libcaca' 'portaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'smbclient' 'libmng' 'libbluray' 'libcdio' 'libxinerama' 'lirc-utils' 'libxss' 'desktop-file-utils' 'libvdpau' 'libass' 'libquvi' 'mpg123')
makedepends=('git' 'mesa' 'python-docutils')
backup=()
provides=('mpv')
conflicts=('mpv')
options=(!emptydirs)
install=mpv.install
source=('mpv.install' 'mpv.desktop')
md5sums=('cbc234e5e789e30c624741173992a225'
        'afa04e3bdb3ef16b78804760c63a3d9a')

_gitroot="https://github.com/mpv-player/mpv"
_gitname="mpv"
_gitbranch="master"

build() {
  # Custom CFLAGS break the mpv build
  unset CFLAGS
  unset LDFLAGS

  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull --rebase origin
    msg "The local files are updated."
  else
    git clone -b $_gitbranch $_gitroot $_gitname
  fi

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  ./configure --prefix=/usr \
      --disable-lircc \
      --disable-joystick \
      --disable-apple-remote \
      --disable-apple-ir \
      --disable-radio \
      --disable-radio-capture \
      --disable-radio-v4l2 \
      --disable-radio-bsdbt848 \
      --disable-winsock2_h \
      --disable-macosx-finder \
      --disable-macosx-bundle \
      --disable-rpath \
      --disable-vstream \
      --disable-libbs2b \
      --disable-direct3d \
      --disable-corevideo \
      --disable-cocoa \
      --disable-coreaudio

  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  make -j1 DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/doc/mpv/examples

  # some extra useful stuff "make install" doesn't install
  install -m644 ${srcdir}/${_gitname}-build/etc/{input,example,encoding-example-profiles}.conf "${pkgdir}/usr/share/doc/mpv/examples"
  install -m644 ${srcdir}/${_gitname}-build/DOCS/{encoding.rst,tech-overview.txt} "${pkgdir}/usr/share/doc/mpv"
  install -m644 ${srcdir}/${_gitname}-build/TOOLS/mpv_identify.sh "${pkgdir}/usr/bin"
  install -Dm644 "${srcdir}/${_gitname}-build/etc/mplayer.xpm" "${pkgdir}/usr/share/pixmaps/mpv.xpm"

  # file association
  install -Dm644 "${srcdir}/mpv.desktop" "${pkgdir}/usr/share/applications/mpv.desktop"
}
