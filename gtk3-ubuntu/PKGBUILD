# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Luigi Ranghetti <ggranga@gmail.com>

pkgname=gtk3-ubuntu
pkgver=3.4.4
pkgrel=1
pkgdesc="GTK+ is a multi-platform toolkit (v3) with Ubuntu's modifications"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord')
makedepends=('gobject-introspection')
provides=("gtk3=$pkgver")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.4/gtk+-$pkgver.tar.xz
        settings.ini
        016_no_offscreen_widgets_grabbing.patch
        017_no_offscreen_device_grabbing.patch
        043_ubuntu_menu_proxy.patch
        062_ubuntu-set-grab-add.patch
        075_expose_gdkwidget_for_gtkrange.patch
        wacom.patch)
sha256sums=('f154e460075034da4c0ce89c320025dcd459da2a1fdf32d92a09522eaca242c7'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            'c4a43b254b9719ac026fa6a92366bdc1d32e42fa79461ff8aaa206933b9f9c7e'
            'cb8f4eb9d10c6d8645d23812aaa7d64c14560476f19f8c779d10a546748de684'
            '3c40814a3ba51dbbd169b5fc8f4a3c47aff6c5b97433a1f6c13fa66d465947d9'
            '5a89bd63afb2d97b98eb3b7acb5a568c388aedd94af38a1106233ce033982354'
            '9ee240b355502d6ba449983f794205e6e5401c8acca2a3ce513df7f0502dc988'
            'fe92039b87651b8925f5adb2d8a3685d1a3bd5caf8c34243a0408b79f007b18b')

build() {
    cd "$srcdir/gtk+-$pkgver"

    # Don't let offscreen widget do grabbing (fix for ido)
    patch -Np1 -i "$srcdir/016_no_offscreen_widgets_grabbing.patch"

    # Do not allow widgets in an offscreen hierarchy to take grabs (fix for ido)
    patch -Np1 -i "$srcdir/017_no_offscreen_device_grabbing.patch"

    # Add Ubuntu menuproxy module (required for appmenu-gtk)
    patch -Np1 -i "$srcdir/043_ubuntu_menu_proxy.patch"

    # Add ubuntu-set-grab-add signal (required for ido)
    patch -Np1 -i "$srcdir/062_ubuntu-set-grab-add.patch"

    # Add gtk_range_get_event_window to get the event_window for a GtkRange (fix for ido)
    patch -Np1 -i "$srcdir/075_expose_gdkwidget_for_gtkrange.patch"

    # Add support for the ayatana overlay scrollbars (required for overlay-scrollbar)
    # patch -Np1 -i "$srcdir/100_overlay_scrollbar_loading.patch"

    # Partially revert BGO#673440 in order to fix BGO#674157
    patch -Np1 -i ../wacom.patch

    aclocal -I m4
    libtoolize --copy --force
    automake
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile
    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "$srcdir/gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}