# Maintainer: György Balló <ballogy@freestart.hu>

_pkgname=gtk3
pkgname=gtk3-ubuntu
pkgver=3.0.12
pkgrel=1
pkgdesc="The GTK+ Toolkit (v3) with Ubuntu's modifications"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info')
makedepends=('gobject-introspection')
provides=('gtk3=3.0.12')
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.0/gtk+-${pkgver}.tar.xz
        settings.ini
        043_ubuntu_menu_proxy.patch
        062_ubuntu-set-grab-add.patch
        063_resize_grip_default.patch
        072_indicator_menu_update.patch
        091_bugzilla_tooltip_refresh.patch
        100_overlay_scrollbar_loading.patch
        gtk+-3.0-right-filter-window-for-XI2-events.patch)
sha256sums=('219fdea120897981840e909fac40d4d433fe061a37b12b9402e215c819ebeae9'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            '6092d407624fc5b27a44fca4e0e1ecdc5ffbfb10dcb91efd3ee073987281f2a7'
            'd95dc7f847fe5edac2e174c11cc7eb2f67f8f4179edc50e66a272ff04ebff161'
            '390f7b6edd4f1e974206ae1cc3234e2afea94a957ba936d70134e8592fd7410b'
            '0b7d3e1d383f78d774e42b5b61881bf8f8f1ca92c64e334f9509a07bff9c4a7a'
            'ab42eb3669ffb18bcc46f54890dd6266fc072829536d55d2a4aca501564b17f3'
            'f11ebc5477f1913f241f9abdc0e3aa8ef19f405574fb901b8c654ec00b50a1ce'
            '43fe524696d62d55d0ea9e8e4665dcfe57f0c1835e2487305c6808c72ec95ddb')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
    patch -Np1 -i "$srcdir/043_ubuntu_menu_proxy.patch"
    patch -Np1 -i "$srcdir/062_ubuntu-set-grab-add.patch"
    patch -Np1 -i "$srcdir/063_resize_grip_default.patch"
    patch -Np1 -i "$srcdir/072_indicator_menu_update.patch"
    patch -Np1 -i "$srcdir/091_bugzilla_tooltip_refresh.patch"
    patch -Np1 -i "$srcdir/100_overlay_scrollbar_loading.patch"
    patch -Np1 -i "$srcdir/gtk+-3.0-right-filter-window-for-XI2-events.patch"

    automake
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile
    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "${srcdir}/gtk+-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
