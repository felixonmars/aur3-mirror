# Maintainer : Anish Bhatt <anish at gatech dot edu>
# Contributor: Alucryd <alucryd at gmail dot com>
# Contributor: Jason Melton <jason dot melton at gmail dot com>
# Contributor: Youpi <max dot flocard at gmail dot com>
# Contributor: sl1pkn07 <sl1pkn07 at gmail dot com>

pkgname=nvidia-beta-dkms
pkgver=337.25
pkgrel=1
pkgdesc="NVIDIA kernel module sources (DKMS) - BETA version"
arch=('i686' 'x86_64')

[[ $CARCH == i686 ]] && _arch=x86 && _pkg=NVIDIA-Linux-${_arch}-${pkgver}
[[ $CARCH == x86_64 ]] && _arch=x86_64 && _pkg=NVIDIA-Linux-${_arch}-${pkgver}-no-compat32

url="http://www.nvidia.com/"
license=('custom')
depends=('dkms' 'linux>=3.7' 'linux<3.15' "nvidia-utils-beta>=${pkgver}")
provides=("nvidia=${pkgver}" 'nvidia-dkms')
conflicts=('nvidia-beta')

source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

md5sums=('2a217632ced8952e21000a51065f85b8')
[ "${CARCH}" = 'x86_64' ] && md5sums[0]='66e19d6d621c9e70fbd5374dd8b44341'

install=${pkgname}.install

prepare() {
  pushd "${srcdir}"

# Extract
  if [[ -d ${_pkg} ]] ; then
    rm -rf ${_pkg}
  fi
  sh ${_pkg}.run --extract-only

  pushd ${_pkg}/kernel
    cat uvm/dkms.conf.fragment >> dkms.conf
  popd

  popd
}

package() {
  cd "${srcdir}"/${_pkg}/kernel

# Install
  install -dm 755 "${pkgdir}"/usr/{lib/modprobe.d,share/licenses,src/nvidia-${pkgver}}
  cp -dr --no-preserve=ownership * "${pkgdir}"/usr/src/nvidia-${pkgver}/

  echo "blacklist nouveau" > "${pkgdir}"/usr/lib/modprobe.d/nvidia.conf
  ln -s /usr/share/licenses/nvidia "${pkgdir}"/usr/share/licenses/${pkgname}

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
  chmod 755 "${pkgdir}"/usr/src/nvidia-${pkgver}/conftest.sh
}

# vim: ts=2 sw=2 et:
