# Contributor: Dave Reisner <d@falconindy.com>

pkgname=dbus-systemd
pkgver=1.4.0
pkgrel=1
pkgdesc="Freedesktop.org message bus system. Built for systemd."
url="http://www.freedesktop.org/Software/dbus"
arch=('i686' 'x86_64')
license=('GPL' 'custom')
depends=('libx11' 'expat' 'coreutils' 'filesystem')
provides=("dbus=$pkgver" "dbus-core=$pkgver")
conflicts=('dbus' 'dbus-core')
groups=('systemd')
options=(!libtool emptydirs)
install=dbus.install
source=("http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz"
        30-dbus)
md5sums=('f59618b18d2fb2bd1fce9e1c5a2a3282'
         'd14e59575f04e55d21a04907b6fd9f3c')

build() {
  cd "$srcdir/${pkgname%-systemd}-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --disable-verbose-mode --disable-static --enable-inotify \
      --disable-dnotify --disable-tests --disable-asserts \
      --with-systemdsystemunitdir=/lib/systemd/system

  make
}

package() {
  cd "$srcdir/${pkgname%-systemd}-$pkgver"

  pushd tools

  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m755 dbus-launch "${pkgdir}/usr/bin/"
  install -m644 dbus-launch.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/30-dbus" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus"

  popd

  make DESTDIR="$pkgdir" install

  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus/"

  mkdir -p ${pkgdir}/var/run/dbus
  chown 81:81 "${pkgdir}/var/run/dbus"

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|;/pidfile\|pid file/d' "${pkgdir}/etc/dbus-1/system.conf"

  #install .keep files so pacman doesn't delete empty dirs
  touch "${pkgdir}/usr/share/dbus-1/services/.keep"
  touch "${pkgdir}/usr/share/dbus-1/system-services/.keep"
  touch "${pkgdir}/etc/dbus-1/session.d/.keep"
  touch "${pkgdir}/etc/dbus-1/system.d/.keep"

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus-core"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus-core/"
}
