# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-color-manager
pkgver=1.6.1
pkgrel=1
pkgdesc="Color management application for MATE."
url="https://github.com/NiceandGently/${pkgname}"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'exiv2' 'gtk2' 'lcms2' 'libcanberra' 'libcups' 'libexif' 'libnotify' 'libtiff'
         'libunique' 'mate-desktop' 'sane' 'vte')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!libtool' '!emptydirs')
groups=('mate-extras-community')
source=(https://github.com/NiceandGently/${pkgname}/archive/v${pkgver}.tar.gz)
md5sums=('ee4f5d1d8439920bc2bac435540d560c')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-scrollkeeper

    #Work around a problem where the .sgml fail to parse.
    sed -e 's:@HAVE_DOCBOOK2MAN_TRUE@.*::' -i man/Makefile.in
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
