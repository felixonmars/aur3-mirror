# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-intel-tbb
pkgver=4.2_20140122
pkgrel=1
pkgdesc='High level abstract threading library (mingw-w64)'
depends=('mingw-w64-crt' 'mingw-w64-dlfcn')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!strip' 'staticlibs')
arch=('any')
url='http://www.threadingbuildingblocks.org/'
license=('GPL')
source=("http://threadingbuildingblocks.org/sites/default/files/software_releases/source/tbb${pkgver/\./}oss_src.tgz"
        'tbb-disable_windows_api.patch')
sha1sums=('d897a6ced4f425c6608422a2e19218898e727260'
          'SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd tbb${pkgver/\./}oss
  
  # do not build debug libraries
  sed -i "/debug/d" Makefile
  
  # not not use win32 api
  patch -p1 -i "$srcdir"/tbb-disable_windows_api.patch
  echo "CPLUS_FLAGS += -DUSE_WIN32_API=0" >> build/windows.gcc.inc
  
  # platform configuration
  sed -i "s|export SHELL = cmd|export SHELL = sh -c|g" build/windows.inc
  sed -i "s|CMD=cmd /C|CMD=sh -c|g" build/windows.inc
  sed -i "s|CWD=\$(shell cmd /C echo %CD%)|CWD=\$(shell pwd)|g" build/windows.inc
  sed -i "s|RM=cmd /C del /Q /F|RM?=rm -f|g" build/windows.inc
  sed -i "s|RD=cmd /C rmdir|RD?=rmdir|g" build/windows.inc
  sed -i "s|MD=cmd /c mkdir|MD?=mkdir -p|g" build/windows.inc
  sed -i "s|SLASH=\\\\\\\\|SLASH=/|g" build/windows.inc
  sed -i "s|NUL = nul|NUL= /dev/null|g" build/windows.inc
  sed -i "s|.DLL = tbb|.DLL = libtbb|g" build/windows.inc
  sed -i "s|.LIB = tbb|.LIB = libtbb|g" build/windows.inc
  sed -i "/MAKE_VERSIONS/d" build/windows.inc
  echo "MAKE_VERSIONS=sh \$(tbb_root)/build/version_info_linux.sh \$(CPLUS) \$(CPLUS_FLAGS) \$(INCLUDES) >version_string.ver" >> build/windows.inc
  sed -i "/MAKE_TBBVARS/d" build/windows.inc
  echo "MAKE_TBBVARS=sh \$(tbb_root)/build/generate_tbbvars.sh" >> build/windows.inc

}

build() {
  cd tbb${pkgver/\./}oss

  for _arch in ${_architectures}; do
    cp build/windows.gcc.inc build/windows.${_arch}-gcc.inc
    sed -i "s|CPLUS = g++|CPLUS = ${_arch}-g++|g" build/windows.${_arch}-gcc.inc
    sed -i "s|CONLY = gcc|CONLY = ${_arch}-gcc|g" build/windows.${_arch}-gcc.inc
    sed -i "s|OUTPUT_KEY = -o #|OUTPUT_KEY = -Wl,--out-implib,\$(BUILDING_LIBRARY).a -o #|g" build/windows.${_arch}-gcc.inc
    sed -i "s|\$(call detect_js,/minversion gcc 4.4)|ok|g" build/windows.${_arch}-gcc.inc

    # use mingw's pthreads
    sed -i "s|USE_WINTHREAD|USE_PTHREAD|g" build/windows.${_arch}-gcc.inc src/tbbmalloc/TypeDefinitions.h
    sed -i "s|LIBDL =|LIBDL = -ldl|g" build/windows.${_arch}-gcc.inc
  
    if test "${_arch}" = "x86_64-w64-mingw32"
    then
      parch=intel64
    else
      parch=ia32
    fi
    unset LDFLAGS
    make arch=$parch tbb_os=windows runtime=mingw compiler=${_arch}-gcc
  done
}

package() {
  cd tbb${pkgver/\./}oss
  for _arch in ${_architectures}; do
    install -d "$pkgdir"/usr/${_arch}/include
    cp -a include/tbb "$pkgdir"/usr/${_arch}/include
    install -d "$pkgdir"/usr/${_arch}/bin
    install -m755 build/windows_*${_arch}*_release/*.dll "$pkgdir"/usr/${_arch}/bin
    install -d "$pkgdir"/usr/${_arch}/lib
    install -m755 build/windows_*${_arch}*_release/*.a "$pkgdir"/usr/${_arch}/lib
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
