# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>
pkgname=tora-svn
pkgver=4502
pkgrel=1
pkgdesc="Toolkit for databases with support for Oracle, MySQL and PostgreSQL"
arch=('i686' 'x86_64')
url="http://tora.sourceforge.net"
license=('GPL')
provides=('tora')
replaces=('tora')
conflicts=('tora')
depends=('qscintilla' 'oracle-instantclient-basic')
makedepends=('cmake' 'oracle-instantclient-sdk' 'subversion' 'boost')
options=()
install=$pkgname.install
source=('tora.desktop'
        'build_fix.patch'
        'tooraclefind_usr_lib.patch')
md5sums=('a959b9e71a5520c48da8c5d14b2e8b98'
         'e2673758d7f223a7ca47718723b46af4'
         'a57d5c0e061b09e2a00fa8cb5f92e485')

_svntrunk=http://tora.svn.sourceforge.net/svnroot/tora/trunk/tora
_svnmod=tora

build() {
  cd "${srcdir}"

  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # tmp fix
  patch -p0 < "${srcdir}"/build_fix.patch
  patch -p0 < "${srcdir}"/tooraclefind_usr_lib.patch

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_SUFFIX="" \
    -DBOOST_ROOT=/usr \
    -DBOOST_INCLUDEDIR=/usr/include/boost \
    -DBOOST_LIBRARYDIR=/usr/lib
  make
}

package() {
  cd "$srcdir/$_svnmod-build"/build
  make DESTDIR="${pkgdir}" install

  # -DLIB_SUFFIX="" breakage, fu...
  test -d "${pkgdir}"/usr/lib64 && mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib

  mkdir -p "${pkgdir}/usr/share/applications"
  mkdir -p "${pkgdir}/usr/share/icons/hicolor/32x32/apps"
  install -m644 ../src/icons/tora.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps"
  install -m644 "${srcdir}"/tora.desktop "${pkgdir}/usr/share/applications"
}
