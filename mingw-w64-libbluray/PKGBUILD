# Maintainer: Karl-Felix Glatzer <karl.glatzer@gmx.de>

pkgname=mingw-w64-libbluray
pkgver=0.3.0
pkgrel=1
pkgdesc="Library to access Blu-Ray disks for video playback (mingw-w64)"
arch=('any')
url="http://www.videolan.org/developers/libbluray.html"
license=('LGPL2.1')
depends=('mingw-w64-crt' 'mingw-w64-libxml2' 'mingw-w64-freetype')
options=(!strip !buildflags !libtool)
makedepends=('mingw-w64-gcc')
source=(ftp://ftp.videolan.org/pub/videolan/libbluray/$pkgver/libbluray-$pkgver.tar.bz2
        'configure.patch')
sha512sums=('96eedf23ff756e97fa64d9624a49608daccbac169714b9b231a284ed4eb63c77ee2ecbdfc42c311781571146cfa0fe340854ac831d3fe1312131e1742a4a7b02'
            '1df5af7beb44bcf388450b7a661bac277fb18f6b1ff3a548887420652dedf2e21ad04ba7cae3bea578995f078c091fd2baa669c485906c56d5cb5eb66108462c')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd ${srcdir}/libbluray-${pkgver}

  patch -Np1 < ../configure.patch
}

build() {
  for _arch in ${_architectures}; do
    mkdir -p ${srcdir}/build-${_arch} && cd ${srcdir}/build-${_arch}

    unset LDFLAGS CPPFLAGS
    ${srcdir}/libbluray-$pkgver/configure --prefix=/usr/${_arch} \
      --host=${_arch} \
      --disable-examples
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/build-${_arch}

    make DESTDIR="$pkgdir" install
    ${_arch}-strip -x -g ${pkgdir}/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/*.a
  done
}

# vim:set ts=2 sw=2 et:
