# Contributor: Slash <demodevil5[at]yahoo[dot]com>

pkgname=true-combat
pkgver=1.1
pkgrel=1
pkgdesc="True Combat: Elite (TCE) is a completely free, realistic, team-based modification for Enemy Territory."
url="http://www.truecombatelite.com/"
arch=('i686' 'x86_64')
license=('custom')
depends=('enemy-territory' 'libstdc++5')
[[ "$CARCH" == "x86_64" ]] && depends=('enemy-territory' 'lib32-libstdc++5')
makedepends=('unzip')
backup=('opt/enemy-territory/tcetest/server.cfg')
install=
source=('true-combat.desktop' 'true-combat.sh' 'true-combatded.sh' \
'http://tce.merlin1991.at/tc/TrueCombat11Full.zip' \
'http://dragons-perch.net/tce/tce049b_all_os_fixed.zip')
md5sums=('7100aaf825e599753b8a3b55628e8e78' 'f83243004e7791e139e6683c733df6e2'\
         '55a9e175b1793463a03b60e8d780c97d' '05890987546cf43f6135e8bae423523b'\
         'ed09515b41a4af92740c42c3053225a9')

build() {
    cd $srcdir

    # Unpack 0.49b Patch into 0.49 directory
    unzip -o $srcdir/tce049b_all_os_fixed.zip -d $srcdir/tcetest/

    # Remove Unneeded Files and Directories
    rm $srcdir/tcetest/._Mac_users_run_me_first.pkg
    rm $srcdir/tcetest/Mac\ Users\ Read\ Me\ First\ -\ Important\ Information.txt
    rm $srcdir/tcetest/{cgame_mac,cgame_mp_x86.dll,qagame_mp_x86.dll,ui_mac,ui_mp_x86.dll}
    rm -r $srcdir/tcetest/{Mac_users_run_me_first.pkg/,qagame_mac.bundle/}

    # Install License
    install -d $pkgdir/usr/share/licenses/$pkgname
    mv $srcdir/tcetest/EULA_TrueCombat_Elite.txt \
        $pkgdir/usr/share/licenses/$pkgname/

    # Copy Data to Package Directory
    install -d $pkgdir/opt/enemy-territory/
    mv $srcdir/tcetest $pkgdir/opt/enemy-territory/

    # Install Client Launcher Script 
    install -D -m 755 true-combat.sh \
        $pkgdir/usr/bin/true-combat

    # Install Server Launcher Script
    install -D -m 755 true-combatded.sh \
        $pkgdir/usr/bin/true-combatded

    # Install Icon File
    install -D -m 644 $pkgdir/opt/enemy-territory/tcetest/tce_icon_pc.ico \
        $pkgdir/usr/share/pixmaps/true-combat.ico

    # Install Desktop File
    install -D -m 644 true-combat.desktop \
        $pkgdir/usr/share/applications/true-combat.desktop
}
