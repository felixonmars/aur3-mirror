# Maintainer: tailinchu <use_my_id at gmail dot com>

pkgname=fcitx-mini
pkgver=4.2.7.116.g81777db
pkgrel=1
pkgdesc="fcitx without any input methods (git version)"
arch=('i686' 'x86_64')
url="http://github.com/fcitx/fcitx"
license=('GPL2')

makedepends=('git' 'intltool' 'cmake' 'iso-codes')
conflicts=('fcitx')
provides=('fcitx')

depends=('pango' 'libxinerama' 'gtk-update-icon-cache' 'shared-mime-info' \
    'hicolor-icon-theme' 'desktop-file-utils' 'libxkbfile' \
    'libxfixes' 'dbus-core' 'icu')

install=fcitx-gtk2.install

source=("git://github.com/fcitx/fcitx.git")
md5sums=('SKIP')

_gitroot="fcitx"

pkgver () {
    cd "$srcdir/$_gitroot"
    git describe --always | sed 's|-|.|g'
}


build() {
    mkdir -p "$srcdir/$_gitroot/build"
    msg "Starting make..."
    cd "$srcdir/$_gitroot/build"
    cmake ".." -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_OPENCC=OFF -DENABLE_TABLE=OFF -DENABLE_QT_IM_MODULE=ON -DENABLE_SNOOPER=OFF -DENABLE_GIR=OFF -DENABLE_ENCHANT=OFF
    make
}

package() {
    cd "$srcdir/$_gitroot/build"
    make PREFIX=/usr DESTDIR="$pkgdir" install

    # clean up
    rm -rf $pkgdir/usr/share/fcitx/{table,pinyin,inputmethod,py-enhance}
    rm -rf $pkgdir/usr/bin/{createPYMB,readPYBase,readPYMB}
    rm -rf $pkgdir/usr/share/man/man1/{createPYMB,readPYBase,readPYMB}.1.gz
    rm -rf $pkgdir/usr/include/fcitx/module/{pinyin,pinyin-enhanced}
    rm -rf $pkgdir/usr/share/fcitx/addon/{fcitx-pinyin.conf,fcitx-pinyin-enhance.conf,fcitx-qw.conf,fcitx-table.conf}
    rm -rf $pkgdir/usr/share/fcitx/configdesc/{fcitx-pinyin.desc,fcitx-pinyin-enhance.desc,fcitx-qw.desc,fcitx-table.desc}
    rm -rf $pkgdir/usr/lib/fcitx/{fcitx-pinyin.so,fcitx-pinyin-enhance.so,fcitx-qw.so,fcitx-table.so}
}

