# Contributor: Josh Rickmar <joshua.rickmar@gmail.com>
# Contributor: Joshua Gonzales <jgonzales6@ucmerced.edu>
# Contributor: Aliaksiej Artamona≈≠ <aliaksiej.artamonau@gmail.com>
# Maintainer: Titiwu  <matthias.busl@googlemail.com>

pkgname=lejos-nxj
pkgver=0.9.1
pkgrel=5
_betaver=3
pkgdesc="Tools for programming the Lego NXT brick in Java"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'APACHE')
url="http://lejos.sourceforge.net/"
depends=('java-environment' 'java-runtime'
         'bluez' 'libusb'
         'java-bluecove' 'java-commons-cli' 'java-bcel')
makedepends=('apache-ant')
source=(http://downloads.sourceforge.net/sourceforge/lejos/leJOS_NXJ_${pkgver}beta-${_betaver}.tar.gz
        70-lego.rules)
md5sums=('c1d76a89f075d6404b724ea88137c521'
         '7ca98482336766dabb6f1af97a5c4286')

install=${pkgname}.install

build() {
    # Compile libnxt
    cd ${startdir}/src/leJOS_NXJ_${pkgver}beta-${_betaver}/build
    ant || return 1
    cd ..
}
package() {
    cd ${startdir}/src/leJOS_NXJ_${pkgver}beta-${_betaver}

    # Install Files to /opt
    install -d -m755 ${pkgdir}/opt/lejos_nxj/{bin,lib} || return 1
    install -d -m755 ${pkgdir}/opt/lejos_nxj/lib/{nxt,pc} || return 1
    install -d -m755 ${pkgdir}/opt/lejos_nxj/lib/pc/{native,3rdparty} || return 1
    install -m644 -t ${pkgdir}/opt/lejos_nxj/lib/nxt \
                     lib/nxt/{*.jar,*.zip} || return 1
    install -m644 -t ${pkgdir}/opt/lejos_nxj/lib/pc \
                     lib/pc/{*.jar,*.zip} || return 1
    install -m644 -t ${pkgdir}/opt/lejos_nxj/lib/pc/3rdparty \
                     lib/pc/3rdparty/{*.jar,*.zip} || return 1
    install -m755 -t ${pkgdir}/opt/lejos_nxj/lib/pc/native \
                     lib/pc/native/linux/${CARCH}/*.so || return 1
    install -m755 -t ${pkgdir}/opt/lejos_nxj/bin \
                     bin/* || return 1
    # Copy docs (copied source zip instead)
    #cp -r   docs/* \
    #        ${pkgdir}/opt/lejos_nxj/docs/ || return 1
    # Link Scripts to /usr/bin
    install -d -m755 ${pkgdir}/usr/bin || return 1
    cd ${pkgdir}/opt/lejos_nxj/bin
    for i in nxj*; do ln -sf /opt/lejos_nxj/bin/${i} ${pkgdir}/usr/bin/; done
    

    # Copy udev-rule
    cd ${srcdir}
    install -d -m755 ${pkgdir}/etc/udev/rules.d || return 1
    install -m644 -t ${pkgdir}/etc/udev/rules.d \
                     ${srcdir}/70-lego.rules || return 1
}
