# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gvfs
pkgname=lib32-${_pkgbase}
pkgver=1.14.2
pkgrel=1
pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-avahi' 'lib32-fuse' 'lib32-libarchive' 'lib32-libcdio' 'lib32-libsoup-gnome' 'lib32-udisks2' 'lib32-libsecret' 'lib32-libbluray' 'lib32-gtk3' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib' 'lib32-smbclient' 'lib32-systemd' 'lib32-libgphoto2' 'lib32-libexif' 'lib32-dbus-glib' 'lib32-bluez' 'lib32-libimobiledevice' 'lib32-usbmuxd' 'lib32-libplist')
url="http://www.gnome.org"
options=(!libtool)
install=gvfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/1.14/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('c3bbdac4bc89de82a2ba373799da80d24fb6a038a9893d9b54e2c9429c878fbc')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --libdir=/usr/lib32 \
      --libexecdir=/usr/lib32/gvfs \
      --disable-bash-completion \
      --disable-documentation \
      --disable-hal \
      --disable-gdu
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  sed -e 's/^am__append_4/#am__append_4/' \
      -e 's/^am__append_5/#am__append_5/' \
      -i monitor/Makefile
  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  rm usr/lib32/gvfs/gvfsd-{smb,smb-browse,afc,afp,afp-browse,gphoto2,obexftp}
  rm -rf usr/{bin,include,share}
}

