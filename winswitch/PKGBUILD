# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: F0ruD <fzerorubigd[at]gmail[dot]com>

pkgname=winswitch
pkgver=0.12.5
pkgrel=1
pkgdesc="Window-Switch is a client server tool to start and control virtual desktops"
arch=('any')
url="http://winswitch.org/"
license=('GPL3')
depends=('pygtk' 'pycrypto' 'twisted' 'pyasn1' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('nautilus: nautilus sendto'
            'thunar: thunar sendto'
            'parti-all: xpra support'
            'nss-mdns: mdns support'
            'python-netifaces: firewall detection')
options=('!emptydirs')
install="$pkgname.install"
source=("${url}src/$pkgname-$pkgver.src.tar.bz2")
md5sums=('c1bcf9578506d137dd2a63d7c8a40e80')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i "s|\(env python\).*|\12|;s|\(bin/python\).*|\12|" \
         `grep -Erl "(env python|bin/python)" .`

  # replace libexec path
  sed -i "s|\(%s/\)libexec|\1share|" setup.py
  sed -i "s|\(usr/\)libexec|\1share|g" `grep -Erl /usr/libexec .`

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir/"
}
