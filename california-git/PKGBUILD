# Maintainer: Martin Poehlmann <mpdeimos@outlook.com>

pkgname=california-git
pkgver=0.1.0.r3.g56e7e33
pkgrel=1
pkgdesc="A new calendar application for GNOME 3. It's currently under heavy development."
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/California"
license=('LGPL2.1')
depends=('evolution-data-server' 'libgee')
makedepends=('automake' 'make' 'vala' 'git' 'intltool' 'gnome-common')
provides=('california')
conflicts=('california')
install=california.install
source=('git://git.gnome.org/california')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/california"
	git describe --tags --long --match california-* | sed -r 's/^california-//; s/-/.r/; s/-/./'
}

build() {
	cd "$srcdir/california"
	sed -i 's#californiadocdir = ${prefix}#californiadocdir = ${datadir}#g' Makefile.am
	./autogen.sh
	./configure \
		--prefix=/usr
	make
}

package() {
	cd "$srcdir/california"
	make install DESTDIR="$pkgdir"
	install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
