# Contributor : Thomas Baechler <thomas@archlinux.org>
# Contributor : Natrio <natrio@list.ru>

pkgname=nvidia-96xx
pkgver=96.43.20
_kbr="3.4"
_kmin="$_kbr.7"
_kless="3.5"
_extramodules=extramodules-${_kbr}-ARCH
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=10
pkgdesc="NVIDIA drivers for linux, 96xx branch."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=("linux>=$_kmin" "linux<$_kless" "nvidia-96xx-utils")
makedepends=("linux-headers>=$_kmin" "linux-headers<$_kless")
conflicts=('nvidia-173xx' 'nvidia')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run"
"generated.patch"
"switch_to.patch"
"acpixf.patch"
"patchlevel.patch")
options=(!strip)
md5sums=('875cc011385ea26ab5edd54a9b65a003'
'6ecffbafdffdd38756185377de4ede31'
'd5e6410dd8048a5b920214967e0af06e'
'd810975f7286f788061a5663e24bafb8'
'8b1ebaf6bd871d1f56b4b315f8bb800c')
[ "$CARCH" = "x86_64" ] && md5sums[0]='df3c87005487bdf54a6c5b783ff373db'

build() {
	cd $srcdir
	sh NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run --extract-only
	cd NVIDIA-Linux-$ARCH-${pkgver}-pkg0
	cd usr/src/nv/
	patch -p1 -i "${srcdir}/generated.patch" || exit 1
	patch -p1 -i "${srcdir}/switch_to.patch" || exit 1
        patch -p1 -i "${srcdir}/acpixf.patch" || exit 1
        patch -p1 -i "${srcdir}/patchlevel.patch" || exit 1
 	ln -s Makefile.kbuild Makefile
	make SYSSRC=/usr/lib/modules/${_kernver}/build module
}

package() {
	cd $srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/
	mkdir -p $pkgdir/usr/lib/modules/${_extramodules}/
	install -m644 nvidia.ko $pkgdir/usr/lib/modules/${_extramodules}/
        mkdir -p $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" $startdir/nvidia.install
        # gzip -9 module
        gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
