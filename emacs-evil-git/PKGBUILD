# Contributor: listdata <linusarver <at> gmail <dot> com>
pkgname=emacs-evil-git
pkgver=20111113
pkgrel=1
pkgdesc="Evil, an extensible Vi layer for Emacs"
arch=('any')
url="http://gitorious.org/evil/pages/Home"
depends=(emacs)
makedepends=('git')
license=('GPLv2')
provides=('emacs-evil')
conflicts=('emacs-evil')
install=$pkgname.install

_gitroot="git://gitorious.org/evil/evil.git"
_gitname="evil"

build() {
  cd $srcdir # $srcdir is an absolute path for convenience

  # checkout the latest GIT source code
  if [[ -d $_gitname ]]; then
    cd $_gitname
    msg "Sanitizing work tree..."
    git clean -dxf # remove all untracked files (this should be even more robust than running `make clean')
    git reset --hard # discard all changes to tracked files (if they had been tampered with somehow)
    msg "Pulling from official repo $_gitroot ..."
    git pull origin # get latest changes
  else
    git clone --depth 1 $_gitroot $_gitname
    cd $_gitname
  fi

  # inside build directory, run the Makefile
  msg "Running make..."
  make
}

package() {
  # create destination path
  install -d $pkgdir/usr/share/emacs/site-lisp/$_gitname
  # copy over files into path
  cp $srcdir/$_gitname/*.{el,elc} $pkgdir/usr/share/emacs/site-lisp/$_gitname
}
