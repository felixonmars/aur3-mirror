# Contributor: listdata <linusarver <at> gmail <dot> com>
pkgname=emacs-evil-git
pkgver=20111102
pkgrel=1
pkgdesc="Evil, an extensible Vi layer for Emacs"
arch=('any')
url="http://gitorious.org/evil/pages/Home"
depends=(emacs)
makedepends=('git')
license=('GPLv2')
provides=('emacs-evil')
conflicts=('emacs-evil')
install=$pkgname.install

_gitroot="git://gitorious.org/evil/evil.git"
_gitname="evil"

build() {
  # $srcdir is an absolute path!
  cd $srcdir

  # checkout the latest GIT source code
  msg "Connecting to the GIT server...."
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    cd ..
    msg "The local files are updated."
  else
    git clone --depth 1 $_gitroot $_gitname
  fi
  msg "GIT checkout done"

  # remove old build directory (if found)
  rm -rf $_gitname-build
  mkdir $_gitname-build

  # copy over git working tree files into build directory, excluding the heavy .git directory (and other useless .git files such as .gitignore)
  cd $_gitname
  ls -A | grep -v .git | xargs -d '\n' cp -r -t ../$_gitname-build

  # inside build directory, run the Makefile
  cd $srcdir/$_gitname-build
  msg "Starting make..."
  make
}

package() {
  # create destination path
  install -d $pkgdir/usr/share/emacs/site-lisp/$_gitname
  # copy over files into path
  cp $srcdir/$_gitname-build/*.{el,elc} $pkgdir/usr/share/emacs/site-lisp/$_gitname

  # remove build directory to save disk space
  rm -rf $srcdir/$_gitname-build
}
