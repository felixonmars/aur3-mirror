# Maintainer: Andreas B. Wagner <AndreasBWagner@pointfree.net>
_pkgname=dmenu
pkgname=$_pkgname-git
pkgver=0.488.55c9be0
pkgrel=1
pkgdesc="The latest git pull of dmenu."
arch=('i686' 'x86_64')
url="http://tools.suckless.org/dmenu/"
license=('MIT')
depends=('libxinerama' 'sh')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+git://git.suckless.org/dmenu/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd "$srcdir/$_pkgname"
  # applying patches
  if ls $SRCDEST/*\.diff 2> /dev/null;then for i in $SRCDEST/*\.diff;do msg "applying $i";git apply $i;done;fi
  # custom config
  if [ -e $SRCDEST/config.h ];then msg "using custom config.h";cp $SRCDEST/config.h .;fi
}

build() {
  cd "$srcdir/$_pkgname"
  make 
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir/" PREFIX="usr/" install
  install -Dm644 "$srcdir/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
