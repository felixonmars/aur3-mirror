# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gssdp
pkgver=0.10.0
pkgrel=1
pkgdesc="A GObject-based API for handling resource discovery and announcement over SSDP"
arch=('i686' 'x86_64')
url="http://gupnp.org/"
license=('LGPL')
depends=('libsoup>=2.26.1')
makedepends=('gobject-introspection>=0.6.4' 'gtk2>=2.12.0')
optdepends=('gtk2: required for gssdp-device-sniffer tool')
options=('!libtool')
source=(http://gupnp.org/sites/all/files/sources/$pkgname-$pkgver.tar.gz)
md5sums=('aec6a56ac1d4f8a4837da83f2d152556')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
