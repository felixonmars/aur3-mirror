# $Id: PKGBUILD 94450 2010-10-07 10:30:45Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgname=lib32-fuse
_pkgname=fuse
pkgver=2.8.5
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program. (multilib version)"
arch=('x86_64')
url="http://fuse.sourceforge.net/"
license="GPL2"
depends=('lib32-glibc')
makedepends=('gcc-multilib' 'pkg-config')
source=(http://downloads.sourceforge.net/$_pkgname/$_pkgname-$pkgver.tar.gz)
options=(!libtool)
sha1sums=('862320b56d6bc4ad5e7be2b1e0b5029166aae09b')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  ./configure --prefix=/usr --enable-lib \
			    --disable-util --bindir=/bin \
			    --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
			    --disable-static
	      
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
  rm -r ${pkgdir}/usr/include
}
