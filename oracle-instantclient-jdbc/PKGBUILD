# Contributor: Adam Nielsen <malvineous@shikadi.net>
# Contributor: Vitaliy Berdinskikh <skipper13@archlinux.org.ua>
# Contributor: Andrea Agosti <cifvts@gmail.com>
# Contributor: Viliam Pucik <viliam.pucik@gmail.com>
# Maintainer: Adam Nielsen <malvineous@shikadi.net>

_pkgname=instantclient-jdbc
pkgname=oracle-${_pkgname}
pkgver=12.1.0.1.0
pkgrel=1
pkgdesc="Additional support for XA, Internationalization, and RowSet operations under JDBC"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technetwork/database/features/instant-client/"
license=('custom:OTN')
depends=(oracle-instantclient-basic=$pkgver)
replaces=('instantclient-jdbc')
options=(!strip)

# These are the only files we want to include in the source package
source=(LICENSE)
md5sums=('2d62e22e4f2d7e6d220fafde0f692a7d')
sha256sums=('f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')

# Skip the next bit if we're building a source package for AUR, as we don't
# want these files included.
if [ $SOURCEONLY -ne 1 ]; then

	# Add the main file, depending on which architecture we're building for
	case "$CARCH" in
		i686)
			source[1]="${_pkgname}-linux-$pkgver.zip"
			md5sums[1]='be0c52004e52448726fb7a4e891c7c98'
			sha256sums[1]='eedd8405e63dd7568464af485b25a4cf5a2c8e0d39e918b2b7ca1ab6119200c8'
			;;
		x86_64)
			source[1]="${_pkgname}-linux.x64-$pkgver.zip"
			md5sums[1]='7b0c4111c6c4a7db062ae961dbc309b6'
			sha256sums[1]='5e5327f81b5484b5cb7347eb3fc19f38333390b0378ee225a154611c917214ce'
			;;
	esac

	# Warn the user if they haven't downloaded the file already
	if [ ! -f "${source[1]}" ]; then
		error "You need to download ${source[1]}"
		echo
		msg2 "This software cannot be downloaded automatically.  You will need to sign up"
		msg2 "for an Oracle account and download the software from Oracle directly.  Place"
		msg2 "the downloaded file in the same directory as the PKGBUILD and re-run makepkg."
		msg2 ""
		msg2 "The source .zip files can be downloaded from:"
		msg2 ""
		msg2 "i686   - http://www.oracle.com/technetwork/topics/linuxsoft-082809.html"
		msg2 "x86_64 - http://www.oracle.com/technetwork/topics/linuxx86-64soft-092277.html"
		echo
		exit 1
	fi

fi

package() {
	local basedir="$srcdir/instantclient_12_1"
	install -d "$pkgdir/usr/lib"
	install -m 755 -t "$pkgdir/usr/lib" "$basedir/"*.so
	install -m 644 -t "$pkgdir/usr/lib" "$basedir/"*.jar
}
