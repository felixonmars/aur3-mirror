# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Maintainer: Val√®re Monseur <valere dot monseur at ymail dot com>

pkgname=gtkam
pkgver=0.1.18
pkgrel=5
pkgdesc="A graphical GTK front-end to gPhoto2"
url="http://gphoto.sourceforge.net/proj/gtkam/"
arch=('i686' 'x86_64')
depends=('libgphoto2>=2.4.6' 'libexif-gtk' 'gdk-pixbuf2' 'libpng')
makedepends=('gimp')
license=('GPL')
source=(http://downloads.sourceforge.net/sourceforge/gphoto/${pkgname}-${pkgver}.tar.gz
        gtkam1.patch
        gtkam2.patch
        gtkam3.patch)
md5sums=('c30304ddc05c37d14f8b38d3a8b3c37a'
         'eef28ce7fceb27f92c08d5c59a75331d'
         '7c28b263ec83e70b2a96c3b110483727'
         'd703b0fabdd4d7d81a4fcd294d937c34')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/gtkam1.patch"
  patch -p1 -i "${srcdir}/gtkam2.patch"
  patch -p1 -i "${srcdir}/gtkam3.patch"

  # needed as patches modify the autotools files 
  autoreconf

  ./configure --prefix=/usr --sysconfdir=/etc \
    --without-bonobo --without-gnome \
    --disable-scrollkeeper

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  sed -i -e 's/^_//' "${pkgdir}/usr/share/applications/gtkam.desktop"
}
