# Maintainer: Nicolas Qui√©not <niQo @ AUR >
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=geis
pkgver=2.2.12
pkgrel=2
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/geis"
license=('GPL' 'LGPL')
depends=('grail' 'xorg-xinput' 'python2' 'dbus-core')
provides=('utouch-geis')
conflicts=('utouch-geis')
replaces=('utouch-geis')
options=('!emptydirs' '!libtool')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  PYTHON2VER=$(ls -l /usr/bin/python2 2>&1 | sed 's/^.*\(.\..\)/\1/g')
  sed -i "/python >= ${PYTHON2VER}/s/python \(>= .\..\)/python-${PYTHON2VER} \1/g" configure
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}/" install
  sed -i 's|\(#!.*bin.*python$\)|\12|g' "${pkgdir}"/usr/bin/{geisview,pygeis} "${pkgdir}/usr/lib/python2.7/site-packages/geisview/__init__.py"
}

md5sums=('dd9c574ca96ca08271d8a20bc35ff332')
