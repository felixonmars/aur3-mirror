# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ethereum-git
pkgver=20140806
pkgrel=1
pkgdesc="Next-generation distributed cryptographic ledger"
arch=('i686' 'x86_64')
depends=('boost' 'boost-libs' 'crypto++' 'gmp' 'leveldb' 'miniupnpc' 'ncurses' 'openssl' 'python2' 'qt5-base' 'qt5-webkit' 'snappy')
makedepends=('automake' 'cmake' 'gcc' 'git' 'glibc' 'yasm')
url="https://github.com/ethereum/cpp-ethereum"
license=('MIT')
source=(${pkgname%-git}::git+https://github.com/ethereum/cpp-ethereum)
sha256sums=('SKIP')
provides=('alethzero' 'cpp-ethereum' 'ethereum' 'lllc' 'sc' 'testeth' 'walleth')
conflicts=('alethzero' 'cpp-ethereum' 'ethereum' 'lllc' 'sc' 'secp256k1' 'testeth' 'walleth')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg 'Fixing cryptopp...'
  find . -type f -print0 | xargs -0 sed -i 's@#include <sha3.h>@#include <cryptopp/sha3.h>@g'
  find . -type f -print0 | xargs -0 sed -i 's@#include <sha.h>@#include <cryptopp/sha.h>@g'
  find . -type f -print0 | xargs -0 sed -i 's@#include <ripemd.h>@#include <cryptopp/ripemd.h>@g'

  msg 'Fixing CMakeLists.txt...'
  sed -i "s@jsonrpc/rpc.h@rpc/rpc.h@" CMakeLists.txt

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd ${pkgname%-git}

  msg 'Building...'
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_THREAD_LIBS_INIT=pthread ./
  make
}

package() {
  cd ${pkgname%-git}

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
  install -Dm 755 alethzero/alethzero "$pkgdir/usr/bin/alethzero"
  install -Dm 755 test/testeth        "$pkgdir/usr/bin/testeth"
  install -Dm 755 walleth/walleth     "$pkgdir/usr/bin/walleth"

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
