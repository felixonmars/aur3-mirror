# Contributor: ant32 <antreimer@gmail.com>
pkgname=mingw-w64-libsodium
pkgver=1.0.2
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl, the Networking and Cryptography library (mingw-w64)"
arch=('any')
url="https://github.com/jedisct1/libsodium"
license=('ISC')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-configure')
options=('staticlibs' '!strip' '!buildflags')
source=("http://download.dnscrypt.org/libsodium/releases/libsodium-${pkgver}.tar.gz")
md5sums=('dc40eb23e293448c6fc908757738003f')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir"/libsodium-${pkgver}
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/libsodium-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/${_arch}/lib/*.la
    rm "$pkgdir"/usr/${_arch}/bin/*.def
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

