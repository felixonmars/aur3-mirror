# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# Contributor: Matias Korhonen <webadmin@draco-vulgaris.net>
# Contributor: Dylon Edwards <deltaecho@archlinux.us>

pkgname=backintime
true && pkgname=(backintime backintime-gtk backintime-gnome backintime-kde4)
pkgver=1.0.34
pkgrel=1
arch=('any')
url="http://backintime.le-web.org/"
license=('GPL')
depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring' 'python2-secretstorage')
optdepends=('openssh: Support for remote backups over SFTP')
source=("http://backintime.le-web.org/download/backintime/backintime-$pkgver.tar.gz")
sha256sums=('2e1846e32e0649d4c0562c5c3821373354591b1ed22116066f1013c7775ea9d6')

build() {
  cd common
  ./configure
  make

  cd "$srcdir/notify"
  ./configure
  make

  for t in gnome kde4; do
    cd "$srcdir/$t"
    ./configure --no-check
    make
  done
}

package_backintime() {
  pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. CLI version."
  depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring' 'python2-secretstorage')
  optdepends=('backintime-gtk: GTK GUI version'
  'backintime-gnome: Gnome GUI version'
  'backintime-kde4: KDE4 GUI version'
  'sshfs: FUSE client based on the SSH File Transfer Protocol'
  'encfs: Encrypted filesystem in user-space')
  conflicts=('backintime-bzr')

  cd common
  make DESTDIR="$pkgdir" install

  cd "$srcdir/notify"
  make DESTDIR="$pkgdir" install

  sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

package_backintime-gnome() {
  pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. Gnome GUI version."
  depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring' 
  "backintime=$pkgver" 'python2-secretstorage' 'pygtk' 'python2-notify'
  'gnome-python' 'meld' 'gksu' 'gnome-session')
  optdepends=('sshfs: FUSE client based on the SSH File Transfer Protocol'
  'encfs: Encrypted filesystem in user-space')
  conflicts=('backintime-bzr' 'backintime-gtk')

  cd "$srcdir/gnome"
  make DESTDIR="$pkgdir" install
  sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

package_backintime-gtk() {
  pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. GTK GUI version."
  depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring'
  "backintime=$pkgver" 'python2-secretstorage' 'pygtk' 'python2-notify'
  'gnome-python' 'meld' 'gksu')
  optdepends=('sshfs: FUSE client based on the SSH File Transfer Protocol'
  'encfs: Encrypted filesystem in user-space')
  conflicts=('backintime-bzr' 'backintime-gnome')

  cd "$srcdir/gnome"
  make DESTDIR="$pkgdir" install
  sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

package_backintime-kde4() {
  pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. KDE4 GUI version."
  depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring'
  "backintime=$pkgver" 'xorg-utils' 'python2-pyqt' 'kdebindings-python2')
  replaces=('backintime-qt4')
  optdepends=('sshfs: FUSE client based on the SSH File Transfer Protocol'
  'encfs: Encrypted filesystem in user-space'
  'kdesdk-kompare: Diff/Patch Frontend')
  conflicts=('backintime-bzr')

  cd "$srcdir/kde4"
  make DESTDIR="$pkgdir" install
  sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
    -i "$pkgdir"/usr/bin/*
}

# Global pkgdesc for the AUR to pick up
pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault."

# vim:set ts=2 sw=2 et:
