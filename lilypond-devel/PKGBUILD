# Maintainer: Patrick McCarty <pnorcks at gmail dot com>

pkgname=lilypond-devel
pkgver=2.15.38
pkgrel=1
pkgdesc="An automated music engraving system (development version)"
arch=('i686' 'x86_64')
url="http://lilypond.org/"
license=('GPL')
depends=('fontconfig' 'freetype2' 'ghostscript'
         'guile' 'pango' 'python2')
makedepends=('bison' 'flex' 'fontforge' 'gettext' 'gsfonts'
             'perl' 'texinfo' 't1utils' 'texlive-core>=2011.23170')
optdepends=('netpbm: building HTML documentation'
            'imagemagick: building HTML documentation'
            'ttf-kochi-substitute: building HTML documentation'
            'texi2html>=1.82: building HTML documentation'
            'rsync: installing HTML documentation')
provides=("lilypond=$pkgver")
conflicts=('lilypond' 'lilypond-git')
install=$pkgname.install
source=(http://download.linuxaudio.org/lilypond/sources/v2.15/lilypond-$pkgver.tar.gz)
sha256sums=('9c1120774607b7c89c0319b0031aafcb9bf321a5af44de45e13fc175e214ede0')

build() {
  cd "$srcdir/lilypond-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  export PYTHON="python2"
  export PYTHON_CONFIG="python2-config"

  ./configure --prefix=/usr
  make all
}

package() {
  cd "$srcdir/lilypond-$pkgver"
  make DESTDIR="$pkgdir/" vimdir="/usr/share/vim/vimfiles" install
}

# vim:set ts=2 sw=2 et:
