# Maintainer:  cuihao <cuihao dot leo at gmail dot com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

# Original PKGBUILD (extra/xfconf):
# $Id: PKGBUILD 145609 2011-12-23 17:39:17Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfconf-devel
_pkgname=xfconf
pkgver=4.9.0
pkgrel=2
pkgdesc="A simple client-server configuration storage and query system (Development Version)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
# http://www.xfce.org/documentation/requirements
# keep dbus, see also http://bugs.archlinux.org/task/14536
depends=('libxfce4util-devel>=4.9.0' 'dbus-glib' 'dbus')
makedepends=('pkgconfig' 'perl-extutils-depends' 'perl-extutils-pkgconfig'
             'glib-perl' 'intltool' 'gtk-doc' 'chrpath')
options=('!libtool' '!emptydirs')
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.9/$_pkgname-$pkgver.tar.bz2)
sha1sums=('e00f9c5a00835cbff185454bbccbe92c91fc8ffd')

groups=('xfce4-devel')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" "$_pkgname-git")

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --enable-gtk-doc \
    --with-perl-options=INSTALLDIRS="vendor" \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove unneeded dynloader bootstrap file
  rm "$pkgdir/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.bs"

  # fix insecure rpath, http://bugs.archlinux.org/task/19980
  chrpath -d "$pkgdir/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.so"
}

# vim:set ts=2 sw=2 et: