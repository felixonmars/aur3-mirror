# Maintainer: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
pkgname=ninja-git
pkgver=20121105
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=('i686' 'x86_64')
url='http://martine.github.com/ninja/'
license=(Apache)
depends=('gcc-libs')
makedepends=('asciidoc' 'emacs' 'python2' 're2c')
optdepends=('emacs: for Emacs mode')
provides=('ninja')
conflicts=('ninja')
install=ninja-git.install
source=()
md5sums=()

_gitroot="git://github.com/martine/ninja.git"
_gitname="ninja"
_python="python2"

build() {
    cd "${srcdir}"

    msg "Connecting to GIT server...."
    if [[ -d "${_gitname}" ]]; then
        cd "${_gitname}" && git pull origin master
        msg "The local files are updated."
    else
        git clone "${_gitroot}" "${_gitname}"
    fi
    msg "GIT checkout done or server timeout"

    msg "Starting build..."
    rm -rf "${srcdir}/${_gitname}-build"
    git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
    cd "${srcdir}/${_gitname}-build"

    ${_python} bootstrap.py
    emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
    asciidoc doc/manual.asciidoc
}

package() {
    cd "${srcdir}/${_gitname}-build"

    # Main binary
    install -m755 -D ninja \
                     "${pkgdir}/usr/bin/ninja"

    # Manual
    install -m644 -D doc/manual.asciidoc \
                     "${pkgdir}/usr/share/doc/ninja/manual.asciidoc"
    install -m644 -D doc/manual.html \
                     "${pkgdir}/usr/share/doc/ninja/manual.html"

    # Emacs mode
    install -m644 -D misc/ninja-mode.el \
                     "${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.el"
    install -m644 -D misc/ninja-mode.elc \
                     "${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.elc"
}
