# Maintainer: Gergely Imreh <imrehgATgmailDOTcom>
pkgname=ninja-git
pkgver=20120430
pkgrel=1
pkgdesc="A small build system similar to make."
url="http://martine.github.com/ninja/"
arch=('i686' 'x86_64')
license=('Apache')
depends=('gcc-libs')
makedepends=('gcc' 'python2')
optdepends=('emacs: for Emacs mode')
provides=('ninja')
conflicts=('ninja')
install="${pkgname}.install"
source=()
md5sums=()

_gitroot="git://github.com/martine/ninja.git"
_gitname="ninja"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  if [ -d ${srcdir}/${_gitname}-build ] ; then
    rm -rf "${srcdir}/${_gitname}-build"
  fi
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Force to use python2
  rm -rf "${srcdir}"/python
  mkdir "${srcdir}"/python
  ln -s /usr/bin/python2 "${srcdir}"/python/python
  export PATH="${srcdir}"/python:$PATH

  ./bootstrap.py
} 

package() {
  cd "${srcdir}/${_gitname}-build"
  install -D ninja "${pkgdir}/usr/bin/ninja"
  install -m644 -D misc/ninja-mode.el \
	"${pkgdir}/usr/share/emacs/site-lisp/ninja-mode.el"
}
