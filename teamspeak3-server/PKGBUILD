# Maintainer: Malte Rabenseifner <mail@malte-rabenseifner.de>
# Adopted by: EnteEnteEnte <ducksource@duckpond.ch>

pkgname="teamspeak3-server"
pkgver="3.0.7.1"
pkgrel=1
pkgdesc="A proprietary VoIP conference software"
arch=('i686' 'x86_64')

if [ "${CARCH}" == "x86_64" ]; then
  _TSARCH='amd64'
  _TSMD5='968d55a1b63a4a0b425c75e7111bd96e'
else
  _TSARCH='x86'
  _TSMD5='146512a6e021851b7b4e3e5e4601eebd'
fi

url="http://www.teamspeak.com/"
license=('custom')
depends=('bash')
backup=('etc/conf.d/teamspeak3-server')
install="${pkgname}.install"
source=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/${pkgname}_linux-${_TSARCH}-${pkgver}.tar.gz"
        'teamspeak3-server.conf.d'
        'teamspeak3-server.rc.d'
        'teamspeak3-server.service')
md5sums=(${_TSMD5}
         '0c96de1dcdda230f5280aa30b5bb8084'
         '1a5e08ea76a0c452d95f01ce59eba578'
         'cc9fea525441ba3b78aaa585fa8ca33d')

package() {
    cd "${srcdir}"
    
    install -d "${pkgdir}/usr/share/doc/${pkgname}"
    install -d "${pkgdir}/usr/share/${pkgname}/sql"
    install -dm 0750 "${pkgdir}/var/lib/${pkgname}"
    install -dm 0750 "${pkgdir}/var/log/${pkgname}"
    
    install -Dm 644 "${pkgname}.conf.d" "${pkgdir}/etc/conf.d/${pkgname}"
    install -Dm 755 "${pkgname}.rc.d" "${pkgdir}/etc/rc.d/${pkgname}"
    install -Dm 644 "${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	
    install -Dm 644 "${pkgname}_linux-${_TSARCH}/libts3db_mysql.so" "${pkgdir}/usr/lib/libts3db_mysql.so"
    install -Dm 644 "${pkgname}_linux-${_TSARCH}/libts3db_sqlite3.so" "${pkgdir}/usr/lib/libts3db_sqlite3.so"
    install -Dm 755 "${pkgname}_linux-${_TSARCH}/ts3server_linux_${_TSARCH}" "${pkgdir}/usr/bin/${pkgname}"

    cp -a "${pkgname}_linux-${_TSARCH}/sql/"* "${pkgdir}/usr/share/${pkgname}/sql"
    cp -a "${pkgname}_linux-${_TSARCH}/doc/"* "${pkgdir}/usr/share/doc/${pkgname}"

    install -Dm 644 "${pkgname}_linux-${_TSARCH}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
