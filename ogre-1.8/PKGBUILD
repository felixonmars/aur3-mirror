# $Id$
# Maintainer: Benjamin Chretien <chretien at lirmm dot fr>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=('ogre-1.8')
pkgver=1.8.1
pkgrel=1
pkgdesc='Scene-oriented, flexible 3D engine written in C++'
arch=('i686' 'x86_64')
url='http://www.ogre3d.org'
license=('custom:MIT')
depends=('boost-libs' 'freeimage' 'freetype2' 'libxaw' 'libxrandr' 
         'nvidia-cg-toolkit' 'zziplib' 'ois' 'glu')
makedepends=('boost' 'cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa')
install=ogre-1.8.install
source=("http://downloads.sourceforge.net/ogre/ogre_src_v${pkgver//./-}.tar.bz2"
        "freetype.patch")
md5sums=('b85e3dcf370a46b3a8624d4fdd722d39'
         '65fd7049ed67142fdb363df821bd40e8')
conflicts=('ogre')
provides=('ogre')

build() {
  cd ${srcdir}/ogre_src_v${pkgver//./-}

  # patching CMake script for Freetype
  patch -p1 -i ${startdir}/freetype.patch

  # get a clean build dir
  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  # generate CMake Makefile
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOGRE_INSTALL_PLUGINS_HEADERS=TRUE \
    -DOGRE_INSTALL_SAMPLES=TRUE \
    -DOGRE_INSTALL_DOCS=TRUE \
    -DOGRE_INSTALL_MEDIA=TRUE \
    -DOGRE_INSTALL_SAMPLES_SOURCE=TRUE \
    -DCMAKE_BUILD_TYPE=Release # set =Debug for debugging version

  # compile
  make
}

package() {
  cd ${srcdir}/ogre_src_v${pkgver//./-}/build

  # install the bugger
  make DESTDIR=${pkgdir} install

  # fix up samples
  install -dm775 -o root -g users ${pkgdir}/opt/OGRE/samples/
  mv ${pkgdir}/usr/share/OGRE/*.cfg ${pkgdir}/opt/OGRE/samples/
  mv ${pkgdir}/usr/bin/SampleBrowser ${pkgdir}/opt/OGRE/samples/

  # make sample launcher
  echo "#!/bin/bash" > ${pkgdir}/usr/bin/OgreSampleBrowser
  echo "cd /opt/OGRE/samples && ./SampleBrowser" >> ${pkgdir}/usr/bin/OgreSampleBrowser
  chmod +x ${pkgdir}/usr/bin/OgreSampleBrowser

  # install license
  install -Dm644 ../Docs/License.html ${pkgdir}/usr/share/licenses/${pkgname}/license.html
}

# vim:set ts=2 sw=2 et:
