# Maintainer: PyroPeter <googlemail.com@abi1789>
# Contributor: Florian Pritz <flo@xssn.at>
 
_pkgbasename=libusb
pkgname=lib32-$_pkgbasename
pkgver=1.0.8
pkgrel=1
pkgdesc="Library to enable user space application programs to communicate with USB devices (32-bit)"
arch=(x86_64)
depends=('gcc-libs-multilib' $_pkgbasename)
makedepends=(gcc-multilib)
replaces=('lib32-libusb1')
url="http://libusb.sourceforge.net/"
license=('LGPL')
options=('!libtool')
source=("http://downloads.sourceforge.net/$_pkgbasename/$_pkgbasename-$pkgver.tar.bz2")
md5sums=('37d34e6eaa69a4b645a19ff4ca63ceef')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
 
  cd "$srcdir/$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}
 
package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
 
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
