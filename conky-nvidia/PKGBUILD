# Contributor ugaciaka <ugaciaka@gmail.com>
# Contributor tasos latsas <tlatsas2000@gmail.com>
# Contributor Kibbles
# Contributor Deffe
# Maintainer  Chris Sakalis <chrissakalis@gmail.com>

pkgname=conky-nvidia
_pkgname=conky
pkgver=1.8.1
pkgrel=5
pkgdesc='An advanced, highly configurable system monitor for X based on torsmo'
arch=('i686' 'x86_64')
url='http://conky.sourceforge.net/'
license=('custom')
replaces=('torsmo' 'conky')
conflicts=('conky')
provides=('conky')
makedepends=('pkg-config')
depends=('alsa-lib' 'libxml2' 'curl' 'wireless_tools' 'libxft' 'glib2' 'libxdamage' 'imlib2' 'nvidia' 'libxnvctrl')
backup=('etc/conky/conky.conf' 'etc/conky/conky_no_x11.conf')

source=("http://downloads.sourceforge.net/project/${_pkgname}/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.bz2"
        "http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/app-admin/conky/files/conky-1.8.1-nvidia-x.patch"
        'current-power.patch'
        'conky-1.8.1-newline.patch')

sha1sums=('9581419b1a53a8a7e37b0291cd4e55ab166d221a'
          '1e3dc3f2a8e44d62e25ab2ab22726e0e91ecf666'
          '2eeef0cc5a0b612826b2a7f0b43b7a3b7e10bf0b'
          '0f78afa15ba61eca4a14e52d62692f1ed053f317')


build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -p1 -i ../conky-1.8.1-newline.patch
  patch -p1 -i ../conky-1.8.1-nvidia-x.patch
  patch -p1 < ../current-power.patch # See FS#21984
  #patch src/conky.c < ../conky-1.8.1-newline.patch


  #Thank Deffe for the sed lines
  sed -i '
  /#include <curl\/types.h>/ c\
  //#include <curl/types.h>' src/eve.c
  sed -i '
  /#include <curl\/types.h>/ c\
  //#include <curl/types.h>' src/ccurl_thread.c

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-ibm \
    --enable-curl \
    --enable-rss \
    --enable-weather-xoap \
    --enable-imlib2 \
    --disable-lua \
    --enable-wlan \
    --enable-nvidia \

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
