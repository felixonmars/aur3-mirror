# Maintainer : Axilleas Pipinellis <axilleas@archlinux.info>
# Contributor: speed145a <speed145a(at)hotmail(dot)com>
# Contributor: Schnouki

pkgname=firefox-beta-bin
pkgver=23.0b1
pkgrel=1
pkgdesc='Standalone web browser from mozilla.org, beta build binaries.'
url='http://www.mozilla.com/en-US/firefox/fx/'
arch=('i686' 'x86_64')
depends=('gtk2' 'dbus-glib' 'desktop-file-utils' 'libxt' 'mime-types' 'nss' 'shared-mime-info')
license=('MPL' 'GPL' 'LGPL')
source=("http://ftp.mozilla.org/pub/firefox/releases/${pkgver}/linux-${CARCH}/en-US/firefox-${pkgver}.tar.bz2"
        "$pkgname.desktop" 
        "$pkgname-safe.desktop")

sha1sums=('9affb8120d7921131f7e85b1173fe1bc4abf8867'
'dd2afa6a6369b9c6ca549152d2df46794b3b385d'
'30990dafa5bea211ea2b84dadb56ac68324f48a3')

[[ "$CARCH" == "i686" ]] && sha1sums[0]="b38c8ef12309ed7e21e31a3b6102315523b5b62d"


package() {
	cd "${srcdir}"
	
	mkdir -p "${pkgdir}"/{usr/{bin,share/{applications,pixmaps}},opt}
	cp -r firefox "${pkgdir}"/opt/${pkgname}-${pkgver}

	ln -s /opt/${pkgname}-${pkgver}/firefox "${pkgdir}"/usr/bin/${pkgname}
	install -m644 "${srcdir}"/{${pkgname}.desktop,${pkgname}-safe.desktop} "${pkgdir}"/usr/share/applications/
	install -m644 "${srcdir}"/firefox/browser/icons/mozicon128.png "${pkgdir}"/usr/share/pixmaps/${pkgname}-icon.png
}
