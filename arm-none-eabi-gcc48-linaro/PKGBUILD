# Maintainer: Johnny Halfmoon <jhalfmoon@milksnot.com>

# Based on the summon-arm-toolchain script by Piotr Esden-Tempski
# https://github.com/esden/summon-arm-toolchain

pkgname=arm-none-eabi-gcc48-linaro
_relver=4.8
_reldate='2013.11'
# This is how I want to define the pkgver, but the AUR doesn't understand it
#pkgver=${_relver}_${_reldate//-/_}
pkgver=4.8_2013.11
pkgrel=1
_relverdate=${_relver}-${_reldate}
_newlibver=2.0.0
pkgdesc="The GNU Compiler Collection - cross compiler for ARM EABI (bare-metal) target."
arch=(i686 x86_64)
url="https://launchpad.net/gcc-linaro/4.8"
license=('GPL' 'LGPL')
groups=('arm-none-eabi-toolchain')
depends=('arm-none-eabi-binutils>=2.23.2' 'gmp' 'mpfr' 'libmpc')
makedepends=('flex' 'bison')
provides=('arm-none-eabi-gcc')
conflicts=('arm-none-eabi-gcc' 'cross-arm-none-eabi-gcc')
options=(!libtool !emptydirs !strip zipman docs)
source=(https://launchpad.net/gcc-linaro/${_relver}/${_relverdate}/+download/gcc-linaro-${_relverdate}.tar.xz
        ftp://sourceware.org/pub/newlib/newlib-${_newlibver}.tar.gz
        gcc-${_relverdate}-multilib2.patch
        gcc-${_relverdate}-no-exceptions.patch)
sha256sums=('c0b74f020aa1fc1b82e1c4aa2266b5c9b399f28ac217902e35b9ef83f82f6f09'
            '49c29e9129325e7c3b221aa829743ddcd796d024440e47c80fc0d6769af72d8a'
            '104b9aa652804a56338470983e6975af1d1e5440eb8bddae3a01a966d2b332cf'
            '3a1d6a17aba8a578ade3552a6d1beb66a129fb4f3268539596d39cbbef88ac6c')
_basedir=gcc-linaro-${_relverdate}

build() {
  cd ${srcdir}
  mv ${srcdir}/newlib-${_newlibver}/newlib   ${_basedir}
  mv ${srcdir}/newlib-${_newlibver}/libgloss ${_basedir}

  cd ${srcdir}/${_basedir}
  patch -Np0 -i "${srcdir}/gcc-${_relverdate}-multilib2.patch"
  patch -Np0 -i "${srcdir}/gcc-${_relverdate}-no-exceptions.patch"

  mkdir build
  cd build

  export CFLAGS="-O2"
  export CXXFLAGS="-O2"
  unset CPPFLAGS
  ../configure --with-pkgversion="Arch User Repository" \
               --with-bugurl="https://aur.archlinux.org/packages/arm-none-eabi-gcc-linaro" \
               --target=arm-none-eabi\
               --prefix=/usr \
               --libexecdir=/usr/lib \
               --datarootdir=/usr/share/arm-none-eabi-gcc \
               --enable-multilib \
               --enable-languages=c,c++ \
               --enable-interwork \
               --with-newlib \
               --with-gnu-as \
               --with-gnu-ld \
               --with-system-zlib \
               --disable-nls \
               --disable-shared \
               --disable-threads \
               --disable-libssp \
               --disable-libstdcxx-pch \
               --disable-libmudflap \
               --disable-libgomp \
               --enable-silent-rules \
               --with-headers=newlib/libc/include \
               --disable-newlib-supplied-syscalls
  make
}

package() {
  cd ${srcdir}/${_basedir}/build
  make -j1 DESTDIR=${pkgdir} install

  # libiberty.a conflicts with host version
  rm -f  $pkgdir/usr/lib/libiberty.a
}

# vim: set ts=2 sw=2 ft=sh et:
