# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Micael Soder <zoulnix@gmail.com>

pkgname=bombermaaan
pkgver=1.4.0.627
pkgrel=3
pkgdesc="A classic Bomberman-like game with multiplayer support"
arch=('i686' 'x86_64')
url="http://bombermaaan.sourceforge.net/"
license=('GPL3')
depends=('gcc-libs' 'sdl_mixer')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/${pkgname}/Bombermaaan_${pkgver}_20081018_src.tar.gz"
        "http://downloads.sourceforge.net/${pkgname}/Bombermaaan_${pkgver}_20081018_res.tar.gz"
        "http://media1.gamefront.com/moddb/2011/02/17/Bombermaan_levels_pack.zip"
        "00-makefiles.patch"
        "01-winreplace.patch"
        "${pkgname}.png"
        "${pkgname}.desktop"
        "install-levels.sh")
md5sums=('ee90bdd9654f0886c9585e6d785a1f52'
         '6c12f92ba80788f5958f7d9b65c81307'
         '9986c2abe340b8831eff6f6c4474ad0d'
         'fab1004d669acbddd1097cf1e63828d1'
         '4929ce96dfa32a96ea514097b11b9362'
         '40b94792c2f9f41ebde2d5c5ddf224c6'
         '831113d9b17d10a45d2409c3d992847c'
         '2123fad1b0d9d6c9513f56c6cda44049')

build() {
  cd "${srcdir}/Bombermaaan_${pkgver}_20081018_src"

  # Copy data files
  cp -r ../Bombermaaan_${pkgver}_20081018_res/* .

  # Apply patches
  patch -Np1 -i ../00-makefiles.patch
  patch -Np1 -i ../01-winreplace.patch

  make
}

package() {
  cd "${srcdir}/Bombermaaan_${pkgver}_20081018_src"

  make DESTDIR="${pkgdir}" prefix=/usr install

  # Install a level pack (by feillyne)
  # http://www.moddb.com/games/bombermaaan/addons/level-pack-151-levels
  mkdir -p "${pkgdir}/usr/share/${pkgname}/levels"
  install -m644 ../Levels/* "${pkgdir}/usr/share/${pkgname}/levels"
  install -m755 ../install-levels.sh "${pkgdir}/usr/bin/Bombermaaan-install-levels"

  # Install a desktop entry
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install a readme file
  install -Dm644 Readme.html "${pkgdir}/usr/share/doc/${pkgname}/README.html"
}
