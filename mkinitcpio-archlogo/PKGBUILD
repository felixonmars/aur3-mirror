# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=mkinitcpio-archlogo
pkgver=0.0.9
pkgrel=1
pkgdesc="Add colored Arch Linux ASCII art logo to early boot process"
arch=('any')
url="http://www.eworm.de/"
license=('GPL')
install=mkinitcpio-archlogo.install
source=('archlogo' 'archlogo_install')
    
build() {
	echo 'run_earlyhook() {' > ${srcdir}/archlogo_hook
	echo '	cat<<EOF' >> ${srcdir}/archlogo_hook
	sed -e 's/\\/\\\\/g' -e 's/`/\\`/g' < ${srcdir}/archlogo >> ${srcdir}/archlogo_hook
	echo 'EOF' >> ${srcdir}/archlogo_hook
	echo '}' >> ${srcdir}/archlogo_hook
}

package() {
	install -D -m644 ${srcdir}/archlogo_hook ${pkgdir}/usr/lib/initcpio/hooks/archlogo
	install -D -m644 ${srcdir}/archlogo_install ${pkgdir}/usr/lib/initcpio/install/archlogo
}

sha256sums=('3f4dd0d381a7f152d7af4a0293da94a241fef47da1cdba88712014b72abcf5d3'
            'b370fa8a4a54949e5f111e84e49a99953e20bd0f3fc62a321d3e87f0dc332298')
