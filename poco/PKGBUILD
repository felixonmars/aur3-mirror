# Maintainer: Robert Knauer <robert@privatdemail.net>
# Contributor: Rodrigo Grumiche Silva <grumiche at integrityit dot com dot br>
# Contributor: nozog
# Contributor: davidhjelm
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>
# Contributor: Nathan Owe < ndowens04 at gmail dot com>


pkgname=poco
pkgver=1.4.4
pkgrel=1
pkgdesc="C++ class libraries for network-centric, portable applications, complete edition"
arch=('i686' 'x86_64')
url="http://www.pocoproject.org/"
license=('custom:boost')
depends=('unixodbc' 'libmysqlclient' 'openssl')
makedepends=('gcc' 'make' 'unixodbc' 'libmysqlclient' 'openssl' 'chrpath')
source=(
  "${pkgname}-${pkgver}-all.tar.gz"::"https://sourceforge.net/projects/${pkgname}/files/sources/${pkgname}-${pkgver}/${pkgname}-${pkgver}-all.tar.gz/download"
)
sha256sums=(
  '07de4617e922930f180172d81aa2730f8f845d4816eab502598612619456771d'
)

build()
{
	cd $srcdir/$pkgname-$pkgver-all
	./configure --prefix=/usr --no-samples --no-tests
	make
}

package()
{
	cd $srcdir/$pkgname-$pkgver-all
	make DESTDIR=$pkgdir install
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
	#removing rpath information from binaries
	chrpath -d $pkgdir/usr/bin/cpspc
	chrpath -d $pkgdir/usr/bin/cpspcd
	chrpath -d $pkgdir/usr/bin/f2cpspd
	chrpath -d $pkgdir/usr/bin/f2cpsp
	#removing debugging libraries
	rm $pkgdir/usr/lib/libPoco*d.so*
}
