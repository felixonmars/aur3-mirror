# Maintainer: Klaas Boesche <aurkagebe@googlemail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=openssh-ldap
_pkgname=openssh
pkgver=5.9p1
pkgrel=2
pkgdesc='Free version of the SSH connectivity tools with Gentoo patch for public keys in LDAP'
arch=('i686' 'x86_64')
license=('custom:BSD')
url='http://www.openssh.org/portable.html'
backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config' 'etc/pam.d/sshd' 'etc/conf.d/sshd')
depends=('krb5' 'openssl' 'libedit')
optdepends=('x11-ssh-askpass: input passphrase in X without a terminal')
provides=('openssh')
conflicts=('openssh')
source=("ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${_pkgname}-${pkgver}.tar.gz"
        "http://distfiles.gentoo.org/distfiles/${_pkgname}-lpk-${pkgver}-0.3.14.patch.gz"
        'sshd.close-sessions'
        'sshd.confd'
        'sshd.pam'
        'sshd')
sha1sums=('ac4e0055421e9543f0af5da607a72cf5922dcc56'
          'eca403eece5fba829c0e5e6045315f583333deec'
          '954bf1660aa32620c37034320877f4511b767ccb'
          'ec102deb69cad7d14f406289d2fc11fee6eddbdd'
          '3413909fd45a28701c92e6e5b59c6b65346ddb0f'
          '21fa88de6cc1c7912e71655f50896ba17991a1c2')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	patch -p1 -i ../${_pkgname}-lpk-${pkgver}-0.3.14.patch
	autoconf
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/ssh \
		--sysconfdir=/etc/ssh \
		--with-ldap \
		--with-privsep-user=nobody \
		--with-md5-passwords \
		--with-pam \
		--with-mantype=man \
		--with-xauth=/usr/bin/xauth \
		--with-kerberos5=/usr \
		--with-ssl-engine \
		--with-libedit=/usr/lib \
		--disable-strip # stripping is done by makepkg
	
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm755 ../sshd "${pkgdir}"/etc/rc.d/sshd
	install -Dm644 ../sshd.pam "${pkgdir}"/etc/pam.d/sshd
	install -Dm644 ../sshd.confd "${pkgdir}"/etc/conf.d/sshd
	install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
	install -Dm755 ../sshd.close-sessions "${pkgdir}/etc/rc.d/functions.d/sshd-close-sessions" # FS#17389

	rm "${pkgdir}"/usr/share/man/man1/slogin.1
	ln -sf ssh.1.gz "${pkgdir}"/usr/share/man/man1/slogin.1.gz

	install -Dm755 contrib/findssl.sh "${pkgdir}"/usr/bin/findssl.sh
	install -Dm755 contrib/ssh-copy-id "${pkgdir}"/usr/bin/ssh-copy-id
	install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}"/usr/share/man/man1/ssh-copy-id.1

	sed \
		-e '/^#ChallengeResponseAuthentication yes$/c ChallengeResponseAuthentication no' \
		-e '/^#UsePAM no$/c UsePAM yes' \
		-i "${pkgdir}"/etc/ssh/sshd_config
}
