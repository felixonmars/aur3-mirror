# Maintainer: Janne Haapsaari <haaja@iki.fi>
# Contributor: Christopher Kroo√ü <didi2002 at web.de>
pkgname=gnome-shell-extension-dash-to-dock-git
pkgver=r261.6d725d1
pkgrel=1
pkgdesc="A gnome-shell extension that transforms the dash into an intellihide dock"
arch=('any')
url="https://github.com/micheleg/dash-to-dock/"
license=('GPL')
depends=('dconf')
makedepends=('git' 'gnome-common' 'intltool')
conflicts=('gnome-shell-extensions-git'
           'gnome-shell-extension-dash-to-dock')
provides=('gnome-shell-extension-dash-to-dock-git')
source=("$pkgname"::'git+https://github.com/micheleg/dash-to-dock.git')
md5sums=('SKIP')
install="gschemas.install"

pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" \
        "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$pkgname"
    sed -i 's/INSTALLBASE = ~\/.local\/share\/gnome-shell\/extensions/INSTALLBASE = ${DESTDIR}/'\
        Makefile 
}

build() {
    cd "$srcdir/$pkgname"
    make
}

package() {
    cd "$srcdir/$pkgname"
    mkdir -p "${pkgdir}/usr/share/gnome-shell/extensions" "${pkgdir}/usr/share/glib-2.0/schemas/"
    make DESTDIR=${pkgdir}/usr/share/gnome-shell/extensions install
    install -m644 "schemas/org.gnome.shell.extensions.dash-to-dock.gschema.xml" \
        "${pkgdir}/usr/share/glib-2.0/schemas/org.gnome.shell.extensions.dash-to-dock.gschema.xml"
}
