# Maintainer : josephgbr <rafael.f.f1 at gmail.com>
# Contributor : angvp <angvp at archlinux.org>

_pkgbase=libaio
pkgname=lib32-${_pkgbase}
pkgver=0.3.109
pkgrel=3
pkgdesc="The Linux-native asynchronous I/O facility (aio) library (32 bit)"
arch=('x86_64')
url="http://lse.sourceforge.net/io/aio.html"
license=('LGPL')
makedepends=('gcc-multilib')
source=(http://ftp.de.debian.org/debian/pool/main/liba/${_pkgbase}/${_pkgbase}_${pkgver}.orig.tar.gz)
md5sums=('435a5b16ca6198eaf01155263d855756')

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  # set lib32 environment in Makefiles
  # "/lib" -> "/lib32"
  sed -e 's/\/lib/\/lib32/g' -i	Makefile
  # "CFLAGS :=" -> "CFLAGS := -m32"
  sed -e '/^libdir/s/\/lib/\/lib32/' \
	  -e '/^CFLAGS/s/:= /&-m32 /' \
	  -i src/Makefile
  
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  rm -rf "${pkgdir}/usr/include"
}

