# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=libmlx4
pkgver=1.0.5
pkgrel=1
pkgdesc="Userspace driver for Mellanox ConnectX InfiniBand HCAs"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibverbs')
source=("http://www.openfabrics.org/downloads/mlx4/${pkgname}-${pkgver}.tar.gz")
# cksum start
md5sums=('1be6e25eca818093af45b33a1408465e')
sha512sums=('a4d7df3136373ea30f174fa48d36049bd128059d57932c18389b3ad8bd26eb918b382ad823e06b61ec031cd2edefcaa749b96d022f82e6bc653254ad8526ac7a')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}