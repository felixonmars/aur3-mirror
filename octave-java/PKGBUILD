# Author: Michael Goffioul <michael[d]goffioul[a]gmail[d]com>
# Submitter: David Runge <david[d]runge[a]frqrec[d]com>
# Maintainer: Clemens Buchacher <drizzd@aon.at>
#
# You can use the newpkg script from
# https://github.com/drizzd/octave-forge-archlinux to automatically generate
# new octave-forge PKGBUILDs or update existing ones. Patches welcome.
#

_pack=java
pkgname=octave-$_pack
pkgver=1.2.8
pkgrel=3
pkgdesc="Provides Java interface with OO-like Java objects manipulation"
arch=(any)
url="http://octave.sourceforge.net/$_pack/"
license=('GPL')
groups=('octave-forge')
depends=('octave>=3.2.0' 'java-runtime-headless')
makedepends=('java-environment')
optdepends=()
backup=()
options=()
install=$pkgname.install
_archive=$_pack-$pkgver.tar.gz
_archive_mod=$_pack-$pkgver-$pkgrel.tar.gz
source=("http://downloads.sourceforge.net/octave/$_archive"
	jvm_path.patch)
md5sums=('e94bcb73e21f2e8f3e5cf43c143ba029'
         'e24e6b41758eaa8cde78f8999201f4b9')

build() {
  cd "$srcdir"

  (
    cd "$_pack"
    patch -Np1 -i ../jvm_path.patch
  )
  tar czf "$_archive_mod" "$_pack"

  mkdir -p builddir
  octave -q -f --eval "pkg build -verbose -nodeps builddir $_archive_mod"
}

package() {
  mkdir -p "$pkgdir/usr/share/octave/packages"
  mkdir -p "$pkgdir/usr/lib/octave/packages"
  cp "$srcdir/builddir/$_archive" "$pkgdir/usr/share/octave/$_pack.tar.gz"
}
