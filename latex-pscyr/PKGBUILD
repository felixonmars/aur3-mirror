pkgname=latex-pscyr
pkgver=0.4d.beta9
pkgrel=1
pkgdesc='Cyrillyc fonts package for (La)TeX'
arch=(any)
url=ftp://ftp.vsu.ru/pub/tex/font-packs/pscyr/
license=(GPL)
install=pscyr.install
depends=(texlive-core)
source=(
 ftp://ftp.vsu.ru/pub/tex/font-packs/pscyr/0.4d-beta/PSCyr-0.4-beta9-tex.tar.gz 
 ftp://ftp.vsu.ru/pub/tex/font-packs/pscyr/0.4d-beta/PSCyr-0.4-beta9-type1.tar.gz
 )
md5sums=('ed0d2e1021994a95a690d73ccc67f581'
         '07de6a50f27325a5ef9ad2f164d9bf75')

package() {
  cd ${srcdir}/PSCyr

  TEXMF=${pkgdir}/usr/share/texmf-dist

  # dirs
  install -dm 755 $TEXMF/tex/latex/pscyr/doc \
                  $TEXMF/fonts/{tfm,vf,type1,afm}/public/pscyr \
		  $TEXMF/fonts/{{map,enc}/dvips,map/dvipdfm}/pscyr \
		  ${pkgdir}/var/lib/texmf/arch/installedpkgs/

  # map & enc
  { cat dvips/pscyr/pscyr.map
    echo 'fadr6t   AdvertisementPSCyr "T2AEncoding ReEncodeFont" <t2a.enc <adver4.pfb'
  } > $TEXMF/fonts/map/dvips/pscyr/pscyr.map
  install -m644 dvipdfm/base/pscyr2.map    $TEXMF/fonts/map/dvipdfm/pscyr
  install -m644 dvips/pscyr/t2{a,d}.enc    $TEXMF/fonts/enc/dvips/pscyr

  # fonts
  install -m644 tex/latex/pscyr/*          $TEXMF/tex/latex/pscyr
  install -m644 fonts/tfm/public/pscyr/*   $TEXMF/fonts/tfm/public/pscyr
  install -m644 fonts/vf/public/pscyr/*    $TEXMF/fonts/vf/public/pscyr
  install -m644 fonts/type1/public/pscyr/* $TEXMF/fonts/type1/public/pscyr
  install -m644 fonts/afm/public/pscyr/*   $TEXMF/fonts/afm/public/pscyr

  # docs
  install -m644 LICENSE doc/README.koi \
                doc/PROBLEMS ChangeLog     $TEXMF/tex/latex/pscyr/doc

  # maps
  echo "Map pscyr.map" > ${pkgname}.maps
  install -m644 ${pkgname}.maps ${pkgdir}/var/lib/texmf/arch/installedpkgs/
}
