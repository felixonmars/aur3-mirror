# Submitter:  Wessel Dirksen "p-we" <wdirksen at gmail dot com>
# Contributor: bas-t (responsible for hosting and further development of ffdecsawrapper)
# Contributor: Petr Vacek "vaca" (providing cardslot.conf for serial port SC readers)
# Contributor: J.P. van Best (coding workaround for procfs changes on kernels >= 3.10)

pkgname=ffdecsawrapper-git-v4l
pkgver=kernel_gitfdw_gitv4l
pkgrel=1
pkgdesc="A SoftCAM empowered virtual DVB interface (formally open-sasc-ng) - compiled with V4L libraries (GIT repo)"
url="https://github.com/bas-t/ffdecsawrapper.git"
arch=('i686' 'x86_64')
license=('GPL')
depends=('linux-headers')
makedepends=('git' 'patchutils' 'perl-proc-processtable' 'linux-headers>=3.10' 'linux-headers<3.11')
conflicts=('ffdecsawrapper' 'sasc-ng')
provides=('ffdecsawrapper')
backup=('etc/camdir/cardclient.conf' 'etc/conf.d/ffdecsawrapper' 'etc/camdir/cardslot.conf')
install='ffdecsawrapper.install'

source=('git://linuxtv.org/media_build.git' \
	'git://github.com/bas-t/ffdecsawrapper.git#branch=stable' \
	'cardclient.conf' 'ffdecsawrapper.rc' 'ffdecsawrapper.conf' \
	'ffdecsawrapper.lr' 'cardslot.conf' 'ffdecsawrapper.service' \
	'ffdecsawrapper.install' 'v4l.patch')

sha256sums=('SKIP'
            'SKIP'
            '7caba03e515fe55aced4aad831e72ef3c0e59a3cdcea7bcdf79f7bfff6fcec75'
            'f20b9a95247c827efb661546e06fae8a4565b2d95d1e8a577d11e9ddb590cfe8'
            '6f64a8a94178c075c793d2b8bbd36f221d5956bfa3eadc8352e78076a4d65de4'
            'f435344dc9f1c0ed7c2e0de74ec434cd73e2130a0d7589a4d38338e45925d8db'
            '436eb5a612aa3cb9e45bb2031429f3d41eb596ed65d18659d3bd708919c61253'
            '8e10e2306772427a31cba905374ab9c4616b2089b21b57df5ea058053414a84d'
            '2f68ae05a545036ff33e8df95b762052b0d93902e7d3ab9ae5b260d735b48f94'
            'eb64024d654308b80f0ec13596fddb7ca2a394e1e012702fb7614d31f2a04bfd')

pkgver() {

	cd $srcdir/ffdecsawrapper
 	_gitffdecsawrapper=`git describe --always | sed 's|-|.|g'`

	cd $srcdir/media_build 
	_gitv4l=`git describe --always | sed 's|-|.|g'`

	_kernel=`uname -r | sed -r 's/-/_/g'`

	echo "$_kernel"_"$_gitffdecsawrapper"_"$_gitv4l"
}

prepare() {

	cd $srcdir/media_build
	sed -i 's/system ("make") == 0 or die "build failed";/#system ("make") == 0 or die "build failed";/' build
	./build

	msg "Applying V4l patch..."
	cd $srcdir/media_build
	patch -p1 < $srcdir/v4l.patch

	make distclean || return 1
	make || return 1
}

build() {

	cd $srcdir/ffdecsawrapper/contrib/ffdecsawrapper      
	./configure --compiletype=release --dvb_dir=$srcdir/media_build/linux
}

package() {

	mkdir -p "$pkgdir/usr/bin"
	mkdir -p "$pkgdir/usr/lib/modules/`uname -r`/extra"
	mkdir -p "$pkgdir/etc/conf.d"
	mkdir -p "$pkgdir/etc/rc.d"
	mkdir -p "$pkgdir/etc/camdir"
	mkdir -p "$pkgdir/etc/logrotate.d/"
	mkdir -p "$pkgdir/usr/lib/systemd/system/"
      
	install -m0755 "$srcdir/ffdecsawrapper/contrib/ffdecsawrapper/ffdecsawrapper" "$pkgdir/usr/bin/"
	install -m0644 "$srcdir/ffdecsawrapper/contrib/ffdecsawrapper/dvbloopback.ko" "$pkgdir/usr/lib/modules/`uname -r`/extra"
	install -m0755 "$srcdir/ffdecsawrapper.rc" "$pkgdir/etc/rc.d/ffdecsawrapper"
	install -m0644 "$srcdir/ffdecsawrapper.conf" "$pkgdir/etc/conf.d/ffdecsawrapper"
	install -m0644 "$srcdir/cardclient.conf" "$pkgdir/etc/camdir/cardclient.conf"
	install -m0644 "$srcdir/cardslot.conf" "$pkgdir/etc/camdir/cardslot.conf"
	install -m0644 "$srcdir/ffdecsawrapper.lr" "$pkgdir/etc/logrotate.d/ffdecsawrapper-git-v4l.lr"
	install -m0644 "$srcdir/ffdecsawrapper.service" "$pkgdir/usr/lib/systemd/system/ffdecsawrapper.service"
}
