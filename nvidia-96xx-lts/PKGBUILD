#Contributor: Bjoern Franke <bjo@nord-west.org>

pkgname=nvidia-96xx-lts
pkgver=96.43.20
_kbr="3.0"
_kmin=${_kbr}
_kless="3.1"
_extramodules=extramodules-${_kbr}-lts
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=1
pkgdesc="NVIDIA drivers for linux-lts, 96xx branch."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=("linux-lts>=${_kmin}" "linux-lts<${_kless}" 
"nvidia-96xx-utils")
makedepends=("linux-lts-headers>=${_kmin}" 
"linux-lts-headers<${_kless}")
conflicts=('nvidia-173xx' 'nvidia')
provides=('nvidia-96xx')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0.run"
"generated.patch")
options=(!strip)
md5sums=('875cc011385ea26ab5edd54a9b65a003'
         '6ecffbafdffdd38756185377de4ede31')
[ "$CARCH" = "x86_64" ] && md5sums[0]='df3c87005487bdf54a6c5b783ff373db'

build() {
	cd $srcdir
	sh NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run --extract-only
	cd NVIDIA-Linux-$ARCH-${pkgver}-pkg0
	cd usr/src/nv/
	patch -p1 -i "${srcdir}/generated.patch" || exit 1
	ln -s Makefile.kbuild Makefile
	make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
	cd $srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/
	mkdir -p $pkgdir/lib/modules/${_extramodules}/
	install -m644 nvidia.ko $pkgdir/lib/modules/${_extramodules}/
        mkdir -p $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" $startdir/nvidia.install
        # gzip -9 module
        gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
