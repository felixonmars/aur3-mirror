# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gerbil
pkgver=0.1.5
pkgrel=1
pkgdesc="A new interactive visualization tool targeted at multispectral and hyperspectral image data, and a toolbox of common algorithms, e.g. for segmentation."
arch=(i686 x86_64)
url="http://sourceforge.net/p/gerbil/home/"
license=('GPL3')
depends=('boost-libs' 'opencv>=2.2' 'qt')
makedepends=('boost' 'cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('07f7506b882eefdba16707dc28f472f6'
         '95c325def4edada78a7841429ec19aa8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure.sh /usr

  # Removing RPATH
  sed -i "s|-Wl,-rpath.*||" gerbil/gerbil-gui/CMakeFiles/gerbil_gui.dir/link.txt

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 q$pkgname "$pkgdir/usr/bin/q$pkgname"

  # Install Desktop file and pixmaps
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 $pkgname-gui/logo/icon.png "$pkgdir/usr/share/pixmaps/q$pkgname.png"
}
