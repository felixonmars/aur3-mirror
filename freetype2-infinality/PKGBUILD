# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Contributor: Andre Fettouhi <A.Fettouhi@gmail.com>

pkgname=freetype2-infinality
pkgver=2.4.9
pkgrel=1
_pkgdate=20120403
_pkgrel=02
pkgdesc="TrueType font rendering library with infinality patch"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh')
optdepends=(
	'fontconfig-infinality: Infinality package for fontconfig (required)'
	'libxft-lcd: LibXft with custom LCD filter'
)
conflicts=('freetype2')
provides=("freetype2=$pkgver")
options=('!libtool')
install='install.sh'
backup=('etc/profile.d/infinality-settings.sh')
source=(
	"http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2"
	"http://www.infinality.net/fedora/linux/zips/freetype-infinality-${pkgver}-${_pkgdate}_${_pkgrel}-x86_64.tar.bz2"
	"freetype-2.2.1-enable-valid.patch"
)
md5sums=('77a893dae81fd5b896632715ca041179'
         '86aee24471768459e961e78fa31f3b88'
         '214119610444c9b02766ccee5e220680')

build() {
	rm -rf "${srcdir}/freetype-${pkgver}-build"
	cp -a "${srcdir}/freetype-${pkgver}" "${srcdir}/freetype-${pkgver}-build"
	cd "${srcdir}/freetype-${pkgver}-build"
	cat "$srcdir/freetype-"*.patch | patch -Np1
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/freetype-${pkgver}-build"
	make DESTDIR="${pkgdir}" install
	install -D -T "${srcdir}/infinality-settings.sh" "${pkgdir}/etc/profile.d/infinality-settings.sh"
}
