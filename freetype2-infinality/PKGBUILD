# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Contributor: Andre Fettouhi <A.Fettouhi@gmail.com>

pkgname=freetype2-infinality
pkgver=2.4.7
pkgrel=1
pkgdesc="TrueType font rendering library with infinality patch"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib')
conflicts=('freetype2')
provides=("freetype2=$pkgver")
options=('!libtool')
install=('infinality.install')
backup=('etc/profile.d/infinality-settings.sh')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        http://www.infinality.net/files/local.conf
        http://www.infinality.net/files/infinality-settings.sh
        freetype-2.2.1-enable-valid.patch
        http://www.infinality.net/files/freetype-add-subpixel-hinting-infinality-20110604-1.patch
        http://www.infinality.net/files/freetype-enable-subpixel-hinting-infinality-20100909-1.patch
        http://www.infinality.net/files/freetype-entire-infinality-patchset-20110604-1.patch)

md5sums=('dbadce8f0c5e70a0b7c51eadf2dd9394'
         '5418bfc078e4fc14a24c090f4a010fe9'
         '75b5be731bdeb3616aacdbf328c3b414'
         '214119610444c9b02766ccee5e220680'
         'ded8954240034a1e6ab2b2115b3ddb8f'
         'a88b1a66da44a90c1fdcdeaa39a2dc09'
         'c3428e393be31d8e4089452135b1ae46')

build() {
  cd "${srcdir}/freetype-${pkgver}"
  local patch_file
  for patch_file in "${source[@]:3}"; do
      patch -Np1 -i "${srcdir}/${patch_file##*/}"
  done

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -T "${srcdir}/infinality-settings.sh" "${pkgdir}/etc/profile.d/infinality-settings.sh"
  install -D -T -m644 "${srcdir}/local.conf" "${pkgdir}/etc/fonts/conf.avail/52-infinality.conf"
}
