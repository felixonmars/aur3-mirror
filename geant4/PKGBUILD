# Maintainer: Luis Sarmiento < lgsarmientop-ala-unal.edu.co >

pkgbase='geant4'
pkgname='geant4'
true && pkgname=('geant4' 
  'geant4-abladata' 
  'geant4-ledata' 
  'geant4-levelgammadata' 
  'geant4-neutronhpdata' 
  'geant4-neutronxsdata' 
  'geant4-piidata'
  'geant4-radioactivedata'
  'geant4-realsurfacedata')

pkgdesc="A simulation toolkit for particle physics interactions."
optdepends=('java-environment: for histogram visualizations and analysis'
	    'tcsh: for C Shell support'
	    'python: for G4Python support')
makedepends=('cmake>=2.6')
depends=('cmake' 'xerces-c' 'qt' 'lesstif' 'mesa')
replaces=('geant4-deb')
pkgver=9.5.1
_pkgver=9.5.p01
pkgrel=2

url="http://geant4.cern.ch/"
arch=('x86_64' 'i686')
license=('custom: http://geant4.cern.ch/license/') 
options=('!emptydirs')

source=("http://geant4.cern.ch/support/source/${pkgbase}.${_pkgver}.tar.gz"
  'geant4.install'
  'http://geant4.cern.ch/support/source/G4ABLA.3.0.tar.gz'
  'geant4-abladata.install'
  'http://geant4.cern.ch/support/source/G4EMLOW.6.23.tar.gz'
  'geant4-ledata.install' 
  'http://geant4.cern.ch/support/source/G4PhotonEvaporation.2.2.tar.gz'
  'geant4-levelgammadata.install' 
  'http://geant4.cern.ch/support/source/G4NDL.4.0.tar.gz'
  'geant4-neutronhpdata.install'
  'http://geant4.cern.ch/support/source/G4NEUTRONXS.1.1.tar.gz'
  'geant4-neutronxsdata.install' 
  'http://geant4.cern.ch/support/source/G4RadioactiveDecay.3.4.tar.gz'
  'geant4-radioactivedata.install'
  'http://geant4.cern.ch/support/source/G4PII.1.3.tar.gz'
  'geant4-piidata.install'
  'http://geant4.cern.ch/support/source/RealSurface.1.0.tar.gz'
  'geant4-realsurfacedata.install')
md5sums=('ef381c39a994943c0d10c87948032d3d'
         '116cde072b1db4c0840974b316f4be5d'
         'd7049166ef74a592cb97df0ed4b757bd'
         '63289a1e828ffda1f4493ddd7146bb3b'
         '98cdca1024bf168c8f8ca1deb40f43b9'
         '3ed72d09a1b08ed637a4aa8f690e9c4b'
         '8010e7ce8a92564e38dd3418e6040563'
         '2e64662a71430ec95a6c9b72893c190c'
         '6d23aff98e5706af1dd7fff397969f37'
         'e447b3dc5729a3104e18922dae3878de'
         '61ef3a05b56525db04e11820e3f603f1'
         '7c18d3325e306849b044d5d877ca1a55'
         'a5b681048584631608ab0965b33c7959'
         '7431f76ecbfb95dd7e9c45a549c9d12c'
         '05f2471dbcdf1a2b17cbff84e8e83b37'
         '23ec6e160de8af430e2a62ead7401759'
         '0dde95e00fcd3bcd745804f870bb6884'
         'b58dda88fc19fd560cf0caa735dfaf29')

build() {
	[ -d ${srcdir}/build ] && rm -rf ${srcdir}/build
	mkdir ${srcdir}/build
	cd ${srcdir}/build
	cmake -DGEANT4_USE_GDML=ON \
              -DGEANT4_USE_G3TOG4=ON \
              -DGEANT4_USE_QT=ON \
	      -DQT_QMAKE_EXECUTABLE=`which qmake` \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DGEANT4_INSTALL_EXAMPLES=ON \
              -DGEANT4_USE_RAYTRACERX=ON \
              -DGEANT4_INSTALL_DATA=OFF \
              -DGEANT4_USE_OPENGL_X11=ON \
              -DGEANT4_RAYTRACER_X11=ON \
              -DGEANT4_SYSTEM_CLHEP=OFF \
              -DGEANT4_XM=ON \
              -DCMAKE_INSTALL_LIBDIR=/usr/lib \
              ../${pkgbase}.${_pkgver}

	make
}

package_geant4(){
  install="${pkgname}.install"

  cd ${srcdir}/build
  make DESTDIR="${pkgdir}" install
  
  echo 'pushd /usr/bin &> /dev/null && source geant4.sh && popd &>/dev/null' > ${srcdir}/geant4.profile
  install -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/geant4.profile ${pkgdir}/etc/profile.d/geant4.sh
}

package_geant4-abladata(){
  pkgdesc="Data files for nuclear shell effects in INCL/ABLA hadronic model"
  depends=('geant4=9.5.1')
  _dataname="G4ABLA"
  pkgver=3.0
  pkgrel=1
  install="${pkgname}.install"

  install -d ${pkgdir}/usr/share/${pkgname}
  mv ${srcdir}/${_dataname}${pkgver} ${pkgdir}/usr/share/${pkgname}
}

package_geant4-ledata(){
  pkgdesc="Data files for low energy electromagnetic processes"
  depends=('geant4=9.5.1')
  _dataname="G4EMLOW"
  pkgver=6.23
  pkgrel=1

  install="${pkgname}.install"
  install -d ${pkgdir}/usr/share/${pkgname}
  mv ${srcdir}/${_dataname}${pkgver} ${pkgdir}/usr/share/${pkgname}
}
 
package_geant4-levelgammadata(){
  pkgdesc="Data files for photon evaporation"
  depends=('geant4=9.5.1')
  _dataname="PhotonEvaporation"
  pkgver=2.2
  pkgrel=1
  install="${pkgname}.install"

  install -d ${pkgdir}/usr/share/${pkgname}
  mv ${srcdir}/${_dataname}${pkgver} ${pkgdir}/usr/share/${pkgname}
}

package_geant4-neutronhpdata(){
  pkgdesc="Neutron data files with thermal cross sections"
  depends=('geant4=9.5.1')
  _dataname="G4NDL"
  pkgver=4.0
  pkgrel=1
  install="${pkgname}.install"

  install -d ${pkgdir}/usr/share/${pkgname}
  mv ${srcdir}/${_dataname}${pkgver} ${pkgdir}/usr/share/${pkgname}
}

package_geant4-neutronxsdata(){
  pkgdesc="Data files for evaluated neutron cross sections on natural composition of elements"
  depends=('geant4=9.5.1')
  _dataname="G4NEUTRONXS"
  pkgver=1.1
  pkgrel=1
  install="${pkgname}.install"

  install -d ${pkgdir}/usr/share/${pkgname}
  mv ${srcdir}/${_dataname}${pkgver} ${pkgdir}/usr/share/${pkgname}

}

package_geant4-piidata(){
  pkgdesc="Data files for shell ionisation cross sections"
  depends=('geant4=9.5.1')
  _dataname="G4PII"
  pkgver=1.3
  pkgrel=1
  install="${pkgname}.install"

  install -d ${pkgdir}/usr/share/${pkgname}
  mv ${srcdir}/${_dataname}${pkgver} ${pkgdir}/usr/share/${pkgname}

}

package_geant4-radioactivedata(){
  pkgdesc="Data files for radioactive decay hadronic processes"
  depends=('geant4=9.5.1')
  _dataname="RadioactiveDecay"
  pkgver=3.4
  pkgrel=1
  install="${pkgname}.install"

  install -d ${pkgdir}/usr/share/${pkgname}
  mv ${srcdir}/${_dataname}${pkgver} ${pkgdir}/usr/share/${pkgname}

}

package_geant4-realsurfacedata(){
  pkgdesc="Data files for measured optical surface reflectance"
  depends=('geant4=9.5.1')
  _dataname="RealSurface"
  pkgver=1.0
  pkgrel=1
  install="${pkgname}.install"

  install -d ${pkgdir}/usr/share/${pkgname}
  mv ${srcdir}/${_dataname}${pkgver} ${pkgdir}/usr/share/${pkgname}

}

#We need to redefine them so they are properly listed on the AUR :)
#apparently the AUR server takes the last defined variable for its interface
pkgver=9.5.1
pkgrel=2
pkgdesc="A simulation toolkit for particle physics interactions."
optdepends=('java-environment: for histogram visualizations and analysis'
	    'tcsh: for C Shell support'
	    'python: for G4Python support')
makedepends=('cmake>=2.6')
depends=('cmake' 'xerces-c' 'qt' 'lesstif' 'mesa')
replaces=('geant4-deb')
