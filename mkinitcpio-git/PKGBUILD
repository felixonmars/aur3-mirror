# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Dave Reisner <dreisner@archlinux.org> ([core] package)
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org> ([core] package)

pkgname=mkinitcpio-git
pkgver=20120725
pkgrel=1
pkgdesc="Modular initramfs image creation utility - git checkout"
arch=('any')
url="http://www.archlinux.org/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox' 'kmod' 'util-linux' 'libarchive' 'coreutils'
         'bash' 'findutils' 'grep' 'filesystem' 'file' 'gzip' 'systemd-tools')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('mkinitcpio')
conflicts=('mkinitcpio')
backup=('etc/mkinitcpio.conf')

_gitroot="git://projects.archlinux.org/mkinitcpio.git"
_gitname="mkinitcpio"

build() {
  msg 'Connecting to GIT server...'

  if [[ -d ${_gitname} ]]; then
    ( cd "${_gitname}" && git pull origin )
    msg 'The local files are updated.'
  else
    git clone "${_gitroot}"
  fi

  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  rm -rf ${_gitname}-build
  git clone ${_gitname}{,-build}
}

package() {
  cd ${_gitname}-build
  make DESTDIR="${pkgdir}" install
}

