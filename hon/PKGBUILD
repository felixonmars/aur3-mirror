# Contributor:	Slash		<yahoo.com: demodevil5>
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

#### To speed up package creation.
#### If you are planning to distribute
#### This on some custon repo, you might
#### want to use the defaut slow but
#### much better xz compression.
PKGEXT=.pkg.tar

pkgname=hon
pkgver=2.6.11
pkgrel=1
pkgdesc="Heroes of Newerth is a Real Time Strategy game heavily influcenced by DotA"
arch=('i686' 'x86_64')
url="http://www.heroesofnewerth.com/"
license=('custom:HoN')
depends=('libgl' 'libxml2' 'mesa' 'speex' 'curl' 'freetype2')
makedepends=('unzip')
options=(!strip)
install=hon.install
source=('hon.desktop' 'license' "http://dl.heroesofnewerth.com/HoNClient-${pkgver}.sh")

package() {
    ## Thx to messo8080 for reminding uss about the /tmp dir
    ## On new installs this dir is in ram by default ^^.

    if `pwd | grep -q "/tmp"`; then
	echo \
"You are trying to build this pkg in /tmp dir.
This is not so good idea as /tmp dir is in ram
by default. If you know that you have lots of
ram (>4G) and swap and want to continue building in
the current directory write yes and press ENTER.
Otherwise write no and press ENTER.
Continue?"
	read answer
		if [ "${answer}" == "no" ]; then
			false
		fi
    fi

    # Create Destination Directory
    mkdir -p "${pkgdir}/opt/"
    mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"

    # Extract Files
    sh -c "unzip \"${srcdir}/HoNClient-${pkgver}.sh\" data/* -d \"${pkgdir}/opt/\"; true"
    mv "${pkgdir}"/opt/{data,hon}

    # Install License
    cp "${srcdir}/license" "${pkgdir}/usr/share/licenses/${pkgname}/"

    # Install Desktop Shortcut
    install -D -m 0644 "${srcdir}/hon.desktop" \
        "${pkgdir}/usr/share/applications/hon.desktop"

    # Install Client Launcher
    mkdir -p "${pkgdir}/usr/bin"
    ln -s /opt/hon/hon.sh "${pkgdir}/usr/bin/hon"

    ## Remove un-needed libs
    ## fmodex and linpng aren't compitable with Arch
    ## freetype causes segfault on some systems O_o
    system_libs="libcurl.so.4  libgcc_s.so.1  libspeexdsp.so.1  libspeex.so.1  libstdc++.so.6 libfreetype.so.6"
    if [ "${CARCH}" == 'x86_64' ]; then
	## remove 32bit's
	rm -rf "${pkgdir}"/opt/hon/*-x86{,.so}
	rm -rf "${pkgdir}"/opt/hon/{editor,game}/*-x86{,.so}
	cd "${pkgdir}/opt/hon/libs-x86_64" && rm ${system_libs}
    else
        ## remove 64bit's
        rm -rf "${pkgdir}"/opt/hon/*-x86_64{,.so}
        rm -rf "${pkgdir}"/opt/hon/{editor,game}/*-x86_64{,.so}
	cd "${pkgdir}/opt/hon/libs-x86" && rm ${system_libs}
    fi

    # Set Ownership to root:games
    chown -R root:games "${pkgdir}/opt/hon"
    chmod -R g+w "${pkgdir}/opt/hon"
}


md5sums=('d3fa56f17b5eb21d4da01e8d628fbc97'
         'dd2c8cd0f5bcafa25cfb583a92e21aec'
         '3bdb04d6beb93a672f80ce79ca9f2a3d')
