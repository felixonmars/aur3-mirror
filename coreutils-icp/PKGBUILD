# Maintainer: Henrik J. Pedersen <HenrikJuul@gmail.com>
# Contributor: Palle Ravn <Ravnzon@gmail.com>
#
# This package is created from PKGBUILD and .install for coreutils-8.19 by:
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
#
# There seems to be a bug in the 'test-getlogin' test;
# http://lists.gnu.org/archive/html/bug-coreutils/2012-08/msg00021.html, all 
# other checks PASS on my build system, but I have commented out the checks for 
# this release, feel free to comment them in again, if you wish to see for 
# yourself. In case you wish to debug on the issue, the failing test is in
# gnulib-tests and can be run by 'make check-recursive' from that folder.

pkgname=coreutils-icp
pkgver=8.19
pkgrel=1
_icpVer=0.9
pkgdesc="GNU Coreutils with icp progressbar patch for copy"
arch=('x86_64')
license=('GPL3')
url="http://bitbucket.org/henrikjuul/icp"
depends=('glibc' 'pam' 'acl' 'gmp' 'libcap')
provides=('coreutils')
conflicts=('coreutils')
install=${pkgname}.install
source=(ftp://ftp.gnu.org/gnu/coreutils/coreutils-$pkgver.tar.xz
		http://bitbucket.org/henrikjuul/icp/downloads/coreutils-$pkgver-icp-$_icpVer.patch)
md5sums=('1a01231a2f3ed37c0efc073ccdda9375'
         '212687fcba95490c762efe7fe48a787f')

build() {
  patch -p1 < coreutils-$pkgver-icp-$_icpVer.patch
  cd ${srcdir}/coreutils-${pkgver}
  
  echo "Running autoreconf, this might take a while..."
  autoreconf

  ./configure --prefix=/usr --libexecdir=/usr/lib \
              --enable-no-install-program=groups,hostname,kill,uptime \
              --enable-pam
  make -j
}

#Left out, see note at top of file.
#check() {
#  cd ${srcdir}/coreutils-${pkgver}
#  make RUN_EXPENSIVE_TESTS=yes check
#}

package() {
  cd ${srcdir}/coreutils-${pkgver}
  make DESTDIR=${pkgdir} install

  cd ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/bin

  # binaries required by FHS
  _fhs=('cat' 'chgrp' 'chmod' 'chown' 'cp' 'date' 'dd' 'df' 'echo' 'false'
        'ln' 'ls' 'mkdir' 'mknod' 'mv' 'pwd' 'rm' 'rmdir' 'stty' 'sync'
        'true' 'uname')
  for i in ${_fhs[@]}; do
    ln -s ../usr/bin/$i ${pkgdir}/bin/$i
  done
}
