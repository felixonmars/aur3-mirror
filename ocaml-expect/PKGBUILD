# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Magnus Therning <magnus@therning.org>
# Contributor: Thomas Pani <thomas.pani@gmail.com>

pkgname=ocaml-expect
pkgver=0.0.2
pkgrel=3
pkgdesc="This is a simple implementation of 'expect' to help building unitary testing of interactive program."
arch=('i686' 'x86_64')
url="http://forge.ocamlcore.org/projects/ocaml-expect/"
license=('LGPL')
depends=('ocaml' 'ocaml-pcre' 'ocaml-extlib')
makedepends=('ocaml-findlib')
changelog=$pkgname.changelog
source=(https://forge.ocamlcore.org/frs/download.php/475/ocaml-expect-${pkgver}.tar.gz)
sha256sums=('178659fe466b1d43c32b183aadfd107d2a310b4e5f5f59019f3114129cd2cd7f')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    ocaml setup.ml -configure --prefix /usr --destdir ${pkgdir}
    ocaml setup.ml -build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    export OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)"

    mkdir -p $OCAMLFIND_DESTDIR
    ocaml setup.ml -install
}


