# Maintainer: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libproxy
pkgname=lib32-libproxy
pkgver=0.4.6
pkgrel=2
pkgdesc="A library that provides automatic proxy configuration management (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-gconf' 'libproxy')
makedepends=('gcc-multilib' 'lib32-gconf' 'cmake' 'automoc4')
#makedepends=('gcc-multilib' 'xulrunner' 'lib32-gconf' 'cmake' 'networkmanager' 'libwebkit' 'automoc4' 'python2' 'kdelibs' 'perl')
url="http://libproxy.googlecode.com"
source=(http://libproxy.googlecode.com/files/${_pkgbasename}-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('199c6b120baf1f7258a55f38d5ec74f5')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_SUFFIX=32 \
    -DLIBEXEC_INSTALL_DIR=/usr/lib32/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=yes \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DWITH_KDE4=no \
    -DWITH_MOZJS=no \
    -DWITH_NM=no \
    -DWITH_PERL=no \
    -DWITH_PYTHON=no \
    -DWITH_WEBKIT=no \
    ..
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
