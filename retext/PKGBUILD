# Maintainer:	Bartosz Chmura <chmurli@gmail.com>
# Contributor:	alfplayer

pkgname=retext
pkgver=2.1.0
pkgrel=1
pkgdesc="Simple text editor for Markdown syntax"
arch=('any')
url="http://sourceforge.net/p/retext/"
license=('GPL3')
depends=('python2'
	 'python2-qt'
	 'python-markdown'
	 'qt>=4')
optdepends=('python-gdata: needed if you want to use Google Docs export')
source=("http://sourceforge.net/projects/retext/files/ReText-${pkgver%.*}/ReText_${pkgver}.tar.gz"
	'retext.desktop')
options=(!strip docs)
md5sums=('d39c11dee9a7188ac5e3b4bcb5076c41'
         '82186876da0a52991d3f81324a52401d')

build () {

	# Use Python 2
	sed -i '1s/python/python2/' {retext.py,wpgen/wpgen.py}

	# Patch translations directory, issue 9 on sourceforge.net
	sed -i 's|RtTranslator\.load("retext_"+QLocale\.system()\.name(), "/usr/lib/retext")|RtTranslator.load("retext_"+QLocale.system().name(), "/usr/share/retext/locale")|' retext.py

}

package () {
	
	sharedir=${pkgdir}/usr/share
	
	install -d -m 755 ${pkgdir}/usr/bin ${sharedir}/{,wpgen,icons,mime/packages,applications,${pkgname}/locale}
	
	# Install scripts retext and wpgen
	install -m 755 ${pkgname}.py ${pkgdir}/usr/bin/retext
	install -m 755 wpgen/wpgen.py ${pkgdir}/usr/bin/wpgen

	# Install documentation
	mkdir doc	
	mv README doc/ 
	for dir in doc* ; do
		install -d -m 755 ${sharedir}/${pkgname}/${dir}
		install -m 644 ${dir}/* ${sharedir}/${pkgname}/${dir}/
	done

	# Install translation files
	install -m 644 *.qm ${sharedir}/${pkgname}/locale/
	
	# Install wpgen files
        install -m 644 wpgen/templates/* ${sharedir}/wpgen/
	
	# Install icon
	install -m 644 icons/* ${sharedir}/icons/
	
	# Install .desktop file
	install -m 644 retext.desktop ${sharedir}/applications/

}

