# Maintainer:	Bartosz Chmura <chmurli@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: alfplayer

pkgname=retext
pkgver=3.0.1
pkgrel=1
pkgdesc="A simple editor for Markdown and ReStructuredText markup languages"
arch=('any')
url="http://retext.sourceforge.net/"
license=('GPL3')
depends=('python2-docutils' 'python2-markdown' 'python2-qt' 'shared-mime-info')
makedepends=('imagemagick')
optdepends=('python2-pyenchant: for spell checking support'
            'python-gdata: for google docs access')
source=("http://sourceforge.net/projects/$pkgname/files/ReText-${pkgver%.*}/ReText_${pkgver}.tar.gz"
        "$pkgname.desktop" "x-retext-markdown.xml" "x-retext-rst.xml")
install="$pkgname".install
sha256sums=('86e9d3a3d61fe8fc9c7bbcdc2b8257ed31f68d435d85cdaaec5057954f0bea9c'
            '7782f4402fe62e48335c1bdd5c1fd5cbb48c408fabaf4f018a074f8b3eef838e'
            'b51611479d3224eec2b58264ed91ace3eccb502b7b806dae3e7a3ab4aab8c4b8'
            '6fef80cccb14813d9cc74810c397a6cd7831d1ca243536759a47c6e8b6cc977a')

package () {
  cd "$srcdir"

  # python2 fix
  sed -i '1s/python/&2/' $pkgname.py wpgen/wpgen.py

  # install bin scripts
  install -d -m 755 $pkgdir/usr/bin
  install -m 755 $pkgname.py $pkgdir/usr/bin/$pkgname
  install -m 755 wpgen/wpgen.py $pkgdir/usr/bin/wpgen

  # create /usr/share/* dirs
  SHAREDIR="$pkgdir/usr/share"
  install -d -m 755 $SHAREDIR/{applications,mime/packages,$pkgname/locale,wpgen}

  # install wpgen data
  install -m 644 wpgen/templates/* $SHAREDIR/wpgen/

  # install locales
  install -m 644 locale/*.qm $SHAREDIR/$pkgname/locale/

  # install icons
  ICONSDIR="$SHAREDIR/icons/hicolor"
  for size in 16 22 24 32 48 128; do
    install -d -m 755 $ICONSDIR/${size}x${size}/apps
    convert -resize $size icons/$pkgname.png $ICONSDIR/${size}x${size}/apps/$pkgname.png
  done
  install -d -m 755 $ICONSDIR/scalable/apps
  install -m 644 icons/$pkgname.svg $ICONSDIR/scalable/apps/$pkgname.svg

  # install mime files
  install -m 644 x-retext-{markdown,rst}.xml $SHAREDIR/mime/packages/

  # install desktop file
  install -m 644 $pkgname.desktop $SHAREDIR/applications/$pkgname.desktop
}

# vim:set ts=2 sw=2 et:
