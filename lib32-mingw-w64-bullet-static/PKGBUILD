pkgname=lib32-mingw-w64-bullet-static
pkgver=2.80
pkgrel=2
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and animation (mingw-w64, 32-bit, static)"
arch=(any)
url="http://www.bulletphysics.com/Bullet/"
license=("zlib")
makedepends=(mingw-w64-gcc cmake)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("http://bullet.googlecode.com/files/bullet-${pkgver}-rev2531.tgz"
"Toolchain-i686-w64-mingw32.cmake"
"Toolchain-x86_64-w64-mingw32.cmake")
md5sums=('d1eec0332110264a21d0fc0f2fb127c3'
         'da10ad00a161800e0b011a77c3efbf76'
         'adea9ccdeea1784c7cd30af195f2a8cb')

_architectures="i686-w64-mingw32"

build() {
  cd "$srcdir/bullet-$pkgver-rev2531"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir "build-${_arch}" && pushd "build-${_arch}"
    PATH="/usr/${_arch}/bin:$PATH" cmake \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr/${_arch} \
      -DCMAKE_INSTALL_LIBDIR:PATH=/usr/${_arch}/lib \
      -DINCLUDE_INSTALL_DIR:PATH=/usr/${_arch}/include \
      -DLIB_INSTALL_DIR:PATH=/usr/${_arch}/lib \
      -DSYSCONF_INSTALL_DIR:PATH=/usr/${_arch}/etc \
      -DSHARE_INSTALL_PREFIX:PATH=/usr/${_arch}/share \
      -DBUILD_SHARED_LIBS:BOOL=OFF \
      -DCMAKE_TOOLCHAIN_FILE=${srcdir}/Toolchain-${_arch}.cmake \
      -DBUILD_DEMOS:BOOL=OFF \
      -DINSTALL_LIBS:BOOL=ON \
      -DUSE_GLUT:BOOL=OFF \
      -DBUILD_EXTRAS:BOOL=ON \
      -DINSTALL_EXTRA_LIBS:BOOL=ON \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/bullet-$pkgver-rev2531/build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o name '*.bat' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    #rm -r "$pkgdir/usr/${_arch}/share"
  done
}