# Maintainer: Mika Fischer <mika.fischer@zoopnet.de>

pkgname=bliss
pkgver=20121002
pkgrel=3
pkgdesc="Simple and accurate album artwork finder"
arch=(any)
url="http://www.blisshq.com/"
license=(custom)
depends=(java-runtime)
makedepends=(java-runtime)
install=bliss.install
source=("http://www.blisshq.com/downloads/${pkgname}-install-${pkgver}.jar"
        'bliss.license'
        'bliss.service')
noextract=("${pkgname}-install-${pkgver}.jar")
md5sums=('1578f8827a4d5698926d74e7d45f2932'
         'd8627533bdb4ed98863a5d3af40914ad'
         '3201c720fe7b132c79457e093f7dc31e')

package() {
    mkdir -p "${pkgdir}/opt/bliss"
    cd "${pkgdir}/opt/bliss"
    java -jar ${srcdir}/${pkgname}-install-${pkgver}.jar -console
    if ! [ -e "${pkgdir}/opt/bliss/bin/bliss.sh" ]; then
        echo "ERROR: In the installer, you must keep the default path (${pkgdir}/opt/bliss)." && false
    fi
    install -D -m644 "${srcdir}/bliss.license" "${pkgdir}/usr/share/licenses/bliss/LICENSE"
    install -D -m644 "${srcdir}/bliss.service" "${pkgdir}/usr/lib/systemd/system/bliss.service"
    mkdir -p "${pkgdir}/usr/bin"
    ln -s "/opt/bliss/bin/bliss.sh" "${pkgdir}/usr/bin/bliss"
}
