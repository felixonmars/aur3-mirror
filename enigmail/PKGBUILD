# Maintainer: Thomas Jost <schnouki@schnouki.net>
# Contributor: Hinrich Harms <arch@hinrich.de>

pkgname=enigmail
pkgver=1.1.2
pkgrel=3
_tb_ver=3.1.9
_tb_libdir=thunderbird-3.1 # Adjust this to your thunderbird directory in /usr/lib
_comm_dir=comm-1.9.2
pkgdesc="OpenPGP security extension for Mozilla Thunderbird 3"
arch=('i686' 'x86_64')
url="http://enigmail.mozdev.org"
license=('MPL' 'GPL')
depends=('gnupg' 'thunderbird>=${_tb_ver}')
makedepends=('zip' 'pkg-config' 'python2' 'libidl2' 'wireless_tools' 'libnotify') # similar to Thunderbird
options=('!ccache' '!distcc')
source=("http://www.mozilla-enigmail.org/download/source/${pkgname}-${pkgver}.tar.gz"
        "http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/${_tb_ver}/source/thunderbird-${_tb_ver}.source.tar.bz2"
        "mozconfig")
md5sums=('7d329d5e8afbbb28214ca1995beb09c9'
         'e6917c419d3aaa8083a1d4b53fb80d8c'
         'ec786d343c0f951dfd71df0438c1f0ce')
sha1sums=('d29fce2b20a36d210b12c28fcbbee965007c95aa'
          '22b153102939430180ae1873ce15ef52286ff08d'
          'b2b9dcace7e54d6cbcaf34373a89bb80c131f241')

build() {
  # The build guide is available online:
  # http://enigmail.mozdev.org/download/source.php

  # Compile needed parts of Thunderbird
  cd $srcdir/$_comm_dir
  export MOZCONFIG="$srcdir/mozconfig"
  export PYTHON=python2
  make -f client.mk export
  cd mozilla/modules/libreg
  make
  cd ../../xpcom/string
  make
  cd ..
  make
  cd obsolete
  make

  # Now Enigmail
  mv $srcdir/enigmail $srcdir/$_comm_dir/mailnews/extensions/enigmail
  cd $srcdir/$_comm_dir/mailnews/extensions/enigmail
  ./makemake -r
  make
  
  # Create XPI archive
  make xpi
}

package() {
  # Install to the Thunderbird lib directory
  # If someone knows a better way to extract the em:id, please let me know :)
  cd $srcdir/$_comm_dir/mailnews/extensions/enigmail/package
  _emid=`grep em:id install.rdf | head -n1 | sed 's/.*>\(.*\)<.*/\1/'`
  mkdir -p $pkgdir/usr/lib/$_tb_libdir/extensions/$_emid
  cd $pkgdir/usr/lib/$_tb_libdir/extensions/$_emid
  # Look for the current (highest) version number of the XPI
  _xpidir=$srcdir/$_comm_dir/mozilla/dist/bin
  _xpifullpath=`ls $_xpidir/enigmail-*-linux-$CARCH.xpi | sort | sed -n '$p'`
  bsdtar -x -f $_xpifullpath

  # Fix permissions
  find -type d -exec chmod 0755 \{\} \+
  find -type f -exec chmod 0644 \{\} \+
  find -name '*.so' -exec chmod 0755 \{\} \+
}
