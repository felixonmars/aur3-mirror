# Maintainer: JokerBoy <jokerboy at punctweb dot ro>

pkgname=komodo-ide
pkgver=8.0.2_12590
pkgrel=1
pkgdesc="Proprietary version of ActiveState Komodo Edit - Based on XUL"
arch=('i686' 'x86_64')
url="http://www.activestate.com/komodo-ide"
license=('MPL')
depends=('glibc>=2.4' 'libjpeg>=6.2' 'gcc-libs' 'gtk2')
optdepends=('xterm: for komodo-toolbox')
[ "$CARCH" = "i686" ] && _arch="x86" && md5sums=('4167632e6623aa57021077c25484f469')
[ "$CARCH" = "x86_64" ] && _arch="x86_64" && md5sums=('f2f6f769e048f59990f2cf754360dcd0')
source=("http://downloads.activestate.com/Komodo/releases/${pkgver%_*}/Komodo-IDE-${pkgver/_/-}-linux-${_arch}.tar.gz")
install=komodo-ide.install
options=(!strip)

package() {
  cd "${srcdir}/Komodo-IDE-${pkgver/_/-}-linux-${_arch}"
  ./install.sh -s -I "${pkgdir}/opt/${pkgname}" --dest-dir "/opt/${pkgname}"
  install -d "${pkgdir}"/usr/{bin,share/applications}
  ln -s "/opt/${pkgname}/share/desktop/komodo-ide-8.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  ln -sf "/opt/${pkgname}/lib/mozilla/komodo" "${pkgdir}/opt/${pkgname}/bin/komodo"
  ln -s "/opt/${pkgname}/lib/mozilla/komodo" "${pkgdir}/usr/bin/${pkgname}"
  grep -r -lZ "$pkgdir" "$pkgdir" 2>/dev/null | while read -rd '' file; do sed -i "s|$pkgdir||g" "$file"; done
}
