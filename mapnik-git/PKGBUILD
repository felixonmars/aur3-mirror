# Maintainer: AJ Ashton <aj.ashton@gmail.com>
pkgname=mapnik-git
pkgver=20120419
pkgrel=1
pkgdesc="Free toolkit for developing mapping applications."
arch=('i686' 'x86_64')
url="http://mapnik.org"
license=('LGPL')
depends=('freetype2' 'libxml2' 'icu' 'boost' 'pycairo' 'cairomm' 'gdal' 'curl')
makedepends=('git' 'pkgconfig' 'python2')
provides=(mapnik)
conflicts=(mapnik)

_gitroot=git://github.com/mapnik/mapnik.git
_gitname=mapnik

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build" --depth=1
  cd "$srcdir/$_gitname-build"

  # Patch SConstruct so libs end up in /usr/lib and not in /usr/lib64 on x86_64
  msg "Fixing LIBDIR_SCHEMA..."
  cd "$srcdir/$_gitname-build"
  sed -i -e "/LIBDIR_SCHEMA=/s/lib64/lib/" SConstruct

  ## CONFIGURATION OPTIONS
  ## - Adjust JOBS to 1 per 2GB of available memory for optimal compile time.
  ## - Set RENDERING_STATS=true for extremely verbose rendering statistics.
  python2 scons/scons.py configure \
    JOBS=1 \
    RENDERING_STATS=false \
    INPUT_PLUGINS=all \
    PREFIX=/usr

  python2 scons/scons.py
}

package() {
  cd "$srcdir/$_gitname-build"
  python2 scons/scons.py install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
