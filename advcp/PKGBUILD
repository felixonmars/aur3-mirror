# Maintainer: Adrien RAFFIN <raffinadrien@gmail.com> 

pkgname=advcp
pkgver=0.5
pkgrel=1

_pkgname=coreutils
_pkgver=8.20
pkgdesc="'cp' and 'mv' utilities with progress bar patches" 
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('glibc' 'pam' 'acl' 'gmp' 'libcap')
provides=('acp' 'amv')
install=${pkgname}.install
source=(ftp://ftp.gnu.org/gnu/$_pkgname/$_pkgname-$_pkgver.tar.xz{,.sig}
        advcpmv-${_pkgver}_${pkgver}.patch)
md5sums=('3d69af8f561fce512538a9fe85f147ff'
         '7e6076e4e0961d24c38cf8685efca35b'
         '2869f9d029bc25a1f10397dda5a50efd')

build() {
  cd ${srcdir}/${_pkgname}-${_pkgver}
    
  patch -p1 -i ${srcdir}/advcpmv-${_pkgver}_${pkgver}.patch

  ./configure --prefix=/usr --libexecdir=/usr/lib \
              --enable-no-install-program=groups,hostname,kill,uptime \
              --enable-pam
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${_pkgver}
  install -D src/cp ${pkgdir}/usr/bin/acp
  install -D src/mv ${pkgdir}/usr/bin/amv
}
