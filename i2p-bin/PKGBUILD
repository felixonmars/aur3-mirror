# Maintainer: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Download sources from within i2p
_i2p_fetch=0
###########################################################

pkgname=i2p-bin
pkgver=0.9.2
pkgrel=1
pkgdesc="A distributed anonymous network (pre-compiled binary)"
url="http://www.i2p2.de"
license=('GPL2')
arch=('i686' 'x86_64')
install='i2p.install'
depends=('java-runtime' 'bash')
conflicts=('i2p' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
provides=('i2p')
noextract=("i2pinstall_${pkgver}.jar")
source=("https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar"
        "https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar.sig"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2pinstall_${pkgver}.jar"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2pinstall_${pkgver}.jar.sig"
        "i2prouter.service")
sha256sums=('7eb1b62bdb955691dfd645acc2172fe7947266e35f201273f702272d57b80a70'
            'c399c37989ebc670fdecbbbd354d8b1007d654d5df2633129c9b5e13ed8d18b9'
            '77105f3a4c52467fa5e70c3b2cf6a8435744760c53feb1358773191e746207b1')

[[ "$_i2p_fetch" = 1 ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2pinstall_${pkgver}.jar"
            "http://echelon.i2p/${pkgver}/i2pinstall_${pkgver}.jar.sig"
            "i2prouter.service")
}

package() {
    cd "$srcdir"
    echo "INSTALL_PATH=${pkgdir}/opt/i2p" >install.properties
    java -jar i2pinstall_${pkgver}.jar -options install.properties

    sed -i $pkgdir/opt/i2p/{i2prouter,wrapper.config,runplain.sh,eepget} \
        -e "s:$pkgdir/opt/i2p:/opt/i2p:g"
    sed -i $pkgdir/opt/i2p/i2prouter \
        -e 's:#RUN_AS_USER=.*:RUN_AS_USER=i2p:' \
        -e 's:I2P_CONFIG_DIR=.*:I2P_CONFIG_DIR="$I2P/.i2p":'

    install -d "${pkgdir}/usr/share/licenses/i2p/"
    install -Dm644 ${srcdir}/i2prouter.service       "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1    "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/man/i2prouter.1 "${pkgdir}/usr/share/man/man1/i2prouter.1"
    mv ${pkgdir}/opt/i2p/LICENSE.txt                 "${pkgdir}/usr/share/licenses/i2p/"
    mv ${pkgdir}/opt/i2p/licenses/*                  "${pkgdir}/usr/share/licenses/i2p/"

    rm -r ${pkgdir}/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,licenses,man}
    chmod -R 750 ${pkgdir}/opt/i2p
    chmod -x ${pkgdir}/opt/i2p/*.config
}
