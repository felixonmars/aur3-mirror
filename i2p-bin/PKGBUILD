# Contributor: DaNiMoTh <jjdanimoth@gmail.com>

pkgname=i2p-bin
pkgver=0.8.11
pkgrel=1
pkgdesc="A distributed anonymous network"
url="http://www.i2p2.de"
license=('GPL')
depends=('java-runtime')
arch=('i686' 'x86_64')
install=i2p.install
source=(http://mirror.i2p2.de/i2pinstall_${pkgver}.exe 
        i2prouter i2prouter.service)
conflicts=('i2p' 'i2p-source')
provides=('i2p')

build() {
  cd $srcdir
  cat >install.properties << EOC
INSTALL_PATH=$pkgdir/opt/i2p
EOC
}

package() {
  cd $srcdir

  java -jar i2pinstall_${pkgver}.exe -options install.properties || return 1

  sed -i "s|$pkgdir/opt/i2p|/opt/i2p|g" $pkgdir/opt/i2p/i2prouter $pkgdir/opt/i2p/wrapper.config $pkgdir/opt/i2p/runplain.sh $pkgdir/opt/i2p/eepget || return 1
  sed -i 's/#RUN_AS_USER=/RUN_AS_USER=i2p/' $pkgdir/opt/i2p/i2prouter || return 1

  rm $pkgdir/opt/i2p/.installationinformation || return 1
  rm -r $pkgdir/opt/i2p/Uninstaller || return 1

  install -Dm755 $srcdir/i2prouter $pkgdir/etc/rc.d/i2prouter || return 1
  install -Dm644 $srcdir/i2prouter.service $pkgdir/lib/systemd/system/i2prouter.service
}

md5sums=('737796ad688aafd062a22d59e52092f8'
         'ac9fa57b32f786f371bdf4dbd4931105'
         '57352bccc56bb4b109f969806a0bcd32')
