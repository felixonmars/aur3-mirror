# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gamgi
pkgver=0.15.5
pkgrel=1
pkgdesc="General Atomistic Modelling Graphic Interface is a free package to construct, view and analyse atomic structures."
arch=('i686' 'x86_64')
url="http://www.gamgi.org/"
license=('GPL')
depends=('gtkglext' 'hicolor-icon-theme')
options=('!emptydirs')
install="$pkgname.install"
source=("${url}src/${pkgname}-all-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('e1acb64b9a9ad286435b2834f6c887aa'
         'b558e09451368776b70e168c8f8e6c13')

build() {
  cd "${srcdir}/${pkgname}${pkgver}/src"

  # strip any lib64 reference
  sed -i "s|lib64|lib|g" make_local

  make
}

package() {
  cd "${srcdir}/${pkgname}${pkgver}"

  install -d "${pkgdir}"/usr/{bin,share/{{doc/,}${pkgname},man/man1}}

  # Main App
  install -Dm755 src/${pkgname} "${pkgdir}/usr/bin"

  # Icons and desktop file
  for i in 16 22 32 48; do
    install -Dm644 doc/icon/${pkgname}${i}.png \
      "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
  done

  install -Dm644 ../${pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 doc/icon/${pkgname}316.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # Man page and docs
  install -Dm644 doc/man/page "${pkgdir}/usr/share/man/man1/$pkgname.1"
  cp -a doc/*[^man] "${pkgdir}/usr/share/doc/${pkgname}"

  # Data files and examples
  cp -a dat/* "${pkgdir}/usr/share/${pkgname}"
}
