# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gfeedline
pkgver=1.5
pkgrel=1
pkgdesc="A social networking client that supports Twitter"
arch=('any')
url="http://code.google.com/p/gfeedline/"
license=('GPL3' 'MIT')
depends=('gtk3' 'libwebkit3' 'python-beautifulsoup' 'python2-oauth'
         'python2-gobject' 'python2-dateutil' 'python2-pyopenssl'
         'pyxdg' 'hicolor-icon-theme' 'dconf' 'twisted')
makedepends=('python-distutils-extra')
install="$pkgname.install"
source=("http://gfeedline.googlecode.com/files/$pkgname-$pkgver.tar.gz")
md5sums=('98dd3e45539ed646a56a3fe65f9b6b4c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/"

  # python2 fix
  find "$pkgdir" -name twitter.py -exec \
    sed -i 's/python/&2/' {} \;

  # fix perms
  chmod -R go+r "$pkgdir/usr/share/$pkgname/html"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
