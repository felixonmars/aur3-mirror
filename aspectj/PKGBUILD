# Maintainer: Michele Damiano Torelli <me_AT_mdtorelli_DOT_it>

pkgname=aspectj
pkgver=1.7.1
pkgrel=1
pkgdesc="A seamless aspect-oriented extension to the Java programming language"
arch=('any')
url="http://www.eclipse.org/aspectj"
license=('EPL')
depends=('java-runtime')
makedepends=('java-environment')
source=(${pkgname}-${pkgver}.jar::"http://www.eclipse.org/downloads/download.php?file=/tools/${pkgname}/${pkgname}-${pkgver}.jar&r=1" aj aj5 ajbrowser ajc ajdoc)
md5sums=('1de6fb7db9fc1091e566940db18ab3ed'
         '88179a58a94840f96ff3a2b8bf164213'
         'fdbaf1a6c01e3a9cde8646b95aafea1d'
         '44069cc7e9ccebd9c7c86b7bf65d286d'
         '11b5103f77585634826d3eb3c04dc0f0'
         '867a137702b5979692897946f240c49a')
	
package() {
  sed -i -e 's|${basedir}/../../lib|/usr/share/java/aspectj|' ./doc/examples/build.xml
  install -dm755 "${pkgdir}"/usr/share/doc/${pkgname}
  find doc -type f -print0 | xargs -0 chmod -x
  cp -Rp ./doc/* "${pkgdir}"/usr/share/doc/${pkgname}
  install -dm755 "${pkgdir}"/usr/share/java/${pkgname}
  install -m644 ./lib/*.jar "${pkgdir}"/usr/share/java/${pkgname}
  install -dm755 "${pkgdir}"/usr/bin
  install -m755 aj aj5 ajbrowser ajc ajdoc "${pkgdir}"/usr/bin
  install -m644 ./*.html "${pkgdir}"/usr/share/doc/${pkgname}
}
