# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=luppp
pkgname=$_name-git
pkgver=20120504
pkgrel=1
pkgdesc="A live looping instrument"
arch=(i686 x86_64)
url="https://github.com/harryhaaren/Luppp"
license=('GPL')
depends=('suil' 'lilv' 'gtkmm' 'libconfig' 'fluidsynth' 'liblo')
makedepends=('git' 'python2' 'ladspa')
optdepends=('ladspa: ladspa plugins support')
provides=("$_name")
conflicts=("$_name")

_gitroot=https://github.com/harryhaaren/Luppp.git
_gitname=$_name

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # resources in /usr/share/$pkgname
  sed -e "s|ui.glade|/usr/share/$_name/&|" \
      -e "s|\"\(.*\.png\)|\"/usr/share/$_name/\1|" \
      -i src/g_{window,masteroutput}.cpp

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$_gitname-build"
  DESTDIR="$pkgdir/"  python2 waf install

  # glade interface
  install -d "$pkgdir/usr/share/$_name"
  install -Dm644 .build/{ui.glade,*.png} \
    "$pkgdir/usr/share/$_name"
}

# vim:set ts=2 sw=2 et:
