# Maintainer: Kyle Keen <keenerd@gmail.com>

# aur-breaking split pkgbuild stuff commented out
#pkgbase=xombrero
#pkgname=('xombrero' 'xombrero3')
pkgname=xombrero
#pkgname=xombrero3  # uncomment to package the gtk3 version
pkgver=1.3.1
pkgrel=1
pkgdesc="A minimalists web browser, vi-like while maintaining traditional web browser behavior."
arch=('i686' 'x86_64')
url="https://opensource.conformal.com/wiki/xombrero"
_watch="https://opensource.conformal.com/snapshots/xombrero/"
license=('custom:ISC')
makedepends=('libwebkit' 'libwebkit3' 'libbsd' 'desktop-file-utils')
install=$pkgname.install
replaces=('xxxterm')
conflicts=('xxxterm')
options=('!emptydirs')
source=(http://opensource.conformal.com/snapshots/$pkgname/$pkgname-$pkgver.tgz \
        LICENSE)
md5sums=('45186fc847d2e8630853e1e456cf217d'
         'f3eeb6e8b70a3dcccb8ee57daf584c9e')

build() {
    cd "$srcdir/$pkgbase-$pkgver"
    # no spam
    sed -i 's|https://www.cyphertite.com|https://archlinux.org|' xombrero.{c,conf,h}
    # relocations
    sed -i 's|/etc/ssl/cert.pem|/etc/ssl/certs/ca-certificates.crt|' xombrero.conf
    #sed -i 's|/usr/local/share/xombrero/style.css|/usr/share/xombrero/style.css|' xombrero.conf
    sed -i 's|/usr/local/share/|/usr/share/|' {unix.c,xombrero.conf,xombrero.h}
    cd ..

    mkdir -p build-gtk{2,3}
    cp -R "$srcdir/$pkgbase-$pkgver" build-gtk2
    cp -R "$srcdir/$pkgbase-$pkgver" build-gtk3

    pushd "build-gtk2/$pkgname-$pkgver"
    make PREFIX="/usr" -C linux GTK_VERSION=gtk2
    popd

    pushd "build-gtk3/$pkgname-$pkgver"
    make PREFIX="/usr" -C linux GTK_VERSION=gtk3
    popd
}

# non-split compatability function (remove for real split pkg)
package() {
    if [[ $pkgname == xombrero ]]; then
        package_xombrero
    elif [[ $pkgname == xombrero3 ]]; then
        package_xombrero3
    fi
}

package_xombrero() {
    depends=('libwebkit' 'libbsd' 'desktop-file-utils')
    conflicts=('xombrero3')

    cd "$srcdir/build-gtk2/$pkgbase-$pkgver"
    make PREFIX="/usr" DESTDIR="$pkgdir" install -C linux GTK_VERSION=gtk2
    install -Dm644 xombrero.conf     "$pkgdir/etc/skel/.xombrero.conf"
    install -Dm644 xombrero.desktop  "$pkgdir/usr/share/applications/xombrero.desktop"
    install -Dm755 config-checker.pl "$pkgdir/usr/bin/config-checker.pl"
    install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_xombrero3() {
    depends=('libwebkit3' 'libbsd' 'desktop-file-utils')
    conflicts=('xombrero')

    cd "$srcdir/build-gtk3/$pkgbase-$pkgver"
    make PREFIX="/usr" DESTDIR="$pkgdir" install -C linux GTK_VERSION=gtk3
    install -Dm644 xombrero.conf     "$pkgdir/etc/skel/.xombrero.conf"
    install -Dm644 xombrero.desktop  "$pkgdir/usr/share/applications/xombrero.desktop"
    install -Dm755 config-checker.pl "$pkgdir/usr/bin/config-checker.pl"
    install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
