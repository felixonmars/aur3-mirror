pkgname=cockatrice-git
pkgver=20120624
pkgrel=1
pkgdesc="A multiplatform application for playing card games such as Magic: The Gathering over a network."
arch=('i686' 'x86_64')
url="http://www.cockatrice.de/index.php"
license=('GPL2')
depends=('qt>=4.5.1' 'protobuf')
makedepends=('git' 'cmake')
source=('arch_build2012.patch')
md5sums=('b47a2e025d35c84c824bd44c7159cadc')

_gitroot="git://github.com/mbruker/Cockatrice.git"
_gitname="Cockatrice"

# Set this to 1 to include servatrice in the package
_COMPILE_SERVATRICE=0

build() {
  cd "$srcdir"

  msg "Connecting to the GIT server...."
  if [ -d "${srcdir}/${_gitname}" ] ; then
    cd ${_gitname}
    git clean -f
    git reset --hard HEAD
    git pull --rebase || return 1
    msg2 "Local files updated"
  else
    git clone ${_gitroot} || return 1
    cd ${_gitname}
  fi
  msg2 "GIT checkout done or server timeout"

  patch -Np1 -i "$srcdir/arch_build2012.patch"

  rm -rf build
  mkdir build
  cd build

  if (( _COMPILE_SERVATRICE )); then
    cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DWITH_SERVATRICE=1 ..
  else
    cmake -DCMAKE_INSTALL_PREFIX=/usr/ ..
  fi
  make
}

package() {
  cd "${srcdir}/${_gitname}/build"

  make DESTDIR="$pkgdir" install
}
