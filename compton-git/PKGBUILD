# Maintainer: OK100 <ok100@lavabit.com>
# Previous Maintainer: Val√®re Monseur <valere dot monseur at ymail dot com>

pkgname=compton-git
pkgver=20121127
pkgrel=1
pkgdesc="X Compositor (a fork of xcompmgr-dana)"
arch=(i686 x86_64)
url="https://github.com/chjj/compton"
license=('MIT')
depends=('libx11' 'libxcomposite' 'libxdamage' 'libxfixes' 'libxext' 'libxrender' 'xproto' 'bash' 'xorg-xprop' 'xorg-xwininfo' 'pcre' 'libconfig' 'mesa')
makedepends=('git' 'make' 'pkg-config')

_gitroot="git://github.com/chjj/compton.git"
_gitname="compton"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=$pkgdir/usr install

  # install license
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$_gitname/LICENSE"
}
