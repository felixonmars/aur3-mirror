# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=savon
pkgname="ruby-${_gemname}-2.2"
pkgver=2.2.0
pkgrel=2
pkgdesc='Heavy metal SOAP client (2.2 series)'
arch=(any)
url='http://savonrb.com/'
license=('MIT')
depends=(
	'ruby'
	'ruby-akami>=1.2.0'
	'ruby-builder>=2.1.2'
	'ruby-gyoku-1.0'
	'ruby-httpi>=2.0.2'
	'ruby-nokogiri>=1.4.0'
	'ruby-nori>=2.1.0'
	'ruby-wasabi>=3.1.0')
makedepends=('rubygems')
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}
md5sums=('099b14f493ef4fa96bea2d4c4e5bed7a')
