# Contributor: Jens Adam (byte/jra) <j_adam@web.de>
pkgname=lazarus-i386-win32
pkgver=1.0.8
pkgrel=1
pkgdesc="Lazarus Component Library (i386-win32)"
url="http://www.lazarus.freepascal.org"
license=("GPL2" "MPL" "custom:LGPL")
arch=(any)
depends=(fpc fpc-src fpc-i386-win32-rtl lazarus)
makedepends=(lazarus ppcross386)
options=(!emptydirs !makeflags !strip)
source=("http://downloads.sourceforge.net/project/lazarus/Lazarus%20Zip%20_%20GZip/Lazarus%20$pkgver/lazarus-$pkgver-0.tar.gz")
md5sums=('ef5085859be265a189db4aa20c648c2d')

build() {
  cd "$srcdir/lazarus"
  lazbuild --os=win32 --ws=win32 --cpu=i386 lcl/lclbase.lpk
  lazbuild --os=win32 --ws=win32 --cpu=i386 lcl/interfaces/lcl.lpk
}

package() {
  cd "$srcdir/lazarus"
  mkdir -p "$pkgdir/usr/lib/lazarus"
  find . -type f -path *i386-win32* -exec cp --parents '{}' "$pkgdir/usr/lib/lazarus/" \;
  find $pkgdir -name '*.o' -o -name '*.a' | xargs -rtl1 i686-w64-mingw32-strip
}

