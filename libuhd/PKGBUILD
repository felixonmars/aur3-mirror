# Maintainer: Dominik Heidler <dheidler@gmail.com>

pkgname=libuhd
pkgver=3.3.2
pkgrel=1
pkgdesc="Universal Software Radio Peripheral userspace driver"
arch=('x86_64' 'i686')
url="http://code.ettus.com/redmine/ettus/projects/uhd/wiki"
license=('GPL')
groups=()
provides=()
depends=('python2-cheetah' 'boost-libs')
makedepends=('cmake' 'make' 'gcc' 'boost' 'python2-cheetah')
install='libuhd.install'

_ifsold="$IFS"
IFS="."
set $pkgver
_verstring="`printf "%.3i" $1`_`printf "%.3i" $2`_`printf "%.3i" $3`"
IFS="$_ifsold"
unset _ifsold

source=("libuhd-$pkgver.tar.gz::https://github.com/EttusResearch/UHD-Mirror/tarball/release_$_verstring")
md5sums=('ab93177a67a02b8d835730e16dc076a7')

build() {
  cd $srcdir/EttusResearch-UHD-Mirror-*/host
  # fix for py2k
  find -name "*.py" | xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr/ -DPYTHON_EXECUTABLE=/usr/bin/python2
  make $MAKEFLAGS
}

check() {
  cd $srcdir/EttusResearch-UHD-Mirror-*/host/build
  make test
}

package() {
  cd $srcdir/EttusResearch-UHD-Mirror-*/host/build
  make DESTDIR=$pkgdir install
} 
