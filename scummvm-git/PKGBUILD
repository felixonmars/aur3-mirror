# Maintainer: Thomas Krug <phragment@lavabit.com>
pkgname=scummvm-git
pkgver=20111209
pkgrel=2
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('i686' 'x86_64')
url="http://www.scummvm.org"
license=('GPL')
depends=('sdl' 'libmad' 'fluidsynth' 'libgl' 'libtheora' 'faad2')
makedepends=('git')
provides=('scummvm')
conflicts=('scummvm')
replaces=('scummvm-svn')
install=${pkgname}.install

_gitroot="git://github.com/scummvm/scummvm.git"
_gitname="scummvm"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"


  ./configure --backend=sdl \
              --with-flac-prefix=/usr \
              --prefix=/usr \
              --enable-all-engines || return 1
  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install
  install -D -m 644 dists/scummvm.desktop ${pkgdir}/usr/share/applications/scummvm.desktop
} 
