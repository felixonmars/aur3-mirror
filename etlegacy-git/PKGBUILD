# Maintainer: Babets
pkgname=etlegacy-git
pkgver=20121016
pkgrel=1
pkgdesc="ET: Legacy is a project that aims to create a fully compatible client/server for the popular online FPS game Wolfenstein: Enemy Territory."
arch=('i686' 'x86_64')
url="http://etlegacy.com/"
license=('GPL3')

[ "$CARCH" == "i686" ] && depends=('sdl' 'libjpeg' 'mesa' 'glu' 'etlegacy-data')
[ "$CARCH" == "x86_64" ] && depends=('lib32-sdl' 'lib32-libjpeg' 'lib32-mesa' 'lib32-glu' 'etlegacy-data')

[ "$CARCH" == "i686" ] && makedepends=('git' 'cmake' 'curl' 'libvorbis' 'openal' 'freetype2')
[ "$CARCH" == "x86_64" ] && makedepends=('git' 'cmake' 'lib32-curl' 'lib32-libvorbis' 'lib32-openal' 'lib32-freetype2')

[ "$CARCH" == "i686" ] && optdepends=('curl' 'libvorbis' 'openal' 'freetype2')
[ "$CARCH" == "x86_64" ] && optdepends=('lib32-curl' 'lib32-libvorbis' 'lib32-openal' 'lib32-freetype2')
provides=('etlegacy')
conflicts=('etlegacy')
replaces=('etlegacy')
source=(etlegacy.desktop)
md5sums=('e766173b645aed3643e044c148a32f49')

#git clone git://github.com/etlegacy/etlegacy.git
_gitroot=git://github.com/etlegacy/etlegacy.git
_gitname=etlegacy

build() {
  cd "$srcdir"
   msg "Connecting to GIT server..."
   if [ -d $_gitname ] ; then
     cd $_gitname && git pull origin
     msg "The local files are updated."
   else
     git clone --depth=1 $_gitroot $_gitname
   fi
   msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  cp -R "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  mkdir build
  cd build

  sed -i 's/\(INSTALL_DEFAULT_BASEDIR[ ]*\)""/\1 ${CMAKE_INSTALL_PREFIX}\/${INSTALL_DEFAULT_MODDIR}/g' ../CMakeLists.txt
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_DEFAULT_MODDIR=share/etlegacy ..
  make
}

package() {
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install

  cd ../..

  # install icon and desktop file
  install -Dm 644 $_gitname-build/misc/wolfet.ico $pkgdir/usr/share/pixmaps/wolfet.ico
  install -Dm 644 $_gitname.desktop $pkgdir/usr/share/applications/$_gitname.desktop

}

# vim:set ts=2 sw=2 et:
