# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>
# Contributor: fernando < arch at liquuid dot net>
# Contributor: Juan Pablo GonzÃ¡lez Tognarelli <lord_jotape@yahoo.com.ar>
pkgname=startupmanager
pkgver=1.9.13
pkgrel=4
pkgdesc="GUI app for changing the settings of GRUB, GRUB2, Usplash and Splashy"
arch=("any")
url="http://sourceforge.net/projects/startup-manager/"
license=("MIT")
depends=("gnome-python" "imagemagick" "yelp" "python2" "xorg-xrandr>=1.3.3")
makedepends=("intltool" "setconf")
source=("http://downloads.sourceforge.net/sourceforge/startup-manager/${pkgname}_${pkgver}.tar.gz")
sha1sums=('b82a65ac6ec488ca7fe16fd3ea277cd15a3627c6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Make the startup script use python2
  echo "#!/usr/bin/env python2" > pyfix
  tail -n +2 startupmanager >> pyfix
  cat pyfix > startupmanager

  # Change the grub2 detection to be Arch-friendly
  setconf bootconfig/grub.py self.update_grub_command \'/sbin/grub-mkconfig\'
  setconf bootconfig/grub.py self.grub_install_command \'/sbin/grub-install\'

  # TIP: bug-workaround: If you have problems with resolution-detection when starting
  # startupmanager, try changing line 159 in /usr/lib/python2.7/site-packages/bootconfig/utils.py to:
  #
  # try:
  #     return matches.group(1) + 'x' + matches.group(2)
  # except:
  #     return "640x480"
  # 
  # Where "640x480" is just an example (but should make it work)

  python2 setup.py install --prefix="$pkgdir/usr/"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
