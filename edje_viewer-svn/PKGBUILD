# Maintainer: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Igor Scabini <furester at gmail.com>

pkgname=edje_viewer-svn
pkgver=79499
pkgrel=1
pkgdesc="Simple viewer for edj files"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('custom')
depends=('elementary')
makedepends=('subversion')
conflicts=('edje_viewer')
provides=('edje_viewer')
options=('!libtool')

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/edje_viewer"
_svnmod="edje_viewer"

build() {
  cd "$srcdir"

  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 "$srcdir/$_svnmod-build/COPYING" \
        "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 "$srcdir/$_svnmod-build/COPYING-PLAIN" \
        "$pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN"

  rm -r "$srcdir/$_svnmod-build"
}
