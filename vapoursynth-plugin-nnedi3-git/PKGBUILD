# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=nnedi3
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20130324.df7ac04
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/dubhater/vapoursynth-${_plug}"
license=('GPL')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
makedepends=('git' 'clang' 'yasm')
source=("${_plug}::git://github.com/dubhater/vapoursynth-${_plug}.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  [ "$CARCH" = "i686" ] && _elf="elf32" && _arch="0" && _output="elf32-i386"
  [ "$CARCH" = "x86_64" ] && _elf="elf64" && _arch="1" && _output="elf64-x86-64" 

  echo "all:
	  yasm -f ${_elf} -o ${_plug}-asm.o -DARCH_X86_64=${_arch} -DPIC ${_plug}.asm
	  objcopy --input binary --output ${_output} --binary-architecture i386 binary1_0.9.4.bin binary1_0.9.4.o
	  clang -O3 -Wall -Wextra -Wno-unused-parameter -shared -fPIC -o lib${_plug}.so ${_plug}.c binary1_0.9.4.o ${_plug}-asm.o -Wl,-z,noexecstack $CXXFLAGS" > Makefile
}

build() {
  cd "${srcdir}/${_gitname}"

  make
}

package(){
  cd "${srcdir}/${_gitname}"
  install -Dm755 "lib${_plug}.so" "${pkgdir}/usr/lib/vapoursynth/lib${_plug}.so"
  install -Dm644 README.rst "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
