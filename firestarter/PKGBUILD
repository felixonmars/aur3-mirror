# Maintainer: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Changelog:
# 090623 - Merged with old PKGBUILD maintained by tobias
# 090827 - Split PKGBUILD to have package() function as well as build()
# 091008 - Updated dependencies
# 091026 - Added perlxml as a depend - thanks to sHyLoCk for pointing that out

pkgname=firestarter
pkgver=1.0.3
pkgrel=9
pkgdesc="GUI fron-end for iptables"
arch=('i686' 'x86_64')
license=('GPLv2')
depends=('iptables' 'gtk2' 'libgnome' 'libgnomeui' 'libglade' 'pango' 'gksu' 'perl-xml-parser')
makedepends=('patch')
url="http://www.fs-security.com/"
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/firestarter/$pkgname-$pkgver.tar.gz"
        'rc.firestarter'
        '12_firestarter_transparent_icon.patch'
        '18_fix_memleak.patch'
        'events-log-fix.patch'
        'menu-toolbar-icons-fix.patch')
md5sums=('f46860a9e16dac4b693bd05f16370b03'
         '76ece068fa966c70b0764ec9e644edf5'
         'b657c53280b30f1b4976ec01eff085e8'
         'b31efd1caf430d979e26abb2618a3659'
         'acd91fefe00edb41dd38fcc91567707f'
         'ea9cd9b92939c554b26ccdfe735a2d90')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  # Apply patches to fix things
  #
  # Transparent tray icon (from Ubuntu launchpad)
  patch -Np1 -i ../12_firestarter_transparent_icon.patch || return 1
  
  # fix GUI crashes on newer Gnome libs (from Ubuntu launchpad)
  patch -Np1 -i ../18_fix_memleak.patch || return 1
  
  # Replace kernel.log with iptables.log
  patch -Np1 -i ../events-log-fix.patch || return 1
  
  # menu-toolbar-icons-fix.patch: fixed a couple menu and toolbar icons to follow GTK theme
  patch -Np1 -i ../menu-toolbar-icons-fix.patch || return 1
  
  # Configure, Build and Install to $pkgdir
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var || return 1
  
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
  
  # Create default empty ruleset and fix permissions
  mkdir ${pkgdir}/etc/firestarter/outbound/ || return 1
  touch ${pkgdir}/etc/firestarter/outbound/allow-service || return 1
  chown -R root:root ${pkgdir}/etc/firestarter/ || return 1

  # Install rc.init script
  install -D -m755 ${srcdir}/rc.firestarter ${pkgdir}/etc/rc.d/firestarter || return 1
  
  # Fix the desktop launcher to run with root permission using gksudo.
  sed -i "s|Exec=|TryExec=gksu\n\0gksu |" \
    ${pkgdir}/usr/share/gnome/apps/Internet/firestarter.desktop || return 1
  
  # Move the .desktop file to a standard location
  install -Dm644 $pkgdir/usr/share/gnome/apps/Internet/${pkgname}.desktop $pkgdir/usr/share/applications/${pkgname}.desktop || return 1
  rm -rf $pkgdir/usr/share/gnome || return 1
  
  # Merge Schema files to /usr/share/gconf so the .install file
  # can update the database at install time.
  mkdir -p ${pkgdir}/usr/share/gconf/schemas || return 1
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
    ${srcdir}/${pkgname}-${pkgver}/*.schemas || return 1
  rm -Rf ${pkgdir}/etc/gconf || return 1
}

# vim:set ts=2 sw=2 et:
