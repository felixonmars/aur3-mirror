# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=hound
pkgver=0.1
pkgrel=1
pkgdesc='Extremely fast source code search engine'
arch=(i686 x86_64)
license=(MIT)
url='https://github.com/etsy/Hound'
makedepends=(ruby go)
source=(https://github.com/etsy/Hound/archive/$pkgver.zip)
sha1sums=('d937e9d5fee4753306cbbdd9205a6671f48cc035')

build() {
  cd Hound-$pkgver
  rake 
}

check() {
  cd Hound-$pkgver
  rake test
}

package() {
  cd Hound-$pkgver
  install -m755 -d "$pkgdir/usr/bin"
  install -m755 -t "$pkgdir/usr/bin" bin/{hound,houndd}
  install -m 644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -m 644 -D config-example.json "$pkgdir"/usr/share/$pkgname/config-example.json
}
