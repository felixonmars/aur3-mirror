# Contributor: Markus Heuser <markus.heuser@web.de>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>
# Maintainer: Thiago Yukio Kikuchi Oliveira <stratust@gmail.com>

pkgname=samtools
pkgver=1.1
pkgrel=1
pkgdesc="tools for manipulating next-generation sequencing data"
arch=('i686' 'x86_64')
url="http://www.htslib.org/"
license=('custom')
depends=('perl' 'htslib')
optdepends=('luajit: needed for r2plot.lua vcfutils.lua'
            'python2: needed for varfilter.py')
options=('staticlibs')
source=("http://downloads.sourceforge.net/samtools/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('2fadd00aaed2a8359c9d733b3b794b09')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make LDLIBS="-lhts"
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" prefix=/usr install

  install -d "$pkgdir/usr/include/bam"
  install -Dm644 "$srcdir/$pkgname-$pkgver"/*.h "$pkgdir/usr/include/bam"

  install -Dm644 libbam.a "$pkgdir/usr/lib/libbam.a"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
