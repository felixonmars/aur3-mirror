pkgname=mingw-w64-fftw
pkgver=3.3.4
pkgrel=1
pkgdesc="A library for computing the discrete Fourier transform (DFT) (mingw-w64)"
arch=(any)
url="http://www.fftw.org"
license=("GPL")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt)
options=(staticlibs !strip !buildflags)
source=("$url/fftw-$pkgver.tar.gz")
md5sums=('2edab8c06b24feeb3b82bbb3ebf3e7b3')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/fftw-${pkgver}"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch}-d && pushd build-${_arch}-d
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-sse2 \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make
    popd

    unset LDFLAGS
    mkdir -p build-${_arch}-ld && pushd build-${_arch}-ld
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-long-double \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make
    popd

    unset LDFLAGS
    mkdir -p build-${_arch}-f && pushd build-${_arch}-f
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-float \
      --enable-sse \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/fftw-${pkgver}/build-${_arch}-d"
    make DESTDIR="$pkgdir" install
    cd "${srcdir}/fftw-${pkgver}/build-${_arch}-ld"
    make DESTDIR="$pkgdir" install
    cd "${srcdir}/fftw-${pkgver}/build-${_arch}-f"
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    rm "$pkgdir/usr/${_arch}/bin/fftw-wisdom-to-conf"
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
