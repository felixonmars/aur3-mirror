# Maintainer: David Adler <david dot jo dot adler at gmail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=yoshimi
pkgver=0.060.12
pkgrel=4
pkgdesc="Experimental ZynAddSubFX with improved JACK audio & MIDI IO"
arch=('i686' 'x86_64')
url="http://yoshimi.sourceforge.net/"
license=('GPL')
depends=('jack' 'fltk' 'fftw' 'mxml')
makedepends=('cmake' 'boost' 'mesa')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/0.060/$pkgname-$pkgver.tar.bz2" yoshimi.patch)
md5sums=('e5c7dfb0d655083b9c84b6a67d1a8cd4'
         '4aa4ba606a31a9c1a0db08ffa8df27cb')

build() {
  cd "$srcdir/$pkgname-$pkgver/src" 

  # Will's SUBsynth and PADsynth panning patch
  patch -p2 -i $srcdir/yoshimi.patch 
  
  # some hack to avoid compile error
  sed -s 's/#include <sys\/stat.h>/#include <sys\/stat.h>\n#include <unistd.h>/' -i MusicIO/JackEngine.cpp

  cmake . -DCMAKE_INSTALL_PREFIX="$pkgdir/usr"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src" 

  make install

  # desktop
  install -d "$pkgdir/usr/share/applications"
  sed "s|@Y.*@|$pkgver|;s|@C.*/||" \
    ../desktop/$pkgname.desktop.in > \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  
  # icon
  install -Dm644 ../desktop/yoshimi.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
