# Maintainer: David Adler <david dot jo dot adler at gmail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=yoshimi
pkgver=0.060.10
pkgrel=4
pkgdesc="Experimental ZynAddSubFX with improved JACK audio & MIDI IO"
arch=('i686' 'x86_64')
url="http://www.graggrag.com/?q=node/19"
license=('GPL')
depends=('jack' 'fltk' 'fftw')
makedepends=('cmake' 'boost' 'mxml')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2"
        "$pkgname-fltk13.patch")
md5sums=('63b55934b1ebc4a4cb94a29f3e01ce91'
         '8a001fc9486653fe23929e19d91749db')
build() {
  cd "$srcdir/$pkgname-$pkgver/src" 

  # fltk patch
  patch -p2 -i ../../$pkgname-fltk13.patch

  cmake . -DCMAKE_INSTALL_PREFIX="$pkgdir/usr"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src" 

  make install

  # desktop file and pixmaps
  install -d "$pkgdir/usr/share/applications"
  sed "s|@Y.*@|0.060.10|;s|@C.*/||;s|pink.*png|$pkgname|" \
      ../desktop/$pkgname.desktop.in > \
      "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 ../desktop/pink_robot_icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
