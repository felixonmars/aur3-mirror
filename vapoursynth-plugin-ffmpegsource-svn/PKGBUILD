# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=ffmpegsource
pkgname=vapoursynth-plugin-${_plug}-svn
pkgver=2.17.4
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (build with static FFmpeg from GIT) (SVN version)"
arch=('i686' 'x86_64')
url="https://github.com/chikuzen/${_plug}"
license=('LGPL2.1')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}" 'vapoursynth-plugin-ffms2')
makedepends=('git' 'subversion')
options=('!libtool'
         '!emptydirs')
source=('ffmpegsource::svn+http://ffmpegsource.googlecode.com/svn/trunk/'
        'git://source.ffmpeg.org/ffmpeg.git')
md5sums=('SKIP'
         'SKIP')
_svnmod="${_plug}"

pkgver() {
  cd "${_svnmod}"
  echo "$(./version.sh)"
}

build() {
  cd "${srcdir}/ffmpeg"
  ./configure --prefix="${srcdir}/build" --disable-{network,{encod,mux}ers,hwaccels,{in,out}devs,debug,programs,doc,vdpau} --enable-pic --enable-avresample --enable-avisynth
  make
  make install

  cd "${srcdir}/${_svnmod}"
  PKG_CONFIG_LIBDIR="${srcdir}/build/lib/pkgconfig" ./configure --enable-avresample --libdir=/usr/lib/vapoursynth/
  make
}

package(){
  cd "${srcdir}/${_svnmod}"
  make DESTDIR="${pkgdir}" install
  find "${pkgdir}" ! -type d ! -name '*.so*' -delete
}
