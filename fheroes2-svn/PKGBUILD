# Maintainer: Mikael Eriksson <mikael_eriksson@miffe.org>

pkgname=fheroes2-svn
pkgver=2860
pkgrel=1
pkgdesc="Free implementation of Heroes of the Might and Magic II engine"
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/mediawiki/fheroes2/index.php?title=Main_Page"
license=('GPL')
depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_net' 'sdl_ttf' 'fheroes2-data')
makedepends=('subversion' 'gettext')
provides=('fheroes2')
conflicts=('fheroes2')
source=(fheroes2.desktop fheroes2.sh)
install=fheroes2.install
md5sums=('0d52069db426be17ce9dbd5deb20d4fd'
         'a39dd6625e7d1582c85087937e00f074')

_svntrunk=https://fheroes2.svn.sourceforge.net/svnroot/fheroes2/trunk/fheroes2 
_svnmod=fheroes2

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build/src"

  sed -i 's/libpng12-config/libpng14-config/g' Makefile
  sed -i '30i#include <unistd.h>' fheroes2/dialog/dialog_selectfile.cpp

	make RELEASE=1 WITH_AI=simple
}

package() {
  install -Dm755 "$srcdir/fheroes2.sh" "$pkgdir/usr/bin/fheroes2"
  install -Dm644 "$srcdir/fheroes2.desktop" "$pkgdir/usr/share/applications/fheroes2.desktop"
  install -Dm644 "$srcdir/fheroes2-build/image/fheroes2_32x32.png" "$pkgdir/usr/share/icons/hicolor/32x32/apps/fheroes2.png"
  install -Dm755 "$srcdir/fheroes2-build/src/dist/fheroes2" "$pkgdir/usr/share/fheroes2/fheroes2"
  install -Dm644 "$srcdir/fheroes2-build/fheroes2.cfg" "$pkgdir/usr/share/fheroes2/fheroes2.cfg"
  install -dm755 "$pkgdir/usr/share/fheroes2/data" "$pkgdir/usr/share/fheroes2/maps"
}

# vim:set ts=2 sw=2 et:
