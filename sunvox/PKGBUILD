# Maintainer: Sarkasper <echo a2FzcGVyLm1lbnRlbkBnbXguY29tCg==|base64 -d>
# Contributor: fuzzix <fuzzix@gmail.com>
# Contributor: David H. Bronke <whitelynx@gmail.com>
pkgname=sunvox
pkgver=1.7.3c
pkgrel=1
pkgdesc="SunVox is a small, fast and powerful modular synthesizer with pattern based sequencer (tracker)"
arch=('i686' 'x86_64')
url="http://www.warmplace.ru/soft/sunvox/"
license=('custom')
depends=('alsa-lib' 'sdl')
changelog=changelog.txt
source=(http://www.warmplace.ru/soft/sunvox/$pkgname-$pkgver.zip sunvox.desktop sunvox.png)
md5sums=('fbe9c17a3a90e208a656b642883645f4'
         '5b3a5306f27cee1e8951db219ab0f89c'
         '8d059cce84b5565506aad73462f97abe')
conflicts=('sunvox-beta')

package() {
  cd "${srcdir}"

  install -d ${pkgdir}/usr/{bin,share/{${pkgname}/instruments/,${pkgname}/examples/,${pkgname}/docs/,applications/,icons/${pkgname}}}

  install -Dm644 "${pkgname}/docs/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for _dir in instruments examples docs ; do
      cd "${srcdir}/${pkgname}/${_dir}/"
      find . -type d -exec install -d "${pkgdir}/usr/share/${pkgname}/${_dir}/"{} \;
      find . -type f -exec install -Dm644 {,"${pkgdir}/usr/share/${pkgname}/${_dir}/"}{} \;
  done
  cd "${srcdir}"

  install -Dm644 "${pkgname}.png" "${pkgdir}/usr/share/icons/${pkgname}/"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/"


  if [ "$CARCH" = "x86_64" ]; then
    _sunvox_bin="${pkgname}/${pkgname}/linux_x86_64/${pkgname}"
  else
    _sunvox_bin="${pkgname}/${pkgname}/linux_x86/${pkgname}"
    _sunvox_bin_lofi="${pkgname}/${pkgname}/linux_x86/${pkgname}_lofi"
  fi

  install -Dm755 ${_sunvox_bin} "${pkgdir}/usr/bin/"

  mv "${pkgdir}/usr/share/${pkgname}/docs" "${pkgdir}/usr/share/${pkgname}/doc"
}

