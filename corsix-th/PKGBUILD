# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=corsix-th
pkgver=0.11
pkgrel=1
pkgdesc='Reimplementation of the game engine of Theme Hospital'
url='http://corsix-th.googlecode.com/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
makedepends=('cmake')
depends=('lua' 'sdl_mixer' 'sdl_ttf' 'timidity++')
source=("${url}files/CorsixTH-${pkgver}-Source.tar.gz"
        'bin')
sha1sums=('55a7cb58511830f8ca80a0074e27c941f175f530'
          '7fd6ae8db366b7f9c4671708e8ea7beb48f1bea3')

install=install

build() {
	cd "${srcdir}/CorsixTH-${pkgver}-Source"
	cmake \
		-D CMAKE_INSTALL_PREFIX=/usr/share/ \
		-D CMAKE_BUILD_TYPE=Release \
		-Wno-dev .
	cd CorsixTH
	make
}

package() {
	cd "${srcdir}/CorsixTH-${pkgver}-Source/CorsixTH"
	make DESTDIR="${pkgdir}" install
	install -Dm755 ../../bin "${pkgdir}/usr/bin/CorsixTH"
	install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../DebianPackage/usr/share/applications/CorsixTH.desktop "${pkgdir}/usr/share/applications/CorsixTH.desktop"
	sed -e 's/games/share/g' -i "${pkgdir}/usr/share/applications/CorsixTH.desktop"
}
