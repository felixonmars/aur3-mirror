# Maintainer: Guillaume Cocatre-Zilgien <guillaume at cocatre dot net>

pkgname='caudec'
pkgver='1.5.0'
pkgrel='1'
pkgdesc='a multi-process command-line audio transcoder'
arch='any'
url='http://caudec.googlecode.com/'
license=('GPL3')
depends=('bash' 'procps-ng' 'grep' 'bc' 'which' 'coreutils' 'shntool' 'sed' 'findutils' )
optdepends=(
	'sox: resampling support'
	'flac: FLAC encoding and decoding'
	'flake: alternative FLAC encoder'
	'wavpack: WavPack encoding and decoding'
	"mac: Monkey's Audio encoding and decoding"
	'alac_decoder: Apple Lossless decoding'
	'ffmpeg: Apple Lossless encoding'
	'lame: MP3 encoding'
	'vorbis-tools: Ogg Vorbis encoding'
	'neroaacenc: Nero AAC encoding'
	'neroaactag: Nero AAC tagging'
	'neroaacdec: Nero AAC decoding (and Replaygain)'
	'musepack-tools: Musepack encoding'
	'opus-tools: Opus encoding'
	'apetag: APEv2 tagging'
	'wine: Windows codecs support (TAK, lossyWAV, etc)'
	'cksfv: CRC32 hashing'
	"wavegain: Replaygain for Monkey's Audio, TAK, ALAC"
	'vorbisgain: Replaygain for Ogg Vorbis'
	'mp3gain: Replaygain for MP3'
	'aacgain-cvs: Replaygain for AAC'
)
backup=('etc/caudecrc')
source=("http://caudec.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
sha1sums=('77a1e8655c064fa8a7d03e0eb418b835cb40d5e0')

package ()
{
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -D -m 0755 'caudec' "${pkgdir}/usr/bin/caudec"
	ln -s 'caudec' "${pkgdir}/usr/bin/decaude"
	install -D -m 0644 'caudecrc' "${pkgdir}/etc/caudecrc"
}
