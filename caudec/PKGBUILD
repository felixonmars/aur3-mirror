# Maintainer: Guillaume Cocatre-Zilgien <guillaume at cocatre dot net>

pkgname='caudec'
pkgver='1.4.2'
pkgrel='2'
pkgdesc='a multi-process command-line audio transcoder'
arch='any'
url='http://caudec.googlecode.com/'
license=('GPL3')
depends=('bash' 'procps-ng' 'grep' 'bc' 'which' 'coreutils' 'shntool' 'sed' 'findutils' )
optdepends=(
	'sox: resampling support'
	'flac: FLAC encoding and decoding'
	'flake: alternative FLAC encoder'
	'wavpack: WavPack encoding and decoding'
	"mac: Monkey's Audio encoding and decoding"
	'alac_decoder: Apple Lossless decoding'
	'lame: MP3 encoding'
	'vorbis-tools: Ogg Vorbis encoding'
	'neroaacenc: Nero AAC encoding'
	'neroaactag: Nero AAC tagging'
	'musepack-tools: Musepack encoding'
	'opus-tools: Opus encoding'
	'apetag: APEv2 tagging'
	'wine: Windows codecs support (TAK, lossyWAV, etc)'
	'cksfv: CRC32 hashing'
)
backup=('etc/caudecrc')
source=("http://caudec.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
sha1sums=('a9672f26ec901692fd0e82308af842557d83048f')

package ()
{
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -D -m 0755 'caudec' "${pkgdir}/usr/bin/caudec"
	ln -s 'caudec' "${pkgdir}/usr/bin/decaude"
	install -D -m 0644 'caudecrc' "${pkgdir}/etc/caudecrc"
}
