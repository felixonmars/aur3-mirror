# Maintainer:  Federico Cinelli <cinelli.federico@gmail.com>
# Contributor: nightfrost <khashayar_naderehvandi@yahoo.co.uk>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=audacity-svn
pkgver=11927
pkgrel=1
pkgdesc="A program that lets you manipulate and record  digital audio waveforms -- development snapshot"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'libsamplerate' 'lame' 'shared-mime-info' 'desktop-file-utils' 'redland' 'jack')
makedepends=('svn' 'python2' 'pkgconfig' 'zip' 'soundtouch')
options=('!makeflags')
conflicts=('audacity')
provides=('audacity')

_svntrunk=http://audacity.googlecode.com/svn/audacity-src/trunk/
_svnmod=audacity-read-only

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  
  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"
  
  WX_CONFIG=/usr/bin/wx-config ./configure --prefix=/usr \
    --with-portmixer=no --with-libsamplerate=system --without-libresample \
    --with-libmad=system --with-id3tag=system --with-flac=system \
    --with-vorbis=system --with-libexpat=system --with-libsndfile=system \
    --with-soundtouch --enable-unicode --without-taglib --without-ffmpeg
  
  make
}
package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}