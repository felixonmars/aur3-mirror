_name=pkgoutofdate
pkgname=$_name-git
pkgver=6.33b1af4
pkgrel=1
pkgdesc="Checks whether Arch packages are out-of-date"
arch=(any)
url="https://github.com/anatol/$_name"
license=(GPL3)
depends=(abs ruby)
makedepends=()
source=("$_name::git://github.com/anatol/$_name.git")
md5sums=('SKIP')

pkgver() {
  cd $srcdir/$_name
  # repo does not have any git tags yet
  echo $(git rev-list --count master).$(git rev-parse --short master) 
}

prepare() {
  cd $srcdir/$_name

  sed -i "s|./parse_pkgbuild.sh|/usr/share/$_name/parse_pkgbuild.sh|" $_name.rb
}

package() {
  cd $srcdir/$_name

  install -d $pkgdir/usr/bin $pkgdir/usr/share/$_name
  install -m755 -T $_name.rb $pkgdir/usr/bin/$_name
  install -m755 -t $pkgdir/usr/share/$_name parse_pkgbuild.sh
} 
