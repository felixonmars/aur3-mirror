# Contributor: graysky <graysky AT archlinux dot us>
# Contributor: Jordi Sanfeliu <jordi AT fibranet dot cat>

pkgname=monitorix
pkgver=2.6.0
pkgrel=4
pkgdesc='A lightweight system monitoring tool that uses rrd databases.'
arch=('any')
license=('GPL')
url="http://www.$pkgname.org"
depends=('perl-cgi' 'perl-mailtools' 'perl-mime-lite' 'perl-libwww' 'rrdtool' 'perl-http-message' 'perl-dbi' 'perl-xml-simple')
optdepends=('anything-sync-daemon: offload your databases to tmpfs to save i/o to your disk'
'lm_sensors: enable support for system temp monitoring'
'smartmontools: enable support for hdd bad sector monitoring.'
'nvidia: enable support for nVidia card temp and usage monitoring.'
'hddtemp: enable support for hdd temp monitoring'
'terminus-font: if your graphs do not contain letters and numbers you may need this font package')
backup=etc/$pkgname.conf
source=(http://www.$pkgname.org/$pkgname-$pkgver.tar.gz rc.$pkgname $pkgname.service)
sha256sums=('0e909a06fd5156d7f2b6604da9175dc5d79309b29d2c904c479eb2fb2d1f5c23'
            'b9471a8fbe808a5fac091f257585d82cd67b701a3c4ee55b7a4f9e9c8579854f'
            '534270c9c0355df5e9f86a46217f6a2f475c18643697be8ad2b6a1ebede2564f')
install=readme.install
_basedir="srv/http/monitorix"
_libdir="var/lib/$pkgname"

package() {
	cd "$srcdir"/$pkgname-$pkgver

	# use Arch defaults in config
	sed -i -e '/^our $BASE_DIR/ s,\/usr\/share,\/srv\/http,' -i -e '/^our $BASE_CGI/ s,-cgi,\/cgi-bin,' $pkgname.conf
	gzip -9 man/man8/$pkgname.8 && gzip -9 man/man5/$pkgname.conf.5

	install -Dm644 $pkgname.conf "$pkgdir"/etc/$pkgname.conf
	install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
	install -Dm755 $pkgname.cgi "$pkgdir"/$_basedir/cgi-bin/$pkgname.cgi
	install -m644 logo_bot.png "$pkgdir"/$_basedir/logo_bot.png
	install -m644 logo_top.png "$pkgdir"/$_basedir/logo_top.png
	install -dm777 "$pkgdir"/$_basedir/imgs
	install -Dm644 docs/$pkgname.logrotate "$pkgdir"/etc/logrotate.d/$pkgname

	for i in Changes COPYING README*; do
		install -Dm644 "$i" "$pkgdir"/usr/share/doc/"$i"
	done

	install -m644 docs/$pkgname-alert.sh "$pkgdir"/usr/share/doc/$pkgname-alert.sh
	install -m644 docs/$pkgname-apache.conf "$pkgdir"/usr/share/doc/$pkgname-apache.conf

	install -Dm644 man/man8/$pkgname.8.gz "$pkgdir"/usr/share/man/man8/$pkgname.8.gz
	install -Dm644 man/man5/$pkgname.conf.5.gz "$pkgdir"/usr/share/man/man5/$pkgname.conf.5.gz
	
	for i in {ca,de,en,it,pl}.html; do
		install -Dm644 "$srcdir"/$pkgname-$pkgver/reports/"$i" "$pkgdir"/$_libdir/"$i"
	done

	install -m755 "$srcdir"/$pkgname-$pkgver/reports/send_reports "$pkgdir"/$_libdir/send_reports
	install -Dm755 "$srcdir"/rc.$pkgname "$pkgdir"/etc/rc.d/$pkgname
	install -Dm755 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
