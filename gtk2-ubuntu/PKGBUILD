# Maintainer: György Balló <ballogy@freestart.hu>

pkgbase=gtk2-ubuntu
pkgname=gtk2-ubuntu
true && pkgname=('gtk2-ubuntu' 'gtk-update-icon-cache-ubuntu')
pkgver=2.24.6
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
options=('!libtool' '!docs')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz
        xid-collision-debug.patch
        gtk2-dont-ellipsize-filters.diff
        012_ubuntu-set-grab-add.patch
        043_ubuntu_menu_proxy.patch
        044_grips.patch
        065_gir_set_packages.patch
        072_indicator_menu_update.patch
        091_bugzilla_tooltip_refresh.patch
        095_git_menus_scrolling.patch
        096_git_gtkprintsettings.patch
        100_overlay_scrollbar_loading.patch)
sha256sums=('8a026b545dc1fa0c1a69b395232ad7c8ee8fa260be5f7d9a2f2ffcd799efefca'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            '9cffc3b4854ae7eec63eebbbbd9698332dbe64660927b0669d5941baa9d3270b'
            '32e6765f7e0bebb135eb715561be05ecdc178b1bd24b6ae6526fef8c815e5b00'
            '83280aa0f3ac1e2456656d3e97d804ec5f891737f93799fcb1cc1f1dd0d9a41a'
            '6d128b78f978452fd719fce8a6ec2f589366c7e2d0276c83573e649617ce6193'
            '45f20ce60c35907e11a806c1159cadebb495b88969020310e13f66c73f82dc8f'
            '2e288dd89adf5b940b7284465a8913025f41dfda4d032a1c25fab9d2e9192ed0'
            'b459a74d3f6dc59d6e2cf03d136a6fa8038e604eddf0b5679d00bb771b6639b3'
            'd2abf34701823ac7f8c21c7776776091d4849439469a748f82e9f475a8bd2156'
            '5941c2b7b99b58ff5dc09480ddd78fa9d84b9169434ca7ac7c235158563aada9'
            'ba229cbf0f4565cc099ca63ea9f75a89ab6a5d73cc086a55c7b024667dafaa1b')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
    patch -Np1 -i "${srcdir}/xid-collision-debug.patch"
    #https://bugzilla.gnome.org/show_bug.cgi?id=657949
    patch -Np1 -i "${srcdir}/gtk2-dont-ellipsize-filters.diff"
    patch -Np1 -i "${srcdir}/012_ubuntu-set-grab-add.patch"
    patch -Np1 -i "${srcdir}/043_ubuntu_menu_proxy.patch"
    patch -Np1 -i "${srcdir}/044_grips.patch"
    patch -Np1 -i "${srcdir}/065_gir_set_packages.patch"
    patch -Np1 -i "${srcdir}/072_indicator_menu_update.patch"
    patch -Np1 -i "${srcdir}/091_bugzilla_tooltip_refresh.patch"
    patch -Np1 -i "${srcdir}/095_git_menus_scrolling.patch"
    patch -Np1 -i "${srcdir}/096_git_gtkprintsettings.patch"
    patch -Np1 -i "${srcdir}/100_overlay_scrollbar_loading.patch"

    automake
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}
package_gtk2-ubuntu() {
    pkgdesc="The GTK+ Toolkit (v2) with Ubuntu's modifications"
    install=gtk2.install
    depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    provides=('gtk2=2.24.6')
    conflicts=('gtk2')
    backup=(etc/gtk-2.0/gtkrc)

    cd "${srcdir}/gtk+-${pkgver}"

    make DESTDIR="${pkgdir}" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm ${pkgdir}/usr/bin/gtk-update-icon-cache
}
package_gtk-update-icon-cache-ubuntu() {
    pkgdesc="The GTK+ update icon cache tool with Ubuntu's modifications"
    depends=('gdk-pixbuf2')
    provides=('gtk-update-icon-cache=2.24.6')
    conflicts=('gtk-update-icon-cache')

    cd "${srcdir}/gtk+-${pkgver}/gtk"

    install -D -m755 gtk-update-icon-cache ${pkgdir}/usr/bin/gtk-update-icon-cache
}
