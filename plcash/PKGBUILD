# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Nathan Owe <ndowens04 at gmail>

pkgname=plcash
pkgver=2.2.2367
pkgrel=1
pkgdesc="A personal finance program"
arch=('any')
url="http://www.arachnoid.com/PLCash/index.html"
license=('custom:careware')
depends=('java-runtime')
source=("http://www.arachnoid.com/PLCash/PLCash.jar"
        "http://www.arachnoid.com/lutusp/old_articles/careware.rtf"
        "${pkgname}.desktop"
        "${pkgname}.sh")
noextract=("PLCash.jar")
md5sums=('8ecfde6f5aa60a3ac638636f77d617a3'
         '390f9eb1765746df88bbfeaf282252b8'
         '59d599d2b0d794794bf60eb9d292d4c2'
         '9aeba04f9b6bec9ab305b97f7803bec9')

package() {
  cd "${srcdir}"

  # Install program files
  install -Dm755 ${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 PLCash.jar "${pkgdir}/usr/share/${pkgname}/PLCash.jar"

  # Install a desktop entry
  bsdtar -xf PLCash.jar PLCash.png
  install -Dm644 PLCash.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install a license file
  install -Dm644 careware.rtf "${pkgdir}/usr/share/licenses/${pkgname}/careware.rtf"
}
