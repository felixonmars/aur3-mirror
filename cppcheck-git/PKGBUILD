# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Matthias matthiaskrgr Krüger < matthias · krueger _strange_curverd_character_ famsik · de >

pkgname=cppcheck-git
pkgver=1.60.1.160.g6fe1b92
pkgver() {
    cd cppcheck
    git describe --tags | sed -e 's/^cppcheck\-//' -e 's/-/./g'
}
pkgrel=1
pkgdesc='A tool for static C/C++ code analysis'
arch=('i686' 'x86_64')
url='http://cppcheck.wiki.sourceforge.net'
license=('GPL3')
conflicts=('cppcheck')
depends=('gcc-libs' 'pcre')
optdeps=('qt4: to build and run cppcheck-gui')
source=('cppcheck::git://github.com/danmar/cppcheck.git')
sha1sums=('SKIP')


build() {
    cd "$srcdir"/cppcheck
    make
    if [[ -z `pacman -T qt4` ]] ; then
		msg 'qt4 found!'
		msg 'Building cppcheck-gui'
		cd "$srcdir"/cppcheck/gui
		qmake-qt4
		make
	fi
}

check() {
    cd "$srcdir"/cppcheck
    make test
}

package() {
    cd "$srcdir"/cppcheck
    make DESTDIR=${pkgdir} install
    if [[ -z `pacman -T qt4` ]] ; then
		cd "$srcdir"/cppcheck/gui
		install -Dm 755 ./cppcheck-gui ${pkgdir}/usr/bin/cppcheck-gui
		# make DESTDIR=${pkgdir} install
	fi
}
