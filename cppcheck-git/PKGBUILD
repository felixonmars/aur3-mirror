# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Matthias matthiaskrgr Krüger < matthias · krueger _strange_curverd_character_ famsik · de >

pkgname=cppcheck-git
pkgver=1.62.1.324.gd0f6f07
pkgver() {
    cd cppcheck
    git describe --tags | sed -e 's/^cppcheck\-//' -e 's/-/./g'
}
pkgrel=1
pkgdesc='A tool for static C/C++ code analysis'
arch=('i686' 'x86_64')
url='http://cppcheck.wiki.sourceforge.net'
license=('GPL3')
provides=('cppcheck')
conflicts=('cppcheck')
makedepends=('git' 'docbook-xsl') 
depends=('gcc-libs' 'pcre' 'python')
optdeps=('qt4: to build and run cppcheck-gui')
source=('cppcheck::git://github.com/danmar/cppcheck.git')
sha1sums=('SKIP')


build() {
    cd "$srcdir"/cppcheck
    export HAVE_RULES=yes
    make SRCDIR=build CFGDIR=/usr/share/cppcheck/cfg
    make DB2MAN=/usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl man

    if [[ -z `pacman -T qt4` ]] ; then
		msg 'qt4 found!'
		msg 'Building cppcheck-gui'
		export HAVE_RULES=yes
		cd "$srcdir"/cppcheck/gui
		qmake-qt4
		make SRCDIR=build CFGDIR=/usr/share/cppcheck/cfg
	fi
}

check() {
    cd "$srcdir"/cppcheck
    make SRCDIR=build CFGDIR=/usr/share/cppcheck/cfg test
}

package() {
    cd "$srcdir"/cppcheck
	make DESTDIR=${pkgdir} CFGDIR=/usr/share/cppcheck/cfg SRCDIR=build install 
    if [[ -z `pacman -T qt4` ]] ; then
		cd "$srcdir"/cppcheck/gui
		install -Dm 755 ./cppcheck-gui ${pkgdir}/usr/bin/cppcheck-gui # translations not supported
		# make DESTDIR=${pkgdir} install # not supported :(
		cd ../ # in $srcdir/cppcheck now
	fi
	install -D -p -m 644 cppcheck.1 "${pkgdir}"/usr/share/man/man1/cppcheck.1
	mkdir -p "${pkgdir}"/usr/share/cppcheck/cfg # create the dir
	install -D ./cfg/* -t "${pkgdir}"/usr/share/cppcheck/cfg # copy all the cfgs into the dir

}
