# Maintainer: Nuno Araujo <nuno.araujo@russo79.com>
pkgname=roccat-tools
true && pkgname=('roccat-tools-common'
                 'roccat-tools-arvo'
                 'roccat-tools-isku'
                 'roccat-tools-iskufx'
                 'roccat-tools-kone'
                 'roccat-tools-koneplus'
                 'roccat-tools-konepure'
                 'roccat-tools-konextd'
                 'roccat-tools-kovaplus'
                 'roccat-tools-lua'
                 'roccat-tools-pyra'
                 'roccat-tools-savu')
pkgbase=roccat-tools
pkgver=0.16.0
pkgrel=2
arch=('i686' 'x86_64')
url="http://roccat.sourceforge.net"
license=('GPL2')
depends=('libcanberra' 'gtk2' 'libnotify>=0.7.0' 'libusb' 'libunique' 'dbus-glib' 'udev' 'hicolor-icon-theme')
makedepends=('cmake' 'kmod-roccat')
conflicts=(kone koneplus pyra roccat)
source=("http://downloads.sourceforge.net/project/roccat/roccat-tools/roccat-tools-$pkgver.tar.bz2")

md5sums=('3e1e5ddf7c41e05b97fdddc42a268218')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DUDEVDIR=/usr/lib/udev/rules.d
  make 
}

package_roccat-tools-common() {
  pkgdesc="ROCCAT tools common files"
  install=roccat-tools.install
  conflicts=('roccat-tools')

  for i in icons sounds libroccat libroccatwidget roccatgui; do
    cd $srcdir/$pkgbase-$pkgver/$i
    make DESTDIR="$pkgdir/" install
  done
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/roccat.h $pkgdir/usr/include/roccat/roccat.h
}

package_roccat-tools-arvo() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Arvo devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/arvo"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/arvo.h $pkgdir/usr/include/roccat/arvo.h
  install -Dm644 udev/90-roccat-arvo.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-arvo.rules
}

package_roccat-tools-isku() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Isku devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/isku"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/isku.h $pkgdir/usr/include/roccat/isku.h
  install -Dm644 udev/90-roccat-isku.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-isku.rules
}

package_roccat-tools-iskufx() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Isku FX devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/iskufx"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/iskufx.h $pkgdir/usr/include/roccat/iskufx.h
  install -Dm644 udev/90-roccat-iskufx.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-iskufx.rules
}

package_roccat-tools-kone() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Kone devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/kone"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/kone.h $pkgdir/usr/include/roccat/kone.h
  install -Dm644 udev/90-roccat-kone.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-kone.rules
}

package_roccat-tools-koneplus() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Kone[+] devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/koneplus"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/koneplus.h $pkgdir/usr/include/roccat/koneplus.h
  install -Dm644 udev/90-roccat-koneplus.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-koneplus.rules
}

package_roccat-tools-konepure() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Kone Pure devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/konepure"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/konepure.h $pkgdir/usr/include/roccat/konepure.h
  install -Dm644 udev/90-roccat-konepure.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-konepure.rules
}

package_roccat-tools-konextd() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Kone XTD devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/konextd"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/konextd.h $pkgdir/usr/include/roccat/konextd.h
  install -Dm644 udev/90-roccat-konextd.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-konextd.rules
}

package_roccat-tools-kovaplus() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Kova[+] devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/kovaplus"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/kovaplus.h $pkgdir/usr/include/roccat/kovaplus.h
  install -Dm644 udev/90-roccat-kovaplus.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-kovaplus.rules
}

package_roccat-tools-lua() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Lua devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/lua"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/lua.h $pkgdir/usr/include/roccat/lua.h
  install -Dm644 udev/90-roccat-lua.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-lua.rules
}

package_roccat-tools-pyra() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Pyra devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/pyra"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/pyra.h $pkgdir/usr/include/roccat/pyra.h
  install -Dm644 udev/90-roccat-pyra.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-pyra.rules
}

package_roccat-tools-savu() {
  pkgdesc="Userland applications to configure and make extended use of ROCCAT Savu devices"
  depends=('roccat-tools-common')
  conflicts=('roccat-tools')

  cd "$srcdir/$pkgbase-$pkgver/savu"
  make DESTDIR="$pkgdir/" install
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm644 include/roccat/savu.h $pkgdir/usr/include/roccat/savu.h
  install -Dm644 udev/90-roccat-savu.rules $pkgdir/usr/lib/udev/rules.d/90-roccat-savu.rules
}