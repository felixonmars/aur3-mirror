# Maintainer: megadriver <megadriver at gmx dot com>

pkgname=gvfs-minimal
_pkgname=gvfs
pkgver=1.12.1
pkgrel=1
pkgdesc="A minimal build of gvfs with most backends disabled"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2')
makedepends=('intltool')
conflicts=('gvfs' 'gconf')
provides=('gvfs')
install=gvfs-minimal.install
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz")
md5sums=('8fddedf1e6bf66a8228d5476c8ec78ad')
options=(!libtool)

build() {
  cd $srcdir/$_pkgname-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static \
  --disable-afc --disable-archive --disable-afp --disable-avahi --disable-bluray \
  --disable-fuse --disable-gdu --disable-gphoto2 --disable-gudev --disable-hal \
  --disable-http --disable-keyring --disable-obexftp --disable-samba \
  --libexecdir=/usr/lib/gvfs --with-bash-completion-dir=/etc/bash_completion.d 
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  sed -e 's/^am__append_4/#am__append_4/' \
	  -e 's/^am__append_5/#am__append_5/' \
	  -i monitor/Makefile
  make DESTDIR=$pkgdir install

  # Disable network automount
  sed -i 's/AutoMount=true/AutoMount=false/' $pkgdir/usr/share/gvfs/mounts/network.mount

  # Remove useless GConf dir
  rm -rf $pkgdir/usr/share/GConf
}
