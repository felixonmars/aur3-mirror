# Maintainer: Josh VanderLinden <arch@cloudlery.com>
pkgname=execline
pkgver=1.3.1.1
pkgrel=1
pkgdesc="A (non-interactive) scripting language, like sh"
arch=('i686' 'x86_64')
url="http://skarnet.org/software/${pkgname}/"
license=('ISC')
depends=('skalibs')
makedepends=()
source=("http://skarnet.org/software/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('3913c1ef0b704703fac1271815651c8b')

build() {
  cd ${srcdir}/admin/${pkgname}-${pkgver}

  echo /usr/lib/skalibs/sysdeps > conf-compile/import
  echo /usr/include/skalibs     > conf-compile/path-include
  echo /usr/lib/skalibs         > conf-compile/path-library
  echo /usr/libexec/${pkgname}  > conf-compile/conf-install-command
  echo /usr/lib/${pkgname}      > conf-compile/conf-install-library
  echo /usr/include/${pkgname}  > conf-compile/conf-install-include

  rm -f conf-compile/flag-slashpackage
  rm -f conf-compile/flag-allstatic

  package/compile
}

package() {
  cd ${srcdir}/admin/${pkgname}-${pkgver}

  for i in package/*.exported
  do
    case $(basename ${i} ) in
      library.so.exported) d=lib                      ;;
      include.exported)    d=usr/include/${pkgname}   ;;
      library.exported)    d=usr/lib/${pkgname}       ;;
      command.exported)    d=usr/libexec/${pkgname}   ;;
    esac
    f=$(basename ${i} | sed 's/.exported//')

    mkdir -p ${pkgdir}/${d}
    install $(sed s,^,${f}/, ${i}) "${pkgdir}/${d}"
  done

  install -D -m644 package/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:ft=sh ts=2 sw=2 et:
