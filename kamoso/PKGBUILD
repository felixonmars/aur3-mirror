# Maintainer: birdflesh <antkoul at gmail dot com>
# Contributor: said
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=kamoso
pkgver=2.0.2
pkgrel=4
pkgdesc="An application to take pictures and videos out of your webcam"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/multimedia/kamoso"
license=('GPL')
depends=('kdebase-runtime' 'qt-gstreamer' 'libkipi' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins')
makedepends=('cmake' 'automoc4')
optdepends=('kipi-plugins: Facebook support')
conflicts=('kamoso-git')
source=("ftp://ftp.kde.org/pub/kde/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
install=${pkgname}.install
md5sums=('c2125e3cbe10ddc397ec4308891a9f4c')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
