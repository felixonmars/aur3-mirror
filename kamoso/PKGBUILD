# Maintainer: birdflesh <antkoul at gmail dot com>
# Contributor: said
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=kamoso
pkgver=2.0.2
pkgrel=6
pkgdesc="An application to take pictures and videos out of your webcam"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/multimedia/kamoso"
license=('GPL')
depends=('kdebase-runtime' 'qt-gstreamer' 'libkipi' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins')
makedepends=('cmake' 'automoc4' 'boost')
optdepends=('kipi-plugins: Facebook support')
conflicts=('kamoso-git')
source=("ftp://ftp.kde.org/pub/kde/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kamoso-2.0.2-include.patch'
        'kamoso-2.0.2-icons.patch')
install=${pkgname}.install
md5sums=('c2125e3cbe10ddc397ec4308891a9f4c'
         '2cf5e2c26387a83db25505e83fc3c1c5'
         '7016554694fb954acc2e063030b09b1b')
         
build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/kamoso-2.0.2-include.patch
  patch -p1 -i "${srcdir}"/kamoso-2.0.2-icons.patch
  for icon in src/plugins/youtube/icons/*-action-youtube.*
  do
    new_name=$(echo ${icon} | sed -e's|-youtube|-kipiplugin_youtube|')
    mv ${icon} ${new_name}
  done
 
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
