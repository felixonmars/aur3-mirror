# $Id: PKGBUILD 157114 2012-04-24 00:17:20Z stephane $
# Upstream Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>
# Maintainer: Fantix King <fantix.king at gmail.com>

_basepkgname=python2
pkgname=binx32-python2
pkgver=2.7.3
pkgrel=2.5
_pybasever=2.7
pkgdesc="A high-level scripting language (x32 ABI)"
arch=('x86_64')
license=('PSF')
url="http://www.python.org/"
depends=("${_basepkgname}=${pkgver}" 'libx32-bzip2' 'libx32-gdbm' 'libx32-openssl' 'libx32-zlib' 'libx32-expat' 'libx32-sqlite3' 'libx32-libffi')
makedepends=('binx32-tk' 'libx32-ncurses' 'libx32-readline')
optdepends=('binx32-tk: for IDLE')
conflicts=('python2-x32')
replaces=('python2-x32')
provides=('python2-x32')
options=('!makeflags')
source=(
    http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz
    Python-2.7.3-multilib-1.patch
    pyconfig-stub.h
)
sha1sums=('b2b0ada7ebed4a8204a855193afbdb3aa3308357'
          '789e6a31c2d05a35f2ce437acc254ed6a084808b'
          '0ad4a8f98b0d111b603323aad7327fcc349927bc')

build() {
  cd "${srcdir}/Python-${pkgver}"

  # http://cblfs.cross-lfs.org/index.php/Python
  patch -p1 -i ${srcdir}/Python-2.7.3-multilib-1.patch
  sed -i -e "s|@@MULTILIB_DIR@@|libx32|g" Lib/distutils/command/install.py \
       Lib/distutils/sysconfig.py \
       Lib/pydoc.py \
       Lib/site.py \
       Lib/sysconfig.py \
       Lib/test/test_dl.py \
       Lib/test/test_site.py \
       Lib/trace.py \
       Makefile.pre.in \
       Modules/getpath.c \
       setup.py
  sed -i "s@/usr/X11R6@/usr@g" setup.py
  sed -i 's@/lib64@/libx32@g' Modules/_ctypes/libffi/m4/libtool.m4 \
      Modules/_ctypes/libffi/configure setup.py
  sed -i "s/'lib64'/'libx32'/g" setup.py
  ln -sf pyconfig.h pyconfig-x32.h

  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2-x32|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  export OPT="${CFLAGS}"
  export CC="gcc -mx32"
  export CXX="g++ -mx32"
  ./configure --prefix=/usr --enable-shared --with-threads --enable-ipv6 \
              --enable-unicode=ucs4 --with-system-expat --with-system-ffi \
              --with-dbmliborder=gdbm:ndbm --libdir=/usr/libx32

  make
}

package() {
  install="${pkgname}.install"

  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" altinstall maninstall
  mv ${pkgdir}/usr/bin/python${_pybasever} ${pkgdir}/usr/bin/python${_pybasever}-x32
  mv ${pkgdir}/usr/bin/python${_pybasever}-config ${pkgdir}/usr/bin/python${_pybasever}-x32-config

  ln -sf python${_pybasever}-x32        "${pkgdir}/usr/bin/python2-x32"
  ln -sf python${_pybasever}-x32-config "${pkgdir}/usr/bin/python2-x32-config"

  ln -sf ../../libpython${_pybasever}.so \
    "${pkgdir}/usr/libx32/python${_pybasever}/config/libpython${_pybasever}.so"

  mv "${pkgdir}/usr/bin/smtpd.py" "${pkgdir}/usr/libx32/python${_pybasever}/"

  # some useful "stuff"
  install -dm755 "${pkgdir}"/usr/libx32/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    "${pkgdir}/usr/libx32/python${_pybasever}/Tools/i18n/"
  install -m755 Tools/scripts/{README,*py} \
    "${pkgdir}/usr/libx32/python${_pybasever}/Tools/scripts/"

  # clean up redundant scripts
  mv "${pkgdir}"/usr/bin/idle{,2-x32}
  rm "${pkgdir}"/usr/bin/pydoc
  rm "${pkgdir}"/usr/bin/2to3

  # clean up #!s
  find "${pkgdir}/usr/libx32/python${_pybasever}/" -name '*.py' | \
    xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2-x32|"
  sed -i "s|#[ ]*![ ]*/usr/bin/python${_pybasever}$|#!/usr/bin/python${_pybasever}-x32|" \
    "${pkgdir}/usr/bin/python${_pybasever}-x32-config" "${pkgdir}/usr/bin/idle2-x32"

  # clean-up reference to build directory
  sed -i "s#${srcdir}/Python-${pkgver}:##" \
    "${pkgdir}/usr/libx32/python${_pybasever}/config/Makefile"

  mv "${pkgdir}/usr/include/python${_pybasever}/pyconfig.h" "${srcdir}/pyconfig-x32.h"
  rm -rf "${pkgdir}/usr/"{include,share}
  install -Dm644 "${srcdir}/pyconfig-x32.h" "${pkgdir}/usr/include/python${_pybasever}/pyconfig-x32.h"
  install -Dm644 "${srcdir}/pyconfig-stub.h" "${pkgdir}/usr/include/python${_pybasever}/pyconfig-stub.h"

  # install license
  install -dm755 "$pkgdir"/usr/share/licenses
  ln -s ${_basepkgname} "${pkgdir}"/usr/share/licenses/${pkgname}
}
