# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=rds-cli
pkgver=1.8.002
pkgrel=3
pkgdesc="Amazon RDS Command Line Toolkit"
arch=("any")
url="http://aws.amazon.com/developertools/2928?_encoding=UTF8&jiveRedirect=1"
license=('custom')
depends=("java-runtime-headless")
makedepends=("findutils")
source=(http://s3.amazonaws.com/rds-downloads/RDSCli.zip libpath.patch rds-cli.profile)
install="$pkgname.install"
md5sums=('e695e43a1bd3b124f729ffd98959ecf9'
         'ed6b503feecb40cbf7cf093935398716'
         '9423633e14adcae96e3ee75567e69166')
package() {
  cd "$srcdir/RDSCli-$pkgver"
  mkdir -p "$pkgdir/usr/lib/rds-cli"
  patch bin/service  < ../libpath.patch
  cp "$srcdir/RDSCli-$pkgver/lib/"* "$pkgdir/usr/lib/rds-cli"
  mkdir -p "$pkgdir/usr/bin"
  # The last $pkgdir/usr/{} does not need the /bin/ because its already stated
  find bin/ -type f -not -iname '*cmd*' -exec cp '{}' "$pkgdir/usr/{}" \;

  # The license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  cp "license.txt" "$pkgdir/usr/share/licenses/$pkgname"
  cp "notice.txt" "$pkgdir/usr/share/licenses/$pkgname"
  cp "THIRDPARTYLICENSE.TXT" "$pkgdir/usr/share/licenses/$pkgname"

  # Docs
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cp "README.TXT" "$pkgdir/usr/share/doc/$pkgname"
  cp "RELEASENOTES.TXT" "$pkgdir/usr/share/doc/$pkgname"
  cp "credential-file-path.template" "$pkgdir/usr/share/doc/$pkgname"

  # ENV vars
  mkdir -p "$pkgdir/etc/profile.d/"
  cp "$srcdir/rds-cli.profile" "$pkgdir/etc/profile.d/rds-cli.sh"
}

# vim:set ts=2 sw=2 et:
