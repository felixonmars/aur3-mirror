#Contributor: Phillipe Smith <phillipe@archlinux.com.br>
pkgname=lxdm-git
pkgver=20120226
pkgrel=1
pkgdesc='Lightweight Display Manager (part of LXDE)'
arch=('i686' 'x86_64')
url="http://blog.lxde.org/?p=531"
license=('GPL')
groups=('lxde')
provides=('lxdm')
conflicts=('lxdm' 'lxdm-svn')
backup=('etc/lxdm/lxdm.conf' 'etc/pam.d/lxdm')
install=lxdm.install
depends=('cairo' 'consolekit' 'dbus-core' 'gdk-pixbuf2' 'glib2' 'gtk2' 'libx11' 'libxcb' 'pango')
makedepends=('intltool' 'git') 
source=('Xsession.patch' 'lxdm.patch' 'lxdm-pam.patch' 'lxdm-daemon' 'arch-dark-theme.tar.gz')
md5sums=('9bdf95adb74d81d4b6b6176fb1142090'
         'baed9055e8825a5511712bc095197519'
         'b20fe3c8487a039050986d60e45233a9'
         '705f394052fdd0dec22e95321d170de0'
         '0cc1eeb79300a982bea2f9bea79c1b06')

_gitroot="git://lxde.git.sourceforge.net/gitroot/lxde/lxdm"
_gitname="lxdm"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
    cd "$srcdir/$_gitname-build"

    ./autogen.sh
    ./configure --sysconfdir=/etc --prefix=/usr --libexecdir=/usr/lib/lxdm || return 1
    make || return 1

    patch -Np0 < ../lxdm.patch
    patch -Np0 < ../Xsession.patch

    # Removing chown from data/Makefile and adding it to lxdm.install
    sed -i '/chown\ root:lxdm/d' ${srcdir}/lxdm-build/data/Makefile 
}

package() { 
    cd "$srcdir/$_gitname-build"
    make DESTDIR=${pkgdir} install || return 1
    install -m644 ${srcdir}/lxdm-pam.patch ${pkgdir}/etc/pam.d/lxdm || return 1
    install -Dm755 ${srcdir}/lxdm-daemon ${pkgdir}/etc/rc.d/lxdm || return 1
    cp -rf ${srcdir}/Arch-Dark ${pkgdir}/usr/share/lxdm/themes || return 1
    # Setting Arch-Dark theme the default LXDM theme
    sed 's/Industrial/Arch-Dark/' -i ${pkgdir}/etc/lxdm/lxdm.conf   
}
