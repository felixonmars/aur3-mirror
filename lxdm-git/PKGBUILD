#Contributor: Phillipe Smith <phillipe@archlinux.com.br>
pkgname=lxdm-git
pkgver=20120504
pkgrel=1
pkgdesc='Lightweight Display Manager (part of LXDE)'
arch=('i686' 'x86_64')
url="http://blog.lxde.org/?p=531"
license=('GPL')
groups=('lxde')
provides=('lxdm')
conflicts=('lxdm' 'lxdm-svn')
backup=('etc/lxdm/lxdm.conf' 'etc/pam.d/lxdm')
install=lxdm.install
depends=('cairo' 'consolekit' 'dbus-core' 'gdk-pixbuf2' 'glib2' 'gtk2' 'libx11' 'libxcb' 'pango')
makedepends=('intltool' 'git') 
source=('Xsession.patch' 'lxdm.patch' 'lxdm.conf.patch' 'autogen.sh.patch'
        'lxdm-pam' 'lxdm-daemon' 'lxdm-themes.tar.gz')
md5sums=('9bdf95adb74d81d4b6b6176fb1142090'
         'baed9055e8825a5511712bc095197519'
         '3d8a8791a41ddf950ff3f406c812992b'         
         '4f50085b4cf3c7fe332a21ea2849598b'         
         'b20fe3c8487a039050986d60e45233a9'
         '705f394052fdd0dec22e95321d170de0'
         '71ea51f2ced593ad07ab5101b9728c05')

_gitroot="git://lxde.git.sourceforge.net/gitroot/lxde/lxdm"
_gitname="lxdm"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf "$srcdir/$_gitname-build"    
    cp -rfT "$srcdir/$_gitname" "$srcdir/$_gitname-build"
    cd "$srcdir/$_gitname-build"

    patch -Np0 < $srcdir/autogen.sh.patch
      
    ./autogen.sh    
    ./configure --sysconfdir=/etc --prefix=/usr --libexecdir=/usr/lib/lxdm || return 1
    make || return 1

    patch -Np0 < $srcdir/lxdm.patch
    patch -Np0 < $srcdir/lxdm.conf.patch
    patch -Np0 < $srcdir/Xsession.patch
}

package() { 
    cd "$srcdir/$_gitname-build"
    make DESTDIR=${pkgdir} install || return 1
    install -m644 ${srcdir}/lxdm-pam ${pkgdir}/etc/pam.d/lxdm || return 1
    install -Dm755 ${srcdir}/lxdm-daemon ${pkgdir}/etc/rc.d/lxdm || return 1
    cp -rf ${srcdir}/lxdm-themes/* ${pkgdir}/usr/share/lxdm/themes || return 1    
}
