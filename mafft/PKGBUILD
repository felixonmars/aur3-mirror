# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Mick Elliot <micke at sfu dot ca>

pkgname=mafft
pkgver=6.956
pkgrel=1
pkgdesc="Multiple alignment program for amino acid or nucleotide sequences"
arch=('i686' 'x86_64')
url="http://mafft.cbrc.jp/alignment/software"
license=('custom')
depends=('ruby')
source=($url/$pkgname-$pkgver-with-extensions-src.tgz)
md5sums=('7aeba2383da6bc8d610401e4d217c97c')

build() {
  cd $pkgname-$pkgver-with-extensions/core
  make clean
  make -e PREFIX=/usr LIBDIR=/usr/lib/mafft

  cd ../extensions
  make clean
  make -e PREFIX=/usr LIBDIR=/usr/lib/mafft
}

package() {
  cd $pkgname-$pkgver-with-extensions/core
  make PREFIX=$pkgdir/usr LIBDIR=$pkgdir/usr/lib/mafft install

  cd ../extensions
  make PREFIX=$pkgdir/usr LIBDIR=$pkgdir/usr/lib/mafft install

  cd ..
  install -Dm644 license $pkgdir/usr/share/licenses/mafft/license
  install -Dm644 license.extensions $pkgdir/usr/share/licenses/mafft/license.extensions

  rm $pkgdir/usr/lib/mafft/*.1

  ln -sf /usr/lib/mafft/mafft-profile $pkgdir/usr/bin
  ln -sf /usr/lib/mafft/mafft-distance $pkgdir/usr/bin
}
