# Contributor: Colin Shea <colin@evaryont.me>
# Maintainer: Colin Shea <colin@evaryont.me>

pkgname=mutt-great-dane
pkgver=1.5.20
pkgrel=5
pkgdesc="A small but very powerful text-based mail client with sidebar, NNTP, compressed, trash folder, urlview, and much more!"
arch=('i686' 'x86_64')
url="http://lunar-linux.org/index.php?page=mutt-sidebar"
depends=('openssl' 'gdbm' 'mime-types' 'libsasl' 'slang' 'libidn' 'heimdal' 'gnupg' 'gpgme')
makedepends=('libxslt' 'lynx')
conflicts=('mutt')
provides=('mutt')
source=(ftp://ftp.mutt.org/mutt/devel/mutt-${pkgver}.tar.gz
        ftp://ftp.mutt.org/pub/mutt/contrib/urlview-0.9.tar.gz
        muttrc.example
        mutt_ssl.patch
        mutt-unmailbox.patch
        patch-${pkgver}.as.echo.2
        patch-${pkgver}.ats.date_conditional.3
        patch-${pkgver}.cd.ifdef.2
        patch-${pkgver}.cd.purge_message.8
        patch-${pkgver}.cd.source_multiple.3
        patch-${pkgver}.csev.ask_passphrase.1
        patch-${pkgver}.csev.yesnet.1
        patch-${pkgver}.dgc.deepif.2
        patch-${pkgver}.indexcolor-3+cb.diff
        patch-${pkgver}.rr.compressed
        patch-${pkgver}.sidebar.20090619.txt
        patch-${pkgver}.tamo.w3mface.3
        patch-${pkgver}.vvv.initials
        patch-${pkgver}.vvv.nntp.2
        patch-${pkgver}.vvv.quote.2
        mutt-attach.patch
        newsrc_stat.patch
        trash_folder-1.5.18.patch
        patch-0.9.dw.urlview-html.3
        patch-0.9.csev.urlview-fixes.1
        patch-0.9.csev.urlview-manprefix.1
        urlview.c.qpd-patch)
md5sums=('027cdd9959203de0c3c64149a7ee351c'
         '67731f73e69297ffd106b65c8aebb2ab'
         '336d1d8e290a0595dbe2cd92d720ffc9'
         '3f54850315502ad47405421339ffae60'
         'fa8e03a49a2fa7b294dc8237d928cdb7'
         '5c67c8bf89160b48cfd6a110d0c8e34a'
         '0670750f2d82a32391c842e202aae775'
         '32d1899cfea50b0d00c7ca88406cb7ee'
         '5f9d902fac287035889ffd3d4faae333'
         '1aa94164f4169e20ab66a1b531535c6c'
         'a952a79d527395e25e3c980497004327'
         '31de8c2733f87de6046cd4f3ba7690a4'
         'b3a07af278ed9c7f16310be2fe1675ea'
         '7cb9520885799d634a7aa127c37e30e6'
         '2d685df96c72dc8a3ae98ae34e5510ba'
         '5786519489877c92e4fff68cf547e869'
         '562339078f5f1be52f8f068a86a31676'
         '9b63d1ff4f702489366660ecfa209271'
         '1aefc046f02b1bbea3ae4b2e8e6a25de'
         'c1330d71d0ba14638dc7baa9bc5a9c4f'
         '659d26f6dae386d55a58c5311e57632e'
         'e44269dcaada8e6ba9bd299bb4597899'
         'a83177bfd298fa3d2644d538cc79379b'
         'fe79ea2cc0c590762cd7940a1c8e2a9a'
         'b9445c1e532a2a0969fb6e3d8fbc86b5'
         'cddf01efd62c477adc8dada7d991b106'
         'babae55851e89535b9719dea76c643a1')
license=('GPL')
build() {
  if [ -e $srcdir/.built ] ; then
    return 0;
  fi
  #####################################
  ## mutt

  cd $srcdir/mutt-$pkgver
  # patch a segfault bug in 1.5.20 -- remove for next release
  patch -p1 -Ni ../mutt-unmailbox.patch
  patch -p1 -Ni ../mutt_ssl.patch # SSL bug fix
  patch -p1 -Ni ../patch-${pkgver}.sidebar.20090619.txt # Sidebar == awesome
  patch -p1 -Ni ../patch-${pkgver}.vvv.nntp.2 # NNTP support. ZOMG w00t! Gmane <3
  # Use normal stat instead of lstat, to support symbolic directories
  patch -p1 -Nui ../newsrc_stat.patch
  patch -p1 -Nui ../patch-${pkgver}.rr.compressed # Support compressed mail*
  patch -p1 -Nui ../patch-${pkgver}.vvv.initials # Extra format for initials
  patch -p1 -Nui ../patch-${pkgver}.vvv.quote.2 # Smarter quote handling
  patch -p1 -Nui ../trash_folder-1.5.18.patch # Smart trash
  patch -p1 -Nui ../mutt-attach.patch # Abort on missing attachments
  # These following were up-ported by me, manually, after applying the previous
  # patches. Most likely they will still apply, but with some fuzziness. Not
  # sure, nor do I care enough to test. I want my mutt this featureful. It's
  # awesome.
  patch -p1 -Nui ../patch-${pkgver}.cd.ifdef.2 # Conditional support stuff
  patch -p1 -Nui ../patch-${pkgver}.as.echo.2 # Feedback from scripts
  # Different colors for each column (e.g. author, date, subject..)
  patch -p1 -Nui ../patch-${pkgver}.indexcolor-3+cb.diff
  # (Required: Trash Folder) - Skip the trash folder
  patch -p1 -Nui ../patch-${pkgver}.cd.purge_message.8
  # Source multiple files in a single call (useful for globbing)
  patch -p1 -Nui ../patch-${pkgver}.cd.source_multiple.3
  patch -p1 -Nui ../patch-${pkgver}.csev.ask_passphrase.1 # Fix passphrase errors
  patch -p1 -Nui ../patch-${pkgver}.tamo.w3mface.3 # Show X-Face in mutt
  patch -p1 -Nui ../patch-${pkgver}.dgc.deepif.2 # Allow nested if statements
  patch -p1 -Nui ../patch-${pkgver}.ats.date_conditional.3 # Conditions on dates

  # Delete all the patch-save files
  find -name \*.orig -delete

  aclocal -I m4
  autoheader
  automake --foreign
  autoconf

  # (apply this patch after automake)
  patch -p1 -i ../patch-${pkgver}.csev.yesnet.1 # Allow xslt to go online & d/l

  ./configure --prefix=/usr --sysconfdir=/etc \
  --enable-gpgme --enable-pop --enable-imap --enable-smtp --enable-nntp \
  --enable-debug --disable-warnings --enable-compressed --enable-exact-address \
  --enable-hcache --with-slang --with-regex --with-gss \
  --with-ssl=/usr --with-sasl --with-idn || return 1

  make || return 1

  #####################################
  ## urlview

  cd $srcdir/urlview-0.9
  patch -p1 -Nui ../patch-0.9.dw.urlview-html.3
  patch -p1 -Nui ../patch-0.9.csev.urlview-fixes.1
  patch -p1 -Nui ../urlview.c.qpd-patch

  ./configure --prefix=/usr --sysconfdir=/etc \
              --with-slang --enable-warnings || return 1

  make || return 1

  # Delete all the patch-save files
  find -name \*.orig -delete
  touch $srcdir/.built
}

package() {
  #####################################
  ## mutt
  cd $srcdir/mutt-$pkgver

  make DESTDIR=$pkgdir install || return 1
  rm -f ${pkgdir}/usr/bin/{flea,muttbug}
  rm -f ${pkgdir}/usr/share/man/man1/{flea,muttbug}.1
  rm -f ${pkgdir}/etc/mime.types*
  install -m644 -D ${srcdir}/muttrc.example ${pkgdir}/etc/muttrc.example

  #####################################
  ## urlview
  cd ../urlview-0.9
  patch -p1 -Nui ../patch-0.9.csev.urlview-manprefix.1
  mkdir -p $pkgdir/usr/man/man1/ $pkgdir/usr/bin
  make DESTDIR=$pkgdir install || return 1
  mv $pkgdir/usr/man/man1/* $pkgdir/usr/share/man/man1/
  rm -r $pkgdir/usr/man

  rm $srcdir/.built
}
