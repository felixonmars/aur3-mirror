pkgname=mendeleydesktop-dev
pkgver=0.9.9
pkgrel=1
url="http://www.mendeley.com/"
pkgdesc="The desktop client for managing and sharing research papers - development version."
license=('custom:mendeley_eula')
arch=('i686' 'x86_64')
[ ${CARCH} = 'x86_64' ] && _arch='x86_64'
[ ${CARCH} = 'i686' ]   && _arch='i486'
source=("http://www.mendeley.com/downloads/linux/${pkgname%%-*}-${pkgver/_/-}-linux-${_arch}.tar.bz2")
[ ${CARCH} = 'x86_64' ] && md5sums=('362ad11b0fd183f8fd54da3d7a23f673')
[ ${CARCH} = 'i686' ]   && md5sums=('8415855aa94313f4ea6d831c1178bbbd')
depends=('qt' 'openssl' 'sqlite3' 'openssl-compatibility' 'libpng12')
makedepends=('cpio')
provides=('mendeleydesktop')
conflicts=('mendeleydesktop')

package() {
        cd ${srcdir}/${pkgname%%-*}-${pkgver/_/-}-linux-${_arch}

        mkdir -p ${pkgdir}/opt/mendeleydesktop/{bin,lib,plugins}
        mkdir -p ${pkgdir}/usr/{bin,lib}

        rm -f INSTALL README

        # install libs
        mv lib/libMendeley.so* \
           lib/libPDFNetC.so \
           ${pkgdir}/usr/lib/ 
        mv lib/*.so* \
           ${pkgdir}/opt/mendeleydesktop/lib

        # install actual executables with pointer to sqlite plugin
        cat > ${pkgdir}/opt/mendeleydesktop/bin/qt.conf <<__EOF__
[Paths]
plugins=/opt/mendeleydesktop/plugins
__EOF__

        install -m755 \
           lib/mendeleydesktop/libexec/{Updater,mendeleydesktop.${_arch}} \
           ${pkgdir}/opt/mendeleydesktop/bin 
        tar -cf - -C lib/mendeleydesktop/plugins . | \
          tar -C ${pkgdir}/opt/mendeleydesktop/plugins/ -xpf - 
        ln -s /usr/share/mendeleydesktop/ ${pkgdir}/opt/mendeleydesktop/bin/share

        # install launcher and fix up path
        sed -i "s~\"\$LOCAL_LIB_PATH\"/mendeleydesktop/libexec/~LD_LIBRARY_PATH=/opt/mendeleydesktop/lib:\$LD_LIBRARY_PATH /opt/mendeleydesktop/bin/~" \
               bin/mendeleydesktop 
        install -D -m755 bin/mendeleydesktop \
                         ${pkgdir}/usr/bin/mendeleydesktop 

        # install license and resources
        install -D -m644 LICENSE \
                         ${pkgdir}/usr/share/licenses/${pkgname%%-*}/LICENSE 
        rm -f LICENSE

        find share/ | cpio -p -dum ${pkgdir}/usr 
        install -Dm644 share/icons/hicolor/48x48/apps/mendeleydesktop.png \
                       ${pkgdir}/usr/share/pixmaps/mendeleydesktop.png
        rm -rf share/{applications,icons}
}
