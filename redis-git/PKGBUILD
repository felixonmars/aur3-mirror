# Maintainer: Hilton Medeiros <medeiros.hilton at gmail dot com>
# Contributor: David Trail <napalm10 at gmail>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=redis-git
pkgver=20101109
pkgrel=1
pkgdesc="Fast memcached key-value database"
arch=('i686' 'x86_64')
url="http://redis.googlecode.com/"
license=('BSD')
depends=('bash')
makedepends=('git')
conflicts=('redis')
provides=('redis')
install=redis.install
backup=('etc/conf.d/redis.conf' 'etc/logrotate.d/redis')
source=("redis.d"
        "redis.logrotate")
md5sums=('4cb1f078848cc32989a97a3d01226773'
         '9e2d75b7a9dc421122d673fe520ef17f')

_gitroot="git://github.com/antirez/redis.git"
_gitname="redis"

build() {
  cd "$srcdir"
  msg "Connecting to Git server...."

  if [ -d $_gitname ]; then
    pushd $_gitname && git pull origin && popd
    msg "The local files are updated"
  else
    git clone $_gitroot
  fi

  msg "Git checkout done or server timeout"
  msg "Starting make..."

  rm -rf $_gitname-build
  git clone $_gitname $_gitname-build
  cd $_gitname-build

  # Must run under c99
  export CFLAGS="$CFLAGS -std=c99 -pedantic -fPIC"
  # Deactivate debug
  export DEBUG=""
  sed -i "s|-Os -g|-fPIC|" $(grep -Rl "\-Os \-g" .)
  
  # We force -j1 because the makefiles don't deal with dependencies very well
  # One would have random build errors otherwise
  make MAKEFLAGS="-j1"
  #make test

  sed -i -e 's|daemonize no|daemonize yes|' \
         -e "s|dir \./|dir /var/lib/$_gitname|" \
         -e "s|logfile stdout|logfile /var/log/$_gitname.log|" \
         "$srcdir/$_gitname-build/$_gitname.conf"
}

package() {
  cd "$srcdir/$_gitname-build"

  make PREFIX="$pkgdir/usr" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_gitname/COPYING"
  install -Dm644 $_gitname.conf "$pkgdir/etc/conf.d/$_gitname.conf"

  install -d "$pkgdir/usr/share/doc/$_gitname"
  install -m644 doc/* "$pkgdir/usr/share/doc/$_gitname"

  install -Dm755 "$srcdir/$_gitname.d" "$pkgdir/etc/rc.d/$_gitname"
  install -Dm644 "$srcdir/$_gitname.logrotate" "$pkgdir/etc/logrotate.d/$_gitname"
}
