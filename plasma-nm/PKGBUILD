# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm
pkgver=5.0.2
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/network/plasma-nm'
license=('GPL2')
depends=('plasma-workspace' 'libmm-qt5' 'libnm-qt5')
makedepends=('extra-cmake-modules' 'kdoctools' 'openconnect')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers' 'openconnect: Cisco AnyConnect VPN plugin')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('74d1f711b38d9fda0ca719bf428b2e56')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-nm-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DLOCALE_INSTALL_DIR=share/locale/kf5 \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
