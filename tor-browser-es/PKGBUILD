# Maintainer  (Spanish version): OmeGa <omega at mailoo dot org>
# Maintainer (Original version): Max Roder <maxroder@web.de>

pkgname='tor-browser-es'
pkgver='2.3.25_1'
_realpkgver='2.3.25-1-dev'
_language='es-ES'
pkgrel='1'
pkgdesc='Navegación anónima utilizando Firefox y Tor.'
url='https://www.torproject.org/projects/torbrowser.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-runtime')
optdepends=('zenity: simple dialog boxes'
      'kdialog: KDE dialog boxes'
      'notify-send: Gnome dialog boxes')
install="${pkgname}.install"
source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz"
    "https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz.asc"
    "${pkgname}.desktop"
    "${pkgname}.png"
    "${pkgname}.sh")
if [[ "$CARCH" == 'x86_64' ]]; then
  sha256sums=('d43efab6d88ef85b19e72bb65f13ebe46a36ed2e825471b0ce8e7db78d8e5cb3'
        'd58c1c599e8047bbcf0292d3936881a8b41d64613b12239dcc7332c7d31df748')
else
  sha256sums=('58373fbb9a17ab401bb68d4668bfe33287cf71ec1af287c896fd2acabf816be9'
        '6db014c0e55296e3265bf84665bd12381787872862a85126f7999330ec13390a')
fi
sha256sums+=('2217f011197329019ae3d282d95623e0230f8f7a3a604290744280530cf1698a'
      '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
      '7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')
noextract=("tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")

package() {
  cd "${srcdir}"

  sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.sh
  sed -i "s/REPL_VERSION/${_realpkgver}/g"	${pkgname}.sh
  sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.sh

  sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.desktop
  sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.desktop
  sed -i "s/REPL_COMMENT/${pkgdesc}/g"		${pkgname}.desktop

  install -Dm 644 ${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm 644 ${pkgname}.png		${pkgdir}/usr/share/pixmaps/${pkgname}.png
  install -Dm 755 ${pkgname}.sh		${pkgdir}/usr/bin/${pkgname}

  install -Dm 644 tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz ${pkgdir}/opt/${pkgname}/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz
}

# vim:set ts=2 sw=2 et:
