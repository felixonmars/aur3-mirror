# Maintainer (Spanish version): OmeGa <omega at mailoo dot org>
# Maintainer (English version): Max Roder <maxroder@web.de>

pkgname=tor-browser-es
pkgver=2.3.25_10
_realpkgver=2.3.25-10-dev
_language=es-ES
pkgrel=1
pkgdesc="Navegación anónima utilizando Firefox y Tor"
arch=('i686' 'x86_64')
url="https://www.torproject.org/projects/torbrowser.html"
license=('GPL')
optdepends=('zenity: simple dialog boxes'
            'kdialog: KDE dialog boxes'
            'libnotify: Gnome dialog boxes')
install=$pkgname.install
source=(https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz
        https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz.asc
        $pkgname.desktop
        $pkgname.png
        $pkgname.sh)
noextract=(tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz)

if [[ $CARCH == x86_64 ]]; then
  sha256sums=('09b0b308dfda636e65a4a9299a0d8799020d49cddcc80d6512b63c875790f35b'
              '6955b5b804d665f934c528beaf93eda7a62a15d69abf6c930a5bea6a5c0d360d')
else
  sha256sums=('c55a4a78c979488ed568b848622acd9151a18d45ecade5e0112b40d08662592a'
              '4681549e59d9854cd029f37d075f10ee01b508ad1715b5482be85ca2157a6104')
fi
sha256sums+=('e73b5788d195e359d7d3621db402998fb4bfca49caf5fc5814b7c2839f93b5f1'
             '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
             '7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')

package() {
  cd "$srcdir"

  sed -i "s/REPL_NAME/$pkgname/g"         $pkgname.sh
  sed -i "s/REPL_LANGUAGE/$_language/g"   $pkgname.sh
  sed -i "s/REPL_VERSION/$_realpkgver/g"  $pkgname.sh

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.desktop
  sed -i "s/REPL_COMMENT/$pkgdesc/"     $pkgname.desktop
  sed -i "s/REPL_LANGUAGE/$_language/"  $pkgname.desktop

  install -Dm755 $pkgname.sh      "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png     "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz \
    "$pkgdir/opt/$pkgname/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz"
}

# vim:set ts=2 sw=2 et:
