# Maintainer (Spanish version): OmeGa <omega at mailoo dot org>
# Maintainer (English version): Max Roder <maxroder@web.de>

pkgname=tor-browser-es
pkgver=3.5
_language=es-ES
pkgrel=1
pkgdesc="Navegación anónima utilizando Firefox y Tor"
arch=('i686' 'x86_64')
url="https://www.torproject.org/projects/torbrowser.html"
license=('GPL')
optdepends=('kdebase-kdialog: KDE dialog boxes'
            'libnotify: Gnome dialog boxes'
            'zenity: simple dialog boxes')
install=$pkgname.install

if [[ $CARCH == "x86_64" ]]; then
  _arch=linux64
else
  _arch=linux32
fi

source=("https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-$_arch-${pkgver}_${_language}.tar.xz"
        "https://www.torproject.org/dist/torbrowser/$pkgver/tor-browser-$_arch-${pkgver}_${_language}.tar.xz.asc"
        "$pkgname.desktop"
        "$pkgname.png"
        "$pkgname.sh")
noextract=(tor-browser-$_arch-${pkgver}_${_language}.tar.xz)

if [[ $CARCH == "x86_64" ]]; then
  sha256sums=('dd901d610dbdf42fd89e7b56d802f8a1839a29e332aa394bf17f4154b38e403e'
              'abac9d1bfd8860ecb56c0839e215bb74768ff6f3acae46c18fc6dd005842e802')
else
  sha256sums=('ee7eaea2fcafe870e238c0ebeae43b72428aba21c70cb8a6bbfaccc028f9d19e'
              '271cc55cb3553264c5764aadaede60fea04f54abf2d944616d02b6389994f5ec')
fi
sha256sums+=('e73b5788d195e359d7d3621db402998fb4bfca49caf5fc5814b7c2839f93b5f1'
             '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
             '9c15a7d9b1893e5e8a3ebe2ed5e6beb6bb0696f206760b8b7e716f982109c340')

package() {
  cd "$srcdir"

  sed -i "s/REPL_NAME/$pkgname/g"         $pkgname.sh
  sed -i "s/REPL_LANGUAGE/$_language/g"   $pkgname.sh
  sed -i "s/REPL_VERSION/$pkgver/g"  $pkgname.sh

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.desktop
  sed -i "s/REPL_COMMENT/$pkgdesc/"     $pkgname.desktop
  sed -i "s/REPL_LANGUAGE/$_language/"  $pkgname.desktop

  install -Dm755 $pkgname.sh      "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png     "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 tor-browser-$_arch-${pkgver}_${_language}.tar.xz \
    "$pkgdir/opt/$pkgname/tor-browser-$_arch-${pkgver}_${_language}.tar.xz"
}

# vim:set ts=2 sw=2 et:
