# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer : Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=ibus-wout-gconf
_pkgname=ibus
pkgver=1.4.1
pkgrel=2
pkgdesc='Next Generation Input Bus for Linux. WITHOUT GCONF'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://ibus.googlecode.com'
depends=('dbus-python>=0.84.0' 'python2' 'pygtk' 'pyxdg' 'iso-codes' 'librsvg' 'python-notify' 'hicolor-icon-theme' 'gtk3')
provides=('ibus')
conflicts=('ibus')
optdepends=('notification-daemon')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc')
options=('!libtool')
install=ibus.install
source=("http://ibus.googlecode.com/files/${_pkgname}-${pkgver}.tar.gz")
sha1sums=('4cc045553885d35a118ec4b44c2694910209509e')

build() {
  rm -fr ${_pkgname}-${pkgver}-build
  cp -R ${_pkgname}-${pkgver} ${_pkgname}-${pkgver}-build
  cd ${_pkgname}-${pkgver}-build

  # python2 fix
  export PYTHON=python2

  sed -i 's|--pkg=ibus-1.0||' src/Makefile.in
  ./configure --prefix=/usr --libexecdir=/usr/lib/ibus --sysconfdir=/etc --disable-gconf --disable-schemas-install --enable-memconf --enable-gtk3 --enable-gtk-doc --enable-surrounding-text
  for file in setup/ibus-setup.in ui/gtk/ibus-ui-gtk.in; do
    sed -i 's_exec python_exec python2_' $file
  done
  make
}

package() {
  cd ${_pkgname}-${pkgver}-build
  export PYTHON=python2
  make DESTDIR=${pkgdir} install
  find ${pkgdir} -type f -exec sed -i 's_exec /usr/bin/python_exec /usr/bin/python2_' {} \;
}
