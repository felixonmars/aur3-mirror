# Maintainer: GordonGR <gordongr@freemail.gr>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=polkit
pkgname=lib32-${_pkgbasename}
pkgver=0.105
pkgrel=2
pkgdesc="Application development toolkit for controlling system-wide privileges (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=("${_pkgbasename}" 'lib32-glib2>=2.30.0' 'lib32-pam' 'lib32-expat')
makedepends=('gcc-multilib' 'intltool')
options=('!libtool')
source=("http://www.freedesktop.org/software/polkit/releases/${_pkgbasename}-${pkgver}.tar.gz")
md5sums=('9c29e1b6c214f0bd6f1d4ee303dfaed9')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libdir=/usr/lib32 --libexecdir=/usr/lib32/polkit-1 \
      --disable-static --enable-introspection=no --enable-systemd=no --enable-man-pages=no\
      CC="gcc -m32" CXX="g++ -m32" PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share},var}
}
