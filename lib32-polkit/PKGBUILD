# Maintainer: jtts
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: GordonGR <gordongr@freemail.gr>

_pkgbasename=polkit
pkgname=lib32-$_pkgbasename
pkgver=0.112
pkgrel=4
pkgdesc="Application development toolkit for controlling system-wide privileges (32-bit)"
arch=(x86_64)
license=(LGPL)
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=($_pkgbasename lib32-glib2 lib32-pam lib32-expat lib32-systemd lib32-js17)
makedepends=(gcc-multilib intltool)
# Not needed. This is a lib32-package.
#install=polkit.install
source=(http://www.freedesktop.org/software/polkit/releases/$_pkgbasename-$pkgver.tar.gz)
# Not needed. This is a lib32-package.
#        polkit.pam)
#md5sums=('b0f2fa00a55f47c6a5d88e9b73f80127'
#         '6564f95878297b954f0572bc1610dd15')
sha256sums=('d695f43cba4748a822fbe864dd32c4887c5da1c71694a47693ace5e88fcf6af6')

build() {
  cd $_pkgbasename-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libdir=/usr/lib32 --libexecdir=/usr/lib32/polkit-1 \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --with-mozjs=mozjs-17.0 \
      --disable-static --enable-introspection=no --enable-libsystemd-login=yes \
      --enable-man-pages=no --disable-gtk-doc \
      CC="gcc -m32" CXX="g++ -m32" PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  # Not needed. This is a lib32-package.
  #chown 102 "$pkgdir/etc/polkit-1/rules.d"
  #chown 102 "$pkgdir/usr/share/polkit-1/rules.d"
  #
  #install -m644 "$srcdir/polkit.pam" "$pkgdir/etc/pam.d/polkit-1"

  # cleanup for lib32 package
  rm -rf $pkgdir/{etc,usr/{bin,lib,include,share}}
}
