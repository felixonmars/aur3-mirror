# Maintainer: bwrsandman <bwrsandman@gmail.com>
# PKGBUILD source: https://github.com/bwrsandman/pkgbuild/tree/master/ogitor-hg

pkgname=ogitor-hg
_hgroot="https://bitbucket.org/jacmoe/ogitor"
_hgrepo="ogitor"
pkgver=733.933dd78bfdf0
pkgrel=1
pkgdesc="Scene editing framework for Ogre3D"
arch=('i686' 'x86_64')
url='http://www.ogitor.org'
license=('LGPL')
depends=('ogre<1.9' 'qt4' 'python2')
makedepends=('cmake' 'mercurial' 'unzip')
conflicts=('ogitor')
provides=('ogitor')

source=(${_hgrepo}::hg+${_hgroot}
        https://bitbucket.org/jacmoe/ogitor/downloads/media.zip
        https://bitbucket.org/jacmoe/ogitor/downloads/projects.zip
	boost-prefix-fix.patch)
md5sums=('SKIP'
         'c43578ed01e383a0eebd0719ae4241f8'
         'b93a9391b3d6e4d835e6a9bfffb3e6e9'
	 '20ad34e9be1321fd392d561d56945efc')

prepare() {
  cd ${_hgrepo}
  # last known merge that supported OGRE 1.8
  hg update 8742524692fdf17ba41f213d0f34a35e0d70adae
  # line 63 causes an error
  sed -i '63d' Dependencies/SkyX/CMakeLists.txt
  patch -uN CMakeLists.txt ${srcdir}/boost-prefix-fix.patch
  # replacing ${Boost_LIBRARIES}
  sed -i 's,TARGET_LINK_LIBRARIES(qtOfs optimized ${Boost_LIBRARIES}),TARGET_LINK_LIBRARIES(qtOfs optimized ${Boost_FILESYSTEM_LIBRARY} ${Boost_SYSTEM_LIBRARY} ${Boost_REGEX_LIBRARY}),' qtOfs/CMakeLists.txt
  sed -i 's,target_link_libraries(qtOgitor optimized QtSolutions_PropertyBrowser ${Boost_LIBRARIES}),target_link_libraries(qtOgitor optimized QtSolutions_PropertyBrowser ${Boost_FILESYSTEM_LIBRARY} ${Boost_SYSTEM_LIBRARY}),' qtOgitor/CMakeLists.txt
}

pkgver() {
  cd ${_hgrepo}
  echo "$(hg identify -n).$(hg identify -i)"
}

build() {
  cd ${srcdir}/${_hgrepo}

  mv ${srcdir}/Media RunPath/ 
  mv ${srcdir}/Projects RunPath/ 

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
	-DPYTHON_INCLUDE_DIR=/usr/include/python2.7
  make
}

package() {
  cd ${srcdir}/${_hgrepo}
  make DESTDIR=${pkgdir} install 
}
