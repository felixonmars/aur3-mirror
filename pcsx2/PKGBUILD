# Maintainer: josephgbr <rafael.f.f1 at gmail.com>

pkgname=pcsx2
pkgver=1.0.0
pkgrel=1
_pkgrev=r5350
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == i686 ]; then
  depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew1.7' 'portaudio'
           'libgl' 'alsa-lib' 'libjpeg-turbo' 'soundtouch'
           'gtk-engines' 'sdl')
  makedepends=('cmake' 'sparsehash' 'p7zip')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
           'lib32-libgl' 'lib32-glew1.7' 'lib32-libjpeg-turbo'
           'lib32-portaudio' 'lib32-alsa-lib' 'lib32-soundtouch'
           'lib32-gtk-engines' 'lib32-sdl')
  makedepends=('cmake' 'sparsehash' 'gcc-multilib' 'p7zip')
fi
conflicts=('pcsx2-bin' 'pcsx2-svn') # Same destination folders
options=('!emptydirs')
install="pcsx2.install"
source=("$pkgname-${pkgver}-${_pkgrev}-sources.7z::http://pcsx2.net/download/releases/source-code/finish/7-source/93-pcsx2-v1-0-0-source-code/0.html")
md5sums=('68c68b5d59fb124b4c425dc963c21879')

build() {
  # For ArchLinux 64-bit, install plugins in lib32 folder
  _plugindir=/usr/lib/pcsx2
  [ $CARCH == x86_64 ] && _plugindir=/usr/lib32/pcsx2
 
  cd ${pkgname}-${pkgver}-${_pkgrev}-sources
  # Fix remaining references of old version
  sed 's@0.9.8@1.0.0@' -i CMakeLists.txt
  
  msg "Starting cmake and build..."
  cmake CMakeLists.txt \
    -DCMAKE_BUILD_TYPE="Release" \
    -DPACKAGE_MODE=TRUE \
    -DFORCE_INTERNAL_SOUNDTOUCH=FALSE \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DPLUGIN_DIR=${_plugindir} \
    -DGAMEINDEX_DIR="/usr/share/pcsx2" \
    -DGLSL_SHADER_DIR="/usr/share/pcsx2" \
    -DXDG_STD=TRUE
  make
}

package() {
  cd ${pkgname}-${pkgver}-${_pkgrev}-sources
  make DESTDIR="${pkgdir}" install
}
