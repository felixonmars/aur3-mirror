# Maintainer: Prurigro

_pkgname=deskcon-desktop
pkgname=${_pkgname}-git
pkgver=20140726.r64.c1e200b
pkgrel=1
pkgdesc="Integrates your Smartphone in your Desktop"
url="https://github.com/screenfreeze/${_pkgname}"
license=('GPL3')
arch=('any')
depends=('python2-pyopenssl')
makedepends=('git')
options=('!strip')
install=${_pkgname}.install

source=("git://github.com/screenfreeze/${_pkgname}.git#branch=master" "${_pkgname}.sh" "${_pkgname}-setup_device.sh" "${_pkgname}.1.gz")
sha512sums=('SKIP'
            'de5f0a7603d3bb6f373288f71de44d9752f5eed10aa385639d236fb97c6c4963ee875f8cae697452e3d1d4e35d2f4450204c1e111f7b314d050e227f0dfe49f2'
            '84436813862a7bb16b46df28ea5aa1d1a3b9b1260a37219c4491cfae1831d404b34f9fa8ac91b0cd12c00062faa2c319653cef45c70dcdfe8b50564443a40bd8'
            'fc97212fe8e33030ffb80e1beed2991a2781cf854e351b358c9d05a28dc6de18586b12b1917c30a514b52d668e85aed4bcc0831f313587fa129243b965269b53')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd ${_pkgname}/server
    find . -type f -exec sed -i 's|/usr/bin/python|/usr/bin/env python2|' '{}' \;
    sed -i 's|python "settingswindow.py"|python2 "settingswindow.py"|' bin/deskcon-server-settings
}

package() {
    install -Dm755 ${_pkgname}.sh "${pkgdir}/usr/bin/${_pkgname}"
    install -Dm755 ${_pkgname}-setup_device.sh "${pkgdir}/usr/bin/${_pkgname}-setup_device"
    install -Dm644 ${_pkgname}.1.gz "${pkgdir}/usr/share/man/man1/${_pkgname}.1.gz"
    install -d "${pkgdir}/usr/share"
    cp -R ${_pkgname}/server "${pkgdir}/usr/share/${_pkgname}"
    install -d "${pkgdir}/usr/share/gnome-shell/extensions"
    cp -R ${_pkgname}/gnome-shell/deskcon@screenfreeze.net "${pkgdir}/usr/share/gnome-shell/extensions/deskcon@screenfreeze.net"
}
