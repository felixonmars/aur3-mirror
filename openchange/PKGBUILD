# Maintainer: Michael Hansen <zrax0111 gmail com>
# Contributor: ngoonee <ngoonee.talk@gmail.com>
# Contributor: Adam Russell <adamlr6+arch@gmail.com>
pkgname=openchange
_codename=QUADRANT
_dlid=220
pkgver=2.0
pkgrel=1
pkgdesc="Library for MAPI connectivity with Microsoft Exchange. This package was SPECIFICALLY created to support evolution-mapi and may not work for any other purpose."
arch=('i686' 'x86_64')
url="http://www.openchange.org"
license=('GPL3')
depends=('samba>=4.0.2-1' 'libical' 'sqlite3' 'file' 'boost' 'python2')
makedepends=('ccache' 'python2' 'subversion' 'docbook-xsl' 'libxslt'
             'flex' 'bison' 'autoconf' 'automake')
options=(!makeflags)
# Releases are mirrored at http://tracker.openchange.org/projects/openchange/files
source=("http://tracker.openchange.org/attachments/download/${_dlid}/${pkgname}-${pkgver}-${_codename}.tar.gz")

sha256sums=('fa9032853104a72e939bf972ccc3805000c1c40957f2905222a7f2665f05acd7')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}-${_codename}

    SAVEIFS=${IFS}
    IFS=$(echo -en "\n\b")
    PYTHON_CALLERS="$(find ${srcdir}/${pkgname}-${pkgver}-${_codename} -name '*.py')
$(find ${srcdir}/${pkgname}-${pkgver}-${_codename}/ -name 'configure.ac')"
    sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
           -e "s|python-config|python2-config|" \
           -e "s|bin/python|bin/python2|" \
        ${PYTHON_CALLERS}
    IFS=${SAVEIFS}

    # Fix linking of boost_thread in autoconf test
    sed -i -e "s|-lboost_thread\$BOOST_LIB_SUFFIX|-lboost_thread\$BOOST_LIB_SUFFIX -lboost_system\$BOOST_LIB_SUFFIX|" \
        configure.ac

    export PYTHON=/usr/bin/python2

    export PKG_CONFIG_PATH="/usr/samba/lib/pkgconfig:/usr/lib/pkgconfig"
    ./autogen.sh
    ./configure --prefix=/usr --enable-pymapi
    make FLEX=/usr/bin/flex BISON=/usr/bin/bison || return 1
}

package() {
    _pyver=`python2 -c 'import sys; print(sys.version[:3])'`

    cd ${srcdir}/${pkgname}-${pkgver}-${_codename}
    make DESTDIR="$pkgdir/" install

    cd ${pkgdir}/usr/lib/
    ln -s libmapi.so libmapi.so.0
    ln -s libocpf.so libocpf.so.0

    find ${pkgdir}/usr/lib/python${_pyver}/site-packages/ -name '*.py' | \
         xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
}
