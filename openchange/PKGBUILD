# Maintainer: Adam Russell <adamlr6+arch@gmail.com>
# Contributor: ngoonee <ngoonee.talk@gmail.com>
pkgname=openchange
_codename=BORG
_dlid=180
pkgver=1.0
pkgrel=2
pkgdesc="Library for MAPI connectivity with Microsoft Exchange. This package was SPECIFICALLY created to support evolution-mapi and may not work for any other purpose."
arch=('i686' 'x86_64')
url="http://www.openchange.org"
license=('GPL3')
depends=('samba4=4.0.0A18' 'popt' 'libical' 'sqlite3' 'file' 'boost' 'zlib')
makedepends=('ccache' 'python2' 'subversion' 'git' 'docbook-xsl' 'libxslt' 'flex' 'bison' 'autoconf' 'automake')
options=(!makeflags)
# Releases are mirrored at http://tracker.openchange.org/projects/openchange/files
source=("http://tracker.openchange.org/attachments/download/${_dlid}/${pkgname}-${pkgver}-${_codename}.tar.gz")

md5sums=('9b57a325e90e5ad3a702ddc6e8762ab6')
_prefix="/opt/samba4"

build() {
	cd ${srcdir}/${pkgname}-${pkgver}-${_codename}

	SAVEIFS=${IFS}
	IFS=$(echo -en "\n\b")
	PYTHON_CALLERS="$(find ${srcdir}/${pkgname}-${pkgver}-${_codename} -name '*.py')
$(find ${srcdir}/${pkgname}-${pkgver}-${_codename}/  -type f)
$(find ${srcdir}/${pkgname}-${pkgver}-${_codename}/ -name 'configure.ac')"
	sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
	       -e "s|python-config$|python2-config|" \
	       -e "s|bin/python$|bin/python2|" \
	    ${PYTHON_CALLERS}
	IFS=${SAVEIFS}

	export PYTHON=/usr/bin/python2

	export PKG_CONFIG_PATH="${_prefix}/samba/lib/pkgconfig:${_prefix}/lib/pkgconfig"
	export BOOST_LIB_SUFFIX="-mt"
	./autogen.sh
	./configure --prefix=${_prefix} --with-samba=${_prefix}
	make FLEX=/usr/bin/flex BISON=/usr/bin/bison || return 1
}

package() {
	_pyver=`python2 -c 'import sys; print(sys.version[:3])'`

	cd ${srcdir}/${pkgname}-${pkgver}-${_codename}
	make DESTDIR="$pkgdir/" install

	cd ${pkgdir}/${_prefix}/lib/
	ln -s libmapi.so libmapi.so.0
	ln -s libocpf.so libocpf.so.0

	find ${pkgdir}/${_prefix}/lib/python${_pyver}/site-packages/ -name '*.py' | \
	     xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
}
