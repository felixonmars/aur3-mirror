# Maintainer: Daniel Renninghoff <renninghoff AT archlinux DOT info>
# Contributor: gucong <gucong43216@gmail.com>
# Contributor: P. A. LÃ³pez-Valencia <palopezv AT gmail DOT com>

pkgname=hoichess
pkgver=0.12.1
pkgrel=2
pkgdesc="Xboard compatible chess engine and xiangqi (Chinese chess) engine"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.hoicher.de/hoichess"
depends=()
source=(${url}/download/${pkgname}-${pkgver}-src.tar.gz)
md5sums=('d570b7d1df1f1ac8ce321379d6c41692')

prepare() {
  cd "$srcdir/$pkgname-$pkgver-src"
  sed -i 's/games/bin/' Makefile
  sed -i '/i686/d' src/Makefile.config
  sed -i 's/CXXFLAGS\ =/CXXFLAGS\ +=/' src/Makefile
  sed -i '/\-O3/d' src/Makefile.config
}

build() {
  cd "$srcdir/$pkgname-$pkgver-src"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver-src"
  make DESTDIR="$pkgdir" install
}
