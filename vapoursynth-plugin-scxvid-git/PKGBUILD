# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=scxvid
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20121203.a805582
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/dubhater/vapoursynth-${_plug}"
license=('GPL')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
makedepends=('git' 'clang')
source=("${_plug}::git://github.com/dubhater/vapoursynth-${_plug}.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  echo "all:
	  clang -shared -fPIC -o lib${_plug}.so ${_plug}.c -lxvidcore $CXXFLAGS" > Makefile
}

build() {
  cd "${srcdir}/${_gitname}"

  make
}

package(){
  cd "${srcdir}/${_gitname}"
  install -Dm755 "lib${_plug}.so" "${pkgdir}/usr/lib/vapoursynth/lib${_plug}.so"
  install -Dm644 README "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
