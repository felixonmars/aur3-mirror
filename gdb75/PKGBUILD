# $Id$
# Maintainer: Felix Schindler <felix at schindlerfamily dot de>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdb75
_real_name=gdb
pkgver=7.5.1
pkgrel=1
pkgdesc="The GNU Debugger"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python2' 'gcc46')
makedepends=('texinfo')
conflicts=('gdb')
backup=('etc/gdb/gdbinit')
options=('!libtool' 'staticlibs' '!emptydirs')
install=gdb.install
source=(http://ftp.gnu.org/gnu/gdb/${_real_name}-${pkgver}.tar.bz2{,.sig} 0001-Fix-bdf-texinfo.patch)
md5sums=('3f48f468b24447cf24820054ff6e85b1'
         'SKIP'
         '4e2ebf5c06e81038039983bbe627676d')

build() {

  cd ${srcdir}/${_real_name}-${pkgver}

  msg "Applying patches..."
  patch -p1 -i ${srcdir}/0001-Fix-bdf-texinfo.patch

  msg "Building ${_real_name}-${pkgver}"
  ./configure \
    --prefix=/usr --disable-nls \
    --with-system-readline \
    --with-python=/usr/bin/python2 \
    --with-system-gdbinit=/etc/gdb/gdbinit \
    CC=gcc-4.6.4 CXX=g++-4.6.4 F77=gfortran-4.6.4

  CPPFLAGS='' CC=gcc-4.6.4 CXX=g++-4.6.4 F77=gfortran-4.6.4 make
}

package() {
  cd ${srcdir}/${_real_name}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install "custom" system gdbinit
  install -dm755 $pkgdir/etc/gdb
  touch $pkgdir/etc/gdb/gdbinit

  # resolve conflicts with binutils
  rm ${pkgdir}/usr/include/{ansidecl,bfd,bfdlink,dis-asm,symcat}.h
  rm ${pkgdir}/usr/lib/{libbfd,libopcodes}.a
  [ -e ${pkgdir}/usr/lib/libiberty.a ] && rm ${pkgdir}/usr/lib/libiberty.a
  [ -e ${pkgdir}/usr/lib64/libiberty.a ] && rm ${pkgdir}/usr/lib64/libiberty.a
  rm ${pkgdir}/usr/share/info/{bfd,configure,standards}.info
}
