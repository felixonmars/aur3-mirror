# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

pkgname=thunderbird-nightly
pkgver=7.0a1
pkgrel=1
pkgdesc="Standalone Mail/News reader - Nightly build"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'mozilla-common' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'fontconfig' 'freetype2' 'cairo' 'desktop-file-utils' 'libpng')
optdepends=('libcanberra: for sound support'
            'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
install=${pkgname}.install
source=(http://ftp.mozilla.org/pub/thunderbird/nightly/latest-comm-central/thunderbird-${pkgver}.en-US.linux-${CARCH}.tar.bz2
        ${pkgname}.desktop)
sha512sums=(`wget ${source/ta*}checksums -qO - | grep bz | cut -d " " -f1`
            '9a9bbe38c27fb4fd172a7be75755a8222a80b2bc3df6dcb8dafcc0088787f58f25e50dddfcd0a7da12677efffda20001003b255ea715dc07b831db4d57255ff4')

package() {
  sed -i 's|/usr/local/lib|/opt|' thunderbird/thunderbird
  install -dm755 "${pkgdir}"/{usr/{bin,share/applications},opt}
  cp -a thunderbird "${pkgdir}/opt/thunderbird-${pkgver}"
  ln -s /opt/thunderbird-${pkgver}/thunderbird "${pkgdir}/usr/bin/${pkgname}"
  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -D thunderbird/chrome/icons/default/default${i/x*/}.png "${pkgdir}/usr/share/icons/hicolor/${i}/apps/${pkgname}.png"
  done
  install -m644 ${pkgname}.desktop "${pkgdir}/usr/share/applications"
}