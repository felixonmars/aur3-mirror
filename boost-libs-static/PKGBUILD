# $Id$
# Maintainer: Mikhail Davidov <sirus@haxsys.net>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

pkgname=('boost-libs-static')
pkgver=1.54.0
_boostver=${pkgver//./_}
pkgrel=1
url="http://www.boost.org/"
arch=('i686' 'x86_64')
license=('custom')
makedepends=('icu>=51.1' 'python' 'python2' 'bzip2' 'zlib' 'openmpi')
source=(http://downloads.sourceforge.net/boost/boost_${_boostver}.tar.gz
        boost-1.53.0-python3.patch
        boost-1.54.0-Fix-macro-for-int128-detection.patch)
sha1sums=('069501636097d3f40ddfd996d29748bb23591c53'
          '34026072a7cb2534164f20e77bb71a5c75093307'
          'bf5177694ab8a0df6bc13aa47b05727c40febebb')


build() {
   export _stagedir="${srcdir}/stagedir"
   local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

   cd "${srcdir}/boost_${_boostver}"

   patch -Np2 -i ../boost-1.54.0-Fix-macro-for-int128-detection.patch

   # Fix build errors with python 3
   sed -i "/PYTHON_ROOT/s/print sys.prefix/print(sys.prefix)/g" bootstrap.sh
   patch -Np1 -i ../boost-1.53.0-python3.patch

   # Shut up strict aliasing warnings
   echo "using gcc : : : <compileflags>-fno-strict-aliasing ;" >> ./tools/build/v2/user-config.jam
   # Add an extra python version. This does not replace anything and python 2.x need to be the default.
   echo "using python : 3.3 : /usr/bin/python3 : /usr/include/python3.3m : /usr/lib ;" >> ./tools/build/v2/user-config.jam
   # Support for OpenMPI
   echo "using mpi ;" >> ./tools/build/v2/user-config.jam

   ./bootstrap.sh --with-toolset=gcc --with-icu --with-python=/usr/bin/python2

   _bindir="bin.linuxx86"
   [ "${CARCH}" = "x86_64" ] && _bindir="bin.linuxx86_64"

   install -d -m 755 "${_stagedir}"/bin
   install "${srcdir}"/boost_${_boostver}/tools/build/v2/engine/${_bindir}/bjam "${_stagedir}"/bin/bjam

   pushd tools
   for _tool in bcp inspect quickbook compiler_status process_jam_log wave; do
      "${_stagedir}"/bin/bjam --toolset=gcc $_tool
   done
   "${_stagedir}"/bin/bjam --toolset=gcc cflags="-std=gnu++11" library_status
   popd
   cp -a dist/bin/* "${_stagedir}"/bin

   # default "minimal" install: "release link=shared,static
   # runtime-link=shared threading=single,multi"
   # --layout=tagged will add the "-mt" suffix for multithreaded libraries
   # and installs includes in /usr/include/boost.
   # --layout=system no longer adds the -mt suffix for multi-threaded libs.
   # install to ${_stagedir} in preparation for split packaging
   "${_stagedir}"/bin/bjam \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=static \
      toolset=gcc \
      python=2.7 \
      --layout=system \
      --prefix="${_stagedir}" \
      ${JOBS} \
      install
}

package_boost-libs-static() {
   pkgdesc="Free peer-reviewed portable C++ source libraries - Static Libraries"
   depends=('bzip2' 'zlib' 'icu' "boost-libs=${pkgver}")
   optdepends=('openmpi: for mpi support')

   install -d -m 755 "${pkgdir}"/usr/lib
   cp -a "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib

   install -D -m 644 "${srcdir}/"boost_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/licenses/boost-libs-static/LICENSE_1_0.txt
}
