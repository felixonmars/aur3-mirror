# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=binwalk
pkgver=1.3.0
pkgrel=4
pkgdesc="A tool for searching a given binary image for embedded files"
arch=('i686' 'x86_64')
url="http://binwalk.org"
license=('MIT')
depends=('python2-magic' 'python2-pyqtgraph' 'python2-pydeep' 'python2-opengl')
optdepends=('firmware-mod-kit: Firmware extract rules helper'
            'git' 'mtd-utils' 'zlib' 'python2-liblzma' 'ncompress' 'gzip' 'bzip2' 'tar'
            'arj' 'p7zip' 'jdk7-openjdk')
source=(https://github.com/devttys0/${pkgname}/archive/v${pkgver}.tar.gz ${pkgname}.patch)
install=binwalk.install
md5sums=('6e79eda646277728719df596ca5d7bae'
         'ead01d529ca8edc03c3a7e046e6bf8e5')

prepare() {
  cd "${srcdir}"
  patch -p0 < binwalk.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  python2 setup.py build
  cd C
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  cd C
  make DESTDIR="${pkgdir}" install
  cd ..
  python2 setup.py install --root="${pkgdir}"
  install -m 644 -D "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  chmod a+x "${pkgdir}"/usr/lib/lib*
}
