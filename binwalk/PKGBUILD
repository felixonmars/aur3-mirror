# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=binwalk
pkgver=0.5.0
pkgrel=1
pkgdesc="A tool for searching a given binary image for embedded files"
arch=('i686' 'x86_64')
url="http://code.google.com/p/binwalk/"
license=('MIT')
depends=('file' 'curl')
provides=('binwalk')
source=(http://binwalk.googlecode.com/files/$pkgname-$pkgver.tar.gz
        binwalk.license)
md5sums=('110dd8087ee1db11a56fdaf8801c5372'
         '544998fb9b84e6dd6b3808965f9c8866')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"
  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  install -m 755 -D $pkgname $pkgdir/usr/bin/$pkgname
  for f in magic.bin*
  do
    install -m 644 -D $f $pkgdir/etc/$pkgname/$f
  done
  install -m 644 -D ../../binwalk.license $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
