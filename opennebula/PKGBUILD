# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Mikhail Kulemin <mihkulemin@gmail.com>
# Contributor: Michael Pusterhofer <pusterhofer (at) student (dot) tugraz (dot) at>

pkgname=opennebula
_unstable_pkg=opennebula-unstable
pkgver=3.8.1
pkgrel=1
pkgdesc="Virtual management infrastructure as a service (IaaS) toolkit for cloud computing (NOTE: Read the PKGBUILD!)"
arch=('i686' 'x86_64')
url="http://opennebula.org/software:rnotes:rn-rel3.8"
license=('Apache')
depends=('ruby>=1.8.7' 'xmlrpc-c-abyss>=1.06' 'openssl>=0.9.8' 'sqlite3>=3.6' 'openssh' 'libxml2>=2.7'
  'curl' 'libxslt' 'expat' 'cdrkit')
makedepends=('xmlrpc-c-abyss>=1.06' 'pkgconfig' 'scons>=0.98')
optdepends=('nfs-utils: for using the shared file system storage model'
  'mysql>=5.1: optional replacement for SQLite as the DB back-end'
  'libmysqlclient>=5.1: required if using MySQL instead of SQLite')
conflicts=('opennebula-unstable')
install=opennebula.install
changelog=ChangeLog
source=("http://dev.opennebula.org/packages/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz"
        'opennebula.service'
        'oned'
        'chown_fix.patch'
        'var_run.patch')
sha512sums=('bc6e51bba1998203c76225b724aaacf23d111d2f617760da1d960069c355cfb176c249b04b1ff19d5145ff16d4059fdf463d792ae6d98de88fe04291219611f4'
            '00db19d355a789fb27320d16917804354fc824c99a851e755a58943824377f87f158e0e560773957acfc3f959a2937462391271d309aff4e6aabb50cb964009d'
            'c72d136d8b83e3c97d94e5898c0982f3f66c32caac1dcd4a8fd88b06a99bd72447e96642d6129ace6502ada15668c9f32345f370b9bc21e68097edb9b1ecc7bd'
            '51c7fa545d747509d889f4402ada5ab49d14683373d3036a20b795b798fe1482852eeedbc7a7b90d4585e960d6f7049992f42c4614d7069ab9e03068ce60f3bc'
            '2a1f00a61e524430c627ceaa6329dc5ea27486ac08154902b49545f76a37c072ac22b3e0b5d7370ece64de000c5d13facdfc406fa17e472d73aaac919c086bb5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #############################################################################
  ##                                                                         ##
  ## It is highly recommended that you read the documentation and tweak the  ##
  ##   PKGBUILD accordingly:                                                 ##
  ## http://opennebula.org/documentation:rel3.8:compile                      ##
  ##                                                                         ##
  ## This package assumes a self-contained install. If you do NOT want a     ##
  ##   self-contained install, then remove `-d /srv/cloud/one` from the      ##
  ##   package() function and MAKE SURE you properly change the appropriate  ##
  ##   sections of opennebula.install                                        ##
  ##                                                                         ##
  #############################################################################

  # This builds the vanilla OpenNebula package. Tweak this line as desired.
  scons
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -D -m755 "$srcdir"/oned "$pkgdir"/etc/rc.d/one
  install -D -m644 "$srcdir"/opennebula.service "$pkgdir"/usr/lib/systemd/system/opennebula.service

  # Patch upstream install script to not attempt to chown the install
  # directories because `makepkg` will otherwise fail on a fresh installation.
  # We do our own chown in post_install().
  patch < "$srcdir/chown_fix.patch"

  # Patch upstream install script to use /run/one instead of /var/run/one.
  patch < "$srcdir/var_run.patch"

  # This checks to see whether OpenNebula is currently installed. To avoid
  # a potentially scary message, errors are sent to /dev/null
  if [[ ("$(pacman -Qq ${pkgname} 2>/dev/null)" == "${pkgname}") || ("$(pacman -Qq ${_unstable_pkg} 2>/dev/null)" == "${_unstable_pkg}") ]]; then
    # Use -k when running ./install.sh to keep previous configuration files
    DESTDIR="$pkgdir/" ./install.sh -k -u oneadmin -g cloud -d /srv/cloud/one
  else
    # Do not use -k when running ./install.sh for new installations
    DESTDIR="$pkgdir/" ./install.sh -u oneadmin -g cloud -d /srv/cloud/one
  fi
}

# vim:set ts=2 sw=2 et:
