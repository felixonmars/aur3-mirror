# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org> ([core] package)

pkgname=openvpn-git
pkgver=20120705
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network) - git checkout"
arch=(i686 x86_64)
url="http://openvpn.net/index.php/open-source.html"
depends=('openssl' 'lzo2' 'iproute2')
optdepends=('easy-rsa: for easy key management')
conflicts=('openvpn')
provides=('openvpn=2.3.0')
license=('custom')
backup=(etc/conf.d/openvpn-tapdev)
source=(openvpn.rc
        openvpn-tapdev.rc
        openvpn-tapdev.conf)

_gitroot="git://openvpn.git.sourceforge.net/gitroot/openvpn/openvpn.git"
_gitname="openvpn"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Build openvpn
  autoreconf -vi
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure \
    --prefix=/usr \
    --enable-password-save \
    --mandir=/usr/share/man \
    --enable-iproute2
  make

  # Build plugins
  for plug in auth-pam down-root; do
    cd ${srcdir}/${_gitname}-build/src/plugins/${plug}
    make
  done
}

package() {
  cd ${srcdir}/${_gitname}-build

  # Install openvpn
  make DESTDIR=${pkgdir} install
  install -d -m755 ${pkgdir}/etc/openvpn

  # Install examples
  install -d -m755 ${pkgdir}/usr/share/openvpn
  cp -r sample/sample-config-files ${pkgdir}/usr/share/openvpn/examples
  find ${pkgdir}/usr/share/openvpn -type f -exec chmod 644 {} \;
  find ${pkgdir}/usr/share/openvpn -type d -exec chmod 755 {} \;

  # Install license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

  # Install plugins
  for plug in auth-pam down-root; do
    install -D -m755 ${srcdir}/${_gitname}-build/src/plugins/${plug}/.libs/openvpn-plugin-${plug}.so ${pkgdir}/usr/lib/openvpn/openvpn-plugin-${plug}.so
  done

  # Install contrib
  install -d -m755 ${pkgdir}/usr/share/openvpn/contrib
  cp -r contrib ${pkgdir}/usr/share/openvpn

  # Install rc scripts
  install -D -m755 ${srcdir}/openvpn.rc ${pkgdir}/etc/rc.d/openvpn
  install -D -m755 ${srcdir}/openvpn-tapdev.rc ${pkgdir}/etc/rc.d/openvpn-tapdev
  install -D -m644 ${srcdir}/openvpn-tapdev.conf ${pkgdir}/etc/conf.d/openvpn-tapdev
}

sha256sums=('e5a0c90ccb454f68141dcbcaf132f5e284d9459f37f55e85bbcc8de42026591a'
            'c2d36429fec4bda9111837b634744070a7aeb3e8705d2ade0381358891971552'
            '2f4eee088858128a22d9dc76e3668080a54d41421196ee3f0f32f72e05908548')
