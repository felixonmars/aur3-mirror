# Maintainer: sudokode <sudokode at gmail dot com>
# Previous Maintainer: Crass00 <crass00 at hotmail dot com>
# Previous Maintainer: Superstar655 <choman000 at hotmail dot com>
# Contributor: Augusto Born de Oliveira <augustoborn at gmail dot com>

pkgname=sickbeard-git
pkgver=20120605
pkgrel=1
pkgdesc="A PVR application that downloads and manages your TV shows"
arch=(any)
url="http://code.google.com/p/sickbeard/"
license=(GPL3)
depends=(python2 python2-cheetah)
makedepends=(git)
optdepends=('sabnzbd: NZB downloader'
				'python-notify: desktop notifications')
backup=(etc/conf.d/sickbeard)
install=sickbeard.install
conflicts=(sickbeard)
source=('sickbeard' 'sickbeard.init' 'sickbeard.confd')
md5sums=('873799bc75eabd3a9c74e9cf208fc3cb'
         'b34db0406f97cde52c5a2afedbb440be'
         '34288612400b8928604fba7b42bfbec9')

_gitroot="git://github.com/midgetspy/Sick-Beard.git"
_gitname="sickbeardpvr"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ]; then
     cd $_gitname && git pull origin
     msg "The local files are updated."
  else
     git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting install..."
}

package() {
  mkdir -p "${pkgdir}/opt/"
  cp -r "$srcdir/$_gitname" "${pkgdir}/opt/sickbeard"

  sed -i 's/python/python2/g' "${pkgdir}/opt/sickbeard/autoProcessTV/sabToSickBeard.py"
  sed -i 's/python/python2/g' "${pkgdir}/opt/sickbeard/autoProcessTV/hellaToSickBeard.py"

  install -D -m755 "${srcdir}/sickbeard" "${pkgdir}/usr/bin/sickbeard"
  install -D -m755 "${srcdir}/sickbeard.init" "${pkgdir}/etc/rc.d/sickbeard"
  install -D -m644 "${srcdir}/sickbeard.confd" "${pkgdir}/etc/conf.d/sickbeard"
}
