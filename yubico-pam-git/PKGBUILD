# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-pam-git
pkgver=20120616
pkgrel=1
pkgdesc="Yubico yubikey PAM module"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-pam"
license=('BSD')
depends=('pam' 'yubico-c-client-git' 'yubikey-personalization-git')
makedepends=('git')
provides=('pam_yubico' 'yubico-pam')
conflicts=('pam_yubico' 'yubico-pam')
source=('automake.patch')

_gitroot="git://github.com/Yubico/yubico-pam.git"
_gitname="yubico-pam"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  patch -Np1 < ${srcdir}/automake.patch

  autoreconf --install
  ./configure --prefix=/usr
  make
  make check
}

package() {
  cd "${srcdir}/${_gitname}-build"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -D -m644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  make DESTDIR="${pkgdir}/" install
}

sha256sums=('8f99572a438111b1b67357526f33004a9335bb72f9b0a4c5fe3de348e27db77b')
