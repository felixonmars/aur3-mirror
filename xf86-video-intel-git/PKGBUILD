# Maintainer: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: breakdown <breakdown[at]archlinux[dot]us>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: William Heinbockel <wheinbockel[at]gmail[dot]com>

pkgname=xf86-video-intel-git
pkgver=20121016
pkgrel=1
pkgdesc='X.org Intel i810/i830/i915/945G/G965+ video drivers'
arch=('i686' 'x86_64')
url='http://intellinuxgraphics.org/'
license=('custom')
depends=('intel-dri' 'libxvmc' 'libpciaccess' 'libdrm-git' 'dri2proto>=2.6' 'xcb-util' 'libxfixes' 'udev')
makedepends=('git' 'xorg-server-devel' 'libx11' 'xf86driproto' 'glproto' 'resourceproto' 'mesa')
provides=('xf86-video-intel')
conflicts=('xf86-video-intel')
options=('!libtool')

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-intel'
_gitname='xf86-video-intel'

build() {
  msg 'Connecting to git.freedesktop.org GIT server....'

  if [ -d $_gitname ] ; then
    pushd $_gitname
    git pull origin
    popd
    msg 'The local files are updated.'
  else
    git clone $_gitroot
  fi

  msg 'GIT checkout done or server timeout'

  [ -d build ] && rm -rf build
  cp -r $_gitname build
  cd build

  msg 'Starting make...'

  ./autogen.sh \
    --prefix=/usr \
    --enable-dri
    # other useful options you can add (as you see fit):
    # --disable-xvmc          Disable XvMC support [[default=yes]]
    # --enable-kms-only       Assume KMS support [[default=no]]
    # --enable-sna            Enable SandyBridge's New Acceleration (SNA)
    #                         [options=default|gen2|gen3|ge4|gen5|gen6]

  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
