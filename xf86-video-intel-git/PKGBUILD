# -*- shell-script -*-
#
# Contributor: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Thomas Dziedzic < gostrc [at] gmail >
# Contributor: breakdown <breakdown[at]archlinux[dot]us>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: William Heinbockel <wheinbockel[at]gmail[dot]com>
# Maintainer:  Adrian C. <anrxc..sysphere.org>

pkgname=xf86-video-intel-git
_gitname=xf86-video-intel
pkgver=2.99.914.84.g407817e
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=("i686" "x86_64")
url="http://intellinuxgraphics.org/"
license=("custom")
depends=("intel-dri" "libxvmc" "libpciaccess" "libdrm" "dri2proto" "xcb-util" "libxfixes" "udev")
makedepends=("git" "xorg-server-devel" "X-ABI-VIDEODRV_VERSION=18" "libx11"
             "xf86driproto" "glproto" "resourceproto" "scrnsaverproto" "mesa" "libxrender")
provides=("xf86-video-intel")
conflicts=("xf86-video-intel")
replaces=("xf86-video-intel")
options=("!libtool")
source=(${pkgname}::git://anongit.freedesktop.org/git/xorg/driver/${_gitname})
sha1sums=("SKIP")


pkgver() {
  cd "${pkgname}"

# Version the software
  git describe --always | sed 's|-|.|g'
}


build() {
  cd "${pkgname}"

# Build the software
# - enable Glamor acceleration:   --enable-glamor
# - enable SandyBridge new accel: --enable-sna
# - assume KMS support:           --enable-kms-only
  ./autogen.sh --prefix=/usr \
     --enable-dri \
     --enable-xvmc
  make
}


package () {
  cd "${pkgname}"

# Install the software
  make DESTDIR=${pkgdir} install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
