# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: Yichao Yu <yyc1992@gmail.com>
# Contributor: kfgz <kfgz@interia.pl>

_pkgname=glib2
pkgname=$_pkgname-git
pkgver=20120915
pkgrel=1
pkgdesc="Common C routines used by GTK+ 2.4 and other libs"
arch=(i686 x86_64)
url=http://www.gtk.org/
license=(LGPL)
depends=(python2)
makedepends=(gtk-doc git)
provides=($_pkgname=2.33.7)
conflicts=($_pkgname)
options=(!libtool !docs !emptydirs)
source=($_pkgname.sh
    $_pkgname.csh)
sha256sums=('9456872cdedcc639fb679448d74b85b0facf81033e27157d2861b991823b5a2a'
    '8d5626ffa361304ad3696493c0ef041d0ab10c857f6ef32116b3e2878ecf89e3')
sha512sums=('dca2bc74d2013fcb24145ac794eef457aa3213c039e40a1a26ca5017694930768e7c80e334e17a56834549dff6549c781ddd91fae6c7bbb26fdd6a083ad8217a'
    'c3899eb7fa5482ce8a35fe02db90fd0f928d50aa7e4365a9529ef35a2dcd1ed86d5a24f6bc5c635ef5b2d95a0ebfebc2bb6bc90404c99f6fb7484ed2fa032c06')

_gitroot=git://git.gnome.org/glib
_gitname=glib

build() {
    cd "$srcdir"

    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]] ; then
        pushd $_gitname && git pull origin
        msg "The local files are updated."
        popd
    else
        git clone ${_gitroot}
    fi
    msg "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh --prefix=/usr \
        --libdir=/usr/lib \
        --sysconfdir=/etc \
        --with-pcre=system \
        --disable-fam
    find -name Makefile -exec sed -i 's:PYTHON = /usr/bin/python:&2:g' '{}' \;
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

    cd ../
    for i in $_pkgname.{,c}sh; do
        install -Dm755 $i "$pkgdir"/etc/profile.d/$i
    done

    for i in "$pkgdir"/usr/share/bash-completion/completions/*; do
        chmod -x "$i"
    done
    sed -i 's:^#!/usr/bin/env python$:&2:g' "$pkgdir"/usr/bin/gdbus-codegen
}
