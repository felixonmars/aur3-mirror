## Contributor: Georgij Kondratjev <smpuj@bk.ru>

pkgname=bespin-svn
pkgver=1606
pkgrel=1
pkgdesc="Qt4/KDE4 style"
arch=(i686 x86_64)
url="http://kde-look.org/content/show.php/Bespin?content=63928"
license=('LGPL')
depends=('qt' 'kdelibs')
optdepends=('kdebase-workspace')
makedepends=('cmake>=2.4' 'automoc4' 'subversion' 'kdebase-workspace')
provides=('bespin')
conflicts=('bespin' 'kdemod4-kstyles')
source=()
md5sums=()

_svntrunk="https://cloudcity.svn.sourceforge.net/svnroot/cloudcity"
_svnmod="bespin"

build() {
  cd ${srcdir}

  if [[ -d ${_svnmod}/.svn ]]; then
     (cd ${_svnmod} && svn up -r $pkgver)
  else
     svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${srcdir}/${_svnmod}-build
  cp -r ${srcdir}/${_svnmod} ${srcdir}/${_svnmod}-build
  cd ${srcdir}/${_svnmod}-build

  cmake -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` -DENABLE_ARGB=ON \
        -DCMAKE_BUILD_TYPE=Release || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -D ${srcdir}/${_svnmod}-build/man/bespin.1 ${pkgdir}/usr/share/man/man1/bespin.1
  install -D ${srcdir}/${_svnmod}-build/extras/bespin-compl ${pkgdir}/etc/bash_completion.d/bespin

}
