# Maintainer: Bastien Dejean <nihilhill@gmail.com>

pkgname=bspwm
pkgver=0.8.5
pkgrel=1
pkgdesc='A tiling window manager based on binary space partitioning'
arch=('i686' 'x86_64')
url='https://github.com/baskerville/bspwm'
license=('custom:BSD')
depends=('libxcb' 'xcb-util' 'xcb-util-wm')
optdepends=('sxhkd: to define keyboard and pointer bindings' 'xdo-git: for the example sxhkdrc' 'xtitle-git: for the example panels' 'sutils-git: for the example panels' 'bar-aint-recursive: for the example bar panel' 'dzen2-bskv-git: for the example dzen2 panel' 'txtw-git: for the example dzen2 panel'  'sres-git: for the example dzen2 panel' 'ttf-dejavu: for the example dzen2 panel')
provides=('bspwm')
conflicts=('bspwm')
source=("https://github.com/baskerville/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('726c35415b26991bdeb7da2e84bf8d44')

build() {
    cd "$srcdir/${pkgname}-${pkgver}"
    make PREFIX=/usr
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
