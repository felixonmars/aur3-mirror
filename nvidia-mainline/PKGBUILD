# Maintainer: Mikael Eriksson <mikael_eriksson@miffe.org>
# Original arch package by:
# Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-mainline
pkgver=280.13
pkgrel=1
pkgdesc="NVIDIA drivers for linux-mainline."
_kernver=$(pacman -Ql linux-mainline|grep -E /lib/modules/.|cut -d/ -f4|uniq)
_kernpkgver=$(pacman -Q linux-mainline|cut -d' ' -f 2)
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-mainline' "nvidia-utils=${pkgver}")
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('c3992c8cef02b7773a398640d8830e77')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('05e62b61feb34bf412f83ca1074e1917')
fi

build() {
	msg "Building for kernel version $_kernver"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    #echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
		eval pkgrel="${pkgrel}_${_kernpkgver/-/_}"
}
