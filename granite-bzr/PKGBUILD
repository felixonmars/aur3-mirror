# Contributor: Dan Serban
# Maintainer: Ner0

pkgname=granite-bzr
pkgver=231
pkgrel=1
pkgdesc="A development library for elementary development"
arch=('i686' 'x86_64')
url="https://launchpad.net/granite"
license=('LGPL3')
depends=('glib2' 'gobject-introspection' 'gtk3>=3.3.14' 'libgee')
makedepends=('bzr' 'cmake' 'vala>=0.16')
conflicts=('granite')
provides=('granite')

_bzrtrunk=lp:granite
_bzrmod=granite

build() {
  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  [ -d "$srcdir/$_bzrmod-build" ] && rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"
  [ -d build ] && rm -rf build
  mkdir build
  cd build
  CFLAGS="$CFLAGS -Wno-deprecated-declarations"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make DESTDIR="$pkgdir/" install
}
