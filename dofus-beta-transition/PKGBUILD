# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>
# Contributor: Schtroumpfette <fpeterschmitt@voila.fr>
pkgname=dofus-beta-transition
provides=('dofus-beta')
conflicts=('dofus-beta')
pkgver=2.14.0
pkgrel=4
pkgdesc="A manga inspired, Massively Multiplayer Online Role-playing Game (MMORPG) for Adobe AIR (beta version)."
url="http://www.dofus.com/"
license=("custom:Dofus License")
arch=('i686' 'x86_64')
source=(air-generic-launcher.sh
        launcher.sh
        transition.conf)
md5sums=('f179eaa5e6e6674b1853cf826fc33c3a'
         '24bbded117183bc01fdba7ff0b9e26d0'
         'edd23f756c37f95706fc33410a7d5e8f')
depends=('ankama-transition' 'adobe-air-sdk')
if [ "$CARCH" == "x86_64" ];then
  source+=(http://dl.ak.ankama.com/games/dofus2-beta/setup/setup-beta-transition-amd64.tar.gz)
  md5sums+=('05fc40837ee492240b267aef6dccb3c9')
  depends+=('lib32-gtk2' 'lib32-libxt' 'lib32-libjpeg6' 'lib32-libpng12'
            'lib32-openssl098' 'lib32-libldap' 'lib32-nss' 'lib32-libxslt'
            'lib32-dbus-glib' 'lib32-alsa-lib')
else
  source+=(http://dl.ak.ankama.com/games/dofus2-beta/setup/setup-beta-transition-x86.tar.gz)
  md5sums+=('72cb7433964c6f513a3d6984a98f4e86')
  depends+=('gtk2' 'libxt' 'libjpeg6' 'libpng12' 'openssl098' 'nss' 'libxslt' 'alsa-lib')
fi

package() {
  cd "$srcdir/DofusBeta"
  install_dir="$pkgdir/opt/dofus-beta"
  install -d "$install_dir"

  msg2 "Installing main applications..."
  cp -R bin share "$install_dir"
  install -D transition/transition.ici "$install_dir/transition/transition.ici"
  install -D transition/transition-service "$pkgdir/opt/ankama/transition/transition-service"

  install -D "$srcdir/air-generic-launcher.sh" "$install_dir/bin/air-generic-launcher.sh"
  install "$srcdir/air-generic-launcher.sh" "$install_dir/share/reg/bin/"

  msg2 "Installing launcher script..."
  install -m 644 "$srcdir/transition.conf" "$install_dir"
  install -D "$srcdir/launcher.sh" "$pkgdir/usr/bin/dofus-beta"

  msg2 "Creating fake Adobe Air..."
  ln -s /usr/bin/true "$pkgdir/usr/bin/Adobe AIR Application Installer"

  msg2 "Installing menu icon..."
  mkdir -p "$pkgdir/usr/share/applications"
  cat > "$pkgdir/usr/share/applications/Dofus-beta.desktop" <<EOF
[Desktop Entry]
Encoding=UTF-8
Type=Application
Name=Dofus
GenericName=Dofus
Comment=Dofus
Icon=/opt/dofus-beta/share/icon/dofus-icon-48.png
Exec=/usr/bin/dofus-beta
Path=/opt/dofus-beta/share/
Categories=Game;RolePlaying;Ankama-Games
EOF
}
# vim:set ts=2 sw=2 et:
