# Maintainer: Vincent Post <vincent.post@w1r3.net>
# Contributor: Chris Hodapp <clhodapp1@gmail.com>

_pkgname=craftbukkit
pkgname=${_pkgname}-git
pkgver=1.7.2.R0.2.15.g2f3dbd3
epoch=1
pkgrel=1
pkgdesc="Implementation of Bukkit Minecraft Server Mod API (git)"
arch=(any)
url="http://bukkit.org"
license=('LGPL')
depends=('tmux' 'java-runtime')
makedepends=('git' 'maven')
provides=("craftbukkit=$(IFS='.'; set -- ${pkgver}; echo ${1}.${2}.${3}${4}.${5} | sed 's|R|r|g')" "bukkit")
conflicts=('craftbukkit')
install=craftbukkit-git.install
source=("git+https://github.com/Bukkit/CraftBukkit.git"
        'craftbukkit.service'
        'craftbukkit.sh')
sha256sums=("SKIP"
            '9f3edffbb61fb647cf679b582793bae6c63c0d7bd9272a4cb821e9aac5828707'
            '70298cd0a102953ecc19ad44408e96d3050913b60344bebc84fa82e6918e89b5')

pkgver() {
        cd "${srcdir}/CraftBukkit"
        git describe --always --tags | sed 's|-|.|g'
}

build() {
        cd "${srcdir}/CraftBukkit"
        mvn clean package
}

package() {
        #cd "${srcdir}/CraftBukkit/target/"
        find "${srcdir}/CraftBukkit/target/" -name 'craftbukkit-*-SNAPSHOT.jar' | xargs -rtl1 -I {} install -Dm644 {} "${pkgdir}/srv/craftbukkit/craftbukkit.jar"
        install -m 755 "${srcdir}/craftbukkit.sh" "${pkgdir}/srv/craftbukkit/craftbukkit.sh"
        install -Dm644 "${srcdir}/craftbukkit.service" "${pkgdir}/usr/lib/systemd/system/craftbukkit.service"
}
