# Maintainer: Christian BÃ¼hler <christian.buehler@ipoplan.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qca-qt5
pkgver=2025.1c0a8d9
pkgrel=1
pkgdesc="Qt Cryptographic Architecture"
arch=('i686' 'x86_64')
url="http://delta.affinix.com/qca/"
license=('LGPL')
depends=('libsasl' 'nss' 'qt5-base')
makedepends=('git' 'cmake' 'extra-cmake-modules-git')
provides=('qca')
conflicts=('qca')
source=("git://anongit.kde.org/qca.git")
md5sums=('SKIP')

pkgver() {
  cd qca
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $srcdir/qca
  sed -i "s|/mkspecs/features|/lib/qt/mkspecs/features|" CMakeLists.txt
}

build() {
  cd $srcdir/qca
  cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DQCA_INSTALL_PLUGINS_IN_QT_PREFIX=ON \
        -DQT_PLUGINS_DIR=/usr/lib/qt/plugins
  make
}

package() {
  cd ${srcdir}/qca
  make DESTDIR=${pkgdir} install
}
