# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: archtux <antonio.arias99999@gmail.com>

pkgname=gsharkdown
pkgver=0.7.0
pkgrel=1
pkgdesc="Application for downloading and playing music from grooveshark.com"
arch=('any')
url="https://bitbucket.org/vkolev/gsharkdown"
license=('GPL3')
depends=('gstreamer0.10-python' 'python-configobj' 'dbus-python' 'python-pycurl')
optdepends=('python2-pylast: last.fm support'
            'python-notify: notification support'
            'xdg-user-dirs: autodetect xdg music dir'
            'libappindicator: indicator integration')
install="$pkgname.install"
source=("https://bitbucket.org/vkolev/gsharkdown/get/${pkgname}_$pkgver.tar.bz2"
        "$pkgname.desktop" "$pkgname.sh")
md5sums=('5559881131be466ef28357e52cdbf9ff'
         '81869090135791eba7e658619f4eafdd'
         'ede76e8bd3dcb1156e01f377a6c49cac')

package() {
  cd "$srcdir/vkolev"*

  # bin
  install -Dm755 ../$pkgname.sh \
    "$pkgdir/usr/bin/$pkgname"

  # data
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a * "$pkgdir/usr/share/$pkgname"

  # icon
  install -Dm644 data/gsharkdown_64.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # suggest to install python2-pylast from AUR
  find "$pkgdir" -name "*.po" -o -name "*.ui" -exec \
    sed -i "s/Please.*it/Install python2-pylast from [AUR]/" {} \;

  # python2 fix
  sed -i "s|env python|&2|" `grep -rl "env python" "$pkgdir"`
}
