# Maintainer: Devin Christensen <quixoten at gmail dot com>
pkgname=powerline-fonts-git
_gitname=powerline-fonts
pkgver=r51.1ceb8dc
pkgrel=1
pkgdesc="Powerline fonts for X11 and the console"
arch=('any')
url=('https://github.com/Lokaltog/powerline-fonts')
license=('CPL')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('git')
conflicts=('powerline-fonts')
install=$pkgname.install
source=('git://github.com/Lokaltog/powerline-fonts.git'
        'git://github.com/Lokaltog/powerline.git')
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  install -d "$pkgdir/usr/share/fonts/TTF"
  install -d "$pkgdir/usr/share/fonts/OTF"
  install -d "$pkgdir/usr/share/fonts/misc"
  install -d "$pkgdir/usr/share/kbd/consolefonts"
  install -d "$pkgdir/etc/fonts/conf.avail"

  cd $srcdir/$_gitname
  find . -iname "*.ttf" -exec install -m644 {} $pkgdir/usr/share/fonts/TTF \;
  find . -iname "*.otf" -exec install -m644 {} $pkgdir/usr/share/fonts/OTF \;
  find . -iname "*.pcf.gz" -exec install -m644 {} $pkgdir/usr/share/fonts/misc \;
  find . -iname "*.psf.gz" -exec install -m644 {} $pkgdir/usr/share/kbd/consolefonts \;

  cd $srcdir/powerline/font
  install -m644 PowerlineSymbols.otf $pkgdir/usr/share/fonts/OTF/
  install -m644 10-powerline-symbols.conf $pkgdir/etc/fonts/conf.avail/
}
