pkgname=mingw-w64-libunistring
pkgver=0.9.3
pkgrel=5
pkgdesc="Library for manipulating Unicode strings and C strings. (mingw-w64)"
arch=(any)
url="http://www.gnu.org/software/libunistring"
license=("GPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-libiconv)
options=(!libtool !strip !buildflags)
source=("ftp://ftp.gnu.org/gnu/libunistring/libunistring-${pkgver}.tar.gz")
md5sums=('db8eca3b64163abadf8c40e5cecc261f')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/libunistring-$pkgver"
  sed -i -e "s,gl_WOE32_DLL,gl_WOE32_DLL\nlt_cv_deplibs_check_method='pass_all'," configure.ac
  autoconf --force
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/libunistring-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-threads=win32
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
