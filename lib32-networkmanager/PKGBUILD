# oi_wtf <brainpower at gulli dot com> 
# Original-Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Original-Maintainer: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

pkgname=lib32-networkmanager
_pkgname=NetworkManager
pkgver=0.9.8.2
pkgrel=1
pkgdesc="Network Management daemon, 32bit libraries"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/$_pkgname/"
depends=(lib32-dbus-glib iproute2 lib32-libnl lib32-nss lib32-polkit udev wpa_supplicant dhcp-client
         lib32-libsoup lib32-systemd networkmanager)
makedepends=(intltool dhcpcd dhclient iptables gobject-introspection gtk-doc)
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver:0:3}/$_pkgname-$pkgver.tar.xz
        disable_set_hostname.patch)
sha256sums=('5c0b2f71b2f61e0dfbe95cebad83001758db252d5cdf5b6f7ceebac9c6bd7ead'
            '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../disable_set_hostname.patch
}

build() {
	export CC='gcc -m32'
	export CXX='g++ -m32'
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
	#export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:/usr/lib32"
	export LIBRARY_PATH="/usr/lib32:$LIBRARY_PATH"

  cd $_pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib/networkmanager \
    --sbindir=/usr/bin \
    --with-crypto=nss \
    --with-dhclient=/usr/bin/dhclient \
    --with-dhcpcd=/usr/bin/dhcpcd \
    --with-iptables=/usr/bin/iptables \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-udev-dir=/usr/lib/udev \
    --with-resolvconf=/usr/bin/resolvconf \
    --with-session-tracking=systemd \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax \
    --enable-modify-system \
    --enable-doc \
    --without-modem-manager-1 \
    --disable-qt # disable qt examples, lib32-qt4 contains wrong include dirs in pkgconfig

		make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

	rm -r "$pkgdir"/usr/{bin,lib,include,share} "$pkgdir/etc"
  rm -r "$pkgdir/var/run"
}
