# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_pkgbasename=openssl
pkgname=openssl098
_ver=0.9.8x
# use a pacman compatible version scheme
pkgver=0.9.8.x
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (0.9.8 branch)'
arch=('i686' 'x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=($_pkgbasename)
makedepends=('perl')
optdepends=('ca-certificates')
provides=('openssl-compatibility')
conflicts=('openssl-compatibility')
options=(!makeflags)
source=("https://www.openssl.org/source/${_pkgbasename}-${_ver}.tar.gz"
        'no-rpath.patch'
        'ca-dir.patch')
md5sums=('ee17e9bc805c8cc7d0afac3b0ef78eda'
         'dc78d3d06baffc16217519242ce92478'
         '3bf51be3a1bbd262be46dc619f92aa90')

build() {
  cd "$srcdir/$_pkgbasename-$_ver"

  # remove rpath: http://bugs.archlinux.org/task/14367
  patch -p0 -i "$srcdir/no-rpath.patch"
  # set ca dir to /etc/ssl by default
  patch -p0 -i "$srcdir/ca-dir.patch"
  # mark stack as non-executable: http://bugs.archlinux.org/task/12434
  ./config --prefix=/usr --openssldir=/etc/ssl --libdir=lib \
    shared zlib enable-md2 -Wa,--noexecstack

  make
}

package() {
  cd "$srcdir/$_pkgbasename-$_ver"

  install -Dm755 libssl.so.0.9.8 "$pkgdir/usr/lib/libssl.so.0.9.8"
  install -Dm755 libcrypto.so.0.9.8 "$pkgdir/usr/lib/libcrypto.so.0.9.8"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
