# This script is incomplete and in development.

# Maintainer: Lekensteyn <lekensteyn@gmail.com>
# Contributor: Samsagax <samsagax@gmail.com>

pkgname=bumblebee-git
_gitname=bumblebee
pkgver=3.2.12.g8244297
pkgrel=1
pkgdesc="Develop branch of Bumblebee. Optimus Support for Linux Through VirtualGL. You need to install proper drivers separately. Can be used with Nouveau or Nvidia"
arch=('i686' 'x86_64')
makedepends=('git')
depends=('virtualgl'
        'glib2' 'mesa-libgl')
optdepends=(
    'xf86-video-nouveau: Nouveau driver'
    'nouveau-dri: 3D acceleration features fo Nouveau'
    'bbswitch: switch on/off discrete card'
    'nvidia-utils-bumblebee: Nvidia utils not breaking LibGL'
    'nvidia: Nvidia kernel driver'
    'primus: faster back-end for optirun'
)
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
     optdepends[${#optdepends[@]}]='lib32-primus: faster back-end for optirun'
fi
url="http://www.Bumblebee-Project.org"
license=("GPL3")
install='bumblebee.install'
conflicts=('bumblebee')
provides=('bumblebee')
backup=('etc/bumblebee/bumblebee.conf')
source=("$_gitname::git://github.com/Bumblebee-Project/Bumblebee.git#branch=develop")
md5sums=('SKIP')

pkgver() {
    cd "$_gitname"

    git describe --always | sed 's|^v||;s|-|.|g'
}

build() {
    cd "$_gitname"

    autoreconf -fi
    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia-bumblebee:/usr/lib32/nvidia-bumblebee \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia-bumblebee/xorg/,/usr/lib/xorg/modules \
        --prefix=/usr \
        --sysconfdir=/etc \
        --without-pidfile
    make
}

package() {
    cd "$_gitname"

    make install DESTDIR="$pkgdir"
    install -D -m755 "scripts/systemd/bumblebeed.service" "$pkgdir/usr/lib/systemd/system/bumblebeed.service"
    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"
    #Install udev rule to prevent GH-#144
    install -D -m644 "conf/99-bumblebee-nvidia-dev.rules" "${pkgdir}/usr/lib/udev/rules.d/99-bumblebee-nvidia-dev.rules"
}
