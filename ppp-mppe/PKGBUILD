pkgname=ppp-mppe
pkgver=2.4.5
pkgrel=3
pkgdesc="A daemon which implements the PPP protocol for dial-up networking"
arch=('i686' 'x86_64')
url="http://www.samba.org/ppp/"
license=('custom:GPL/BSD')
depends=('glibc' 'libpcap>=1.0.0')
provides=('ppp')
conflicts=('ppp')
replaces=('ppp')
backup=(etc/ppp/{chap-secrets,pap-secrets,options,ip-up,ip-down})
source=(ftp://ftp.samba.org/pub/ppp/ppp-$pkgver.tar.gz
        https://wiresphere.de/files/mppc/ppp-2.4.4-mppe-mppc-1.1.patch
        options
        pon
        poff
        ppp
        plog
        pon.1
        ip-up
        ip-down
        ip-up.d.dns.sh
        ip-down.d.dns.sh)
md5sums=('4621bc56167b6953ec4071043fe0ec57'
         'f1705fe3dd68b86d466f0ecb92c9be54'
         '7a9259a8f038073eeea7e4552ff1849f'
         '48c024f73a80c8b69c4def22f86902cc'
         '2d811f8470ccdea3b8c4505a438483e9'
         '7561c73b557292d5ba1a9c5dbd270fde'
         '86cdaf133f7a79fb464f02d83afc7734'
         '44cc662ba9aa61dd9add3ddd4c5ded57'
         'e4beb16ed600b61336d50b2bd2df7cd5'
         '2fa0671f40711e69194ccf60979f8b33'
         '4a074d74c29625b254a1db720cb87b99'
         '8d1be5af8e46393ba6eb273377831e38')

build() {
  cd "$srcdir/ppp-$pkgver"

  patch -p1 -i "$srcdir/ppp-2.4.4-mppe-mppc-1.1.patch"

  # fix CFLAGS
  # -D_GNU_SOURCE is needed for IPv6 to work apparently
  export CFLAGS="$CFLAGS -D_GNU_SOURCE"
  sed -i "s:-O2 -pipe -Wall -g:${CFLAGS}:" pppd/Makefile.linux
  sed -i "s:-g -O2:${CFLAGS}:" pppd/plugins/Makefile.linux
  sed -i "s:-O2:${CFLAGS}:" pppstats/Makefile.linux
  sed -i "s:-O2 -g -pipe:${CFLAGS}:" chat/Makefile.linux
  sed -i "s:-O:${CFLAGS}:" pppdump/Makefile.linux

  # enable active filter
  sed -i "s:^#FILTER=y:FILTER=y:" pppd/Makefile.linux
  # enable ipv6 support
  sed -i "s:^#HAVE_INET6=y:HAVE_INET6=y:" pppd/Makefile.linux
  # Enable Microsoft proprietary Callback Control Protocol
  sed -i "s:^#CBCP=y:CBCP=y:" pppd/Makefile.linux 

  # Fix build error with recent kernels
  rm include/linux/if_pppol2tp.h

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/ppp-$pkgver"

  make DESTDIR="$pkgdir/usr" install
  install -D -m644 "$srcdir/options" "$pkgdir/etc/ppp/options"
  install -D -m755 "$srcdir/ip-up" "$pkgdir/etc/ppp/ip-up"
  install -D -m755 "$srcdir/ip-down" "$pkgdir/etc/ppp/ip-down"
  install -d -m755 "$pkgdir/etc/ppp/ip-up.d"
  install -d -m755 "$pkgdir/etc/ppp/ip-down.d"
  install -m755 "$srcdir/ip-up.d.dns.sh" "$pkgdir/etc/ppp/ip-up.d/00-dns.sh"
  install -m755 "$srcdir/ip-down.d.dns.sh" "$pkgdir/etc/ppp/ip-down.d/00-dns.sh"
  install -D -m755 "$srcdir/pon" "$pkgdir/usr/bin/pon"
  install -D -m755 "$srcdir/poff" "$pkgdir/usr/bin/poff"
  install -D -m755 "$srcdir/plog" "$pkgdir/usr/sbin/plog"
  install -D -m600 etc.ppp/pap-secrets "$pkgdir/etc/ppp/pap-secrets"
  install -D -m600 etc.ppp/chap-secrets "$pkgdir/etc/ppp/chap-secrets"
  install -D -m755 "$srcdir/ppp" "$pkgdir/etc/rc.d/ppp"
  install -D -m644 "$srcdir/pon.1" "$pkgdir/usr/share/man/man1/pon.1"
  install -d -m755 "$pkgdir/etc/ppp/peers"
}
