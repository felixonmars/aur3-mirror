# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=scrawl
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=v1.0.11.g1e16ea5
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/dubhater/vapoursynth-${_plug}"
license=('custom:WTFPL' 'custom:OFL')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
makedepends=('git')
source=("${_plug}::git://github.com/dubhater/vapoursynth-${_plug}.git"
        'COPYING::http://www.wtfpl.net/txt/copying/')
md5sums=('SKIP'
         '8365d07beeb5f39d87e846dca3ae7b64')
options=('!libtool')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --always | tr - .)"
}

build() {
  cd "${_gitname}"
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib/vapoursynth
  make
}

package(){
  cd "${_gitname}"
  make install DESTDIR="$pkgdir"
  install -Dm644 readme.rst "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"

  install -Dm644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/${_gitname}/ofl.txt" "${pkgdir}/usr/share/licenses/${pkgname}/ofl.txt"
}
