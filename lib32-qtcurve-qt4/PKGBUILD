# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: xduugu

_pkg=qtcurve-qt4
pkgname=lib32-$_pkg
pkgver=1.8.9
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk. Qt4-only version. Multilib.'
arch=('i686' 'x86_64')
url="http://craigd.wikispaces.com/"
license=('GPL')
groups=('qtcurve')
depends=('lib32-qt')
makedepends=('cmake' 'gcc-multilib')
conflicts=('lib32-qtcurve-kde4')
source=("${url}file/view/QtCurve-KDE4-$pkgver.tar.bz2")
md5sums=('0028e3da714cd0932e16c7f937e487a1')

build() {
  cd "$srcdir/QtCurve-KDE4-$pkgver"

  # export multilib parameters
  export CC='gcc -m32'
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQTC_QT_ONLY=true

  make
}

package() {
  cd "$srcdir/QtCurve-KDE4-$pkgver"

  install -Dm755 style/qtcurve.so "$pkgdir/usr/lib32/qt/plugins/styles/qtcurve.so"
}
