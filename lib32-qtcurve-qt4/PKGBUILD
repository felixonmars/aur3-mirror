# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: xduugu

pkgname=lib32-qtcurve-qt4
pkgver=1.8.12
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk. Qt4-only version. Multilib.'
arch=('x86_64')
url="http://craigd.wikispaces.com/"
license=('GPL')
groups=('qtcurve')
depends=('lib32-qt')
makedepends=('cmake' 'gcc-multilib')
source=("${url}file/view/QtCurve-KDE4-$pkgver.tar.bz2")
md5sums=('8a08d28204fab1b3db6813519f174014')

build() {
  cd "$srcdir/QtCurve-KDE4-$pkgver"

  # export multilib parameters
  export CC='gcc -m32'
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  [ -d bld ] || mkdir bld && cd  bld

  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT_ONLY=true
  make
}

package() {
  cd "$srcdir/QtCurve-KDE4-$pkgver/bld"

  install -Dm755 style/qtcurve.so \
    "$pkgdir/usr/lib32/qt/plugins/styles/qtcurve.so"
}
