# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>
# Former Maintainer: Michael Whapples <mwhapples@aim.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk-git
pkgver=2.9.4.1719.c60babb
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('at-spi2-core-git' 'libx11' 'atk' 'dconf')
makedepends=('intltool')
install=at-spi2-atk.install
options=('!libtool')
groups=('gnome')
conflicts=('at-spi2-atk')
provides=('at-spi2-atk')
_gitname="at-spi2-atk"
source=('git://git.gnome.org/at-spi2-atk')
md5sums=('SKIP')

pkgver() {
	cd $_gitname

  version=$(grep AC_INIT configure.ac | sed 's/AC_INIT(\[at-spi2-atk\], \[//' | sed 's/\], \[accessibility-atspi@lists.linux-foundation.org\])//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  
  echo $version.$revision.$hash

}

build() {
	cd $_gitname

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  # autoreconf --force --install

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  make
}

package() {
	cd $_gitname
  make DESTDIR="${pkgdir}" install
}
