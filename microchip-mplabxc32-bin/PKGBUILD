# Maintainer: joat <joat at lavabit dot com>
# Submitter: BxS <bxsbxs at gmail dot com>

pkgname=microchip-mplabxc32-bin
pkgver=1.11
pkgrel=1
pkgdesc="C/C++ compiler for PIC32 MCUs"
arch=(i686 x86_64)
url=http://www.microchip.com/xc32
license=(custom)
depends=(xclm-dirs)
[ $CARCH = x86_64 ] && depends+=(lib32-gcc-libs lib32-expat)
provides=(mplabxc32)
conflicts=(mplabxc32)
options=(!strip docs libtool emptydirs !zipman)
install=$pkgname.install
instdir=/opt/microchip/xc32/v$pkgver
instname=xc32-v$pkgver-linux
installer=$instname-installer.run
source=(http://ww1.microchip.com/downloads/en/DeviceDoc/$installer.tar)
md5sums=(8c021cef4d5bf46d5de044d5eadcf670)

package() {
  echo -e "Creating the Package\n  Please wait..."

  cd $srcdir
  tar -xf $installer.tar

  cd $pkgdir

  mkdir -p $pkgdir$instdir

  chmod 0755 $srcdir/$installer
  echo -e "i\ni\ni\ni\ni\ni\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\ny\n\n\n\ny\n\n$pkgdir$instdir\nn\ny\ni\ni\ni\ni\ni\ni\ni\n" > inst_input
  $srcdir/$installer --mode text < inst_input &> /dev/null
  rm inst_input

  rm $pkgdir$instdir/Uninstall*
  rm -r $pkgdir$instdir/rollbackBackupDirectory || true

  chmod -R go-w $pkgdir$instdir/{bin,docs,etc}
  
  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  ln -s $instdir/MPLAB_XC32_Compiler_License.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
