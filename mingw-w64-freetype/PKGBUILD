# Maintainer: Daniel Kirchner <daniel at ekpyron dot org>

pkgname=mingw-w64-freetype
pkgver=2.4.10
pkgrel=2
pkgdesc="TrueType font rendering library (mingw-w64)"
arch=('any')
url="http://www.freetype.org/"
license=('GPL')
depends=(mingw-w64-zlib mingw-w64-crt)
makedepends=(mingw-w64-gcc)
source=(http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.gz)
options=(!strip !buildflags !libtool)
md5sums=('4f255c6ee5d5cc2b5c3d423a07386fcb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/build-${_arch}"
    cd "${srcdir}/build-${_arch}"
    CC=${_arch}-gcc ../freetype-${pkgver}/configure --host=${_arch} \
    	--prefix=/usr/${_arch} --enable-static --enable-shared \
    	--with-zlib=/usr/${_arch}
    make
  done
}

package () {
  for _arch in ${_architectures}; do
  	cd "${srcdir}/build-${_arch}"
  	make DESTDIR="${pkgdir}" install
  	${_arch}-strip -x -g "${pkgdir}/usr/${_arch}/bin/"*.dll
  	${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
  done
}
