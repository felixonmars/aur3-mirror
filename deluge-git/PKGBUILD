# Mantainer Berseker  <berseker86 at gmail dot com>
# PKGBUILD based on Ionut Biru <ibiru@archlinux.org> extra\deluge's PKGBUILD, adapted for -git version

pkgname=deluge-git
pkgver=20120621
pkgrel=1
pkgdesc="A bittorrent client written with python and pygtk - Git Version"
arch=('any')
url="http://deluge-torrent.org/"
license=('GPL3')
depends=('pyxdg' 'python2-distribute' 'libtorrent-rasterbar>=0.15.0' 
'twisted' 'xdg-utils' 'python2-chardet' 
'python2-pyopenssl')
makedepends=( 'intltool' 'pygtk' 'librsvg' 'python-mako' 'git' 'python2-distribute')
optdepends=('python-notify: libnotify notifications'
	    'pygtk: needed for gtk ui'
	    'librsvg: needed for gtk ui'
	    'python2-mako: needed for web ui')
provides=('deluge')
conflicts=('deluge' 'deluge-svn')
replaces=('deluge-svn')
source=(deluged deluged.conf.d deluge-web)
install=deluge-git.install

_gitroot="git://deluge-torrent.org/deluge.git"
_gitname="deluge"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "${srcdir}/$_gitname-build"
  python2 setup.py build
}

package() {
  cd "${srcdir}/$_gitname-build"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -d -m755 "${pkgdir}"/srv/deluge
  install -D -m644 build/lib/deluge/ui/data/pixmaps/deluge.svg "${pkgdir}"/usr/share/pixmaps/deluge.svg 
  install -D -m755 "${srcdir}"/deluged "${pkgdir}"/etc/rc.d/deluged
  install -D -m755 "${srcdir}"/deluge-web "${pkgdir}"/etc/rc.d/deluge-web
  install -D -m644 "${srcdir}"/deluged.conf.d "${pkgdir}"/etc/conf.d/deluged
}
md5sums=('7d5790eeb14cd0b47d1bb3e0a19c9935'
         '332d23fe0d21ce92b494efd73322856c'
         '6c913ec7322eaa60494ac9d53103005f')
