# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# PKGBUILD based on extra\deluge's PKGBUILD, adapted for -git version by Berseker

pkgname=deluge-git
pkgver=20130527
pkgrel=1
pkgdesc="A bittorrent client written with python and pygtk - Git Version"
arch=('any')
url="http://deluge-torrent.org"
license=('GPL3')
conflicts=('deluge' 'deluge-stable-git')
provides=('deluge')
install=deluge.install
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libtorrent-rasterbar>=0.16.9'
         'python2-chardet' 'python2-pyopenssl' 'python2-xdg' 'twisted' 'xdg-utils')
makedepends=('intltool' 'librsvg' 'pygtk>=2.16' 'python2-mako' 'python2-distribute' 'git' )
optdepends=('librsvg: needed for gtk ui'
            'pygtk: needed for gtk ui'
            'python2-mako: needed for web ui'
            'python2-notify: libnotify notifications'
            'geoip: display peer locations')

_branch=develop
source=("git://deluge-torrent.org/deluge.git#branch=$_branch"
        'deluged.service' 'deluge-web.service')
md5sums=('SKIP'
         '97971f45591621861b3c82634811be06'
         '2a73f62c04e8b147c731fbb50666e846')

pkgver() {
    cd "$srcdir/deluge"
    git log -1 --format="%cd" --date=short | tr -d '-'
}

build() {
    cd "$srcdir/deluge"
    python2 setup.py build
}

package() {
    cd "$srcdir/deluge"
    python2 setup.py install \
        --prefix=/usr \
        --root="$pkgdir" \
        --optimize=1
    
    install -d "$pkgdir/srv"
    install -d -m 664 -o 125 -g 125 "$pkgdir/srv/deluge"
    install -Dm644 "deluge/ui/data/pixmaps/deluge.svg" "$pkgdir/usr/share/pixmaps/deluge.svg"
    install -Dm644 "$srcdir/deluged.service"           "$pkgdir/usr/lib/systemd/system/deluged.service"
    install -Dm644 "$srcdir/deluge-web.service"        "$pkgdir/usr/lib/systemd/system/deluge-web.service"
}
