# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=jabref-git
pkgver=2.10.1.g6457b2f
pkgrel=1
pkgdesc="GUI frontend for BibTeX, written in Java --built from git"
arch=('any')
url="http://jabref.sourceforge.net/"
license=('GPL')
depends=('java-runtime' 'sh')
makedepends=('git' 'apache-ant')
provides=('jabref')
conflicts=('jabref')
source=('jabref::git://git.code.sf.net/p/jabref/code' 'jabref.desktop' 'jabref.sh')
md5sums=('SKIP'
         '496d2094ee194a5568baee7af5b70fa2'
         '9da2f8a5010e25bb04c81225309cc9e5')
_gitname="jabref"

pkgver() {
  cd $_gitname
  git describe --tags|sed 's/-/./g'| cut -c3-
}

build() {
  cd "$srcdir/$_gitname/$_gitname"
  /etc/profile.d/apache-ant.sh
  ant
}

package() {
  cd "$srcdir/$_gitname"
  install -Dm644 jabref/build/lib/JabRef-2.10.jar \
    ${pkgdir}/usr/share/java/JabRef.jar 
  install -Dm755 $srcdir/jabref.sh ${pkgdir}/usr/bin/jabref
  install -Dm644 $srcdir/jabref.desktop \
    ${pkgdir}/usr/share/applications/jabref.desktop
  install -Dm644 jabref/build/images/JabRef-icon-48.png \
    ${pkgdir}/usr/share/pixmaps/jabref.png
  install -d ${pkgdir}/usr/share/doc/$pkgname
  cp -r htdocs/{help,manuals} ${pkgdir}/usr/share/doc/$pkgname
}
