# $Id$
# Maintainer:  BartÅ‚omiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>
# Contributor: monty <linksoft [at] gmx [dot] de>

pkgname=bin32-nodejs
pkgver=0.8.3
pkgrel=1
pkgdesc='Evented I/O for V8 javascript (i686 build for x86_64)'
arch=('x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2')
checkdepends=('curl') # curl used for check()
optdepends=('lib32-openssl: TLS support')
options=('!emptydirs')
source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz")
md5sums=('53604d2e3773d62e90fa5a340a362e08')
depends=('lib32-gcc-libs')
makedepends=('gcc-multilib')

build() {
  cd node-v${pkgver}

  msg 'fixing for python2 name'
  find -type f -exec sed -e 's_^#!/usr/bin/env python$_&2_' -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' -e 's_^#!/usr/bin/python$_&2_' -e "s_'python'_'python2'_" -i {} \;
  find test -type f -exec sed -e "s|python |python2 |" -i {} \;
  #sed -i "s|cmd_R = 'python |cmd_R = 'python2 |" wscript
  sed -i "s|python |python2 |" Makefile
  #find test -type f -exec sed -e 's/python/&2/' -i {} \;
  #sed -i "s/python/&2/" configure

  export PYTHON=python2

  ./configure \
    --prefix=/usr \
    --shared-openssl \
    --dest-cpu=ia32

  make
}

check() {
  cd node-v${pkgver}

  # test failures in 0.6 are known
  # specifically https://github.com/joyent/node/pull/1699
  make test || true
}

package() {
  cd node-v${pkgver}

  # fix installer.js not using DESTDIR
  sed -i "s|var npmDir = path.resolve(node_prefix, 'lib/node_modules/npm');|var npmDir = path.join(dest_dir, node_prefix, 'lib/node_modules/npm');|" tools/installer.js
  
  make DESTDIR=${pkgdir} install

  # install docs as per user request
  install -d ${pkgdir}/usr/share/doc/nodejs-32
  cp -r doc/api/*.html \
    ${pkgdir}/usr/share/doc/nodejs-32

  # compress man pages
  find ${pkgdir}/usr/lib/node_modules/npm/man -type f | xargs gzip -9

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/nodejs-32/LICENSE
  
  # correct usr/lib to usr/lib32
  cd ${pkgdir}/usr
  mv lib lib32
  
  # correct soft-link to npm
  cd ${pkgdir}/usr/bin
  rm npm
  ln -s ../lib32/node_modules/npm/bin/npm-cli.js npm
  
  # add -32 to binaries
  mv node node-32
  mv node-waf node-waf-32
  mv npm npm-32
  
  # add -32 to other files
  cd ${pkgdir}/usr
  mv include/node include/node-32
  mv share/man/man1/node.1 share/man/man1/node-32.1
}

# vim:set ts=2 sw=2 et:
