pkgname=mingw-w64-isl
pkgver=0.10
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints (mingw-w64)"
arch=(any)
url="http://www.kotnet.org/~skimo/isl/"
license=("LGPL2.1")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-gmp)
options=(!libtool !strip !buildflags)
source=("http://www.kotnet.org/~skimo/isl/${pkgname#mingw-w64-}-$pkgver.tar.bz2")
md5sums=('c1ece653891bb2a5f55ca25e3f4e8f35')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}