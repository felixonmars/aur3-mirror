# Maintainer: jose <jose1711 [at] gmail (dot) com>

pkgname=pacmanager
pkgver=4.1.2.2
pkgrel=1
pkgdesc="Perl/GTK Gnome replacement for SecureCRT/Putty"
arch=('i686' 'x86_64')
url="http://sites.google.com/site/davidtv/"
license=("GPL-3")
source=("https://sourceforge.net/projects/pacmanager/files/pac-4.0/pac-${pkgver}-all.tar.gz")
depends=("perl-gtk2-ex-simple-list" "perl-expect" "perl-io-stty" "perl-yaml" "glade-perl" "gconf-perl" "perl-crypt-cbc" "perl-crypt-blowfish" "perl-net-arp" "perl-socket6" "uuid" "perl-freezethaw" "vte" "perl-gnome2-vte" "perl-gtk2-sourceview2")
optdepends=('tightvnc' 'rdesktop' 'openssh')
md5sums=('c6c764655702694bce48e0fe9a597d6b')

build() {
  # some patching needed
  cd $srcdir/pac/lib
  sed -i -e '/^require.*Vte.pm/s/.*/use Gnome2::Vte;/' -e '/Could not load Gnome2::Vte Perl/d' PACShell.pm
  sed -i -e '/^require.*Vte.pm/s/.*/use Gnome2::Vte;/' -e '/Could not load Gnome2::Vte Perl/d' PACTerminal.pm
  rm -r ex/vte32 ex/vte64
}

package() {
  cd $srcdir/pac
  mkdir -p $pkgdir/usr/{share/pacmanager/{lib,res,utils},bin}
  /bin/tar cf - lib/* | ( cd ../../pkg/usr/share/pacmanager/; tar xfp - )
  install -D -m 644 res/*png $pkgdir/usr/share/pacmanager/res
  install -D -m 644 res/*jpg $pkgdir/usr/share/pacmanager/res
  install -D -m 644 res/*glade $pkgdir/usr/share/pacmanager/res
  install -D -m 755 utils/pac_from_mcm.pl $pkgdir/usr/share/pacmanager/utils/pac_from_mcm.pl
  install -D -m 755 utils/pac_from_putty.pl $pkgdir/usr/share/pacmanager/utils/pac_from_putty.pl
  install -D -m 755 pac $pkgdir/usr/share/pacmanager/pac
  install -D -m 644 README $pkgdir/usr/share/pacmanager/README
  install -D -m 644 res/pac.1 $pkgdir/usr/share/man/man1/pac.1
  install -D -m 644 res/pac.yml $pkgdir/usr/share/pacmanager/res/pac.yml
  install -D -m 644 res/pac64x64.png ${pkgdir}/usr/share/pixmaps/pac.png && \
  install -D -m 644 res/pac.desktop ${pkgdir}/usr/share/applications/pac.desktop
  install -D -m 644 res/pac_start.desktop ${pkgdir}/usr/share/applications/pac_start.desktop
  ln -s /usr/share/pacmanager/pac ${pkgdir}/usr/bin/pac
}
