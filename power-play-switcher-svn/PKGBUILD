# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=power-play-switcher-svn
pkgver=5
pkgrel=1
pkgdesc="PowerPlay GUI for the open source ATI driver."
arch=(any)
url="http://code.google.com/p/power-play-switcher"
license=(GPL3)
depends=(python2 xf86-video-ati)
makedepends=(subversion)
install=power-play-switcher.install
changelog=Changelog
conflicts=(power-play-switcher)
source=(power-play-switcher.desktop)
md5sums=('3bb7a8b8cdcf199e4aa776f1503c88f3')

_svntrunk="http://power-play-switcher.googlecode.com/svn/trunk"
_svnmod="trunk"

build() {

  msg2 "Starting SVN checkout..."
  cd "${srcdir}"
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg2 "SVN checkout done or server timeout"

  msg2 "Fix Python script shebang..."
  sed -i -e 's/\#\!\/usr\/bin\/python/\#\!\/usr\/bin\/env\ python2/' "${srcdir}"/${_svnmod}/PowerPlaySwitcher.py

}

package() {

  msg2 "Install script at /usr/bin/power-play-switcher..."
  install -Dm755 "${srcdir}"/${_svnmod}/PowerPlaySwitcher.py "${pkgdir}"/usr/bin/power-play-switcher

  msg2 "Install desktop application entry..."
  install -Dm644 "${srcdir}"/power-play-switcher.desktop "${pkgdir}"/usr/share/applications/power-play-switcher.desktop

}

# vim:set ts=2 sw=2 et:
