# Maintainer:  Daniel YC Lin <dlin.tw (at) gmail.com>
pkgname=grub4dos-svn
pkgver=212
pkgrel=1
pkgdesc="A GRUB boot loader support menu on windows(fat,ntfs)/linux(ext2,3,4)"
arch=('any')
url="http://code.google.com/p/grub4dos-chenall"
license=('GPL2')
backup=(etc/grub4dos.conf boot/grub/menu.lst)
makedepends=('svn')
install=grub4dos.install
conflicts=(grub4dos)

# known namcap warnings: these are acceptable
#grub4dos-svn E: ELF file ('usr/sbin/bootlace.com') found in an 'any' package.
#grub4dos-svn W: File (boot) exists in a non-standard directory.

source=(
  http://grubutils.googlecode.com/files/unifont-2011-05-10.zip
  http://grub4dos-chenall.googlecode.com/files/arialuni_U16.zip
  README_arch.txt
  grub4dos.conf
  menu.lst
  )

_svntrunk="http://grub4dos-chenall.googlecode.com/svn/trunk/"
_svnmod="grub4dos-svn"

build() {
  cd $srcdir
  msg "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntrunk $_svnmod
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  #rm -rf $_svnmod-build
  svn export --force $_svnmod $_svnmod-build
  cd $_svnmod-build
  ./configure --enable-preset-menu=preset_menu.lst
  make
}

package() {
  install -Dm 644 "$srcdir/grub4dos.conf" "$pkgdir/etc/grub4dos.conf"
  for _f in grub.exe grldr ; do
    install -Dm 644 "$srcdir/$_svnmod-build/stage2/$_f" "$pkgdir/boot/grub/$_f"
  done
  _f=menu.lst ;  install -Dm 644 "$srcdir/$_f" "$pkgdir/boot/grub/$_f"
  _f=default ;  install -Dm 644 "$srcdir/$_svnmod-build/$_f" "$pkgdir/boot/grub/$_f"
  for _f in unifont arialuni_U16.bin ; do
    install -Dm 644 "$srcdir/$_f" "$pkgdir/boot/grub/$_f"
  done
  gzip $pkgdir/boot/grub/arialuni_U16.bin
  _f=bootlace.com ; install -Dm 755 "$srcdir/$_svnmod-build/stage2/$_f" "$pkgdir/usr/sbin/$_f"
  _f=README_arch.txt ; install -Dm 644 "$srcdir/$_f" "$pkgdir/usr/share/doc/grub4dos/$_f"
  for _f in README_GRUB4DOS.txt ChangeLog_chenall.txt ChangeLog_GRUB4DOS.txt ; do
    cp $srcdir/$_svnmod-build/$_f $pkgdir/usr/share/doc/grub4dos
  done

  #rm -r $srcdir/$_svnmod-build/
}
md5sums=('3984c1b0dfdf22ece221e54b6690196b'
         '479f066a8f84095a2cb40ac3c473430d'
         '4fe1655e3f7f3eb45c3c58d6cc74e17c'
         'b706d5506f9c6a29727a71e32a546854'
         'be41e2a0a8080db15497eb73ac43db15')
# vim:set ts=2 sw=2 et sta:
