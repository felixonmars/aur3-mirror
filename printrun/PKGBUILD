# Contributor: Richard Kaka≈° <richard.kakas@gmail.com>
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=printrun
pkgver=20140126
pkgrel=1
_projectname='Printrun'
_gittag="printrun-${pkgver}"
pkgdesc='GUI interface for 3D printing on RepRap and other printers'
arch=('any')
url='https://github.com/kliment/Printrun'
license=('GPL')
provides=('printrun-git')
conflicts=('printrun-git')
depends=('python2-pyserial' 'wxpython' 'pyglet')
source=("https://github.com/kliment/${_projectname}/archive/${_gittag}.tar.gz"
        'pronterface.desktop' 'pronterface.png')
sha256sums=('68808a6dc15713b155c3ba2e7fa302601f0c2bb66397e3642d319041441d1ba1'
            'd2b419a6b00540a4cd1d1e7e561aaf763ebf6dbfe5520569da4d4372a9d09918'
            'b6321224cc52ce18f5450c68af8b9f92ec11e85a2a58c41be049b79faefa2ace')

build() {
  cd "${srcdir}/${_projectname}-${_gittag}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_projectname}-${_gittag}"
  python2 setup.py install --root="${pkgdir}" --prefix=/usr

  install -D -m644 "${srcdir}/pronterface.desktop" \
    "$pkgdir/usr/share/applications/pronterface.desktop"
  install -D -m644 "${srcdir}/pronterface.png" \
    "$pkgdir/usr/share/pixmaps/pronterface.png"
}
