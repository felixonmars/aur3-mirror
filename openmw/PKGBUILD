# Maintainer: Sandy Carter <bwrsandman@gmail.com>
# PKGBUILD source: https://github.com/bwrsandman/pkgbuild/tree/master/openmw

pkgname=openmw
pkgver=0.25.0
pkgrel=1
pkgdesc="An open-source engine reimplementation for the role-playing game Morrowind."
arch=('i686' 'x86_64')
url="http://www.openmw.org"
license=('GPL3' 'custom')

depends=('openal' 'ogre' 'mygui' 'bullet' 'mpg123' 'libsndfile' 'qt4' 'ffmpeg' 'sdl2')
makedepends=('cmake' 'boost')

source=(https://openmw.googlecode.com/files/${pkgname}-${pkgver}-source.tar.gz)
sha1sums=('4e4e27cfdc125f2249280580bee933ca42415c96')


build() {
  cd "${srcdir}"

  msg "Starting make..."
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4
  make
}

package() {
  cd "${srcdir}"
  make DESTDIR="$pkgdir" install

  # Replace resources location
  sed 's,resources=\./resources,resources=/usr/share/games/openmw/resources,' openmw.cfg > "$pkgdir"/etc/openmw/openmw.cfg 

  # Custom Licences
  install -d -m755 "$pkgdir"/usr/share/licenses/openmw
  install -D -m644 "DejaVu Font License.txt" "$pkgdir/usr/share/licenses/openmw/"
  install -D -m644 "Daedric Font License.txt" "$pkgdir/usr/share/licenses/openmw/"
  install -D -m644 "OFL.txt" "$pkgdir/usr/share/licenses/openmw/"
  install -D -m644 "extern/shiny/License.txt" "$pkgdir/usr/share/licenses/openmw/Shiny License.txt"
}

# vim:set ts=2 sw=2 et:
