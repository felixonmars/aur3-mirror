# Maintainer: Roman Timushev <rtimush@gmail.com>
pkgname=(hunspell-ru)
pkgver=0.99g5
pkgrel=4
pkgdesc="Russian dictionaries for Hunspell by Alexander Lebedev"
arch=(any)
url="ftp://scon155.phys.msu.ru/pub/russian/ispell/"
license=('custom')
depends=('hunspell')
makedepends=()
source=(ftp://scon155.phys.msu.ru/pub/russian/ispell/rus-ispell-$pkgver.tar.gz)
md5sums=('1fc423903273dd30a1fe0b834b17a8dc')

build() {
  cd "$srcdir"
  sed -i -e "s/^\\s*buildhash.*$//" Makefile

  # yo version
  LANG=C make YO=1
  # ie version
  # LANG=C make

  wc -l < russian.dict > ru_RU.dic
  cat russian.dict >> ru_RU.dic

  echo "SET KOI8-R" > ru_RU.aff
  ispellaff2myspell --lowercase="£ÊÃÕËÅÎÇÛÝÚÈßÆÙ×ÁÐÒÏÌÄÖÜÑÞÓÍÉÔØÂÀ" --uppercase="³êãõëåîçûýúèÿæù÷áðòïìäöüñþóíéôøâà" russian.aff >> ru_RU.aff
}

package() {
  cd "$srcdir"
  install -D -m644 ru_RU.aff "$pkgdir/usr/share/hunspell/ru_RU.aff"
  install -D -m644 ru_RU.dic "$pkgdir/usr/share/hunspell/ru_RU.dic"
  install -dm755 "$pkgdir/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
  ln -s /usr/share/hunspell/ru_RU.aff
  ln -s /usr/share/hunspell/ru_RU.dic
  popd
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
