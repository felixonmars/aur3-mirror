# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=sx-git
_gitname='sx'
pkgver=f055069
pkgrel=1
pkgdesc="Modular Bitcoin cmdline utilities (SpesmiloXchange)"
arch=('i686' 'x86_64')
depends=('obelisk-git' 'python2')
makedepends=('gcc' 'git' 'make')
optdepends=('qrencode: Generate Bitcoin QR codes offline')
url="https://github.com/genjix/sx/"
license=('AGPL3')
source=(git+https://github.com/genjix/sx/)
sha256sums=('SKIP')
provides=('sx')
conflicts=('sx')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $_gitname

  msg 'Building...'
  autoreconf -i
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/bin --sysconfdir=/etc --sharedstatedir=/etc --localstatedir=/etc
  make
}

package() {
  cd $_gitname

  msg 'Installing...'
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
