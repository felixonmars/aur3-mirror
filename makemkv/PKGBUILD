# Maintainer: hydro <hydro@freenet.de>

pkgname=makemkv
pkgver=1.7.4
pkgrel=1
pkgdesc="convert blu-ray and dvd to mkv"
arch=('i686' 'x86_64')
url="http://www.makemkv.com"
license=('LGPL' 'MPL' 'custom')
depends=(qt)
install=makemkv.install
source=(${url}/download/${pkgname}_v${pkgver}_bin.tar.gz
        ${url}/download/${pkgname}_v${pkgver}_oss.tar.gz
        makemkv.desktop)
md5sums=('11de3a3876b59df8b6b58f2e6e4dc3c6'
         'f8a7c902d47bc93eaf90b7e14a3886ea'
         'c893a71b2bd2b39cb70698805a46ec56')

build() {
  cd $srcdir/${pkgname}_v${pkgver}_oss
  make -f makefile.linux
}

package() {
  cd $srcdir/${pkgname}_v${pkgver}_oss
  install -d $pkgdir/usr/lib
  install -t $pkgdir/usr/lib out/libdriveio.so.0 out/libmakemkv.so.1
  install -D out/makemkv $pkgdir/usr/bin/makemkv
  install -Dm 644 makemkvgui/src/img/128/mkv_icon.png $pkgdir/usr/share/pixmaps/makemkv.png

  cd $srcdir/${pkgname}_v${pkgver}_bin
  if [ "$CARCH" = "x86_64" ]; then
    install -D bin/amd64/makemkvcon $pkgdir/usr/bin/makemkvcon
  else
    install -D bin/i386/makemkvcon $pkgdir/usr/bin/makemkvcon
  fi
  install -d $pkgdir/usr/share/MakeMKV
  install -t $pkgdir/usr/share/MakeMKV src/share/makemkv_*.mo.gz src/share/default.mmcp.xml
  
  install -Dm 644 $srcdir/makemkv.desktop $pkgdir/usr/share/applications/makemkv.desktop
  install -Dm 644 src/eula_en_linux.txt $pkgdir/usr/share/licenses/$pkgname/eula_en_linux.txt
}
