# Maintainer: hydro <hydro@freenet.de>

pkgname=makemkv
pkgver=1.7.6
pkgrel=1
pkgdesc="DVD and Blu-ray to MKV converter and network streamer"
arch=('i686' 'x86_64')
url="http://www.makemkv.com"
license=('LGPL' 'MPL' 'custom')
depends=('qt')
install=makemkv.install
source=(${url}/download/${pkgname}-bin-${pkgver}.tar.gz
        ${url}/download/${pkgname}-oss-${pkgver}.tar.gz)
md5sums=('b93d54a7ae2a4e85c746281c112125e0'
         '7e5224fe56a885fdb9c8b00f081883c6')

build() {
  cd $srcdir/${pkgname}-oss-${pkgver}
  make -f makefile.linux
}

package() {
  cd $srcdir/${pkgname}-oss-${pkgver}
  make -f makefile.linux DESTDIR="$pkgdir" install

  cd $srcdir/${pkgname}-bin-${pkgver}
  install -d $pkgdir/usr/bin/
  install -t $pkgdir/usr/bin/ bin/i386/mmdtsdec
  if [ "$CARCH" = "x86_64" ]; then
    install -t $pkgdir/usr/bin/ bin/amd64/makemkvcon
  else
    install -t $pkgdir/usr/bin/ bin/i386/makemkvcon
  fi
  install -d $pkgdir/usr/share/MakeMKV
  install -t $pkgdir/usr/share/MakeMKV src/share/makemkv_*.mo.gz src/share/default.mmcp.xml
  
  install -Dm 644 src/eula_en_linux.txt $pkgdir/usr/share/licenses/$pkgname/eula_en_linux.txt
}
