# Maintainer: Muflone <webreg@vbsimple.net>

pkgname=remmina-plugin-ultravnc
pkgver=1.0.0.0
pkgrel=3
_builderver=1.0.0.2
pkgdesc="A protocol plugin for Remmina to connect via VNC using UltraVNC viewer."
arch=(i686 x86_64)
url="https://github.com/muflone/remmina-plugin-ultravnc"
license=('GPL')
depends=('gtk3' 'remmina' 'hicolor-icon-theme' 'ultravnc-viewer')
makedepends=('pkg-config' 'cmake')
optdepends=()
install=${pkgname}.install
source=(
  "remmina-plugin-builder_${_builderver}.tar.gz::https://github.com/muflone/remmina-plugin-builder/archive/${_builderver}.tar.gz"
  "${pkgname}_${pkgver}.tar.gz::https://github.com/muflone/${pkgname}/archive/${pkgver}.tar.gz"
)
md5sums=(
  'd74a95c501a38774f5422941ed5684e9'
  '0224ac1521ddf47293c9b77028a90fb1'
)

build() {
  [ -d build ] && rm -rf build
  cp -r remmina-plugin-builder-${_builderver} build
  cp -r "${pkgname}-${pkgver}/${pkgname}"/* build/remmina-plugin-to-build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd ${srcdir}/build/
  make DESTDIR=${pkgdir} install
}
