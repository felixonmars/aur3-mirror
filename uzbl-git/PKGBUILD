# Maintainer: 
# Contributor: jsteel <mail at jsteel dot org>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>
# Contributor: Brendan Taylor <whateley@gmail.com>
# Contributor: L42y <423300@gmail.com>

pkgname=uzbl-git
_gitname=uzbl
pkgver=20131006
pkgrel=1
pkgdesc="All uzbl web interface tools: latest from the git master branch"
arch=('i686' 'x86_64')
url="http://github.com/Dieterbe/uzbl"
license=('GPL3')
depends=('webkitgtk' 'socat' 'zenity' 'xclip' 'dmenu' 'python' 'pygtk')
makedepends=('git' 'pkgconfig')
provides=('uzbl-core' 'uzbl-browser' 'uzbl-tabbed')
conflicts=('uzbl-core' 'uzbl-browser' 'uzbl-tabbed' 'uzbl-experimental-git')
source=(git://github.com/Dieterbe/uzbl.git
        Makefile.patch)
md5sums=('SKIP'
         '88fee50f255dd9ad7ee610acb4120519')

pkgver() {
  cd $_gitname

  git log -n1 --format="%ci" | awk '{print $1}' | sed s/-//g
}

prepare() {  
  cd $_gitname

  patch -Np0 -i "$srcdir"/Makefile.patch
}

build() {
  cd $_gitname

  make
}

package() {
  cd $_gitname

  make DESTDIR="$pkgdir"/ PREFIX=/usr install
}
