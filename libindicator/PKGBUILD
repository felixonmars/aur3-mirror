# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libindicator
pkgver=0.4.1
pkgrel=2
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
makedepends=('gtk2>=2.18')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.4/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('c8f2900fa8e028bb2ea7511a0f368756')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Use different location for header files for GTK+3
  sed -i 's/libindicator-/libindicator3-/' libindicator/indicator3-0.4.pc.in.in
  sed -i 's/libindicator-/libindicator$(VER)-/' libindicator/Makefile.in

  # Disable building tests
  sed -i '/tests/ d' Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --with-gtk=2
  make
}

package() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2>=2.18')

  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
