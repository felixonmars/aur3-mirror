# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=libindicator
pkgname=libindicator
true && pkgname=(libindicator libindicator3)
pkgver=0.4.1
pkgrel=2
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
makedepends=('gtk2>=2.18' 'gtk3>=2.91')
options=(!libtool)
source=(http://launchpad.net/$pkgbase/0.4/$pkgver/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('c8f2900fa8e028bb2ea7511a0f368756')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  # Use different location for header files for GTK+3
  sed -i 's/libindicator-/libindicator3-/' libindicator/indicator3-0.4.pc.in.in
  sed -i 's/libindicator-/libindicator$(VER)-/' libindicator/Makefile.in

  # Disable building tests
  sed -i '/tests/ d' Makefile.in

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static
  make
  popd
}

package_libindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2>=2.18')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make DESTDIR="$pkgdir/" install
}

package_libindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3>=2.91')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make -C libindicator DESTDIR="$pkgdir/" install
  make -C tools DESTDIR="$pkgdir/" install

  rm -r "$pkgdir/usr/share"
}

depends=('gtk2>=2.18' 'gtk3>=2.91')
true && depends=()
