# Maintainer: Beej Jorgensen <beej@beej.us>
pkgname=therion
pkgver=5.3.10
pkgrel=1
pkgdesc="Cave map rendering software"
arch=('i686' 'x86_64')
url="http://therion.speleo.sk/"
license=('GPL')
depends=('wxgtk' 'texlive-core' 'texlive-langcyrillic' 'bwidget' 'tkimg' 'vtk')
makedepends=()
backup=('etc/therion/therion.ini' 'etc/therion/xtherion.ini')
source=(http://therion.speleo.sk/downloads/$pkgname-$pkgver.tar.gz
        "${pkgname}_aur.patch")
    
md5sums=('fcf1938a3d32a42d47ff6459b40ecdd1'
         'dbe9d450896c74b36914cdeeb1323fc9')

build() {
  # directories have wrong permissions in source
  cd "$srcdir"
  find . -type d -exec chmod u+x {} \;

  cd "$pkgname"

  # patch the loch Makefile so it looks in the right place for VTK
  # libs, and patch the config file search path so it looks in
  # /etc/therion/:
  patch -Np1 < $srcdir/${pkgname}_aur.patch

  # extract current version number (e.g. "5.4", "5.6")
  VTKVERSION=$(ls --color=never -d /usr/include/vtk-*)
  VTKVERSION=${VTKVERSION#*-}
  export VTKVERSION

  make

  # install rules from makeinstall.tcl
  install -m755 -D therion ${pkgdir}/usr/bin/therion
  install -m755 -D xtherion/xtherion ${pkgdir}/usr/bin/xtherion
  install -m755 -D loch/loch ${pkgdir}/usr/bin/loch
  install -m644 -D therion.ini ${pkgdir}/etc/therion/therion.ini
  install -m644 -D xtherion/xtherion.ini ${pkgdir}/etc/therion/xtherion.ini

  # additional install rules from makebinary.pl
  install -m644 -D man/therion.1 ${pkgdir}/usr/share/man/man1/therion.1
  install -m644 -D man/xtherion.1 ${pkgdir}/usr/share/man/man1/xtherion.1

  mkdir -p ${pkgdir}/usr/share/doc/therion
  cp -a \
    CHANGES COPYING README SVG.problems SYMBOLS.txt \
    TODO.M TODO.MS TODO.S TODO.SM thbook/thbook.pdf samples \
    ${pkgdir}/usr/share/doc/therion
}

# vim:set ts=2 sw=2 et:
