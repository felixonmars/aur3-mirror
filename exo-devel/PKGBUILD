# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be> 
# Contributor: Aurelien Desbrieres <ice.cube@gmx.com>

pkgname=exo-devel
pkgver=0.6.2
pkgrel=1
pkgdesc="Application library for the Xfce desktop environment (Development Version)"
arch=(i686 x86_64)
license=('GPL2' 'LGPL')
url="http://www.xfce.org/projects/exo/"
groups=('xfce4-devel')
depends=('libxfce4util-devel>=4.8.1' 'gtk2' 'hicolor-icon-theme')
makedepends=('pygtk' 'perl-uri' 'intltool' 'gtk-doc' 'xorg-iceauth')
optdepends=('perl-uri: for mail-compose helper script')
conflicts=('exo' 'exo-git')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/exo/0.6/exo-${pkgver}.tar.bz2)
md5sums=('e25333df350abc30999cd16fe96ab690')

build() {
  cd "${srcdir}/exo-${pkgver}"
  # python2 fix
  export PYTHON=python2
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var  \
	--disable-static \
	--enable-gio-unix \
	--enable-python \
	--enable-gtk-doc \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/exo-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
