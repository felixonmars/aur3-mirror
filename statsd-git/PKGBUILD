# Maintainer: Sid Karunaratne <sid at karunaratne dot net>
pkgname=statsd-git
pkgver=20120530
pkgrel=1
pkgdesc="a network daemon for aggregating statistics (counters and timers), rolling them up, then sending them to graphite"
arch=('i686' 'x86_64')
url="https://github.com/etsy/statsd"
license=('MIT')
depends=(nodejs)
makedepends=(git)
source=("statsd.sh" "statsd.conf" "statsd.rc")
sha256sums=("bacc115f5a29fa7f920a2fde3656cfaed18f805925709cb3bf563efdcd3b3a93"
    "438669ee20909e2a0d364937d543c22e4d56dcdd9cfb6e09fefd7c81580312c2"
    "6920b6b522a33b9ae59d762c2e699e492ebd0952b273638359d0bb4be020256c")
install=install

_gitroot="git://github.com/etsy/statsd.git"
_gitname="statsd"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth=1 $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"

  cd "$srcdir/$_gitname"

  npm install nodeunit
  npm install temp
  npm install underscore
}


package() {
  mkdir -p "${pkgdir}/opt/"
  cp -r "${srcdir}/${_gitname}" "${pkgdir}/opt/"
  install -Dm 755 "${srcdir}/statsd.sh" "${pkgdir}/usr/bin/${_gitname}"
  install -Dm 755 "${srcdir}/statsd.rc" "${pkgdir}/etc/rc.d/${_gitname}"
  install -Dm 755 "${srcdir}/statsd.conf" "${pkgdir}/etc/conf.d/${_gitname}"
}
