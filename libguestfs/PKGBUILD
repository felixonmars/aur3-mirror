# Libguestfs: Installer: Arch
# Maintainer: Thomas S Hatch <thatch45 (at) Gmail.com>

pkgname=libguestfs
pkgver=1.8.1
pkgrel=2
pkgdesc="Manages virtual machine images"
arch=("i686" "x86_64")
url="http://libguestfs.org"
license=("GPL2" "LGPL2")
depends=("qemu-kvm"
         "febootstrap"
         "augeas"
         "libvirt"
         "hivex")
makedepends=("perl-io-stringy"
             "perl-test-simple"
             "ocaml-findlib"
             "java-environment=6"
             "python2"
             "ocaml"
             "php"
             "perl"
             "pcre"
             "fuse"
             "libxml2"
             "zfs-fuse"
             "gperf")
optdepends=("python2: Python bindings"
            "ocaml: Ocaml libs"
            "php: PHP bindings"
            "perl: Perl Bindings"
            "java-environment: Java Bindings")
options=(!emptydirs)
sha1sums=('5963b795d54180a36b49c491a51170c23ae0f9fc')
source=("http://libguestfs.org/download/1.8-stable/${pkgname}-${pkgver}.tar.gz")
BUILDENV=(!fakeroot !distcc color !ccache)

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure \
        PYTHON=python2 \
        --prefix=/usr --libdir=/usr/lib \
        --mandir=/usr/share/man \
        --sysconfdir=/etc \
        --with-qemu="qemu-kvm qemu" \
        --with-drive-if=virtio \
        --disable-ruby --disable-haskell
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
