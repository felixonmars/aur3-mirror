# Maintainer: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Maintainer: trapanator <trap[at]trapanator[dot]com>
# Maintainer: tomprogrammer <Thomas-Bahn[at]gmx[dot]net>

pkgname=phc-intel
pkgver=0.3.2.12.1
_realver=0.3.2-12-1
pkgrel=2
pkgdesc="frequency driver for Intel CPUs with undervolting feature"
url="http://www.linux-phc.org"
arch=('any')
license=('GPL')
depends=('kernel26>=2.6.33' 'kernel26<2.6.41' 'kernel26-headers>=2.6.33' 'kernel26-headers<2.6.41')
provides=('linux-phc')
backup=(etc/conf.d/phc-intel)
install=phc-intel.install
source=(phc-intel-$_realver.tar.bz2::$url/forum/download/file.php?id=94
phc-intel.{conf,rc,sleep} mperf.h
acpi-cpufreq_2.6.{35..36}.c linux-phc-0.3.2_2.6.{35..36}.patch)
md5sums=('0f55f583f9b62b0f6812a3cd2fd24680'
         'bc8b26dc2966cc9210c5631aceee025a'
         'f651d35ed1e5ba5f327cfd2510a56e4c'
         '65f4702ccb5fa1e2f3bf9e5b5ac8ca83'
         'eff9f1e59689cdd1b44d415072c26d95'
         '6068632173e19a8f10c882ba7da84ac9'
         '3b27dcabe6d1bb0b766d24d89a02d71b'
         '2f15cbdaf6d9ab40f60f97fa221f71a9'
         'e52792d26694ce7e38884dbe66b12bd6')

build() {
	cd phc-intel-$_realver
	sed -i 's,^\tinstall -m 644 -o root -g root phc-intel.modprobe,#\tinstall -m 644 -o root -g root phc-intel.modprobe,' Makefile
	sed -i 's,/sbin/modprobe phc-intel |,/sbin/modprobe phc-intel \&\& /etc/rc.d/phc-intel set |,' phc-intel.modprobe
}

package() {
	install -Dm644 phc-intel.conf "$pkgdir/etc/conf.d/phc-intel"
	install -Dm644 phc-intel-$_realver/phc-intel.modprobe "$pkgdir/etc/modprobe.d/phc-intel.conf"
	install -Dm755 phc-intel.rc "$pkgdir/etc/rc.d/phc-intel"
	install -Dm755 phc-intel.sleep "$pkgdir/usr/lib/pm-utils/sleep.d/00phc-intel"

	install -d "$pkgdir/usr/src/phc-intel"
	cp -LR phc-intel-$_realver/{inc,Makefile,prepare.sh} "$srcdir/mperf.h" "$pkgdir/usr/src/phc-intel/"

	cd "$pkgdir/usr/src/phc-intel/"
	local i
	for i in 2.6.{35..36}; do
		mkdir -p inc/$i
		cp "$srcdir/acpi-cpufreq_$i.c" inc/$i/acpi-cpufreq.c
		cp "$srcdir/linux-phc-0.3.2_$i.patch" inc/$i/linux-phc-0.3.2.patch
	done
	for i in 2.6.{37..40}; do
		ln -s 2.6.36 inc/$i
	done
}
