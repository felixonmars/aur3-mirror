# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: Eric Forgeot < http://ifiction.free.fr >
pkgname=zoom
pkgver=1.1.5
pkgrel=3
pkgdesc="A player for Z-Code stories or games (text adventures / interactive fiction)."
arch=('i686' 'x86_64')
url="http://www.logicalshift.co.uk/unix/zoom/"
license=('LGPL2.1')
depends=('libpng' 'libxft' 't1lib') 
changelog=Changelog
source=("${url}${pkgname}-${pkgver}.tar.gz"
	'xft_detect.patch')
sha256sums=('f2967f1c0555df8d4ae90443502d14cf33b6ac65b602f499fbe3e9e38e55dbbc'
            '785923c33fabc4feb549ff8e2fc48437bfbc03927c56da5ece231466869cc27d')

prepare() {
	cd $srcdir/$pkgname-$pkgver
	patch -i ../xft_detect.patch
}

build() {
	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	# documentation
	install -dm755 $pkgdir/usr/share/doc/$pkgname
	install -m644 \
		AUTHORS \
		ChangeLog \
		NEWS \
		README \
		THANKS \
		TODO \
		$pkgdir/usr/share/doc/$pkgname

	# manual
	install -dm755 $pkgdir/usr/share/doc/$pkgname/manual
	install -m644 manual/[^M]* $pkgdir/usr/share/doc/$pkgname/manual
}

