# Contributor: Austin ( doorknob60 [at] gmail [dot] com )
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=broadcom-wl
pkgver=5.100.82.112
pkgrel=3
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('i686' 'x86_64')
license=('custom')
depends=('linux')
makedepends=('linux-headers')

[[ $CARCH = x86_64 ]] && ARCH=x86_64 || ARCH=x86_32
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz"
        'modprobe.d'
        'linux3.patch'
        'license.patch'
        'multicast.patch'
        'semaphore.patch')
sha1sums=('01aa32f9e85621253a3f15cf4361bb80d41da3e8'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          '3e18f905bbe5e8b99a53d6ecb3b80a919f3531f2'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          '78c1fde0acce7c5b6212ba6d027a16c03f3ba1b7'
          '105f8e1d48ebe1f25d53859e5ab9326a27435c66')
[[ $CARCH = x86_64 ]] && sha1sums[0]='5bd78c20324e6a4aa9f3fafdc6f0155e884d5131'

backup=('etc/modprobe.d/broadcom-wl.conf')
install=install

_extramodules=extramodules-3.3-ARCH
_kernver="$(cat /lib/modules/${_extramodules}/version)"

build() {
	cd "${srcdir}"

	patch -p1 -i linux3.patch
	patch -p1 -i license.patch
	patch -p1 -i multicast.patch
	patch -p1 -i semaphore.patch

	make -C /lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"

	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"

	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl.conf
}
