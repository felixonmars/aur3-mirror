# Contributor: Austin ( doorknob60 [at] gmail [dot] com )
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=broadcom-wl
pkgver=5.100.82.111
pkgrel=2
pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver'
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('i686' 'x86_64')
license=('custom')
depends=('linux')
makedepends=('linux-headers')

[[ $CARCH = x86_64 ]] && ARCH=x86_64 || ARCH=x86_32
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz"
        'modprobe.d'
        'linux3.patch'
        'license.patch'
        'semaphore.patch')

sha1sums=('af23f31679973a1bcacea79b65d10dbe231f8862'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          '3e18f905bbe5e8b99a53d6ecb3b80a919f3531f2'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          '105f8e1d48ebe1f25d53859e5ab9326a27435c66')
[[ $CARCH = x86_64 ]] && sha1sums[0]='8028950609631106c7844bb039a29f78e92baae7'

backup=('etc/modprobe.d/broadcom-wl.conf')
install=install

_kernver="$(uname -r)"

build() {
	cd "${srcdir}"
	patch -p1 -i linux3.patch
	patch -p1 -i license.patch
	patch -p1 -i semaphore.patch
	make -C /lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"
	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/net/wireless/wl.ko"
	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl.conf
}
