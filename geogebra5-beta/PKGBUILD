# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=geogebra5-beta
pkgver=4.9.21.0
pkgrel=2
pkgdesc="Dynamic mathematics software with interactive graphics (2D and 3D), algebra and spreadsheet"
arch=('any')
url="http://www.geogebra.org/"
license=('custom')
# Application and source code under GPL / Language files under CCPL
depends=('java-runtime' 'jogl' 'desktop-file-utils')
optdepends=('kde-thumbnailer-geogebra: generates thumbnails of GeoGebra files in KDE'
	    'gnome-thumbnailer-geogebra: generates thumbnails of GeoGebra files in GNOME')
install='geogebra5-beta.install'
noextract=(*.jar)
_root="http://www.geogebra.org/webstart/5.0/"
source=("${pkgname}.png"
	"${pkgname}.desktop"
	"${pkgname}.sh"
	"${_root}geogebra.jar"
	"${_root}geogebra_3d.jar"
	"${_root}geogebra_algos.jar"
	"${_root}geogebra_cas.jar"
	"${_root}geogebra_export.jar"
	"${_root}geogebra_gui.jar"
	"${_root}geogebra_javascript.jar"
	"${_root}geogebra_main.jar"
	"${_root}geogebra_properties.jar"
	"${_root}geogebra_usb.jar"
	"${_root}jlatexmath.jar"
	"${_root}jlm_cyrillic.jar"
	"${_root}jlm_greek.jar"
	"${url}download/license.txt")
md5sums=('e324ee3a2bb438cee625e3f29770a315'
         '9dcd074a16b1ec498c7aefd3241e73fb'
         'd7e8d83a6bf9c0ba9f6a889cfa46c551'
         'f82a9b9ad2a7a82c21d851bd5814f051'
         'af436bf151a2107865a218a209517a12'
         'b83ce922a54de309ccc1c8eadf3fa0cb'
         '66ec3733eb7f96024f2066e8353e82bf'
         'd7455d8587ca12755bf2e34b34783530'
         '4b706acae8b55e6391778a0d37d848a3'
         'db6741817490f276a3f9d54c244458b4'
         '9a9bc9013d75010ab29ae08e556fa61b'
         'd43ed112738f4c0ab80a0ca626dc4126'
         'f239e01ab7daf5b3cb5b1cda11da7905'
         'c7bf540b6a88af2afb78e87e93578308'
         '87f050230474ad6a9a137498d2b6020b'
         '8e94c6b1643a40f6e740e35d001b7301'
         '90b23253931127e7ece468aa0b8ad383')

package() {
  cd "${srcdir}"

  # Installing application
  install -d -m755 "${pkgdir}/usr/share/java/${pkgname}"
  install -D -m644 *.jar "${pkgdir}/usr/share/java/${pkgname}"
  install -D -m755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Creating menu item
  install -D -m644 "${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -D -m644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Installing license
  install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Needed to use the jogl package
  ln -s "/usr/share/java/jogl/gluegen-rt.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/gluegen-rt.jar"
  ln -s "/usr/share/java/jogl/jogl.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/jogl.jar"

  # Needed to use the jlatexmath package
#   ln -s "/usr/share/java/jlatexmath-minimal/jlatexmath-minimal.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlatexmath.jar"
#   ln -s "/usr/share/java/jlatexmath-minimal/jlm_greek.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlm_greek.jar"
#   ln -s "/usr/share/java/jlatexmath-minimal/jlm_cyrillic.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlm_cyrillic.jar"
}
