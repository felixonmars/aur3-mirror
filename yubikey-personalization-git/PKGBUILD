# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubikey-personalization-git
pkgver=20120610
pkgrel=1
pkgdesc="Yubico yubikey personalization library"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubikey-personalization"
license=('BSD')
depends=('libusb' 'yubico-c-client-git')
makedepends=('git')
provides=('ykpers')
conflicts=('ykpers')
source=('automake.patch')

_gitroot="git://github.com/Yubico/yubikey-personalization.git"
_gitname="yubikey-personalization"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "$srcdir/$_gitname-build"

  # fix automake 1.12
  patch -Np1 < ${srcdir}/automake.patch

  autoreconf --install
  ./configure --prefix=/usr
  make
  make check
}

package() {
  cd "${srcdir}/${_gitname}-build"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  make DESTDIR="${pkgdir}/" install
} 

sha256sums=('9a27b1300a1e03edda7a9a9cb5f02ce3c50d4ff5d893145927e657a8c068b736')
