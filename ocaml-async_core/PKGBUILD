# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>

pkgname=ocaml-async_core
_pkgname=${pkgname/ocaml-/}
pkgver=108.07.01
pkgrel=1
pkgdesc="Jane Street Capital's asynchronous execution library (core)"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12'
         'ocaml-type-conv'
         'ocaml-sexplib'
         'ocaml-fieldslib'
         'ocaml-bin-prot'
         'ocaml-core')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
url="http://bitbucket.org/yminsky/ocaml-core/wiki/Home"
source=("http://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('6fac99d205e41de0206cbd8862325302')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
