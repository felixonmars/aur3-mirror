# Maintainer: Boohbah <boohbah at gmail.com>

pkgname=libsodium-git
_gitname=libsodium
pkgver=458.8ba5834
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl-based crypto library (git version)"
arch=('i686' 'x86_64')
url="https://github.com/jedisct1/libsodium"
license=('MIT')
makedepends=('git')
conflicts=('libsodium')
provides=('libsodium')
source=('git://github.com/jedisct1/libsodium.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  sh autogen.sh
  ./configure --prefix="/usr"
  make
}

check() {
  cd $_gitname
  make check
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
