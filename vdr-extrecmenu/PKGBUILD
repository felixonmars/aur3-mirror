# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Franz Burgmann <f dot burgmann at gmail dot com>

pkgname=vdr-extrecmenu
_pluginname=extrecmenu
pkgver=1.2.4
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin that extends the recordings menu"
url="http://projects.vdr-developer.org/projects/show/plg-extrecmenu"
license=('GPL2')
depends=('vdr' 'vdrsymbols-ttf')
backup=('usr/bin/dvdarchive.sh')
source=(http://projects.vdr-developer.org/attachments/download/1783/${pkgname}-${pkgver}.tgz)
md5sums=('12724c725267626a18dfe4a4993c69cd')

package() {
  cd ${srcdir}/${_pluginname}-${pkgver}

  # Assumed VDR directory environment (VDRDIR/Make.config):
  # MANDIR       = /usr/share/man
  # BINDIR       = /usr/bin
  # LOCDIR       = /usr/share/locale
  # PLUGINLIBDIR = /usr/lib/vdr
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr

  install -d -m755 ${pkgdir}/usr/bin
  install -d -m755 ${pkgdir}/usr/lib/vdr
  install -d -m755 ${pkgdir}/usr/share/doc/${pkgname}
  install -d -m755 ${pkgdir}/usr/share/locale

  make VDRDIR=/usr/include/vdr \
       LIBDIR=${pkgdir}/usr/lib/vdr \
       LOCALEDIR=${pkgdir}/usr/share/locale all

  # Uncomment lines below to build and install getlength/isodetect utilities
  #gcc contrib/getlength.c -o getlength
  #install -m755 getlength ${pkgdir}/usr/bin/getlength
  #gcc contrib/isodetect.c -o isodetect
  #install -m755 isodetect ${pkgdir}/usr/bin/isodetect

  # Install script
  install -m755 scripts/dvdarchive.sh ${pkgdir}/usr/bin/dvdarchive.sh

  # Install documents
  for _file in README HISTORY; do
    install -m644 ${_file} ${pkgdir}/usr/share/doc/${pkgname}/${_file}
  done
}
