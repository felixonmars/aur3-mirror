# Maintainer: bouquetf <bouquet.frederic@gmail.com>
pkgname=xwiki-enterprise
pkgver=6.1
pkgrel=1
depends=(tomcat7)
arch=('any')

pkgdesc="XWiki Enterprise is a professional wiki that has powerful \
  extensibility features such as scripting in pages, plugins and a highly \
  modular architecture."
url="http://www.xwiki.org/"
license=(GPL)
makedepends=(unzip)
conflicts=(xwiki-manager)
backup=(etc/xwiki-enterprise/server.xml
        etc/xwiki-enterprise/web.xml
	etc/xwiki-enterprise/context.xml
        etc/xwiki-enterprise/hibernate.cfg.xml
        etc/xwiki-enterprise/xwiki.cfg)
options=()

source=(http://download.forge.objectweb.org/xwiki/${pkgname}-web-${pkgver}.war
        http://repo1.maven.org/maven2/hsqldb/hsqldb/1.8.0.7/hsqldb-1.8.0.7.jar
        web.xml
        server.xml
        context.xml
        systemd_xwiki.service
       	systemd_tmpfiles.d_xwiki.conf
)
noextract=(${pkgname}-web-${pkgver}.war)

md5sums=('31a477adf34a61b16a85a7127a75d5de'
         'ed0ff8ff5ba782cd6c508d3599039e13'
         '80c63e5d61ac2c485041fe99f12143a7'
         '243c711930a975db7507077c41bde651'
         '85dba04282daafb94d67d95f60c2af8b'
         '9de40098506ee7b543ea97742db0c1a3'
         'a6e71e5d542bc534ffbc2b98f8df70a8')

package() {
  #log
  install -dm775 -o 71 -g 19 ${pkgdir}/var/log/${pkgname}
  install -dm775 -o 71 -g 19 ${pkgdir}/usr/share/${pkgname}
  ln -s /var/log/${pkgname} ${pkgdir}/usr/share/${pkgname}/logs
  touch ${pkgdir}/var/log/${pkgname}/catalina.{out,err}
  chown 71:19 ${pkgdir}/var/log/${pkgname}/catalina.{out,err}

  #webapp
  install -dm775 ${pkgdir}/var/lib/${pkgname}
  install -dm775 ${pkgdir}/var/lib/${pkgname}/webapps
  unzip $srcdir/${pkgname}-web-${pkgver}.war -d ${pkgdir}/var/lib/${pkgname}/webapps/ROOT
  chown -R 71:71 ${pkgdir}/var/lib/${pkgname}
  ln -s /var/lib/${pkgname}/webapps ${pkgdir}/usr/share/${pkgname}/webapps

  #conf
  install -dm775 ${pkgdir}/etc/${pkgname}
  install -g 71 -m640 ${srcdir}/web.xml ${pkgdir}/etc/${pkgname}
  install -g 71 -m640 ${srcdir}/server.xml ${pkgdir}/etc/${pkgname}
  install -g 71 -m640 ${srcdir}/context.xml ${pkgdir}/etc/${pkgname}
  install -d -g 71 -m775 ${pkgdir}/etc/${pkgname}/Catalina
  ln -s /etc/${pkgname} ${pkgdir}/usr/share/${pkgname}/conf
  mv ${pkgdir}/var/lib/${pkgname}/webapps/ROOT/WEB-INF/xwiki.cfg ${pkgdir}/etc/${pkgname}/xwiki.cfg
  ln -s /etc/${pkgname}/xwiki.cfg ${pkgdir}/var/lib/${pkgname}/webapps/ROOT/WEB-INF/xwiki.cfg
  mv ${pkgdir}/var/lib/${pkgname}/webapps/ROOT/WEB-INF/hibernate.cfg.xml ${pkgdir}/etc/${pkgname}/hibernate.cfg.xml 
  ln -s /etc/${pkgname}/hibernate.cfg.xml ${pkgdir}/var/lib/${pkgname}/webapps/ROOT/WEB-INF/hibernate.cfg.xml 
  #lib
  install -dm775 ${pkgdir}/usr/share/${pkgname}/lib
  install -Dm775 -g71 -o71 ${srcdir}/hsqldb-1.8.0.7.jar ${pkgdir}/usr/share/${pkgname}/lib

  #temp, work
  install -dm1777 ${pkgdir}/var/tmp
  install -dm775 -o 71 -g 71 ${pkgdir}/var/tmp/${pkgname}/{temp,work}
  ln -s /var/tmp/${pkgname}/temp ${pkgdir}/usr/share/${pkgname}/temp
  ln -s /var/tmp/${pkgname}/work ${pkgdir}/usr/share/${pkgname}/work

  #systemd scripts
  install -Dm644 ${srcdir}/systemd_xwiki.service \
                 ${pkgdir}/usr/lib/systemd/system/${pkgname}.service
  install -Dm644 ${srcdir}/systemd_tmpfiles.d_xwiki.conf \
                 ${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf

}

# vim:set ts=2 sw=2 et:

