# Maintainer: josephgbr <rafael.f.f1 at gmail.com>
# Contributor: Themaister <maister at archlinux.us>

# Hint: for a cleaner but less informative usage, replace "Debug"
# with "Release" in the line '-DCMAKE_BUILD_TYPE=' below

pkgname=pcsx2-svn
pkgver=5607
pkgrel=1
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL3')
if [[ $CARCH == i686 ]]; then
    depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew' 'portaudio'
             'libgl' 'alsa-lib' 'libjpeg-turbo' 'soundtouch'
             'gtk-engines' 'sdl' 'libaio')
    makedepends=('cmake' 'subversion' 'sparsehash')
elif [[ $CARCH == x86_64 ]]; then
    depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
             'lib32-libgl' 'lib32-glew' 'lib32-libjpeg-turbo'
             'lib32-portaudio' 'lib32-alsa-lib' 'lib32-soundtouch'
             'lib32-gtk-engines' 'lib32-sdl' 'lib32-libaio')
    makedepends=('cmake' 'subversion' 'sparsehash' 'gcc-multilib')
fi
provides=('pcsx2')
conflicts=('pcsx2' 'pcsx2-bin')
options=('!emptydirs' '!strip') # if build type = debug, then
                                # avoid strip symbols
install="${pkgname}.install"
changelog="ChangeLog"
source=(pcsx2::svn+http://pcsx2.googlecode.com/svn/trunk)
md5sums=('SKIP')

pkgver () {
  svnversion "${SRCDEST:-$srcdir}"/pcsx2
}

build() {
  cd "${srcdir}"/pcsx2
  
  # For ArchLinux 64-bit, install plugins in lib32 folder
  _plugindir=/usr/lib/pcsx2
  [[ $CARCH == x86_64 ]] && _plugindir=/usr/lib32/pcsx2
  
  # Disable plugins as they are only for debug performance
  # and might confuse users
  mv plugins/GSnull plugins/GSnull-disabled
  mv plugins/SPU2null plugins/SPU2null-disabled
  
  msg "Starting cmake and build..."
  cmake CMakeLists.txt \
      -DCMAKE_BUILD_TYPE="Debug" \
      -DPACKAGE_MODE=TRUE \
      -DCMAKE_INSTALL_PREFIX="/usr" \
      -DPLUGIN_DIR=${_plugindir} \
      -DGAMEINDEX_DIR="/usr/share/pcsx2" \
      -DGLSL_SHADER_DIR="/usr/share/pcsx2" \
      -DBUILD_REPLAY_LOADERS=FALSE \
      -DXDG_STD=TRUE
  make
}

package() {
  cd "${srcdir}"/pcsx2
  make DESTDIR="${pkgdir}" install

  # There are some different licenses embedded, that end up compatible with
  # GPLv3. So, making this information in the package. File made for Debian.
  install -Dm644 debian-unstable-upstream/copyright \
                    "${pkgdir}"/usr/share/licenses/pcsx2/copyright
}
