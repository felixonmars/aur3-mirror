# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=telepathy-indicator
pkgver=0.1.0
pkgrel=1
pkgdesc="Telepathy integration with the messaging menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/telepathy-indicator"
license=('GPL')
depends=('empathy' 'libindicate-gtk3' 'libunity')
makedepends=('intltool')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('1e506d402b96d3d4c7143a299a146a22')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/indicate-gtk-0.6/indicate-gtk3-0.6/' configure{,.ac}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir/usr/share/indicators/messages/applications"
  echo '/usr/share/applications/empathy.desktop' > "$pkgdir/usr/share/indicators/messages/applications/empathy"
}
