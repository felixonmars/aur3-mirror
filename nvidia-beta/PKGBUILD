# Maintainer: Det
# Contributors: Ng Oon-Ee, Dan Vratil
# Based on [extra]'s nvidia: https://www.archlinux.org/packages/extra/x86_64/nvidia/

pkgname=nvidia-beta
pkgver=331.17
pkgrel=1
pkgdesc="NVIDIA driver for linux (beta)"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux' 'nvidia-libgl-beta' "nvidia-utils-beta=${pkgver}")
makedepends=('linux-headers')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
provides=('nvidia')
license=('custom:NVIDIA')
install=${pkgname}.install
options=('!strip')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    md5sums=('4d4ac6de414df7572abfebe81f7ea229')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    md5sums=('c0d411c4f0ad7a577881d16ff8999097')
fi

#source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
        'nvidia-linux-3.11.patch')
md5sums+=('fa89e842c853e0636ce56fc87b1c75d8') # nvidia-linux-3.11.patch

build() {
    # Version of 'linux'
    _major=$(pacman -Q linux | awk -F '[ .]' '{print $2"."$3}')
    _extramodules=extramodules-${_major}-ARCH
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    # Remove previous builds
    if [ -d ${_pkg} ]; then
        rm -rf ${_pkg}
    fi

    # Extract
    sh ${_pkg}.run --extract-only
    cd ${_pkg}/kernel

    # Patch
    if [[ ${_kernver} = 3.11* ]]; then
        msg2 "Applying nvidia-linux-3.11.patch..."
        patch -p2 -i "${srcdir}/nvidia-linux-3.11.patch"
    fi

    # Build module
    msg2 "Starting make module..."
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    # Version of 'linux'
    _major=$(pacman -Q linux | awk -F '[ .]' '{print $2"."$3}')
    _extramodules=extramodules-${_major}-ARCH

    # Install/compress module
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

    # Write $_extramodules to .install
    sed -i "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "${startdir}/${install}"

    # Blacklist Nouveau
    install -d "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
}