# Maintainer : Dan Vratil <vratil@progdansoft.com>

pkgname=nvidia-beta 
pkgver=302.17
pkgrel=1
pkgdesc="NVIDIA beta drivers for linux." 
arch=('i686' 'x86_64') 
[ "$CARCH" = x86_64 ] && ARCH=x86_64 &&_srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32
[ "$CARCH" = "i686" ] && ARCH=x86 && _srcname=NVIDIA-Linux-x86-${pkgver}
provides=("nvidia=${pkgver}")
url="http://www.nvidia.com/" 
depends=("nvidia-utils-beta=${pkgver}") 
makedepends=('linux-headers')
conflicts=('nvidia-96xx' 'nvidia-71xx' 'nvidia-legacy' 'nvidia') 
license=('custom') 
install=nvidia.install
source=("http://us.download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run")

_kernver="$(uname -r | cut -d'.' -f-2)"
_extramodules=extramodules-${_kernver}-ARCH
_kernname="$(cat /lib/modules/${_extramodules}/version)"

[ "$CARCH" = "i686" ] && md5sums=('b7f908ea08218df08db06026215ec419' )
[ "$CARCH" = "x86_64" ] && md5sums=('7e768412a16853078b04037a7cc9c8ac' )

build()
{
	# Extract the nvidia drivers
	cd "${srcdir}"
	if [ -d ${_srcname} ]; then
		rm -rf ${_srcname}
	fi
	sh ${_srcname}.run --extract-only
	cd ${_srcname}/kernel

	make SYSSRC=/usr/src/linux-${_kernname} module
}

package() 
{
	cd "${srcdir}/${_srcname}/kernel"

	# Install kernel module
	mkdir -p "${pkgdir}/lib/modules/${_extramodules}/"
	install -m644 nvidia.ko "${pkgdir}/lib/modules/${_extramodules}/"

	# Blacklist nouveau since 2.6.34
	mkdir -p "${pkgdir}/etc/modprobe.d/"
	echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
                      
}

