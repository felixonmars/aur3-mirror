# Contributor: Paul Bredbury <brebs@sent.com>
# Old Maintainer: Biru Ionut <biru.ionut at gmail.com>
# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
pkgname=fontconfig-ubuntu
pkgver=2.8.0
_pkgver=2ubuntu1
pkgrel=1
pkgdesc="A library for configuring and customizing font access, with Ubuntu's LCD rendering patches"
arch=('i686' 'x86_64')
url="http://packages.ubuntu.com/lucid/fontconfig"
license=('custom')
depends=('expat>=2.0.1' 'freetype2-ubuntu>=2.3.11')
conflicts=('fontconfig')
provides=("fontconfig=$pkgver")
options=('!libtool')
install=fontconfig.install
source=(http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_${pkgver}.orig.tar.gz
        http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_${pkgver}-${_pkgver}.diff.gz)

md5sums=('77e15a92006ddc2adbb06f840d591c0e'
         'ea8666fd1906bf101b9cfc42bf325f94')

build() {
  cd ${srcdir}/fontconfig-${pkgver}

  patch -Np1 -i ../fontconfig_${pkgver}-${_pkgver}.diff || return 1

  for _f in $(cat debian/patches/series) ; do
     patch -Np1 -i "debian/patches/${_f}" || return 1
  done

  # Enable Position Independent Code for prelinking
  export CFLAGS="${CFLAGS} -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  rm -f ${pkgdir}/etc/fonts/conf.d/*.conf

  # License
  install -Dm0644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING || return 1

  # Docs
  install -Dm0644 debian/changelog ${pkgdir}/usr/share/doc/fontconfig/changelog || return 1
}

