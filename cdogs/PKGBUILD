# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: nut543 <kfs1@online.no>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com>
# Maintainer: jose riha <jose 1711 gmail com>

pkgname=cdogs
pkgver=0.4
pkgrel=4
pkgdesc='An SDL port of the old DOS arcade game "C-Dogs" (aka "Cyberdogs 2")'
arch=('i686' 'x86_64')
url="http://lumaki.com/code/cdogs/"
license=('GPL')
depends=('sdl_mixer')
install=$pkgname.install
source=(http://icculus.org/cdogs-sdl/files/src/cdogs-sdl-$pkgver.tar.bz2 \
	http://icculus.org/cdogs-sdl/files/data/cdogs-data-2007-07-06.tar.bz2 \
	$pkgname.png \
	$pkgname.desktop \
	fixes.diff)
md5sums=('62f7e055d5febfb8d27ea7ef2c45019f'
         '8525152c0d97cbbc9ae61026bcc08f86'
         '933aef96cc07ab6c3b3bd6b1ae1fd05c'
         '418769d1f9544da2a08b42199f2fac43'
         '2d1aa8f152f36823595689028961f056')

build() {
  cd $pkgname-sdl-$pkgver/src

  patch -Np2 -i "$srcdir"/fixes.diff

  export LDFLAGS="-lSDL_mixer -lSDL"

  make	cdogs \
	DATADIR=/usr/share/cdogs \
	SYSTEM=linux \
	I_AM_CONFIGURED=yes \
	CFLAGS="${CFLAGS} $(sdl-config --cflags)" \
	STRIP=:
}

package() {
  cd $pkgname-sdl-$pkgver/src
  mkdir -p "$pkgdir"/usr/{bin,share/doc}
  install -m755 cdogs "$pkgdir"/usr/bin/
  cp -rf ../doc/ "$pkgdir"/usr/share/doc/cdogs/
  cp -rf "$srcdir"/cdogs-data-2007-07-06/ "$pkgdir"/usr/share/cdogs/

  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
