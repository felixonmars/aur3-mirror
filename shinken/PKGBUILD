# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Julien "Adyxax" Dessaux <judessaux@gmail.com>

pkgname='shinken'
pkgver='1.2.2'
pkgrel='1'
pkgdesc='An open source Nagios® like tool, redesigned and rewritten from scratch. Its main goal is to meet today’s system monitoring requirements while still following compatibility to Nagios®'
arch=('any')
url='http://www.shinken-monitoring.org/'
license=('GPL3')
depends=('python2-pyro')
makedepends=('python2-distribute')
optdepends=('nagios-plugins: various monitoring checks'
            'nagcon: a simple yet great curses interface over nagios style status.dat file'
            'python-pysqlite: for the livestatus module'
            'python-simplejson: for the livestatus module')
install='shinken.install'
source=("http://shinken-monitoring.org/pub/$pkgname-$pkgver.tar.gz"
        'shinken.install'
        'shinken.logrotate'
        'shinken.tmpfiles'
        'shinken-arbiter.service'
        'shinken-broker.service'
        'shinken-poller.service'
        'shinken-reactionner.service'
        'shinken-receiver.service'
        'shinken-scheduler.service'
        'setup.cfg')

build() {
	cp -f setup.cfg "$srcdir/$pkgname-$pkgver/setup.cfg"
	cd "$srcdir/$pkgname-$pkgver"

	find -name '*.py' -exec sed -i 's|^#!/usr/bin/env python2.6$|#!/usr/bin/env python2.7|' {} \;
	find -type f -exec sed -i 's|/etc/init.d|/etc/rc.d|' {} \;
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	python2 setup.py install --root="$pkgdir"

	mv "$pkgdir/etc/init.d" "$pkgdir/etc/rc.d"
	mv "$pkgdir/usr/"{bin,sbin}

	# man
	install -dm0755 "$pkgdir/usr/share/man/man3"
	cp doc/man/* "$pkgdir/usr/share/man/man3"

	# logrotate
	install -Dm0644 "$srcdir/shinken.logrotate" "$pkgdir/etc/logrotate.d/shinken"

	# systemd
	install -Dm0644 "$srcdir/shinken.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/shinken.conf"
	for service in arbiter broker poller reactionner receiver scheduler; do
	    install -Dm0644 "$srcdir/shinken-$service.service" "$pkgdir/usr/lib/systemd/system/shinken-$service.service"
	done

	# cleanup
	rm "$pkgdir/usr/sbin/"*.py
	rm "$pkgdir/etc/shinken/"{brokerd-windows.ini,nagios-windows.cfg,pollerd-windows.ini,schedulerd-windows.ini}
	find "$pkgdir/etc/" -type f -exec sed -i "s|$pkgdir||g" {} \;
	sed -i 's|/usr/lib/nagios/plugins|/usr/share/nagios/libexec|g' "$pkgdir/etc/shinken/resource.cfg"
	sed -i 's|modules.*Livestatus, Simple-log|modules Status-Dat, Simple-log|' "$pkgdir/etc/shinken/shinken-specific.cfg"
	sed -i 's|log_end_msg [01]|echo |' "$pkgdir/etc/rc.d/shinken"

	mkdir "$pkgdir/var/lib/shinken/rw"
}

sha256sums=('50f6be3fb7aed9345699d826f937d9a3e1da67194cf096049285e011709d2524'
            '695bc1a2d20ff20f04a5809eb94bdad6bd674779b497d8b29abb51e9206e8c68'
            'fb460d2c790093d3737dea62ac632df80293a58c983e8f837b968f5c3490cdd5'
            'e0f4017bd5f270909cd668279b64f09a73674ead3f1d0e29f11ec392397b9b71'
            '988e28b47b6ebe98e9e25b29ec2ec2b75707640607464e32e0b978bb29d6681a'
            '5caa62318f01ce1ad374f0fa1e62648f06c650ba6a0324eb0b18d1888f215a25'
            '4e1bff0bd93c060583169959223dd4963c2056b3815ba19ee7206cdb06963aa6'
            '637f69feb39db7874ba05c9db64cabea338d00454a8247c1a481d972a7957d06'
            'c0114ffec0dc2b16b8909d33eac3914ae72d734d23ae165ced61f1a9aacd9919'
            'ed822f1061bf6649e640bc21defa71057c98d279cc1a0cc77eddbc53f0d24834'
            'e58d8b4244f4995c463f10a5975854cdaaf5c27b55a23a47ef1a81f02fdda2c0')
