# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Julien "Adyxax" Dessaux <judessaux@gmail.com>

pkgname='shinken'
pkgver='1.0.1'
pkgrel='1'
pkgdesc='An open source Nagios® like tool, redesigned and rewritten from scratch. Its main goal is to meet today’s system monitoring requirements while still following compatibility to Nagios®'
arch=('any')
url='http://www.shinken-monitoring.org/'
license=('GPL3')
depends=('python2-pyro')
makedepends=('python2-distribute')
optdepends=('nagios-plugins: various monitoring checks'
            'nagcon: a simple yet great curses interface over nagios style status.dat file'
            'python-pysqlite: for the livestatus module'
            'python-simplejson: for the livestatus module')
install='shinken.install'
source=("http://shinken-monitoring.org/pub/$pkgname-$pkgver.tar.gz" 'shinken.install' 'shinken.setup_parameters')

build() {
	cp -f shinken.setup_parameters "$srcdir/$pkgname-$pkgver/setup_parameters"
	cd "$srcdir/$pkgname-$pkgver"

	find -name '*.py' -exec sed -i 's|^#!/usr/bin/env python2.6$|#!/usr/bin/env python2.7|' {} \;
	find -type f -exec sed -i 's|/etc/init.d|/etc/rc.d|' {} \;
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	python2 setup.py install --root="$pkgdir"
	mv "$pkgdir/etc/init.d" "$pkgdir/etc/rc.d"
	rm "$pkgdir/etc/shinken/"{brokerd-windows.ini,nagios-windows.cfg,pollerd-windows.ini,schedulerd-windows.ini}
	find "$pkgdir/etc/" -type f -exec sed -i "s|$pkgdir||g" {} \;
	sed -i 's|/usr/lib/nagios/plugins|/usr/share/nagios/libexec|g' "$pkgdir/etc/shinken/resource.cfg"
	sed -i 's|modules.*Livestatus, Simple-log|modules Status-Dat, Simple-log|' "$pkgdir/etc/shinken/shinken-specific.cfg"
	sed -i 's|log_end_msg [01]|echo |' "$pkgdir/etc/rc.d/shinken"
	mkdir "$pkgdir/var/lib/shinken/rw"
}

md5sums=('591dc55ac92dd975a222eb779419724b'
         '069d026262e4077089e5526e6e672f9c'
         '85d8cb4b28662f83df26a3da05e2f4ff')
