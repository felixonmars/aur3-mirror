# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Julien "Adyxax" Dessaux <judessaux@gmail.com>
# Contributor: Arthur Vuillard <arthur@hashbang.fr>

pkgname=shinken
pkgver=2.0.3
pkgrel=2
pkgdesc='An open source Nagios® like tool, redesigned and rewritten from scratch. Its main goal is to meet today’s system monitoring requirements while still following compatibility to Nagios®'
arch=('any')
url='http://www.shinken-monitoring.org/'
license=('GPL3')
depends=('python2-pyro' 'python2-pycurl')
makedepends=('python2-setuptools' 'python2-sphinx')
optdepends=(
    'monitoring-plugins: various monitoring checks'
)
install='shinken.install'
source=(
	"https://pypi.python.org/packages/source/S/Shinken/Shinken-${pkgver//_/-}.tar.gz"
	"shinken-arbiter.service"
	"shinken-broker.service"
	"shinken.install"
	"shinken.logrotate"
	"shinken-poller.service"
	"shinken-reactionner.service"
	"shinken-receiver.service"
	"shinken-scheduler.service"
	"shinken.tmpfiles"
)

prepare() {
	cd "$srcdir/Shinken-${pkgver//_/-}"
	find -name '*.py' -exec sed -i 's|^#!/usr/bin/env python2.6$|#!/usr/bin/env python2|' {} \;
	find -name '*.py' -exec sed -i 's|^#!/usr/bin/env python\s*$|#!/usr/bin/env python2|' {} \;
	find bin -exec sed -i 's|^#!/usr/bin/env python\s*$|#!/usr/bin/env python2|' {} \;
	sed "s#/usr/lib/nagios/plugins#/usr/lib/monitoring-plugins#" etc/resource.d/paths.cfg
}

build() {
	cd "$srcdir/Shinken-${pkgver//_/-}"
	python2 setup.py build

	msg2 "Building docs"
	cd doc
	sphinx-build2 -b man -d build/doctrees/ source/ build/man/
}

package() {
	cd "$srcdir/Shinken-${pkgver//_/-}"
	python2 setup.py install --skip-build -O1 --root="$pkgdir"

	cd "$pkgdir"
	install -dm0755 var/lib/shinken/rw

	# man
	install -dm0755 usr/share/man
	mv var/lib/shinken/doc/build/man usr/share/man/man1

	# cleanup
	rm -rf var/lib/shinken/doc etc/init.d

	cd "$srcdir"

	# logrotate
	install -Dm0644 shinken.logrotate "$pkgdir/etc/logrotate.d/shinken"

	# systemd
	install -Dm0644 shinken.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/shinken.conf"
	for service in *.service; do
	    install -Dm0644 $service "$pkgdir/usr/lib/systemd/system/$service"
	done
    
	install -dDm750 "$pkgdir/var/log/shinken"
	install -dDm710 "$pkgdir/var/run/shinken"
}

sha256sums=('c47b68c2ba4073bbb3b5e65f46eb73c76479e69b363b6f570a7bdb6e0bc3ed67'
            'efa31d6bac681f6e994f5a0544ecaa5ab044099057d5e75d2e6b41e9e69f361f'
            '845315a7eb1b1a3f7fca8051b1470d28b5e9950ce55cda17bf2d0a7bc4a1e644'
            'd39c703e7266190be49002297f0d92526d006ff27d711ed256a90df6d4d77ddb'
            '2af98d2de44b7b1d587f10fb46c3c3dceb1d96083d6398596b2e49ca5b69053a'
            'd6c6cfe8c7cffd7f12eb905b1d740d1c1f599a2611e07ce975679071c2f19648'
            '8c2bdcc3af62f06a2f8168fb9eef74d65a931fba8be91a4947edc7d3e93666b1'
            'eaf9ca44e8d4a1589d694ea9341ad91c41306a6c0038d5fa7a41e36556cfafff'
            'b4d80c1b54dfa5890d4a9e68622e197444aa8c6132d003b7ddd12ae9b2a15615'
            '3b4a1f756422dec77e19e4968e4cda3e3e6030cb90de9e783957c995c64af109')
