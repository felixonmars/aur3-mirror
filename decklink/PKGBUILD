# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=decklink
_basever=9.6.4
_dlid=4819889
_mever=3.1.2
pkgver=${_basever}a24
pkgrel=1
pkgdesc="Drivers for Blackmagic Design DeckLink, Intensity or Multibridge video editing cards"
arch=('i686' 'x86_64')
url="http://www.blackmagic-design.com/products/"
license=('custom')
depends=('linux-headers' 'libxml2' 'mesa' 'qt' 'gcc-libs')
options=('!strip')
install='decklink.install'
source=("http://www.blackmagic-design.com/media/${_dlid}/Blackmagic_Desktop_Video_Linux_${_basever}.tar.gz"
        'decklink.conf')
sha256sums=('87540538a87128f99b3ae390ad28d690e63f89caee4c915b5d7450bb415418a7'
            '49c6f841e7f0d663bc426552159eba1610f880259db9fee6f2e2bf62988358b7')

package() {
  # license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s /usr/share/doc/desktopvideo/License.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # automatic module loading
  install -D -m 644 decklink.conf "$pkgdir/etc/modules-load.d/decklink.conf"

  cd "$srcdir"
  [ "$CARCH" = "i686" ] && _arch='i386' && _debarch='i386'
  [ "$CARCH" = "x86_64" ] && _arch='x86_64' && _debarch='amd64'

  # Driver
  tar xf "desktopvideo-${_basever}-${_arch}.tar.gz"
  cd "$srcdir"
  cp -a "desktopvideo-$pkgver-${_arch}/"* "$pkgdir"
  rm "$pkgdir/usr/lib/blackmagic/libQt"*

  # MediaExpress
  ar xf "mediaexpress-${_mever}-${_debarch}.deb"
  tar xf data.tar.gz -C "$pkgdir"
}

# vim:set ts=2 sw=2 et:
