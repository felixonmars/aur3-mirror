# Maintainer: Tofe <chris.chapuis@gmail.com>
# Contributor: 4javier
# Contributor: snoopy33

pkgname=cairo-dock
pkgver=2.4.0
pkgrel=2
pkgdesc="A light eye-candy fully themable animated dock for any Linux desktop. It has a family-likeness with OSX dock, but with more options."
url="https://launchpad.net/cairo-dock-core"
license="GPL"
arch=('i686' 'x86_64')
depends=('cairo' 'librsvg' 'dbus-glib' 'gtkglext')
makedepends=('cmake' 'libtool' 'pkgconfig' 'gettext' 'inputproto')
options=('!libtool')
source=(http://launchpad.net/$pkgname-core/2.4/$pkgver/+download/$pkgname-$pkgver~$pkgrel.tar.gz)
md5sums=('2407e97b74a97c383dd7ef4f0c83a206')

build() {
  cd $srcdir/$pkgname-$pkgver~$pkgrel

  sed -i '/\/gtypes/d' src/gldit/cairo-dock-keybinder.h
  
  [[ -e build ]] || mkdir build 
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  # put the right path for the themes
  sed -i '6athemesdir=/usr/share/cairo-dock/themes' cairo-dock.pc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver~$pkgrel/build
  make install DESTDIR=$pkgdir
}

