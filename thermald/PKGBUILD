# Maintainer: wallnuss <v dot churavy at gmail dot com
# Contributor: WonderWoofy <sugar.and.scruffy@gmail.com>
pkgname=thermald
_name=thermal_daemon
pkgver=1.1
_srcdir=$_name-${pkgver//_/-}
pkgrel=2
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('i686' 'x86_64')
url="https://github.com/01org/thermal_daemon"
license=('GPL2')
depends=('dbus-glib>=0.94' 'libxml2>=2.4')
backup=('etc/thermald/thermal-conf.xml')
makedepends=('git')
conflicts=('thermald')
provides=('thermald')
source=(${url}/archive/v${pkgver//_/-}.tar.gz
        'modules-load-msr.conf')
md5sums=('e30d0bd8fb32bd493df207aa34a48751'
         '3f8abd21c1be92e631fd9f557c61355b')

build() {
  cd $_srcdir 
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd $_srcdir
  make DESTDIR="$pkgdir" install
  mkdir -p ${pkgdir}/usr/lib/modules-load.d
  install -m644 ${srcdir}/modules-load-msr.conf ${pkgdir}/usr/lib/modules-load.d/thermald.conf
}
