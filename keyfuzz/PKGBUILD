# Mantainer: Tom Kuther <archlinux@kuther.net>
# Contributer: N30N <archlinux@alunamation.com>
# Contributer: Aerion <this.id.was.still.free@aerion.co.uk>

pkgname=keyfuzz
pkgver=0.2
pkgrel=9
pkgdesc="A Utility for manipulating the scancode/keycode translation tables of keyboard drivers"
url="http://0pointer.de/lennart/projects/keyfuzz/"
license="GPL2"
arch=("i686" "x86_64")
depends=()
makedepends=("lynx")

# original source is down since 2014/08. It's small, so pack it in the aurball...
#source=("${url}${pkgname}-${pkgver}.tar.gz"
source=("${pkgname}-${pkgver}.tar.gz"
	"${pkgname}.systemd"
	"logitech_media_keyboard_600.keyfuzz"
	"support_by_id.patch")
md5sums=('dffcc16ffd68988fafe7365f1124517d'
         '0c543cc90d9918794f4376174b1dcbe3'
         '3cf549f08569db59aaf14a244921f6f8'
         '058e0bdca92ce10a690dccf7a24c7daf')

build() {
	cd "${pkgname}-${pkgver}"
	patch -p0 < $srcdir/support_by_id.patch
	./configure --prefix=/usr \
		--with-initdir=/usr/lib/systemd/scripts \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D -m644 ${srcdir}/keyfuzz.systemd "${pkgdir}/usr/lib/systemd/system/keyfuzz.service"
	install -D -m644 ${srcdir}/logitech_media_keyboard_600.keyfuzz "${pkgdir}/etc/keyfuzz/logitech_media_keyboard_600.keyfuzz"
}

# vim: set noet ff=unix:
