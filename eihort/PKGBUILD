pkgname=eihort
pkgver=0.3.11
pkgrel=1
pkgdesc='A OpenGL world renderer for minecraft with a really long view distance'
license=('BSD')
url="http://www.minecraftforum.net/topic/438878-/"
arch=('i686' 'x86_64')
depends=('lua' 'glew' 'sdl_image' 'zlib')
makedepends=('python2')
source=("https://bitbucket.org/lloigor/$pkgname/get/$pkgver.tar.gz"
        'icon.tar.gz' "$pkgname" "$pkgname.desktop")
noextract=("$pkgver.tar.gz")
sha1sums=('3c768095b346994f7636f2909b075794bb07d822'
          'ba8443da8ff9ff45f3a29069726afb440aaabf1a'
          '54aca14c3362849a64e0a81bd66652408526095a'
          '5fd5dd65b7c2cbc728d384e482163d3a8bf25a80')

build() {
	cd "$srcdir"
	if [[ ! -d "$pkgname-$pkgver" ]]; then
		mkdir "$pkgname-$pkgver"
		tar -xf "../$pkgver.tar.gz" --strip-components=1 -C "$pkgname-$pkgver"
	fi
	cd "$pkgname-$pkgver"
	
	#replaces hg for the makefile
	cat >hg <<-EOF
	#!/bin/sh
	echo "v$pkgver"
	EOF
	chmod +x hg
	
	PATH=".:$PATH" make -f makefile.gnu "$pkgname-$pkgver-linux-$(uname -m)"
}

package() {
	cd "$pkgdir"
	
	install -D "$srcdir/$pkgname-$pkgver/$pkgname-$pkgver-linux-$(uname -m)" "usr/share/$pkgname/$pkgname"
	install -m644 $(find "$srcdir/$pkgname-$pkgver/deploy/" \( ! -name "*.dll" \) -type f) "usr/share/$pkgname/"
	
	for f in "$srcdir/icon/"*.png; do
		s="$(basename $f .png)"
		install -Dm644 "$f" "usr/share/icons/hicolor/${s}x${s}/apps/$pkgname.png"
	done
	
	install -D "$srcdir/$pkgname" "usr/bin/$pkgname"
	install -D "$srcdir/$pkgname.desktop" "usr/share/applications/$pkgname.desktop"
}