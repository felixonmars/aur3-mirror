# Maintainer: Filip Brcic <brcha@gna.org>
# Contributor: Philip A Reimer <antreimer@gmail.com>

_qt_module=qttranslations
pkgname="mingw-w64-qt5-translations"
pkgver=5.1.0
pkgrel=1
arch=('any')
pkgdesc="Qt5 for Windows -- QtTranslations module (mingw-w64)"
depends=('mingw-w64-qt5-base')
makedepends=('mingw-w64-gcc' 'mingw-w64-qt5-tools')
options=(!strip !buildflags)
replaces=("${pkgname/qt5-/qt5-qt}<=5.0.2")
conflicts=("${pkgname/qt5-/qt5-qt}")
license=('GPL3' 'LGPL')
url="http://qt-project.org/"
_pkgfqn="${_qt_module}-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/5.1/${pkgver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('a9332ce3ca07d5c98194e9b9b793d117')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd ${srcdir}/${_pkgfqn}

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}

    ${_arch}-qmake-qt5 ../${_qt_module}.pro
    make

    popd
  done
}

package() {
  cd ${srcdir}/${_pkgfqn}

  for _arch in ${_architectures}; do
    pushd build-${_arch}

    make INSTALL_ROOT=$pkgdir install

    popd
  done
}
