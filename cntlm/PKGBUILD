# Maintainer: Jordi De Groof <jordi.degroof@gmail.com>
# Contributor: Max Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Simon Tan <simon1tan@yahoo.com>
# Contributor: Angel 'angvp' Vel√°squez <angvp[at]archlinux.com.ve>

pkgname=cntlm
pkgver=0.92.3
pkgrel=2
license=('GPL2')
pkgdesc="An NTLM, NTLM2SR, and NTLMv2 authenticating HTTP proxy"
arch=('i686' 'x86_64')
url="http://cntlm.sourceforge.net"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname%20$pkgver/$pkgname-$pkgver.tar.gz"
	'cntlm'
	'cntlm.service')
backup=('etc/cntlm.conf')
md5sums=('0d7fcfbfbef0546306b896be246caa88'
         '8aa5c57c208371f86f465c9c8e0bce70'
         '859cfe08d3d5bdf2100da28389781222')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make \
		SYSCONFDIR="$pkgdir/etc" \
		BINDIR="$pkgdir/usr/bin" \
		MANDIR="$pkgdir/usr/share/man" \
		install
	install -Dm 755 "$srcdir/cntlm" "$pkgdir/etc/rc.d/cntlm"
	install -Dm 644 "$srcdir/cntlm.service" "$pkgdir/usr/lib/systemd/system/cntlm.service"
}
