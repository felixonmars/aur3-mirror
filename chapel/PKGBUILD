# Maintainer: TDY <tdy.aur@gmail.com>

pkgname=chapel
pkgver=1.6.0
pkgrel=1
pkgdesc="An emerging parallel programming language from Cray Inc designed for high-end computing"
arch=('i686' 'x86_64')
url="http://chapel.cray.com/"
license=('BSD' 'GPL' 'custom')
depends=('bc' 'python2')
options=('!emptydirs')
install=chpl.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
        chplenv.sh)
md5sums=('7d32e4519b1a0e5e26f4c2be8d1ccf57'
         '700f7a34abe3a7f549ca2fc5e083111c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find -type f -exec sed -i '1s/python/&2/' '{}' \;
  make depend all
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # docs
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/technotes/"
  install -m644 doc/technotes/* "$pkgdir/usr/share/doc/$pkgname/technotes/"
  install -m644 doc/chapelLanguageSpec.pdf "$pkgdir/usr/share/doc/$pkgname/"
  install -m644 doc/quickReference.pdf "$pkgdir/usr/share/doc/$pkgname/"
  install -m644 doc/README* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 man/man1/chpl.1 "$pkgdir/usr/share/man/man1/chpl.1"

  # dist
  install -dm755 "$pkgdir/opt/$pkgname/"
  cp -a bin lib make modules runtime third-party util "$pkgdir/opt/$pkgname/"
  install -Dm755 ../chplenv.sh "$pkgdir/etc/profile.d/chplenv.sh"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # vim
  install -Dm644 etc/vim/ftdetect/chpl.vim "$pkgdir/usr/share/vim/vimfiles/ftdetect/chpl.vim"
  install -Dm644 etc/vim/indent/chpl.vim "$pkgdir/usr/share/vim/vimfiles/indent/chpl.vim"
  install -Dm644 etc/vim/syntax/chpl.vim "$pkgdir/usr/share/vim/vimfiles/syntax/chpl.vim"
  install -Dm644 etc/vim/README "$pkgdir/usr/share/doc/$pkgname/vim/README"
  install -Dm644 etc/vim/sample-vimrc "$pkgdir/usr/share/doc/$pkgname/vim/vimrc"
  install -Dm644 etc/vim/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE.vim"

  # emacs
  install -Dm644 etc/emacs/22/chpl-mode.el "$pkgdir/usr/share/emacs/chpl-mode.el"
  install -Dm644 etc/emacs/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE.emacs"

  # cleanup
  cd "$pkgdir/opt/$pkgname"
  rm -r $(find -type d -name docs -o -name man)

  msg2 "Enforcing text file permissions..."
  for _file in $(find -type f); do
    (head -n1 "$_file" | grep '^#!' &>/dev/null) && chmod 755 "$_file" || chmod 644 "$_file"
  done

  msg2 "Enforcing directory and ELF permissions..."
  chmod 755 $(find ! -name '*.o' -exec file '{}' \; | grep -E 'ELF|directory' | cut -d: -f1)
}

# vim:set ts=2 sw=2 et:
