# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>
pkgname=coinor-dylp
pkgver=1.9.4
pkgrel=1
pkgdesc="COIN-OR DyLPume Algorithm"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/DyLP"
license=('EPL')
groups=('coinor')
depends=('coinor-osi')
source=("http://www.coin-or.org/download/source/DyLP/DyLP-${pkgver}.zip")
sha1sums=('74d15800daecc8b7d4f5aa72fa31e394b72fa4c8')

build() {
  cd DyLP-$pkgver
  COIN_SKIP_PROJECTS="Sample" \
  LDFLAGS="$LDFLAGS -Wl,-lm" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" -C
  make
}

# check() {
#   cd DyLP-$pkgver
# #   make test
# }

package() {
  cd DyLP-$pkgver
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir"/ install
}
