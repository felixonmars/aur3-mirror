# Maintainer: Bastien Dejean <baskerville@lavabit.com>

pkgname=bspwm-git
pkgver=20130106
pkgrel=2
pkgdesc='A tiling window manager based on binary space partitioning'
arch=('i686' 'x86_64')
url='https://github.com/baskerville/bspwm'
license=('custom:BSD')
depends=('libxcb' 'xcb-util' 'xcb-util-wm')
makedepends=('git')
provides=('bspwm')
conflicts=('bspwm')
_gitroot='git://github.com/baskerville/bspwm.git'
_gitname='bspwm'

build() {
    cd "$srcdir"
    if [ -d "$_gitname" ] ; then
        cd "$_gitname" && git pull origin
    else
        git clone "$_gitroot" "$_gitname"
    fi

    cd "$srcdir/$_gitname"
    make clean
    make PREFIX="/usr"
}

package() {
    cd "$srcdir/$_gitname"
    make PREFIX="/usr" DESTDIR="$pkgdir" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
