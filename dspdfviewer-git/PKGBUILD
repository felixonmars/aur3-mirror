# Maintainer: rich_o <rich_o@lavabit.com>

_pkgname="dspdfviewer"
pkgname="${_pkgname}-git"
pkgver=12.ddd82df
pkgrel=1
pkgdesc="Viewer for latex-beamer presentations that are built with the «show notes on second screen»-option"
arch=('i686' 'x86_64')
url="https://github.com/dannyedel/dspdfviewer"
license=('GPL2')
depends=('boost-libs' 'poppler-qt')
makedepends=('git' 'cmake' 'boost')
conflicts=('dspdfviewer')
source=("git+git://github.com/dannyedel/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${SRCDEST}/${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/${_pkgname}"
  cmake -DCMAKE_INSTALL_PREFIX="/usr/" -DSYSCONFDIR="/etc" -DCMAKE_BUILD_TYPE=Release 
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  find "${pkgdir}" -type d -name .git -exec rm -r '{}' +
}

# vim:set ts=2 sw=2 et:
