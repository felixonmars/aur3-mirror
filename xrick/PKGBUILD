# Maintainer: carstene1ns <arch carsten-teibes de>
# Contributor: grimi <grimi at poczta dot fm>

pkgname=xrick
pkgver=021212
pkgrel=10
pkgdesc='A clone of Rick Dangerous'
arch=('i686' 'x86_64')
url='http://www.bigorno.net/xrick'
license=('custom: spirit of GPL')
depends=('sdl' 'zlib')
makedepends=('imagemagick')
source=("http://www.bigorno.net/xrick/$pkgname-$pkgver.zip"
        "$pkgname.desktop"
        "$pkgname-fs.desktop")
md5sums=('6a56d23679f51c602b285642c6451fb6'
         'SKIP'
         'SKIP')
sha256sums=('2a167169ccdb2c108e97d5d021da9be29356d756283f1c20d5a7ad121b4accf0'
            'SKIP'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  # change data path
  sed -i 's|"data.zip"|"/usr/share/xrick/data.zip"|' src/xrick.c

  # add our build flags
  sed -i "s|cflags)|cflags) ${CFLAGS}|" Makefile

  make

  # create icon
  convert src/xrickST.ico xrick.png

  # change data path in manual page
  gunzip xrick.6.gz
  sed -i 's|where xrick is|/usr/share/xrick.|' xrick.6
  sed -i 's|run from.||' xrick.6
  gzip xrick.6
}

package() {
  cd $pkgname-$pkgver

  # install binary, icon, data, manual, licence and desktop files
  install -d $pkgdir/usr/{bin,share/{xrick,applications,man/man6,licenses/$pkgname,pixmaps}}
  install -m755 xrick $pkgdir/usr/bin/xrick
  install -m644 {data.zip,KeyCodes} $pkgdir/usr/share/xrick/
  install -m644 xrick.6.gz $pkgdir/usr/share/man/man6/
  tail -n +11 README | head -n 18 > $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 ../xrick.desktop $pkgdir/usr/share/applications/
  install -m644 xrick.png $pkgdir/usr/share/pixmaps/
}
