# Contributor: Nebulosa <nebulosa2007 na yandekse>

pkgname=gnome-connection-manager
pkgver=1.1.0
pkgrel=1
pkgdesc="Tabbed ssh connection manager for gtk+ environments."
arch=('i686' 'x86_64')
url="http://kuthulu.com/gcm/"
license=('GPL')
depends=('python2' 'pygtk' 'expect' 'vte' 'openssh')
source=("http://kuthulu.com/gcm/gnome-connection-manager_${pkgver}_all.tgz" "$pkgname.desktop")
md5sums=('3dd40d49817f60aa9dbfbb27200ad182'
         '7078085c58ce264b357df22c7bc09730')

build() {
	_pyexec=${srcdir}/gcm-${pkgver}/gnome_connection_manager.py
	sed -i "s|BASE_PATH = os.path.*|BASE_PATH = '/usr/share/$pkgname'|" $_pyexec
	sed -i "s|glade_dir = \"\"|glade_dir = BASE_PATH|" $_pyexec
	sed -i "s|env python\s*$|env python2|" $_pyexec
}

package() {
	_datadir=$pkgdir/usr/share/$pkgname
	_docdir=$pkgdir/usr/share/doc/$pkgname
	_bindir=$pkgdir/usr/bin
	mkdir -p $_datadir $_docdir $_bindir
	cp -r $srcdir/gcm-${pkgver}/* $_datadir/
	find $_datadir -type f -exec chmod 0644 {} \;
	find $_datadir -type d -exec chmod 0755 {} \;
	chmod 0755 $_datadir/*.py $_datadir/*.expect
	ln -sf ${_datadir/$pkgdir/}/gnome_connection_manager.py $pkgdir/usr/bin/$pkgname
	ln -sf ${_datadir/$pkgdir/}/README.txt $_docdir/README
	install -D $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
