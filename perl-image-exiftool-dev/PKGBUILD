# Maintainer speps <spepser at aur dot archlinux dot org>

_src=Image-ExifTool
_base=perl-image-exiftool
pkgname=$_base-dev
pkgver=9.06
pkgrel=1
license=('GPL' 'PerlArtistic')
pkgdesc="Reader and rewriter of EXIF informations that supports raw files. Development releases."
arch=('any')
url="http://www.sno.phy.queensu.ca/~phil/exiftool/"
depends=('perl>5.10.0')
provides=("$_base=$pkgver" "perl-exiftool=$pkgver" "perl-exiftool-dev=$pkgver")
conflicts=("$_base" 'perl-exiftool' 'perl-exiftool-dev')
replaces=('perl-exiftool-dev' 'perl-exiftool')
options=('!emptydirs')
source=("$url$_src-$pkgver.tar.gz")
md5sums=('8763116d1b1f7777dd6de884dbe18663')

build() {
  cd "$srcdir/$_src-$pkgver"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/$_src-$pkgver"
  make install DESTDIR="$pkgdir/"
}
