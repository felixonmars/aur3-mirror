# maintainer: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Alexander Jenisch <nt@divzero.at>
# Contributor: Chris Brannon <cmbrannon@cox.net>
# Contributor: Andreas Messer <andi@bupfen.de>

pkgname=speech-dispatcher-git
pkgver=20111103
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="High-level device independent layer for speech synthesis interface"
license=('GPL2' 'FDL')
depends=('glib2' 'libao' 'python2')
optdepends=('festival-freebsoft-utils: Speech output using Festival.'
            'flite: Speech output using Festival Lite.'
            'espeak: Speech output using ESpeak.')
makedepends=('pkgconfig' 'espeak' 'dotconf' 'automake' 'intltool')
provides=("speech-dispatcher")
conflicts=('speech-dispatcher')
options=('!libtool') 
url="http://www.freebsoft.org/speechd"
source=("speechd.run" "speechd.sh" "speechd.logrotate")

install="speech-dispatcher.install"

backup=('etc/speech-dispatcher/clients/gnome-speech.conf'
	'etc/speech-dispatcher/clients/emacs.conf'
	'etc/speech-dispatcher/modules/ibmtts.conf'
	'etc/speech-dispatcher/modules/espeak-generic.conf'
	'etc/speech-dispatcher/modules/espeak.conf'
	'etc/speech-dispatcher/modules/swift-generic.conf'
	'etc/speech-dispatcher/modules/festival.conf'
	'etc/speech-dispatcher/modules/cicero.conf'
	'etc/speech-dispatcher/modules/espeak-mbrola-generic.conf'
	'etc/speech-dispatcher/modules/dtk-generic.conf'
	'etc/speech-dispatcher/modules/llia_phon-generic.conf'
	'etc/speech-dispatcher/modules/ivona.conf'
	'etc/speech-dispatcher/modules/epos-generic.conf'
	'etc/speech-dispatcher/modules/flite.conf'
	'etc/speech-dispatcher/speechd.conf'
	'etc/logrotate.d/speechd')

_gitroot="git://git.freebsoft.org/git/speechd"
_gitname="speechd"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # Building speechd
  autoreconf -i
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --with-alsa --with-libao
  make
  make DESTDIR=${pkgdir} install

  # Remove info dir and compress info pages
  rm -f ${pkgdir}/usr/share/info/dir
  gzip -9nf ${pkgdir}/usr/share/info/*

  # Install startup, profile and logrotate files
  install -D -m755 ${srcdir}/speechd.run ${pkgdir}/etc/rc.d/speechd
  install -D -m644 ${srcdir}/speechd.sh ${pkgdir}/etc/profile.d/speechd.sh
  install -D -m644 ${srcdir}/speechd.logrotate ${pkgdir}/etc/logrotate.d/speechd

  # Modify speechd.conf to accomodate running as a system wide service
  cd "${pkgdir}/etc/speech-dispatcher"
  sed -i -e 's|LogDir  "default"|#LogDir  "default"|' speechd.conf
  sed -i -e 's|#LogDir  "/var/log/speech-dispatcher/"|LogDir  "/var/log/speech-dispatcher/"|' speechd.conf
  sed -i -e 's|# AudioOutputMethod "pulse"|AudioOutputMethod "alsa"|' speechd.conf
  sed -i -e 's|# DisableAutoSpawn|DisableAutoSpawn|' speechd.conf

  # Fix spd-conf to use python 2.7
  sed -i -e 's|#!/usr/bin/python|#!/usr/bin/python2|' ${pkgdir}/usr/bin/spd-conf
}
md5sums=('bcd48386c47e907442f3d773601637b8'
         '9e8c90ef357b1932e30dfcfa9ed92d0f'
         'eb921b0672daae025bb075398b914525')
