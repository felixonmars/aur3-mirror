# Contributor: Dan Serban
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Maintainer: Ner0

pkgname=plank-bzr
pkgver=621
pkgrel=1
pkgdesc="A stupidly simple dock from elementary"
arch=('i686' 'x86_64')
url="https://launchpad.net/plank"
license=('GPL3')
depends=('bamf' 'glib2' 'gtk3' 'libgee' 'libwnck3' 'libx11' 'python2' 'hicolor-icon-theme' 'cairo' 'pango')
makedepends=('dbus-glib' 'intltool' 'pkg-config' 'vala')
options=('!libtool')
install=$pkgname.install

_bzrtrunk=lp:plank
_bzrmod=plank

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build
  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$_bzrmod-build/"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
  sed -i 's/python/python2/' "$pkgdir/usr/share/apport/package-hooks/source_plank.py"
}
