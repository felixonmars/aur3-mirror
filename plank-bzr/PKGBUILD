# Maintainer: Ner0

pkgname=plank-bzr
pkgver=809
pkgrel=1
pkgdesc="A stupidly simple dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/plank"
license=('GPL3')
groups=('pantheon')
depends=('bamf' 'glib2' 'gtk3' 'libgee06' 'libwnck3' 'libx11' 'python2' 'hicolor-icon-theme' 'cairo' 'pango' 'libdbusmenu-gtk3')
makedepends=('bzr' 'dbus-glib' 'intltool' 'vala' 'gnome-common')
options=('!libtool')
install=plank.install
source=('plank::bzr+http://bazaar.launchpad.net/~docky-core/plank/trunk/')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/plank"
  bzr revno
}

prepare() {
  cd "$srcdir/plank"
  sed -i 's/python$/python2/' data/apport/source_plank.py
}

build() {
  cd "$srcdir/plank"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-gee-0.8
  make
}

package() {
  cd "$srcdir/plank"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
