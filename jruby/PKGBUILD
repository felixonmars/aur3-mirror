# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=1.7.0rc1
_pkgver=1.7.0.RC1
pkgrel=1
pkgdesc="JRuby is 100% pure-Java implementation of the Ruby programming language."
arch=(any)
url="http://www.jruby.org"
license=(CPL GPL2 LGPL2.1 custom)
depends=(java-runtime)
options=(!strip)
source=("http://$pkgname.org.s3.amazonaws.com/downloads/$_pkgver/$pkgname-bin-$_pkgver.tar.gz")
sha256sums=('a52ea970f116a11454a7c77c40b6700765def02ccbea45c16ed8cf110617fe94')

if false; then
makedepends=(java-environment apache-ant)
build() {
  cd jruby
  ant dist-bin
  tar -C .. -xf dist/$pkgname-bin-$_pkgver.tar.gz
}
fi

package() {
  cd "$pkgdir"

  mkdir opt
  cp -r "$srcdir/jruby-$_pkgver" opt/jruby

  mkdir -p usr/bin
  for _file in jirb{,_swing} jruby{,c} jgem; do
    ln -s ../../opt/jruby/bin/$_file usr/bin/$_file
  done

  mkdir -p usr/share/licenses/$pkgname
  ln -s ../../../../opt/jruby/COPYING \
    usr/share/licenses/$pkgname/COPYING
}
