# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=lib32-lua 
pkgver=5.1.5
pkgrel=2
pkgdesc="A powerful light-weight programming language designed for extending applications" 
arch=('x86_64')
url="http://www.lua.org/"
depends=('lua' 'lib32-glibc')
makedepends=('lib32-readline')
license=('MIT')
options=('!makeflags' '!emptydirs')
source=(http://www.lua.org/ftp/lua-${pkgver}.tar.gz
        lua-arch.patch lua-5.1-cflags.diff)
md5sums=('2e115fe26e435e33b0d5c022e4490567'
         'fa25feb70ef9fec975b1c20da5098b3c'
	 '249582bf1fd861ccf492d2c35a9fe732')

build() { 
  cd "${srcdir}/lua-${pkgver}"
  patch -p1 -i "${srcdir}/lua-arch.patch"
  patch -p1 -i "${srcdir}/lua-5.1-cflags.diff"
  export CFLAGS="$CFLAGS -march=i686 -m32 -fPIC"
  export LDFLAGS="$LDFLAGS -march=i686 -m32"

  make INSTALL_DATA="cp -d" TO_LIB="liblua.a liblua.so liblua.so.5.1" \
    INSTALL_TOP="${pkgdir}/usr" MYLDFLAGS="$LDFLAGS" \
    linux
}

package() {
  cd "${srcdir}/lua-${pkgver}"
  make INSTALL_DATA="cp -d" TO_LIB="liblua.a liblua.so liblua.so.5.1 liblua.so.$pkgver" \
    INSTALL_TOP="${pkgdir}/usr" \
    install
  rm -rf ${pkgdir}/usr/{bin,include,man}
  mv ${pkgdir}/usr/lib ${pkgdir}/usr/lib32
}
