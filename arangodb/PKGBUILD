# Maintainer: Bambang Purnomosidi D. P. <bpurnomo at akakom dot ac dot id>
# Contributor: Jan Steemann <j.steemann at triagens dot de>
pkgname=arangodb
pkgver=1.1.0
pkgrel=1
pkgdesc="A universal open-source database with a flexible data model for documents, graphs, and key-values."
arch=('i686' 'x86_64')
url="http://www.arangodb.org/"
license=('Apache')
depends=('openssl' 'bash')
makedepends=('python2')
source=(http://www.arangodb.org/repositories/Source/ArangoDB-$pkgver.tar.bz2)
md5sums=('00b2d2490ea08526abe3e12e6f929ec9')

build() {
  cd "$srcdir/ArangoDB-$pkgver"
  ln -s -f /usr/bin/python2 python
  export PATH="`pwd`:$PATH"
  ./configure --bindir=/usr/bin --sbindir=/usr/sbin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share
  make CFLAGS="-Wno-c++0x-compat" CXXFLAGS="-Wno-c++0x-compat" 
}

check() {
  cd "$srcdir/ArangoDB-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/ArangoDB-$pkgver"
  make DESTDIR="$pkgdir/" install
  mkdir $pkgdir/usr/share/doc
  mkdir $pkgdir/usr/share/doc/arangodb
  cp -R $srcdir/ArangoDB-$pkgver/Documentation/* $pkgdir/usr/share/doc/arangodb/
  rm -rf $pkgdir/usr/share/doc/arangodb/man*
}

# vim:set ts=2 sw=2 et:
