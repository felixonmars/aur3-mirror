pkgname=fpc-arm-android-lnet
pkgver=0.6.5
pkgrel=2
pkgdesc="lNet or Lightweight Networking Library is a collection of classes and components to enable event-driven TCP or UDP networking. (arm-android)"
url="http://wiki.freepascal.org/lNet"
license=("LGPL")
arch=(any)
makedepends=(fpc android-ndk dos2unix)
depends=(fpc-arm-android-rtl)
options=(!strip)
source=("http://downloads.sourceforge.net/lazarus-ccr/lnet-$pkgver.zip")
sha1sums=('829aef9af514aa30aa1b25c94a04fed5ec5722d6')
_unittgt=arm-android
_fpcver=`fpc -iV`

prepare() {
	cd "${srcdir}/lnet/lib"
	rm lws2tcpip.pp
	rm openssl.pas
	sed -i "s,OPENSSLaddallalgorithms,OpenSSL_add_all_algorithms,g" lnetssl.pp
	find . -type f -exec dos2unix {} \;
}

build() {
  cd "${srcdir}/lnet/lib"
  if [ $CARCH == "i686" ]; then
		export PATH=$PATH:$ANDROID_NDK/toolchains/arm-linux-androideabi-4.8/prebuilt/linux-x86/bin
	else
		export PATH=$PATH:$ANDROID_NDK/toolchains/arm-linux-androideabi-4.8/prebuilt/linux-x86_64/bin
	fi
  for file in `ls *.pp`
  do
    ppcrossarm -CfVFPV3 -O3 -Xs -XX -Isys -Sm -XParm-linux-androideabi- -Tandroid $file
  done
}

package() {
  cd "${srcdir}/lnet/lib"
  if [ $CARCH == "i686" ]; then
		export PATH=$PATH:$ANDROID_NDK/toolchains/arm-linux-androideabi-4.8/prebuilt/linux-x86/bin
	else
		export PATH=$PATH:$ANDROID_NDK/toolchains/arm-linux-androideabi-4.8/prebuilt/linux-x86_64/bin
	fi
  find . -name '*.o' -o -name '*.ppu' -o -name '*.rst' -o -name '*.a'|
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/fpc/$_fpcver/units/$_unittgt/lnet/"{}
  find $pkgdir -name '*.o' -o -name '*.a' | xargs -rtl1 arm-linux-androideabi-strip -g
}
