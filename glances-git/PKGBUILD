# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=glances-git
pkgver=1.7.4.r2.gd8f76cb
pkgrel=1
pkgdesc="A cross-platform curses-based monitoring tool"
arch=('any')
url="https://github.com/nicolargo/glances"
license=('LGPL3')
depends=('python2-psutil' 'python2-setuptools')
makedepends=('git')
optdepends=('hddtemp: HDD temperature monitoring support'
            'python2-jinja: HTML output'
            'python2-sensors: temperature monitoring support')
provides=('glances')
conflicts=('glances')
source=('git://github.com/nicolargo/glances.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir"/glances
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/v//'
}

prepare() {
  cd "$srcdir"/glances
  sed -i 's/env python/&2/' glances/glances.py
}

build() {
  cd "$srcdir"/glances
  python2 setup.py build
}

package() {
  cd "$srcdir"/glances
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
