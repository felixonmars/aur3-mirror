# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=glances-git
pkgver=20120727
pkgrel=1
pkgdesc="CLI curses-based monitoring tool"
arch=('any')
url="https://github.com/nicolargo/glances"
license=('LGPL')
depends=('python2-distribute' 'python2-psutil')
makedepends=('git')
optdepends=('python2-jinja: HTML output')
provides=('glances')
conflicts=('glances')

_gitroot="git://github.com/nicolargo/glances.git"
_gitname="glances"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # python2 fix
  sed -i 's/env python/&2/' {setup,glances/{glances,unitest}}.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname-build"

  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
