# Maintainer: naelstrof <naelstrof@gmail.com>
pkgname=mingw-w64-libvorbis
pkgver=1.3.3
pkgrel=1
pkgdesc="Vorbis codec library (mingw-w64)"
arch=(any)
url="http://xiph.org"
license=('custom')
depends=('mingw-w64-libogg' 'mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'mingw-w64-binutils')
options=(!libtool !strip !buildflags)
source=(http://downloads.xiph.org/releases/vorbis/libvorbis-${pkgver}.tar.gz)
md5sums=('6b1a36f0d72332fae5130688e65efe1f')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  #-march=i686 optimizes too much, strip it out
  for _arch in ${_architectures}; do
      mkdir -p ${srcdir}/libvorbis-${pkgver}-build-${_arch}
      cd ${srcdir}/libvorbis-${pkgver}-build-${_arch}
      CFLAGS=${CFLAGS/-march=$CARCH} ${srcdir}/libvorbis-${pkgver}/configure --enable-static --enable-shared --prefix=/usr/${_arch} --host=${_arch}
      make
  done
}

package() {
  for _arch in ${_architectures}; do
      cd ${srcdir}/libvorbis-${pkgver}-build-${_arch}
      make DESTDIR="$pkgdir" install
      rm -r $pkgdir/usr/${_arch}/share/doc
  done
}
