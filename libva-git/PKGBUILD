# Maintainer: TryA <tryagainprod@gmail.com>

pkgname=libva-git
pkgver=20110728
pkgrel=1
pkgdesc="VA API library, providing an interface for hardware video decoding and encoding"
arch=('i686' 'x86_64')
license=('MIT')
url="http://www.freedesktop.org/wiki/Software/vaapi"
depends=('libdrm' 'libgl')
provides=('libva=1.0.14')
conflicts=('libva' 'libva-sds')
options=(!libtool)
source=(libva-dont-install-tests.patch)
md5sums=('3a948c916882c5d7035bf03c1b88bff8')

_gitroot="git://anongit.freedesktop.org/libva"
_gitname="libva"

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d ${srcdir}/${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${srcdir}/${_gitname}-build
  cp -r ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build

  patch -Np1 -i "${srcdir}/libva-dont-install-tests.patch"
  ./autogen.sh
  ./configure --prefix=/usr --enable-i965-driver --enable-glx
  make
}

package() {
  cd ${srcdir}/${_gitname}-build
  make DESTDIR="${pkgdir}" install
  install -m644 -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
