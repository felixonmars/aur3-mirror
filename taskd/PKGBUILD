# Maintainer: Markus Holtermann <aur@markusholtermann.eu>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=taskd
pkgver=1.0.0
pkgrel=3
pkgdesc="A lightweight, secure server providing multi-user, multi-client access to task data"
url='http://tasktools.org/projects/taskd.html'
license=('MIT')
arch=('i686' 'x86_64')
depends=('task' 'gnutls')
makedepends=('cmake')
install=taskd.install
source=("git://tasktools.org/taskd.git#tag=v1.0.0"
        'taskd.conf' 'taskd.service'
        '0001-fix-pki-generate-path.patch')
md5sums=('SKIP'
         '53d39485cf6868ff591aa0a32813bb7d'
         'ccbe4f8376b33c6e69231267a0f0ef90'
         'aa2eee8972a8c0fffa0e65ddabe0c305')

prepare() {
  mkdir -p build
}

build() {
  patch -p0 < "0001-fix-pki-generate-path.patch"
  cd build
  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/taskd.conf \
    "${pkgdir}"/etc/conf.d/taskd

  install -Dm644 "${srcdir}"/taskd.service \
    "${pkgdir}"/usr/lib/systemd/system/taskd.service

  install -d "${pkgdir}"/var/lib/taskd

  install -Dm644 ../${pkgname}/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  install -d "${pkgdir}"/usr/share/taskd/pki

  install -D "${srcdir}"/taskd/pki/generate{,.ca,.client,.crl,.server} \
    "${pkgdir}"/usr/share/taskd/pki/
}
