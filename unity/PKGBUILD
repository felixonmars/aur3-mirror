# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=unity
pkgname=unity
true & pkgname=(unity-common unity)
pkgver=4.24.0
pkgrel=3
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
makedepends=('nux>=1.4.0-2' 'libunity' 'libindicator3' 'compiz-core-dev' 'bamf' 'libunity-misc>=0.4.0' 'json-glib' 'libnotify' 'gnome-desktop' 'cmake' 'boost' 'intltool' 'glproto' 'dri2proto')
source=(http://launchpad.net/$pkgbase/4.0/$pkgver/+download/$pkgbase-$pkgver.tar.bz2
        launchpad-export.tar.gz
        nautilus-home.desktop
        debian-changes-4.24.0-0ubuntu2
        make-tests-optional.patch
        disable-multitouch.patch
        use-unity-profile.patch
        default-settings.patch
        unity.ini
        unity.sh
        unity.session
        unity.desktop)
md5sums=('dd804fc0e0d08b7bf7da7fc2d0fbc53a'
         'abba8f4ebdcc90983633248ebab361e1'
         '236c7d9173d65adc704b953d77724739'
         '2ce17276159e7baf2dd1f4a1e4e87fed'
         '24a0bc31e9d01a3c99bf70982f8a7f2c'
         '4ac83fa16eff01b836bca357b6094f0f'
         'cd19df00c59f647b1ae8b9d8cfd94c24'
         '79b8df38ecd937393d8366d297a1575e'
         'd954f6aba40501f4baeec94247aefef4'
         'c151208ef97f8e6139daeef72db6e08c'
         'e0b1d90fc06601ccc57fd49827266f9d'
         'fe17963c172e71d68704dfa4bb04bea4')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/*
  patch -Np1 -i "$srcdir/debian-changes-4.24.0-0ubuntu2"
  patch -Np1 -i "$srcdir/make-tests-optional.patch"
  patch -Np1 -i "$srcdir/disable-multitouch.patch"
  patch -Np1 -i "$srcdir/use-unity-profile.patch"
  patch -Np1 -i "$srcdir/default-settings.patch"

  # Install updated language files
  echo 'ace af am an ar as ast az be bem bg bn bo br bs ca ca@valencia crh cv cy cs da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fo fr fur fy gd gl gu gv he hi hr ht hu hy id is it ja ka kk km kn ko ku ky lb lt ltg lv mg mhr mk ml mr ms my nb ne nl nn oc or os pa pl pmy pt pt_BR ro ru rw sc sd si sk sl sq sr sv sw ta te th ti tl tr tt ug uk ur uz vec vi wae zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgbase- '' ../po/$pkgbase-*.po
  mv -f -t po ../po/*

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=Release -DCOMPIZ_PLUGIN_INSTALL_TYPE=package -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_TESTS=OFF
  make
}

package_unity-common() {
  pkgdesc="Shared files between unity and unity-2d"
  depends=('nux>=1.4.0-2' 'libunity' 'libindicator3' 'dconf')
  install=unity-common.install

  cd "$srcdir/$pkgbase-$pkgver/build"

  make DESTDIR="$pkgdir/" install

### Split unity files
  [[ -d $srcdir/unity ]] && rm -r "$srcdir/unity/"
  [[ -d $srcdir/unity-gconf ]] && rm -r "$srcdir/unity-gconf/"
  mkdir -p "$srcdir"/unity{,-gconf}/usr/{lib,share/{glib-2.0/schemas,man/man1}}

  mv {"$pkgdir","$srcdir/unity"}/usr/bin
  mv {"$pkgdir","$srcdir/unity"}/usr/lib/compiz
  mv {"$pkgdir","$srcdir/unity"}/usr/share/man/man1/unity.1
  mv {"$pkgdir","$srcdir/unity"}/usr/share/ccsm
  mv {"$pkgdir","$srcdir/unity"}/usr/share/compiz
  mv {"$pkgdir","$srcdir/unity-gconf"}/usr/share/gconf
  mv "$pkgdir"/usr/share/glib-2.0/schemas/org.freedesktop.compiz.*.gschema.xml "$srcdir/unity/usr/share/glib-2.0/schemas"
  mv {"$pkgdir","$srcdir/unity"}/usr/share/locale

  # Install Home folder for launcher
  install -Dm644 "$srcdir/nautilus-home.desktop" "$pkgdir/usr/share/applications/nautilus-home.desktop"
}

package_unity() {
  pkgdesc="A desktop experience designed for efficiency of space and interaction"
  depends=("unity-common=$pkgver" 'compiz-core-dev' 'bamf' 'libunity-misc>=0.4.0' 'unity-asset-pool' 'gnome-session' 'gnome-settings-daemon')
  optdepends=('indicator-application: take menus from applications and place them in the panel'
              'indicator-appmenu: host the menus from an application'
              'indicator-datetime: a very, very simple clock'
              'indicator-messages: a place on the users desktop that collects messages that need a response'
              'indicator-power: show the power status of your devices'
              'indicator-session: change your status, switch users'
              'indicator-sound: a unified sound menu'
              'unity-lens-applications: exposes your applications with their usage statistics and status'
              'unity-lens-files: exposing your files and file history'
              'unity-lens-music: music, in the dash')
  install=unity.install

  cd "$srcdir/$pkgbase-$pkgver/build"

  mv "$srcdir"/unity/* "$pkgdir"

  # Install gconf schema
  mkdir -p "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/schemas/compiz-unity.schemas \
        "$srcdir"/unity-gconf/usr/share/gconf/schemas/*.schemas
  sed -i 's|<schemalist/>||' "$pkgdir"/usr/share/gconf/schemas/compiz-unity.schemas

  # Install Unity Compiz profile
  install -Dm644 "$srcdir/unity.ini" "$pkgdir/etc/compizconfig/unity.ini"
  install -Dm755 "$srcdir/unity.sh" "$pkgdir/etc/profile.d/unity.sh"

  # Install session and desktop files
  install -Dm644 "$srcdir/unity.session" "$pkgdir/usr/share/gnome-session/sessions/unity.session"
  install -Dm644 "$srcdir/unity.desktop" "$pkgdir/usr/share/xsessions/unity.desktop"
}

depends=('nux>=1.4.0-2' 'libunity' 'libindicator' 'compiz-core-dev' 'bamf' 'libunity-misc>=0.4.0' 'unity-asset-pool' 'gnome-session' 'gnome-settings-daemon')
true && depends=()
