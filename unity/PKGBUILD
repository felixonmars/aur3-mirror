# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=unity
pkgver=6.12.0
pkgrel=2
pkgdesc="Desktop experience designed for efficiency of space and interaction"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
depends=('compiz-devel' 'nux' 'bamf' 'libindicator3' 'libunity-misc' 'json-glib' 'libunity' 'libzeitgeist' 'unity-asset-pool')
makedepends=('cmake' 'libxslt' 'mesa' 'boost')
optdepends=('indicator-application: take menus from applications and place them in the panel'
            'indicator-appmenu: host the menus from an application'
            'indicator-datetime: a very, very simple clock'
            'indicator-messages: a place on the users desktop that collects messages that need a response'
            'indicator-power: show the power status of your devices'
            'indicator-printers: show active print jobs'
            'indicator-session: change your status, switch users'
            'indicator-sound: a unified sound menu'
            'indicator-sync: aggregate services performing background synchronization'
            'unity-lens-applications: exposes your applications with their usage statistics and status'
            'unity-lens-files: exposing your files and file history'
            'unity-lens-gwibber: microblogging lens for Unity'
            'unity-lens-music: music, in the dash'
            'unity-lens-photos: all your photos, in the Dash'
            'unity-lens-shopping: gives you more suggestions when searching Unity'
            'unity-lens-video: lens for Unity to search local and remote videos'
            'unity-scope-gdocs: allows you to browse and search your Google Documents'
            'unity-scope-video-remote: Videos Lens scope for online sources')
install=unity.install
source=(http://launchpad.net/unity/6.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        disable-pointerbarrier.patch
        fix-build.patch
        default-settings.patch
        unity.session
        unity.desktop
        unity.sh
        gnome-fallback-mount-helper-unity.desktop
        gnome-screensaver-unity.desktop)
md5sums=('6a7f3d7f7629d1deb5f0662314c6022d'
         '0a4606b775f0e6f514d77b95b1e0c408'
         '9ea2105ab95bc9f0c0d1933b9423453e'
         '7f1228776597800fc71a7eb6ef058e0d'
         'ca28339d142119028070fc3c9ca41397'
         'fe17963c172e71d68704dfa4bb04bea4'
         '05abccd5c803b66a5e355130e3e91e6b'
         'a2fd9ad2ccf2658ede4c56fd389acd2f'
         '3bdfb8c7b40014b17d8a46f5b52a0c66')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/{unity.cmake,makebootchart.py}

  # Disable PointerBarrier
  patch -Np1 -i "$srcdir/disable-pointerbarrier.patch"

  # Fix build
  patch -Np1 -i "$srcdir/fix-build.patch"

  # Set some default settings
  patch -Np1 -i "$srcdir/default-settings.patch"

  # Use unity session instead of ubuntu
  sed -i '/COMPIZ_CONFIG_PROFILE/ s/ubuntu/unity/' tools/unity.cmake

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DUSE_GSETTINGS=TRUE \
           -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
           -DCOMPIZ_PACKAGING_ENABLED=TRUE \
           -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
           -DGETTEXT_FOUND=TRUE
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  # Install session and desktop files
  install -Dm644 "$srcdir/unity.session" "$pkgdir/usr/share/gnome-session/sessions/unity.session"
  install -Dm644 "$srcdir/unity.desktop" "$pkgdir/usr/share/xsessions/unity.desktop"

  # Install compiz profile configuration file
  install -Dm755 "$srcdir/unity.sh" "$pkgdir/etc/profile.d/unity.sh"

  # Install XDG autostart files for some required components
  install -Dm644 "$srcdir/gnome-fallback-mount-helper-unity.desktop" "$pkgdir/etc/xdg/autostart/gnome-fallback-mount-helper-unity.desktop"
  install -Dm644 "$srcdir/gnome-screensaver-unity.desktop" "$pkgdir/etc/xdg/autostart/gnome-screensaver-unity.desktop"
}
