# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=unity
pkgname=unity
true && pkgname=(unity-common unity)
pkgver=4.24.0
pkgrel=2
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
makedepends=('nux>=1.4.0-2' 'libunity' 'libindicator3' 'compiz-core-dev' 'bamf' 'libunity-misc>=0.4.0' 'libnotify' 'gnome-desktop' 'cmake' 'boost' 'intltool' 'glproto' 'dri2proto')
source=(http://launchpad.net/$pkgbase/4.0/$pkgver/+download/$pkgbase-$pkgver.tar.bz2
        bzr-fixes.patch
        disable-multitouch.patch
        disable-tests.patch
        unity.session
        unity.desktop)
md5sums=('dd804fc0e0d08b7bf7da7fc2d0fbc53a'
         'cf5d7ee3d267dddede0e98a64043d2ec'
         '4ac83fa16eff01b836bca357b6094f0f'
         '1d4322e39361d2dc412e5350e14b629f'
         'e0b1d90fc06601ccc57fd49827266f9d'
         'fe17963c172e71d68704dfa4bb04bea4')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  patch -Np1 -i "$srcdir/bzr-fixes.patch"
  patch -Np1 -i "$srcdir/disable-multitouch.patch"
  patch -Np1 -i "$srcdir/disable-tests.patch"

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=Release -DCOMPIZ_PLUGIN_INSTALL_TYPE=package -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_unity-common() {
  pkgdesc="Shared files between unity and unity-2d"
  depends=('nux>=1.4.0-2' 'libunity' 'libindicator3' 'dconf')
  install=unity-common.install

  cd "$srcdir/$pkgbase-$pkgver/build"

  make DESTDIR="$pkgdir/" install

### Split unity files
  [[ -d $srcdir/unity ]] && rm -r "$srcdir/unity/"
  [[ -d $srcdir/unity-gconf ]] && rm -r "$srcdir/unity-gconf/"
  mkdir -p "$srcdir"/unity{,-gconf}/usr/{lib,share/{glib-2.0/schemas,man/man1}}

  mv {"$pkgdir","$srcdir/unity"}/usr/bin
  mv {"$pkgdir","$srcdir/unity"}/usr/lib/compiz
  mv {"$pkgdir","$srcdir/unity"}/usr/share/man/man1/unity.1
  mv {"$pkgdir","$srcdir/unity"}/usr/share/ccsm
  mv {"$pkgdir","$srcdir/unity"}/usr/share/compiz
  mv {"$pkgdir","$srcdir/unity-gconf"}/usr/share/gconf
  mv "$pkgdir"/usr/share/glib-2.0/schemas/org.freedesktop.compiz.*.gschema.xml "$srcdir/unity/usr/share/glib-2.0/schemas"
  mv {"$pkgdir","$srcdir/unity"}/usr/share/locale
}

package_unity() {
  pkgdesc="A desktop experience designed for efficiency of space and interaction"
  depends=("unity-common=$pkgver" 'compiz-core-dev' 'bamf' 'libunity-misc>=0.4.0' 'unity-asset-pool' 'gnome-session' 'gnome-settings-daemon')
  optdepends=('indicator-application: take menus from applications and place them in the panel'
              'indicator-appmenu: host the menus from an application'
              'indicator-datetime: a very, very simple clock'
              'indicator-messages: a place on the users desktop that collects messages that need a response'
              'indicator-power: show the power status of your devices'
              'indicator-session: change your status, switch users'
              'indicator-sound: a unified sound menu'
              'unity-lens-applications: exposes your applications with their usage statistics and status'
              'unity-lens-files: exposing your files and file history'
              'unity-lens-music: music, in the dash')
  install=unity.install

  cd "$srcdir/$pkgbase-$pkgver/build"

  mv "$srcdir"/unity/* "$pkgdir"

  # Install gconf schema
  mkdir -p "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/schemas/compiz-unity.schemas \
        "$srcdir"/unity-gconf/usr/share/gconf/schemas/*.schemas
  sed -i 's|<schemalist/>||' "$pkgdir"/usr/share/gconf/schemas/compiz-unity.schemas

  # Install session and desktop files
  install -Dm644 "$srcdir/unity.session" "$pkgdir/usr/share/gnome-session/sessions/unity.session"
  install -Dm644 "$srcdir/unity.desktop" "$pkgdir/usr/share/xsessions/unity.desktop"
}

depends=('nux>=1.4.0-2' 'libunity' 'libindicator' 'compiz-core-dev' 'bamf' 'libunity-misc>=0.4.0' 'unity-asset-pool' 'gnome-session' 'gnome-settings-daemon')
true && depends=()
