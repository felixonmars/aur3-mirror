# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=unity
pkgname=unity
true && pkgname=(unity libunity-core)
pkgver=4.24.0
pkgrel=1
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
makedepends=('compiz-core-dev' 'nux>=1.4.0-2' 'bamf' 'libindicator3' 'libunity-misc>=0.4.0' 'gconf' 'utouch-geis' 'libnotify' 'gnome-desktop' 'libunity>=4.0.0' 'unity-asset-pool' 'dconf' 'cmake' 'boost' 'glproto' 'dri2proto')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/4.0/$pkgver/+download/$pkgname-$pkgver.tar.bz2
        unity.session
        unity.desktop)
md5sums=('dd804fc0e0d08b7bf7da7fc2d0fbc53a'
         'e0b1d90fc06601ccc57fd49827266f9d'
         'fe17963c172e71d68704dfa4bb04bea4')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_BUILD_TYPE=Release -DCOMPIZ_PLUGIN_INSTALL_TYPE=package -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_unity() {
  pkgdesc="A desktop experience designed for efficiency of space and interaction"
  depends=("libunity-core=$pkgver" 'compiz-core-dev' 'bamf' 'libindicator3' 'libunity-misc>=0.4.0' 'gconf' 'utouch-geis' 'libnotify' 'gnome-desktop' 'libunity>=4.0.0' 'unity-asset-pool' 'dconf')
  optdepends=('indicator-application: take menus from applications and place them in the panel'
              'indicator-appmenu: host the menus from an application'
              'indicator-datetime: a very, very simple clock'
              'indicator-messages: a place on the users desktop that collects messages that need a response'
              'indicator-power: show the power status of your devices'
              'indicator-session: change your status, switch users'
              'indicator-sound: a unified sound menu'
              'unity-lens-applications: exposes your applications with their usage statistics and status'
              'unity-lens-files: exposing your files and file history'
              'unity-lens-music: music, in the dash')

  cd "$srcdir/$pkgbase-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  # Remove libunity-core
  rm -r "$pkgdir"/usr/{include,lib/{pkgconfig,unity/unity-panel-service,libunity-core-4.0.so*},share/dbus-1}

  # Install session and desktop files
  install -Dm644 "$srcdir/unity.session" "$pkgdir/usr/share/gnome-session/sessions/unity.session"
  install -Dm644 "$srcdir/unity.desktop" "$pkgdir/usr/share/xsessions/unity.desktop"
}

package_libunity-core() {
  cd "$srcdir/$pkgbase-$pkgver/build"
  pkgdesc="This package contains shared dbus service and libraries to be used by unity-3d and unity-2d"
  depends=('nux>=1.4.0-2' dee)

  make -C services DESTDIR="$pkgdir/" install
  make -C UnityCore DESTDIR="$pkgdir/" install
}

pkgdesc="A desktop experience designed for efficiency of space and interaction"
depends=('nux>=1.4.0-2' dee 'compiz-core-dev' 'bamf' 'libindicator' 'libunity-misc>=0.4.0' 'gconf' 'utouch-geis' 'libnotify' 'gnome-desktop' 'libunity>=4.0.0' 'unity-asset-pool' 'dconf')
true && depends=()
