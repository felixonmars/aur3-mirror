# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=unity
pkgname=unity
true && pkgname=(unity-common unity)
pkgver=5.0.0
pkgrel=1
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
makedepends=('nux' 'libunity' 'libindicator3' 'compiz-core-devel' 'bamf' 'libunity-misc' 'json-glib' 'libnotify' 'gnome-desktop' 'libgdu' 'cmake' 'boost' 'intltool' 'glproto' 'dri2proto')
source=(http://launchpad.net/$pkgbase/5.0/$pkgver/+download/$pkgbase-$pkgver.tar.bz2
        launchpad-export.tar.gz
        nautilus-home.desktop
        make-tests-optional.patch
        disable-multitouch.patch
        use-unity-profile.patch
        default-settings.patch
        unity.ini
        unity.sh
        unity.session
        unity.desktop
        unity.xml.defaults)
md5sums=('e41f5991ca0cdaac73f5c807a671b374'
         'abba8f4ebdcc90983633248ebab361e1'
         '236c7d9173d65adc704b953d77724739'
         'dfe8f2933a88c387f07ce2aa0794fa21'
         '9c5cbf40364615ea578bf8abb82ff837'
         'cd19df00c59f647b1ae8b9d8cfd94c24'
         '8a8562a167973519666efceaf06d05a0'
         'd954f6aba40501f4baeec94247aefef4'
         '00713cf28268247a582f7bf1fcf1fc20'
         'e0b1d90fc06601ccc57fd49827266f9d'
         'fe17963c172e71d68704dfa4bb04bea4'
         'a4d4fd820a458962faf07238d613ffbe')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/*

  # Disable multitouch support
  patch -Np1 -i "$srcdir/disable-multitouch.patch"
  rm plugins/unityshell/src/{GeisAdapter,GestureEngine}.{h,cpp}

  # Make tests optional
  patch -Np1 -i "$srcdir/make-tests-optional.patch"

  # Use unity profile
  patch -Np1 -i "$srcdir/use-unity-profile.patch"

  # Set some default settings
  patch -Np1 -i "$srcdir/default-settings.patch"

  # Install updated language files
  echo 'ace af am an ar as ast az be bem bg bn bo br bs ca ca@valencia crh cv cy cs da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fo fr fur fy gd gl gu gv he hi hr ht hu hy id is it ja ka kk km kn ko ku ky lb lt ltg lv mg mhr mk ml mr ms my nb ne nl nn oc or os pa pl pmy pt pt_BR ro ru rw sc sd si sk sl sq sr sv sw ta te th ti tl tr tt ug uk ur uz vec vi wae zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgbase- '' ../po/$pkgbase-*.po
  mv -f -t po ../po/*

  # Fix build
  echo "$pkgver" > VERSION

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DCOMPIZ_PACKAGING_ENABLED=ON \
           -DCOMPIZ_BUILD_WITH_RPATH=OFF \
           -DBUILD_TESTS=OFF
  make
}

package_unity-common() {
  pkgdesc="Shared files between unity and unity-2d"
  depends=('nux' 'libunity' 'libindicator3' 'dconf')
  install=unity-common.install

  cd "$srcdir/$pkgbase-$pkgver/build"

  make DESTDIR="$pkgdir/" install

### Split unity files
  [[ -d $srcdir/unity ]] && rm -r "$srcdir/unity/"
  [[ -d $srcdir/unity-gconf ]] && rm -r "$srcdir/unity-gconf/"
  mkdir -p "$srcdir"/unity{,-gconf}/usr/{lib,share/man/man1}

  mv {"$pkgdir","$srcdir/unity"}/usr/bin
  mv {"$pkgdir","$srcdir/unity"}/usr/lib/compiz
  mv {"$pkgdir","$srcdir/unity"}/usr/share/man/man1/unity.1
  mv {"$pkgdir","$srcdir/unity"}/usr/share/ccsm
  mv {"$pkgdir","$srcdir/unity"}/usr/share/compiz
  mv {"$pkgdir","$srcdir/unity-gconf"}/usr/share/gconf
  mv {"$pkgdir","$srcdir/unity"}/usr/share/locale

  # Install Home folder for launcher
  install -Dm644 "$srcdir/nautilus-home.desktop" "$pkgdir/usr/share/applications/nautilus-home.desktop"
}

package_unity() {
  pkgdesc="A desktop experience designed for efficiency of space and interaction"
  depends=("unity-common=$pkgver" 'compiz-decorator-gtk-devel' 'compizconfig-backend-gconf-devel' 'bamf' 'libunity-misc' 'libgdu' 'unity-asset-pool' 'gnome-session' 'gnome-settings-daemon')
  optdepends=('indicator-application: take menus from applications and place them in the panel'
              'indicator-appmenu: host the menus from an application'
              'indicator-datetime: a very, very simple clock'
              'indicator-messages: a place on the users desktop that collects messages that need a response'
              'indicator-power: show the power status of your devices'
              'indicator-session: change your status, switch users'
              'indicator-sound: a unified sound menu'
              'unity-lens-applications: exposes your applications with their usage statistics and status'
              'unity-lens-files: exposing your files and file history'
              'unity-lens-music: music, in the dash')
  install=unity.install

  cd "$srcdir/$pkgbase-$pkgver/build"

  mv "$srcdir"/unity/* "$pkgdir"

  # Install gconf schema
  mkdir -p "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/schemas/compiz-unity.schemas \
        "$srcdir"/unity-gconf/usr/share/gconf/schemas/*.schemas
  sed -i '/<schemalist\/>/d' "$pkgdir"/usr/share/gconf/schemas/compiz-unity.schemas

  # Install Compiz profile and settings
  install -dm755 "$pkgdir"/etc/gconf/unity.xml.defaults
  gconftool-2 --direct --config-source xml:readwrite:"$pkgdir/etc/gconf/unity.xml.defaults" --load "$srcdir/unity.xml.defaults"
  install -Dm644 "$srcdir/unity.ini" "$pkgdir/etc/compizconfig/unity.ini"
  install -Dm755 "$srcdir/unity.sh" "$pkgdir/etc/profile.d/unity.sh"

  # Install session and desktop files
  install -Dm644 "$srcdir/unity.session" "$pkgdir/usr/share/gnome-session/sessions/unity.session"
  install -Dm644 "$srcdir/unity.desktop" "$pkgdir/usr/share/xsessions/unity.desktop"
}

depends=('nux' 'libunity' 'libindicator' 'compiz-devel' 'compizconfig-backend-gconf-devel' 'bamf' 'libunity-misc' 'libgdu' 'unity-asset-pool' 'gnome-session' 'gnome-settings-daemon')
true && depends=()
