# Maintainer: OmeGa <omega at mailoo dot org>

pkgname=nemo-seahorse-git
pkgver=20130103
pkgrel=1
pkgdesc="An extension for Nemo which allows encryption and decryption of OpenPGP files using GnuPG."
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-extensions"
license=('GPL2')
depends=('gconf' 'libcryptui' 'nemo-fm')
makedepends=('intltool' 'git')
options=('!libtool')
install=$pkgname.install

_gitroot="git://github.com/linuxmint/nemo-extensions.git"
_gitname="nemo-extensions"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server..."
  if [[ -d $_gitname ]]; then
    (cd $_gitname && git pull origin)
    msg "The local files are updated."
  else
    git clone --depth=1 $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout."
  msg "Starting make..."

  [[ -d $_gitname-build ]] && \
    rm -rf $_gitname-build
  cp -R $_gitname $_gitname-build
  cd $_gitname-build/nemo-seahorse

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "$_gitname-build/nemo-seahorse"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
