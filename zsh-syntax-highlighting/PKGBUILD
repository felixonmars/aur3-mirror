# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=zsh-syntax-highlighting
pkgver=0.1.2
pkgrel=1
pkgdesc="Fish shell like syntax highlighting for Zsh."
arch=(any)
url="https://github.com/zsh-users/zsh-syntax-highlighting"
license=(BSD)
depends=("zsh>=4.3.9")
install=${pkgname}.install
changelog=Changelog
conflicts=(${pkgname}-git)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/zsh-users/${pkgname}/tarball/${pkgver}")
md5sums=('9d738b3a07d82b1c3d9651961dd95bce')

package() {
  msg2 "Install scripts at /usr/share/zsh/plugins/${pkgname}..."
  install -m0755 -d "${pkgdir}/usr/share/zsh/plugins/${pkgname}"
  cp -r "${srcdir}"/zsh-users-${pkgname}-*/* "${pkgdir}/usr/share/zsh/plugins/${pkgname}"

  msg2 "Install links to the documentation resources at /usr/share/doc/${pkgname}..."
  install -m0755 -d "${pkgdir}/usr/share/doc"
  ln -s /usr/share/zsh/plugins/${pkgname} "${pkgdir}/usr/share/doc/${pkgname}"

  msg2 "Install links to the license resources at /usr/share/licenses/${pkgname}..."
  install -m0755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/zsh/plugins/${pkgname}/COPYING.md "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
