# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=referencer-bzr
pkgver=911
pkgrel=2
pkgdesc="a Gnome application to organise documents or references, and \
         ultimately generate a BibTeX bibliography file"
arch=('i686' 'x86_64')
url="http://icculus.org/referencer/index.html"
license="GPL"
makedepends=(   'autoconf'
                'automake' 
                'bzr'
                'pkgconfig'
                'glib2'
                'intltool'
                'gnome-common'
                'gnome-doc-utils'
                'gnome-icon-theme')
depends=(       'poppler-glib'
                'libgnomeuimm'
                'yelp'
                'boost'
                'python2>=2.5'
                'gtkmm-utils')
conflicts=('referencer' 'referencer-svn' 'referencer-hg')
replaces=('referencer-svn' 'referencer-hg')
provides=referencer
install=referencer.install
source=()
md5sums=()

_bzrtrunk="https://code.launchpad.net/~referencer-devs/referencer/trunk"
_bzrmod="referencer"

# # # # #

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr --no-plugins pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  #
  # BUILD HERE
  #
  msg "Starting autogen.sh..."
  ./autogen.sh --prefix="/usr" --disable-update-mime-database
  PYTHON="/usr/bin/python2" ./configure --prefix="/usr" --disable-update-mime-database \
    --enable-python
  sed -i 's|$(MAKE) $(AM_MAKEFLAGS) install-data-hook||' data/Makefile
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
