# Maintainer: loonyphoenix <loonyphoenix at gmail dot com>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: Army <uli.armbruster@gmail.com>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: dale <dale@archlinux.org>

# This package is a modified ttf-ms-fonts without Arial, Times New Roman and Courier New.
# These fonts have free metric substitutes, which are Liberation Sans, Liberation Serif and 
# Liberation Mono. This package depends upon the ttf-liberation package that contains them.

pkgname=ttf-ms-webfonts
pkgver=2.0
pkgrel=2
pkgdesc="Core TTF Fonts from Microsoft minus Arial, Times New Roman and Courier New"
arch=('any')
url="http://corefonts.sourceforge.net/"
license=('custom:microsoft')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils' 'ttf-liberation')
makedepends=('cabextract')
conflicts=('ttf-ms-fonts')
provides=('ttf-ms-fonts')
install=$pkgname.install
_sfpath="http://downloads.sourceforge.net/corefonts"
source=(${_sfpath}/andale32.exe ${_sfpath}/comic32.exe  ${_sfpath}/georgi32.exe
        ${_sfpath}/impact32.exe ${_sfpath}/trebuc32.exe ${_sfpath}/verdan32.exe
        ${_sfpath}/webdin32.exe
        LICENSE)
md5sums=('cbdc2fdd7d2ed0832795e86a8b9ee19a'
         '2b30de40bb5e803a0452c7715fc835d1'
         '4d90016026e2da447593b41a8d8fa8bd'
         '7907c7dd6684e9bade91cff82683d9d7'
         '0d7ea16cac6261f8513a061fbfcdb2b5'
         '12d2a75f8156e10607be1eaa8e8ef120'
         '230a1d13a365b22815f502eb24d9149b'
         '7a760fc266e256421078597e47f1af68')

package() {
	install -dm755 ${pkgdir}/usr/share/fonts/TTF
	for font in ${srcdir}/*.exe
	do
		cabextract --lowercase --directory=${pkgdir}/usr/share/fonts/TTF/ ${font} -F '*.ttf'
	done
	install -Dm644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
