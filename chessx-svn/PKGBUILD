# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: yescalona <yescalona@ug.uchile.cl>

_pkgname=chessx
pkgname=$_pkgname-svn
pkgver=1185
pkgrel=1
pkgdesc="A free chess database"
arch=(i686 x86_64)
url=http://chessx.sourceforge.net
license=(GPL2)
depends=(qt)
makedepends=(subversion)
provides=($_pkgname=0.8)
conflicts=($_pkgname)
options=(!emptydirs)
install=$_pkgname.install
source=($_pkgname.desktop $_pkgname.png)
sha256sums=('3c68442c3a06bcbe9c5cc976c6ba263647e3a49f134b8e731006709bdd9f41a8'
    'b7cfb00a9fd533011529a18e0c0d52deb7b65ae872c68963df7b99116d3f4256')
sha512sums=('1f7837d191b99485fc77736abd0d343773b3170ffe260a3cad5fd7d6a02decdae6d332a70c04a22aa530ef747ba38a8ace898c08fa430a24146389bf5a4649d2'
    'f2e42766b166f56aed4cc65e79c187162b8e15aa22407ccc97a449e8380b33d034458d1318dbaab43e26c90af2f67373ae50d86188609c1b5642204bd247fe41')

_svntrunk=http://$_pkgname.svn.sourceforge.net/svnroot/$_pkgname/trunk
_svnmod=trunk

build() {
    install -d "$srcdir"/$_pkgname/
    cd "$srcdir"/$_pkgname/

    msg "Starting SVN checkout..."
    if [[ -d $_svnmod/.svn ]]; then
        pushd $_svnmod && svn up -r $pkgver
        msg "The local files have been updated."
        popd
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi
    msg "SVN checkout done or server timeout"

    rm -rf $_svnmod-build
    cp -r $_svnmod $_svnmod-build
    cd $_svnmod-build

    msg "Compiling..."

    sed -i "s:qm:ts:g" resources.qrc
    qmake
    make
}

package() {
    cd "$srcdir"
    desktop-file-install $_pkgname.desktop --dir "$pkgdir"/usr/share/applications/

    install -Dm644 $_pkgname.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png

    install -d "$pkgdir"/usr/bin/
    ln -sf /usr/chessx/bin/chessx "$pkgdir"/usr/bin/chessx

    cd $_pkgname/$_svnmod-build/
    install -d "$pkgdir"/usr/$_pkgname
    cp -R bin/ data/ i18n/  "$pkgdir"/usr/$_pkgname
}
