# Maintainer: Patrick McCarty <pnorcks at gmail dot com>

pkgname=guile-git
pkgver=20120807
pkgrel=1
pkgdesc="A portable, embeddable Scheme implementation (Git snapshot)"
url="http://www.gnu.org/software/guile/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gc' 'gmp>=4.3.1' 'gperf' 'libffi' 'libtool'
         'libunistring' 'ncurses>=5.7' 'texinfo')
provides=('guile')
conflicts=('guile' 'guile-devel')
options=('!strip')
source=()
md5sums=()

_gitroot="git://git.sv.gnu.org/guile.git"
_gitname="guile"

build() {
  cd "$srcdir"
  msg "Connecting to git.sv.gnu.org..."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -R "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  ./configure --prefix=/usr --disable-error-on-warning
  make LDFLAGS+="-lpthread"
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
