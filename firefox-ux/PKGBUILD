# Maintainer: Isaac Aggrey <isaac.aggrey@gmail.com>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>
# Contributor: Luis von Bernus <PaterSiul@gmail.com>
# Contributors: L42y, aeosynth, Dan Serban, Kalipath

_name=firefox
_channel=ux
pkgname="${_name}-${_channel}"
pkgver=26.0a1.20130807040232
_version=26.0a1
pkgdesc='Standalone web browser from Mozilla, UX nightly build'
pkgrel=1
url='https://people.mozilla.com/~jwein/ux-nightly/'
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
_file="${_name}-${_version}.en-US.linux-${CARCH}"
_srcurl="http://ftp.mozilla.org/pub/mozilla.org/firefox/nightly/latest-ux"
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 
         'hunspell' 'libevent' 'libnotify' 'libvpx' 'libxt' 'libxt' 'mime-types' 
         'mozilla-common' 'nss' 'sqlite' 'startup-notification')
source=("${_srcurl}/${_file}.tar.bz2"
        "${pkgname}.desktop"
        "${pkgname}-safe.desktop")
_srcsum="$(curl -vs "${_srcurl}/${_file}.checksums" 2>&1 | grep bz | grep sha512 | cut -d " " -f1)"
sha512sums=("${_srcsum}"
            "a29cf51c22f02fb350364047a3f47dbcef419f2592ef6e1f759d51e8bdf2f7eb2244bf9800e3d2d0e4a2127f13953b3ca2616370fa84d37697711de0a46fc24f" 
            "ca99fab33dd26d0762372d9b5f38a0d37b391650a201a6f84406b3ccc5efe884fb1b577d3ebf38c2fa7e8b147a22140331647d81c7a81e6b399f45600bba49aa")

pkgver() {
   cd "${_name}"
   echo "${_version}.$(cat platform.ini | grep BuildID | cut -d "=" -f2 )"
}

package() {
    install -d "${pkgdir}"/{usr/{bin,share/applications},opt}
    mv firefox "${pkgdir}/opt/${pkgname}"
    ln -s "/opt/${pkgname}/firefox" "${pkgdir}/usr/bin/${_name}-${_channel}"
    install -Dm644 "${srcdir}"/{firefox-ux.desktop,firefox-ux-safe.desktop} \
                   "${pkgdir}/usr/share/applications/"
    install -Dm644 "${pkgdir}/opt/${pkgname}/browser/icons/mozicon128.png" \
                   "${pkgdir}/usr/share/pixmaps/${pkgname}-icon.png"
}
