# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=wine-menus
pkgver=0.b1
pkgrel=1
pkgdesc="Wine configuration tools in Wine Menu (beta-test)"
arch=('any')
url="http://www.winehq.org/"
license=('GPL')
depends=('wine')
source=(wine-utilities.menu
        wine-wine.directory
        winecfg.desktop
        winecontrol.desktop
        wineuninstaller.desktop)
sha256sums=('5e7b0afad9acb41454e1d63d97d273c254b8975f8d7e7abe42b8212b27d90171'
            '9ccdf625e580b2481c089ff7e551ea19b35fd0f4844139354f33af2e95bfddf1'
            'e3b44a0741ddd81391213f430cb0469ea4bb747a74d8d34220babb01f36ac2cc'
            '8330091bb3051569cf8f980d2d16dabee6321be67d80065d605f5052bc03b715'
            'a9ed2405d20ee88ca4e1872554cf31641a61e16bffeb6963e2cda72d9907f91f')

package() {
  # Configuration for system applications.menu (new "Wine" category)
  install -dm755 "$pkgdir/etc/xdg/menus/applications-merged/"
  install -m644  "$srcdir/wine-utilities.menu" \
                 "$pkgdir/etc/xdg/menus/applications-merged/"

  # Configuration for menu directory (icon/name/etc)
  install -dm755 "$pkgdir/usr/share/desktop-directories/"
  install -m644  "$srcdir/wine-wine.directory" \
                 "$pkgdir/usr/share/desktop-directories/"

  # Few new entries to be inside this menu
  install -dm755 "$pkgdir/usr/share/applications/"
  for i in "${source[@]}"; do
    if [ "${i##*.}" = "desktop" ]; then
      install -m644 "$srcdir/$i" "$pkgdir/usr/share/applications/$i"
    fi
  done
}
