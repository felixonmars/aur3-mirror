# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gnulib
pkgver=20120422
pkgrel=1
pkgdesc="GNU Portability Library"
arch=('any')
url="http://www.gnu.org/software/gnulib"
license=('custom')
makedepends=('git')
install=

_gitroot="git://git.savannah.gnu.org/gnulib.git"
_gitname="gnulib"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i "/^[ ]*gnulib_dir=/s#\`[^\`]*\`#/usr/share/$pkgname#" gnulib-tool
  make info
}

package() {
  cd "$srcdir/$_gitname-build"
  install -dm755 $pkgdir/usr/bin
  install -m755 -t $pkgdir/usr/bin check-module gnulib-tool

  dir=$(find build-aux lib m4 modules top -type d)

  for i in $dir; do
    install -dm755 $pkgdir/usr/share/$pkgname/$i
    install -m644 -t $pkgdir/usr/share/$pkgname/$i $(find $i -maxdepth 1 -type f) 
  done

  install -Dm644 doc/gnulib.info $pkgdir/usr/share/info/gnulib.info
  install -Dm644 COPYING $pkgdir/usr/share/licenses/${pkgname}/COPYING
} 
