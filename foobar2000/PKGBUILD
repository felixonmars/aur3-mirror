# Maintainer: Ryan Roper <ryan dot roper at gmail dot com>
pkgname=foobar2000
pkgver=1.3.4
pkgrel=1
pkgdesc="An advanced freeware audio player (uses Wine)."
arch=(i686 x86_64)
url="http://www.foobar2000.org/"
license=('custom')
depends=(wine desktop-file-utils)
makedepends=(unarchiver)
source=("https://dl.dropboxusercontent.com/u/8031923/${pkgname}_v${pkgver}.exe"
        "LICENSE"
        "${pkgname}.sh"
        "${pkgname}.png"
        "${pkgname}.desktop")
install="${pkgname}.install"
md5sums=('dcf56a41136525d642219b8b74bed35d'
         '91873c4c3115bdd1afcf52c3023dc896'
         '6e2884a32aba29c9e028e09d4817489b'
         '927c185d2f680b44faf1a47f528905c7'
         '138cb528cb0cad4e2764c3c77555369c')

package() {

  # unpack NSIS installer .exe into destination
  unar -q -D -e windows-1252 -o "${pkgdir}/usr/share/${pkgname}" "${srcdir}/${pkgname}_v${pkgver}.exe"

  # remove NSIS installer directory & unneeded files in portable mode
  rm -fr "${pkgdir}/usr/share/${pkgname}/NSIS Plugins Directory" \
            "${pkgdir}/usr/share/${pkgname}/icons" \
            "${pkgdir}/usr/share/${pkgname}/foobar2000 Shell Associations Updater.exe" \
            "${pkgdir}/usr/share/${pkgname}/Register R0"

  # run foobar2000 in portable mode
  touch "${pkgdir}/usr/share/${pkgname}/portable_mode_enabled"

  # Install the license
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install the script
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install the icon
  install -Dm644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # Install the .desktop
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
