# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-libffi
pkgver=3.0.13
pkgrel=2
pkgdesc="A portable, high level programming interface to various calling conventions (mingw-w64)"
arch=('any')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!libtool' '!buildflags' '!strip')
license=('MIT')
url="http://sourceware.org/libffi"
source=("ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz"
        "libffi-mingw-w64-compatibility.patch")
md5sums=('45f3b6dbc9ee7c7dfbbbc5feba571529'
         'fc9bff7e45690d609d4fef11e5b4fa3f')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/libffi-${pkgver}"

  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  unset LDFLAGS

  for _arch in ${_architectures}; do
    export PKG_CONFIG_PATH="/usr/${_arch}/lib/pkgconfig/"
    mkdir -p ../build-${_arch} && pushd ../build-${_arch}
    ../libffi-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --enable-shared
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    rm -rf "$pkgdir/usr/${_arch}/share"
    ${_arch}-strip --strip-unneeded $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-unneeded $pkgdir/usr/${_arch}/lib/libffi.dll.a
    ${_arch}-strip --strip-debug $pkgdir/usr/${_arch}/lib/libffi.a
  done
}

# vim:set ts=2 sw=2 et:
