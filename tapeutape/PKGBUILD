# Maintainer: David Adler <david dot jo dot adler at gmail dot com>
pkgname=tapeutape
pkgver=0.1.1
pkgrel=4
pkgdesc="MIDI controllable sampler"
arch=(i686 x86_64)
url="http://hitmuri.net/tardigrade/index.php/Software/Tapeutape"
license=('GPL')
depends=('fltk' 'lash')
makedepends=('scons')
source=(http://www.hitmuri.net/tardigrade/uploads/$pkgname-$pkgver.tar.gz sconstruct_tapeutape.diff)
md5sums=('ac1168e2b93a2ae1a18d583f904c9dd1'
         '239aaa46affe8f3bc3aad090f035f704')

build() {
  cd $srcdir/$pkgname-$pkgver

  # patch SConstruct to allow for DESTDIR & PREFIX
  patch -i ../../sconstruct_tapeutape.diff SConstruct

  # remove object files
  rm src/*/*.o

  # fix bug exposed by gcc 4.7 (undeclared getcwd)
  sed -i 's/#include "sample.h"/&\n#include <unistd.h>/' src/audio/sample.cpp

  scons
}

package() {
  cd $srcdir/$pkgname-$pkgver
  scons DESTDIR=$pkgdir PREFIX=/usr install
}

