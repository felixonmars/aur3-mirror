# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=sunrise-commander-addons  
pkgver=425
pkgrel=1
pkgdesc="Addons for Sunrise Commander"
url="http://www.emacswiki.org/emacs/Sunrise_Commander"
arch=('any')
license=('GPL')
depends=('sunrise-commander')
_emacs_major_version=$(emacs --version | head -1|cut -b 11,12)
source=(http://www.emacswiki.org/emacs/download/sunrise-x-buttons.el \
  http://www.emacswiki.org/emacs/download/sunrise-x-mirror.el \
  http://www.emacswiki.org/emacs/download/sunrise-x-popviewer.el \
  http://www.emacswiki.org/emacs/download/sunrise-x-loop.el \
  http://www.emacswiki.org/emacs/download/sunrise-x-modeline.el \
  http://www.emacswiki.org/emacs/download/sunrise-x-tree.el \
  http://www.emacswiki.org/emacs/download/sunrise-x-checkpoints.el \
  http://www.emacswiki.org/emacs/download/sunrise-x-old-checkpoints.el)
md5sums=('ecd48e6c252b9ef98282f0af09677c70'
         '13998f92ac08971d27543695102fe987'
         'a99d7826533c47423a1aba15cc682b3f'
         '4e98bf38e217a1670d75238e75a457b0'
         '8d767281bde86e08c944b7b10fa15fa9'
         'a67a98c4e70f2f7a4c1e5f5194acf65a'
         'e168688e3b59a8c0fff71f9fee56ccbb'
         'c2477a8fa9162dfc99a943d1bc20b370')

build() {
  cd "$srcdir"
  if [ ${_emacs_major_version} == 22 ]; then
    rm sunrise-x-checkpoints.el
  else
    rm sunrise-x-old-checkpoints.el
  fi
  for _i in *.el
  do
    emacs --batch -q -f batch-byte-compile ${_i}
  done 
}
package() {
  install -d $pkgdir/usr/share/emacs/site-lisp
  cd "$srcdir"
  for _i in *.el
  do
    install ${_i} $pkgdir/usr/share/emacs/site-lisp/${_i}
    install ${_i}c $pkgdir/usr/share/emacs/site-lisp/${_i}c
  done
}
