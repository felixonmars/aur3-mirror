# Contributor: Bernd Pr√ºnster <bernd.pruenster@gmail.com>
# Based on # $Id: PKGBUILD 52337 2011-07-23 11:20:14Z rvanharen $
_pkgname=compiz-fusion-plugins-main
pkgname=compiz-fusion-plugins-main-genie
pkgver=0.8.8
pkgrel=2
pkgdesc="Compiz Fusion Main plugins patched for genie effect (a.k.a. zero-waves magic lamp)"
arch=('i686' 'x86_64')
url="http://www.compiz.org"
license=('GPL')
install=compiz.install
depends=('compiz-core' 'libjpeg>=7' 'librsvg' 'compiz-bcop' 'libxdamage' 'libxcomposite'
	'libxinerama' 'startup-notification')
makedepends=('intltool' 'pkgconfig' 'gettext')
groups=('compiz-fusion' 'compiz-fusion-kde' 'compiz-fusion-gtk')
conflicts=(compiz-fusion-plugins-main-git ${_pkgname}=${pkgver})
provides=($_pkgname=$pkgver)
source=(http://releases.compiz.org/${pkgver}/compiz-plugins-main-${pkgver}.tar.bz2 magiclamp.patch)
options=(!libtool)
sha1sums=('3c99841b744ae6c4a3edf2019626484880607dc6' 'ec04c326131a8a91404ee2a552b1b45a46ea18ce')

build() {
  cd "${srcdir}/compiz-plugins-main-${pkgver}"
  echo "patching magic lamp effect for zero-waves"
  patch -Np0 -i $srcdir/magiclamp.patch
  CPPFLAGS="$CPPFLAGS -I/usr/include/compiz -I/usr/include/startup-notification-1.0 \
        -I/usr/include/libxml2" ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "${srcdir}/compiz-plugins-main-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
