pkgname=qxmpp-git
pkgver=20120514
pkgrel=1
pkgdesc="A cross-platform C++ XMPP client library based on Qt"
arch=('i686' 'x86_64')
url="https://code.google.com/p/qxmpp/"
license=('LGPL')
depends=('qt>=4.5')

_gitroot='https://code.google.com/p/qxmpp/'
_gitname='qxmpp'
_buildir="$_gitname-build"

_optimal_make_jobs() {
	if [ -r /proc/cpuinfo ]; then
		local core_count=$(grep -Fc processor /proc/cpuinfo)
	else
		local core_count=0
	fi

	if [ $core_count -gt 1 ]; then
		echo -n $[$core_count-1]
	else
		echo -n 1
	fi
}

build() {
	cd "$srcdir"

	msg 'Connecting to GIT server...'
	if [ -d "$_gitname" ]; then
		cd "$_gitname" && git pull origin
		cd ..
	else
		git clone "$_gitroot"
	fi

	msg 'GIT checkout done or server timeout.'

	if [ -d "$_buildir" ]; then
		msg 'Cleaning previous build...'
		rm -rf "$_buildir"
	fi

	git clone "$_gitname" "$_buildir"
	cd "$_buildir"

	msg 'Running qmake...'
	qmake PREFIX=/usr

	msg 'Running make...'
	make -j$(_optimal_make_jobs)
}

package() {
	cd "$srcdir/$_buildir"
	make INSTALL_ROOT="$pkgdir" install
	rm -rf "$srcdir/$_buildir"
}
