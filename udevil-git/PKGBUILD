# Maintainer: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/
pkgname="udevil-git"
pkgver=20120615
pkgrel=1
pkgdesc="Mount without password - includes devmon - git next branch"
arch=('i686' 'x86_64')
url=("http://ignorantguru.github.com/udevil/")
license=('GPL3')

makedepends=('intltool' 'gettext')
depends=('udev' 'glib2')
# See devmon dependency table:
#     http://igurublog.wordpress.com/downloads/script-devmon/#withtable
optdepends=('nfs-utils:  mount nfs shares'
            'sshfs:      mount sftp shares'
            'curlftpfs:  mount ftp shares'
            'cifs-utils: mount samba shares'
            'eject:      eject via devmon'
            'zenity:     devmon popups'
            'pmount:     devmon mount without suid udevil'
            'udisks:     devmon mount without suid udevil'
            'udisks2:    devmon mount without suid udevil')
conflicts=('udevil' 'devmon' 'devmon-git')
provides=('udevil' 'devmon')
backup=('etc/udevil/udevil.conf')

_gitroot="git://github.com/IgnorantGuru/udevil.git"
_gitname="udevil"

# Set USE_GDB=1 to build for gdb debugging, otherwise set USE_GDB=0
USE_GDB=1

if (( USE_GDB == 1 )); then
    options=('!strip')
fi

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ] ; then
        cd $_gitname && git pull origin next
        msg "The local files are updated."
    else
        git clone -b next $_gitroot $_gitname
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
    cd "$srcdir/$_gitname-build"

    if (( USE_GDB == 1 )); then
        CFLAGS+="-g" ./configure --prefix=/usr
        echo
        CFLAGS+="-g" make
    else
        ./configure --prefix=/usr
        echo
        make
    fi
    echo
    msg "REMEMBER to enable kernel polling when using devmon without udisks:"
    msg "         http://ignorantguru.github.com/udevil/#polling"
    echo
}

package() {
    cd "${srcdir}/$_gitname-build"
    echo
    make DESTDIR="${pkgdir}/" install
    echo
}

