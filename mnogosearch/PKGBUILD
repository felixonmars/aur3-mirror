# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: Martin Devera <devik@cdi.cz>

pkgname=mnogosearch
pkgver=3.3.12
pkgrel=1
pkgdesc="Search Engine Software"
arch=(i686 x86_64)
url=http://www.mnogosearch.org
license=(GPL2)
depends=(libmysqlclient)
makedepends=(mysql)
backup=(etc/$pkgname/{{indexer,langmap,stopwords}.conf,search.htm})
source=($url/Download/$pkgname-$pkgver.tar.gz)
options=(!emptydirs !libtool)
sha256sums=('c7f878d2637e7cf13b6ed8b8670c9e126be35c364cc99cf36f80b6eee2a67ae4')
sha512sums=('b4f1a713db254fbae49681d72dae191d84ab724dbd47933cdeec926b557ec15dfbdcc154f58e868cd4e112fa1493a84dbf1594562bcfe55fbf9c9a702334a51e')

build() {
    cd "$srcdir"/$pkgname-$pkgver/
    ./configure --prefix=/usr \
        --sysconfdir=/etc/$pkgname \
        --datadir=/usr/share/$pkgname \
        --localstatedir=/var \
        --with-mysql
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    make DESTDIR="$pkgdir" install

    cd "$pkgdir"
    mv usr/doc/ usr/share/doc/
    for f in ${backup[@]};do
        install -Dm644 $f-dist $f
    done
}
