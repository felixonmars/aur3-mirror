# Maintainer: roger <roger@rogerpc.com.ar>

pkgname=qtile-git
pkgver=1486.623cd31
pkgrel=1
pkgdesc="A full-featured, pure-Python tiling window manager. (git version)"
arch=('i686' 'x86_64')
url="http://www.qtile.org"
license=('MIT')
depends=('python2' 'cairo>=1.12' 'pycairo-xcb-git' 'pygtk' 'xorg-xpyb-git')
makedepends=('python2-distribute')
provides=('qtile=0.6')
conflicts=('qtile')
install=${pkgname}.install
source=('git://github.com/qtile/qtile.git')
md5sums=('SKIP')

_gitname="qtile"

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_gitname
  # license
  msg "Copying license..."
  install -D -m 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  msg "Copying default config..."
  install -D -m 644 $srcdir/$_gitname/libqtile/resources/default_config.py $pkgdir/usr/share/doc/$pkgname/default_config.py

  # install
  msg "Running setup.py"
  python2 setup.py install --root=${pkgdir} --prefix=/usr
}
