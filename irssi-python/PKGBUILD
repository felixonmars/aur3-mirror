# Maintainer: Trashlord <dornenreich666 at gmail dot com>

pkgname=irssi-python
pkgver=test3
pkgrel=2
pkgdesc="Provides Python scripting support for Irssi"
url="http://irssi.org/"
arch=('x86_64' 'i686')
install='irssi-python.install'
depends=('python2')
license=('GPL2')

source=('http://irssi.org/files/irssi-0.8.15.tar.gz' 
        'https://github.com/downloads/danielrichman/irssi_rstatus/irssi-python-ac.tar.gz')
	
md5sums=('870db8e319f640c2bf446c30d0c24ef6'
	 '92f95ae4f6b48e0b661c361fc90e0638')

build() {  
    # The following line makes "configure" look for python2 instead of python,
    # since python on Arch refers to Python 3, which would cause an error.
    export PYTHON_VERSION=2

    # Extract sources
    tar xvf irssi-0.8.15.tar.gz
    tar xvf irssi-python-ac.tar.gz
    
    # Build
    cd "irssi-python"
    ./configure --with-irssi=../irssi-0.8.15 --prefix=/usr
    make -C src constants
    make
}

package() {
    # libpython.so will be installed in /usr/lib/irssi/modules
    # Scripts will be installed in /usr/share/irssi/scripts
    # Documentation will be installed in /usr/share/doc/irssi
    cd "$srcdir/irssi-python/"
    mkdir -p "$pkgdir/usr/share/doc/irssi"
    install -m644 "docs/irssi-python.html" "$pkgdir/usr/share/doc/irssi"
    make DESTDIR="$pkgdir" install
}



