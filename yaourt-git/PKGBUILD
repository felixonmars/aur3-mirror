# Maintainer:  tuxce <tuxce;net@gmail.com>
# Contributor: Julien MISCHKOWITZ <wain@archlinux.fr>

pkgname=yaourt-git
pkgver=20121005
pkgrel=1
pkgdesc="A pacman wrapper with extended features and AUR support"
arch=('any')
url="http://www.archlinux.fr/yaourt-en/"
license=('GPL')
depends=('package-query-git' 'pacman>=4')
makedepends=('git')
optdepends=('aurvote: vote for your favorite AUR packages'
    'customizepkg: automatically modify PKGBUILD during install/upgrade'
    'rsync: retrieve PKGBUILD from official repositories'
    'pacman-color: fully colorized output')
provides=('yaourt')
conflicts=('yaourt')
backup=('etc/yaourtrc')

_gitroot=http://projects.archlinux.fr/yaourt.git
_gitname=yaourt

build() {
  cd "$srcdir"

  if [[ -d $_gitname ]]; then
    cd $_gitname && git pull origin && cd ..
  else
    git clone $_gitroot
  fi

  rm -rf $_gitname-build
  git clone $_gitname $_gitname-build
  cd "$srcdir/$_gitname-build/src"
  make PREFIX=/usr sysconfdir=/etc localstatedir=/var 

}

package() {
  cd "$srcdir/$_gitname-build/src"
  make PREFIX=/usr sysconfdir=/etc localstatedir=/var DESTDIR=$pkgdir install
}
