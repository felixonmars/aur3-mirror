# Mantainer: Franco Tortoriello

pkgname=dosbox-svn-patched
pkgver=3779
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with several patches to increase functionality"
arch=('i686' 'x86_64')
url="http://dosbox.sourceforge.net/"
license=('GPL')
depends=('sdl_net' 'zlib' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'openglide-cvs')
makedepends=('mesa' 'subversion')
provides=('dosbox' 'dosbox-svn')
conflicts=('dosbox' 'dosbox-svn')
source=('dosbox.desktop' dosbox.png::"http://www.dosbox.com/dosbox_wiki2.png"
	# The original patches are from
	# http://sourceforge.net/tracker/?group_id=52551&atid=467234 and the vogons forums
	'glide.patch' 'ne2000.patch' 'beep.patch' 'swapstereo.patch'
	'multiimage.patch' 'ctrlbreak.patch')
md5sums=('7bd1353acaa9b3d50826595b054da292'
	 '71e0f4db01b89d227e1941bfe2716ac5'
	 'dd9b50b74cbaf07b2a6b4bdd4d974400'
	 'd5a5226e87835530a844b31fe869b3f0'
	 '4729dcdeebdb22639facfbacb7a3d454'
	 'be48ff2d296667d0cb4ac4938ac83fc5'
	 '7833f0c92a350c3ac8d3411f4bd9b3a2'
	 '805b4733f71fcb6813287951bc8360ff')

_svntrunk=https://dosbox.svn.sourceforge.net/svnroot/dosbox/dosbox/trunk
_svnmod=dosbox

build() {
  cd "$srcdir"

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  msg "Patching..."
  patch -p1 -i ../glide.patch
  patch -p1 -i ../ne2000.patch
  patch -p1 -i ../beep.patch
  patch -p1 -i ../swapstereo.patch
  patch -p1 -i ../multiimage.patch
  patch -p1 -i ../ctrlbreak.patch

  msg "Starting build..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/dosbox
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

  install -Dm644 README "$pkgdir/usr/share/doc/dosbox/README"
  install -Dm644 docs/README.video "$pkgdir/usr/share/doc/dosbox/README.video"
  install -Dm644 ../dosbox.png "$pkgdir/usr/share/pixmaps/dosbox.png"
  install -Dm644 ../dosbox.desktop "$pkgdir/usr/share/applications/dosbox.desktop"
}
