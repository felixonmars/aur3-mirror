# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=logcheck
pkgver=1.3.13
pkgrel=2
pkgdesc="looks for anomalies in the system logfiles"
license=('GPL')
arch=('i686' 'x86_64')
url="http://packages.qa.debian.org/l/logcheck.html"
depends=('cron' 'shadow' 'grep' 'logger' 'perl>=5.8.0' 'lockfile-progs' 'run-parts' 'mime-construct>=1.11-2')
install="$pkgname.install"
backup=(etc/logcheck/logcheck.conf etc/logcheck/logcheck.logfiles etc/cron.hourly/logcheck)
source=(http://ftp.debian.org/debian/pool/main/l/$pkgname/${pkgname}_${pkgver}.tar.gz
	http://logcheck.org/docs/logcheck.8
	local-ignorefiles.tar.gz
	local-violationsfiles.tar.gz)

build() {
    cd $startdir/src/$pkgname

    make install DESTDIR=$startdir/pkg || return 1

    # man
    install -d -m 755 $startdir/pkg/usr/share/man/man{1,8}
    install -m 644 docs/logtail.8 			$startdir/pkg/usr/share/man/man8
    install -m 644 docs/logtail2.8 			$startdir/pkg/usr/share/man/man8
    install -m 644 docs/logcheck-test.1 	$startdir/pkg/usr/share/man/man1
    install -m 644 $startdir/logcheck.8 		$startdir/pkg/usr/share/man/man8

    # doc
    install -d -m 755 $startdir/pkg/usr/share/doc/logcheck/tools
    install -m 644 AUTHORS 	$startdir/pkg/usr/share/doc/logcheck
    install -m 644 CHANGES 	$startdir/pkg/usr/share/doc/logcheck
    install -m 644 CREDITS 	$startdir/pkg/usr/share/doc/logcheck
    install -m 644 INSTALL 	$startdir/pkg/usr/share/doc/logcheck
    install -m 644 TODO 	$startdir/pkg/usr/share/doc/logcheck
    cp -r docs/*		$startdir/pkg/usr/share/doc/logcheck
    cp -r debian/*		$startdir/pkg/usr/share/doc/logcheck
#    install -m 644 docs/README* 	$startdir/pkg/usr/share/doc/logcheck
#    install -m 755 docs/tools/log-summary-ssh $startdir/pkg/usr/share/doc/logcheck/tools
#    install -m 644 debian/* 	$startdir/pkg/usr/share/doc/logcheck
    chmod 755 $startdir/pkg/usr/share/doc/logcheck/rules

    # mail header
    install -m 644 debian/header.txt $startdir/pkg/etc/logcheck

    # logfiles entries
    sed -i -e 's/\/var\/log\/syslog$/\/var\/log\/syslog.log/' $startdir/pkg/etc/logcheck/logcheck.logfiles
    echo "/var/log/messages.log" >> $startdir/pkg/etc/logcheck/logcheck.logfiles
    echo "/var/log/kernel.log" >> $startdir/pkg/etc/logcheck/logcheck.logfiles
    echo "/var/log/mail.log" >> $startdir/pkg/etc/logcheck/logcheck.logfiles
    echo "/var/log/user.log" >> $startdir/pkg/etc/logcheck/logcheck.logfiles

    # template ignore files
    mkdir -p $startdir/pkg/usr/share/logcheck/templates/{ignore.d.server,violations.ignore.d}
    install -m 644 $startdir/src/local-ignorefiles/* $startdir/pkg/usr/share/logcheck/templates/ignore.d.server
    install -m 644 $startdir/src/local-violationsfiles/* $startdir/pkg/usr/share/logcheck/templates/violations.ignore.d

    # cron entry
    install -m 755 -d $startdir/pkg/etc/cron.hourly
    cronfile="$startdir/pkg/etc/cron.hourly/logcheck"
    echo "#!/bin/bash" > $cronfile
    echo 'if [ -x /usr/sbin/logcheck ]; then nice -n 10 su -s /bin/bash -c "/usr/sbin/logcheck" logcheck; fi' >> $cronfile
    chmod 755 $cronfile
}

md5sums=('e2ff14f522bf2e30d5947c85fed44973'
         'a9d8c93ecc12d61b0e87bf07ed951ac8'
         '27ff65b04d11292eabf7f6ffd3a25bf0'
         '2afbd6872cba6523eaec8a87f5ea288e')
