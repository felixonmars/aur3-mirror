# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=3.1.9
pkgrel=4
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=(i686 x86_64)
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost>=1.36')
makedepends=('cmake>=2.8')
optdepends=('openssl: for SSL support in built-in webserver'
            'zlib: for compression in HTTP protocol'
            'fcgi: for FastCGI support'
            'postgresql-libs: for PostgreSQL Dbo support'
            'sqlite3: for Sqlite3 Dbo support'
            'libharu: for PDF generation (WPdfImage)'
            'graphicsmagick: for raster image support (WRasterImage)'
            'pango: for advanced font rendering (WRasterImage)'
            'mysql++: for the hangman example'
            'qt: for the Wt/Qt interopability example (wtwithqt)')
source=(http://downloads.sourceforge.net/witty/$pkgname-${pkgver}.tar.gz
        wt-3.1.9-filetreetable-example.patch
        wt-3.1.9-fontsupport-boost-fs-3.patch)
md5sums=('ee9490d5f24a47766e54f630ba0da2bb'
         '4d2885d2db91125340b9d18cdc7b1cd8'
         '0f91bc57df612405a5a917b3aef1803c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i "$srcdir/wt-3.1.9-filetreetable-example.patch" || return 1
  patch -Np1 -i "$srcdir/wt-3.1.9-fontsupport-boost-fs-3.patch" || return 1
  mkdir build
  cd build
  cmake -DCONNECTOR_HTTP=ON -DCMAKE_INSTALL_PREFIX=/usr \
    -DWEBUSER=http -DWEBGROUP=http -DRUNDIR=/var/run/wt -DDEPLOYROOT=/var/www/wt \
    -DDESTDIR=${pkgdir} -DWT_CMAKE_FINDER_INSTALL_DIR="share/cmake-2.8/Modules" ..
  make || return 1
  make DESTDIR=${pkgdir} install
}
