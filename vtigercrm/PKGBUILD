# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
pkgname=vtigercrm
pkgver=5.1.0
pkgrel=2
pkgdesc="A free, full-featured, 100% Open Source CRM software ideal for small and medium businesses"
arch=(any)
url="http://vtiger.com"
license=('custom')
depends=('apache>=2.0.40' 'mysql>=4.1' 'php>=5.0' 'gd' 'perl' 'perl-archive-zip' 'libpng' 'libjpeg' 'freetype2' 'zlib')
backup=("srv/http/$pkgname/config.db.php"
        "srv/http/$pkgname/config.inc.php"
        "srv/http/$pkgname/config.performance.php"
        "srv/http/$pkgname/config.php"
        "srv/http/$pkgname/connection.php")
options=('!strip')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz http-$pkgname.conf)
md5sums=('983da0f15182e7923cba098505a43b68'
         'fbb511340a85cd80442beef70c142c42')

build() {
  msg "Nothing to compile for $pkgname"
}

package() {
  _INSTDIR="${pkgdir}/srv/http/${pkgname}"
  _HTTPDIR="${pkgdir}/etc/httpd/conf/extra"
  
  # Install the server files
  msg "Installing http files to $_INSTDIR then setting ownership and permissions"
  mkdir -p $_INSTDIR || return 1
  cp -ra $srcdir/$pkgname/* $_INSTDIR || return 1
  chown -R 33:33 $_INSTDIR || return 1
  find $_INSTDIR -type d -exec chmod 755 {} \; || return 1
  find $_INSTDIR -type f -exec chmod 644 {} \; || return 1
  
  # Install the Apache config file
  msg "Installing Apache config file"
  install -Dm644 -o33 -g33 $srcdir/http-$pkgname.conf $_HTTPDIR/httpd-$pkgname.conf || return 1

  # Install the custom license
  msg "Installing licences to $pkgdir/usr/share/licenses/$pkgname/"
  install -Dm644 $srcdir/$pkgname/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt || return 1
  install -Dm644 $srcdir/$pkgname/LICENSE_AGREEMENT.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE_AGREEMENT.txt || return 1
}

# vim:set ts=2 sw=2 et:
