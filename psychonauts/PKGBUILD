# Maintainer: Eric Anderson <ejona86 at gmail dot com>

pkgname=psychonauts
pkgver=20120604
_srcpkgver=06042012
pkgrel=2
pkgdesc="A mind-bending platforming adventure (game sold separately)"
arch=('i686' 'x86_64')
url="http://www.psychonauts.com/"
license=('custom')
makedepends=('unzip')
if [ "$CARCH" = "x86_64" ]; then
    depends=('lib32-sdl' 'lib32-openal' 'bash' 'lib32-gcc-libs')
    optdepends=('lib32-libtxc_dxtn: texture decoding for open source ATI and Intel drivers')
else
    depends=('sdl' 'openal' 'bash' 'gcc-libs')
    optdepends=('libtxc_dxtn: texture decoding for open source ATI and Intel drivers')
fi
options=('!strip')
_gamepkg="${pkgname}-linux-${_srcpkgver}-bin"
source=("${pkgname}.desktop"
        "psychonauts.sh"
        "${_gamepkg}")
md5sums=('91be805610718903bea5c3a3d1d127cf'
         '6282a96e6c4c4f6f94f92d6f2dfdbefe'
         'ade9789da951bbd6a39847ef5d52ded1')
PKGEXT='.pkg.tar'

# Set to 'true' to enable 0.4 patch seen at
# http://icculus.org/pipermail/psychonauts/2012-June/000026.html
_usepatch=false
if $_usepatch; then
    source+=("http://treefort.icculus.org/psychonauts/psychonauts-linux-0.4-patch.tar.bz2")
    md5sums+=('b653dac841011e4757f7c10c0602a316')
fi

if [ ! -f "${startdir}/${_gamepkg}" ]; then
  warning ""
  warning "Requires purchased game download placed at ${startdir}/${_gamepkg}"
  warning ""
fi

build(){
    cd "${srcdir}"
    unzip "${_gamepkg}" -d "${pkgname}" || true
}

package(){
    cd "${srcdir}"

    mkdir -p "${pkgdir}/opt"
    cp -Rl "${srcdir}/${pkgname}/data" "${pkgdir}/opt/"
    mv "${pkgdir}/opt/data" "${pkgdir}/opt/${pkgname}"

    if $_usepatch; then
      cp -lf "${srcdir}/Psychonauts" "${pkgdir}/opt/${pkgname}/"
    fi

    # Use system-provided libraries
    rm "${pkgdir}/opt/${pkgname}/libSDL-1.2.so.0"
    rm "${pkgdir}/opt/${pkgname}/libopenal.so.1"

    install -D -m644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/EULA.rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/EULA.rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-de (German).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-de (German).rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-en (English).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-en (English).rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-es (Spanish).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-es (Spanish).rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-fr (French).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-fr (French).rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-it (Italian).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-it (Italian).rtf"

    install -D -m755 "${srcdir}/psychonauts.sh" "${pkgdir}/usr/bin/${pkgname}"
}
