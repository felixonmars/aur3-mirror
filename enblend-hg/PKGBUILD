# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=enblend-hg
pkgver=793
pkgrel=1
pkgdesc="Intelligent blend tool for overlapping pictures"
arch=('i686' 'x86_64')
license=('GPL')
conflicts=('enblend' 'enblend-enfuse')
provides=('enblend' 'enblend-enfuse')
replaces=('enblend-cvs' 'enblend-staging-bzr')
url="http://enblend.sourceforge.net/"
depends=('libgl' 'openexr' 'libxmi' 'glew' 'freeglut' 'lcms' 'libpng' 'boost-libs')
makedepends=('mercurial' 'cmake' 'perl-timedate' 'boost')
source=()
md5sums=()

_hgroot="http://enblend.hg.sourceforge.net:8000/hgroot/enblend"
_hgrepo="enblend"

build() {
  cd "$srcdir"
  msg "Connecting to $_hgrepo Mercurial server......."
  if [ -d $_hgrepo ]; then
    cd "$srcdir"/$_hgrepo
    hg pull -u
    cd "$srcdir"
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."
  
  if [ -e "$srcdir"/$_hgrepo-build ]; then
	  rm -rf "$srcdir"/$_hgrepo-build
  fi
  mkdir "$srcdir"/$_hgrepo-build
  cd "$srcdir"/$_hgrepo-build

  sed -i 's|doc/enblend|share/doc/enblend|' ../$_hgrepo/doc/CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_OPENMP=ON \
    -DENABLE_IMAGECACHE=OFF \
    -DENABLE_GPU=ON \
    ../$_hgrepo
  make
}

package() {
  cd "$srcdir"/$_hgrepo-build
  make DESTDIR="$pkgdir" install
}
