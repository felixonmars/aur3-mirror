# Maintainer: Jesse Spangenberger <azulephoenix@gmail.com>

pkgname=notepadqq-git
_pkgname=notepadqq
pkgver=r485.143db18
pkgrel=1
pkgdesc="A Linux clone of Notepad++"
arch=('i686' 'x86_64')
url="http://notepadqq.altervista.org/wp/"
license=('GPL3')
depends=('qt5-webkit' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('git' 'qt5-svg')
provides=('notepadqq')
conflicts=('notepadqq-bin')
install=${_pkgname}.install
sha1sums=('SKIP')

source=(git+https://github.com/notepadqq/notepadqq.git)

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	
	 ./configure --qmake-path qmake-qt5 --prefix "${pkgdir}/usr"
	 make
	
}

package() {
	cd "${srcdir}/${_pkgname}/src/ui"
	make DESTDIR="${pkgdir}/usr" install
	
	install -Dm644 "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${_pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${_pkgname}.svg"
}
