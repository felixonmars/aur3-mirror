# Maintainer: Limao Luo <luolimao@gmail.com>

pkgname=pari-git
pkgver=20120831
pkgrel=1
pkgdesc="Pari/GP: Computer algebra system designed for fast computations in number theory (nightly snapshot)"
arch=(i686 x86_64)
url='http://pari.math.u-bordeaux.fr/'
license=(GPL)
depends=(libx11)
makedepends=(git texlive-core)
provides=(pari=2.6.0)
conflicts=(pari pari-nightly-snapshot)
replaces=(pari-nightly-snapshot)

_gitroot="http://pari.math.u-bordeaux.fr/git/pari.git"
_gitname="pari"

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d $_gitname ]] ; then
	cd $_gitname && git pull origin master
	msg "The local files are updated."
	cd ../
    else
	git clone $_gitroot 
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf $_gitname-build
    cp -rL $_gitname $_gitname-build
    cd $_gitname-build

    # configuration to make stuff work
    sed 's:\$addlib64: :' -i config/get_libpth
    ./Configure --prefix=/usr --with-gmp --with-readline
    make all
}

check() {
    cd "$srcdir"/$_gitname-build/
    make bench
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install

    # symlink pointing to a symlink that is going to be moved by zipman
    rm "$pkgdir"/usr/share/man/man1/pari.1
    ln -s gp.1.gz "$pkgdir"/usr/share/man/man1/pari.1.gz

    install -Dm644 misc/gprc.dft "$pkgdir"/etc/gprc
}
