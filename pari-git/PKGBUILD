# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=pari
pkgname=$_pkgname-git
pkgver=2.6.1.453.gdcd5076
pkgrel=1
pkgdesc="Pari/GP: Computer algebra system designed for fast computations in number theory (git version)"
arch=(i686 x86_64)
url=http://pari.math.u-bordeaux.fr
license=(GPL2)
depends=(libx11)
makedepends=(git texlive-core)
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
backup=(etc/gprc)
source=(git+$url/git/$_pkgname.git)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname/
    git describe | sed 's/pari-//;s/-/./g'
}

build() {
    cd $_pkgname/
    ./Configure --prefix=/usr --with-gmp --with-readline
    make all
}

check() {
    make -C $_pkgname bench
}

package() {
    cd $_pkgname/
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/share/man/man1/$_pkgname.1
    install -Dm644 misc/gprc.dft "$pkgdir"/etc/gprc
}
