# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=pumpa
pkgver=0.7.4
pkgrel=1
pkgdesc="A simple pump.io client written in C++ and Qt"
arch=('i686' 'x86_64')
url="http://gitorious.org/pumpa"
license=('GPL3')
depends=('qt5-base' 'aspell')
install="$pkgname.install"
source=("$url/pumpa/archive-tarball/v$pkgver")
md5sums=('8c5bb5a866d16a22671e551b486be360')

prepare() {
  cd "$srcdir/$pkgname-$pkgname"

  # desktop file paths fix
  sed -i 's|./\(pumpa\).*|\1|' $pkgname.desktop
}

build() {
  cd "$srcdir/$pkgname-$pkgname"
  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgname"

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 images/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
