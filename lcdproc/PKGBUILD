# Maintainer: BlackEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Bernhard Walle <bernhard.walle@gmx.de>
pkgname=lcdproc
pkgver=0.5.5
pkgrel=4
pkgdesc="Utility to drive one or more LCD (and LCD-like) devices"
url="http://lcdproc.omnipotent.net/"
license="GPL"
depends=('libusb-compat')
optdepends=(
	'perl: needed for some lcdproc tools'
)
backup=(
	'etc/LCDd.conf'
	'etc/lcdexec.conf'
	'etc/lcdproc.conf'
	'etc/lcdvc.conf'
)
arch=('i686' 'x86_64')
source=(
	"http://switch.dl.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
	'lcdd'
	'lcdd.service'
)

build() {
	cd "$pkgname-$pkgver"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-libusb \
		--enable-lcdproc-menus \
		--enable-stat-smbfs \
		--enable-drivers=all
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	sed -e "s/server\/drivers\//\/usr\/lib\/lcdproc\//g" -i "$pkgdir/etc/LCDd.conf"
	install -Dm755 "$srcdir/lcdd" "$pkgdir/etc/rc.d/lcdd"
	install -Dm644 "$srcdir/lcdd.service" "$pkgdir/usr/lib/systemd/system/lcdd.service"
}
sha256sums=('b136b47d7bf585a003334f432d8730a36ef5ed1cd520084b919667d825e48d42'
            'b43dfd02e38f0149a02396cdc355e5ca2b51217b2da6092de56d426490abfcab'
            'e2848f977724a33f319a6b9322129b55b0a7f7e8b655148426481a767daf9011')
