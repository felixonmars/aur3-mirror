# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Eric Forgeot < http://ifiction.free.fr >

pkgname=wxlua
pkgver=2.8.12.2
pkgrel=1
pkgdesc="A set of bindings to the wxWidgets library for the Lua programming language."
arch=('i686' 'x86_64')
url="http://wxlua.sourceforge.net"
license=('custom:wxWindows')
depends=('lua' 'desktop-file-utils' 'wxgtk')
install="$pkgname.install"
source=("https://launchpad.net/~zerobranestudio/+archive/zerobranestudio/+files/wxlua_$pkgver.orig.tar.gz"
        "http://www.wxwidgets.org/about/licence3.txt")
#source=("http://downloads.sourceforge.net/$pkgname/$pkgname/$pkgver/wxLua-$pkgver-src.tar.gz")
md5sums=('6570b61a1ed62cce90d8c773cf09dce5'
         '8b3f4d426b97ea335b053353f9f220b2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Let configure find system lua
  sed -i "s_\(lua\)5.1_\1_g;s_\(include\)/lua_\1_" \
	configure \
	build/autoconf/configure.ac \
	apps/Makefile.in \
	modules/Makefile.in

  # Prevent installing lua headers
  sed -i "/include\/lauxlib\.h/d" modules/Makefile.in

  # let wxbind sources be installed
  # in a more suitable path
  sed -i "s|src/wxbind|include/wxbind|g" \
      modules/{Makefile.in,build/bakefiles/modules.bkl}

  ./configure --prefix=/usr \
              --with-lua-prefix=/usr/lib/lua/5.1/ \
              --enable-systemlua

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Install custom license file
  install -Dm 644 "$srcdir/licence3.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
