# Maintainer: Dennis Hamester <dennis.hamester [AT] gmail [DOT] com>
# Original from cummunity by:
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>
pkgname=dwarffortress-phoebus
pkgver=0.34.11.0
_dfver=34_11
_phver=${_dfver}v00
pkgrel=3
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world. Packed with Phoebus' graphics set."
arch=(i686 x86_64)
url="http://www.bay12forums.com/smf/index.php?topic=57557.0"
install="$pkgname.install"
license=('custom:dwarffortress_phoebus')
depends=(gtk2 mesa sdl_image libsndfile openal sdl_ttf)
if [[ $CARCH == 'x86_64' ]]; then
  depends=(lib32-glu lib32-gtk2 lib32-mesa lib32-sdl_image lib32-libsndfile lib32-openal lib32-libxdamage lib32-ncurses lib32-sdl_ttf)
  optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
              'lib32-catalyst-utils: If you have ATI graphics'
              'lib32-alsa-lib: for alsa sound'
              'lib32-libpulse: for pulse sound')
fi

source=(http://www.bay12games.com/dwarves/df_${_dfver}_linux.tar.bz2
        Phoebus_${_phver}.zip::"http://dffd.wimbli.com/download.php?id=2430&f=Phoebus_${_phver}.zip"
        enable_sound_and_intro.patch
        dwarffortress-phoebus
        dwarffortress-phoebus.desktop
        dwarffortress-phoebus.png)

backup=(opt/df_linux-phoebus/data/init/init.txt)

# Change this to 1 to disable TrueType fonts.
_nott=1

build() {
  cd $srcdir
  patch -p1 -i enable_sound_and_intro.patch
}

package() {
  cd $srcdir/df_linux
  install -dm755 $pkgdir/opt/
  cp -r $srcdir/df_linux/ $pkgdir/opt/
  cp -rf $srcdir/raw/ $pkgdir/opt/df_linux/
  if [[ $_nott == 0 ]]; then
      cp -rf $srcdir/data/init/phoebus/* $pkgdir/opt/df_linux/data/init/
  else
      cp -rf $srcdir/data/init/phoebus_nott/* $pkgdir/opt/df_linux/data/init/
  fi 
  cp -rf $srcdir/data/art/ $pkgdir/opt/df_linux/data/
  cp $srcdir/df_savegame_updater.sh $pkgdir/opt/df_linux/

  # Yay for precompiled stuff with junk permissions! :D
  find $pkgdir/opt/df_linux -type d -exec chmod 755 {} +
  find $pkgdir/opt/df_linux -type f -exec chmod 644 {} +

  install -Dm755 $srcdir/dwarffortress-phoebus $pkgdir/usr/bin/dwarffortress-phoebus

  chmod 755 $pkgdir/opt/df_linux/libs/Dwarf_Fortress
  chmod 755 $pkgdir/opt/df_linux/df_savegame_updater.sh

  install -d -m775 -o root -g games $pkgdir/opt/df_linux/data/save

  # This probably is overkill, but I don't know what specific files df needs permission for in here.
  chmod 775 $pkgdir/opt/df_linux
  chown root:games $pkgdir/opt/df_linux
  chown -R root:games $pkgdir/opt/df_linux/data
  find $pkgdir/opt/df_linux/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/df_linux/data -type f -exec chmod 664 {} +

  # Desktop launcher with icon
  install -Dm644 $srcdir/dwarffortress-phoebus.desktop $pkgdir/usr/share/applications/dwarffortress-phoebus.desktop
  install -Dm644 $srcdir/dwarffortress-phoebus.png     $pkgdir/usr/share/pixmaps/dwarffortress-phoebus.png

  install -Dm644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress-phoebus/readme.txt

  mv $pkgdir/opt/df_linux $pkgdir/opt/df_linux-phoebus
}

# vim:set ts=2 sw=2 et:

sha1sums=('a66d56562ffba698198127c8d9a1194a2313c992'
          '781d5e11fadbe80bddc9058d9dd44399eada21f2'
          '1336eb91716a8ff6a172b67dec59be81bc6d4d1f'
          '14111dd10e39cb709070c98623b6374b991833e3'
          '59dd658ade880b861e00730bc3150555728ad27b'
          '9b7f721588e0ff9c9d115a28d51d63823e3a1e7c')

