# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-frameworks
pkgver=5.0.2
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('xapian-core' 'kfilemetadata5' 'kidletime' 'kcmutils' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('baloo4')
provides=('baloo')
source=("http://download.kde.org/stable/plasma/$pkgver/baloo-$pkgver.tar.xz")
md5sums=('58822e05e04f7f01da63f7ba05e9e3be')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../baloo-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DLOCALE_INSTALL_DIR=share/locale/kf5 \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
