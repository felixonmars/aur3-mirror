# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

basename=netbeans
edname=javase
pkgname=${basename}-${edname}
pkgver=7.1.2
pkgdate=201204101705
pkgrel=1
pkgdesc="Oracle's Java (SE) IDE"
arch=("any")
url="http://${basename}.org"
license=("CDDL")
depends=("java-environment")
conflicts=("${basename}")
install=${pkgname}.install
options=(!strip)
source=(http://download.${basename}.org/${basename}/${pkgver}/final/zip/${basename}-${pkgver}-${pkgdate}-ml-${edname}.zip)

package() {
	cd "${srcdir}"

	# Removals
	rm ${basename}/bin/${basename}.exe

	# Directories
	install -d "${pkgdir}"/usr/share/${basename} "${pkgdir}"/usr/bin

	# Files
	cp -r ${basename}/* "${pkgdir}"/usr/share/${basename}/
	ln -s /usr/share/${basename}/bin/${basename} "${pkgdir}"/usr/bin/${basename}

	# Desktop
	install -D -m644 "${startdir}"/${basename}.desktop "${pkgdir}"/usr/share/applications/${basename}.desktop
}

sha1sums=('87a94bb5a6460e253fc3d54855afdcc7bbd2a894')
