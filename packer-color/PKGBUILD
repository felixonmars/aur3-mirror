# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Bruenig <Matthew (dot) Bruenig (at) Gmail (dot) com>

basename=packer
pkgname=${basename}-color
pkgver=20121212
pkgrel=1
pkgdesc="Adds AUR support to 'pacman' | Adds better colourisation and error handling to 'packer'"
url="http://github.com/bruenig/${basename}"
license="GPL3"
arch=("any")
makedepends=("git")
depends=("grep" "sed" "bash" "curl" "pacman-color" "jshon")
optdepends=("sudo: install and update packages as non-root"
            "customizepkg: apply customizepkg modifications")

_gitroot="http://github.com/keenerd/${basename}.git"
_gitname="${basename}"

build() {
	cd "${srcdir}"

	# Repository
	msg "Connecting to GIT server ..."
	if [ -d "${srcdir}/${_gitname}" ] ; then
		cd ${_gitname} && git pull origin
	else
		git clone "${_gitroot}"
		cd ${_gitname}
	fi
	
	# Patches
	patch -Np1 < "${startdir}"/${pkgname}.patch

	# Changes
	sed -i 's|packer |packer-color |g' ${basename}
	sed -i 's|"pacman"|"pacman-color"|g' ${basename}
	sed -i 's|pacman -|pacman-color -|g' ${basename}
	sed -i 's|runasroot pacman |runasroot pacman-color |g' ${basename}
	sed -i 's| pacman "| pacman-color "|g' ${basename}
	sed -i "s|echo \"packer: Option .* is not valid.\"|pacman-color \"\$\@\"|g" ${basename}
}

package() {
	cd ${_gitname}

	# Install
	install -D -m755 ${basename} "${pkgdir}/usr/bin/${pkgname}"
	install -D -m644 ${basename}.8 "${pkgdir}/usr/share/man/man8/${pkgname}.8"
}
