pkgname=mldonkey
pkgver=3.1.3
pkgrel=3
pkgdesc='A multi-network P2P client'
arch=(i686 x86_64)
url='http://mldonkey.sourceforge.net/'
license=(GPL)
depends=(desktop-file-utils file gd)
makedepends=(lablgtk2 librsvg ocaml)
optdepends=('librsvg: GUI support'
            'libx11: GUI support')
backup=(etc/conf.d/mldonkey)
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/$pkgname-$pkgver.tar.bz2
        mldonkey.conf
        mldonkey.service
        mldonkey.tmpfiles
        gd_version.patch)
sha1sums=('424386f277e84df55a2cbab213fae60787e42c8b'
          '7e70e5f3c7bf5036debde4dacbed463c0464a1e7'
          'd6886479cf36fbee0a62def0291706973c91dfa5'
          'a454b919fac518ecb32f6262afbef0dfc423401c'
          '18c4a0753d338bf24cac28dcbd43abcf59ce4534')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 < ../gd_version.patch # a fix from upstream http://cvs.savannah.gnu.org/viewvc/mldonkey/config/configure.in?root=mldonkey&r1=1.357&r2=1.358&view=patch

  rm config/configure  # remove this file to force its regeneration. Otherwise i see a weird timing issue and ./configure does not include the patch above.
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-gui=newgui2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -d  "$pkgdir/var/lib/mldonkey"

  install -Dm644 icons/rsvg/type_source_normal.svg "$pkgdir/usr/share/icons/mldonkey.svg"
  install -Dm644 distrib/mldonkey.desktop "$pkgdir/usr/share/applications/mldonkey.desktop"

  install -Dm644 ../mldonkey.conf "$pkgdir/etc/conf.d/mldonkey"
  install -Dm644 ../mldonkey.service "$pkgdir/usr/lib/systemd/system/mldonkey.service"
  install -Dm644 ../mldonkey.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/mldonkey.conf"
}
