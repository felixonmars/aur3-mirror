# Maintainer: HÃ¥vard Pettersson <haavard.pettersson@gmail.com>

pkgname=tox-weechat-git
pkgver=r140.8f1d007
pkgrel=1

pkgdesc="Tox protocol plugin for WeeChat."
arch=('i686' 'x86_64')
url="https://github.com/haavardp/tox-weechat"
license=('GPL3')

depends=('tox-git' 'weechat>=1.0.1')
makedepends=('git' 'cmake>=2.8.7')

source=("${pkgname}::git://github.com/haavardp/tox-weechat.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make -j$(nproc)
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}/" install
}

