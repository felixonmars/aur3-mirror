# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>

_pkgname=tox-weechat
pkgname="${_pkgname}-git"
pkgver=r249.g37e3b66
pkgrel=1

pkgdesc='Tox protocol plugin for WeeChat'
arch=('i686' 'x86_64')
url='https://github.com/haavardp/tox-weechat'
license=('GPL3')

depends=('tox' 'weechat>=1.0.1')
makedepends=('git' 'cmake>=2.8.7')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=("${pkgname}::git+https://github.com/haavardp/tox-weechat.git")
md5sums=('SKIP')

install="${pkgname}.install"

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}/" install
}

