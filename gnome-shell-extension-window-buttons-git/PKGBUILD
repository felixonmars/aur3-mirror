# Maintainer: XZS <d dot f dot fischer at web dot de>
# Contributor: Pi3R1k <pierrick.brun at gmail dot com>
pkgname=gnome-shell-extension-window-buttons-git
pkgver=140.9aad6a8
pkgrel=1
pkgdesc="Extension which puts minimize, maximize and close buttons in the top panel."
arch=('any')
url="https://github.com/danielkza/Gnome-Shell-Window-Buttons-Extension"
license=('GPL3')
depends=('gnome-shell>=3.4')
makedepends=('git')
install=gschemas.install
source=("$pkgname::git+$url")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname/*@*
  rm schemas/gschemas.compiled
}

package () {
  cd $pkgname
  destdir="$pkgdir/usr/share/gnome-shell/extensions"
  schemadir="$pkgdir/usr/share/glib-2.0"
  install -d "$destdir" "$schemadir"
  cp -r *@* "$destdir"
  mv "$destdir"/*@*/schemas "$schemadir"
}
