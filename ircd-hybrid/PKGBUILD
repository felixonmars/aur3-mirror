# -*- shell-script -*-
#
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=ircd-hybrid
pkgver=8.0.1
pkgrel=3
arch=("i686" "x86_64")
pkgdesc="High performance secure IRC server"
url="http://www.ircd-hybrid.org"
license=("GPL")
depends=("zlib" "pcre" "openssl")
optdepends=("ircd-hybrid-serv: IRC services designed for ircd-hybrid server"
            "anope: IRC services designed for flexibility and ease of use")
conflicts=("ircd")
provides=("ircd")
backup=("etc/ircd-hybrid/ircd.conf"
	"etc/conf.d/ircd-hybrid")
install="${pkgname}.install"
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-8.0.1.tgz"
        "modules-install-exec.patch"
        "${pkgname}.rc"
        "${pkgname}.conf"
        "${pkgname}.confd"
        "${pkgname}.service")
md5sums=("4ee140baf0962c72d719dbcd20bff01a"
         "caad81bf8958943baf3b1bf6975e5f48"
         "54033f685fef4aa6e78072226f616a20"
         "52e94d5fac8f1ecb45700b0cd14a397f"
         "8cea39dfd7f9d5ea6d0e90e0259c5861"
         "17343809e27b9a3c9f5b653d9a01acf0")


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# User compile time patches
  patch -Np1 -i ${srcdir}/modules-install-exec.patch

# User compile time options
  _nicklen=16
  _topiclen=364


# Configure Hybrid
  ./configure --prefix=/usr \
      --with-nicklen=${_nicklen} \
      --with-topiclen=${_topiclen} \
      --bindir=/usr/sbin \
      --sysconfdir=/etc/${pkgname} \
      --localstatedir=/var \
      --datarootdir=/usr/share \
      --mandir=/usr/share/man \
      --includedir=/usr/include \
      --libdir=/usr/lib \
      --enable-rtsigio \
      --enable-poll \
      --enable-openssl \
      --enable-halfops

# Build Hybrid
  make
  make -C contrib
}


package () {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Install Hybrid
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" -C contrib install
  make DESTDIR="${pkgdir}" -C messages install

# Install Hybrid sample configuration
  install -D -m 0660 ${srcdir}/${pkgname}.conf ${pkgdir}/etc/${pkgname}/ircd.conf

# Install Arch Linux init scripts
  install -D -m 0755 ${srcdir}/${pkgname}.rc ${pkgdir}/etc/rc.d/${pkgname}
  install -D -m 0644 ${srcdir}/${pkgname}.confd ${pkgdir}/etc/conf.d/${pkgname}

# Install systemd service file
  install -D -m 0644 ${srcdir}/${pkgname}.service ${pkgdir}/usr/lib/systemd/system/${pkgname}.service

# Prepare extra directories
  install -d "${pkgdir}/var/log/ircd"
  install -d "${pkgdir}/var/lib/ircd"
  install -d "${pkgdir}/var/run/ircd"
  install -d -m 0750 "${pkgdir}/etc/${pkgname}/ssl"
}
