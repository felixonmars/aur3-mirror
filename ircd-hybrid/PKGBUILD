# -*- shell-script -*-
#
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=ircd-hybrid
pkgver=8.0.6
pkgrel=1
arch=("i686" "x86_64")
pkgdesc="High performance secure IRC server"
url="http://www.ircd-hybrid.org"
license=("GPL")
depends=("zlib" "pcre" "openssl")
optdepends=("ircd-hybrid-serv: IRC services designed for ircd-hybrid server"
            "anope: IRC services designed for flexibility and ease of use")
conflicts=("ircd")
provides=("ircd")
backup=("etc/ircd-hybrid/ircd.conf"
	"etc/conf.d/ircd-hybrid")
install="${pkgname}.install"
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tgz"
        "modules-install-exec.patch"
        "${pkgname}.rc"
        "${pkgname}.conf"
        "${pkgname}.confd"
        "${pkgname}.service"
        "${pkgname}.tmpfiles")
md5sums=("6149a0976c99b15d678ca89456899415"
         "caad81bf8958943baf3b1bf6975e5f48"
         "54033f685fef4aa6e78072226f616a20"
         "695fd1cc7417b1d8d45cfba982d5bbb8"
         "26b5e5f8fdf52bb11025973d2431153b"
         "2d96623dd5307e1572478e0c0c32ca1b"
         "e51723ab29e7a3ddc3589846815de0c9")


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# User compile time patches
  patch -Np1 -i ${srcdir}/modules-install-exec.patch


# Configure Hybrid
  ./configure --prefix=/usr \
      --bindir=/usr/sbin \
      --sysconfdir=/etc/${pkgname} \
      --localstatedir=/var \
      --datarootdir=/usr/share \
      --mandir=/usr/share/man \
      --includedir=/usr/include \
      --libdir=/usr/lib \
      --enable-poll \
      --enable-openssl \
      --enable-halfops

# Build Hybrid
  make
  make -C contrib
}


package () {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Install Hybrid
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" -C contrib install
  make DESTDIR="${pkgdir}" -C messages install

# Install Hybrid sample configuration
  install -D -m 0660 ${srcdir}/${pkgname}.conf ${pkgdir}/etc/${pkgname}/ircd.conf

# Install Arch Linux init scripts
  install -D -m 0755 ${srcdir}/${pkgname}.rc ${pkgdir}/etc/rc.d/${pkgname}
  install -D -m 0644 ${srcdir}/${pkgname}.confd ${pkgdir}/etc/conf.d/${pkgname}

# Install systemd service file
  install -D -m 0644 ${srcdir}/${pkgname}.service ${pkgdir}/usr/lib/systemd/system/${pkgname}.service
  install -D -m 0644 ${srcdir}/${pkgname}.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d//${pkgname}.conf

# Prepare extra directories
  install -d "${pkgdir}/var/log/ircd"
  install -d "${pkgdir}/var/lib/ircd"
  install -d "${pkgdir}/var/run/ircd"
  install -d -m 0750 "${pkgdir}/etc/${pkgname}/ssl"
}
