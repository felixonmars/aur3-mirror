# Maintainer: Pierre Buard <pierre.buard+aur gmail com>
pkgname=pgl-git
pkgver=dad2918
pkgrel=1
pkgdesc='PeerGuardian is a privacy oriented firewall application (GUI).'
url='http://sourceforge.net/projects/peerguardian/'
arch=('i686' 'x86_64')
depends=('cron' 'logrotate' 'wget' 'libnetfilter_queue' 'iptables' 'qt4' 'polkit-qt')
optdepends=('unzip: for zipped blocklists'
	    'p7zip: for 7z blocklists'
	    'net-tools: to whitelist local IP addresses'
	    'networkmanager: to whitelist newly started network interfaces'
	    'smtp-forwarder: to send reports')
license=('GPL')
replace=('pgl-nogui')
conflicts=('blockcontrol' 'moblock' 'pgl-cli')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf'
	'etc/logrotate.d/pglcmd'
	'etc/logrotate.d/pgld')
source=('git://git.code.sf.net/p/peerguardian/code/')
md5sums=('SKIP')
install=install

pkgver() {
    cd ${srcdir}/code/
    git describe --always | sed 's|-|.|g'
}

prepare() {
    cd ${srcdir}/code/pgl/
    ./autogen.sh    
}

build() {
    # makes sure qt4 is used
    export QMAKE=qmake-qt4
    export MOC=moc-qt4
    export UIC=uic-qt4
    export RCC=rcc-qt4

    cd ${srcdir}/code/pgl/
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
		--with-piddir=/run --with-systemdsystemunitdir=/usr/lib/systemd/system --without-initddir
    make
}

package() {
    cd ${srcdir}/code/pgl/
    make DESTDIR=${pkgdir} install
}
