# Maintainer: Philipp Überbacher <murks at lavabit dot com>
pkgname=phasex-git
pkgver=20101115
pkgrel=1
pkgdesc="Synthesizer using phase offset modulation."
arch=('i686' 'x86_64')
url="http://www.sysex.net/phasex/"
license=('GPL2')
depends=('jack' 'gtk2' 'libsamplerate')
makedepends=('git')
provides=(phasex)
conflicts=(phasex)

_gitroot="http://sysex.net/git/phasex.git"
_gitname="phasex"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"
  

  aclocal
  autoconf
  automake
  autoheader
  ./configure --prefix="/usr" --enable-arch="native"
  make
}

package(){
  cd "${srcdir}/${_gitname}-build"

  make DESTDIR="${pkgdir}" install
}
