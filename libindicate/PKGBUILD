# Maintainer: György Balló <ballogy@freestart.hu>
pkgbase=libindicate
pkgname=libindicate
true && pkgname=(libindicate libindicate-gtk2 libindicate-gtk3)
pkgver=0.6.1
pkgrel=3
pkgdesc="A small library for applications to raise 'flags' on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
makedepends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76' 'gtk2>=2.12' 'gtk3>=3.0' 'gobject-introspection>=0.6.7' 'vala' 'gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'gnome-doc-utils>=0.3.2')
options=(!libtool !emptydirs)
source=(http://launchpad.net/$pkgbase/0.6/$pkgver/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('dc7ba61069900937868d3e8fddb403f7')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' examples/{im-client,listen-and-print}.py

  # Use different location for header files for GTK+3
  sed -i 's/libindicate-0.6/libindicate3-0.6/' libindicate-gtk/indicate-gtk3-0.6.pc.in
  sed -i 's/libindicate-0.6/libindicate$(VER)-0.6/' libindicate-gtk/Makefile.in

  # Do not link pyglib explicitly
  sed -i '/pyglib/ d' configure

  # Fix mono location
  sed -i 's|indicate-sharp-0.1|mono/indicate-sharp|' bindings/mono/indicate/indicate-sharp-0.1.pc.in
  sed -i 's|indicate-gtk-sharp-0.1|mono/indicate-gtk-sharp|' bindings/mono/indicate-gtk/indicate-gtk-sharp-0.1.pc.in
  sed -i 's|$(libdir)/$(ASSEMBLY_NAME)-sharp-0.1|$(libdir)/mono/$(ASSEMBLY_NAME)-sharp|
          s|-package $(ASSEMBLY_NAME) |-package $(ASSEMBLY_NAME)-sharp |' bindings/mono/indicate{,-gtk}/Makefile.in

  # Disable building tests
  sed -i '/DIST_SUBDIRS/ s/tests//
          /tests/ d' Makefile.in

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --with-gtk=2 \
              PYTHON=python2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
  popd
}

package_libindicate() {
  depends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make -C libindicate DESTDIR="$pkgdir/" install
  make -C bindings/mono/indicate DESTDIR="$pkgdir/" install
  make -C docs DESTDIR="$pkgdir/" install
}

package_libindicate-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=("libindicate=$pkgver" 'gtk2>=2.12')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make -C libindicate DESTDIR="$pkgdir/" install
  make -C libindicate-gtk DESTDIR="$pkgdir/" install
  make -C bindings/python DESTDIR="$pkgdir/" install
  make -C bindings/mono/indicate-gtk DESTDIR="$pkgdir/" install
  make -C examples DESTDIR="$pkgdir/" install
  make -C libindicate DESTDIR="$pkgdir/" uninstall
}

package_libindicate-gtk3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=("libindicate=$pkgver" 'gtk3>=3.0')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make -C libindicate DESTDIR="$pkgdir/" install
  make -C libindicate-gtk DESTDIR="$pkgdir/" install
  make -C libindicate DESTDIR="$pkgdir/" uninstall
}

depends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76' 'gtk2>=2.12' 'gtk3>=3.0')
true && depends=()
