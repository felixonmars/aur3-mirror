_pkgname=netsurf
pkgname=netsurf-fb
pkgver=3.0
pkgrel=2
pkgdesc='Lightweight and fast web browser'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('GPL')
depends=('libnsfb' 'libmng' 'curl' 'librsvg' 'desktop-file-utils' 'libnsbmp' 'libnsgif' 'libcss' 'libdom' 'js') # 'libharu' 'gstreamer0.10' 'libwebp'
makedepends=('re2c' 'lcms' 'gendesk' 'netsurf-buildsystem' 'libglade' 'nsgenbind')
install="netsurf.install"
source=("netsurf.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png"
        "http://download.${_pkgname}-browser.org/${_pkgname}/releases/source/${_pkgname}-$pkgver-src.tar.gz")
sha256sums=('f0dbcc5d80bf03d706aa8b28a322aa7f169a40813848c2d1505691f6e2c7ef00'
            '7c6a48d3cc3e9a3e3a51b532ddf60f7697e97bf8b61a6d3b2ced1a2e89fbccc6')

_exec=('netsurf %U')
_genericname=('Web Browser')
_comment=('Lightweight web browser')
_mimetype=('text/html;application/xhtml+xml;x-scheme-handler/http;x-scheme-handler/https;x-scheme-handler/geo')

prepare() {
  cd "$srcdir"

  gendesk
}

build() {
  cd "${_pkgname}-$pkgver"

  make \
    PREFIX=/usr \
    TARGET=framebuffer \
    NETSURF_USE_WEBP=NO \
    NETSURF_USE_VIDEO=NO \
    NETSURF_USE_MOZJS=YES \
    NETSURF_USE_HARU_PDF=NO 
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"

  make install TARGET=framebuffer PREFIX=/usr DESTDIR="$pkgdir"
  install -Dm644 "../${_pkgname}.png" \
    "$pkgdir/usr/share/pixmaps/${_pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" \
    "$pkgdir/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/${_pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
