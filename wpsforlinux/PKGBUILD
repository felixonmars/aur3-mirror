# Maintainer: limser <echo bGltc2VyQHFxLmNvbQo=|base64 -d>

pkgbase=wpsforlinux
pkgname=wpsforlinux
true && pkgname=('wpsoffice' 'ttf-fzfont')
pkgver=8.1.0.3552
_pkgver=$pkgver+wps+wpp+et~a7
pkgrel=1
pkgdesc='WPS Office is an office productivity suite. This is an ALPHA package. Use it at your own risk.'
arch=('i686' 'x86_64')
url='http://linux.wps.cn/'
license=('custom')
if [ "$CARCH" = "i686" ]; then
    depends=('fontconfig' 'glib2' 'libpng12' 'libsm' 'libxext' 'libxrender')
elif [ "$CARCH" = "x86_64" ]; then
    depends=('lib32-fontconfig' 'lib32-glib2' 'lib32-libpng12' 'lib32-libsm' 'lib32-libxext' 'lib32-libxrender')
fi
depends+=('desktop-file-utils' 'shared-mime-info' 'xdg-utils')
conflicts=('wps-office')
##获得安装包方式：1.到官方论坛求包子 2.搜索泄露的下载地址。
##让我们慢慢等待beta版本的到来吧，到那时下载地址便能公开了^_^
source=("http://sorry/I/cannot/tell/you/wps-office_${_pkgver}_i386.deb")
sha1sums=('dc506b6686a7a86385858ecef608f16fbc3d7232')
#PKGEXT='.pkg.tar' ##less time

_etMT="MimeType=application\/wps-office.et;application\/x-ett;application\/xslt+xml;application\/vnd.ms-excel;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.template;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.sheet;"
_wppMT="MimeType=application\/wps-office.dps;application\/wps-office.dpt;application\/vnd.ms-powerpoint;\
application\/vnd.openxmlformats-officedocument.presentationml.presentation;\
application\/vnd.openxmlformats-officedocument.presentationml.slideshow;\
application\/vnd.openxmlformats-officedocument.presentationml.template;"
_wpsMT="MimeType=application\/wps-office.wps;application\/wps-office.wpt;\
application\/msword;application\/rtf;application\/msword-template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.document;"

package_wpsoffice () {
    optdepends=('ttf-fzfont: FangZheng fonts')
    install=wps-office.install

    cd "${srcdir}"
    tar zxvf data.tar.gz -C "${pkgdir}" --exclude=usr/share/fonts

    ##mime
    sed -i '3,31d' $pkgdir/usr/share/mime/packages/wps-office-et.xml
    sed -i '3,36d' $pkgdir/usr/share/mime/packages/wps-office-wpp.xml
    sed -i '3,30d' $pkgdir/usr/share/mime/packages/wps-office-wps.xml

    ##desktop with shared-mime-info wps-office-{et,wpp,wps}.xml
    #_et
    sed -i "s/^MimeType.*$/$_etMT/" $pkgdir/usr/share/applications/wps-office-et.desktop
    sed -i 's/^Icon.*$/Icon=\/usr\/share\/icons\/hicolor\/48x48\/apps\/wps-office-etmain.png\nCategories=Office;Spreadsheets;Qt;/'\
        $pkgdir/usr/share/applications/wps-office-et.desktop #Fix menu
    #_wpp
    sed -i "s/^MimeType.*$/$_wppMT/" $pkgdir/usr/share/applications/wps-office-wpp.desktop
    #_wps
    sed -i "s/^MimeType.*$/$_wpsMT/" $pkgdir/usr/share/applications/wps-office-wps.desktop
}

package_ttf-fzfont() {
    pkgver=5.20
    pkgrel=1
    pkgdesc="FangZheng fonts separated from wps-office"
    arch=('any')
    url="http://www.foundertype.com/"
    depends=('fontconfig')
    install=ttf-fzfont.install

    cd "${srcdir}"
    tar zxv -C "${pkgdir}" -f data.tar.gz ./usr/share/fonts
}
