# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=fotoxx
pkgver=13.12
pkgrel=1
pkgdesc="A program for improving image files made with a digital camera"
url="http://www.kornelix.com/fotoxx"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('dcraw' 'gtk3' 'gimp-ufraw' 'perl-image-exiftool>=0.8.6' 
  'xdg-utils' 'desktop-file-utils')
optdepends=('mashup: for composing pages to print from within fotoxx' 
  'brasero: for burning')
source=("http://www.kornelix.com/uploads/1/3/0/3/13035936/$pkgname-$pkgver.tar.gz")
md5sums=('1d0e0284198cf07c7e4f34df8cc2d1a5')
install=fotoxx.install

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's+xdg-deskto+#xdg-deskto+' Makefile
  LDFLAGS="-lpthread" make PREFIX=/usr 
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir PREFIX=/usr install
}
