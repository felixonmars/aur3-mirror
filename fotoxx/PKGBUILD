# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=fotoxx
pkgver=12.09.1
pkgrel=1
pkgdesc="A program for improving image files made with a digital camera"
url="http://www.kornelix.com/fotoxx"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('dcraw' 'gtk3' 'gimp-ufraw' 'perl-image-exiftool' 'xdg-utils' 'desktop-file-utils')
optdepends=('mashup: for composing pages to print from within fotoxx' 'brasero: for burning')
source=("http://www.kornelix.com/uploads/1/3/0/3/13035936/$pkgname-$pkgver.tar.gz")
md5sums=('8245aba705b965088e5d4dd75a7e8dea')
install=fotoxx.install

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's+xdg-deskto+#xdg-deskto+' Makefile
  make PREFIX=/usr 
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir PREFIX=/usr install
  cp $pkgdir/usr/share/applications/kornelix-$pkgname.desktop \
    $pkgdir/usr/share/applications/$pkgname.desktop 
  rm $pkgdir/usr/share/applications/kornelix-$pkgname.desktop 
  rm $pkgdir/usr/share/doc/$pkgname/$pkgname.man
}
