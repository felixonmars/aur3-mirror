# Maintainer: max-k <max-k@post.com>
pkgname=gns3
pkgver=0.8.3
pkgrel=3
pkgdesc="Graphical network simulator based on Dynamips. With pemu included."
arch=(i686 x86_64)
url="http://www.gns3.net/"
license=('GPLv2')
depends=('python2-pyqt' 'python2-sip' 'dynagen' 'inetutils' 'xdotool')
optdepends=('wireshark: to capture frames from your virtual networks'
'virtualbox-sdk>=4.1: to use virtualbox hosts'
'qemu: to run qemu hosts or emulate Cisco ASA5520 and IDS 4235/4215 Sensor'
'vde2: to emulate virtual switches')
conflicts=('pemu')
provides=('pemu')
install='gns3.install'
source=("http://downloads.sourceforge.net/gns-3/GNS3-${pkgver}-src.tar.gz"
"http://downloads.sourceforge.net/project/gns-3/Pemu/2008-03-03/pemu_2008-03-03_bin.tar.bz2")
md5sums=('06ab6ca463d3bb12dc84971227e4ef6b'
'147ab04acdca5a79d6a4ab6307a1243d')

build() {
  cd ${srcdir}/GNS3-${pkgver}-src
  python2 setup.py build
}

package() {
  cd ${srcdir}/GNS3-${pkgver}-src
  sed -i "s#/usr/lib#${pkgdir}/usr/lib#" setup.py
  sed -i "s#/usr/local/libexec#${pkgdir}/usr/local/libexec#" setup.py
  sed -i "s#/usr/local/share#${pkgdir}/usr/local/share#" setup.py
  sed -i '86s/QEMU/VBOX/' src/GNS3/Config/Defaults.py
  python2 setup.py install --prefix ${pkgdir}/usr
  gzip ./docs/man/gns3.1
  install -D -m 644 ./docs/man/gns3.1.gz ${pkgdir}/usr/share/man/man1/gns3.1.gz
  sed -i 's/#!\/usr\/bin\/env python/#!\/usr\/bin\/env python2/' \
${pkgdir}/usr/lib/${pkgname}/qemuwrapper.py
  sed -i 's/#!\/usr\/bin\/env python/#!\/usr\/bin\/env python2/' \
${pkgdir}/usr/lib/${pkgname}/vboxwrapper.py
  sed -i 's/#!\/usr\/bin\/env python/#!\/usr\/bin\/env python2/' \
${pkgdir}/usr/lib/${pkgname}/vboxcontroller_4_1.py
  cd ${srcdir}/pemu_2008-03-03_bin
  mkdir -p ${pkgdir}/usr/share/GNS3/pemu
  install -D -m 755 ./pemu ${pkgdir}/usr/lib/${pkgname}/pemu
  install -D -m 644 ./pemu.ini ${pkgdir}/usr/lib/${pkgname}/pemu.ini
  install -D -m 755 ./ifup ${pkgdir}/usr/lib/${pkgname}/ifup
  install -D -m 644 ./ifup.ini ${pkgdir}/usr/lib/${pkgname}/ifup.ini
  install -D -m 644 ./README ${pkgdir}/usr/lib/${pkgname}/README.pemu
}
