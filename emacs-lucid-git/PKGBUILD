# See AUR interface for current maintainer.

#
# See <https://lists.gnu.org/archive/html/emacs-devel/2014-01/msg00005.html> 
# for the start of the discussion on why emacs development moves to git.
#
# See <https://lists.gnu.org/archive/html/emacs-devel/2014-01/msg00015.html>
# for the actual community proposal about moving to git with RMS's blessing.
#
# See <https://plus.google.com/+EricRaymond/posts/agdbxccbxGu> for RMS
# heads up on the switchover on Nov. 11, 2014.
#
# See <http://lists.gnu.org/archive/html/emacs-devel/2014-11/msg00681.html>
# for the completion announcement.
#
# TIP: Don't run this on a tmpfs unless you have oodles of RAM.
#      The initial cloned repo is big, 200MB or more. You can run
#      "git gc --aggressive" on it to reduce its size drastically,
#      but it will take a long time in a low-spec machine or if you
#      have less than 4GB RAM.
#

pkgname=emacs-lucid-git
pkgver=25.0.50.r118124
pkgrel=1
pkgdesc="GNU Emacs. Official git trunk."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/"
license=('GPL')
depends=('librsvg' 'gpm' 'giflib' 'libxfixes' 'm17n-lib' 
	 'libxrandr' 'libxinerama' 'hicolor-icon-theme'
	 'desktop-file-utils' 'alsa-lib' 'imagemagick' 'gnutls')
makedepends=('git')
conflicts=('emacs')
provides=('emacs')
install=emacs.install
source=("$pkgname::git://git.savannah.gnu.org/emacs.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.r%s" \
    "$(grep AC_INIT configure.ac | sed -e 's/^.\+\ \([0-9]\+\.[0-9]\+\.[0-9]\+\).\+$/\1/')" \
    "$(git rev-list --count HEAD)"
}


prepare() {
  cd "$srcdir/$pkgname"
  ./autogen.sh                                                            
}

build() {
  cd "$srcdir/$pkgname"
  ac_cv_lib_gif_EGifPutExtensionLast=yes \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --pdfdir=/usr/share/doc/emacs/pdf \
    --with-sound=alsa \
    --without-gconf \
    --with-x-toolkit=lucid \
    --with-xft

  make
  cd "$srcdir/$pkgname/lisp"
  make autoloads
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install

  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1.gz}
  # remove conflict with texinfo
  rm "$pkgdir"/usr/share/info/info.info.gz
  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/ -exec chown root:root {} \;
  # Delete compressed .el.gz files. Comment out if needed.
  find "$pkgdir"/usr/share/emacs/ -name "*.el.gz" -exec rm {} \;
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
}

# vim:set ft=sh ts=2 sw=2 et:
