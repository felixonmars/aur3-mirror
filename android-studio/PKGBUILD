# Maintainer: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>

pkgname=android-studio
pkgver=0.1
pkgrel=5
pkgdesc="A new Android development environment based on IntelliJ IDEA."
arch=(i686 x86_64)
url="http://developer.android.com/sdk/installing/studio.html"
license=('unknown')
depends=('python' 'fontconfig' 'mesa' 'libxrender' 'android-sdk')
optdepends=('android-sdk-platform-tools'
            'android-platform-17')
options=('!strip')
source=(http://dl.google.com/android/studio/android-studio-bundle-130.677228-linux.tgz
        ${pkgname}.desktop
        ${pkgname}.png)
md5sums=('62b9ce75e4b74b7c1236ea2f1f99da34'
         '8a429621df3ffc4e5aa4097fad757656'
         '59b0b689e4f9aa7565fde6041821a409')

[ $CARCH = "x86_64" ] && depends=('python' 'lib32-fontconfig' 'lib32-mesa' 'lib32-libxrender' 'android-sdk')

prepare() {
    cd "${srcdir}/${pkgname}"

    # remove pause after open jdk warning
    sed -i 's/read IGNORE//' bin/studio.sh
}

package() {
    cd "${srcdir}/${pkgname}"
    mkdir -p "${pkgdir}/opt/${pkgname}/" "${pkgdir}/etc/profile.d"

    echo 'export PATH=$PATH:/opt/android-studio/bin' > "${pkgdir}/etc/profile.d/${pkgname}.sh"
    echo 'setenv PATH ${PATH}:/opt/android-studio/bin' > "${pkgdir}/etc/profile.d/${pkgname}.csh"

    cp -Rf bin lib plugins "${pkgdir}/opt/${pkgname}/"

    chmod -R 755 "${pkgdir}/opt/${pkgname}/"{bin,lib,plugins} "${pkgdir}/etc/profile.d"
    chmod +rx "${pkgdir}/opt/${pkgname}/bin/"{fsnotifier*,inspect.sh,studio.sh}

    ln -s /opt/android-studio/bin/studio.sh "${pkgdir}/opt/${pkgname}/bin/android-studio"

    # icon extracted from lib/resources.jar artwork/icon_AS_128.png
    install -Dm655 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm655 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
