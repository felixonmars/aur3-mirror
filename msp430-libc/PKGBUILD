# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=msp430-libc
pkgver=20110612
pkgrel=4
pkgdesc="C runtime library for the MSP430 family of microcontrollers"
arch=('i686' 'x86_64')
url="http://mspgcc4.sourceforge.net/"
license=('custom')
depends=('gcc-msp430')
options=(!strip)

_mspgcc_ver=20110716
_sf_base="http://sourceforge.net/projects/mspgcc/files"
_patches_base="${_sf_base}/Patches/LTS/${_mspgcc_ver}"
_patches=("msp430-libc-${pkgver}-sf3387164.patch"
          "msp430-libc-${pkgver}-sf3402836.patch")

source=("https://sourceforge.net/projects/mspgcc/files/msp430-libc/msp430-libc-${pkgver}.tar.bz2"
        "${_patches[0]}::${_patches_base}/${_patches[0]}/download"
        "${_patches[1]}::${_patches_base}/${_patches[1]}/download")
sha1sums=('4e44f4320254ecbb38e516ab9933532a19fdd5eb'
          'e39b237f2e8b05afe8e468e99de3f0415705f92d'
          'ff5e82cc9f3d7e83ad38739a6b35b18a46e0054e')

_builddir="${srcdir}/build"

build() {
  cp -r "${srcdir}/${pkgname}-${pkgver}" ${_builddir} && cd ${_builddir}/src

  (cd "${_builddir}" &&
    for patch in ${_patches[@]} ; do
      msg "Applying ${patch}"
      patch -p1 < "${srcdir}/${patch}"
    done)

  make PREFIX=/usr
}

package() {
  cd ${_builddir}/src
  mkdir -p ${pkgdir}/usr/msp430/lib
  make -j1 install PREFIX=${pkgdir}/usr

  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sts=2 ts=2 sw=2 et:
