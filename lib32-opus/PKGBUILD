# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=opus
pkgname=lib32-$_pkgbase
pkgver=1.0.2
pkgrel=1
pkgdesc="Codec designed for interactive speech and audio transmission over the Internet"
arch=('x86_64')
url="http://www.opus-codec.org/"
license=('custom')
depends=('lib32-glibc')
options=('!libtool')
source=("http://downloads.xiph.org/releases/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('c503ad05a59ddb44deab96204401be03')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share/aclocal}
  install -dm755 "${pkgdir}"/usr/share/licenses
  ln -s $_pkgbase "${pkgdir}"/usr/share/licenses/$pkgname
  
}
