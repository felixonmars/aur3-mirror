# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Maintainer: Erico Nunes <nunes dot erico at gmail>

_target="arm-linux-gnueabi"
pkgname=${_target}-gcc
pkgver=4.7.3
pkgrel=1
pkgdesc="The GNU Compiler Collection (${_target})"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=("${_target}-binutils" "${_target}-eglibc" 'libmpc' 'elfutils')
conflicts=("${_target}-gcc-stage1" "${_target}-gcc-stage2")
replaces=("${_target}-gcc-stage1" "${_target}-gcc-stage2")
provides=("${_target}-gcc-stage1=${pkgver}" "${_target}-gcc-stage2=${pkgver}")
options=(!libtool !emptydirs zipman docs !strip)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2)
md5sums=('86f428a30379bdee0224e353ee2f999e')


build() {
  _basedir="${srcdir}/gcc-${pkgver}"
  cd $_basedir

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  echo ${pkgver} > gcc/BASE-VER

  [[ -d gcc-build ]] || mkdir gcc-build
  cd gcc-build

  [ $NOEXTRACT -eq 1 ] || ../configure \
        --prefix=/usr \
        --program-prefix=${_target}- \
        --target=${_target} \
        --host=$CHOST \
        --build=$CHOST \
        --enable-shared \
        --disable-nls \
        --enable-threads=posix \
        --enable-languages=c,c++ \
        --enable-multilib \
        --with-sysroot=/usr/${_target} \
        --with-build-sysroot=/usr/${_target} \
        --with-as=/usr/bin/${_target}-as \
        --with-ld=/usr/bin/${_target}-ld \
        --enable-softfloat \
        --with-float=soft \
        --enable-interwork \
        --disable-libgomp \
        --enable-__cxa_atexit \
        --enable-addons

  make all-gcc all-target-libgcc all-target-libstdc++-v3
}

package() {
  _basedir="${srcdir}/gcc-${pkgver}"
  cd $_basedir/gcc-build

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  make DESTDIR=$pkgdir install-gcc install-target-libgcc install-target-libstdc++-v3

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info

  cp -r $pkgdir/usr/libexec/* $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/libexec

  rm -rf $pkgdir//usr/share/gcc-${pkgver}/python

  # strip it manually
  strip $pkgdir/usr/bin/* 2>/dev/null || true
  find $pkgdir/usr/lib -type f -exec /usr/bin/${_target}-strip --strip-unneeded {} \; 2>/dev/null || true
}

