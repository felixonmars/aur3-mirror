# Maintainer : <dreamspepser at yahoo dot it>

pkgbase=distrho-git
pkgname=distrho-git
true && pkgname=('distrho-standalone-git' 'distrho-lv2-git' 'distrho-extra-git')
pkgver=20120519
pkgrel=1
pkgdesc="Cross-Platform Audio Plugins (TAL), using the JUCE Toolkit"
arch=('i686' 'x86_64')
url="http://distrho.sourceforge.net/"
license=('GPL' 'GPL3' 'LGPL')
makedepends=('premake3' 'jack' 'lv2' 'libxinerama'
             'libxrender' 'libxcursor' 'qt')

_gitroot=git://distrho.git.sf.net/gitroot/distrho/distrho
_gitname=distrho

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # generate build script
  scripts/premake-update.sh linux

  # --as-needed incompatible
  export LDFLAGS="${LDFLAGS//,--as-needed}"

  make standalone
  make lv2

  # generate lv2 ttl
  scripts/generate-ttl.sh
}

package_distrho-standalone-git() {
  true && pkgdesc=("Distrho Standalone Audio Plugins (TAL), using the JUCE Toolkit")
  true && depends=('alsa-lib' 'fftw' 'libxext' 'freetype2')
  true && optdepends=('jack: jack audio support'
                      'distrho-extra-git: HybridReverb2 plugin')
  true && provides=('distrho-standalone' 'distrho-plugins')
  true && conflicts=('distrho-standalone')
  cd "$srcdir/$_gitname-build"

  # standalone bins
  install -d "$pkgdir/usr/bin"
  find bin/standalone -maxdepth 1 -type f -exec \
  install -Dm755 {} "$pkgdir/usr/bin" \;
}

package_distrho-lv2-git() {
  true && pkgdesc=("Distrho LV2 Audio Plugins (TAL), using the JUCE Toolkit")
  true && depends=('lv2' 'fftw' 'qt')
  true && optdepends=('jack: jack audio support'
                      'distrho-extra-git: HybridReverb2 plugin')
  true && provides=('distrho-lv2' 'distrho-plugins')
  true && conflicts=('distrho-lv2' 'distrho-plugins-lv2-git')
  true && replaces=('distrho-plugins-lv2-git')
  cd "$srcdir/$_gitname-build"

  # lv2 plugins
  install -d "$pkgdir/usr/lib/lv2"
  cp -a bin/lv2/*.lv2 \
    "$pkgdir/usr/lib/lv2"
}

package_distrho-extra-git() {
  true && pkgdesc=("Extras from Distrho Audio Plugins (HybridReverb2)")
  true && arch=('any')
  true && depends=('distrho-plugins')
  true && provides=('distrho-extra')
  true && conflicts=('distrho-extra')
  cd "$srcdir/$_gitname-build"

  # HybridReverb2 data
  cd ports/hybridreverb2/data
  install -Dm644 HybridReverb2.conf \
    "$pkgdir/etc/HybridReverb2/HybridReverb2.conf"
  install -d "$pkgdir/usr/share"
  cp -a HybridReverb2 \
    "$pkgdir/usr/share"
}

# vim:set ts=2 sw=2 et:
