pkgname=loliwm-git
pkgver=r86.7a6f376
pkgrel=1

pkgdesc='Wayland "window manager" based on wlc.'
url='https://github.com/Cloudef/loliwm'
arch=('i686' 'x86_64')
license=('GPL')

depends=('wayland-git' 'pixman' 'libxkbcommon' 'wlc-git')
makedepends=('git' 'cmake')
optdepends=('weston-git: To test weston clients in loliwm')

provides=('loliwm')
conflicts=('loliwm')

source=('git://github.com/Cloudef/loliwm')

md5sums=('SKIP')

options=('debug' '!strip')

pkgver() {
    cd loliwm
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git describe --always)"
}

build() {
    cd loliwm
    cmake -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd loliwm
    make DESTDIR="$pkgdir" install
    
    # On Arch, logind, via udev sets uaccess for the input group, so we can just
    # mark it setgid for users who launch it via the tty.
    chown root:input "$pkgdir"/usr/bin/loliwm
    chmod 2755 "$pkgdir"/usr/bin/loliwm
}
