# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-pandas
pkgver=0.8.1
pkgrel=1
pkgdesc="Cross-section and time series data analysis toolkit"
arch=('i686' 'x86_64')
url="http://pandas.sourceforge.net/"
license=('BSD')
depends=('python2-numpy' 'python2-dateutil')
makedepends=('python2-distribute' 'cython2')
optdepends=('python-pytables: for HDF5-based storage'
            'python2-matplotlib: for plotting'
            'python2-pytz: for time zone support'
            'python2-scipy: for miscellaneous statistical functions'
            'scikits-statsmodels: for parts of pandas.stats')
options=(!emptydirs)

source=("http://pypi.python.org/packages/source/p/pandas/pandas-${pkgver}.tar.gz")
md5sums=('dd1e45e25e7efe8813a6270928c30ec0')

build() {
  cd "$srcdir"/pandas-$pkgver

  python2 setup.py build
}

package() {
  cd "$srcdir"/pandas-$pkgver
  python2 setup.py install --root="$pkgdir"/ --optimize=1

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find "${pkgdir}" -name '*.py')

  install -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

