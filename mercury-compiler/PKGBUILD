# Maintainer: Bart Verhoeven <nepherte at archlinux dot us>

pkgname=mercury-compiler
pkgver=11.07
pkgrel=1
pkgdesc="stable release of the mercury language compiler"
arch=('i686' 'x86_64')
url="http://www.cs.mu.oz.au/research/mercury/"
license=('GPL')
depends=('gcc' 'make')
makedepends=('flex' 'bison')
source=(http://www.mercury.csse.unimelb.edu.au/download/files/$pkgname-$pkgver.tar.gz)
md5sums=('ad842e8e33f31966605572c3052a49ea')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info --disable-debug-grades \
              --disable-inefficient-grades 

  # Change -j accordingly to speed up compilation
  make PARALLEL="-j1"
}

package() {
  cd $srcdir/$pkgname-$pkgver

  # Change -j accordingly to speed up compilation
  make INSTALL_PREFIX="$pkgdir/usr" INSTALL_MAN_DIR="$pkgdir/usr/share/man" \
       INSTALL_INFO_DIR="$pkgdir/usr/share/info" \
       MERCURY_COMPILER="$pkgdir/usr/bin/mercury_compile" \
       MERCURY_CONFIG_DIR="$pkgdir/usr/lib/mercury" \
       PARALLEL="-j1"  install
}

# vim:set ts=2 sw=2 et:
