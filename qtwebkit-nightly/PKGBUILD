# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Maintainer: Mladen PejakoviÄ‡ <pejakm at gmail dot com>

pkgname=qtwebkit-nightly
pkgver=112532
_qtwkver=2.2.1
pkgrel=2
arch=('i686' 'x86_64')
url='http://trac.webkit.org/'
pkgdesc='An open source web browser engine (Qt port) - nightly builds'
license=('LGPL2.1' 'GPL3')
depends=('qt' 'qtmobility' 'gstreamer0.10-base')
makedepends=('python2' 'mesa' 'bison' 'chrpath' 'gperf')
conflicts=('qt<4.8' 'qtwebkit')
provides=('qtwebkit')

source=("http://builds.nightly.webkit.org/files/trunk/src/WebKit-r${pkgver}.tar.bz2"
        "ftp://ftp.archlinux.org/other/${pkgname}/qwebview-4.8.0.tar.bz2"
        "addressupdatefix.patch")
md5sums=('c0cb30f6f06ebf9092ce3ae7d2e47212'
         '13a4ac75d98452c2bf7ef710353e91d8'
         '61b60add314678c308f44218c0011dd2')

build() {
  cd "${srcdir}"/WebKit-r${pkgver}
  rm -f "${srcdir}"/WebKit-r${pkgver}/qwebview-4.8.0
  ln -s "${srcdir}"/QtWebKit-${_qtwkver}/qwebview-4.8.0 "${srcdir}"/WebKit-r${pkgver}/

  patch -Np0 -i "${srcdir}"/addressupdatefix.patch
  cd Tools/Scripts

#   "safe" flags
  QTDIR=/usr/share/qt ./build-webkit --prefix=/usr --qt --release --3d-canvas --accelerated-2d-canvas --3d-rendering --microdata --link-prefetch --geolocation --blob --request-animation-frame --animation-api --fullscreen-api --input-type-date --input-type-datetime --input-type-datetimelocal --input-type-month --input-type-time --input-type-week --style-scoped --svg --svg-dom-objc-bindings --svg-fonts --tiled-backing-store --filters --web-sockets --web-timing --page-visibility-api --mathml --meter-tag --mhtml --workers --shared-workers --touch-events --touch-icon-loading --icon-database --javascript-debugger --progress-tag --download-attribute --sql-database --xslt --ftpdir --media-source --input-speech --makeargs="${MAKEFLAGS}"

#   unsafe flags, possible build fail
#   --indexed-database --file-system --css-grid-layout --css-filters --css-shaders --input-color --register-protocol-handler --web-audio --inspector 

  # Build the QWebView plugin (FS#27914)
  cd "${srcdir}"/WebKit-r${pkgver}/qwebview-4.8.0/plugins/qwebview
  qmake
  make
}

package() {
  cd "${srcdir}"/WebKit-r${pkgver}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  # Fix RPATH
  chrpath -r /usr/lib/ "${pkgdir}"/usr/lib/qt/imports/QtWebKit/libqmlwebkitplugin.so

  cd "${srcdir}"/WebKit-r${pkgver}/qwebview-4.8.0/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
