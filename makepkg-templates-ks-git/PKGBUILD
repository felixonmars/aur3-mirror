# Maintainer: William Giokas <1007380@gmail.com>

pkgname=makepkg-templates-ks-git
_pkgname=makepkg-templates-ks
pkgver=0.4.gb88ab9d
pkgrel=1
pkgdesc="A collection of templates for makepkg-template"
arch=('any')
url="http://173.234.237.218/cgit/wgiokas/makepkg-templates.git"
license=('MIT')
optdepends=('pacman-git: makepkg-template is only in pacman-git at the moment')
makedepends=('git')
provides=('makepkg-templates-ks')
conflicts=('makepkg-templates-ks')
source=("${_pkgname}::git://173.234.237.218/pub/wgiokas/makepkg-templates")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.$(git rev-list --count HEAD).g$(git log -1 --format="%h")"
}

package() {
  cd "$srcdir/$_pkgname"

  for template in *.template; do
    unset nover
    nover=${template%-*.template}
    install -Dm644 $template "$pkgdir/usr/share/makepkg-template/$template"
    ln -sv $template "$pkgdir/usr/share/makepkg-template/${nover}.template" 
  done
}
