# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=lightdm-kde-git
pkgver=20121005
pkgrel=1
pkgdesc="LightDM Greeter for KDE"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/base/lightdm"
license=('GPL')
depends=('lightdm' 'qt' 'openssl' 'perl' 'phonon' 'kdelibs')
makedepends=('git' 'cmake')
provides=('lightdm-kde')
conflicts=('lightdm-kde')

_gitroot='git://anongit.kde.org/lightdm'
_gitname='lightdm-kde'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  mkdir build/ && cd build/
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build/build"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
