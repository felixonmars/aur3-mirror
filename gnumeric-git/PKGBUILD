# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=gnumeric-git
conflicts=('gnumeric')
pkgver=20110815
pkgrel=1
pkgdesc="A GNOME Spreadsheet Program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice-git' 'gconf' 'desktop-file-utils' 'hicolor-icon-theme' 'gtk2')
makedepends=('git' 'intltool' 'python2' 'gnome-doc-utils' 'pygobject' 'psiconv' 'gnome-common')
optdepends=('pygobject: for python plugin support'
            'psiconv: for Psion 5 file support'
            'yelp: for viewing the help manual')
install=gnumeric-git.install
options=('libtool')

_gitroot="git://git.gnome.org/gnumeric"
_gitname="gnumeric"

build() {

  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-install --enable-ssindex
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}/etc/gconf/schemas/*.schemas"
  rm -rf "${pkgdir}/etc"
}

