# Maintainer: Michael Lass <bevan@bi-co.net>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: webjdm <web.jdm@gmail.com>
# Contributor: coderoar <coderoar@gmail.com>

_pkgbasename=popt
pkgname=lib32-$_pkgbasename
pkgver=1.16
pkgrel=5
pkgdesc="A commandline option parser"
arch=('x86_64')        
url="http://rpm5.org"
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://launchpad.net/$_pkgbasename/head/$pkgver/+download/$_pkgbasename-$pkgver.tar.gz)
md5sums=('3743beefa3dd6247a73f8f7a32c14c33')

build() {
  cd $srcdir/$_pkgbasename-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 || return 1
  make || return 1
}

package() {
  cd $srcdir/$_pkgbasename-$pkgver
  make DESTDIR=$pkgdir install || return 1
  rm -rf $pkgdir/usr/{include,lib,share}
  
  ln -sf libpopt.so.0 ${pkgdir}/usr/lib32/libpopt.so
  ln -sf libpopt.so.0.0.0 ${pkgdir}/usr/lib32/libpopt.so.0

  # install license
  install -Dm644 $srcdir/$_pkgbasename-$pkgver/COPYING \
    $pkgdir/usr/share/licenses/$pkgname/LICENSE || return 1
}
