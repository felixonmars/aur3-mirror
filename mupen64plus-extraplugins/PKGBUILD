# Maintainer: TryA <tryagainprod@gmail.com>

pkgname=mupen64plus-extraplugins
pkgver=1.99.4
pkgrel=1
pkgdesc="Additional plugins for Mupen64Plus"
arch=('i686' 'x86_64')
url="http://code.google.com/p/mupen64plus"
license=('GPL')
depends=('glew' "mupen64plus=$pkgver")
options=(!strip)
source=("http://mupen64plus.googlecode.com/files/mupen64plus-extraplugins-src-1.99.4.tar.gz")
md5sums=('2f9ddddc9938247537e15ec45c1d8d34')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"
  for _plugdir in mupen64*; do
    cd ${_plugdir}
    make -C projects/unix PREFIX=/usr all
    cd ..
  done
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"
  for _plugdir in mupen64*; do
    cd ${_plugdir}
    make -C projects/unix PREFIX=/usr DESTDIR=${pkgdir} install
    cd ..
  done
}
