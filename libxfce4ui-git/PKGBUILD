# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from libxfce4ui package)
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

_pkgname=libxfce4ui
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="A freedesktop.org compliant UI implementation for Xfce"
arch=(i686 x86_64)
url=http://git.xfce.org/xfce/$_pkgname/tree/README
license=(GPL2)
groups=(xfce4-git)
depends=(gtk2 gtk3 hicolor-icon-theme libsm libxfce4util-git startup-notification xfconf)
makedepends=(git xfce4-dev-tools)
provides=($_pkgname=4.10.1)
conflicts=($_pkgname)
options=(!libtool)
install=$_pkgname.install

_gitroot=git://git.xfce.org/xfce/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static \
        --enable-gtk-doc \
        --disable-debug \
        --with-vendor-info="Arch-Linux"
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
