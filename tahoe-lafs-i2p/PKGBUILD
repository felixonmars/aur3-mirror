# Contributor: DaNiMoTh <jjdanimoth@gmail.com>

pkgname=tahoe-lafs-i2p
pkgver=1.8.3
pkgrel=1
pkgdesc="Secure, decentralized, fault-tolerant filesystem over the I2P network"
url='http://tahoe-lafs.org'
license=('GPL')
arch=('i686' 'x86_64')
conflicts=("tahoe-lafs")
provides=("tahoe-lafs")

#makedepends=('python2-setuptools' 'darcsver') # tahoe build script pulls these in locally
depends=('net-tools' # Tahoe-LAFS uses /sbin/ifconfig explicitly
         'epsilon>=0.6.0'
         'nevow>=0.10.0'
         'twisted>=12.0.0'
         'python2-argparse>=1.2.1'
         'python-foolscap-i2p>=0.6.3'
         'python2-mock>=0.8.0'
         'python2-pyopenssl>=0.11'
         'python2-pyasn1>=0.1.2'
         'pycrypto>=2.4'
         'pycryptopp>=0.6.0'
         'pyutil>=1.8.7'
         'python-simplejson>=2.5.0'
         'zbase32>=1.1.3'
         'zfec>=1.4.22'
         'python2-zope-interface>=3.8.0'
         'i2p')

_patchver="${pkgver}-r3"
source=("https://tahoe-lafs.org/source/tahoe-lafs/snapshots/allmydata-tahoe-${pkgver}.tar.bz2"
        #"https://tahoe-lafs.org/source/tahoe-lafs/deps/tahoe-deps.tar.bz2"
        "http://killyourtv.i2p.to/tahoe-lafs/patches/tahoe-lafs-i2p-${_patchver}.patch")
        
sha256sums=('a8c9cda31e0ea558eec9d5a2687d49c4ce7472a21a6b28c563b644c6b73bf502'
            #'d753cbf88e4f7e19785a73c01808946a441708c043bc2e53c98fc232d04cbdd6'
            '7b605ae16adc27414a61d61e2640eac1ecb28245bd7ed2ae3a272ceb16715a5d')

build(){
    cd "$srcdir/allmydata-tahoe-$pkgver"
    patch -Np1 -i $srcdir/tahoe-lafs-i2p-${_patchver}.patch
    #export PYCRYPTOPP_DISABLE_EMBEDDED_CRYPTOPP=1
    python2 setup.py build

    msg "Running tests... this may take a while"
    python2 setup.py test || true
}

package(){
    cd "$srcdir/allmydata-tahoe-$pkgver"
    python2 setup.py install --root="$pkgdir"
}

