# Contributor: DaNiMoTh <jjdanimoth@gmail.com>

pkgname=tahoe-lafs-i2p
pkgver=1.9.2a1
pkgrel=1
pkgdesc="Secure, decentralized, fault-tolerant filesystem over the I2P network"
url='http://tahoe-lafs.org'
license=('GPL')
arch=('i686' 'x86_64')
conflicts=("tahoe-lafs")
provides=("tahoe-lafs")

depends=('net-tools' # Tahoe-LAFS uses /sbin/ifconfig explicitly
         'epsilon>=0.6.0'
         'nevow>=0.10.0'
         'twisted>=12.0.0'
         'python2-argparse>=1.2.1'
         'python-foolscap-i2p>=0.6.3'
         'python2-mock>=0.8.0'
         'python2-pyopenssl>=0.11'
         'python2-pyasn1>=0.1.2'
         'pycrypto>=2.4'
         'pycryptopp>=0.6.0'
         'pyutil>=1.8.7'
         'python-simplejson>=2.5.0'
         'zbase32>=1.1.3'
         'zfec>=1.4.22'
         'python2-zope-interface>=3.8.0'
         'i2p')

#Local copy of the patch will be kept in the source in case kytv or the inproxy dies
_patchver="${pkgver}-r1"
source=("https://tahoe-lafs.org/source/tahoe-lafs/snapshots/allmydata-tahoe-${pkgver}.tar.bz2"
        #"https://tahoe-lafs.org/source/tahoe-lafs/deps/tahoe-deps.tar.bz2"
        "http://killyourtv.i2p.to/tahoe-lafs/patches/tahoe-lafs-i2p-${_patchver}.patch"
        "tahoe-lafs-i2p-${_patchver}.patch")
        
sha256sums=('b617ec4edf45337a7311d9b089ce5dceda6fc637bb3bd1102eb44827592733a4'
            #'d753cbf88e4f7e19785a73c01808946a441708c043bc2e53c98fc232d04cbdd6'
            'a4bc7b1e6883c8914246bb5b6be8a1b6f0e65b810028104c0db3827447afe582'
            'a4bc7b1e6883c8914246bb5b6be8a1b6f0e65b810028104c0db3827447afe582')

build(){
    cd "$srcdir/allmydata-tahoe-$pkgver"
    patch -Np1 -i $srcdir/tahoe-lafs-i2p-${_patchver}.patch
    python2 setup.py build

    msg "Running tests... this may take a while"
    python2 setup.py test
}

package(){
    cd "$srcdir/allmydata-tahoe-$pkgver"
    python2 setup.py install --root="$pkgdir"
}

