# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=smc-git
pkgver=208.1817d55
pkgrel=1
pkgdesc="Secret Maryo Chronicles"
arch=('i686' 'x86_64')
url="http://www.secretmaryo.org"
license=('GPL3')
depends=('sdl_image' 'sdl_ttf' 'sdl_mixer' 'cegui' 'boost-libs' 'libgl'
         'gcc-libs')
makedepends=('autoconf' 'automake' 'boost' 'git')
conflicts=('smc' 'smc-data')
provides=('smc' 'smc-data')
source=(git+http://github.com/FluXy/SMC.git 'smc.desktop')
md5sums=('SKIP' 'ba9caedf907cc93de35843055206b57d')

pkgver(){
  cd SMC
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd SMC/smc
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd SMC/smc
  make DESTDIR="$pkgdir" install
  
  # man and .desktop
  install -Dm644 makefiles/unix/man/smc.6 \
             "$pkgdir"/usr/share/man/man6/smc.6
  install -Dm644 makefiles/unix/smc.xpm \
             "$pkgdir"/usr/share/pixmaps/smc.xpm
  install -Dm644 "$srcdir"/smc.desktop \
             "$pkgdir"/usr/share/applications/smc.desktop
  # remove useless .po file (keep only .mo needed)
  find "$pkgdir" -name *.po -exec rm {} \;
}
