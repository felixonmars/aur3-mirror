pkgname=mingw-w64-sdl_image
pkgver=1.2.12
pkgrel=4
pkgdesc="A simple library to load images of various formats as SDL surfaces (mingw-w64)"
arch=(any)
url="http://www.libsdl.org/projects/SDL_image"
license=("zlib")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt mingw-w64-sdl)
options=(!libtool !strip !buildflags)
source=("$url/release/SDL_image-$pkgver.tar.gz")
md5sums=('a0f9098ebe5400f0bdc9b62e60797ecb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/SDL_image-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
