# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ocropus
pkgver=0.5.2
pkgrel=2
pkgdesc="State-of-the-art document analysis and OCR system"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ocropus/"
license=('APACHE')
depends=('python2-numpy' 'iulib' 'ocropus-models')
makedepends=('mercurial' 'scons' 'swig' 'cmake')
source=('ocrorast-redeclaration.diff')
md5sums=('e6f95103e3e3b6427621ceb9cc410cd0')

build() {
  cd "$srcdir"
  msg "Connecting to $pkgname Mercurial server......."
  if [ -d $pkgname ]; then
    cd "$srcdir"/$pkgname
    hg pull -u -r ocropus-$pkgver
    cd "$srcdir"
  else
    hg clone https://ocropus.googlecode.com/hg/ $pkgname -r ocropus-$pkgver
  fi
  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir"/$pkgname-build ]; then
    rm -rf "$srcdir"/$pkgname-build
  fi
  cp -r "$srcdir"/$pkgname "$srcdir"/$pkgname-build
  cd "$srcdir"/$pkgname-build

  ## apply patches
  patch -Np1 < "$srcdir"/ocrorast-redeclaration.diff || true
  ##

  ## detect the number of simultaneous jobs
  JOBS=`echo $MAKEFLAGS | sed 's|.*\(-j[1-9][0-9]*\).*|\1|'`
  ##

  # compile iulib
  # NOT NEEDED: it is provided by the iulib package

  # compile ocrolseg
  cd ocrolseg
  sed 's|python |python2 |' -i Makefile
  make
  cd ..

  # compile ocrorast
  cd ocrorast
  make
  python2 setup.py build_ext
  cd ..

  # compile ocropy
  cd ocropy
  python2 setup.py build
  cd ..

  # compile llpy
  cd llpy
  python2 setup.py build
  cd ..

  # compile ocrofst
  cd ocrofst/ocrofstll
  scons $JOBS prefix="/usr"
  cd ..
  python2 setup.py build_ext
  cd ..

  # compile openfst 
  rm -rf openfst-1.1
  test -f DIST/openfst-1.1.tar.gz && tar -zxvf DIST/openfst-1.1.tar.gz 
  cd openfst-1.1
  CXXFLAGS="-fpermissive" ./configure --prefix=/usr
  make
  cd ..

  # compile Python bindings for openfst
  cd pyopenfst
  CFLAGS="-fpermissive -I../openfst-1.1/src/include/ -L../openfst-1.1/src/lib/.libs/" python2 setup.py build
  cd ..

  # optionally compile FLANN
  rm -rf flann-1.7.1-src
  bsdtar xf DIST/flann-1.7.1-src.zip
  cd flann-1.7.1-src
  # fix python bindings install
  sed 's|${PYTHON_EXECUTABLE}|python2|' -i src/python/CMakeLists.txt
  sed "s|setup.py install|setup.py install --root=$pkgdir --optimize=1|" -i src/python/CMakeLists.txt
  # fix libdir on x86_64
  sed 's|lib64|lib|' -i cmake/flann_utils.cmake
  # build
  rm -rf build
  mkdir build
  cd build
  cmake .. -DBUILD_MATLAB_BINDINGS=OFF -DCMAKE_INSTALL_PREFIX=/usr
  make
  cd ../../../..
}

package() {
  cd "$srcdir"/$pkgname-build

  # install ocrolseg
  cd ocrolseg
  python2 setup.py install --root="$pkgdir/" --optimize=1
  cd ..

  # install ocrorast
  cd ocrorast
  python2 setup.py install --root="$pkgdir/" --optimize=1
  cd ..

  # install ocropy
  cd ocropy
  python2 setup.py install --root="$pkgdir/" --optimize=1
  cd ..

  # install llpy
  cd llpy
  python2 setup.py install --root="$pkgdir/" --optimize=1
  cd ..

  # install ocrofst
  cd ocrofst/ocrofstll
  scons $JOBS destdir="$pkgdir" prefix="/usr" install
  cd ..
  python2 setup.py install --root="$pkgdir/" --optimize=1
  cd ..

  # install openfst
  cd openfst-1.1
  make DESTDIR="$pkgdir" install
  cd ..

  # install Python bindings for openfst
  cd pyopenfst
  python2 setup.py install --root="$pkgdir/" --optimize=1
  cd ..

  # optionally install FLANN
  cd flann-1.7.1-src/build
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
