# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor:  Artyom Smirnov <smirnoffjr@gmail.com>
pkgname=ocropus
pkgver=0.4.4
pkgrel=2
pkgdesc="State-of-the-art document analysis and OCR system"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ocropus/"
license=('APACHE')
depends=("iulib=$pkgver" 'sqlite3' 'giflib')
makedepends=('mercurial' 'scons')

build() {
  cd "$srcdir"
  msg "Connecting to $pkgname Mercurial server......."
  if [ -d $pkgname ]; then
    cd "$srcdir"/$pkgname
    hg pull -u -r ocropus-$pkgver
    cd "$srcdir"
  else
    hg clone https://ocropus.googlecode.com/hg/ $pkgname -r ocropus-$pkgver
  fi
  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir"/$pkgname-build ]; then
    rm -rf "$srcdir"/$pkgname-build
  fi
  cp -r "$srcdir"/$pkgname "$srcdir"/$pkgname-build
  cd "$srcdir"/$pkgname-build

  # fix the datadir
  sed -i 's|/usr/local|/usr|' ocr-commands/ocr-commands.h

  JOBS=`echo $MAKEFLAGS | sed 's|.*\(-j[1-9][0-9]*\).*|\1|'`
  scons $JOBS prefix=/usr
}

package() {
  cd "$srcdir"/$pkgname-build
  scons destdir="$pkgdir" prefix=/usr install
}

# vim:set ts=2 sw=2 et:
